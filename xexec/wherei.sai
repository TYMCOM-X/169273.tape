begin
require "(SAILIB)SAIL.DEF" source!file;
require "(WRS)ARGS.REQ" source!file;

preload!with
    null,
    "~",
    "(XEXEC)",
    "(SPL)",
    "(MPL)",
    "(DNDUTIL)",
    "(M33)",
    "(FTSYS)",
    "(SYS)",
    "(*6NEWS)";
string array UserNameList[1:10];

preload!with
    "SAV",
    "HGH",
    "SHR",
    "SWR";
string array ExeExtList[1:4];

preload!with
    "DOC",
    "MAN",
    "MEM",
    "HLP",
    "INS",
    "INF";
string array DocExtList[1:6];

integer A,U,X,C,E;
string S;

if ARGC = 1 then begin
    print("
Usage:
    WHEREIS cmd1 cmd2 ... cmdN

Summary:
    Attempts to find the name of the program that is run by (XEXEC)MEXEC
    for the commands listed.  Files with extensions implying documentation
    are also looked for.
");
    call(0,"EXIT");
end;

for U := arrinfo(UserNameList,1) upto arrinfo(UserNameList,2) do
    if UserNameList[u] = "~" then UserNameList[u] := 
	"("& cv6str(calli(-'22,'41))& cv6str(calli(-'21,'41))& ")";

open(C_getchan,"DSK",0,4,0,0,0,E);
for A := 1 upto ARGC-1 do begin "A"
    print( S_argv[a]&":", (#ht&#ht)[length(S)%8+1 to inf] );
    S := "no executable file";
    for U := arrinfo(UserNameList,1) upto arrinfo(UserNameList,2) do begin "U"
	for X := arrinfo(ExeExtList,1) upto arrinfo(ExeExtList,2) do begin "X"
	    string FN;
	    lookup(C,FN_UserNameList[u]&cv6str(cvsix(argv[a]))&"."&ExeExtList[x],E);
	    if not E then begin
		close(C);
		S := FN;
		done "U";
	    end;
	end "X";
    end "U";
    print( S, (#ht&#ht&#ht)[length(S)%8+1 to inf] );
    S := "no documentation file";
    for U := arrinfo(UserNameList,1) upto arrinfo(UserNameList,2) do begin "U"
	for X := arrinfo(DocExtList,1) upto arrinfo(DocExtList,2) do begin"X"
	    string FN;
	    lookup(C,FN_UserNameList[u]&cv6str(cvsix(argv[a]))&"."&DocExtList[x],E);
	    if not E then begin
		close(C);
		S := FN;
		done "U";
	    end;
	end "X";
    end "U";
    print( S, crlf );
end "A";

release( C );
call(0,"EXIT");
end $
