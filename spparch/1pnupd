1PNUPD ** FICH/FRAME BREAK *****
PNUPD3
PNUPD  IDENT   PRINTER/NODE UPDATE PROGRAM DEC 2, 1975
*      VERSION 2
*  1.  CREATE CHNODPRI WHEN EXITING PROGRAM
*  2.  LICENSE REQUIRED ONLY FOR THOSE COMMANDS
*      THAT CHANGE INFORMATION.
       BRU     START
       BRS     10
SKNE   OPD     536B5,2
SPTR   OPD     514B5,2

       ASC     "T'M B'S"
FILE   ZRO     0
TFILE  ZRO     0
IFILE  ZRO     0
OFILE  ZRO     1
T      ZRO     0
N      ZRO     0
SA     ZRO     0
SB     ZRO     0
SX     ZRO     0
TP     BSS     2
CR     DATA    155B
PROMPT DATA    11
CMD    ZRO
ASW    ZRO
1SW    ZRO
INTFL  ZRO     0
PL     ZRO     0
UPDFLG  ZRO     0

PNBUFF EQU     24000B
PNBUFE EQU     33777B

ADBUFF EQU     34000B
L1     EQU     ADBUFF+1
L2     EQU     L1+20
L3     EQU     L2+20
L4     EQU     L3+20
TN     EQU     L4+20
ADBUFE EQU     ADBUFF+128

TYPE   POPD    100B5,2
       STA     SA
       STB     SB
       STX     SX
       EAX*    0
       CXA
       ETR     =37777B
       LDB     =-1
       LDX     OFILE
       BRS     34
       LDA     SA
       LDB     SB
       LDX     SX
       BRR     0

START  STP     IFILE
       BRM     GNPI            GET NODE PRINTER INFORMATION
       LDA     =ESC
       STA     201B
       LDA     =2B6
       BRS     78

       LDA     CR
       CIO     OFILE
C      LDA     =4B4
       BRS     12
       LDA     =-1
       STA     INTFL
       LDA     CR
       CIO     OFILE
       LDA     PROMPT
       CIO     OFILE
       CLA
       STA     ASW
       CIO     IFILE
       SKNE    ='Q'
       BRU     QUIT
       LRSH    8
       CIO     IFILE
       LRSH    8
       CIO     IFILE
       LSH     16
       STA     CMD
       LDX     =CL-CLE
       SKNE    CLE,2
       BRU*    BLE,2
       BRX     *-2
CMDERR LDA     CR
       CIO     OFILE
       LDA     =37B
       CIO     OFILE
       LDA     CR
       CIO     OFILE
       LDA     IFILE
       CAX
       SKG     =0
       BRS     11
       BRU     C
ESCL   ZRO
ESC    NOP     ESCL
       LDA     =2B6
       BRS     78
       BRS     17
       CLA
       LDB     =1
       STP     IFILE
       LDX     =-1
       BRS     11
       BRS     29
       BRU     C

CL     ASC     'NPP'
       ASC     'APP'
       ASC     'ONP'
       ASC     'ONA'
       ASC     'LPA'
       ASC     'ONC'
       ASC     'LPC'
       ASC     'OND'
       ASC     'LPD'
       ASC     'ANP'
       ASC     'LEH'
       ASC     'NIA'
       ASC     'NIP'
       ASC     'NID'
       ASC     'NIC'
CLE    BSS     0
BL     DATA    PPN
       DATA    PPA
       DATA    PNO
       DATA    ANO
       DATA    APL
       DATA    CNO
       DATA    CPL
       DATA    DNO
       DATA    DPL
       DATA    PNA
       DATA    HELP
       DATA    AIN
       DATA    PIN
       DATA    DIN
       DATA    CINR
BLE    BSS     0

PPN    LDA     CR
       CIT     IFILE
       BRU     *+2
       TYPE    M1
       SBRM    RDNO
       SKG     =0
       BRU     IVP
       STA     PL
       BRM     PPI
       SKN     1SW
       BRU     C
       TYPE    M9
       BRU     C
       BRU     C

PPA    CIO     IFILE
       SKE     CR
       BRU     *-2
       LDX     =3
       CLAB
       TYPE    M10
       BRS     63
       BRU     *-4
       XMA     OFILE
       STA     TFILE
       LDA     =1
       STA     PL
       LDA     =-1
       STA     ASW
       BRM     PPI
       MIN     PL
       LDA     PL
       SKG     =3777B
       BRU     *-4
       LDA     TFILE
       XMA     OFILE
       BRS     20
       BRU     C


PIN    CLA
       STA     INTFL
PNO    LDA     CR
       CIT     IFILE
       BRU     *+2
       TYPE    M5
PNO2   SBRM    RONO
       SKN     INTFL
       ADD     =2048
       SKG     =4095
       SKG     =0
       BRU     IVN
       STA     T
       LDA     CR
       CIO     OFILE
       LDX     T
       LDA     PNBUFF,2
       LRSH    12
       ETR     =3777B
       SKNE    =0
       BRU     PNO3
       TYPE    M1
       LDB     =10
       LDX     OFILE
       BRS     36
       LDA     CR
       CIO     OFILE
       BRU     C
PNO3   TYPE    M8
       BRU     C

PNA    CIO     IFILE
       SKE     CR
       BRU     *-2
       TYPE    M29             HEADING
       LDX     =-7000B
       LDA     PNBUFE,2
       SKA     =3777B4
       BRU     PNA1
PNA2   BRX     *-3
       LDA     CR
       CIO     OFILE
       BRU     C
PNA1   STX     T
       ETR     =3777B
       SBRM    PONO
       LDX     T
       LDA     =4B3
       SKA     PNBUFE,2
       BRU     *+2
       BRU     *+3
       TYPE    M28A
       BRU     *+2
       TYPE    M28
       LDX     T
       LDA     PNBUFE,2
       LRSH    12
       ETR     =3777B
       SBRM    PDNO
       LDA     CR
       CIO     OFILE
       LDX     T
       BRU     PNA2

AIN    CLA
       STA     INTFL
ANO    LDA     CR
       CIT     IFILE
       BRU     ANO1
       BRM     VER
       TYPE    M11
       SBRM    RONO
       BRU     ANO2
ANO1   SBRM    RONO
       STA     T
       BRM     VER
       LDA     T
ANO2   SKN     INTFL
       ADD     =2048
       SKG     =4095
       SKG     =0
       BRU     IVN
       STA     N
       CAX
       LDA     PNBUFF,2
       SKA     =3777B4
       BRU     ANOE1
       TYPE    M12
       SBRM    RDNO
       CLB
       LSH     12
       MRG     N
       MRG     =4B7
       LDX     N
       STA     PNBUFF,2
       BRM     WNPI
       TYPE    M18
       BRU     C

ANOE1  TYPE    M13             (NODE ALREADY ASSIGNED)
       BRU     C

CINR   CLA
       STA     INTFL
CNO    LDA     CR
       CIT     IFILE
       BRU     CNO1
       BRM     VER
       TYPE    M11
       SBRM    RONO
       BRU     CNO2
CNO1   SBRM    RONO
       STA     T
       BRM     VER
       LDA     T
CNO2   SKN     INTFL
       ADD     =2048
       SKG     =4095
       SKG     =0
       BRU     IVN
       STA     N
       CAX
       LDA     PNBUFF,2
       LRSH    12
       ETR     =3777B
       TYPE    M14             (PRINTER LOCATION IS)
       SBRM    PDNO
       TYPE    M15             (ENTER NEW PRINTER LOCATION)
       SBRM    RDNO
       CLB
       LSH     12
       MRG     N
       MRG     =4B7
       LDX     N
       STA     PNBUFF,2
       BRM     WNPI
       TYPE    M18
       BRU     C

DIN    CLA
       STA     INTFL
DNO    LDA     CR
       CIT     IFILE
       BRU     DNO1
       BRM     VER
       TYPE    M11             (NODE)
       SBRM    RONO
       BRU     DNO2
DNO1   SBRM    RONO
       STA     T
       BRM     VER
       LDA     T
DNO2   SKN     INTFL
       ADD     =2048
       SKG     =4095
       SKG     =0
       BRU     IVN
       STA     N
       CAX
       LDA     PNBUFF,2
       LRSH    12
       ETR     =3777B
       TYPE    M12
       SBRM    PDNO
       TYPE    M16             (CONFIRM)
       BRM     YN
       BRU     C
       LDA     N
       MRG     =4B7
       LDX     N
       STA     PNBUFF,2
       BRM     WNPI
       TYPE    M18
       BRU     C

DPL    LDA     CR
       CIT     IFILE
       BRU     DPLA
       BRM     VER
       TYPE    M17
       SBRM    RDNO
       BRU     DPLB
DPLA   SBRM    RDNO
       STA     T
       BRM     VER
       LDA     T
DPLB   SKG     =0
       BRU     IVP
       STA     PL
       BRM     PPI
       TYPE    M16
       BRM     YN
       BRU     C
       LDA     =-1
       STA     ADBUFF
       LDX     =-127
       CLA
       STA     ADBUFE,2
       BRX     *-1
       BRM     WPLA
       LDX     =-4096
DPL1   LDA     PL
       LDB     =3777B
       LSH     12
       SKM     PNBUFE,2
       BRU     *+2
       BRU     DPL2
       BRX     *-3
       BRM     WNPI
       TYPE    M18
       BRU     C
DPL2   CXA
       ADD     =3777B
       ETR     =7777B
       MRG     =4B7
       STA     PNBUFE,2
       BRU     DPL1


CPL    LDA     CR
       CIT     IFILE
       BRU     CPLA
       BRM     VER
       TYPE    M17
       SBRM    RDNO
       BRU     CPLB
CPLA   SBRM    RDNO
       STA     T
       BRM     VER
       LDA     T
CPLB   SKG     =0
       BRU     IVP
       STA     PL
       BRM     GPLA
       LDA     CR
       CIO     OFILE
       SPTR    L1
       BRM     PB1LIN
       SPTR    L2
       BRM     PB1LIN
       SPTR    L3
       BRM     PB1LIN
       SPTR    L4
       BRM     PB1LIN
       LDA     CR
       CIO     OFILE
       SPTR    TN
       BRM     PB1LIN
       LDX     =-128
       CLA
       STA     ADBUFE,2
       BRX     *-1
       TYPE    M30
       SPTR    L1
       BRM     RDLINE
       BRU     *-3
       SPTR    L2
       BRM     RDLINE
       BRU     CPL1
       SPTR    L3
       BRM     RDLINE
       BRU     CPL1
       SPTR    L4
       BRM     RDLINE
       NOP
CPL1   TYPE    M31
       SPTR    TN
       BRM     RDLINE
       BRU     CPL1
       LDA     =-1
       STA     ADBUFF
       BRM     WPLA
       TYPE    M18
       BRU     C

RDLINE ZRO
       STP     TP
       LDA     =TP
       LDB     =155B
       BRS     33
       LDA     TP
       SKE     TP+1
       MIN     RDLINE
       BRR     RDLINE

APL    CIO     IFILE
       SKE     CR
       BRU     APL
       BRM     VER
       TYPE    M30             (ENTER ADDRESS (4 LINES))
       LDX     =-128
       CLA
       STA     ADBUFE,2
       BRX     *-1
       SPTR    L1
       BRM     RDLINE
       BRU     APL
       SPTR    L2
       BRM     RDLINE
       BRU     APL1
       SPTR    L3
       BRM     RDLINE
       BRU     APL1
       SPTR    L4
       BRM     RDLINE
       NOP
APL1   TYPE    M31             (ENTER TELEPHONE NUMBER)
       SPTR    TN
       BRM     RDLINE
       BRU     APL1
       LDP     PRILOC
       BRS     64
       BRU     GPLAE1
       STA     FILE
       RSP     FILE
       LRSH    23
       DIV     =128*3
       STA     PL
       LDA     FILE
       BRS     20
       TYPE    M32             (TYPE NODES ENDING WITH . )
APL2   LDA     ='.'
       CIT     IFILE
       BRU     *+2
       BRU     APL3
       SBRM    RONO
       SKG     =4095
       SKG     =0
       BRU     APL4
       STA     N
       LRSH    12
       LDA     PL
       LSH     12
       MRG     =4B7
       LDX     N
       STA     PNBUFF,2
       BRU     APL2
APL3   CIO     IFILE
       SKE     CR
       BRU     APL3
       LDA     =-1
       STA     ADBUFF
       BRM     WPLA
       BRM     WNPI
       TYPE    M18
       LDA     PL
       SBRM    PDNO
       LDA     CR
       CIO     OFILE
       TYPE    M18
       BRU     C
APL4   TYPE    M27
       BRU     APL2
IVN    TYPE    M27
       BRU     C
IVP    TYPE    M26
       BRU     C

QUIT   CIO     IFILE
       SKE     CR
       BRU     *-2
       SKN     UPDFLG
       BRU     *+2
       BRM     WUPDFL
       BRS     10
HELP   CIO     IFILE
       SKE     CR
       BRU     HELP
       TYPE    HMSG
       BRU     C

HMSG   ASC     '$THE FOLLOWING COMMANDS ARE VALID$'
       ASC     '$  PPN  PRINT PRINTER LOCATION ADDRESS AND NODES'
       ASC     '$       FOR ONE PRINTER LOCATION NUMBER'
       ASC     '$  PPA  SAME AS PPN EXCEPT FOR ALL PRINTER LOCATIONS'
       ASC     '$  PNO  PRINT PRINTER LOCATION NUMBER FOR ONE NODE'
       ASC     '$  PIN  PRINT PRINTER LOCATION NUMBER FOR ONE INTERNAL NODE'
       ASC     '$  PNA  PRINT PRINTER LOCATION NUMBER FOR ALL NODES'
       ASC     '$* ANO  ADD NEW NODE'
       ASC     '$* AIN  ADD NEW INTERNAL NODE'
       ASC     '$* CNO  CHANGE NODE'
       ASC     '$* CIN  CHANGE INTERNAL NODE'
       ASC     '$* DNO  DELETE NODE'
       ASC     '$* DIN  DELETE INTERNAL NODE'
       ASC     '$* APL  ADD PRINTER LOCATION AND NODES'
       ASC     '$* CPL  CHANGE PRINTER LOCATION INFORMATION'
       ASC     '$* DPL  DELETE PRINTER LOCATION AND ASSOCIATED NODES'
       ASC     '$$* INDICATES CUSTOMER SERVICE LICENSE REQUIRED'
       ASC     '$$ + INDICATES INTERNAL NODE$$/'

PPI    ZRO
       LDA     =-1
       STA     1SW
       LDX     =-4096
       LDA     PL
       LDB     =3777B
       LSH     12
       SKM     PNBUFE,2
       BRU     *+2
       BRM     PR
       BRX     *-3
       BRR     PPI
PR     ZRO
       STX     T
       SKN     1SW
       BRU     PR1
       LDA     CR
       CIO     OFILE
       TYPE    M1
       LDA     PL
       LDB     =10
       LDX     OFILE
       BRS     36
       LDA     CR
       CIO     OFILE
       BRM     GPLA            GET PRINTER LOCATION ADDRESS
       SPTR    L1
       BRM     PB1LIN          PRINT LINE DELETING TRAILING BLANKS
       SPTR    L2
       BRM     PB1LIN
       SPTR    L3
       BRM     PB1LIN
       SPTR    L4
       BRM     PB1LIN
       SPTR    TN
       BRM     PB1LIN
       TYPE    M2
       MIN     1SW
       LDX     T
PR1    CXA
       ADD     =7777B
       ETR     =3777B
       LDB     =8
       LDX     OFILE
       BRS     36
       LDA     T
       SKG     =-4000B
       BRU     *+2
       TYPE    M33
       LDA     CR
       CIO     OFILE
       LDA     PL
       LDB     =3777B
       LSH     12
       LDX     T
       BRR     PR

PB1LIN ZRO
       ADD     =60
       XAB
       STP     TP
       GCD     TP
       BRR     PB1LIN
       SNE     =0
       BRU     *-3
       WCI     TP
       LDP     TP
       LDX     OFILE
       BRS     35
       LDA     CR
       CIO     OFILE
       BRR     PB1LIN


WNPI   ZRO
       LDP     NODPRI
       LDX     =2
       BRS     65
       BRU     GNPIE1
       STA     FILE
       LDA     =4096
       LDX     =PNBUFF
       BIO     FILE
       BRU     WNPIE1
       LDA     FILE
       BRS     20
       SKR     UPDFLG
       BRU     *-1
       BRR     WNPI

WNPIE1 TYPE    M20
       BRS     10


GNPI   ZRO
       LDP     NODPRI
       BRS     64
       BRU     GNPIE1
       STA     FILE
       LDA     =4096
       LDX     =PNBUFF
       BIO     FILE
       BRU     GNPIE2
       LDA     FILE
       BRS     20
       BRR     GNPI

GNPIE1 TYPE    M6
       BRS     10
GNPIE2 TYPE    M7
       BRS     10
WPLA   ZRO
       LDP     PRILOC
       LDX     =2
       BRS     65
       BRU     GPLAE1
       STA     FILE
       LDA     PL
       MUL     =128*3
       LSH     23
       MRG     =4B7
       SCP     FILE
       BRU     WPLAE1
       LDA     =128
       LDX     =ADBUFF
       BIO     FILE
       BRU     WPLAE1
       LDA     FILE
       BRS     20
       SKR     UPDFLG
       BRU     *-1
       BRR     WPLA

WPLAE1 TYPE    M21             (UNABLE TO UPDATE ADDRESS FILE)
       BRS     10

WUPDFL ZRO
       LDP     UPDFL
       LDX     =2
       BRS     65
       BRU     WUPDE1
       STA     FILE
       CLA
       LDX     =-128
       STA     ADBUFE,2
       BRX     *-1
       LDA     =128
       LDX     =ADBUFF
       BIO     FILE
       BRU     WUPDE2
       LDA     FILE
       BRS     20
       BRR     WUPDFL

WUPDE1 TYPE    M22
       BRS     10
WUPDE2 TYPE    M23
       BRS     10

GPLA   ZRO
       LDP     PRILOC
       BRS     64
       BRU     GPLAE1
       STA     FILE
       LDA     PL
       MUL     =128*3
       LSH     23
       SCP     FILE
       BRU     GPLAE2
       LDA     =128
       LDX     =ADBUFF
       BIO     FILE
       BRU     GPLAE2
       LDA     FILE
       BRS     20
       BRR     GPLA

GPLAE1 BRS     181
       STP     IFILE
       BRS     17
       TYPE    M3              UNABLE TO OPEN FILE
       BRU     CMDERR
GPLAE2 SKN     ASW
       BRU     *+2
       BRU     C
       TYPE    M4
       BRU     CMDERR

*M
M1     ASC     'PRINTER LOCATION NUMBER /'
M2     ASC     '$NODES$$/'
M3     ASC     '$UNABLE TO OPEN PRINTER ADDRESS FILE$/'
M4     ASC     '$UNABLE TO READ PRINTER ADDRESS FILE$/'
M5     ASC     'NODE NUMBER /'
M6     ASC     '$UNABLE TO OPEN NODE PRINTER FILE$/'
M7     ASC     '$UNEXPECTED END OF FILE--NODE PRINTER FILE$/'
M8     ASC     '$NO PRINTER LOCATION DEFINED FOR THIS NODE$/'
M9     ASC     '$NO NODES FOR THIS PRINTER LOCATION$/'
M10    ASC     '$OUTPUT TO: /'
M11    ASC     '$NODE: /'
M12    ASC     '$PRINTER LOCATION NUMBER: /'
M13    ASC     '$NODE ALREADY ASSIGNED$/'
M14    ASC     '$PRINTER LOCATION IS /'
M15    ASC     '$ENTER NEW PRINTER LOCATION: /'
M16    ASC     '$OK? /'
M17    ASC     '$PRINTER LOCATION: /'
M18    ASC     '$END OF JOB$/'
M20    ASC     '$UNABLE TO UPDATE NODE PRINTER FILE$/'
M21    ASC     '$UNABLE TO UPDATE PRINTER ADDRESS FILE$/'
M22    ASC     '$UNABLE TO OPEN COMMUNICATIONS FILE$/'
M23    ASC     '$UNABLE TO WRITE COMMUNICATIONS FILE$/'
M29    ASC     '$$NODE   PRINTER LOCATION NUMBER$$/'
M27    ASC     '$INVALID NODE NUMBER$/'
M26    ASC     '$INVALID PRINTER LOCATION$/'
M28    ASC     '        /'
M28A   ASC     ' +      /'
M30    ASC     '$ENTER ADDRESS, UP TO FOUR LINES.'
       ASC     '$TWO CARRIAGE RETURNS TERMINATES ADDRESS$/'
M31    ASC     '$ENTER TELEPHONE NUMBER: /'
M32    ASC     '$ENTER NODES TERMINATING WITH A "."$/'
M33    ASC     ' +/'

*P
ASCP   MACRO   D
NNN    NCHR    D(1)
       DATA    **3+5+716B5,**3+NNN
       ASC     D(1)
       ENDM
PRILOC ASCP    '(SPOOL)PRIADR'
NODPRI ASCP    '(SPOOL)NODPRI'
UPDFL  ASCP    '(SPOOL)CHNODPRI'

YN     ZRO
       CIO     IFILE
       SKNE    ='Y'
       MIN     YN
       SKNE    CR
       BRR     YN
       CIO     IFILE
       BRU     *-3

RTN    ZRO
PONO   NOP     RTN
       LDB     =8
       BRU     *+3
PDNO   NOP     RTN
       LDB     =10
       LDX     OFILE
       BRS     36
       SBRR    PONO

RONO   NOP     RTN
       LDB     =8
       BRU     *+3

RDNO   NOP     RTN
       LDB     =10
       LDX     IFILE
       BRS     38
       SBRR    RONO

VER    ZRO; SKN VERSW; BRR VER
       BRS 71; BRU VER1A
       SKR STASW; BRU *-1; BRU VER1B
VER1A  LDP SFPTR; BRS 48; BRU ERV1; LDB =SFBUF
       LDX =4; BRS 96; LDA =4; SKA SFBUF+3; BRU *+2; BRU ERV2
       LPTR; BRS 64; BRU OPFER; STA SFIL; CLA; SCP SFIL; HLT 0
VER1B  TYPE VM1 (ENTER NAME)
       LDA VPTR1; STA VPTR1+1; LDA =VPTR1; LDB =155B; CLX; BRS 33
       SKN STASW; BRU *+4; BRM EDVER; MIN VERSW; BRR VER
       LDA VPTR1+1; SUB VPTR1; STA VT1
VER1   CIO SFIL; SKE =137B; BRU *+2; BRU NONAME
       SKE =155B; BRU VER1; CIT SFIL; BRU *+2; BRU NONAME
       LDA VPTR2; STA VPTR2+1; LDA =VPTR2; LDB =155B
       LDX SFIL; BRS 33; LDA VPTR2+1; SUB VPTR2
       SKG =0; BRU NONAME; SKE VT1; BRU VER1
       LDP VPTR2; SKSE VPTR1; BRU VER1; GCI VPTR1; HLT 0
       SKR VPTR1; NOP; STA VXT8; BRM FONO; SKA =40000B
       BRU *+2; BRU ERV3; SKA =1B7; BRU *+2; BRU ERV3
       SKA     =4B6
       BRU     *+2
       BRU     ERV3
       ETR =377B; STA DIST
       BRM FONO; STA VT1; BRM FONO; STA VT2; LDA SFIL; BRS 20
       LDX =-1; LDA =3; BRS 12; TYPE VM2 (ENTER YOUR PASSWORD PLEASE)
SGPS   LDP TERMWD; STP VXT1; STP VXT3; STB VXT5
       CLA; STA TLV1; LDA VXT8; ADM VXT3
GPS    LDA =20; STA TLV11
GPS1   LDX TLV1; LDA VXT1,2; MUL =6065B
       DIV =37777761B; CBA; ADD VXT2,2; CAB; ETR =3
       STA TLV1; LDA VXT1,2; XAB; STP VXT1,2; LDA VXT5
       XMA VXT1; STA VXT5; SKR TLV11
       BRU GPS1; CIO =0; SKE =155B; BRU *+2; BRU *+3
       ADM VXT1; BRU GPS
       LDX =-1; LDA =4B4; BRS 12
       LDA VXT1; ADD VXT2; SKE VT1; BRU NOPASS; LDA VXT3; ADD VXT4
       SKE VT2; BRU NOPASS; BRM EDVER; MIN VERSW; BRR VER

FONO   ZRO; LDB =8; LDX SFIL; BRS 38; BRR FONO

TLV1   ZRO
TLV11  ZRO

VERSW  DATA -1
STASW  ZRO
DIST   ZRO
NOPASS TYPE VME5; LDX =-1; LDA =3; BRS 12; BRU SGPS

NONAME TYPE VME6; BRU VERRET


TERMWD DATA 27657537B,55555555B

SFPTR  DATA 3*SF-1,3*SF+5
SF     ASC '()PSWD'
VPTR1   DATA 3*OPNAM-1,3*OPNAM-1
OPNAM  BSS 20
VPTR2   DATA 3*BUF2-1,3*BUF2-1
BUF2   BSS 20
SFBUF  BSS 20
VT1    ZRO
VT2    ZRO
VXT1   ZRO
VXT2   ZRO
VXT3   ZRO
VXT4   ZRO
VXT5   ZRO
VXT8   ZRO
SFIL   ZRO
ERV1   TYPE VME1
ERV    TYPE VME; BRU VERRET
ERV2   TYPE VME2; BRU ERV
OPFER  TYPE VME3; BRU VERRET
ERV3   TYPE VME4; BRU VERRET
VERRET LDX =-20; CLA; STA OPNAM+20,2; STA BUF2+20,2; STA SFBUF+20,2
       BRX *-3; BRS 10

EDVER  ZRO; LDP VPTR1; BRM EDLIN; STP VPTR1; BRR EDVER
EDLIN  ZRO; STP EDPT
EDLIN2 GCD EDPT; BRU EDLIN1; SKE =200B; BRU *+2; BRU EDLIN2
       SKE =377B; BRU *+2; BRU EDLIN2
       SKG =0; BRU EDLIN2; MIN EDPT+1
EDLIN1 LDP EDPT; BRR EDLIN
EDPT   BSS 2
VME1    ASC '$NO PASSWORD FILE/'
VME     ASC '$NOTIFY DIVISION OPNS MGR AND SYS. GROUP TECH DIV/'
VME2    ASC '$PASSWORD FILE STATUS LOST/'
VME3    ASC '$PASSWORD FILE BUSY-TRY LATER$/'
VME4    ASC '$UNVALIDATED NAME/'
VM1     ASC '$YOUR NAME: /'
VM2     ASC '$YOUR PASSWORD: /'
VME5    ASC '$ERROR, TYPE PASSWORD /'
VME6    ASC '$NAME DOES NOT APPEAR IN PASSWORD FILE$/'
       END
1PNUPD ** FICH/FRAME BREAK *****
PNUPD3 SYMSORT

SYM.  PG.LN. IDENT.

1SW     1 26 PNUPD   ADBUFE  2  1 PNUPD   ADBUFF  1 34 PNUPD   
AIN     7 16 PNUPD   ANO     7 18 PNUPD   ANO1    7 25 PNUPD   ANO2    7 29 PNUPD   
ANOE1   8 12 PNUPD   APL    13  3 PNUPD   APL1   13 24 PNUPD   APL2   13 39 PNUPD   
APL3   14 16 PNUPD   APL4   14 30 PNUPD   ASCP   21 19 PNUPD   ASW     1 25 PNUPD   
BL      4 12 PNUPD   BLE     4 27 PNUPD   BUF2   23 36 PNUPD   C       2 27 PNUPD   
CINR    8 15 PNUPD   CL      3 35 PNUPD   CLE     4 11 PNUPD   CMD     1 24 PNUPD   
CMDERR  3 11 PNUPD   CNO     8 17 PNUPD   CNO1    8 24 PNUPD   CNO2    8 28 PNUPD   
CPL    11 15 PNUPD   CPL1   12 22 PNUPD   CPLA   11 22 PNUPD   CPLB   11 26 PNUPD   
CR      1 22 PNUPD   DIN     9 13 PNUPD   DIST   23 23 PNUPD   DNO     9 15 PNUPD   
DNO1    9 22 PNUPD   DNO2    9 26 PNUPD   DPL    10 10 PNUPD   DPL1   10 36 PNUPD   
DPL2   11  7 PNUPD   DPLA   10 17 PNUPD   DPLB   10 21 PNUPD   EDLIN  24 17 PNUPD   
EDLIN1 24 21 PNUPD   EDLIN2 24 18 PNUPD   EDPT   24 22 PNUPD   EDVER  24 16 PNUPD   
ERV    24  9 PNUPD   ERV1   24  8 PNUPD   ERV2   24 10 PNUPD   ERV3   24 12 PNUPD   
ESC     3 23 PNUPD   ESCL    3 22 PNUPD   FILE    1 12 PNUPD   FONO   23 16 PNUPD   
GNPI   18  7 PNUPD   GNPIE1 18 20 PNUPD   GNPIE2 18 22 PNUPD   GPLA   19 33 PNUPD   
GPLAE1 20 12 PNUPD   GPLAE2 20 17 PNUPD   GPS    23  5 PNUPD   GPS1   23  6 PNUPD   
HELP   15  5 PNUPD   HMSG   15 11 PNUPD   IFILE   1 14 PNUPD   INTFL   1 27 PNUPD   
IVN    14 32 PNUPD   IVP    14 34 PNUPD   L1      1 35 PNUPD   L2      1 36 PNUPD   
L3      1 37 PNUPD   L4      1 38 PNUPD   M1     20 24 PNUPD   M10    20 33 PNUPD   
M11    20 34 PNUPD   M12    20 35 PNUPD   M13    20 36 PNUPD   M14    20 37 PNUPD   
M15    20 38 PNUPD   M16    20 39 PNUPD   M17    21  1 PNUPD   M18    21  2 PNUPD   
M2     20 25 PNUPD   M20    21  3 PNUPD   M21    21  4 PNUPD   M22    21  5 PNUPD   
M23    21  6 PNUPD   M26    21  9 PNUPD   M27    21  8 PNUPD   M28    21 10 PNUPD   
M28A   21 11 PNUPD   M29    21  7 PNUPD   M3     20 26 PNUPD   M30    21 12 PNUPD   
M31    21 14 PNUPD   M32    21 15 PNUPD   M33    21 16 PNUPD   M4     20 27 PNUPD   
M5     20 28 PNUPD   M6     20 29 PNUPD   M7     20 30 PNUPD   M8     20 31 PNUPD   
M9     20 32 PNUPD   N       1 17 PNUPD   NNN    21 20 PNUPD   NODPRI 21 25 PNUPD   
NONAME 23 26 PNUPD   NOPASS 23 24 PNUPD   OFILE   1 15 PNUPD   OPFER  24 11 PNUPD   
OPNAM  23 34 PNUPD   PB1LIN 17  9 PNUPD   PDNO   22  2 PNUPD   PIN     5 30 PNUPD   
PL      1 28 PNUPD   PNA     6 22 PNUPD   PNA1    6 34 PNUPD   PNA2    6 30 PNUPD   
PNBUFE  1 32 PNUPD   PNBUFF  1 31 PNUPD   PNO     5 32 PNUPD   PNO2    5 36 PNUPD   
PNO3    6 19 PNUPD   PONO   21 38 PNUPD   PPA     5  5 PNUPD   PPI    15 30 PNUPD   
PPN     4 29 PNUPD   PR     16  3 PNUPD   PR1    16 30 PNUPD   PRILOC 21 24 PNUPD   
PROMPT  1 23 PNUPD   QUIT   14 37 PNUPD   RDLINE 12 32 PNUPD   RDNO   22 12 PNUPD   
RONO   22  8 PNUPD   RTN    21 37 PNUPD   SA      1 18 PNUPD   SB      1 19 PNUPD   
SF     23 32 PNUPD   SFBUF  23 37 PNUPD   SFIL   24  7 PNUPD   SFPTR  23 31 PNUPD   
SGPS   23  3 PNUPD   SKNE    1  8 PNUPD   SPTR    1  9 PNUPD   START   2 18 PNUPD   
STASW  23 22 PNUPD   SX      1 20 PNUPD   T       1 16 PNUPD   TERMWD 23 29 PNUPD   
TFILE   1 13 PNUPD   TLV1   23 18 PNUPD   TLV11  23 19 PNUPD   TN      1 39 PNUPD   
TP      1 21 PNUPD   TYPE    2  3 PNUPD   UPDFL  21 26 PNUPD   UPDFLG  1 29 PNUPD   
VER    22 18 PNUPD   VER1   22 28 PNUPD   VER1A  22 21 PNUPD   VER1B  22 24 PNUPD   
VERRET 24 13 PNUPD   VERSW  23 21 PNUPD   VM1    24 28 PNUPD   VM2    24 29 PNUPD   
VME    24 24 PNUPD   VME1   24 23 PNUPD   VME2   24 25 PNUPD   VME3   24 26 PNUPD   
VME4   24 27 PNUPD   VME5   24 30 PNUPD   VME6   24 31 PNUPD   VPTR1  23 33 PNUPD   
VPTR2  23 35 PNUPD   VT1    23 38 PNUPD   VT2    23 39 PNUPD   VXT1   24  1 PNUPD   
VXT2   24  2 PNUPD   VXT3   24  3 PNUPD   VXT4   24  4 PNUPD   VXT5   24  5 PNUPD   
VXT8   24  6 PNUPD   WNPI   17 26 PNUPD   WNPIE1 18  3 PNUPD   WPLA   18 24 PNUPD   
WPLAE1 19  7 PNUPD   WUPDE1 19 28 PNUPD   WUPDE2 19 30 PNUPD   WUPDFL 19 10 PNUPD   
YN     21 28 PNUPD   
1PNUPD ** FICH/FRAME BREAK *****
BUILDPNUPD
NARP
PNUPD3
B


RES
XDD
;TB
;U%F
SAV 3,3777 O 1PNUPD

240
DEL B
"

CHECKSUMS


THE GO FILE SHOULD BE DECLARED PROPRIETARY
WITH EXEC LICENSE.

"
CHECKSUM
.BUILDPNUPD-BUILD-FILE
PNUPD3-SOURCE-FILE
1PNUPD-GO-FILE
.
COM T
 4@l«