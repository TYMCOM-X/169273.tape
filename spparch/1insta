1INSTALL ** FICHE/FRAME BREAK *****
INSTALL5
INSTAL IDENT  6-2-76    DUMPS ONTO DISC 2-16, EXCLUDING DISC 11 AND 14
*AND IPL LOADER TO DISC ADDRESS 0. INCLUDES MAKING COMPARE FILE FOR IPL
       BRU START; BRS 10

VERS   ASC '$1INSTALL VERSION 5.00$/'
       ASC 'BLOOD'

SPTR   OPD 514B5,2
MSG    POPD 100B5,2
       STP SA; STX SX
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX TOUT; BRS 34
MSG1   LDP SA; LDX SX; BRR 0

MSGP   POPD 101B5,2
       STP SA; STX SX
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX FNO; BRS 34; BRU MSG1

MERR   POPD 102B5,2
       BRS 181; STP CIN
       LDX =-1; BRS 11; EAX* 0; CXA; ETR =37777B; LDB =-1; LDX TOUT; BRS 34
       BRU RESET

TMSG   POPD 103B5,2
       STP SA; STX SX; EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BRS 34
       BRU MSG1
SA     BSS 2
SX     BSS 1

START  STP CIN; BRS 39; SKA =1B7; BRU START1; MSG NOPER; BRS 10
NOPER  ASC '$MINIMUM OF OPERATORS LICENSE REQUIRED$/'
START1 RSH 2; ETR =377B; STA DIST; MSG VERS; BRM EQUIP; BRM MDSI1

RESET  CLA; STA ERFLG; STA SECSW; STA LOADSW; STA RECSW
       LDA =4B4; LDX =-1; BRS 12
       BRS 147
GLTCH  LDA =155B; CIO TOUT; CIO TOUT; LDA =32B; CIO TOUT
GLTCH1 CIO CIN; SKG =77B; BRU *+2; BRU GLTCH1; LRSH 8; CIO CIN
       SKG =77B; BRU *+2; BRU CHK; LRSH 8; CIO CIN; SKG =77B
       BRU *+2; BRU ERR; LRSH 8; CIO CIN; SKG =77B; BRU *-2
       LDX =CTBL-CTBLND; CBA; SKE CTBLND,2; BRX *-1; BRU* BTBLND,2
CHK    CLA; LSH 8; SKE =61B; BRU *+2; BRU QUIT; SKE =37B
       BRU ERR; BRU HEL
ERR    LDA ERFLG; SKE =0; BRU ERR1A; MIN ERFLG; MSG ERMSG; BRU GLTCH
ERR1A   MSG ERMSG1; BRU RESET
CTBL   ASC 'LEHIUQITUSYSMIDAOLMYTAHCVOMPOCREVKAMEHC'
CTBLND DATA 0,HEL,QUIT,UTIL,SYSTEM,DIM,LOADER,TNET,CHGMAP,MVSYS
       DATA COPROU,VERROU,CHEROU,CHECK
BTBLND DATA ERR
ERMSG1 ASC '$FOR ASSISTANCE, TYPE HELP$/'
ERMSG  ASC '$?$/'
ERFLG  ZRO
GTFIL  ZRO; LDP HNPTR; STP NAMPTR; MSG M1 (FILE NAME)
       BRM CLBUF
GTFIL1     CIO CIN; SKE =155B; BRU *+2; BRU GTFIL2; WCI NAMPTR; BRU GTFIL1
GTFIL2 LDP NAMPTR; BRS 64; BRU FNERR; STA FNI; BRR GTFIL
UTIL   BRM VER; LDA DIST; SKE =0; MERR EM1 (MUST BE DISTRICT 0)
       BRM GTFIL; BRM GTDISC; SNE =10B; BRU UTIL1; SNE =15B; BRU UTIL1
       SNE =16B; BRU UTIL1; SNE =17B; BRU UTIL1; BRU NOUTIL
UTIL1  STA TO; STA HLOC; BRM CALCAD; STA TOADR; STA DISCW; LDA =7
       STA PAGES; BRM DFILE
       BRM GTCSM; MSG M3 (LOC.); LDA TO; BRM PONO; LDA =0; CIO TOUT
       LDA CKSM1; BRM PONO; BRU MAPUPD

TNET   BRM VER; SKN TNETSW; MERR EM15 (MUST HAVE TYMNET LICENSE)
       BRM GTFIL; BRM GTDISC; SNE =11B; BRU UTIL1; SNE =14B; BRU UTIL1
       MERR EM16 (ONLY 11 OR 14 ALLOWED)
DIM    BRM VER; SKN INSTSW; MERR VERR1
       LDA EQNO; ETR =7; SKE =6; MERR EM7; BRM GTFIL
       LDA =6; STA TO; BRM CALCAD; STA TOADR; ADD =40B*4; STA TOADR1
       BRM TDNAME; BRM CKSTAT; LDA TOADR; STA DISCR; LDA TOADR1; STA DISCW
       LDA =3; STA CNT
DIM1   BRM CPG; BRM RPG; BRM WPG; SKR CNT; BRU DIM1
       LDA TOADR; STA DISCW; LDA =3; STA PAGES; BRM DFILE
       BRM GCSM6A; BRM GCSM6B; MSG M4 (LOC 6A); LDA CKSM6A; BRM PONO
       MSG M5 (LOC. 6B); LDA CKSM6B; BRM PONO; BRU DMUPD
TDNAME ZRO; LDA FN; SKE DIMNAM; BRU CKS2; BRR TDNAME

SYSTEM BRM VER; SKN INSTSW; MERR VERR1; BRM GTFIL; BRM GTDISC
       SKE =2; BRU *+2; BRU SYS1
       SKE =4; BRU *+2; BRU SYS1
       SKE =12B; BRU *+2; BRU SYS1
       MERR EM8
SYS1   STA TO; STA HLOC; ADD =1; STA TO1; LDA TO; BRM CALCAD; STA TOADR
       LDA TO1; BRM CALCAD; STA TOADR1; BRM TSNAME; BRM CKSTAT; LDA TOADR
       STA DISCW; LDA =7; STA PAGES; BRM DFILE; LDA FN; ETR =77600377B
       MRG FN1; STA FN; LDP NAMPTR; BRS 64; MERR EM9
       STA FNI; SKR SECSW; BRU *-1; BRM CKSTAT; LDA TOADR1
       STA DISCW; LDA =7; STA PAGES; BRM DFILE
       BRM GTCSM; MSG M3; LDA TO; BRM PONO; LDA =0; CIO TOUT
       LDA CKSM1; BRM PONO; MSG M3; LDA TO1; BRM PDNO; LDA =0; CIO TOUT
       LDA CKSM2; BRM PONO; BRU SMUPD
TSNAME ZRO; LDA FN; ETR =@377B; SKE SYSNAM; BRU CKS2; BRR TSNAME

LOADER BRM VER; LDA DIST; SKE =0; MERR EM14
       LDA EQNO; ETR =7; SKE =6; MERR EM7
       BRM GTFIL; BRM TLNAME; SKR LOADSW; BRU *-1; BRM CKSTAT
       BRM CPG
       WIO FNI; SKE =0; MERR EM12; WIO FNI; SKE =17777B; MERR EM12
       LDA =3*17664B+9; SCP FNI; HLT
       LDX =17664B-20000B; LDA =3*BFR-1; CAB; STP P
L1     CIO FNI; CIO FNI; WCI P; CIO FNI; WCI P; BRX L1
       LDA =9; SCP FNI; HLT; LDX =-44B
L2     CIO FNI; CIO FNI; WCI P; CIO FNI; WCI P; BRX L2
       LDA =BFR; LDB =7B7; LDX =400B; BRS 125
       BRS 147; LDP IPLFPT; LDX =2; BRS 65; HLT; STA FNI
       LDX =BFR; LDA =400B; BIO FNI; NOP
       BRU RESET
FILIPL ASC '(*6UTIL)IPLCKFILE'
IPLFPT DATA 3*FILIPL-1,3*FILIPL-1+17

TLNAME ZRO; LDA FN; SNE LOANAM; BRR TLNAME; MERR EM13


EQUIP  ZRO; LDA =CBUF; LDX =12; BRS 7; LDA CBUF; STA EQNO; BRR EQUIP


CALCAD ZRO; CLB; LSH 13; ADD =17400B; BRR CALCAD

WPG    ZRO; LDA =CBUF; LDX =4000B; LDB DISCW; BRS 125; LDA =40B
       ADM DISCW; BRR WPG

RPG    ZRO; LDA =CBUF; LDX =4000B; LDB DISCR; BRS 124; LDA =40B
       ADM DISCR; BRR RPG

CPG    ZRO; CLA; LDX =-4000B; STA EBUF,2; BRX *-1; BRR CPG








GTDISC ZRO; MSG M2 (DUMP TO LOC); BRM RONO; BRR GTDISC

NOUTIL MERR EM2 (NOT AUTHORIZED LOC FOR UTIL)


DFILE  ZRO; BRM CPG; WIO FNI; WIO FNI; WIO FNI; LDA PAGES; STA CNT
DFILE1 BRM BIOPGI; BRM WPG; BRM CPG; SKN FNI; BRU *+2; BRU DFILE2
       SKR CNT; BRU DFILE1; BRR DFILE
DFILE2 SKR CNT; BRU DFILE3; BRR DFILE
DFILE3 BRM WPG; BRU DFILE2

BIOPGI ZRO; LDA =4000B; LDX =CBUF; BIO FNI; NOP; BRR BIOPGI

CKSTAT ZRO; LDP NAMPTR; BRS 48; HLT; LDB =STABUF; LDX =6
       BRS 96; LDA STABUF+3; SKA =2; BRR CKSTAT
       SKN LOADSW; BRU *+2; MERR EM13
       SKN SECSW; BRU *+2; BRU CKS1
CKS2   MSG EM4
       LDA TO; ADD =20B; CIO TOUT; LDA =155B; CIO TOUT; BRU RESET
CKS1   MSG EM11; LDA TO1; ADD =20B; CIO TOUT; LDA =155B; CIO TOUT
       MERR EM10


M1     ASC '$FILE NAME: /'
M2     ASC '$DUMP TO LOC: /'
M3     ASC '$LOC. /'
M4     ASC '$LOC. 6A /'
M5     ASC '$LOC. 6B /'
M6     ASC ' LOC. /'
M7     ASC 'CHECKSUM LOC.: /'
EM1    ASC '$UTILITY PROGRAMS MAY ONLY BE DUMPED BY DISTRICT 0 PERSONNEL$/'
EM2    ASC '$INVALID LOC.  NOT AUTHORIZED FOR UTILITY PROGRAMS$/'
EM4    ASC '$NOT AN AUTHORIZED PROGRAM TO DUMP ON LOC. /'
EM7    ASC '$NOT A DISC PACK SYSTEM$/'
EM8    ASC '$INVALID LOC. NOT AUTHORIZED FOR SYSTEM PROGRAMS$/'
EM9    ASC '$UNABLE OPEN 2ND SYSTEM FILE.  1ST SYSTEM FILE ALREADY'
       ASC '$DUMPED$/'
EM10   ASC '$1ST SYSTEM FILE ALREADY DUMPED$/'
EM11   ASC '$2ND SYSTEM FILE IS NOT AN AUTHORIZED FILE TO'
       ASC '$DUMP ON LOC /'
EM12   ASC '$FILE NOT SAVED FROM 0 T 17777$/'
EM13   ASC '$UNAUTHORIZED FILE FOR DUMPING AS DIM LOADER$/'
EM14   ASC '$DIM LOADER MAY ONLY BE DUMPED BY DISTRICT 0 PERSONNEL$/'
EM15   ASC '$TYMNET LICENSE REQUIRED TO DUMP BASE CODE$/'
EM16   ASC '$BASE CODE MAY ONLY BE DUMPED TO LOC. 11 OR LOC. 14$/'
EM17   ASC '$INVALID LOCATION$/'
EM18   ASC '$NO PROGRAM AT THIS LOCATION$/'
EOJ    ASC '$END OF JOB$/'

NAMPTR DATA 3*FN-1,3*FN-1
HNPTR  DATA 3*FN-1,3*FN-1
FN     BSS 40
FN1    ASC ' 2 '
FNERR  LDX =-1; BRS 11; BRU GTFIL+1
DIMNAM ASC 'DIM'
SYSNAM ASC 'M1 '
LOANAM ASC 'IPL'

CIN    ZRO
TOUT   ZRO
FNI    ZRO
EQNO   ZRO
PAGES  ZRO
SECSW  ZRO
LOADSW ZRO
TO     ZRO
TO1    ZRO
TOADR  ZRO
TOADR1 ZRO
TLV1   ZRO
TLV11  ZRO
CNT    ZRO
DISCW  ZRO
DISCR  ZRO
P      DATA 0,0
STABUF BSS 20
CBUF    EQU 20000B
EBUF   EQU CBUF+4000B
BFR    EQU 20000B
VER    ZRO; SKN VERSW; BRR VER; BRS 71; BRU VER1A; CBA; SKE =6B7; BRU VER1A
       SKR STASW; BRU *-1; SKR INSTSW; BRU *-1; SKR TNETSW; BRU *-1
       CLA; STA DIST; BRU VER1B
VER1A  LDP SFPTR; BRS 64; BRU OPFER; STA SFIL
VER1C  CLA; SCP SFIL; HLT 0
VER1B  TMSG VM1 (ENTER NAME)
       LDA VPTR1; STA VPTR1+1; LDA =VPTR1; LDB =155B; CLX; BRS 33
       SKN STASW; BRU *+3; MIN VERSW; BRR VER
       LDA VPTR1+1; SUB VPTR1; STA VT1
VER1   CIO SFIL; SKE =137B; BRU *+2; BRU NONAME
       SKE =155B; BRU VER1; CIT SFIL; BRU *+2; BRU NONAME
       LDA VPTR2; STA VPTR2+1; LDA =VPTR2; LDB =155B
       LDX SFIL; BRS 33; LDA VPTR2+1; SUB VPTR2
       SKG =0; BRU NONAME; SKE VT1; BRU VER1
       LDP VPTR2; SKSE VPTR1; BRU VER1; GCI VPTR1; HLT 0
       SKR VPTR1; NOP; STA VXT8; BRM FONO; SKA =40000B
       BRU *+2; BRU ERV3; SKA =1B6; BRM SETINS; SKA =2B6; BRM SETNET
       ETR =377B; STA DIST
       BRM FONO; STA VT1; BRM FONO; STA VT2; LDA SFIL; BRS 20
       LDX =-1; LDA =3; BRS 12; TMSG VM2 (ENTER YOUR PASSWORD PLEASE)
SGPS   LDP TERMWD; STP VXT1; STP VXT3; STB VXT5
       CLA; STA TLV1; LDA VXT8; ADM VXT3
GPS    LDA =20; STA TLV11
GPS1   LDX TLV1; LDA VXT1,2; MUL =6065B
       DIV =37777761B; CBA; ADD VXT2,2; CAB; ETR =3
       STA TLV1; LDA VXT1,2; XAB; STP VXT1,2; LDA VXT5
       XMA VXT1; STA VXT5; SKR TLV11
       BRU GPS1; CIO =0; SKE =155B; BRU *+2; BRU *+3
       ADM VXT1; BRU GPS
       LDX =-1; LDA =4B4; BRS 12
       LDA VXT1; ADD VXT2; SKE VT1; BRU NOPASS; LDA VXT3; ADD VXT4
       SKE VT2; BRU NOPASS; MIN VERSW; BRR VER

FONO   ZRO; LDB =8; LDX SFIL; BRS 38; BRR FONO


NOPASS TMSG VME5; LDX =-1; LDA =3; BRS 12; BRU SGPS

NONAME TMSG VME6; BRU VERRET


TERMWD DATA 27657537B,55555555B

SFPTR  DATA 3*SF-1,3*SF+5
SF     ASC '()PSWD'
VPTR1   DATA 3*OPNAM-1,3*OPNAM-1
$OPNAM  BSS 20
VPTR2   DATA 3*BUF2-1,3*BUF2-1
BUF2   BSS 20
SFBUF  BSS 20
VT1    ZRO
VT2    ZRO
VXT1   ZRO
VXT2   ZRO
VXT3   ZRO
VXT4   ZRO
VXT5   ZRO
VXT8   ZRO
SFIL   ZRO
ERV    TMSG VME; BRS 10
OPFER  TMSG VME3; BRS 10
ERV3   TMSG VME4; BRS 10
VERRET LDX =-20; CLA; STA OPNAM+20,2; STA BUF2+20,2; STA SFBUF+20,2
       BRX *-3; LDA =4B4; LDX =-1; BRS 12; BRU VER1C

SETINS ZRO; SKR INSTSW; BRU *-1; BRR SETINS
SETNET ZRO; SKR TNETSW; BRU *-1; BRR SETNET
VME    ASC '$NOTIFY DIVISION OPNS MGR AND SYS. GROUP TECH DIV/'
VME3   ASC '$PASSWORD FILE BUSY-TRY LATER$/'
VME4   ASC '$UNVALIDATED NAME/'
VM1    ASC '$YOUR NAME: /'
VM2    ASC '$YOUR PASSWORD: /'
VME5   ASC '$ERROR, TYPE PASSWORD /'
VME6   ASC '$NAME DOES NOT APPEAR IN PASSWORD FILE$/'
INSTSW ZRO
TNETSW ZRO
STASW  ZRO
VERSW  DATA -1
DIST   ZRO

QUIT   MSG EOJ; BRS 10

GTCSM  ZRO; LDA HLOC; STA TO; SNE =2; BRU ADDON; SNE =4; BRU ADDON; SNE =12B
       BRU ADDON; LDA =-1; STA TO1; STA CKSM2
GTCSM1 LDA TO; BRM CALCAD; STA TOADR; LDA TO1; SNE =-1; BRU GTCSM2
       BRM CALCAD; STA TOADR1
GTCSM2 LDA TOADR; STA DISCR; LDA =7; STA CNT; BRM CKSM
       LDA HCKSM; STA CKSM1; LDA TO1; SNE =-1; BRR GTCSM
       LDA TOADR1; STA DISCR; LDA =7; STA CNT; BRM CKSM
       LDA HCKSM; STA CKSM2; BRR GTCSM
ADDON  ADD =1; STA TO1; BRU GTCSM1
CKSM   ZRO; CLA; STA HCKSM; BRM STCKSM
1CKSM  BRM CPG; BRM RPG; LDA =CBUF; STA PTR; ADD =4000B; STA EPTR
       BRM MKSUM; SKR CNT; BRU 1CKSM; BRR CKSM

MKSUM  ZRO; LDA* PTR; MIN PTR; XMA HCKSM; CLB; LCY 7; ADD HCKSM; STB HCKSM
       ADM HCKSM; LDA PTR; SKE EPTR; BRU MKSUM+1; BRR MKSUM

STCKSM ZRO; CLA; BRM 2CKSM; LDA =17777B; SKN SIXSW; LDA =37777B; BRM 2CKSM
       CLA; BRM 2CKSM; BRR STCKSM
2CKSM  ZRO; XMA HCKSM; CLB; LCY 7; ADD HCKSM; STB HCKSM; ADM HCKSM; BRR 2CKSM

CKSM1  ZRO
CKSM2  ZRO
PTR    ZRO
EPTR   ZRO
HCKSM  ZRO
HLOC   ZRO
PDNO   ZRO; LDX TOUT; LDB =10; BRS 36; BRR PDNO
PONO   ZRO; LDX TOUT; LDB =8; BRS 36; BRR PONO

GCSM6A ZRO; SKR SIXSW; BRU *-1
       LDA =6; STA TO; BRM CALCAD; STA DISCR; LDA =3; STA CNT; BRM CKSM
       LDA HCKSM; STA CKSM6A; CLA; STA SIXSW; BRR GCSM6A

GCSM6B ZRO; SKR SIXSW; BRU *-1
       LDA =6; STA TO; BRM CALCAD; STA DISCR; LDA =3; STA CNT
       BRM RPG; SKR CNT; BRU *-2; LDA =3; STA CNT
       BRM CKSM; LDA HCKSM; STA CKSM6B; CLA; STA SIXSW; BRR GCSM6B

SIXSW  ZRO
CKSM6A ZRO
CKSM6B ZRO

SYSNUM DATA 770B


MTAB   BSS 2
L2MSG  DATA 1100B
L3MSG  DATA 1326B
L4MSG  DATA 1554B
L5MSG  DATA 2002B
L6AMSG DATA 2230B
L7MSG  DATA 2456B
L10MSG DATA 2704B
L11MSG DATA 3132B
L12MSG DATA 3360B
L13MSG DATA 3606B
L14MSG DATA 4034B
L15MSG DATA 4262B
L16MSG DATA 4510B
L17MSG DATA 4736B
L6BMSG DATA 2343B

MO     DATA 5164B
DAY    DATA 5167B

CSMTAB BSS 2
L2CSM  DATA 5172B
L3CSM  DATA 5175B
L4CSM  DATA 5200B
L5CSM  DATA 5203B
L6ACSM DATA 5206B
L7CSM  DATA 5211B
L10CSM DATA 5214B
L11CSM DATA 5217B
L12CSM DATA 5222B
L13CSM DATA 5225B
L14CSM DATA 5230B
L15CSM DATA 5233B
L16CSM DATA 5236B
L17CSM DATA 5241B
L6BCSM DATA 5244B

RECSYS DATA 5247B
MOTAB  BSS 2
L2MO   DATA 5252B
L3MO   DATA 5255B
L4MO   DATA 5260B
L5MO   DATA 5263B
L6AMO  DATA 5266B
L7MO   DATA 5271B
L10MO  DATA 5274B
L11MO  DATA 5277B
L12MO  DATA 5302B
L13MO  DATA 5305B
L14MO  DATA 5310B
L15MO  DATA 5313B
L16MO  DATA 5316B
L17MO  DATA 5321B
L6BMO  DATA 5324B

DAYTAB BSS 2
L2DAY  DATA 5327B
L3DAY  DATA 5332B
L4DAY  DATA 5335B
L5DAY  DATA 5340B
L6ADAY DATA 5343B
L7DAY  DATA 5346B
L10DAY DATA 5351B
L11DAY DATA 5354B
L12DAY DATA 5357B
L13DAY DATA 5362B
L14DAY DATA 5365B
L15DAY DATA 5370B
L16DAY DATA 5373B
L17DAY DATA 5376B
L6BDAY DATA 5401B

CHGMAP BRM VER; BRM OMAP; BRU IN
OMAP   ZRO; LDP FILPTR; MRG PRIV; LDX =1; BRS 65; MERR E1; STA FNO
       LDA =BUF; LDB =200B; LDX =100B; BRS 124; BRM GTDATE; BRR OMAP

FILPTR DATA 3*FIL-1,3*FIL-1+16
FIL    ASC '(*6HELP)1DISCMAP';*MAKE *6HELP IN FINAL VERSION
PRIV   DATA 116B5

E1     ASC '$CANNOT OPEN 1DISCMAP$'

FNO    ZRO
BUF    BSS 300B
NOSYS  EQU BUF+63

IN     BRM SDATE; BRU IN1
SDATE  ZRO; LDA SYSNUM; BRM SETCUR
       LDB NOSYS; LDX =-3; CLA; LSH 8; CIO FNO; BRX *-3; LDA SLASH
       CIO FNO; LDA MO; BRM SETCUR
       LDA MONTH; WIO FNO; LDA TODAY; WIO FNO; BRR SDATE
IN1    MSG CM1 (WHICH LOC. DO YOU WISH TO CHANGE)
       BRM CLBUF; BRM RONO; SKG SLASH; BRU *+2; BRU BADDSC; SKG =0; BRU IN1A
       STA HLOC; CAX; LDA OKLOC,2; SNE =-1; BRU BADDSC; SKE HLOC
       BRU BADDSC; LDA HLOC; SNE =6; BRU IN26; CAX; LDA MTAB,2; BRM SETCUR
       BRM CLRB; MSG CM3; BRM GTSTG; BRM GTCSM; LDA =BUF; STA PTR
       LDX =-50; LDA* PTR; MIN PTR; WIO FNO; BRX *-3
       LDX HLOC; LDA CSMTAB,2; BRM SETCUR; LDA CKSM1; WIO FNO
       LDA HLOC; SNE =2; BRU IN2A; SNE =4; BRU IN2A; SNE =12B; BRU IN2A
IN2B   LDX HLOC; LDA MOTAB,2; BRM SETCUR; LDA MONTH; WIO FNO
       LDX HLOC; LDA DAYTAB,2; BRM SETCUR; LDA TODAY; WIO FNO
       CLA; SCP FNO; HLT 0; BRU MORE
IN2A   SKR RECSW; BRU *-1
       ADD =1; CAX; LDA CSMTAB,2; BRM SETCUR; LDA CKSM2; WIO FNO
       BRU IN2B
IN26   LDA L6AMSG; BRM SETCUR; MSG CM7 (ENTER LOC 6A SYS OR PROG)
       BRM CLRB; BRM GTSTG; BRM GCSM6A; BRM W6; LDA L6ACSM; BRM SETCUR
       LDA CKSM6A; WIO FNO; LDA L6AMO; BRM SETCUR; LDA MONTH; WIO FNO
       LDA L6ADAY; BRM SETCUR; LDA TODAY; WIO FNO
       LDA L6BMSG; BRM SETCUR; MSG CM8 (ENTER LOC 6B SYS OR PROG)
       BRM CLRB; BRM GTSTG; BRM GCSM6B; BRM W6
       LDA L6BCSM; BRM SETCUR; LDA CKSM6B; WIO FNO; LDA L6BMO; BRM SETCUR
       LDA MONTH; WIO FNO; LDA L6BDAY; BRM SETCUR; LDA TODAY; WIO FNO
       CLA; SCP FNO; HLT; BRU MORE
W6     ZRO; LDA =BUF; STA PTR; LDX =-25; LDA* PTR; MIN PTR
       WIO FNO; BRX *-3; BRR W6

BADDSC LDX =-1; BRS 11; BRU IN1

SLASH  DATA 17B
WBIT   DATA 4B7
RONO   ZRO; CLA; LDB =8; LDX CIN; BRS 38; BRR RONO
CLBUF  ZRO; LDA =155B; CIT CIN; BRR CLBUF; BRU *-2
BUFPT  DATA 3*BUF-1,3*BUF-1
HBUFPT DATA 3*BUF-1,3*BUF-1

GTSTG  ZRO; LDP HBUFPT; STP BUFPT
       BRM CLBUF
GTSTG1 CIO CIN
       SNE =155B; BRU GTSTG2
       SNE SLASH; BRU GTSTG1
       SNE =152B; LDA =4
       WCI BUFPT; BRU GTSTG1
GTSTG2 LDA SLASH; WCI BUFPT; BRR GTSTG




SETCUR ZRO; MRG WBIT; SCP FNO; HLT; BRR SETCUR


MORE   MSG CM4 (ANOTHER LOC.)
       BRM CLBUF; CIO CIN; SKE ='   Y'; BRU *+2; BRU IN1
       SKE ='   N'; BRU MORE
MORE1  SKN RECSW; BRU RESET
       MSG CM5 (WHICH LOC. CONTAINS RUNNING SYS.)
       BRM CLBUF; BRM RONO; SNE =2; BRU MORE2; SNE =4; BRU MORE2
       SNE =12B; BRU MORE2; LDX =-1; BRS 11; BRU MORE1
MORE2  STA RSYS; LDA RECSYS; BRM SETCUR; LDA RSYS; WIO FNO
       LDA FNO; BRS 20; MSG MU1; BRU RESET
RECSW  ZRO
RSYS   ZRO

CM1     ASC '$WHICH LOC. DO YOU WISH TO CHANGE? /'
CM3     ASC '$ENTER SYS. OR PROG. /'
CM4     ASC '$ANOTHER LOC.? /'
CM5     ASC '$WHICH LOC. HAS THE RUNNING SYS.? /'
CHCNT  ZRO
CLRB   ZRO; LDX =-300B; CLA; STA BUF+300B,2; BRX *-1; BRR CLRB
IN1A   CBA; SNE ='?'; BRU MORE
IN1B   MSG CM6 (WHICH LOC. DO YOU WISH TO CLEAR)
       BRM CLBUF; BRM RONO; SKG SLASH; BRU *+2; BRU IN1C
       SKG =0; BRU IN1D; CAX; LDA OKLOC,2; SNE =-1; BRU IN1C
       SNE =6; BRU IN16; LDA MTAB,2; BRM SETCUR; LDA =-1; WIO FNO
       BRU MORE
IN16   LDA L6AMSG; BRM SETCUR; LDA =-1; WIO FNO; LDA L6BMSG; BRM SETCUR
       LDA =-1; WIO FNO; BRU MORE
IN1C   LDX =-1; BRS 11; BRU IN1B
IN1D   CBA; SKE ='?'; BRU IN1C; BRU MORE

CM6    ASC '$WHICH LOC. DO YOU WISH TO CLEAR /'
CM7    ASC '$ENTER LOC. 6A SYS. OR PROG. /'
CM8    ASC '$ENTER LOC. 6B SYS. OR PROG. /'
OKLOC  DATA -1,-1,2,-1,4,-1,6,7,10B,11B,12B,-1,14B,15B,16B,17B
       DATA -1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1
GTDATE ZRO; BRS 42; XAB; ETR =@74B6; STA TLV1; CLA; DIV =1800; ADD TLV1
       STA TLV1; LRSH 16; ADD =1; STA MONTH; LDA TLV1
       ETR =177777B; LRSH 23; DIV =1440; ADD =1; STA TODAY; BRR GTDATE
MONTH  ZRO
TODAY  ZRO

MVSYS  BRM VER; SKN INSTSW; MERR VERR1
       MSG MV1; BRM CLBUF; BRM RONO; BRM VERLOC; STA FROM
       BRM CALCAD; STA FADR; LDA FROM; ADD =1; STA FROM1
       BRM CALCAD; STA FADR1; MSG MV2; BRM CLBUF; BRM RONO
       BRM VERLOC; STA MVTO; BRM CALCAD; STA TOADR; LDA MVTO; ADD =1
       STA MVTO1; BRM CALCAD; STA TOADR1; LDA FADR; STA DISCR
       LDA TOADR; STA DISCW; LDA =7; STA CNT
S1     BRM CPG; BRM RPG; BRM WPG; SKR CNT; BRU S1
       LDA FADR1; STA DISCR; LDA TOADR1; STA DISCW; LDA =7; STA CNT
S2     BRM CPG; BRM RPG; BRM WPG; SKR CNT; BRU S2
       LDA FROM; STA HLOC; BRM GTCSM; LDA CKSM1; STA FCKSM1
       LDA CKSM2; STA FCKSM2; LDA MVTO; STA HLOC; BRM GTCSM
       LDA CKSM1; SKE FCKSM1; MERR VERR2; LDA CKSM2; SKE FCKSM2
       MERR VERR2; MSG M3; LDA HLOC; BRM PONO; LDA =0; CIO TOUT
       LDA CKSM1; BRM PONO; MSG M3; LDA HLOC; ADD =1; BRM PONO
       LDA =0; CIO TOUT; LDA CKSM2; BRM PONO; BRU MVMUPD
VERLOC ZRO; SKE =2; BRU *+2; BRR VERLOC; SKE =4; BRU *+2; BRR VERLOC
       SKE =12U *+2; BRR VERLOC; MSG EM8; LDA VERLOC; SUB =4
       STA VERLOC; BRR VERLOC


MV1    ASC '$MOVE SYSTEM FROM LOC: /'
MV2    ASC '$MOVE SYSTEM TO LOC: /'
VERR1  ASC '$INSTALLER LICENSE REQUIRED$/'
VERR2  ASC '$CHECKSUM MISMATCH, MOVE BAD$/'
FROM   ZRO
FROM1  ZRO
FADR   ZRO
FADR1  ZRO
MVTO   ZRO
MVTO1  ZRO
FCKSM1 ZRO
FCKSM2 ZRO

*MAP UPDATE FOR MVSYS

MVMUPD BRM OMAP; BRM SDATE; LDX FROM; LDA MTAB,2; BRM SETCUR
       RCP FNO; ETR =@4B7; SCP FNO; HLT 0 (SET TO READ MODE)
       LDA =BUF; STA PTR; LDX =-50
MVUP1  WIO FNO; STA* PTR; MIN PTR; BRX MVUP1
       LDX MVTO; LDA MTAB,2; BRM SETCUR; LDA =BUF; STA PTR; LDX =-50
MVUP2  LDA* PTR; MIN PTR; WIO FNO; BRX MVUP2
       LDX MVTO; LDA CSMTAB,2; BRM SETCUR; LDA CKSM1; WIO FNO
       LDA CKSM2; WIO FNO; LDX MVTO; LDA MOTAB,2; BRM SETCUR
       LDA MONTH; WIO FNO; LDX MVTO; LDA DAYTAB,2; BRM SETCUR
       LDA TODAY; WIO FNO; SKR RECSW; BRU *-1; BRU MORE1
MAPEND LDA FNO; BRS 20; MSG MU1; BRU RESET
MU1    ASC '$1DISCMAP UPDATED$/'
MU2    ASC '$ENTER SYSTEM OR PROG. FOR 1DISCMAP: /'

*ROUTINE TO CHECK FILE NAME FOR SYSTEM OR PROGRAM ENTRY FOR 1DISCMAP
*IF NOT FORMATTED CORRECTLY GET NAME FROM USER

SFNAM  ZRO; BRM CLRB; LDP NAMPTR; STP SCANPT
       GCI SCANPT; HLT 0 (MUST BE AT LEAST ONE CHAR THERE)
       SNE ='/'; BRU SFNAM4 (FILE STARTS WITH SLASH NOT RIGHT FORMAT)
SFNAM1 GCI SCANPT; BRU SFNAM4 (NO SLASH FOUND NOT RIGHT FORMAT)
       SNE ='/'; BRU SFNAM2 (FOUND FIRST SLASH)
       SNE ='-'; BRU SFNAM4 (NO SLASH FOUND BEFORE COMMENT)
       BRU SFNAM1
SFNAM2 LDP HBUFPT; STP WORKPT
SFNAM3 GCI SCANPT; BRU SFNAM4 (END OF FILE NAME WITHOUT FINDING END SLASH)
       SKE ='/'; BRU *+3; WCI WORKPT; BRR SFNAM (FOUND END)
       WCI WORKPT; BRU SFNAM3
SFNAM4 BRM CLRB; MSG MU2 (ENTER SYS OR PROG NAME)
       BRM GTSTG; LDP BUFPT; STP WORKPTT; BRR SFNAM
SCANPT BSS 2
WORKPT BSS 2

*MAP UPDATE FOR DIM

DMUPD  BRM OMAP; BRM SDATE; LDA L6AMSG; BRM SETCUR; RCP FNO
       ETR =@4B7; SCP FNO; HLT 0 (SET TO READ MODE)
       LDX =-25; WIO FNO; STA BUF+25,2; BRX *-2; LDA L6BMSG; BRM SETCUR
       LDX =-25; LDA BUF+25,2; WIO FNO; BRX *-2
       LDA L6BCSM; BRM SETCUR; LDA CKSM6B; WIO FNO; LDA L6BMO; BRM SETCUR
       LDA MONTH; WIO FNO; LDA L6BDAY; BRM SETCUR; LDA TODAY; WIO FNO
       BRM SFNAM; LDA L6AMSG; BRM SETCUR; LDX =-25; LDA BUF+25,2
       WIO FNO; BRX *-2; LDA L6ACSM; BRM SETCUR; LDA CKSM6A; WIO FNO
       LDA L6AMO; BRM SETCUR; LDA MONTH; WIO FNO; LDA L6ADAY; BRM SETCUR
       LDA TODAY; WIO FNO; BRU MAPEND

*MAP UPDATE ROUTINE FOR TYMNET AND UTIL

MAPUPD BRM OMAP; BRM SDATE; BRM SFNAM; LDX HLOC; LDA MTAB,2
       BRM SETCUR; LDX =-50; LDA BUF+50,2; WIO FNO; BRX *-2
       LDX HLOC; LDA CSMTAB,2; BRM SETCUR; LDA CKSM1; WIO FNO; LDX HLOC
       LDA MOTAB,2; BRM SETCUR; LDA MONTH; WIO FNO; LDX HLOC
       LDA DAYTAB,2; BRM SETCUR; LDA TODAY; WIO FNO; BRU MAPEND

*MAP UPDATE ROUTINE FOR SYSTEM DISCS

SMUPD  BRM OMAP; BRM SDATE; BRM SFNAM; LDX TO; LDA MTAB,2; BRM SETCUR
       LDX =-50; LDA BUF+50,2; WIO FNO; BRX *-2; LDX TO; LDA CSMTAB,2
       BRM SETCUR; LDA CKSM1; WIO FNO; LDA CKSM2; WIO FNO
       LDX TO; LDA MOTAB,2; BRM SETCUR; LDA MONTH; WIO FNO; LDX TO
       LDA DAYTAB,2; BRM SETCUR; LDA TODAY; WIO FNO
       SKR RECSW; BRU *-1; BRU MORE1


*
* WHEN AN OPERATING SYSTEM IS INSTALLED ON A 940, THE EXISTING SYSTEM
* IS MOVED FROM DISC LOCATION 2 TO LOCATION 12.  IN ADDITION TO MOVING
* THE MONITOR AND THE EXEC, WHICH IS DONE BY THE MVSYS PROGRAM, IT IS
* NECESSARY TO RENAME THE SYMBOL TABLE FILES IN *6HELP AND THE THE
* RAD SUBSYSTEM FILES.  THE LATTER EXIST IN USER NAME *6UTIL FOR THE
* SYSTEM ON DISC 2 AND IN USER NAME *6WSD2 FOR THE SYSTEM ON DISC 12,
* WHERE THEY ARE REFERENCED BY THE 1WSD AND 2WSD PROGRAMS RESPECTIVELY.
*
*
* THESE ROUTINES ARE  USED TO VERIFY, CHECKSUM, OR COPY THE RAD
* SUBSYSTEM FILES IN *6UTIL OR *6WSD2.  THESE ARE REFERED
* TO SUBSEQUENTLY AS RAD FILES FOR LOC 2 AND LOC 12 RESPECTIVELY.
*
* THE VERIFY FUNCTION READS THE RAD FILES AND VERIFIES THEIR CHECKSUMS
*      AGAINST THE CHECKSUM FILES KEPT IN *6UTIL OR *6WSD2.  DISCREPANCIES
*      WILL BE REPORTED, INDICATING A BAD CHECKSUM FILE OR A BAD RAD FILE.
*
* THE CHECK FUNCTION READS THE RAD FILES IN PLACE AND CREATES NEW
*      CHECKSUM FILES.  THIS FUNCTION CAN BE USED AFTER INSTALLING
*      A NEW RAD FILE OR TO REPLACE A MISSING OR BAD CHECKSUM FILE.
*
* THE COPY FUNCTION COPIES ALL OF THE RAD FILES FROM ONE LOCATION TO
*      THE OTHER, E.G. FROM LOC 2 TO LOC 12 (ACTUAL COPYING IN THIS
*      CASE WOULD BE FROM (*6UTIL) TO (*6WSD2).  THIS FUNCTION WOULD
*      BE USED BEFORE INSTALLING NEW RAD FILES TO MOVE THE EXISTING
*      RAD FILES TO THE ALTERNATE USER NAME.  IT MAY JUST AS EASILY
*      BE USED TO MOVE THE ALTERNATE RAD FILES TO THE PRIME DIRECTORY,
*      IN CASE AN OPERATING SYSTEM IS BEING DEINSTALLED.
*
*
* LICENSE REQUIRED:  THE USER MUST HAVE ACCESS TO THE FILES IN *6UTIL
* AND *6WSD2.  FOR THIS PURPOSE SYSTEM LICENSE WILL SUFFICE.  IN ADDITION,
* THE USER MUST ENTER HIS NAME AND OPERATOR PASSWORD.  HE MUST HAVE
* INSTALLER LICENSE TO PROCEED, UNLESS HE ALSO HAS EXEC LICENSE. NO LICENSE
*IS REQUIRED FOR THE VERIFY FUNCTION.
*

PFILE ZRO; LDX TOUT; BRS 35; BRR PFILE

MRONO   ZRO
       LDX CIN; LDB =8; BRS 38; XAB; SKE =155B; MERR NOCR; XAB
       SKE =12B; SNE =2; BRU *+2; MERR BADLOC; LDX =MRFN1; SKE =2
       LDX =MRFN2; SPTR 0,2; ADD =8; XAB; BRR MRONO
MRFN1    ASC '(*6UTIL)'
MRFN2    ASC '(*6WSD2)'

MKNAM  ZRO;* MAKE FILENAME.  USER NAME PTRS IN A AND B.
*      ADDRESS OF BUFFER TO HOLD NAME IN X.  RETURNS
*      POINTERS TO NAME IN A AND B AND X DESTROYED.
       STP WK1; SPTR 0,2; STP WK2
       GCI WK1; BRU *+3; WCI WK2; BRU *-3
       LDX FLX; SPTR 0,2; ADD =18; XAB; STP WK1
       GCI WK1; BRU *+5; SNE =0; BRU *+3; WCI WK2; BRU *-5
       LDP WK2; BRR MKNAM
COPY1 BSS 1
CPT    BSS 2
MRCBUF BSS 1
COUNT BSS 1
CKSUM BSS 1
FLX BSS 1      (ADDRESS OF FILENAME IN NTAB BUFFER)
MVERSW  BSS 1 (0 TO CHECKSUM FILES, -1 TO VERIFY THEM)
INFIL BSS 1
OUTFIL BSS 1
WK1 BSS 2
WK2 BSS 2
CFILE BSS 2 (STG PTRS FOR COPY FROM USERNAME PREFIX)
CFBUF BSS 9 (COPY FROM FILENAME)
CFPTR BSS 2 (COPY FROM FILENAME PTRS)
TFILE BSS 2 (STG PTRS FOR COPY TO USERNAME PREFIX)
TFBUF BSS 9 (COPY TO FILENAME)
TFPTR BSS 2 (COPY TO FILENAME PTRS)
* NAMES MAY NOT HAVE IMBEDDED BLANKS
NTAB   ASC 'EXECD1               '
       ASC 'EXECD2               '
       ASC 'DDT                  '
       ASC 'XDDT                 '
       ASC 'EDITOR               '
       ASC 'COMMON               '
       ASC 'SFOR1                '
       ASC 'SFOR2                '
       ASC 'SFOR3                '
       ASC 'BFOR1                '
       ASC 'BFOR2                '
       ASC 'BFOR3                '
       ASC 'CAL                  '
       ASC 'FOS                  '
       ASC 'F2OS                 '
       ASC 'SBASIC               '
       ASC 'FTC                  '
       ASC 'F2C                  '
       ASC 'NBASIC               '
       ASC 'NBASIC1              '
       ASC 'XFOR1                '
       ASC 'XFOR2                '
       ASC 'XFOR3                '
NTABE  EQU *
* TABLE FOR MDSI
MDTAB   ASC 'EXECD1               '
       ASC 'EXECD2               '
       ASC '                     '
       ASC 'XDDT                 '
       ASC 'EDITOR               '
       ASC 'COMMON               '
       ASC '                     '
       ASC '                     '
       ASC '                     '
       ASC 'BFOR1                '
       ASC 'BFOR2                '
       ASC 'BFOR3                '
       ASC '                     '
       ASC '                     '
       ASC '                     '
       ASC 'SBASIC               '
       ASC '                     '
       ASC '                     '
       ASC '                     '
       ASC '                     '
       ASC 'XFOR1                '
       ASC 'XFOR2                '
       ASC 'XFOR3                '
MDTABE  EQU *

*COMMANDS

VERROU LDA =-1; BRU CHER1A
CHEROU BRM VER; SKN INSTSW; MERR VERR1; CLA
CHER1A STA MVERSW
       MSG LOC; BRM MRONO; STP TFILE; BRM BSUM; LDA =NTAB
CHER1  STA FLX; COPY AX,A; SKE 0,2; BRU *+2; BRU CHER5
       STA CKSUM
       BRM TMK; BRS 64; BRU CHER; STA INFIL
CHER2  LDA =4000B; LDX =3B4; BIO INFIL; NOP; LDB INFIL; SKB =14B5; BRU CHRER
       SKB =2B5; BRU CHER3 (EOF)
       LDA =2047; BRM CK; BRU CHER2
CHER3  SUB =3B4; SKG =0; BRU CHER4; SUB =1; BRM CK
CHER4  LDA =155B; CIO TOUT; LDP TFPTR; BRM PFILE
       LDA CKSUM; LDX FLX; SKN MVERSW; BRU CHER6 (CHECK)
       SKE 6,2; BRU BADCK; MSG OK
CHER5  BRS 147; LDA FLX; ADD =7; SKG =NTABE-1; BRU CHER1
       SKN MVERSW; BRU COPR6 (WRITE OUT SUMSUB FILE)
       BRU RESET
CHER6  STA 6,2; BRU CHER5
BADCK  MSG EX (EXPECTED CHECKSUM); LDX FLX; LDA 6,2; BRM PONO
       MSG GOT (GOT); LDA CKSUM; BRM PONO; BRU CHER5
CHER   MSG CFERM
       LDP TFPTR; BRM PFILE; BRU CHER5
CHRER  MSG RDERM; BRU CHER+1

COPROU BRM VER; SKN INSTSW; MERR VERR1
       MSG CF (COPY FROM); BRM CLBUF; BRM MRONO; STP CFILE
       MSG CT (COPY TO); BRM MRONO; SNE CFILE; MERR SAMLOC (SAME LOC); STP TFILE
       LDA =NTAB
COPR1  STA FLX; COPY AX,A; SKE 0,2; BRU *+2; BRU COPR5
       STA CKSUM
       LDP CFILE; LDX =CFBUF; BRM MKNAM; STP CFPTR
       BRS 64; BRU CFER; STA INFIL
       BRM TMK
       LDX TOUT; BRS 65; BRU TFER; STA OUTFIL
       RSP INFIL; STA COPY1
       LDA =2B7-1; SSP OUTFIL; BRU *-1
COPR2  LDA =4000B; LDX =3B4; BIO INFIL; NOP
       LDB INFIL; SKB =14B5; BRU RDER
       SKB =2B5; BRU COPR3 (EOF)
       LDA =4000B; LDX =3B4; BIO OUTFIL; BRU WRTER
       LDA =2047; BRM CK
       BRU COPR2
COPR3  SUB =3B4; STA COUNT; LDX =3B4; SNE =0; BRU *+2; BIO OUTFIL; BRU WRTER
       RSP OUTFIL; XMA COPY1; MRG =4B7; SCP OUTFIL; BRU WRTER
       LDA COPY1; PCE OUTFIL; BRU PCER
       LDA COUNT; SNE =0; BRU COPR4; SUB =1; BRM CK
COPR4  LDA CKSUM; LDX FLX; STA 6,2
       LDA =155B; CIO TOUT
       LDP CFPTR; BRM PFILE; MSG COPT; LDP TFPTR; BRM PFILE
COPR5  BRS 147; LDA FLX; ADD =7; SKG =NTABE-1; BRU COPR1
*      CREATE THE SUMSUB FILE
COPR6  LDA =SUBS; BRM WF
       BRU RESET
* COPY ERRORS
RDER   MSG RDERM
RDER1  LDP CFPTR; BRM PFILE; BRU COPR5

CFER   MSG CFERM; BRU RDER1
WRTER  MSG WRTERM
WRTER1 LDP TFPTR; BRM PFILE; BRU COPR5

PCER   MSG PCERM; BRU WRTER1

TFER   MSG TFERM; BRU WRTER1
LOC    ASC 'LOC /'
GOT    ASC ' GOT /'
EX     ASC ' EXPECTED CHECKSUM /'
OK     ASC ' OK/'
CF     ASC 'COPY FROM LOC /'
RDERM  ASC '$ERROR READING /'
CFERM  ASC '$CANNOT LOCATE /'
WRTERM ASC '$ERROR WRITING /'
PCERM  ASC '$ERROR ERASING /'
TFERM ASC '$CANNOT OPEN COPY TO FILE: /'
NOFM ASC '$CANNOT OPEN CHECKSUM FILE: /'
WFM1 ASC '$CHECKSUM FILE /'
WFM2 ASC ' WRITTEN./'
COPT ASC ' COPIED TO /'
CT     ASC 'COPY TO LOC /'
NOCR   ASC '$NO CR AFTER LOC NO.$/'
SAMLOC ASC '$CANNOT COPY TO SAME LOC.$/'
BADLOC ASC '$VALID RESPONSE IS 2 OR 12$/'

* WRITE NAMES AND CHECKSUMS TO SUMSUB

SUBS ASC 'SUMSUB '

WF ZRO
       STA FLX; BRM TMK
       LDX =2; BRS 65; BRU NOF; STA OUTFIL; LDA =NTABE-NTAB
       LDX =NTAB; BIO OUTFIL; BRU NOF1; LDA OUTFIL; BRS 20
       MSG WFM1
       LDP TFPTR; BRM PFILE; MSG WFM2
       BRR WF
NOF1   MSG WRTERM
NOF2   LDP TFPTR; BRM PFILE; BRR WF
NOF    MSG NOFM; BRU NOF2
MDSI ZRO
MDSI1 ZRO
       LDA =MDSI; LDX =31; BRS 7
       LDA MDSI; ETR =77B; SKE =1; BRR MDSI1
       BRM MNMOVE
       BRR MDSI1
MNMOVE ZRO; LDX =NTAB-NTABE; LDA MDTABE,2; STA NTABE,2; BRX *-2; BRR MNMOVE
TMK ZRO; LDP TFILE; LDX =TFBUF; BRM MKNAM; STP TFPTR; BRR TMK

CK2 ZRO
CK ZRO; STA COUNT; LDX =3B4; CLB; LDA CKSUM
       LCY 7; ADD 0,2; STB CKSUM; ADD CKSUM; CLB; EAX 1,2
       SKR COUNT; BRU *-7; STA CKSUM
       BRR CK
BSUM ZRO;* BRING CHECKSUM FILE TO NTAB FOR VERIFY COMMAND
       SKN MVERSW; BRR BSUM
BSUM2  CLA; STA CKSUM; STA CK2
       LDA =SUBS; STA FLX; BRM TMK; BRS 64; BRU BSUM4
       STA INFIL; STA CK2; LDA =NTABE-NTAB; LDX =3B4; BIO INFIL; BRU BSUM3
* CHECKSUM SUMSUB AND LEAVE AT 30000B
       LDA =NTABE-NTAB-1
BSUM4  LDA CK2; SKE =0; BRU BSUM9
       MERR NOSUM (COULD NOT FIND SUMSUB)
BSUM9  MSG FSUM (FOUND SUMSUB)
       LDA =3B4; LDB =NTAB; LDX =NTABE-NTAB-1; BRM MOVE
BSUM10 BRS 147; BRR BSUM
BSUM3  MSG RDERM; LDP TFPTR; BRM PFILE; BRU BSUM4
NOSUM  ASC '$SUMSUB NOT FOUND/'
FSUM   ASC '$FOUND SUMSUB/'

MOVE ZRO; STA MOVE1; STB MOVE2; STX MOVE3
       LDA* MOVE1; STA* MOVE2; MIN MOVE1; MIN MOVE2; SKR MOVE3; BRU *-5
       BRR MOVE
MOVE1 ZRO
MOVE2 ZRO
MOVE3 ZRO

CHECK  BRM OMAP; MSG M7 (CHECKSUM LOC); BRM CLBUF; BRM RONO
       STA TO; STA HLOC; SNE =6; BRU CK6; CAX; LDA OKLOC,2; SNE =-1; MERR EM17
       LDX HLOC; BRM RMSG; LDA BUF; SNE =-1; BRU CK7
       BRM GTCSM; LDA =155B; CIO TOUT
       MSG BUF; MSG M3 (LOC.); LDA TO; BRM PONO
       LDA =0; CIO TOUT; LDA CKSM1; BRM PONO; SKN TO1; BRU *+2; BRU CHECK1
       MSG M6 ( LOC ); LDA TO; ADD =1; BRM PONO; LDA =0; CIO TOUT
       LDA CKSM2; BRM PONO
CHECK1 LDA =155B; CIO TOUT; BRU RESET

RMSG   ZRO; LDA MTAB,2; BRM SETCUR; RCP FNO; ETR =@4B7; SCP FNO; HLT
       LDX =-50; WIO FNO; STA BUF+50,2; BRX *-2; BRR RMSG


CK7    MSG EM18; BRU RESET
CK6    CAX; BRM RMSG; BRM GCSM6A; BRM GCSM6B; LDA =155B; CIO TOUT
       MSG M4; MSG BUF; LDA =155B; CIO TOUT
       LDA CKSM6A; BRM PONO; LDA =155B; CIO TOUT; MSG M5; MSG BUF+25
       LDA =155B; CIO TOUT; LDA CKSM6B; BRM PONO; BRU CHECK1
HEL    MSG HELMSG; BRU RESET

HELMSG ASC '$$VALID COMMANDS ARE:'
       ASC '$$SYSTEM                    DUMPS OPERATING SYSTEM FILES TO'
       ASC '$                          LOC. 2, 4, OR 12.'
       ASC '$                          REQUIRES INSTALLER LICENSE.'
       ASC '$$MOVE SYSTEM               MOVES OPERATING SYSTEMS BETWEN LOC.'
       ASC '$                          2, 4, AND 12.'
       ASC '$                          REQUIRES INSTALLER LICENSE.'
       ASC '$$DIM                       MOVES OLD VERSION OF DIM FROM LOC. 6A'
       ASC '$                          TO LOC. 6B AND DUMPS DIM FILE TO LOC.'
       ASC '$                          6A.'
       ASC '$                          REQUIRES INSTALLER LICENSE.'
       ASC '$$LOADER                    DUMPS DIM IPL TO PAGE ZERO OF THE DISC'
       ASC '$                          REQUIRES USER BE IN DISTRICT 0.'
       ASC '$$UTIL                      DUMPS A UTILITY PROGRAM TO LOC. 10,'
       ASC '$                          15, 16, OR 17'
       ASC '$                          REQUIRES USER BE IN DISTRICT 0.'
       ASC '$$TYMNET                    DUMPS BASE CODE FILES TO LOC. 11 OR 14'
       ASC '$                          REQUIRES TYMNET LICENSE.'
       ASC '$$CHANGE MAP                CHANGES ENTRIES BY SPECIFIED LOCATIONS'
       ASC '$                          IN 1DISCMAP.'
       ASC '$$CHECKSUM                  DISPLAYS SYSTEM OR PROGRAM NAME AT'
       ASC '$                          A SPECIFIED LOCATION AND CHECKSUMS'
       ASC '$                          THE CODE AT THAT LOCATION'
       ASC '$$VERIFY SUBSYSTEMS         VERIFIES SUBSYSTEMS AGAINST CONTROL'
       ASC '$                          FILES USED BY WSD.  LOC. 2 CHECKS *6UTIL'
       ASC '$                          AND LOC. 12 CHECKS *6WSD2'
       ASC '$$MAKE SUBSYSTEM CONTROL    MAKES A CONTROL FILE FOR WSD.'
       ASC '$                          LOC. 2 MAKES CONTROL FILE FOR 1WSD.'
       ASC '$                          LOC. 12 MAKES CONTROL FILE FOR 2WSD.'
       ASC '$                          REQUIRES INSTALLER LICENSE.'
       ASC '$$COPY SUBSYSTEMS           COPIES SUBSYSTEM FILES FROM *6UTIL TO'
       ASC '$                          TO *6WSD2 AND VICE VERSA.  LOC. 2 IS'
       ASC '$                          *6UTIL AND LOC. 12 IS *6WSD2'
       ASC '$                          REQUIRES INSTALLER LICENSE'
       ASC '$$FILES TO BE DUMPED MUST RESIDE IN THE USERS DIRECTORY, CROSS'
       ASC '$DIRECTORY FILE REFERENCES ARE NOT ALLOWED.  DUMPING TO THE'
       ASC '$DISC ALSO UPDATES 1DISCMAP'
       ASC '$$ALL OF THE ABOVE COMMANDS MAY SHORTENED TO THE FIRST THREE'
       ASC '$CHARACTERS$/'
       END
1INSTALL ** FICHE/FRAME BREAK *****
INSTALL5 SYMSORT

SYM.  PG.LN. IDENT.

1CKSM   8 32 INSTAL  2CKSM   9  1 INSTAL  ADDON   8 30 INSTAL  
BADCK  20 23 INSTAL  BADDSC 12 28 INSTAL  BADLOC 22  8 INSTAL  BFR     6 17 INSTAL  
BIOPGI  4 28 INSTAL  BSUM   22 38 INSTAL  BSUM10 23 10 INSTAL  BSUM2  23  1 INSTAL  
BSUM3  23 11 INSTAL  BSUM4  23  6 INSTAL  BSUM9  23  8 INSTAL  BTBLND  2  9 INSTAL  
BUF    11 33 INSTAL  BUF2    7 27 INSTAL  BUFPT  12 34 INSTAL  CALCAD  3 39 INSTAL  
CBUF    6 15 INSTAL  CF     21 34 INSTAL  CFBUF  18 25 INSTAL  CFER   21 23 INSTAL  
CFERM  21 36 INSTAL  CFILE  18 24 INSTAL  CFPTR  18 26 INSTAL  CHCNT  13 29 INSTAL  
CHECK  23 22 INSTAL  CHECK1 23 30 INSTAL  CHER   20 25 INSTAL  CHER1  20  9 INSTAL  
CHER1A 20  7 INSTAL  CHER2  20 12 INSTAL  CHER3  20 15 INSTAL  CHER4  20 16 INSTAL  
CHER5  20 19 INSTAL  CHER6  20 22 INSTAL  CHEROU 20  6 INSTAL  CHGMAP 11 22 INSTAL  
CHK     2  2 INSTAL  CHRER  20 27 INSTAL  CIN     5 36 INSTAL  CK     22 34 INSTAL  
CK2    22 33 INSTAL  CK6    23 37 INSTAL  CK7    23 36 INSTAL  CKS1    4 36 INSTAL  
CKS2    4 34 INSTAL  CKSM    8 31 INSTAL  CKSM1   9  3 INSTAL  CKSM2   9  4 INSTAL  
CKSM6A  9 22 INSTAL  CKSM6B  9 23 INSTAL  CKSTAT  4 30 INSTAL  CKSUM  18 17 INSTAL  
CLBUF  12 33 INSTAL  CLRB   13 30 INSTAL  CM1    13 25 INSTAL  CM3    13 26 INSTAL  
CM4    13 27 INSTAL  CM5    13 28 INSTAL  CM6    14  3 INSTAL  CM7    14  4 INSTAL  
CM8    14  5 INSTAL  CNT     6 10 INSTAL  COPR1  20 33 INSTAL  COPR2  21  2 INSTAL  
COPR3  21  8 INSTAL  COPR4  21 12 INSTAL  COPR5  21 15 INSTAL  COPR6  21 17 INSTAL  
COPROU 20 29 INSTAL  COPT   22  4 INSTAL  COPY1  18 13 INSTAL  COUNT  18 16 INSTAL  
CPG     4  8 INSTAL  CPT    18 14 INSTAL  CSMTAB 10  9 INSTAL  CT     22  5 INSTAL  
CTBL    2  6 INSTAL  CTBLND  2  7 INSTAL  DAY    10  7 INSTAL  DAYTAB 11  5 INSTAL  
DFILE   4 22 INSTAL  DFILE1  4 23 INSTAL  DFILE2  4 25 INSTAL  DFILE3  4 26 INSTAL  
DIM     2 28 INSTAL  DIM1    2 33 INSTAL  DIMNAM  5 32 INSTAL  DISCR   6 12 INSTAL  
DISCW   6 11 INSTAL  DIST    8 18 INSTAL  DMUPD  16  7 INSTAL  E1     11 30 INSTAL  
EBUF    6 16 INSTAL  EM1     5  8 INSTAL  EM10    5 15 INSTAL  EM11    5 16 INSTAL  
EM12    5 18 INSTAL  EM13    5 19 INSTAL  EM14    5 20 INSTAL  EM15    5 21 INSTAL  
EM16    5 22 INSTAL  EM17    5 23 INSTAL  EM18    5 24 INSTAL  EM2     5  9 INSTAL  
EM4     5 10 INSTAL  EM7     5 11 INSTAL  EM8     5 12 INSTAL  EM9     5 13 INSTAL  
EOJ     5 25 INSTAL  EPTR    9  6 INSTAL  EQNO    5 39 INSTAL  EQUIP   3 36 INSTAL  
ERFLG   2 12 INSTAL  ERMSG   2 11 INSTAL  ERMSG1  2 10 INSTAL  ERR     2  4 INSTAL  
ERR1A   2  5 INSTAL  ERV     7 38 INSTAL  ERV3    8  1 INSTAL  EX     21 32 INSTAL  
FADR   15  2 INSTAL  FADR1  15  3 INSTAL  FCKSM1 15  6 INSTAL  FCKSM2 15  7 INSTAL  
FIL    11 27 INSTAL  FILIPL  3 30 INSTAL  FILPTR 11 26 INSTAL  FLX    18 18 INSTAL  
FN      5 29 INSTAL  FN1     5 30 INSTAL  FNERR   5 31 INSTAL  FNI     5 38 INSTAL  
FNO    11 32 INSTAL  FONO    7 12 INSTAL  FROM   14 39 INSTAL  FROM1  15  1 INSTAL  
FSUM   23 13 INSTAL  GCSM6A  9 12 INSTAL  GCSM6B  9 16 INSTAL  GLTCH   1 36 INSTAL  
GLTCH1  1 37 INSTAL  GOT    21 31 INSTAL  GPS     7  1 INSTAL  GPS1    7  2 INSTAL  
GTCSM   8 22 INSTAL  GTCSM1  8 24 INSTAL  GTCSM2  8 26 INSTAL  GTDATE 14  8 INSTAL  
GTDISC  4 17 INSTAL  GTFIL   2 13 INSTAL  GTFIL1  2 15 INSTAL  GTFIL2  2 16 INSTAL  
GTSTG  12 37 INSTAL  GTSTG1 12 39 INSTAL  GTSTG2 13  5 INSTAL  HBUFPT 12 35 INSTAL  
HCKSM   9  7 INSTAL  HEL    24  2 INSTAL  HELMSG 24  4 INSTAL  HLOC    9  8 INSTAL  
HNPTR   5 28 INSTAL  IN     11 36 INSTAL  IN1    12  2 INSTAL  IN16   13 37 INSTAL  
IN1A   13 31 INSTAL  IN1B   13 32 INSTAL  IN1C   13 39 INSTAL  IN1D   14  1 INSTAL  
IN26   12 16 INSTAL  IN2A   12 13 INSTAL  IN2B   12 10 INSTAL  INFIL  18 20 INSTAL  
INSTSW  8 14 INSTAL  IPLFPT  3 31 INSTAL  L1      3 23 INSTAL  L10CSM 10 16 INSTAL  
L10DAY 11 12 INSTAL  L10MO  10 34 INSTAL  L10MSG  9 35 INSTAL  L11CSM 10 17 INSTAL  
L11DAY 11 13 INSTAL  L11MO  10 35 INSTAL  L11MSG  9 36 INSTAL  L12CSM 10 18 INSTAL  
L12DAY 11 14 INSTAL  L12MO  10 36 INSTAL  L12MSG  9 37 INSTAL  L13CSM 10 19 INSTAL  
L13DAY 11 15 INSTAL  L13MO  10 37 INSTAL  L13MSG  9 38 INSTAL  L14CSM 10 20 INSTAL  
L14DAY 11 16 INSTAL  L14MO  10 38 INSTAL  L14MSG  9 39 INSTAL  L15CSM 10 21 INSTAL  
L15DAY 11 17 INSTAL  L15MO  10 39 INSTAL  L15MSG 10  1 INSTAL  L16CSM 10 22 INSTAL  
L16DAY 11 18 INSTAL  L16MO  11  1 INSTAL  L16MSG 10  2 INSTAL  L17CSM 10 23 INSTAL  
L17DAY 11 19 INSTAL  L17MO  11  2 INSTAL  L17MSG 10  3 INSTAL  L2      3 25 INSTAL  
L2CSM  10 10 INSTAL  L2DAY  11  6 INSTAL  L2MO   10 28 INSTAL  L2MSG   9 29 INSTAL  
L3CSM  10 11 INSTAL  L3DAY  11  7 INSTAL  L3MO   10 29 INSTAL  L3MSG   9 30 INSTAL  
L4CSM  10 12 INSTAL  L4DAY  11  8 INSTAL  L4MO   10 30 INSTAL  L4MSG   9 31 INSTAL  
L5CSM  10 13 INSTAL  L5DAY  11  9 INSTAL  L5MO   10 31 INSTAL  L5MSG   9 32 INSTAL  
L6ACSM 10 14 INSTAL  L6ADAY 11 10 INSTAL  L6AMO  10 32 INSTAL  L6AMSG  9 33 INSTAL  
L6BCSM 10 24 INSTAL  L6BDAY 11 20 INSTAL  L6BMO  11  3 INSTAL  L6BMSG 10  4 INSTAL  
L7CSM  10 15 INSTAL  L7DAY  11 11 INSTAL  L7MO   10 33 INSTAL  L7MSG   9 34 INSTAL  
LOADER  3 16 INSTAL  LOADSW  6  3 INSTAL  LOANAM  5 34 INSTAL  LOC    21 30 INSTAL  
M1      5  1 INSTAL  M2      5  2 INSTAL  M3      5  3 INSTAL  M4      5  4 INSTAL  
M5      5  5 INSTAL  M6      5  6 INSTAL  M7      5  7 INSTAL  MAPEND 15 21 INSTAL  
MAPUPD 16 20 INSTAL  MDSI   22 24 INSTAL  MDSI1  22 25 INSTAL  MDTAB  19 17 INSTAL  
MDTABE 20  1 INSTAL  MERR    1 18 INSTAL  MKNAM  18  5 INSTAL  MKSUM   8 35 INSTAL  
MNMOVE 22 30 INSTAL  MO     10  6 INSTAL  MONTH  14 11 INSTAL  MORE   13 13 INSTAL  
MORE1  13 16 INSTAL  MORE2  13 20 INSTAL  MOTAB  10 27 INSTAL  MOVE   23 15 INSTAL  
MOVE1  23 18 INSTAL  MOVE2  23 19 INSTAL  MOVE3  23 20 INSTAL  MRCBUF 18 15 INSTAL  
MRFN1  18  2 INSTAL  MRFN2  18  3 INSTAL  MRONO  17 37 INSTAL  MSG     1  9 INSTAL  
MSG1    1 12 INSTAL  MSGP    1 14 INSTAL  MTAB    9 28 INSTAL  MU1    15 22 INSTAL  
MU2    15 23 INSTAL  MV1    14 35 INSTAL  MV2    14 36 INSTAL  MVERSW 18 19 INSTAL  
MVMUPD 15 11 INSTAL  MVSYS  14 14 INSTAL  MVTO   15  4 INSTAL  MVTO1  15  5 INSTAL  
MVUP1  15 14 INSTAL  MVUP2  15 16 INSTAL  NAMPTR  5 27 INSTAL  NOCR   22  6 INSTAL  
NOF    22 23 INSTAL  NOF1   22 21 INSTAL  NOF2   22 22 INSTAL  NOFM   22  1 INSTAL  
NONAME  7 17 INSTAL  NOPASS  7 15 INSTAL  NOPER   1 30 INSTAL  NOSUM  23 12 INSTAL  
NOSYS  11 34 INSTAL  NOUTIL  4 19 INSTAL  NTAB   18 31 INSTAL  NTABE  19 15 INSTAL  
OK     21 33 INSTAL  OKLOC  14  6 INSTAL  OMAP   11 23 INSTAL  OPFER   7 39 INSTAL  
OPNAM   7 25 INSTAL  OUTFIL 18 21 INSTAL  P       6 13 INSTAL  PAGES   6  1 INSTAL  
PCER   21 27 INSTAL  PCERM  21 38 INSTAL  PDNO    9  9 INSTAL  PFILE  17 35 INSTAL  
PONO    9 10 INSTAL  PRIV   11 28 INSTAL  PTR     9  5 INSTAL  QUIT    8 20 INSTAL  
RDER   21 20 INSTAL  RDER1  21 21 INSTAL  RDERM  21 35 INSTAL  RECSW  13 22 INSTAL  
RECSYS 10 26 INSTAL  RESET   1 33 INSTAL  RMSG   23 32 INSTAL  RONO   12 32 INSTAL  
RPG     4  5 INSTAL  RSYS   13 23 INSTAL  S1     14 21 INSTAL  S2     14 23 INSTAL  
SA      1 26 INSTAL  SAMLOC 22  7 INSTAL  SCANPT 16  2 INSTAL  SDATE  11 37 INSTAL  
SECSW   6  2 INSTAL  SETCUR 13 10 INSTAL  SETINS  8  5 INSTAL  SETNET  8  6 INSTAL  
SF      7 23 INSTAL  SFBUF   7 28 INSTAL  SFIL    7 37 INSTAL  SFNAM  15 28 INSTAL  
SFNAM1 15 31 INSTAL  SFNAM2 15 35 INSTAL  SFNAM3 15 36 INSTAL  SFNAM4 15 39 INSTAL  
SFPTR   7 22 INSTAL  SGPS    6 38 INSTAL  SIXSW   9 21 INSTAL  SLASH  12 30 INSTAL  
SMUPD  16 28 INSTAL  SPTR    1  8 INSTAL  STABUF  6 14 INSTAL  START   1 29 INSTAL  
START1  1 31 INSTAL  STASW   8 16 INSTAL  STCKSM  8 38 INSTAL  SUBS   22 12 INSTAL  
SX      1 27 INSTAL  SYS1    3  5 INSTAL  SYSNAM  5 33 INSTAL  SYSNUM  9 25 INSTAL  
SYSTEM  2 39 INSTAL  TDNAME  2 37 INSTAL  TERMWD  7 20 INSTAL  TFBUF  18 28 INSTAL  
TFER   21 29 INSTAL  TFERM  21 39 INSTAL  TFILE  18 27 INSTAL  TFPTR  18 29 INSTAL  
TLNAME  3 33 INSTAL  TLV1    6  8 INSTAL  TLV11   6  9 INSTAL  TMK    22 31 INSTAL  
TMSG    1 23 INSTAL  TNET    2 25 INSTAL  TNETSW  8 15 INSTAL  TO      6  4 INSTAL  
TO1     6  5 INSTAL  TOADR   6  6 INSTAL  TOADR1  6  7 INSTAL  TODAY  14 12 INSTAL  
TOUT    5 37 INSTAL  TSNAME  3 14 INSTAL  UTIL    2 17 INSTAL  UTIL1   2 20 INSTAL  
VER     6 18 INSTAL  VER1    6 27 INSTAL  VER1A   6 21 INSTAL  VER1B   6 23 INSTAL  
VER1C   6 22 INSTAL  VERLOC 14 30 INSTAL  VERR1  14 37 INSTAL  VERR2  14 38 INSTAL  
VERRET  8  2 INSTAL  VERROU 20  5 INSTAL  VERS    1  5 INSTAL  VERSW   8 17 INSTAL  
VM1     8 10 INSTAL  VM2     8 11 INSTAL  VME     8  7 INSTAL  VME3    8  8 INSTAL  
VME4    8  9 INSTAL  VME5    8 12 INSTAL  VME6    8 13 INSTAL  VPTR1   7 24 INSTAL  
VPTR2   7 26 INSTAL  VT1     7 29 INSTAL  VT2     7 30 INSTAL  VXT1    7 31 INSTAL  
VXT2    7 32 INSTAL  VXT3    7 33 INSTAL  VXT4    7 34 INSTAL  VXT5    7 35 INSTAL  
VXT8    7 36 INSTAL  W6     12 25 INSTAL  WBIT   12 31 INSTAL  WF     22 14 INSTAL  
WFM1   22  2 INSTAL  WFM2   22  3 INSTAL  WK1    18 22 INSTAL  WK2    18 23 INSTAL  
WORKPT 16  3 INSTAL  WPG     4  2 INSTAL  WRTER  21 24 INSTAL  WRTER1 21 25 INSTAL  
WRTERM 21 37 INSTAL  
1INSTALL ** FICHE/FRAME BREAK *****
INSTALLDOC5
NARP
INSTALL5
BINSTALL5



RES
DDT
;TBINSTALL5
;U1%L%F
SAVE 3 T 10777 ON G1INSTALL5

240
DUMP D1INSTALL

"

        1INSTALL VERSION 5

"
CHECKSUM
.
(SS940UTIL)INSTALL5-SYMBOLICS
(SS940UTIL)G1INSTALL5-GO-FILE
(SS940UTIL)INSTALLDOC5-DOC-FILE
(SS940UTIL)MAKE1INSTALL5-BLD-FILE
.
"

"
COM TEL
1INSTALL ** FICHE/FRAME BREAK *****
MAKE1INSTALL5

                              1INSTALL
                              VERSION 5

       ADDED CODE TO CREATE FILE (*6UTIL)IPLCKFILE WHENEVER THE LOADER
COMMAND IS USED TO WRITE THE IPL TO THE DISC.  THIS FILE WILL BE USED
BY 1SYSCHECK TO VERIFY THAT THE IPL ON THE DISC IS GOOD.


                              1INSTALL
                              VERSION 2

       CHANGED VERIFY, COPY, AND MAKE COMMANDS TO BE CONCERNED ONLY
WITH FILE SUMSUB (FORMALLY SUBSUM), AND TO NO LONGER CONCERN THEMSELVES
WITH FTSUM.  THUS, FILES CREATED BY LMR NO LONGER ARE LOOKED AT BY
1INSTALL OR BY THE WSD PROGRAMS.  THIS IS TO ELIMINATE THE PROBLEM OF
INSTALLING A SUBSYSTEM, MAKING THE CONTROL FILE WITH 1INSTALL
AND FORGETTING TO ENTER INTO LMR.

       CHANGED CHECKSUM AND VERIFY COMMANDS TO ONLY REQUIRE USER
HAS SUFFICIENT LICENSE SET WHEN CALLING 1INSTALL TO ACCESS FILES IN
*6HELP, *6UTIL, OR *6WSD2 TO USE THESE COMMANDS.

       CAUTION, THIS VERSION OF 1INSTALL MUST BE INSTALLED AT THE SAME
TIME AS 1WSD AND 2WSD VERSION 94.03, AND THE MAKE COMMAND MUST
BE RUN FOR LOCATION 2 AND 12 TO CREATE THE NEW CONTROL FILES.



                               1INSTALL
                               VERSION 1

       1INSTALL IS A PROGRAM DESIGNED PRIMARLY TO AID THOSE RESPONSIBLE
FOR INSTALLATION OF TYMCOM IX OPERATING SYSTEMS, TYMNET BASE CODES,
AND UTILITY PROGRAMS WHICH ARE DUMPED TO SPECIFIED LOCATIONS ON
THE DISC.  THE PROGRAM 1DISCMAP, WHICH RECORDS THE OPERATING SYSTEM AND
OTHER PROGRAMS WHICH ARE RUN FROM DISC DUMP LOCATIONS, IS AUTOMATICALLY
UPDATED WHEN THE INSTALLATION IS MADE.  A INDEPENDENT FUNCTION IS PROVIDED
FOR CHANGING 1DISCMAP ENTRIES.

       ADDITIONALLY, THE PROGRAM IS USED TO MOVE SUBSYSTEMS FROM *6UTIL
TO *6WSD2 TO BE ACCESSABLE BY 1WSD AND 2WSD RESPECTFULLY, MAKES WSD
CONTROL FILES, AND ALLOWS CHECKSUMMING THE SUBSYSTEMS AGAINST CONTROL FILES
ALREADY IN PLACE.  THE PROGRAM ALSO ALLOWS CHECKING THE VALIDITY OF CODE
AT SPECIFIED DISC LOCATION BY CHECKSUMMING.

       1INSTALL REPLACES 1TYMB, 1MAKEMAP, (QASYSINP)INSTALL,
(QASYSINP)MVSYS, AND (QASYSINP)MVRAD.

       THE FOLLOWING COMMANDS ARE AVAILABLE IN 1INSTALL:


SYSTEM                    DUMPS OPERATING SYSTEM FILES TO 
                          LOC. 2, 4, OR 12. 
                          REQUIRES INSTALLER LICENSE.

MOVE SYSTEM               MOVES OPERATING SYSTEMS BETWEN LOC.
                          2, 4, AND 12.  
                          REQUIRES INSTALLER LICENSE.

DIM                       MOVES OLD VERSION OF DIM FROM LOC. 6A 
                          TO LOC. 6B AND DUMPS DIM FILE TO LOC.  
                          6A.
                          REQUIRES INSTALLER LICENSE.

LOADER                    DUMPS DIM IPL TO PAGE ZERO OF THE DISC
                          REQUIRES USER BE IN DISTRICT 0.  

UTIL                      DUMPS A UTILITY PROGRAM TO LOC. 10,
                          15, 16, OR 17  
                          REQUIRES USER BE IN DISTRICT 0.  

TYMNET                    DUMPS BASE CODE FILES TO LOC. 11 OR 14
                          REQUIRES TYMNET LICENSE.

CHANGE MAP                CHANGES ENTRIES BY SPECIFIED LOCATIONS
                          IN 1DISCMAP.

CHECKSUM                  DISPLAYS SYSTEM OR PROGRAM NAME AT 
                          A SPECIFIED LOCATION AND CHECKSUMS  
                          THE CODE AT THAT LOCATION  

VERIFY SUBSYSTEMS         VERIFIES SUBSYSTEMS AGAINST CONTROL
                          FILES USED BY WSD.  LOC. 2 CHECKS *6UTIL  
                          AND LOC. 12 CHECKS *6WSD2  

MAKE SUBSYSTEM CONTROL    MAKES A CONTROL FILE FOR WSD.
                          LOC. 2 MAKES CONTROL FILE FOR 1WSD. 
                          LOC. 12 MAKES CONTROL FILE FOR 2WSD.
                          REQUIRES INSTALLER LICENSE.  

COPY SUBSYSTEMS           COPIES SUBSYSTEM FILES FROM *6UTIL TO 
                          TO *6WSD2 AND VICE VERSA.  LOC. 2 IS
                          *6UTIL AND LOC. 12 IS *6WSD2  
                          REQUIRES INSTALLER LICENSE 


       FILE TO BE DUMPED MUST RESIDE IN THE USERS DIRECTORY, CROSS DIRECTORY
FILE REFERENCES, E.G. (USER NAME)FILENAME, ARE NOT PERMITTED.

       IF THE FILE IS IN FORMAT FILENAME/SYSTEM/ THE PORTION OF THE FILE
NAME BETWEEN THE SLASHS WILL BE USED AS THE SYSTEM OR PROGRAM NAME FOR
UPDATING 1DISCMAP, OTHERWISE THE USER WILL BE ASKED TO INPUT THE SYSTEM
OR PROGRAM NAME FROM THE TELETYPE AT THE TIME OF INSTALLATION.  EACH
ENTRY OF 1DISDMAP WILL BE DATE STAMPED WITH THE DATE OF THE LATEST
INSTALLATION.
   -EG+