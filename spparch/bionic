BIONIC ** FICHE/FRAME BREAK *****
SBIONIC
BIONIC IDENT 08/23/77          * HI-SPEED TERMINAL READER *
       NOLIST STAT,EXT

SPTR   OPD 514B5,2
TYPE   OPD 100B5,2
ERR    OPD 101B5,2
SPB    OPD 102B5,2

* CHAR DEFINITIONS
RRXOF  EQU 314B                (RESET REMOTE XON-XOFF)
SRXOF  EQU 315B                (SET REMOTE XON-XOFF)
DC3    EQU 23B
EOTCH  EQU 4
CRCH   EQU 155B
LFCH   EQU 152B
XOFFCH EQU 163B
XONCH  EQU 161B

* ASCII TYPES
N      EQU 0   NULL
L      EQU 2   LOWER CASE
U      EQU 4   UPPER CASE DATA
B      EQU 6   BLANK
EOL    EQU 8   END OF LINE
COL    EQU 10  CONTINUATION OF LINE
E      EQU 12  END OF TAPE
* PARAMETERS
DBUF   EQU 10000B              * ADDRESS OF DISC BUFFER *
NBUF   EQU 44                  * LIMIT COMMAND BUFFER TO 132 CHARS *
EOTFLG EQU 4B6 (BLOCK ENDED BY EOT)
TIMFLG EQU 2B6 (BLOCK ENDED BY 14 SECOND TIMEOUT)
* OPTION FLAGS
SB     EQU 4B7 (SIGN)
UPC    EQU 2B7 (UPPER CASE)
LOW    EQU 1B7 (LOWER CASE--SAME AS MONITOR TF1 BIT)
BIN    EQU 4B6 (BINARY MODE)
SUP    EQU 2B6 (SUPPRESS TRAILING BLANKS)

PZERO  EQU *
       BSS 1
$POPA  BSS 1
$POPB  BSS 1
$POPX  BSS 1
$XCIN  BSS 1
$TOUT  BSS 1
$T1    ZRO
$T2    ZRO
$T3    ZRO
$T4    ZRO
       BSS 100B+PZERO-*
       BSS 1   (RESERVED FOR MESSAGE POP)
       BSS 1   (RESERVED FOR ERROR POP)
       BRU* 0  (SPB POP)
CBUF   BSS NBUF
CBUFC  BSS 1
$CBUFP BSS 2
TID    ZRO;* TERMINAL ID CHAR
TERMC  BSS 1   (TERMINATING CHAR FOR INPUT STRING)
$PROMPT BSS 1   (NEGATIVE WHEN TERM CHAR IS BLANK)
       BSS 201B+PZERO-*
       DATA INT201             (ESCAPE ROUTINE)
OUTFIL DATA -1                 (FILE TO READ TO)
PT     ZRO;* POINTER INTO DISC BUFFER
PTCTR  ZRO;* COUNTER FOR PUTCH
RECORD ZRO;* RECORD COUNTER
FILTYP ZRO;* FILE TYPE FOR OUTPUT FILE
LCI    ZRO;* SWITCH WHEN NEGATIVE PERMITS LOWER CASE INPUT (DEFAULT)
$ACKSW ZRO;* SUPPRESS SENDING OF XON AFTER PUTTING UP FIRST READ WHEN NEG.
2780SW ZRO;* SWITCH WHEN NEGATIVE FOR TERMINALS OTHER THAN 2780
STATE  ZRO;* POINTER TO STATE TABLE
RTRY   ZRO;* RETRY COUNTER FOR BLOCK I/O TIMEOUT.  
CTR    ZRO;* TOTAL NO. OF CHARS WRITTEN TO OUTPUT FILE
DVR    ZRO;* POINTER TO APPROPRIATE READER ROUTINE
TRIMSW ZRO;* SET -1 WHEN TRIMMING BLANKS.  DEFAULT IS 0.
CH     ZRO;* SAVED CHARACTER
BC     ZRO;* BLANK COUNTER.  -1 WHEN NO BLANKS HAVE BEEN SEEN
TCWD   ZRO;* TERMINAL CHARACTERISTICS WORD
OPT    ZRO;* COMMAND OPTIONS
*PANIC TABLE
PL ZRO
PA BSS 2
PX BSS 1
       BSS 2
       BSS 1
       BSS 240B+PZERO-*
START  BRU *+2; BRS 10; STP XCIN
* READ TERMINAL TYPE FROM TS PAGE
       LDA =47; LDB =TID; LDX =1; BRS 67
* READ AND SAVE TERMINAL CHARACTERISTICS
       LDX PORT; BRS 132; STA TCWD
       LDA TID; RSH 15; ETR =17B; CLB; SKE =13B; LDB =-1; STB 2780SW
       LDA XCIN; SKE =0; BRU CD0; LDX =-1; BRS 13; BRU *+2; BRU CD0
       CIT XCIN; BRU CD0; LDA =-1; STA PROMPT; BRU CD
CD0    TYPE VERS

$CD    LDA =-1; XMA OUTFIL; SKE =-1; BRS 20
       LDA =2B6; BRS 78
       LDX =-1; LDA =4B4; BRS 12 (COMMAND EDIT)
       BRS 114
       SKN PROMPT; BRU *+2; BRU CD1
       TCO =36B; SKN 2780SW; TCO =CRCH
CD1    BRM RDKW; BRU CD        (NULL)
       STP TGTP
       LDA =CTBL; LDB =CTBLE; BRM MATCH
       BRU NXCH3 (NOT RECOGNIZED)
       BRU NXCH4 (AMBIGUOUS)
       BRU* 0,2 (BRANCH TO APPROPRIATE COMMAND)
$NXCH3 BRM PNR
$NXCH4 SPB CLEAN; SKN 2780SW; BRU QUIT; BRU CD

INT201 ZRO T1
       SPB CLEAN; BRS 29
       BRM RESETX
       TYPE MESC; BRU CD
MESC   ASC '$(ESC)$/'


PNR    ZRO;* PRINT ' ' NOT RECOGNIZED FROM CBUFP
       TCO =2; LDP CBUFP; LDX =1; BRS 35; TCO =2
       TYPE NR; BRR PNR
NR     ASC ' NOT RECOGNIZED.$/'

$RDNO  ZRO;* READ NUMBER AND SKIP IF ONE IS READ.
       BRM LOOK; SKG =31B; SKG =17B; BRR RDNO
       LDX XCIN; LDB =10; BRS 38; STB TERMC
       LDB =-1; SKB TERMC; CLB; STB PROMPT
       MIN RDNO; BRR RDNO

LOOK ZRO;* RETURN NEXT NON-BLANK CHAR WITHOUT CONSUMING IT
       LDA =377B; CIT XCIN; SKE =0; BRR LOOK; SPB NXCH; BRU LOOK+1

$RDKW  ZRO;* READ KEYWORD TO TERMINATING CHARACTER.
*      SKIP RETURN IF KEYWORD READ
*      TERMINATING CHAR RETURNED IN TERMC
*      PROMPT SWITCH SET NEGATIVE FOR  BLANK TERMC
       LDA =NBUF*3-1; STA CBUFC
       SPTR CBUF; STP CBUFP; BRM LOOK
RDKW1  SPB NXCH
       SKE =CRCH; SKG =0; BRU RDKW2
       SKE =LFCH; BRU *+2; BRU RDKW2
       SKR CBUFC; WCI CBUFP; BRU RDKW1
RDKW2  STA TERMC; CLB; SKG =0; LDB =-1; STB PROMPT
       LDA CBUFP+1; SUB CBUFP; SKG =0; BRR RDKW
       LDA CBUFP; LDB CBUFP+1
       MIN RDKW; BRR RDKW
MBNI   ASC '$MULTIPLE BLANK IN COMMANDS FILES NOT IMPLEMENTED$/'

$NXCH  EQU * READ NEXT CHAR FROM INPUT FILE
       CIO XCIN; SKE =137B; BRU NXCH1
       CLA; BRS 151; BRU NXCH
NXCH1  SKE =135B; BRU *+2; ERR MBNI
       SKG =132B; SKG =100B; BRR 0; SUB =40B
       BRR 0

$CLEAN EQU * CLEAN UP COMMANDS FILES AFTER AN ERROR
       BRM RESETX
       CLA; STA PROMPT
       BRS 181; STP XCIN; LDX =-1; BRS 11
       BRR 0

*      TABLES

CTBL   EQU *
       ASC 'EXIT'
       0 EXIT,4
       ASC 'LOGOUT'
       0 LOGOUT,4
       ASC 'QUIT'
       0 QUIT,4
       ASC 'READ'
       0 READ,4
       ASC 'HELP'
       0 HELP,4
CTBLE  EQU *

*      COMMANDS

READ   CLA; STA CTR (NO CHARS READ YET)
       STA RECORD (NO RECORDS READ YET)
       STA ACKSW (SET SWITCH TO EMIT XON AT CORRECT INSTANT)
       STA OPT (NO OPTIONS ENTERED YET)
       LDA =-1
       STA BC                  (NO BLANKS ENCOUNTERED YET)
       LDA =3
       STA RTRY                (RETRY 4 TIMES ON TIMEOUT FOR 56 SECONDS)
       LDA =3777B; STA PTCTR
       LDA =DBUF; STA PT
       LDA =1B5+BRT; STA BR
READ2  SKN PROMPT; BRU READ1 (NO OPTIONS SPECIFIED)
       BRM RDKW; BRU READ1 (NULL)
       STP TGTP
       LDA =READT; LDB =READTE; BRM MATCH
       BRU NXCH3 (NOT RECOGNIZED)
       BRU NXCH4 (AMBIGUOUS)
       LDA 0,2; MRG OPT; STA OPT; BRU READ2
*DECODE OPTIONS
READ1  SKA =BIN; BRU READ7
       SKA =UPC+LOW; BRU READ3
*NO OPTIONS SUPPLIED.  USE LCI SETTING FROM MONITOR
       LDX =-1; BRS 40; ETR =LOW; SKA =LOW; BRU *+2; LDA =UPC
       MRG OPT (POSSIBLE SUPPRESS BIT)
READ3  SKA =UPC; BRU READ4
       LDB =-1; LDX =L
READ5  STB LCI; STX ASCTYP+174B; STX ASCTYP+176B
       CLB
       SKA =SUP
       LDB =-1
       STB TRIMSW
       LDA =3; LDB =ASCII
READ6  STA FILTYP
       STB DVR
       TYPE TOFI (TO FILE)
       SKN 2780SW; TCO =CRCH
       BRM RDFIL; LDX FILTYP; BRM BRS65; BRU *-3; STA OUTFIL
       BRM INIT
       BRM GOUGE
       TYPE RDY (READY FOR INPUT)
       SKN 2780SW; BRM SETX
       BRU* DVR
READ7  SKA =UPC+LOW+SUP
       ERR WHAT
       LDA =2; LDB =BINARY
       BRU READ6
READ4  SKA =LOW; ERR BOTH
       CLB; LDX =N
       BRU READ5


READT  EQU *
       ASC 'UPPER'
       DATA SB+UPC
       ASC 'SUPPRESS'
       DATA SB+SUP
       ASC 'BINARY'
       DATA SB+BIN
       ASC 'MIXED'
       DATA SB+LOW
READTE EQU *

*BINARY READER
BINARY BRM GC; BRU BEOT
       LDB =177B
       SKM =DC3                (XOFF)
       BRU BCHR
       SKN 2780SW; BRU BCHR
       TCO =XONCH
BCHR   BRM PUTCH
       BRU BINARY
BEOT   SKA =EOTFLG; BRU EOT0
       SKA =TIMFLG; BRU BEOT1
       ERR BTIERR
*2780 READ TO EOT ALWAYS
BEOT1  BRM INIT; SKN 2780SW; BRU BINARY
       LDA CTR; SKE =0; BRU EOT0       (SOMETHING READ)
       SKR RTRY; BRU BINARY; BRU EOT0

*ASCII READER
ASCII  LDA =S0; STA STATE
NXCHR  BRM GC; BRU EOTST
NXCHR1 ETR =177B; COPY AX,AB; LDA ASCTYP,2  (ASCTYP =0,2,4,...)
       ADD STATE; COPY AX,BA; LDB 1,2; STB STATE (NEW STATE)
       BRU* 0,2 (DISPATCH TO APPROPRIATE ROUTINE)

ST     MACRO D
Y      NARG
Y      EQU Y/2
X      EQU 1
       RPT (Z=1,Y)
       DATA D(X),S.&D(X+1)
X      EQU X+2
       ENDR
       FRGT X,Y,Z
       ENDM

*      STATE TABLES

S0     EQU *   NO DATA READ YET
       ST IGNR,0,LC,1,DATA,1,BT24,2,CR,6,NXCHR,10,EOT0,0
S1     EQU * LINE STARTED
       ST IGNR,1,LC,1,DATA,1,BT35,3,CR,6,NXCHR,11,EOT1,1
S2     EQU * BLANK FIELD STARTED--NO TRIM OPTION
S3     EQU * SAME AS S2 BUT LINE STARTED
       ST IGNR,2,LCX,1,DATAX,1,INCBC,2,CRX,6,LFX,11,EOT2,2
S4     EQU * BLANK FIELD STARTED--TRIM OPTION--LINE NOT STARTED
       ST IGNR,4,LCX,1,DATAX,1,INCBC,4,CRC,6,LFC,10,EOT0,4
S5     EQU * BLANK FIELD STARTED--TRIM OPTION--LINE STARTED
       ST IGNR,5,LCX,1,DATAX,1,INCBC,5,CRC,6,LFC,11,EOT1,5
S6     EQU * CARRIAGE RETURN READ--LINE NOT STARTED
S7     EQU * SAME AS S6 BUT LINE STARTED
       ST IGNR,6,LC,1,DATA,1,BT24,2,CR,6,NXCHR,0,EOT0,6
S10    EQU * LINEFEED READ--LINE NOT STARTED
       ST IGNR,10,LC,1,DATA,1,BT24,2,LF,1,NXCHR,10,EOT0,10
S11    EQU * LINEFEED READ--LINE STARTED
       ST IGNR,11,LC,1,DATA,1,BT35,3,LF,1,NXCHR,11,EOT1,11


ASCTYP EQU *   TABLE OF ASCII CHARACTER TYPES
       DATA N  (NUL)
       DATA U  (SOH)
       DATA U  (STX)
       DATA U  (ETX)
       DATA E  (EOT)
       DATA U  (ENQ)
       DATA U  (ACK)
       DATA U  (BEL)
       DATA U  (BS)
       DATA U  (HT)
       DATA COL (LF)
       DATA U  (VT)
       DATA U  (FF)
       DATA EOL (CR)
       DATA U  (SO)
       DATA U  (SI)
       DATA U  (DLE)
       DATA U  (DC1)
       DATA U  (DC2)
       DATA N  (DC3)
       DATA U  (DC4)
       DATA U  (NAK)
       DATA U  (SYN)
       DATA U  (ETB)
       DATA U  (CAN)
       DATA U  (EM)
       DATA U  (SUB)
       DATA N  (ESC)
       DATA N  (FS)
       DATA N  (GS)
       DATA E  (RS--CONTROL SHIFT N)
       DATA N  (US)
       DATA B  (BLANK)
       RPT (X=1,15); DATA U; ENDR
       RPT (X=1,10); DATA U; ENDR
       RPT (X=1,7); DATA U; ENDR
       RPT (X=1,26); DATA U; ENDR
       RPT (X=1,6); DATA U; ENDR
       RPT (X=1,27); DATA L; ENDR
       DATA L (173--LOWER CASE ALWAYS)
       DATA L (174--LOWER CASE OR IGNORE)
       DATA N (175--ALWAYS IGNORE TO AVOID CONFUSION)
       DATA L (176--LOWER CASE OR IGNORE)
       DATA N (RUBOUT)


*EXPAND BLANKS BEFORE UPPER CASE
DATAX  STA CH; CLA; BRM PUTCH; SKR BC; BRU *-3; LDA CH; BRU DATA
*EXPAND BLANKS BEFORE LOWER CASE
LCX    STA CH; CLA; BRM PUTCH; SKR BC; BRU *-3; LDA CH
*LOWER CASE
LC     SKN LCI; SUB =40B
*UPPER CASE
DATA   SUB =40B
       ETR =177B
       BRM PUTCH
       BRU NXCHR

*CARRIAGE RETURN PROCESSING
CRC    LDA =CRCH; BRM PUTCH; MIN RECORD
LFC    LDA =-1; STA BC; BRU NXCHR
CRX    CLA; BRM PUTCH; SKR BC; BRU CRX
CR     LDA =CRCH; BRM PUTCH; MIN RECORD; BRU NXCHR

*LINEFEED PROCESSING
LFX    CLA; BRM PUTCH; SKR BC; BRU LFX; BRU NXCHR
LF     LDA =LFCH; BRM PUTCH; BRU NXCHR

*BLANK PROCESSING
BT24   MIN BC; SKN TRIMSW; BRU NXCHR
       LDA =S4; STA STATE; BRU NXCHR
BT35   MIN BC; SKN TRIMSW; BRU NXCHR
       LDA =S5; STA STATE; BRU NXCHR
INCBC  MIN BC; BRU NXCHR

* CHECK INGORED CHARACTERS FOR X-OFF.
IGNR   SKE =DC3; BRU NXCHR
       SKN 2780SW; BRU NXCHR
       TCO =XONCH; BRU NXCHR

EOTST  EQU *   (CHECK FOR TRUE END OF TAPE)
       SKA =EOTFLG; BRU EOT
       SKA =TIMFLG; BRU EOTST1
       LDX PORT; BRS 99; BRS 178
       ERR BTIERR
EOTST1 BRM INIT; SKN 2780SW; BRU NXCHR
       LDA CTR; SKE =0; BRU EOT
       SKR RTRY; BRU NXCHR
EOT    LDA =EOTCH; BRU NXCHR1  (REPLAY EOT CHAR)

* END-OF-TAPE ROUTINES

EOT2   EQU * EXPAND BLANKS, ADD CR, FLUSH
       CLA; BRM PUTCH; SKR BC; BRU EOT2
EOT1   EQU * ADD CR, FLUSH
       LDA =CRCH; BRM PUTCH; MIN RECORD
EOT0   EQU * FLUSH
       LDX =-1; BRS 113; BRS 99
       SKN 2780SW; BRU *+2; TCO =XOFFCH
       LDA CTR; ADD =2; RSH 23; DIV =3*4000B
       CBA; LDX =DBUF; SKG =0; BRU *+3; BIO OUTFIL; ERR WRITER
       LDA CTR; MRG =4B7; SCP OUTFIL; ERR SCPER
       RSP OUTFIL; PCE OUTFIL; ERR ERASER
       TYPE MEOT
       LDA DVR; SKE =BINARY; BRU *+2; BRU EOT3
       LDA RECORD; LDB =10; LDX TOUT; BRS 36
       TYPE MEOT1 (RECORDS READ)
EOT3   BRM RESETX; BRS 178; SKN 2780SW; BRS 10; BRU CD

$DEFER ZRO;* SEND XON AFTER GC HAS PUT UP FIRST READ
       LDA =-1; STA ACKSW
       SKN 2780SW; BRR DEFER; TCO =XONCH; BRR DEFER
SETX   ZRO;* SET XON-XOFF SWITCH IN REMOTE
       LDX PORT; LDA =RRXOF; BRS 172
       LDX =-24
       TCO =CRCH (EXTRA CR/LF)
       BRX *-1
       LDA =5*1000; BRS 81 (5 SECOND DELAY ALLOWS OUTPUT)
*                              (TO COMPLETE BEFORE RETURNING GREEN BALL)
       BRR SETX
GOUGE  ZRO;* GOUGE CIRCUIT AND TURN OFF ECHO
       LDX PORT; BRS 132; ETR =37777777B; EOR =14B4; BRS 160
       EOR =14B4; BRS 160; BRR GOUGE

RESETX ZRO
       LDX PORT; LDA TCWD; BRS 160
* SET REMOTE XON-OFF
       SKN 2780SW; BRU *+2; BRR RESETX
       LDA =SRXOF
       BRS 172
       TCO =LFCH
       BRR RESETX
PUTCH  ZRO
       MIN CTR
BR     BRU BRT
BRT    BRU CHR1; BRU CHR2; BRU CHR3
CHR1   CLB; LSH 16; STA* PT; MIN BR; BRR PUTCH
CHR2   CLB; LSH 8; ADM* PT; MIN BR; BRR PUTCH
CHR3   ADM* PT; MIN PT
       LDA =1B5+BRT; STA BR
       SKR PTCTR; BRR PUTCH
       LDA =4000B; LDX =DBUF; STX PT; BIO OUTFIL; ERR WRITER
       LDA =3777B; STA PTCTR; BRR PUTCH

HELP   TYPE HELPM; BRU CD
QUIT   BRS 10

EXIT   LDA ="EXI"; BRS 142
LOGOUT LDA ="LOG"; BRS 142


$BRS64 ZRO;* OPEN FILE IN USER-STATUS FORK
       STP PA; STX PX; LDA =BRS64A; STA PL
       LDA =PL+1B7; BRS 9
       LDA PL; ETR =37777B; SKE =BRS64C; MIN BRS64
       LDP PA; LDX PX; BRR BRS64
BRS64A BRS 64; BRU BRS64B; BRS 10
BRS64B SPB CLEAN; TYPE RTF
BRS64C BRS 10
RTF    ASC 'RETYPE FILE NAME: /'

BRS65  ZRO;* OPEN FILE IN USER-STATUS FORK
       STP PA; STX PX; LDA =BRS65A; STA PL
BRS65C LDA =PL+1B7; BRS 9
       LDX PL; LDA 0,2; SKE BS172; BRU BRS65B
       LDA PA; LDX PX
BS172  BRS 172
       MIN PL; BRU BRS65C
BRS65B LDA PL; ETR =37777B; SKE =BRS64C; MIN BRS65
       LDP PA; LDX PX; BRR BRS65
BRS65A BRS 65; BRU BRS64B; BRS 10

$RDFIL ZRO;* READ STRING TO CR OR LF
       SPTR CBUF
       STP CBUFP; LDA =75; STA CBUFC
       SPB NXCH; SKE =CRCH; SKG =0; BRU *-3; BRU *+2
RDFIL1 SPB NXCH; SKE =CRCH; SNE =LFCH; BRU RDFIL2
       SKR CBUFC; WCI CBUFP; BRU RDFIL1
RDFIL2 GCD CBUFP; BRU RDFIL3; SKG =0; BRU RDFIL2
       WCI CBUFP
RDFIL3 LDP CBUFP; BRR RDFIL


TOFI   ASC '$TO FILE: /'
RDY    ASC '$READY FOR INPUT$/'
MEOT   ASC '$END OF TRANSMISSION$/'
MEOT1  ASC ' RECORDS READ.$/'
SCPER  ASC '$ERROR POSITIONING FILE$/'
WRITER ASC '$ERROR WRITING TO FILE$/'
ERASER ASC '$ERROR ERASING FILE$/'
BOTH   ASC '$YOU SPECIFIED BOTH MIXED AND UPPER CASE$/'
WHAT   ASC '$YOU SPECIFIED ASCII OPTIONS WITH BINARY MODE$/'
HELPM  ASC '$VALID COMMANDS ARE'
       ASC '$HELP - PRINTS THIS LIST'
       ASC '$QUIT - QUITS THE PROGRAM'
       ASC '$EXIT - EQUIVALENT TO EXECUTIVE EXIT COMMAND'
       ASC '$LOGOUT - EQUIVALENT TO EXECUTIVE LOGOUT COMMAND'
       ASC '$READ <OPTIONS> - READ FROM REMOTE DEVICE TO FILE.  OPTIONS ARE:'
       ASC '$                  BINARY - READ WITHOUT CONVERSION'
       ASC '$                  UPPER - CONVERT LOWER TO UPPER CASE'
       ASC '$                  MIXED - ALLOW LOWER AND UPPER CASE'
       ASC '$                  SUPPRESS - SUPPRESSES TRAILING BLANKS IN RECORDS'
       ASC '$                          WRITTEN TO OUTPUT FILE.'
       ASC '$  IF NO OPTIONS ARE SPECIFIED THEN ASCII MODE IS ASSUMED.$$/'
BTIERR ASC '$ERROR IN BUFFERED TERMINAL INPUT$/'
VERS   ASC '$BIONIC VERSION 1.2$$/'
EXPDIG ASC '$EXPECTED DIGIT$/'
       END
BIONIC ** FICHE/FRAME BREAK *****
SBIONIC SYMSORT

SYM.  PG.LN. IDENT.

2780SW  2 30 BIONIC  ACKSW   2 29 BIONIC  ASCII   7 24 BIONIC  
ASCTYP  8 24 BIONIC  B       1 23 BIONIC  BC      2 37 BIONIC  BCHR    7 13 BIONIC  
BEOT    7 15 BIONIC  BEOT1   7 19 BIONIC  BIN     1 36 BIONIC  BINARY  7  7 BIONIC  
BOTH   13 20 BIONIC  BR     12  3 BIONIC  BRS64  12 20 BIONIC  BRS64A 12 25 BIONIC  
BRS64B 12 26 BIONIC  BRS64C 12 27 BIONIC  BRS65  12 30 BIONIC  BRS65A 12 39 BIONIC  
BRS65B 12 37 BIONIC  BRS65C 12 32 BIONIC  BRT    12  4 BIONIC  BS172  12 35 BIONIC  
BT24   10 16 BIONIC  BT35   10 18 BIONIC  BTIERR 13 34 BIONIC  CBUF    2 15 BIONIC  
CBUFC   2 16 BIONIC  CBUFP   2 17 BIONIC  CD      3 18 BIONIC  CD0     3 16 BIONIC  
CD1     3 24 BIONIC  CH      2 36 BIONIC  CHR1   12  5 BIONIC  CHR2   12  6 BIONIC  
CHR3   12  7 BIONIC  CLEAN   4 38 BIONIC  COL     1 25 BIONIC  CR     10  9 BIONIC  
CRC    10  6 BIONIC  CRCH    1 14 BIONIC  CRX    10  8 BIONIC  CTBL    5  7 BIONIC  
CTBLE   5 18 BIONIC  CTR     2 33 BIONIC  DATA    9 39 BIONIC  DATAX   9 33 BIONIC  
DBUF    1 28 BIONIC  DC3     1 12 BIONIC  DEFER  11 17 BIONIC  DVR     2 34 BIONIC  
E       1 26 BIONIC  EOL     1 24 BIONIC  EOT    10 35 BIONIC  EOT0   11  4 BIONIC  
EOT1   11  2 BIONIC  EOT2   10 39 BIONIC  EOT3   11 15 BIONIC  EOTCH   1 13 BIONIC  
EOTFLG  1 30 BIONIC  EOTST  10 27 BIONIC  EOTST1 10 32 BIONIC  ERASER 13 19 BIONIC  
ERR     1  6 BIONIC  EXIT   12 16 BIONIC  EXPDIG 13 36 BIONIC  FILTYP  2 27 BIONIC  
GOUGE  11 28 BIONIC  HELP   12 13 BIONIC  HELPM  13 22 BIONIC  IGNR   10 23 BIONIC  
INCBC  10 20 BIONIC  INT201  3 33 BIONIC  L       1 21 BIONIC  LC      9 37 BIONIC  
LCI     2 28 BIONIC  LCX     9 35 BIONIC  LF     10 13 BIONIC  LFC    10  7 BIONIC  
LFCH    1 15 BIONIC  LFX    10 12 BIONIC  LOGOUT 12 17 BIONIC  LOOK    4 12 BIONIC  
LOW     1 35 BIONIC  MBNI    4 29 BIONIC  MEOT   13 15 BIONIC  MEOT1  13 16 BIONIC  
MESC    3 37 BIONIC  N       1 20 BIONIC  NBUF    1 29 BIONIC  NR      4  4 BIONIC  
NXCH    4 31 BIONIC  NXCH1   4 34 BIONIC  NXCH3   3 30 BIONIC  NXCH4   3 31 BIONIC  
NXCHR   7 25 BIONIC  NXCHR1  7 26 BIONIC  OPT     2 39 BIONIC  OUTFIL  2 23 BIONIC  
PA      3  3 BIONIC  PL      3  2 BIONIC  PNR     4  1 BIONIC  POPA    2  2 BIONIC  
POPB    2  3 BIONIC  POPX    2  4 BIONIC  PROMPT  2 20 BIONIC  PT      2 24 BIONIC  
PTCTR   2 25 BIONIC  PUTCH  12  1 BIONIC  PX      3  4 BIONIC  PZERO   1 39 BIONIC  
QUIT   12 14 BIONIC  RDFIL  13  2 BIONIC  RDFIL1 13  6 BIONIC  RDFIL2 13  8 BIONIC  
RDFIL3 13 10 BIONIC  RDKW    4 15 BIONIC  RDKW1   4 21 BIONIC  RDKW2   4 25 BIONIC  
RDNO    4  6 BIONIC  RDY    13 14 BIONIC  READ    5 22 BIONIC  READ1   6  2 BIONIC  
READ2   5 33 BIONIC  READ3   6  7 BIONIC  READ4   6 29 BIONIC  READ5   6  9 BIONIC  
READ6   6 15 BIONIC  READ7   6 25 BIONIC  READT   6 34 BIONIC  READTE  7  4 BIONIC  
RECORD  2 26 BIONISETX 11 32 BIONIC  RRXOF   1 10 BIONIC  RTF    12 28 BIONIC  
RTRY    2 32 BIONIC  S0      8  4 BIONIC  S1      8  6 BIONIC  S10     8 18 BIONIC  
S11     8 20 BIONIC  S2      8  8 BIONIC  S3      8  9 BIONIC  S4      8 11 BIONIC  
S5      8 13 BIONIC  S6      8 15 BIONIC  S7      8 16 BIONIC  SB      1 33 BIONIC  
SCPER  13 17 BIONIC  SETX   11 20 BIONIC  SPB     1  7 BIONIC  SPTR    1  4 BIONIC  
SRXOF   1 11 BIONIC  ST      7 30 BIONIC  START   3  8 BIONIC  STATE   2 31 BIONIC  
SUP     1 37 BIONIC  T1      2  7 BIONIC  T2      2  8 BIONIC  T3      2  9 BIONIC  
T4      2 10 BIONIC  TCWD    2 38 BIONIC  TERMC   2 19 BIONIC  TID     2 18 BIONIC  
TIMFLG  1 31 BIONIC  TOFI   13 13 BIONIC  TOUT    2  6 BIONIC  TRIMSW  2 35 BIONIC  
TYPE    1  5 BIONIC  U       1 22 BIONIC  UPC     1 34 BIONIC  VERS   13 35 BIONIC  
WHAT   13 21 BIONIC  WRITER 13 18 BIONIC  X       7 36 BIONIC  X       7 33 BIONIC  
XCIN    2  5 BIONIC  XOFFCH  1 16 BIONIC  XONCH   1 17 BIONIC  Y       7 32 BIONIC  
Y       7 31 BIONIC  
BIONIC ** FICHE/FRAME BREAK *****
SBTI
BTI    IDENT 05/05/76
       NOLIST STAT

SPTR   OPD 514B5,2
NBUF EQU 15    NO. OF BUFFERS
COMBUF EQU 4000B
BUF EQU COMBUF+200B
FREE   EQU 4B7
BUSY EQU 2B7
CZFLG  EQU 1B7
EOTFLG EQU 4B6
TIMFLG EQU 2B6
TERFLG EQU 1B6
ABOFLG EQU 4B5
ICCFLG EQU 1B5
PCEFLG EQU 4B4
EMPTY  EQU 1
CNTMSK EQU 3774B4
$INIT ZRO;* INITIALIZE RING.  MARKS ALL BUFFERS AS FREE AND EMPTY,
*      AND THREADS COMMANDS PROPERLY.  CALL THIS ROUTINE AFTER
*      ANY EXCEPTION TO SET THE FLAGS FOR A GOOD RESTART.
       LDA =BUF; STA WCP
       LDA =COMBUF+3; STA RCP
       LDA =NBUF-1; STA CCTR
       LDX =COMBUF
INIT1 LDB RCP; LDA =FREE+EMPTY
       STA 0,2 (INIT FLAG WORD)
       STB 1,2 (FORWARD PTR)
       LDA WCP; STA 2,2 (BUFFER ADDR)
       LDA =200B; ADM WCP; LDA =3; ADM RCP
       EAX 3,2; SKR CCTR; BRU INIT1
*CCTR INITIALIZED TO -1
       LDA =COMBUF; STA -2,2; STA RCP; STA WCP
       LDA =BUF; STA RCPTR; STA GCCNT
       BRR INIT
GCCNT DATA 37777777B           (COUNTER FOR WD)
WD     DATA 0                  (CHARACTER DEASSEMBLY)
RCPTR DATA BUF
RCP   DATA COMBUF (CURRENT COMMAND FOR CHARACTER FETCH)
WCP   DATA COMBUF (CURRENT COMMAND TO BE STARTED)
CCTR  DATA -1 (COMMAND COUNTER FOR STRTIO)
$PORT  DATA -1

STRTIO ZRO;* START BUFFERED TERMINAL I/O INTO WCP BUFFER.
*      STARTS I/O FOR THIS BUFFER AND ALL BUFFERS FURTHER ALONG
*      UNTIL A NON-EMPTY BUFFER IS REACHED.  SKIP RETURN IF READING
*      AHEAD HAS CEASED DUE TO AN EXCEPTION (EOT, TIMEOUT, ETC)
       LDX WCP; LDA 0,2; SKA =EMPTY; BRU *+2; BRR STRTIO
       LDA =7B7; XMA 2,2; ETR =37777B; ADM 2,2 (FORM INPUT COMMAND)
STIO   LDA WCP; LDB CCTR; MIN CCTR; LDX PORT; BRS 97 (CLEAR FLAG BITS)
       BRU IOSTOP (I/O HAS STOPPED)
* NEXT TWO INSTRUCTIONS PROGRAM DEPENDENT
       SKN ACKSW; BRM DEFER (SLAVE NOW IN BLOCK INPUT MODE)
       LDX WCP; LDA 1,2; STA WCP; BRU STRTIO+1 (ADVANCE TO NEXT COMMAND)
IOSTOP LDX PORT; LDA =COMBUF; BRS 75; CAX
       SKE WCP; BRU STERR (I/O STOPPED ON SOME PREVIOUS COMMAND)
       LDA 0,2; SKE =FREE+BUSY+EMPTY; BRU STERR1 (EXCEPTIONAL CONDITION NOTED)
       LDA =-1; STA CCTR
       BRU STIO (MONITOR HAD TO STOP FOR USER.  RESTART I/O)
STERR1 SKA =BUSY; BRR GC (PREVENT LOOPING AT GC+6)
STERR  MIN STRTIO
       BRR STRTIO (SKIP RETURN FOR EXCEPTION)


$GC    ZRO;* FETCH CHARACTER.  START MONITOR READING AHEAD.  AS GC
*      EMPTIES BUFFERS IT TURNS ON THE "EMPTY" FLAG BIT WHICH PERMITS
*      STRTIO TO START FILLING THE BUFFER.  IT IS IMPORTANT TO
*      REALIZE THAT BRS 97 ZEROES OUT THE FLAG BITS (INCLUDING THE
*      "EMPTY" BIT, AND THE MONITOR DOES NOT MAKE THE BUFFER
*      AVAILABLE AGAIN UNTIL THE "FREE" BIT REAPPEARS.
       LDX RCP; SKN 0,2; BRU GC2 (WAIT FOR MONITOR TO FREE BUFFER)
       LDA =EMPTY; SKA 0,2; BRU GC2 (NO I/O SINCE INIT)
       LDA 2,2; SKA =CNTMSK; BRU GC1 (NON-ZERO COUNT)
* EMPTY BUFFER.  ADVANCE TO NEXT BUFFER IF NO EXCEPTIONS FLAGGED
       LDA 0,2; SKA =-1-FREE-BUSY; BRR GC
       LDA =EMPTY; ADM 0,2 (BUFFER CAN NOW BE FILLED)
       LDX 1,2; STX RCP (ADVANCE)
       LDA 2,2; ETR =37777B; STA RCPTR; STA GCCNT (BETTER NOT BE ZERO)
       BRM STRTIO
       BRU GC+1 (NORMAL RETURN)
       BRU GC+1 (EXCEPTION RETURN)
GC1    SUB =4B4; STA 2,2 (DECREMENT COUNT)
       MIN GC (FOR SKIP RETURN)
       SKR GCCNT; BRU GWD; LDB WD
GCH    CLA; LSH 8; STB WD; BRR GC
GWD    LDB* RCPTR; MIN RCPTR; LDA =2+4B7; STA GCCNT; BRU GCH
GC2    BRM STRTIO (ATTEMPT TO GET I/O GOING)
* COULD SIMULATE INTERRUPT INSTEAD OF SLEEPING IN SOME PROGRAMS
* BY REPLACING BRS 45 WITH A SBRM* 2XX.  ALTERNATIVELY, IF IT IS
* DESIRED TO SLEEP, BRS 178 WOULD YIELD GREATER EFFICIENCY IF THE DELAY
* CAN BE TOLERATED.
       BRS 45 (SLEEP AWHILE)
       BRU GC+1 (EXCEPTION RETURN)
       END
BIONIC ** FICHE/FRAME BREAK *****
SBTI SYMSORT

SYM.  PG.LN. IDENT.

ABOFLG  1 14 BTI     BUF     1  7 BTI     BUSY    1  9 BTI     
CCTR    2  2 BTI     CNTMSK  1 18 BTI     COMBUF  1  6 BTI     CZFLG   1 10 BTI     
EMPTY   1 17 BTI     EOTFLG  1 11 BTI     FREE    1  8 BTI     GC      2 26 BTI     
GC1     3  4 BTI     GC2     3  9 BTI     GCCNT   1 36 BTI     GCH     3  7 BTI     
GWD     3  8 BTI     ICCFLG  1 15 BTI     INIT    1 19 BTI     INIT1   1 26 BTI     
IOSTOP  2 16 BTI     NBUF    1  5 BTI     PCEFLG  1 16 BTI     PORT    2  3 BTI     
RCP     1 39 BTI     RCPTR   1 38 BTI     SPTR    1  4 BTI     STERR   2 22 BTI     
STERR1  2 21 BTI     STIO    2 11 BTI     STRTIO  2  5 BTI     TERFLG  1 13 BTI     
TIMFLG  1 12 BTI     WCP     2  1 BTI     WD      1 37 BTI     
BIONIC ** FICHE/FRAME BREAK *****
TPTSUBS
TPTSUB IDENT 6/10/76
       NOLIST EXT,STAT

SPTR   OPD 514B5,2
SPB    OPD 102B5,2

TYPE   POPD 100B5,2
       STP POPA; STX POPX
       EAX* 0; CXA; ETR =37777B
       LDB =-1; LDX TOUT; BRS 34
XPOP   LDP POPA
ABPOP  LDX POPX
       BRR 0

ERR    POPD 101B5,2
$ERRPOP LDA 0; STA ERR1
       SPB CLEAN
       TYPE* ERR1
       BRU CD
ERR1   ZRO
FND    ZRO
FND1   ZRO
MATT   ZRO
TBL    ZRO
ETBL   ZRO
$TGTP  BSS 2   (TARGET PAIR)
TGT    BSS 2   (WORKING PAIR)
LISTP  BSS 2   (LIST ENTRY PAIR)

$MATCH ZRO;* SEARCH TABLE FOR STRING AND SUBSTRING MATCHES
*      FOR STRING WHOSE POINTERS ARE IN TGTP.  TYPICAL CALL
*      FOLLOWS:
*        LDA =ADDR1            (TABLE START)
*        LDB =ADDR2            (TABLE END)
*        BRM MATCH
*      (NONSKIP RETURN--NO MATCH)
*      (SKIP RETURN--NO UNIQUE MATCH FOUND)
*      (DOUBLE SKIP RETURN--MATCH FOUND)
*      ON EXIT, 'X' WILL CONTAIN ADDRESS OF LAST WORD IN MATCHING
*      TABLE ENTRY.
*
*      TABLE CONSISTS OF VARIABLE LENGTH ENTRIES WITH THE SIGN
*      BIT ON IN THE LAST WORD OF EACH ENTRY.  EACH ENTRY BEGINS
*      WITH STRING DATA TO BE MATCHED, TAKING AS MANY WORDS AS
*      REQUIRED.  THE STRING IS FOLLOWED BY A BLANK TERMINATING
*      CHARACTER OR A BYTE WITH THE 200 BIT ON.
*
       STA TBL; CBA; SUB =1; STA ETBL
       LDX TBL; BRM MAT; BRR MATCH     (NOT FOUND)
       MIN MATCH
       STX FND; EAX 1,2; CXA; SKG ETBL; BRU MATCH2
MATCH1 LDX FND; MIN MATCH; BRR MATCH
MATCH2 BRM MAT; BRU MATCH1     (UNIQUE)
       TYPE MATERR             (CANNOT DISTINGUISH AMONG:)
       LDX TBL
MATCH3 BRM MAT; BRR MATCH; STX FND
       LDX FND1; SPTR 0,2; ADD =100; XAB; STP TGT
MATCH4 GCI TGT; NOP; SKE =0; SKA =200B; BRU MATCH5
       CIO TOUT; BRU MATCH4
MATCH5 LDA =155B; CIO TOUT
       LDX FND; EAX 1,2; CXA; SKG ETBL; BRU MATCH3
       BRR MATCH

MATERR ASC '$CANNOT DISTIGUISH AMONG:$/'
MAT    ZRO
MAT1   SPTR 0,2; ADD =100; XAB; STP LISTP; LDP TGTP; STP TGT
MAT2   GCI TGT; BRU MAT5       (MATCH WHEN TARGET EXHAUSTED)
       STA MATT
       GCI LISTP; NOP; SKE =0; SKA =200B; BRU MAT3
       SKE MATT; BRU MAT3; BRU MAT2
* NO MATCH
MAT3   SKN 0,2; BRU MAT4
       EAX 1,2; CXA; SKG ETBL; BRU MAT1; BRR MAT
MAT4   EAX 1,2; BRU MAT3
* MATCH
MAT5   STX FND1
MAT6   SKN 0,2; BRU MAT7; MIN MAT; BRR MAT
MAT7   EAX 1,2; BRU MAT6

       END
BIONIC ** FICHE/FRAME BREAK *****
TPTSUBS SYMSORT

SYM.  PG.LN. IDENT.

ABPOP   1 12 TPTSUB  ERR     1 15 TPTSUB  ERR1    1 20 TPTSUB  
ERRPOP  1 16 TPTSUB  ETBL    1 25 TPTSUB  FND     1 21 TPTSUB  FND1    1 22 TPTSUB  
LISTP   1 28 TPTSUB  MAT     2 26 TPTSUB  MAT1    2 27 TPTSUB  MAT2    2 28 TPTSUB  
MAT3    2 33 TPTSUB  MAT4    2 35 TPTSUB  MAT5    2 37 TPTSUB  MAT6    2 38 TPTSUB  
MAT7    2 39 TPTSUB  MATCH   1 30 TPTSUB  MATCH1  2 13 TPTSUB  MATCH2  2 14 TPTSUB  
MATCH3  2 17 TPTSUB  MATCH4  2 19 TPTSUB  MATCH5  2 21 TPTSUB  MATERR  2 25 TPTSUB  
MATT    1 23 TPTSUB  SPB     1  5 TPTSUB  SPTR    1  4 TPTSUB  TBL     1 24 TPTSUB  
TGT     1 27 TPTSUB  TGTP    1 26 TPTSUB  TYPE    1  7 TPTSUB  XPOP    1 11 TPTSUB  

BIONIC ** FICHE/FRAME BREAK *****
BIONICDOC
INTRODUCTION
------------

BIONIC _)_3 _! _0_2_/_'_2_!_- _&_/_2 _0_2_/_6_)_$_)_._' _%_#_/_._/_-_)_#_!_, _(_)_'_(-_3_0_%_%_$
_)_._0_5_4 _4_/ _4_(_% 940 _&_2_/_- _!_. _%_8_4_%_2_._!_, ASR _/_2 _3_5_0_0_/_2_4_%_$ HSA _$_%_6_)_#_%.
F_/_2 _%_#_/_._/_-_9 _2_%_!_3_/_._3 BIONIC _(_!_3 _&_%_7 _/_0_4_)_/_._3.


RUNNING BIONIC
--------------

BIONIC _)_3 _#_!_,_,_%_$ _&_2_/_- _4_(_% 940 _,_)_"_2_!_2_9 _"_9 _4_9_0_)_._' "BIONIC" _!_4 _4_(_%
E_8_%_#_5_4_)_6_% _0_2_/_-_0_4.  BIONIC _0_2_)_._4_3 _! ">" _!_3 _)_4_3 _0_2_/_-_0_4:

               -BIONIC(_#_2)

               BIONIC VERSION 1.1

               >

N_/_4_%:  W_(_%_. _)_._0_5_4 _)_3 _&_2_/_- _#_!_2_$_3 _/_. _! _3_5_0_0_/_2_4_%_$ HSA _$_%_6_)_#_%,
_! _-_5_,_4_)_0_5_._#_(_%_$ & 5 8 9 _#_/_,_5_-_. _-_!_9 _"_% _5_3_%_$ _&_/_2 _#_!_2_2_)_!_'_% _2_%_4_5_2_.
_)_. _/_2_$_%_2 _4_/ _0_,_!_#_% _-_5_,_4_)_0_,_% _)_._0_5_4 _,_)_._%_3 _/_. _/_._% _#_!_2_$.


BIONIC COMMANDS
---------------

T_(_% _&_/_,_,_/_7_)_._' _#_/_-_-_!_._$_3 _!_2_% _)_-_0_,_%_-_%_._4_%_$ _)_. BIONIC.  E_!_#_( _-_!_9 _"_%
_!_"_"_2_%_6_)_!_4_%_$ _!_3 _! _5_._)_1_5_% _,_%_&_4 _3_5_"_3_%_4 _/_& _4_(_% _#_/_-_-_!_._$:

       HELP - _0_2_)_._4_3 _! _,_)_3_4 _/_& BIONIC _#_/_-_-_!_._$_3
       QUIT - _%_8_)_4_3 _4_(_% _0_2_/_'_2_!_-
       EXIT - _0_%_2_&_/_2_-_3 _!_. E_8_%_#_5_4_)_6_% EXIT _#_/_-_-_!_._$
       LOGOUT - _0_%_2_&_/_2_-_3 _!_. E_8_%_#_5_4_)_6_% LOGOUT _#_/_-_-_!_._$
       READ <_/_0_4_)_/_._3> - _2_%_!_$_3 _&_2_/_- _2_%_-_/_4_% _$_%_6_)_#_% _!_#_#_/_2_$_)_._' _4_/ _/_0_4_)_/_._3
_3_0_%_#_)_&_)_%_$.  T_(_% _/_0_4_)_/_._3 _!_2_%:
               BINARY - _2_%_!_$ _7_)_4_(_/_5_4 _#_/_._6_%_2_3_)_/_.
               UPPER - _2_%_!_$ _!_._$ _#_/_._6_%_2_4 _4_/ _)_._4_%_2_._!_, ASCII, _!_,_3_/
                               _#_/_._6_%_2_4_)_._' _,_/_7_%_2 _4_/ _5_0_0_%_2 _#_!_3_%.
               MIXED - _2_%_!_$ _!_._$ _#_/_._6_%_2_4 _4_/ _)_._4_%_2_._!_, ASCII, _!_,_,_/_7_)_._'
                               _,_/_7_%_2 _#_!_3_%.
               SUPPRESS - _2_%_!_$ _!_._$ _#_/_._6_%_2_4 _4_/ _)_._4_%_2_._!_, ASCII, _!_,_3_/
                               _3_5_0_0_2_%_3_3_)_._' _4_2_!_)_,_)_._' _"_,_!_._+_3 _)_. _2_%_#_/_2_$_3
                               _7_2_)_4_4_%_. _4_/ _4_(_% _/_5_4_0_5_4 _&_)_,_%.

       I_& _._/ _/_0_4_)_/_._3 _!_2_% _3_5_0_0_,_)_%_$, _4_(_%_. _#_/_._6_%_2_3_)_/_. _4_/ _)_._4_%_2_._!_, ASCII
_4_!_+_%_3 _0_,_!_#_% _5_3_)_._' _4_(_% _0_2_%_6_!_)_,_)_._' _,_/_7_%_2 _#_!_3_% _/_0_4_)_/_. _3_%_,_%_#_4_)_/_. _!_3
_$_%_4_%_2_-_)_._%_$ _"_9 _4_(_% E_8_%_#_5_4_)_6_% ONLC/OFFLC _#_/_-_-_!_._$_3.
       L_)_._%_&_%_%_$_3 _!_0_0_%_!_2_)_._' _)_. _4_(_% _)_._0_5_4 _3_4_2_%_!_- _!_2_% _)_'_._/_2_%_$ _5_._,_%_3_3
_&_/_,_,_/_7_%_$ _"_9 _#_!_2_2_)_!_'_% _2_%_4_5_2_..  T_(_5_3, CR/LF _0_!_)_2_3 _!_2_% _2_%_!_$ _!_3 _!
_#_!_2_2_)_!_'_% _2_%_4_5_2_. _!_._$ LF/CR _0_!_)_2_3 _!_2_% _2_%_!_$ _!_3 _! _,_)_._%_&_%_%_$.  T_(_% _&_/_,_,_/_7_)_._'
_#_(_!_2_!_#_4_%_2_3 _!_2_% _._/_4 _2_%_!_$ _)_._4_/ _4_(_% _$_!_4_! _&_)_,_%:  NUL,EOT (_!_#_4_3 _!_3 _%_._$
_/_& _4_2_!_._3_-_)_3_3_)_/_.),DC3 (_#_!_5_3_%_3 DC1 _/_2 X-ON _4_/ _"_% _%_#_(_/_%_$),ESC,FS,GS,
RS (_#_/_._4_2_/_,-_3_(_)_&_4 N _!_#_4_3 _!_3 _%_._$ _/_& _4_2_!_._3_-_)_3_3_)_/_.),US,"_<" (_5_._,_%_3_3 MIXED
_/_0_4_)_/_. _)_3 _3_0_%_#_)_&_)_%_$),"  ,"_>" (_5_._,_%_3_3 MIXED _/_0_4_)_/_. _)_3 _3_0_%_#_)_&_)_%_$),DEL.

       T_(_% READ _#_/_-_-_!_._$ _2_%_1_5_)_2_%_3 _! _&_)_,_%_._!_-_% _4_/ _"_% _)_._0_5_4.
T_(_% _&_)_,_%_._!_-_% _3_0_%_#_)_&_)_%_$ _7_)_,_, _"_% _/_0_%_._%_$ _&_/_2 _/_5_4_0_5_4 _!_._$ _%_2_!_3_%_$.  BIONIC
_4_(_%_. _2_%_3_0_/_._$_3 _7_)_4_( _4_(_% _-_%_3_3_!_'_%, "READY FOR INPUT" _!_4 _7_(_)_#_( _4_)_-_%
_4_(_% _)_._0_5_4 _$_%_6_)_#_% _-_!_9 _"_% _4_5_2_._%_$ _/_. (_!_. X-ON _)_3 _3_%_._4 _4_/ ASR _$_%_6_)_#_%_3
_4_/ _4_5_2_. _4_(_%_- _/_. _)_. _#_!_3_% _4_(_% ASR _$_%_6_)_#_% _)_3 _! _#_!_3_%_4_4_% _2_%_!_$_%_2).

EXAMPLE
-------

       -BIONIC(_#_2)

       BIONIC VERSION 1.0

       >READ(_#_2)

       TO FILE: TESTDATA(_#_2)

       READY FOR INPUT

       END OF TRANSMISSION
       16 RECORDS READ.
       >

A_4 _4_(_)_3 _0_/_)_._4, _!_._/_4_(_%_2 _#_/_-_-_!_._$ _-_!_9 _"_% _)_3_3_5_%_$ (N_/_4_%:  W_(_%_. _)_._0_5_4
_)_3 _&_2_/_- _._%_4_7_/_2_+ _3_5_0_0_/_2_4_%_$ HSA _$_%_6_)_#_%, _4_(_% _0_2_/_'_2_!_- _2_%_4_5_2_._3 _4_/ _4_(_%
E_8_%_#_5_4_)_6_% _#_/_-_-_!_._$ _,_%_6_%_, "-" _)_._3_4_%_!_$ _/_& BIONIC'_3 _#_/_-_-_!_._$ _$_)_3_0_!_4_#_(_%_2).

END OF TRANSMISSION
-------------------

       W_(_%_. BIONIC _0_2_)_._4_3 "READY FOR INPUT" _)_4 _"_%_'_)_._3 _! 60 _3_%_#_/_._$
_4_)_-_%_2.  I_& _._/ _#_(_!_2_!_#_4_%_2_3 _(_!_6_% _"_%_%_. _2_%_!_$ _!_&_4_%_2 60 _3_%_#_/_._$_3, BIONIC
_7_)_,_, _0_2_)_._4 "END OF TRANSMISSION" _!_._$ _2_%_4_5_2_. _4_/ _#_/_-_-_!_._$ _,_%_6_%_,.
O_._#_% _2_%_!_$_)_._' _(_!_3 _"_%_'_5_., _4_(_% _4_)_-_%_2 _)_3 _3_(_/_2_4_%_._%_$ _4_/ 14 _3_%_#_/_._$_3.  BINARY
_2_%_!_$_)_._' _5_3_%_3 _4_(_)_3 _4_)_-_%_/_5_4 _4_/ _%_._$ _4_2_!_._3_-_)_3_3_)_/_..  A_3_#_)_) _4_2_!_._3_-_)_3_3_)_/_.
_)_3 _%_._$_%_$ _7_(_%_. _%_)_4_(_%_2 (1) _! 14 _3_%_#_/_._$ _4_)_-_%_/_5_4 _/_#_#_5_2_3 _/_2 (2) _!_. EOT
(_#_/_._4_2_/_, D) _/_2 RS (_#_/_._4_2_/_,-_3_(_)_&_4 N) _)_3 _2_%_!_$.  N_/_4_% _4_(_!_4 _4_(_% EOT _#_(_!_2_!_#_4_%_2
_7_)_,_, _._/_4 _"_% _3_%_%_. _5_._4_)_, _4_(_% 14 _3_%_#_/_._$ _4_)_-_%_/_5_4 _/_#_#_5_2_3.
BIONIC ** FICHE/FRAME BREAK *****
MAKEBIONIC
NARP
SBTI
BTI

T
CON
SBIONIC
BI

T
CON
TPTSUBS
BTPT

T
RES
XDD
0;TBI
;TBTPT
;TBTI
%QSAV 3 TO 3777 ON BIONIC

240
"
FILES AND CHECKSUMS:
"
CHECKSUM
.
SBTI-BUFFERED-TERMINAL-INPUT-SUBROUTINES
SBIONIC-MAIN-PROGRAM
TPTSUBS-MISCELLANEOUS-SUBROUTINES
BIONICDOC-INFORMATION-FILE
MAKEBIONIC-COMMANDS-FILE
BIONIC-GO-FILE
.
COM T
    6;"@Ë