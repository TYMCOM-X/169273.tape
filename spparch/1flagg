1FLAGGOFF ** FICHE/FRAME BREAK *****
OFFLAG3
FLGOFF IDENT 3-29-76 REMOVE CHANGE FLAGS FROM FILES AND SETS STORAGE AFTER
*IDSC REBUILD

MAXSIZ EQU 37777B              (MAX FILE SIZE MASK IN DIR)
ENFD   EQU 3777B
LUNO   EQU 5577B
FCHB   EQU 2B7
LIBP   EQU 1B6-1

       BRU START; BRS 10

VERMSG ASC '$FLAGOFF VERSION 3.00$/'

MSG    POPD 100B5,2
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BRS 34; BRR 0

START  STA CIN; SKE =0; STA CINSW
       BRS 71; BRU ST1; CBA; SNE =6B7; BRU ST2
ST1    BRS 39; SKA =1B7; BRU *+3; MSG ERMSG; BRS 10
       RSH 2; ETR =1777B; SNE =0; BRU ST2; MSG ERMSG; BRS 10
ST2    MSG VERMSG

RESET  CLA; STA ERFLG; STA DISCSW; STA UNO
       LDA =3777B; STA LDIR
       LDA =FCHB; STA WD1MSK
       LDA =4B4; LDX =1; BRS 12
GLTCH  SKN CINSW; BRU IN1; LDA =155B; CIO =1; CIO =1
       LDA ='>'; CIO =1
IN1    CIO CIN; SNE =155B; BRU IN1; SNE =0; BRU IN1; LRSH 8
IN2    CIO CIN; SNE =155B; BRU CHK; SNE =0; BRU CHK; LRSH 8
IN3    CIO CIN; SNE =155B; BRU ERR; SNE =0; BRU ERR; LRSH 8
IN4    CIO CIN; SNE =155B; BRU IN5; SKE =0; BRU IN4
IN5    STA TERMCH; LDX =CTBL-CTBLND; CBA; SKE CTBLND,2; BRX *-1; BRU* BTBLND,2
CHK    LDX =-1; BRS 11; CLA; LSH 8; SNE ='Q'; BRU THRU
       SNE ='?'; BRU HEL; BRU ERR
ERR    LDA ERFLG; SKE =0; BRU ERR1; MIN ERFLG; MSG ERMSG (PRINT ? AND CR)
       BRU GLTCH
ERR1   MSG ERMSG1; BRU RESET
ERFLG  ZRO
ERMSG  ASC '$ ?$/'
ERMSG1 ASC '$FOR ASSISTANCE, TYPE HELP$/'

CTBL   ASC 'LEHSIDLESLIFIUQ'
CTBLND DATA 0,HEL,DISC,SELECT,FILE,THRU
BTBLND DATA ERR

CLINP  ZRO; LDX =-1; BRS 11; BRR CLINP
VERSW  DATA -1
VER    ZRO; SKN VERSW; BRR VER
       BRS 71; BRU VER1A
       CBA; SKE =6B7; BRU VER1A; SKR MGRSW; BRU *-1
       SKR STASW; BRU *-1; BRU VER1B
VER1A  LDP SFPTR; BRS 48; BRU ERV1
       LDP SFPTR; BRS 64; BRU OPFER; STA SFIL; CLA; SCP SFIL; HLT 0
VER1B  MSG VM1 (ENTER NAME)
       LDA VPTR1; STA VPTR1+1; LDA =VPTR1; LDB =155B; CLX; BRS 33
       SKN STASW; BRU *+3; MIN VERSW; BRR VER
       LDA VPTR1+1; SUB VPTR1; STA VT1
VER1   CIO SFIL; SKE =137B; BRU *+2; BRU NONAME
       SKE =155B; BRU VER1; CIT SFIL; BRU *+2; BRU NONAME
       LDA VPTR2; STA VPTR2+1; LDA =VPTR2; LDB =155B
       LDX SFIL; BRS 33; LDA VPTR2+1; SUB VPTR2
       SKG =0; BRU NONAME; SKE VT1; BRU VER1
       LDP VPTR2; SKSE VPTR1; BRU VER1; GCI VPTR1; HLT 0
       SKR VPTR1; NOP; STA VXT8; BRM FONO; SKA =40000B
       BRU *+2; BRU ERV3; SKA =1B7; BRU *+2; BRU ERV3
       SKA =1B3; BRM SETMGR
       ETR =1777B; STA DIST
       BRM FONO; STA VT1; BRM FONO; STA VT2; LDA SFIL; BRS 20
       LDX =-1; LDA =3; BRS 12; MSG VM2 (ENTER YOUR PASSWORD PLEASE)
SGPS   LDP TERMWD; STP VXT1; STP VXT3; STB VXT5
       CLA; STA TLV1; LDA VXT8; ADM VXT3
GPS    LDA =20; STA TLV11
GPS1   LDX TLV1; LDA VXT1,2; MUL =6065B
       DIV =37777761B; CBA; ADD VXT2,2; CAB; ETR =3
       STA TLV1; LDA VXT1,2; XAB; STP VXT1,2; LDA VXT5
       XMA VXT1; STA VXT5; SKR TLV11
       BRU GPS1; CIO =0; SKE =155B; BRU *+2; BRU *+3
       ADM VXT1; BRU GPS
       LDX =-1; LDA =4B4; BRS 12
       LDA VXT1; ADD VXT2; SKE VT1; BRU NOPASS; LDA VXT3; ADD VXT4
       SKE VT2; BRU NOPASS; MIN VERSW; BRR VER

FONO   ZRO; LDB =8; LDX SFIL; BRS 38; BRR FONO


NOPASS MSG VME5; LDX =-1; LDA =3; BRS 12; BRU SGPS

NONAME MSG VME6; BRS 10


TERMWD DATA 27657537B,55555555B

SFPTR  DATA 3*SF-1,3*SF+5
SF     ASC '()PSWD'
VPTR1   DATA 3*OPNAM-1,3*OPNAM-1
OPNAM  BSS 20
VPTR2   DATA 3*BUF2-1,3*BUF2-1
BUF2   BSS 20
SFBUF  BSS 20
VT1    ZRO
VT2    ZRO
VXT1   ZRO
VXT2   ZRO
VXT3   ZRO
VXT4   ZRO
VXT5   ZRO
VXT8   ZRO
SFIL   ZRO
ERV1   MSG VME1
OPFER  MSG VME3; BRS 10
ERV3   MSG VME4; BRS 10
SETMGR ZRO; SKR MGRSW; BRU *-1; BRR SETMGR
MGRSW  ZRO;*MINUS IF MANAGERS LICENSE
STASW  ZRO;*MINUS IF EXEC SET
DIST   ZRO
TLV1   ZRO
TLV11  ZRO
VM1    ASC '$YOUR NAME: /'
VM2    ASC '$YOUR PASSWORD: /'
VME5   ASC '$ERROR, TYPE PASSWORD /'
VME6   ASC '$NAME DOES NOT APPEAR IN PASSWORD FILE$/'
VME3   ASC '$PASSWORD FILE BUSY-TRY LATER$/'
VME4   ASC '$UNVALIDATED NAME/'
VME1   ASC '$NO PASSWORD FILE/'
CINSW  DATA -1
CIN    ZRO
TERMCH ZRO;*TERMINATING CHARACTER OF A COMMAND
CHFD   ZRO;*IF MINUS FILE DIRECTORY HAS NOT BEEN CHANGED
DISCSW ZRO;*IF NEGATIVE RESET STORAGE ROUTINES WILL BE RUN
LDIR   DATA 3777B;*LAST FILE DIRECTORY TO BE PROCESSED
UNO    ZRO;*FIRST FILE DIRECTORY MINUS ONE TO BE PROCESSED
HDISC  ZRO;*LAST DISC ADDRES READ HOLD FOR POSSIBLE WRITE
NPOS   ZRO;*POSITION OF CURRENT LOCATION IN LUD
T1     ZRO;*TEMPORARY STORAGE CELL
T2     ZRO;*TEMPORARY STORAGE CELL
GRP    ZRO;*CURRENT FILE DIRECTORY GROUP BEING PROCESSED

*ROUTINE TO TURN OFF ALL CHANGE FLAGS AND RESET PEAK AND ACTUAL STORAGE
*TO BE USED ONLY AFTER A DISC REBUILD

DISC   BRM VER; SKN MGRSW; BRU NOMGR; MSG MWARN (WARN HIM AND ASK IF OK)
       BRM YN; BRU RESET (HE DECIDED NOT TO GO AGEAD)
       SKR DISCSW (SET SWITCH TO DO THE STORAGE PART); BRU *-1
       BRU ST

NOMGR  MSG MGRLIC; CLA; STA MGRSW; BRU RESET
MGRLIC ASC "$MANAGER'S LICENSE IS REQUIRED$/"
MWARN  ASC '$THIS COMMAND IS TO BE USED ONLY AFTER A DISC REBUILD, AS IT'
       ASC '$RESETS ALL CHANGE FLAGS AND REINITIALIZES ALL STORAGE'
       ASC '$$DO YOU WISH TO PROCEED? /'
MWARN9 ASC '$THIS COMMAND IS TO BE USED ONLY ONCE WHEN MONITOR S38'
       ASC '$IS FIRST INSTALLED. IT ZEROES OUT THE HIGH ORDER'
       ASC '$BITS OF FILE LENGTH.'
       ASC '$DD YOU WISH TO PROCEED? /'

*ROUTINE TO TURN OFF CHANGE FLAGS ON SELECTED FILE DIRECTORIES ONLY

SELECT BRM VER; SKN MGRSW; BRU NOMGR; MSG MWARN1; BRM YN; BRU RESET
SEL1A  MSG MFDIR (FIRST FILE DIRECTORY); BRM CLBUF; BRM RONO
       SKG =3777B; SKG =0; BRU SEL3; SUB =1; STA UNO
SEL1   MSG MLDIR; BRM CLBUF; BRM RONO; SKG =3777B; SKG =0; BRU SEL4
       SNE UNO; BRU SEL2; SKG UNO; BRU SEL4
SEL2   STA LDIR; BRU ST
SEL3   MSG ERMSG; BRM CLINP; BRU SEL1A
SEL4   MSG ERMSG; BRM CLINP; BRU SEL1
MWARN1 ASC '$THIS COMMAND WILL RESET CHANGE FLAGS IN SELECTED FILE'
       ASC '$DIRECTORIES ONLY AND IS NOT THE PROPER COMMAND TO USE AFTER'
       ASC '$DISC REBUILDS'
       ASC '$$DO YOU WISH TO PROCEED? /'
MFDIR  ASC '$START WITH FILE DIRECTORY NO: /'
MLDIR  ASC '$END WITH FILE DIRECTORY NO: /'

CLBUF  ZRO; LDA =377B; CIT CIN; SKE =155B; BRU CLBUF1; CIO CIN; BRU CLBUF+1
CLBUF1 SKE ='?'; BRR CLBUF; CIO CIN; BRU RESET

RONO   ZRO; LDX CIN; LDB =8; BRS 38; BRR RONO

THRU   MSG MEOJ; BRS 10
MEOJ   ASC '$E.O.J.$/'

*YES NO ROUTINE, NO SKIP NO, SKIP YES, OTHERWISE MSG REPEATED

YN     ZRO; BRM CLBUF; CIO CIN; CAB; CIO CIN; SKE =155B; BRU *-2
       CBA; SKE ='N'; BRU YN1; BRR YN
YN1    SNE ='Y'; BRU YN2; BRM CLINP; LDA YN; SUB =2; STA YN; BRR YN
YN2    MIN YN; BRR YN

MSTART ASC '$STARTING$/'
SPECIAL BRM VER; SKN MGRSW; BRU NOMGR; MSG MWARN9; BRM YN; BRU RESET
       LDA =1777B4; STA WD1MSK
ST     MSG MSTART
ST3    MIN UNO; LDA UNO; SKG =ENFD; BRU ST4A
ST3A   SKN DISCSW; BRU RESET; BRU STORG
ST4A   SKG LDIR; BRU ST4; BRU ST3A
ST4    SKG =417B; SKG =377B; BRU *+2; BRU ST3 (NO PROCESS 400-417B)
       BRM PUN; BRU ST3

*WITH USER NO IN A, GETS F.D. (&OVERFLOW), SEARCHES FOR NEW FILES
*THEN TAKES OFF CHANGE BIT AND REWITES DIRECTORIES

PUN    ZRO
PUNA   STA GRP; BRM MGBUSY; LDA GRP; BRM GFD; LDX =FST; LDB =-1; STB CHFD
       BRU *+2
PUN1   LDA 0,2; SKG =0; BRU PUN2; LDA 1,2; SKA WD1MSK; BRM REMFLG
PUN4   EAX 3,2; EAX 1,2; SKN 0,2; BRU *-2; EAX 1,2; BRU PUN1
PUN2   SKE =0; BRU PUN2A; SKN CHFD; BRU *+2; BRU PUN2C+1 (NOT CHANGED)
       LDA =-2; STA 0,2; BRU PUN2C
PUN2A  SKE =-2; BRU PUN2B; SKN CHFD; BRU PUN2C; BRU PUN2C+1
PUN2C  BRM SFD; BRM MGUBSY; BRU PUN5
PUN2B  SKN CHFD; BRM SFD; BRM MGUBSY; LDA OVFP; SKG =LUNO; SKG =ENFD
       BRU *+2; BRU PUNA; SKE UNO; BRU PUN5 (OVERFLOW NOT FOR THIS USER)
       LDA OVFP+1; SKG =LUNO; SKG =ENFD; BRU PUN5 (INVALID)
       BRU PUNA
PUN5   LDX =-1; BRS 13; BRU *+2; BRR PUN
       CIO =0; SNE =155B; BRR PUN
       SKE ='L'; BRU PUN5A; BRM TPARA; BRR PUN
PUN5A  SKE ='S'; BRR PUN; BRM TPARA; MSG OPABT; BRS 114; BRM CLINP; BRU RESET
OPABT  ASC '$CURRENT OPERATION ABORTED$/'
FST    BSS     128
OVFP   EQU     *-4
       DATA    -1,-1,-1,-1,-1
GFD    ZRO; RSH 23; DIV =64; SKG =3; BRU GFD1
       SKG =35; BRU *+3; SUB =16; BRU GFD1
       SUB =4; XAB; SUB =100B; XAB
GFD1   STB T2; CLB; LSH 12; ADD T2; LSH 1; ADD =200B; CAB
       LDA =FST; LDX =128; STB HDISC; BRS 124; LDA FST
       LDX =FST
BADFD  BRR GFD

*WRITES FILE DIR. BACK ON DISC
SFD    ZRO; STX T1; LDA =FST; LDB HDISC; LDX =128; BRS 125; LDX T1
BFDWR  BRR SFD

*MAKE A FILE DIRECTORY GROUP BUSY

MGBUSY ZRO; LDA GRP; LDX =7; BRS 141; BRR MGBUSY

*UNBUSY A FILE DIRECTORY GROUP

MGUBSY ZRO; LDA =7776B; LDX =7; BRS 141; BRR MGUBSY

*REMOVE A CHANGE FLAG FROM A FILE DIRECTORY ENTRY

WD1MSK DATA 2B7 (BITS IN WD 1 WE WANT TO TURN OFF)
REMFLG ZRO; LDA WD1MSK; EOR =-1; ETR 1,2; STA 1,2; MIN CHFD; BRR REMFLG

TPARA  ZRO; MSG MLUNO; LDA UNO; BRM TONO; LDA =155B; CIO =1; BRR TPARA
MLUNO  ASC '$LAST FILE DIRECTORY PROCESSED WAS /'
TONO   ZRO; LDX =1; LDB =8; BRS 36; BRR TONO

MSG    OPD 100B5,2
*STORAGE PROCESSING ROUTINES

MSTGST ASC '$STORAGE PROCESSING STARTING$/'
STORG  MSG MSTGST
       BRS 113
LF1    CLA; STA SIZE; STA OFSW; BRM GLUDG; BRU SETBAS; STX NPOS; LDA 4,2
       ETR =3777B; STA FDPT
       SKG =417B; SKG =377B; BRU *+2; BRU LF1; BRM GFD
LF1A   LDA 0,2; SKG =0; BRU LF5; LDA 2,2; SKA =2B7; BRU *+2; BRU LF3+1
       STX ENTPT (SAVE STARTING LOCATION OF ENTRY)
       LDA 1,2; ETR =MAXSIZ; STA TEM; SKG =0 (CK IF LENGTH IN DIR)
       BRU *+2; BRU LF2A (YES, DO NOT CALCULATE)
       LDA 3,2; SKE =0; BRU *+2; BRU LF3 (ZERO INDEX BLOCK PTR)
       BRM CLEN; STA TEM
LF2A   LDX ENTPT; LDB =7B6; LDA 2,2; SKM =5B6; BRU *+2
       BRU LF3+1 (TEMP FILE DO NOT ADD TO TOTAL OR BASE)
       LDA TEM; ETR =MAXSIZ; ADM SIZE (ACCUMULATE STORAGE FOR THIS USER)
LF3    LDX ENTPT; EAX 3,2; EAX 1,2; SKN 0,2; BRU *-2
       EAX 1,2; SKN 0,2; BRU LF1A
       BRM TOFL (TEST FOR OVERFLOW)
       BRU LF4 (NO OVERFLOW OR NO ADDITIONAL OVERFLOW)
       BRM GFD; BRU LF1A
LF4    BRM UPBTAB; BRU LF1

UPBTAB ZRO; LDA SIZE; LDX FDPT; STA BTAB,2; BRR UPBTAB
BTAB   EQU 10000B
PTAB   EQU 14000B

LF5    SKN OFSW; BRU LF6; BRM TOFL *1 OR MORE OVERFLOW ALREADY READ
       BRU LF4 *NO ADDITONAL OVERFLOW WRITE STORAGE
       BRM GFD *THERE ARE ADDITIONAL OVERFLOWS GET NEXT
       BRU LF1A
LF6    BRM TOFL; BRU LF4 *NO OVERFLOW DIRECTORIES
       BRM GFD *HAS 1 OR MORE OVERFLOW GET NEXT
       BRU LF1A
OFSW   ZRO
*OPEN FILE AND CALCULATE LENGTH

CLEN   ZRO; STA IBP; SKG =LIBP (CK IF VALID I.B. PTR.)
       SKG =0; BRU CLEN1 (ZERO I.B.P.)
       LDX FDPT; LDB =41B; BRS 1; BRU CLEN2 (COULD NOT OPEN FILE)
       STA TFIL; RSP TFIL; LDA TFIL; BRS 20
       CLA; DIV =256*3/2; BRR CLEN
CLEN1  MSG M27; LDA IBP; BRM TONO
CLEN1A MSG M28; LDA FDPT; BRM TONO; CLA; BRR CLEN
CLEN2  CXA; SKE =1; BRU *+2; BRU CLEN3
       MSG M29; LDA IBP; BRM TONO; BRU CLEN1A
CLEN3  CLA; BRR CLEN

*TESTS FOR OVERFLOW DIR.  NO SKIP IF NONE, IF OFLOW SKIPS AND
*RETURNS GROUP NO. IN A

TOFL   ZRO; LDX =FST; LDA 125,2; SKE =0; BRU *+5
       LDA 124,2; SKE =0; BRU *+2; BRR TOFL; SKE FDPT
       BRU *+2; BRR TOFL; SKR OFSW; BRU *-1; MIN TOFL; BRR TOFL

DTWD   ZRO
DTWD1  ZRO
ENTPT  ZRO
IBP    ZRO
MO     ZRO
TFIL   ZRO

*FOLLOWING LOCATIONS CLEARED AT START UP
SIZE   ZRO

TEM    ZRO
FDPT    ZRO

GLUDG  ZRO; SKN BLNO; BRU GLUDG5
GLUDG1 BRM NBLK; BRR GLUDG
       LDX BX
GLUDG2 LDA 0,2; SKG =0; BRU *+2; BRU GLUDG4; SKE =-1
       BRU *+2; BRU GLUDG1; SKE =0; BRU *+2; BRU GLUDG1
       SKE =-2; HLT 0
GLUDG3 EAX 9,2; STX BX; BRU GLUDG2
GLUDG4 LDA 6,2; ETR =1777777B; MIN GLUDG; BRR GLUDG
GLUDG5 LDX BX; BRU GLUDG3
BLNO   DATA -1
BLX    DATA 34000B-64
BX     ZRO
EPAGE  ZRO
LBUF   EQU 34000B
NBLK   ZRO; LDA =64; ADM BLX; MIN BLNO; LDA BLNO; SKG =895; BRU *+2; BRR NBLK
       BRM GBLK; BRU *+3; MIN NBLK; BRR NBLK; LDA BLX; STA BX; MIN NBLK; BRR NBLK
GBLK   ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB
       LSH 13; ADD MTEMP; ADD =320200B; SKG EPAGE; BRR GBLK (SAME PAGE)
       CAB; ADD =37B; STA EPAGE; LDA =LBUF; LDX =4000B; BRS 124
       EAX LBUF; STX BLX; STX BX; MIN GBLK; BRR GBLK


MTEMP  ZRO
EPTR   DATA 20000B
GAN    ZRO
PTR    ZRO
PTR1   ZRO
PTR2   ZRO
PTR3   ZRO
M27    ASC '$BAD I.B.P.: /'
M28    ASC ' IN F.D. /'
M29    ASC '$UNABLE TO OPEN FILE WITH I.B.P.: /'
SETBAS LDA =1; STA UNO
SBAS1  LDA UNO; SKG =3777B; BRU *+2; BRU SBAS2; CAX; LDA BTAB,2
       CAB; BRS 190; HLT 0; MIN UNO; BRU SBAS1
SBAS2  BRM FLUSH; BRS 114; BRU RESET

RDBUF  ZRO; RSH 5; ETR =60B; ADD =120200B; CAB; LDX =2000B
       LDA =34000B; BRS 124; BRR RDBUF


FLUSH  ZRO; BRS 191; BRU *+2; BRR FLUSH; LDA =3000; BRS 81; BRU FLUSH+1

*PROCESS ONE FILE, TURN CHANGE BIT ON OR OFF AS DESIRED

FILE   LDP HFPTRS; STP FPTRS; MSG MFNAM; BRM CLBUF
FILE1  CIO CIN; SNE =155B; BRU FILE2; WCI FPTRS; BRU FILE1
FILE2  LDP FPTRS; BRS 48; BRU FILE10 (CANT FIND)
       LDX =5; LDB =FILBUF; BRS 96; LDA FILBUF; LRSH 12
       STA GRP; BRM MGBUSY; BRM GFD; LDA FILBUF+4; STA IBP
FILE3  LDA 0,2; SKG =0; BRU FILE11 (COULD NOT FIND IBP)
       LDA 3,2; SKE IBP; BRU *+2; BRU FILE4 (FOUND IT)
       EAX 3,2; EAX 1,2; SKN 0,2; BRU *-2; EAX 1,2; BRU FILE3
FILE4  STX NPOS; LDA 1,2; SKA =FCHB; BRU FILE5
       MSG MFLGO; BRM YN; BRU FILE9 (NO)
       LDX NPOS; LDA 1,2; MRG =FCHB; STA 1,2; BRU FILE6
FILE5  MSG MFGOFF; BRM YN; BRU FILE9 (NO)
       LDX NPOS; LDA 1,2; SUB =FCHB; STA 1,2
FILE6  BRM SFD
FILE9  BRM MGUBSY; BRU RESET
FILE10 MSG MNOFIL; BRU RESET
FILE11 MSG MNOFIL; BRU FILE9
MFNAM  ASC '$FILE NAME: /'
MFLGO  ASC '$TURN CHANGE FLAG ON? /'
MFGOFF ASC '$TURN CHANGE FLAG OFF? /'
MNOFIL ASC '$UNABLE LOCATE FILE$/'
FILBUF BSS 10
HFPTRS DATA 3*FILBUF-1,3*FILBUF-1
FPTRS  BSS 2


HEL    MSG MHELP; BRU RESET
MHELP  ASC '$$VALID COMMAND ARE:'
       ASC '$$DISC FIX       -    *TURNS OFF ALL CHANGE FLAGS AND RESETS'
       ASC '$                    PEAK STORAGE TO ACTUAL STORAGE - SHOULD'
       ASC '$                    ONLY BE USED AFTER A DISC REBUILD'
       ASC '$$SELECT        -     *TURNS OFF CHANGE FLAGS IN SELECTED FILE'
       ASC '$                    DIRECTORIES - DOES NOT EFFECT STORAGE'
       ASC '$$FILE          -     REVERSES THE CHANGE FLAG FOR A SELECTED'
       ASC '$                    FILE'
       ASC '$$QUIT OR Q     -     QUITS TO THE EXEC'
       ASC '$$HELP OR ?     -     REPRINTS THIS LIST'
       ASC '$$* INDICATES MANAGERS LICENSE IS REQUIRED TO USE THE COMMAND'
       ASC '$$ALL COMMANDS MAY BE SHORTENED TO THE FIRST THREE CHARACTERS$/'
       END
1FLAGGOFF ** FICHE/FRAME BREAK *****
OFFLAG3 SYMSORT

SYM.  PG.LN. IDENT.

BADFD   6 34 FLGOFF  BFDWR   6 38 FLGOFF  BLNO    9 19 FLGOFF  
BLX     9 20 FLGOFF  BTAB    8  5 FLGOFF  BTBLND  2  6 FLGOFF  BUF2    3 21 FLGOFF  
BX      9 21 FLGOFF  CHFD    4 12 FLGOFF  CHK     1 34 FLGOFF  CIN     4 10 FLGOFF  
CINSW   4  9 FLGOFF  CLBUF   5 18 FLGOFF  CLBUF1  5 19 FLGOFF  CLEN    8 18 FLGOFF  
CLEN1   8 23 FLGOFF  CLEN1A  8 24 FLGOFF  CLEN2   8 25 FLGOFF  CLEN3   8 27 FLGOFF  
CLINP   2  8 FLGOFF  CTBL    2  4 FLGOFF  CTBLND  2  5 FLGOFF  DISC    4 25 FLGOFF  
DISCSW  4 13 FLGOFF  DIST    3 38 FLGOFF  DTWD    8 36 FLGOFF  DTWD1   8 37 FLGOFF  
ENFD    1  5 FLGOFF  ENTPT   8 38 FLGOFF  EPAGE   9 22 FLGOFF  EPTR    9 33 FLGOFF  
ERFLG   1 39 FLGOFF  ERMSG   2  1 FLGOFF  ERMSG1  2  2 FLGOFF  ERR     1 36 FLGOFF  
ERR1    1 38 FLGOFF  ERV1    3 32 FLGOFF  ERV3    3 34 FLGOFF  FCHB    1  7 FLGOFF  
FDPT    9  8 FLGOFF  FILBUF 10 37 FLGOFF  FILE   10 16 FLGOFF  FILE1  10 17 FLGOFF  
FILE10 10 31 FLGOFF  FILE11 10 32 FLGOFF  FILE2  10 18 FLGOFF  FILE3  10 21 FLGOFF  
FILE4  10 24 FLGOFF  FILE5  10 27 FLGOFF  FILE6  10 29 FLGOFF  FILE9  10 30 FLGOFF  
FLUSH  10 12 FLGOFF  FONO    3  6 FLGOFF  FPTRS  10 39 FLGOFF  FST     6 25 FLGOFF  
GAN     9 34 FLGOFF  GBLK    9 26 FLGOFF  GFD     6 28 FLGOFF  GFD1    6 31 FLGOFF  
GLTCH   1 27 FLGOFF  GLUDG   9 10 FLGOFF  GLUDG1  9 11 FLGOFF  GLUDG2  9 13 FLGOFF  
GLUDG3  9 16 FLGOFF  GLUDG4  9 17 FLGOFF  GLUDG5  9 18 FLGOFF  GPS     2 34 FLGOFF  
GPS1    2 35 FLGOFF  GRP     4 20 FLGOFF  HDISC   4 16 FLGOFF  HEL    11  3 FLGOFF  
HFPTRS 10 38 FLGOFF  IBP     8 39 FLGOFF  IN1     1 29 FLGOFF  IN2     1 30 FLGOFF  
IN3     1 31 FLGOFF  IN4     1 32 FLGOFF  IN5     1 33 FLGOFF  LBUF    9 23 FLGOFF  
LDIR    4 14 FLGOFF  LF1     7 24 FLGOFF  LF1A    7 27 FLGOFF  LF2A    7 33 FLGOFF  
LF3     7 36 FLGOFF  LF4     8  2 FLGOFF  LF5     8  8 FLGOFF  LF6     8 12 FLGOFF  
LIBP    1  8 FLGOFF  LUNO    1  6 FLGOFF  M27     9 39 FLGOFF  M28    10  1 FLGOFF  
M29    10  2 FLGOFF  MAXSIZ  1  4 FLGOFF  MEOJ    5 24 FLGOFF  MFDIR   5 15 FLGOFF  
MFGOFF 10 35 FLGOFF  MFLGO  10 34 FLGOFF  MFNAM  10 33 FLGOFF  MGBUSY  7  3 FLGOFF  
MGRLIC  4 31 FLGOFF  MGRSW   3 36 FLGOFF  MGUBSY  7  7 FLGOFF  MHELP  11  4 FLGOFF  
MLDIR   5 16 FLGOFF  MLUNO   7 15 FLGOFF  MNOFIL 10 36 FLGOFF  MO      9  1 FLGOFF  
MSG     7 18 FLGOFF  MSG     1 14 FLGOFF  MSTART  5 33 FLGOFF  MSTGST  7 21 FLGOFF  
MTEMP   9 32 FLGOFF  MWARN   4 32 FLGOFF  MWARN1  5 11 FLGOFF  MWARN9  4 35 FLGOFF  
NBLK    9 24 FLGOFF  NOMGR   4 30 FLGOFF  NONAME  3 11 FLGOFF  NOPASS  3  9 FLGOFF  
NPOS    4 17 FLGOFF  OFSW    8 15 FLGOFF  OPABT   6 24 FLGOFF  OPFER   3 33 FLGOFF  
OPNAM   3 19 FLGOFF  OVFP    6 26 FLGOFF  PTAB    8  6 FLGOFF  PTR     9 35 FLGOFF  
PTR1    9 36 FLGOFF  PTR2    9 37 FLGOFF  PTR3    9 38 FLGOFF  PUN     6  7 FLGOFF  
PUN1    6 10 FLGOFF  PUN2    6 12 FLGOFF  PUN2A   6 14 FLGOFF  PUN2B   6 16 FLGOFF  
PUN2C   6 15 FLGOFF  PUN4    6 11 FLGOFF  PUN5    6 20 FLGOFF  PUN5A   6 23 FLGOFF  
PUNA    6  8 FLGOFF  RDBUF  10  8 FLGOFF  REMFLG  7 12 FLGOFF  RESET   1 23 FLGOFF  
RONO    5 21 FLGOFF  SBAS1  10  4 FLGOFF  SBAS2  10  6 FLGOFF  SEL1    5  6 FLGOFF  
SEL1A   5  4 FLGOFF  SEL2    5  8 FLGOFF  SEL3    5  9 FLGOFF  SEL4    5 10 FLGOFF  
SELECT  5  3 FLGOFF  SETBAS 10  3 FLGOFF  SETMGR  3 35 FLGOFF  SF      3 17 FLGOFF  
SFBUF   3 22 FLGOFF  SFD     6 37 FLGOFF  SFIL    3 31 FLGOFF  SFPTR   3 16 FLGOFF  
SGPS    2 32 FLGOFF  SIZE    9  5 FLGOFF  SPECIA  5 34 FLGOFF  ST      5 36 FLGOFF      1 19 FLGOFF  ST2     1 21 FLGOFF  ST3     5 37 FLGOFF  ST3A    5 38 FLGOFF  
ST4     6  1 FLGOFF  ST4A    5 39 FLGOFF  START   1 17 FLGOFF  STASW   3 37 FLGOFF  
STORG   7 22 FLGOFF  T1      4 18 FLGOFF  T2      4 19 FLGOFF  TEM     9  7 FLGOFF  
TERMCH  4 11 FLGOFF  TERMWD  3 14 FLGOFF  TFIL    9  2 FLGOFF  THRU    5 23 FLGOFF  
TLV1    3 39 FLGOFF  TLV11   4  1 FLGOFF  TOFL    8 32 FLGOFF  TONO    7 16 FLGOFF  
TPARA   7 14 FLGOFF  UNO     4 15 FLGOFF  UPBTAB  8  4 FLGOFF  VER     2 10 FLGOFF  
VER1    2 20 FLGOFF  VER1A   2 14 FLGOFF  VER1B   2 16 FLGOFF  VERMSG  1 12 FLGOFF  
VERSW   2  9 FLGOFF  VM1     4  2 FLGOFF  VM2     4  3 FLGOFF  VME1    4  8 FLGOFF  
VME3    4  6 FLGOFF  VME4    4  7 FLGOFF  VME5    4  4 FLGOFF  VME6    4  5 FLGOFF  
VPTR1   3 18 FLGOFF  VPTR2   3 20 FLGOFF  VT1     3 23 FLGOFF  VT2     3 24 FLGOFF  
VXT1    3 25 FLGOFF  VXT2    3 26 FLGOFF  VXT3    3 27 FLGOFF  VXT4    3 28 FLGOFF  
VXT5    3 29 FLGOFF  VXT8    3 30 FLGOFF  WD1MSK  7 11 FLGOFF  YN      5 28 FLGOFF  
YN1     5 30 FLGOFF  YN2     5 31 FLGOFF  
1FLAGGOFF ** FICHE/FRAME BREAK *****
MAKEFLAGOFF
NARP
OFFLAG3
BOFFLAG3

T
RES
XDD
;TBOFFLAG3
%FSAV 3 TO 3777 ON GFLAGOFF3

240
"

FILES AND CHECKSUMS

"
CHECKSUM
.
OFFLAG3-SOURCE
GFLAGOFF3-GO-FILE
MAKEFLAGOFF-COMMANDS-FILE
.
COM T
   _KW