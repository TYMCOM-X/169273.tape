1COUNT ** FICHE/FRAME BREAK *****
SCOUNT
COUNT  IDENT  4/05/76

MSG    OPD  100B5,2,0
SPACE  OPD  101B5,2
CARET  OPD 102B5,2

       BRU START; BRS 10
       ASC  'B2.04'
       ASC  'KAHRS'
       ASC %C'KY'R%

START  STP CIN; LDA TV100; STA 100B; LDA TV101; STA 101B
       LDA TV102; STA 102B; BRS 39
       SKA =5B7
       BRU BEGIN; MSG MSTAT; BRS 10
BEGIN  CARET 1
       LDX =-1; CLB; BRS 13; LDB =-1; STB SWITCH
       LDX =12; LDA =EQPT; BRS 7
       LDA =TABLE; CLX; BRS 7; LDA EQPT; CLB; SKA =4; LDB =-1; STB A2314
       SKN SWITCH; BRU ERRCHK; LDA =152B; CIT CIN; BRU *+2
       BRU ERRCHK; LDB =3*INSTR-1
       STB INPTR; STB INPTR+1; CIO CIN; WCI INPTR
LOOP   LDX =-1; BRS 13; BRU *+2; BRU ENDSTR; CIO CIN
       SKE =155B; BRU *+2; BRU ENDSTR; WCI INPTR; BRU LOOP

ENDSTR LDA =-1; STA OPTION; LDA =1; STA LOC; LDA =3*SSTR-1
       STA SPTR; STA SPTR+1
MKSTR  GCI INPTR; BRU STRSEP; SKG =0; BRU STRSEP; SKE ='   ,'
       BRU *+2; BRU STRSEP; SKE ='   -'; BRU *+2; BRU DASH
       WCI SPTR; BRU MKSTR
DASH   LDA INPTR; STA INPTR+1
STRSEP LDP NPTR; STP PTR; LDP SPTR; STP TPTR

TEST   LDP TPTR; SKSE PINFIL; BRU *+2; BRU FILES; SKSE POUTFL
       BRU *+2; BRU FILES
ADVANC GCI TPTR; BRU TABTST; STA CHAR; GCI PTR; BRU ENTER
       SKG =0; BRU *-3; SKE CHAR; BRU SKPCTR; BRU ADVANC

SKPCTR GCI PTR; BRU ENTER; SKG =0; BRU *-3; SKE ='   .'; BRU SKPCTR; MIN LOC
       BRU STRSEP+2
TABTST GCI PTR; BRU ENTER; SKG =0; BRU *-3; SKE ='   .'; BRU SKPCTR
SYSCTR LDP SPTR; LDX TOUT; BRS 35; SPACE 1; LDX LOC; LDA TABLE,2
       BRM PDNO; CARET 1
ENTER  LDA INPTR; SKE INPTR+1; BRU ENDSTR; BRU ALARM

ERRCHK LDX =1; BRM READ; BRM RAD; LDX =-20; CLA; STA GOOD,2
       BRX *-1; LDX =2; BRM READ; BRM DISC

FILES  EQU *  COUNT OPEN INPUT AND OUTPUT FILES
       LDA =TABLE; LDX =9; BRS 7 (XBP); MRG =2B7; STA LOC
       SUB =TABLE; ETR =77B; CNA; STA INDEX; LDA =GFLG
       LDX =6; BRS 7 (GFLG); ADM INST (LDA 0,2)
       LDX INDEX; LDA* LOC; SKE =-1; BRM FILE; BRX *-3
       LDA INFIL; SKG =0; BRU OUTPUT; MSG MINFIL; LDA INFIL
       BRM PDNO; CARET 1
OUTPUT LDA =TABLE; CLX; BRS 7 (READ MISC TABLES); LDA OUTFIL
       SKG =0; BRU ENDFIL; MSG MOUTFL; LDA OUTFIL; BRM PDNO
       CARET 1
ENDFIL SKN OPTION; BRU MISC; BRU ENTER

MISC   EQU *  MISCELLANEOUS MONITOR TABLES
       MIN BUFPT; CLA; SKE* BUFPT; BRU PRINT
SKIP   GCI PTR; BRU PEQPT; SKE ='   .'; BRU SKIP; BRU MISC
PRINT  GCI PTR; BRU PEQPT; SKG =0; BRU PRINT (IGNORE SPACES)
       SKE ='   .'; BRU TYPE; SPACE 1; LDA* BUFPT; BRM PDNO
       CARET 1; BRU MISC
TYPE   CIO TOUT; BRU PRINT
PEQPT  MSG MEQPT; LDA EQPT; BRM PONO; LDA ="  B"; CIO TOUT; CARET 1

ALARM  EQU *  ALARM CELL
       LDA IOPMAR; SNE =0; BRU ALRM2; MSG IOPM; LDA IOPMAR; BRM PONO
       LDA ="  B"; CIO TOUT; CARET 1
ALRM2  EQU *
       LDA =TABLE; LDX =8; BRS 7 (READ ALARM CELL); LDA TABLE
       SKA =1; MSG MREAL; LDA TABLE; SKA =2; MSG MFULL

BITMAP EQU *  TEST FOR BIT MAP SET
       BRS 143; BRU *+2; BRU END; MSG MNOSET
END    BRS 10

DISC   ZRO;*  PRINT DISC ERRORS
       SKN SWITCH; BRU *+2; MSG MDISC; LDA =-1; STA DISCSW; BRR DISC

EXPAND ZRO;*  PRINT EXPANDED RAD ADDRESS ERROR
       STA RA; BRM JONO; MSG MEQUAL; LDA RA; LRSH 14; CLB
       LSH 5; BRM JRA; LDA ='   ,'; CIO TOUT
       LDA RA; LRSH 7; ETR =177B
       BRM JRA; LDA ='   )'; CIO TOUT
       CARET 1; BRR EXPAND

FILE   ZRO;*  CLASSIFY FILE BY FILE BITS
       EXU INST; SKA =6 (WRITE OR ERASE); BRU WRITE
       MIN INFIL; BRR FILE
WRITE  MIN OUTFIL; BRR FILE

JRA    ZRO;*  PRINT JUSTIFED WRITE COUNT AND RAD ADDRESS
       LDX JRA; STX JONO; BRU JIN

J5 ZRO;* PRINT JUSTIFIED OCTAL INTEGER IN WIDTH 5
       SKG =7777B; SPACE 1; SKG =777B; SPACE 1
       SKG =77B; SPACE 1; SKG =7; SPACE 1
       BRM PONO; BRR J5

JONO   ZRO;*  PRINT JUSTIFIED DISC ADDRESS
       SKG =0; BRU PJON; SKG =7777777B; SPACE 1; SKG =777777B
       SPACE 1
JIN    SKG =77777B; SPACE 1; SKG =7777B; SPACE 1; SKG =777B; SPACE 1
       SKG =77B; SPACE 1; SKG =7B; SPACE 1
PJON   BRM PONO; BRR JONO



TCARET STA SA; STX SX; EAX* 0; CXA; ETR =37777B; CNA; CAX
       LDA =155B; CIO TOUT; BRX *-1; LDA SA; LDX SX; BRR 0

TSPACE STA SA; STX SX; EAX* 0; CXA; ETR =37777B; CNA; CAX; CLA
       CIO TOUT; BRX *-1; LDA SA; LDX SX; BRR 0
MSGOUT EAX* 0; LDA TOUT; XXA; ETR =37777B; LDB =-1; BRS 34
       BRR 0

PNTRAD EQU *  PRINT RAD ERRORS
       LDX =-20; LDA GOOD,2; SKE =0; BRU *+2; BRU ERROR+2
       STX INDEX; SKN SWITCH; BRU *+2; BRU NORMAL; STA RA; ETR =35B
       SKE =35B; BRU RADADR (RA); MSG MRC; LDA RA; ETR =37777B
       BRM PONO; SKN RA; BRU RDRAD; MSG MWRITE; BRU NXTRAD
RDRAD  MSG MREAD; BRU NXTRAD
RADADR SKR RADHDR; MSG MRAHDR; MSG MRAPR; LDA RA; BRM EXPAND
       MSG MRAOB; MIN INDEX; LDX INDEX; LDA GOOD,2; BRM EXPAND
       BRU NXTRAD+1
NORMAL BRM PONO
NXTRAD CARET 1; LDX INDEX; BRX PNTRAD+1; BRU ERROR+2

PONO   ZRO;*  PRINT OCTAL NUMBER
       LDB =8; LDX TOUT; BRS 36; BRR PONO

PJNO   ZRO;*  PRINT JUSTIFIED DECIMAL NUMBER
       SKG =9999; SPACE 1; SKG =999; SPACE 1; SKG =99; SPACE 1
       SKG =9; SPACE 1; BRM PDNO; BRR PJNO

PDNO   ZRO;*  PRINT DECIMAL NUMBER
       LDB =10; LDX TOUT; BRS 36; BRR PDNO

RAD    ZRO;*  PRINT RAD ERROR TABLES
       SKN SWITCH; BRU *+2; MSG MRAD; BRR RAD

READ   ZRO;* READ A MONITOR TABLE
       MIN READ; LDA =TABLE; BRS 7; ADD =2B7; STA LOC
       SUB =TABLE; ETR =77B; CNA; STA INDEX; AXC; MRG* LOC
       BRX *-1; SKE =0 (CHECK FOR BITS); BRU *+2; BRR READ
       EXU* READ; LDX INDEX; LDB =-1; STB PNTSW
SORT   STX INDEX; LDA* LOC; SKE =0; BRU *+2; BRU ERROR
       CAB; LDX =-20
CHKBLK SKE GOOD,2; BRU NXTDUP
CONT   LDX INDEX; BRX SORT; BRU ERROR
NXTDUP BRX CHKBLK; LDX =-20
NEXT   CLA; SKE GOOD,2; BRX *-1; STB GOOD,2; STA PNTSW
       BRU CONT
ERROR  SKN PNTSW; BRU NODUP; CARET 1; BRR READ

NODUP  SKN DISCSW; BRU PNTRAD; LDX =-20
PNTDSK LDA GOOD,2; STA DA; SKE =0; BRU *+2; BRU ERROR+2
       STX INDEX; SKN SWITCH; BRU *+2; BRU SHORT; SKN A2314
       BRU DPDISC; BRM GETINT
       SKR IBMHDR; MSG MIBM; LDA IADR; BRM JONO; BRM SPACE
       LDA DA; BRM JONO
       BRM SPACE; LDA DA; LRSH 18; ETR =7; BRM J5
       BRM SPACE; LDA DA; LRSH 10; ETR =377B; BRM J5
       BRM SPACE; LDA DA; LRSH 5; ETR =37B; BRM J5
       BRM SPACE; LDA DA; LRSH 2; ETR =7B; ADD =1
       BRM J5; BRU ROUND
DPDISC SKR DPHDR; MSG MDP; LDA DA; BRM JRA; BRM SPACE
       LDA DA; ETR =37777777B; LRSH 13; BRM JRA; BRM SPACE
       LDA DA; LRSH 5; ETR =377B; BRM JRA (LOGICAL PAIR)
       BRM SPACE; LDA DA; ETR =37B; BRM JRA
       BRU ROUND


GETINT ZRO; LDA DA; LRSH 18; ETR =7; MUL =128000
       LSH 23; STA PACK; LDA DA; LRSH 10; ETR =377B
       MUL =640; LSH 23; STA CYL; LDA DA; LRSH 5; ETR =37B
       MUL =32; LSH 23; STA HEAD; LDA DA; LRSH 2
       ETR =7; MUL =4; LSH 23; ADD CYL; ADD HEAD; ADD PACK
       STA IADR; BRR GETINT
CYL    ZRO
HEAD   ZRO
PACK   ZRO
IADR   ZRO
ROUND  LDX INDEX; CARET 1; BRX PNTDSK; BRU ERROR+2

SHORT  BRM PONO; BRU ROUND

SPACE  ZRO;*  PRINT TWO SPACES
       SPACE 1; SPACE 1; BRR SPACE

A2314  ZRO
BUFPT  DATA TABLE
CHANT  ZRO
CHAN   ZRO
CHAR   ZRO
CIN    ZRO
TOUT   ZRO
SA     ZRO
SX     ZRO
DA     ZRO
DISCSW ZRO
DPHDR  DATA 1
GFLG   BSS 50
GOOD   BES 20
HUNGSW DATA 1
IBMHDR DATA 1
INDEX  ZRO
INFIL  ZRO
INFSTR ASC  'INFIL'
INPTR  DATA 3*INSTR-1,3*INSTR-1
INSTR  BSS 30
INST   LDA 0,2
LINHDR DATA 1
LOC    ZRO
OPTION ZRO
OUTFIL ZRO
OUTSTR ASC  'OUTFIL'
PINFIL DATA 3*INFSTR-1,3*INFSTR+4
PNTSW  ZRO
POUTFL DATA 3*OUTSTR-1,3*OUTSTR+5
PTR    DATA 3*STR-1,3*ESTR-1
RA     ZRO
RADHDR DATA 1
STR    ASC  'MAPNUM.DBITS.NRTSON.NRTZAP.TFICTR.MAXUS.MINPAC.'
       ASC  'TIICTR.NPGDRT.TOICTR.ACTDLY.NU.UTIME.REAL.SETTB.'
       ASC  'WETTB.STIME.SPT.PAGES.'
       ASC  'SSWC.RPAGE.PAGEW.SDSKMT.ARMOT.SWHNG.SWTIM.EXTPU.'
       ASC  'NB112.RBER.UPSHUT.NCHRLS.NNCHRLS.B44HNG.B44TIM.'
       ASC     'NPGFLT.PGTIM.NQTPUT.'
ESTR   EQU *
NPTR   DATA 3*STR-1,3*ESTR-1
SPTR   DATA 3*SSTR-1,3*SSTR-1
SSTR   BSS 30
TPTR   BSS 2
SWITCH ZRO
IOPMAR EQU *
TABLE  BSS 50
EQPT   DATA 0
TTY    ZRO
TTYNO  ZRO
TV100  BRU MSGOUT
TV101  BRU TSPACE
TV102  BRU TCARET

IOPM ASC 'IOPMAR /'
MEQPT ASC 'EQPT /'
MDISC  ASC  'DISC ERRORS.$/'
MDP    ASC  'DISC ADR.   DISC  TRACK   SECTOR$/'
MEQUAL ASC  '=(/'
MFULL  ASC  'DISC FULL.$/'
MIBM   ASC  'INT.ADR.  EXT.ADR.   PACK   CYL.   HEAD   REC.'
       ASC '$                           (OCT)  (OCT)  (OCT)$/'
MINFIL ASC  'INFIL /'
MNOSET ASC  'BIT MAP NOT SET.$/'
MOUTFL ASC  'OUTFIL /'
MRAD   ASC  'RAD ERRORS.$/'
MRAHDR ASC  ' RAD ADR.   MONITOR   RAD    WRITE'
       ASC  '$  ERROR     ADDRESS ADDRESS  COUNT$/'
MRAOB  ASC  'OBSERVED : /'
MRAPR  ASC  'PREDICTED: /'
MRC    ASC  'RC ERROR AT: /'
MREAD  ASC  ' (READ)/'
MREAL  ASC  'REAL ABOUT TO OVERFLOW.$/'
MSTAT  ASC  '$MIN. OF PROPRIETARY STATUS REQUIRED.$/'
MWRITE ASC  ' (WRITE)/'

       FRGT ESTR

       END
1COUNT ** FICHE/FRAME BREAK *****
SCOUNT SYMSORT

SYM.  PG.LN. IDENT.

A2314   5 39 COUNT   ADVANC  1 36 COUNT   ALARM   2 31 COUNT   
ALRM2   2 34 COUNT   BEGIN   1 16 COUNT   BITMAP  2 38 COUNT   BUFPT   6  1 COUNT   
CARET   1  5 COUNT   CHAN    6  3 COUNT   CHANT   6  2 COUNT   CHAR    6  4 COUNT   
CHKBLK  4 36 COUNT   CIN     6  5 COUNT   CONT    4 37 COUNT   CYL     5 28 COUNT   
DA      6  9 COUNT   DASH    1 31 COUNT   DISC    3  3 COUNT   DISCSW  6 10 COUNT   
DPDISC  5 15 COUNT   DPHDR   6 11 COUNT   END     3  1 COUNT   ENDFIL  2 20 COUNT   
ENDSTR  1 26 COUNT   ENTER   2  5 COUNT   EQPT    7  8 COUNT   ERRCHK  2  7 COUNT   
ERROR   5  2 COUNT   ESTR    6 39 COUNT   EXPAND  3  6 COUNT   FILE    3 13 COUNT   
FILES   2 10 COUNT   GETINT  5 22 COUNT   GFLG    6 12 COUNT   GOOD    6 13 COUNT   
HEAD    5 29 COUNT   HUNGSW  6 14 COUNT   IADR    5 31 COUNT   IBMHDR  6 15 COUNT   
INDEX   6 16 COUNT   INFIL   6 17 COUNT   INFSTR  6 18 COUNT   INPTR   6 19 COUNT   
INST    6 21 COUNT   INSTR   6 20 COUNT   IOPM    7 15 COUNT   IOPMAR  7  6 COUNT   
J5      3 21 COUNT   JIN     3 29 COUNT   JONO    3 26 COUNT   JRA     3 18 COUNT   
LINHDR  6 22 COUNT   LOC     6 23 COUNT   LOOP    1 23 COUNT   MDISC   7 17 COUNT   
MDP     7 18 COUNT   MEQPT   7 16 COUNT   MEQUAL  7 19 COUNT   MFULL   7 20 COUNT   
MIBM    7 21 COUNT   MINFIL  7 23 COUNT   MISC    2 22 COUNT   MKSTR   1 28 COUNT   
MNOSET  7 24 COUNT   MOUTFL  7 25 COUNT   MRAD    7 26 COUNT   MRAHDR  7 27 COUNT   
MRAOB   7 29 COUNT   MRAPR   7 30 COUNT   MRC     7 31 COUNT   MREAD   7 32 COUNT   
MREAL   7 33 COUNT   MSG     1  3 COUNT   MSGOUT  4  1 COUNT   MSTAT   7 34 COUNT   
MWRITE  7 35 COUNT   NEXT    4 39 COUNT   NODUP   5  4 COUNT   NORMAL  4 13 COUNT   
NPTR    7  1 COUNT   NXTDUP  4 38 COUNT   NXTRAD  4 14 COUNT   OPTION  6 24 COUNT   
OUTFIL  6 25 COUNT   OUTPUT  2 17 COUNT   OUTSTR  6 26 COUNT   PACK    5 30 COUNT   
PDNO    4 23 COUNT   PEQPT   2 29 COUNT   PINFIL  6 27 COUNT   PJNO    4 19 COUNT   
PJON    3 31 COUNT   PNTDSK  5  5 COUNT   PNTRAD  4  4 COUNT   PNTSW   6 28 COUNT   
PONO    4 16 COUNT   POUTFL  6 29 COUNT   PRINT   2 25 COUNT   PTR     6 30 COUNT   
RA      6 31 COUNT   RAD     4 26 COUNT   RADADR  4 10 COUNT   RADHDR  6 32 COUNT   
RDRAD   4  9 COUNT   READ    4 29 COUNT   ROUND   5 32 COUNT   SA      6  7 COUNT   
SHORT   5 34 COUNT   SKIP    2 24 COUNT   SKPCTR  1 39 COUNT   SORT    4 34 COUNT   
SPACE   5 36 COUNT   SPACE   1  4 COUNT   SPTR    7  2 COUNT   SSTR    7  3 COUNT   
START   1 12 COUNT   STR     6 33 COUNT   STRSEP  1 32 COUNT   SWITCH  7  5 COUNT   
SX      6  8 COUNT   SYSCTR  2  3 COUNT   TABLE   7  7 COUNT   TABTST  2  2 COUNT   
TCARET  3 35 COUNT   TEST    1 34 COUNT   TOUT    6  6 COUNT   TPTR    7  4 COUNT   
TSPACE  3 38 COUNT   TTY     7  9 COUNT   TTYNO   7 10 COUNT   TV100   7 11 COUNT   
TV101   7 12 COUNT   TV102   7 13 COUNT   TYPE    2 28 COUNT   WRITE   3 16 COUNT   

1COUNT ** FICHE/FRAME BREAK *****
MAKE1COUNT
NARP
SCOUNT
BSCOUNT

T
RES
XDD
;TBSCOUNT
%FSAV 3 TO 3777 ON G1COUNT

240
"

FILES AND CHECKSUMS

"
CHECKSUM
.
SCOUNT-B204-SYMBOLICS
G1COUNT-GO-FILE
MAKE1COUNT-COMMANDS-FILE
.
COM T
 