SUMMARY ** FICHE/FRAME BREAK *****
SUPSUM8
SUPSUM IDENT   3-26-76  USE WITH EXEC 96.11 AND UP ONLY
*THIS IS THE 1SUMMARY PROGRAM
*ASSEMBLE IN NARP

MAXSIZ EQU     37777B          (MAX FILE SIZE MASK IN DIR)


       BRU START; BRS 10
       ASC 'R8.00'
       ASC 'BLOOD'
MSG    POPD    100B5,2
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BRS 34; BRR 0
MSGP   POPD    101B5,2
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX FNO; BRS 34; BRR 0

SPACE  POPD    103B5,2
       STA SAVA; STX SAVX; EAX* 0; CXA; ETR =37777B; CNA; CAX; CLA
       CIO FNO; BRX *-1; LDA SAVA; LDX SAVX; BRR 0
CR     POPD    104B5,2
       STA SAVA; STX SAVX; EAX* 0; CXA; ETR =37777B; CNA; CAX
       LDA =155B; CIO FNO; BRX *-1; LDA SAVA; LDX SAVX; BRR 0

CINSW  DATA -1
CALLER ZRO
START  STA CIN; SKG =0; STA CINSW; BRS 39; SKA =1B7
       BRU *+3; TCO ='?'; BRS 10
       LRSH 2; ETR =377B; STA UDIST; SKG =0; BRU *+2; BRM SETDSW
       LDA =8; LDB =CALLER; LDX =1; BRS 67; LDX =1; BRS 141
BEGIN  LDA =UBUF+1; LDB =200B; LDX =100B; BRS 124
       MSG M2 (OUTPUT TO); BRM CLBUF; LDA CIN; LDX =3; BRS 63; BRU *-5
       STA FNO; ADM FORMAT; MSG M1 (ALL?); BRM CLBUF; BRM YN
       BRU BEGIN1; LDA TERCH; SKE =152B; BRU PROC; BRM STYPE; BRU PROC
BEGIN1 SKR SINGSW; BRU *-1; MSG M4 (DO YOU KNOW THE GLOBAL ACCT NO?)
       BRM CLBUF; BRM YN; BRU BEGIN2; MSG M10 (ENTER THE GLOBAL ACCT NO)
       BRM CLBUF; BRM RONO; STA ACCT; BRU PROC
BEGIN2 BRM GETGAN; STA ACCT; BRU PROC
STYPE  ZRO; SKR SELSW; BRU *-1
STYPE1 MSG M9 (FREE OR BILLABLE?)
       BRM CLBUF; CIO CIN; SKE ='   F'; BRU STYPE2; BRM YN4
       SKR FSW; BRU *-1; BRR STYPE
STYPE2 SKE ='   B'; BRU STYPE3; BRM YN4; SKR BSW; BRU *-1; BRR STYPE
STYPE3 BRM YN4; BRU STYPE1
SETDSW ZRO; SKE =2; BRU *+2; BRR SETDSW; BRS 71; BRU *+2; BRR SETDSW
       LDA UDIST; STA DSW; BRR SETDSW
DSW    DATA -1
*YES NO ROUTINE EXPECTS CALL TO BE IN FOLLOWING SEQUENCE
*MSG, BRM CLBUF, BRM YN  NO SKIP REPLY NO  SKIP REPLY YES

YN     ZRO; CIO CIN; SKE ='   Y'; BRU YN1; BRM YN4
       MIN YN; BRR YN
YN1    SKE ='   N'; BRU YN3; BRM YN4; BRR YN
YN3    LDA CIN; SKE =0
       BRU YNERR; LDX MINONE; BRS 11; LDA YN; SUB =3; STA YN; BRR YN
YNERR  MSG M38; BRS 10
YN4    ZRO; CIO CIN; SKE =155B; BRU *+2; BRU *+3; SKE =152B
       BRU YN4+1; STA TERCH; BRR YN4

M38    ASC '$INCORRECT REPLY TO YES OR NO INTEROGTORY'

*PRINTS 12DIGIT NAME OR PASSWORD STRING JUSTIFIED TO 24 SPACES

PNAME  ZRO; MUL =3; LSH 23; SUB =1; STA TLV21; ADD =12; STA TLV22
PNAME1 GCI TLV21; BRU PNRET; BRM PCHAR; BRU PNAME1
PNRET  LDA =24; SUB SCNT; CNA; CAX; SPACE 1; BRX *-1; CLA; STA SCNT
       LDA SEMI; BRR PNAME
PCHAR  ZRO; SKG =77B; BRU PCHAR1; STA TLV1; LDA AMPER; CIO FNO
       MIN SCNT; LDA TLV1; ETR =77B
PCHAR1 CIO FNO; MIN SCNT; BRR PCHAR

RONO   ZRO; LDX CIN; LDB =8; BRS 38; BRR RONO
*OUTPUT A DECIMAL NUMBER TO A FILE

PDNO   ZRO; LDB =10; LDX FNO; BRS 36; BRR PDNO

*PRINT A OCTAL NUMBER

PONO   ZRO; LDB =8; LDX FNO; BRS 36; BRR PONO

AMPER  DATA 6
CIN    ZRO
FDBUF   BSS 128
DISC   ZRO
FNO    ZRO
GRP    ZRO
SAVA   ZRO
SAVX   ZRO
SCNT   ZRO
SEMI   DATA 33B
TERCH  ZRO
TLV1   ZRO
TLV21  ZRO
TLV22  ZRO
UBUF   BSS 64
SYSNO  ZRO
M1     ASC '$ALL? /'
TEMP   ZRO
CLBUF  ZRO; LDA =155B; CIT CIN; BRR CLBUF; BRU *-2
LF1    ZRO; CLA; STA OFSW; LDA UNO
       BRM GFD
LF1A   LDA 0,2; SKG =0; BRU LF5; LDA 2,2; SKA =2B7; BRU *+2; BRU LF3+1
       STX ENTPT (SAVE STARTING LOCATION OF ENTRY)
       LDA 1,2; ETR =MAXSIZ; STA TEM; SKG =0 (CK IF LENGTH IN DIR)
       BRU *+4; MIN TFILS; MIN UFIL; BRU LF2A (YES, DO NOT CALCULATE)
       LDA 3,2; SKE =0; BRU *+2; BRU LF3 (ZERO INDEX BLOCK PTR)
       BRM CLEN; STA TEM
LF2A   LDX ENTPT; LDB =7B6; LDA 2,2; SKM =5B6; BRU *+2
       BRU LF3+1 (TEMP FILE DO NOT ADD TO TOTAL)
       LDA TEM; ETR =MAXSIZ; ADM SIZET; ADM SIZE (ACCUMULATE STORAGE FOR THIS USER)
LF3    LDX ENTPT; EAX 3,2; EAX 1,2; SKN 0,2; BRU *-2
       EAX 1,2; SKN 0,2; BRU LF1A
       BRM TOFL (TEST FOR OVERFLOW)
       BRU LF4 (NO OVERFLOW OR NO ADDITIONAL OVERFLOW)
       BRM GFD; BRU LF1A
LF4    BRR LF1

LF5    SKN OFSW; BRU LF6; BRM TOFL *1 OR MORE OVERFLOW ALREADY READ
       BRU LF4 *NO ADDITONAL OVERFLOW WRITE STORAGE
       BRM GFD *THERE ARE ADDITIONAL OVERFLOWS GET NEXT
       BRU LF1A
LF6    BRM TOFL; BRR LF1 *NO OVERFLOW DIRECTORIES
       BRM GFD *HAS 1 OR MORE OVERFLOW GET NEXT
       BRU LF1A
OFSW   ZRO
*OPEN FILE AND CALCULATE LENGTH

CLEN   ZRO; STA IBP; SKG =LIBP (CK IF VALID I.B. PTR.)
       SKG =0; BRU CLEN1 (ZERO I.B.P.)
       LDB =41B; BRS 1; BRU CLEN1 (COULD NOT OPEN FILE)
       STA TFIL; RSP TFIL; LDA TFIL; BRS 20; MIN UFIL; MIN TFILS
       CLA; DIV =256*3/2; BRR CLEN
CLEN1  MIN BFIL; MIN TFILS; CLA; BRR CLEN

*READS FILE DIRECTORY INTO BUFFER AND RETURNS PTR IN X

GFD    ZRO; STA GRP; BRM RDIR; LDX =FDBUF
       BRR GFD

RDIR   ZRO; LDA GRP; LRSH 23; DIV =64; SKG =3; BRU RDIR1
       SKG =35; BRU *+3; SUB =16; BRU RDIR1
       SUB =4; XAB; SUB =100B; XAB
RDIR1  STB TEMP; CLB; LSH 12; ADD TEMP; LSH 1; ADD =200B
       CAB; STB DISC (SAVE DISC ADR)
       LDA =FDBUF; LDX =200B; BRS 124; BRR RDIR

*TESTS FOR OVERFLOW DIR.  NO SKIP IF NONE, IF OFLOW SKIPS AND
*RETURNS GROUP NO. IN A

TOFL   ZRO; LDX =FDBUF; LDA 125,2; SKE =0; BRU *+5
       LDA 124,2; SKE =0; BRU *+2; BRR TOFL; SKE UNO
       BRU *+2; BRR TOFL; SKR OFSW; BRU *-1; MIN TOFL; BRR TOFL



UDIST  ZRO
BFIL   ZRO
ACSIZE ZRO
IBP    ZRO
MINONE DATA 77777777B
SIZE   ZRO
TEM    ZRO
UNO    ZRO
TFIL   ZRO
M6     ASC '$GLOBAL ACCOUNT NO: /'
ENTPT  ZRO
LIBP   EQU 1B6-1

NONE   MSG NONMSG; BRU TOT1
NONMSG ASC '$NO USERS FOUND/'
PROC   SKN SINGSW; BRU PROALL; BRM ITEM; BRM GETACT; BRU PROALL+2
PROALL BRM ITEM; BRM MFDT; SKN HITSW; BRU NONE
       BRM PROC1B; LDA FDPTR1; SUB =1
       STA EPTR; SKE =24000B; BRM SORTG; BRM SETPT
PROA1  BRM GACCT; BRU TOTALS; LDA HACCT; SKN FSW; BRU *+2; BRU CFREE
       SKN BSW; BRU *+2; BRU CBILL
PROA2  ETR =1777777B; STA ACCT; SKE PACCT; BRM ACCTOT; BRM PROACT
       BRU PROA1
CFREE  SKN HACCT; BRU PROA1; BRU PROA2
CBILL  SKN HACCT; BRU PROA2; BRU PROA1
SETPT  ZRO; LDA =FDTAB1; STA PTR; ADD =4000B; STA PTR1; BRR SETPT
GACCT  ZRO; LDA PTR; SKG EPTR; BRU *+2; BRR GACCT
       LDA* PTR; STA HACCT; LDA* PTR1; STA HFDPT; MIN PTR; MIN PTR1
       MIN GACCT; BRR GACCT
ITEM   ZRO; MSG M5 (ITEMIZE BY USER)
       BRM CLBUF; BRM YN; BRR ITEM; SKR ITEMSW; BRU *-1; BRR ITEM
PROC1B ZRO; MSGP H2 (SYSTEM NO)
       LDA =SYSNO; LDB =3; LDX FNO; BRS 34
       CR 1; BRM DATE; BRR PROC1B
ACCTOT ZRO; LDA PACCT; SKG =0; BRR ACCTOT
       MSGP M3 (TOTAL)
       LDA ACSIZE; BRS 51; STP POINT; LDA =256*3; BRS 51; FMP POINT
       LDX FORMAT; BRS 53
       LDA AFIL; SKG =0; BRU *+4; MSGP M7 (TOTAL FILES)
       LDA AFIL; BRM PDNO; LDA BFIL; SKG =0
       BRU *+4; MSGP M8 (FILES NOT CALCULATED)
       LDA BFIL; BRM PDNO; CR 1
       CLA; STA ACSIZE; STA AFIL; STA BFIL; BRR ACCTOT
PROACT ZRO; LDA PACCT; SKE ACCT; BRM ACCTHD; LDA HFDPT
       SKN ITEMSW; BRU *+2; BRM PRONAM; ETR =3777B; STA UNO; BRM LF1
       LDA UFIL; ADM AFIL; LDA SIZE; ADM ACSIZE; SKN ITEMSW
       BRU PROAC1; LDA SIZE; BRS 51; STP POINT; LDA =256*3
       BRS 51; FMP POINT; LDX FORMAT; BRS 53; SPACE 1
       LDA UFIL; BRM SPTST; BRM PDNO; SPACE 1; LDA DIST; BRM SPTST
       BRM PDNO; CR 1
PROAC1 CLA; STA UFIL; STA SIZE; LDA ACCT; STA PACCT; BRR PROACT
ACCTHD ZRO; MSGP M6; LDA ACCT; ETR =1777777B; BRM PONO
       CR 1; SKN ITEMSW; BRR ACCTHD; MSGP H1; CR 1; BRR ACCTHD

PRONAM ZRO; LRSH 12; STA BLNO
PRON1  BRM GE1
PRON2  LDA 0,2; SKE =0; BRU *+2; HLT; SKE =-1; BRU *+2
       BRU PRON4; SKE =-2; BRU *+2; BRU PRON3; LDA 4,2; SKG =0
       HLT; LDB =3777B; SKM HFDPT; BRU PRON3; LDA 8,2; LRSH 2
       ETR =377B; STA DIST; CXA; BRM PNAME
       LDA HFDPT; BRR PRONAM
PRON3  EAX 9,2; BRU PRON2
PRON4  MIN BLNO; BRU PRON1
GETACT ZRO; SKR NUMSW; BRU *-1; BRM MFDT; BRR GETACT
TOTALS BRM ACCTOT; SKN SINGSW; BRU *+2; BRU TOT1; MSGP M12
       BRM TTOT; MSGP M7; LDA TFILS; BRM PDNO
TOT1   CR 2; MSG EOJ; BRS 10
EOJ    ASC '$END OF JOB$/'
TTOT   ZRO 0; LDA SIZET; BRS 51; STP POINT
       LDA =256*3; BRS 51; FMP POINT; LDX FORMAT; BRS 53; BRR TTOT
FORMAT DATA 30012000B
POINT  BSS 2
       ZRO 0
SIZET  ZRO 0
TFILS  ZRO 0
M12    ASC '$$TOTAL SELECTED ACCOUNT STORAGE    /'
M2     ASC '$OUTPUT TO: /'
M3     ASC '$TOTAL  /'
M4     ASC '$DO YOU KNOW THE GLOBAL ACCT. NO.? /'
ITEMSW ZRO
M5     ASC '$ITEMIZED BY USER? /'
AFIL   ZRO
UFIL   ZRO
M7     ASC '$TOTAL FILES /'
M8     ASC '$FILES NOT CALCULATED /'
M9     ASC '$FREE OR BILLABLE? /'
M10    ASC '$ENTER THE GLOBAL ACCT. NO.: /'
SPTST  ZRO
       STA TA
       SKG =999999; SPACE 1; SKG =99999; SPACE 1; SKG =9999; SPACE 1
       SKG =999; SPACE 1; SKG =99; SPACE 1; SKG =9; SPACE 1
       LDA TA; BRR SPTST
TA     ZRO
H1     ASC 'USER                         CHARS.   FILES   DIST/'
DATE   ZRO; BRS 42; STX YR; XAB; ETR =@74B6; STA TLV1; CLA
       DIV =1800; ADD TLV1
       STA TLV11; LRSH 16; ADD =1
       CR 1; BRM PDNO; LDA SLASH; CIO FNO
       LDA TLV11; ETR =177777B; RSH 23; DIV =1440; STB TLV11; ADD =1
       BRM PDNO; LDA SLASH; CIO FNO; LDA YR; ADD =1964; BRM PDNO
       SPACE 2; LDA TLV11; RSH 23; DIV =60; STB TLV11
       BRM PDNO; LDA COLON; CIO FNO; LDA TLV11; SKG =9; BRM ZERO
       BRM PDNO; CR 1; BRR DATE
ZERO   ZRO; STA SAVA; LDA =20B; CIO FNO; LDA SAVA; BRR ZERO
COLON  DATA 32B
H2     ASC '$SYSTEM NO. /'
SLASH  DATA 17B
TLV11  ZRO
*MAKES TABLE OF FD POINTERS USED 

MFDT  ZRO; LDA 34000B; LDA 30000B; LDA 24000B (GRAB PAGES)
       CLA; STA BLNO; BRM RPAGE
MFDT1 LDA BLNO; SKG =895; BRU *+2; BRR MFDT; CLB; LSH 12; STA HBLNO
       LRSH 35; DIV =128; STB MTEMP; CLB; LSH 13; ADD =320200B
       ADD MTEMP; SKG EPAGE; BRU *+2; BRM RPAGE; LDX BLX
MFDT2 LDA 0,2; SKE =0; BRU *+2; BRU NBLK; SKE =-1; BRU *+2
       BRU NBLK; SKE =-2; BRU *+2; BRU MFDT3
       SKN DSW; BRM CKDIST
       LDA 4,2 (FD POINTER)
       SKG =0; HLT
       SKN NUMSW; BRU *+2; BRM CKACCT
       SKR HITSW; BRU *-1
       MRG HBLNO; STA* FDPTR; MIN FDPTR
       LDA 6,2; STA* FDPTR1; MIN FDPTR1
MFDT3  EAX 9,2; BRU MFDT2
NBLK   LDA =64; ADM BLX; MIN BLNO; BRU MFDT1

CKDIST ZRO; LDA 8,2; LRSH 2; ETR =377B; SKE UDIST; BRU MFDT3; BRR CKDIST

RPAGE  ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB; LSH 13
       ADD MTEMP; ADD =320200B; CAB; LDA =LBUF
       LDX =4000B; BRS 124; CBA; ADD =37B; STA EPAGE; EAX LBUF
       STX BLX; BRR RPAGE
CKACCT ZRO; LDA 6,2; LDB =1777777B; SKM ACCT; BRU MFDT3
       LDA 4,2; BRR CKACCT
HITSW  ZRO
EPAGE  ZRO
BLX    ZRO
BLNO   ZRO
MTEMP  ZRO
NUMSW  ZRO
HBLNO  ZRO
LBUF   EQU 34000B
FDTAB  EQU 30000B
FDTAB1 EQU 24000B
FDPTR  DATA FDTAB
FDPTR1 DATA FDTAB1
GETGAN ZRO; SKN CINSW; MSG M49
GETG7  BRM GTNS1
       BRU EGETG2 (ERROR NAME STRING)
       LDA =NAMBF1; STA LPOINT; BRM GENTRY; BRU EGETG3 (NAME NOT IN LUD)
       LDA 6,2; ETR =1777777B; BRR GETGAN

EGETG2 MSG ERR1; LDA CIN; SKE =0; BRU ENU1A; BRM CLBUF1; LDX =-1
       BRS 11; BRU GETG7
EGETG3 MSG ERR7; LDA CIN; SKE =0; BRU ENU1A; BRU EGETG2

GENTRY ZRO; LDA LPOINT; MUL =3; LSH 23; SUB =1; CAB; ADD =12; XAB
       STP GEPTR; CLA; LDX =-4; ADD* LPOINT; MIN LPOINT; BRX *-2
       LRSH 23; DIV =887; STB BLNO; CBA; SKG =895; BRU *+2; HLT
       BRM GE1
GE2    STX BLX; LDA 0,2; SKE =0; BRU *+3; LDX =1; BRR GENTRY (NOT IN LUD)
       SKE =-1; BRU *+3; MIN BLNO; BRU GE2-1; SKE =-2; BRU *+2
       BRU GE3; CXA; MUL =3; LSH 23; SUB =1; CAB; ADD =12; XAB
       SKSE GEPTR; BRU GE3; LDX BLX; MIN GENTRY; BRR GENTRY
GE3    LDX BLX; EAX 9,2; BRU GE2

GE1    ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB; LSH 13
       ADD =320200B; ADD MTEMP; CAB; LDA =UBUF; LDX =100B; BRS 124
       EAX UBUF; BRR GE1
ERR1   ASC '$ERROR: TYPE USER NAME /'
ERR7   ASC '$NAME NOT IN LUD/'
M49    ASC '$ENTER A USER NAME BELONGING TO THIS'
       ASC '$GLOBAL ACCT. /'
GTNS1  ZRO; BRM CLBUF; LDA NAMPTA; STA NAMPT; STA NAMPT1; BRM GUNAM
       BRR GTNS1; MIN GTNS1; BRR GTNS1
GUNAM  ZRO; CLA; STA NAMCNT
GUNAM1 CIO CIN; SKE =155B; BRU *+2; BRU GUNAM4
       SKE =141B; BRU GUNAM2; GCD NAMPT; NOP; TCO =77B
       LDA NAMCNT; SKG =0; BRU GUNAM1; SUB =1; STA NAMCNT; BRU GUNAM1
GUNAM2 SKG =77B (IGNORE CONTROL LETTERS); BRU *+2; BRU GUNAM1
       STA NAMTEM; LDA NAMCNT; SKE =12; BRU *+2; BRU GUNAM3
       LDA NAMTEM; WCI NAMPT; MIN NAMCNT; BRU GUNAM1
GUNAM3 CIO CIN; SKE =155B; BRU *-2; LDX =2; BRR GUNAM
GUNAM4 LDA NAMCNT; SKE =0; BRU *+3; LDX =1; BRR GUNAM
       MIN GUNAM; BRR GUNAM

NAMCNT ZRO
LPOINT  ZRO
NAMTEM ZRO
NAMBF1 BSS 4
NAMPTA DATA 3*NAMBF1-1
NAMPT  ZRO
NAMPT1 ZRO
GEPTR  BSS 2
CLBUF1 ZRO; LDX =-4; CLA; STA NAMBF1+4,2; BRX *-1; BRR CLBUF1
SORTG  ZRO; LDA FDTAB1; SKE =0; BRU *+2; BRR SORTG
       LDA =FDTAB1; STA PTR; ADD =1; STA PTR1
SORTG1  LDA* PTR1; ETR =1777777B; STA TACCT; LDA* PTR
       ETR =1777777B; SKG TACCT; BRU *+3; BRM EXCH
       BRU SORTG1; MIN PTR1; LDA PTR1; SKG EPTR; BRU SORTG1
       MIN PTR; LDA PTR; SKE EPTR; BRU *+2; BRR SORTG; ADD =1
       STA PTR1; BRU SORTG1
EXCH   ZRO; LDA PTR; ADD =4000B; STA PTR2; LDA PTR1; ADD =4000B
       STA PTR3; LDA* PTR; XMA* PTR1; STA* PTR; LDA* PTR2
       XMA* PTR3; STA* PTR2; BRR EXCH
PTR    ZRO
PTR1   ZRO
PTR2   ZRO
PTR3   ZRO
TACCT  ZRO
EPTR   ZRO
BSW    ZRO
FSW    ZRO
SINGSW ZRO
ACCT   ZRO
HACCT  ZRO
HFDPT  ZRO
PACCT  ZRO
SELSW  ZRO
ENU1A  MSG ERR3; BRS 10
ERR3   ASC '$ERROR IN COMMANDS FILE/'
YR     ZRO
DIST   ZRO
       END
SUMMARY ** FICHE/FRAME BREAK *****
SUPSUM8 SYMSORT

SYM.  PG.LN. IDENT.

ACCT   10 18 SUPSUM  ACCTHD  6 10 SUPSUM  ACCTOT  5 32 SUPSUM  
ACSIZE  4 39 SUPSUM  AFIL    7  1 SUPSUM  AMPER   3  1 SUPSUM  BEGIN   1 29 SUPSUM  
BEGIN1  1 33 SUPSUM  BEGIN2  1 36 SUPSUM  BFIL    4 38 SUPSUM  BLNO    8 19 SUPSUM  
BLX     8 18 SUPSUM  BSW    10 15 SUPSUM  CALLER  1 24 SUPSUM  CBILL   5 22 SUPSUM  
CFREE   5 21 SUPSUM  CIN     3  2 SUPSUM  CINSW   1 23 SUPSUM  CKACCT  8 14 SUPSUM  
CKDIST  8  8 SUPSUM  CLBUF   3 19 SUPSUM  CLBUF1  9 37 SUPSUM  CLEN    4  9 SUPSUM  
CLEN1   4 14 SUPSUM  COLON   7 24 SUPSUM  CR      1 19 SUPSUM  DATE    7 14 SUPSUM  
DISC    3  4 SUPSUM  DIST   10 26 SUPSUM  DSW     2  6 SUPSUM  EGETG2  8 34 SUPSUM  
EGETG3  8 36 SUPSUM  ENTPT   5  8 SUPSUM  ENU1A  10 23 SUPSUM  EOJ     6 26 SUPSUM  
EPAGE   8 17 SUPSUM  EPTR   10 14 SUPSUM  ERR1    9 12 SUPSUM  ERR3   10 24 SUPSUM  
ERR7    9 13 SUPSUM  EXCH   10  6 SUPSUM  FDBUF   3  3 SUPSUM  FDPTR   8 26 SUPSUM  
FDPTR1  8 27 SUPSUM  FDTAB   8 24 SUPSUM  FDTAB1  8 25 SUPSUM  FNO     3  5 SUPSUM  
FORMAT  6 29 SUPSUM  FSW    10 16 SUPSUM  GACCT   5 24 SUPSUM  GE1     9  9 SUPSUM  
GE2     9  3 SUPSUM  GE3     9  7 SUPSUM  GENTRY  8 38 SUPSUM  GEPTR   9 36 SUPSUM  
GETACT  6 22 SUPSUM  GETG7   8 29 SUPSUM  GETGAN  8 28 SUPSUM  GFD     4 18 SUPSUM  
GRP     3  6 SUPSUM  GTNS1   9 16 SUPSUM  GUNAM   9 18 SUPSUM  GUNAM1  9 19 SUPSUM  
GUNAM2  9 22 SUPSUM  GUNAM3  9 25 SUPSUM  GUNAM4  9 26 SUPSUM  H1      7 13 SUPSUM  
H2      7 25 SUPSUM  HACCT  10 19 SUPSUM  HBLNO   8 22 SUPSUM  HFDPT  10 20 SUPSUM  
HITSW   8 16 SUPSUM  IBP     5  1 SUPSUM  ITEM    5 27 SUPSUM  ITEMSW  6 38 SUPSUM  
LBUF    8 23 SUPSUM  LF1     3 20 SUPSUM  LF1A    3 22 SUPSUM  LF2A    3 28 SUPSUM  
LF3     3 31 SUPSUM  LF4     3 36 SUPSUM  LF5     3 38 SUPSUM  LF6     4  3 SUPSUM  
LIBP    5  9 SUPSUM  LPOINT  9 30 SUPSUM  M1      3 17 SUPSUM  M10     7  6 SUPSUM  
M12     6 34 SUPSUM  M2      6 35 SUPSUM  M3      6 36 SUPSUM  M38     2 19 SUPSUM  
M4      6 37 SUPSUM  M49     9 14 SUPSUM  M5      6 39 SUPSUM  M6      5  7 SUPSUM  
M7      7  3 SUPSUM  M8      7  4 SUPSUM  M9      7  5 SUPSUM  MAXSIZ  1  5 SUPSUM  
MFDT    7 30 SUPSUM  MFDT1   7 32 SUPSUM  MFDT2   7 35 SUPSUM  MFDT3   8  5 SUPSUM  
MINONE  5  2 SUPSUM  MSG     1 11 SUPSUM  MSGP    1 13 SUPSUM  MTEMP   8 20 SUPSUM  
NAMBF1  9 32 SUPSUM  NAMCNT  9 29 SUPSUM  NAMPT   9 34 SUPSUM  NAMPT1  9 35 SUPSUM  
NAMPTA  9 33 SUPSUM  NAMTEM  9 31 SUPSUM  NBLK    8  6 SUPSUM  NONE    5 11 SUPSUM  
NONMSG  5 12 SUPSUM  NUMSW   8 21 SUPSUM  OFSW    4  6 SUPSUM  PACCT  10 21 SUPSUM  
PCHAR   2 27 SUPSUM  PCHAR1  2 29 SUPSUM  PDNO    2 34 SUPSUM  PNAME   2 23 SUPSUM  
PNAME1  2 24 SUPSUM  PNRET   2 25 SUPSUM  POINT   6 30 SUPSUM  PONO    2 38 SUPSUM  
PROA1   5 17 SUPSUM  PROA2   5 19 SUPSUM  PROAC1  6  9 SUPSUM  PROACT  6  2 SUPSUM  
PROALL  5 14 SUPSUM  PROC    5 13 SUPSUM  PROC1B  5 29 SUPSUM  PRON1   6 14 SUPSUM  
PRON2   6 15 SUPSUM  PRON3   6 20 SUPSUM  PRON4   6 21 SUPSUM  PRONAM  6 13 SUPSUM  
PTR    10  9 SUPSUM  PTR1   10 10 SUPSUM  PTR2   10 11 SUPSUM  PTR3   10 12 SUPSUM  
RDIR    4 21 SUPSUM  RDIR1   4 24 SUPSUM  RONO    2 31 SUPSUM  RPAGE   8 10 SUPSUM  
SAVA    3  7 SUPSUM  SAVX    3  8 SUPSUM  SCNT    3  9 SUPSUM  SELSW  10 22 SUPSUM  
SEMI    3 10 SUPSUM  SETDSW  2  4 SUPSUM  SETPT   5 23 SUPSUM  SINGSW 10 17 SUPSUM  
SIZE    5  3 SUPSUM  SIZET   6 32 SUPSUM  SLASH   7 26 SUPSUM  SORTG   9 38 SUPSUM  
SORTG1 10  1 SUPSUM  SPACE   1 16 SUPSUM  SPTST   7  7 SUPSUM  START   1 25 SUPSUM  
STYPE   1 37 SUPSUM  STYPE1  1 38 SUPSUM  STYPE2  2  2 SUPSUM  STYPE3  2  3 SUPSUM  
SYSNO   3 16 SUPSUM  TA      7 12 SUPSUM  TACCT  10 13 SUPSUM  TEM     5  4 SUPSUM  
TEMP    3 18 SUPSUM  TERCH   3 11 SUPSUM  TFIL    5  6 SUPSUM  TFILS   6 33 SUPSUM  
TLV1    3 12 SUPSUM  TLV11   7 27 SUPSUM  TLV21   3 13 SUPSUM  TLV22   3 14 SUPSUM  
TOFL    4 31 SUPSUM  TOT1    6 25 SUPSUM  TOTALS  6 23 SUPSUM  TTOT    6 27 SUPSUM  
UBUF    3 15 SUPSUM  UDIST   4 37 SUPSUM  UFIL    7  2 SUPSUM  UNO     5  5 SUPSUM  
YN      2 10 SUPSUM  YN1     2 12 SUPSUM  YN3     2 13 SUPSUM  YN4     2 16 SUPSUM  
YNERR   2 15 SUPSUM  YR     10 25 SUPSUM  ZERO    7 23 SUPSUM  
SUMMARY ** FICHE/FRAME BREAK *****
SUPVRSUMMARY8
SUMSUP IDENT   3-26-76 USE WITH EXEC 96.11 AND UP ONLY
*THIS IS THE "SUMMARY" PROGRAM AS DOCUMENTED IN THE EXECUTIVE MANUAL.
*ASSEMBLE IN NARP

*SUPERVISOR SYSTEMS ONLY

MAXSIZ EQU     37777B          (MAX FILE SIZE MASK IN DIR)
RCS    OPD     525B5,2


       BRU START; BRS 10
       ASC 'S8.00'
       ASC 'BLOOD'
MSG    POPD    100B5,2
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BR BRR 0
MSGP   POPD    101B5,2
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX TOUT; BRS 34; BRR 0

SPACE  POPD    103B5,2
       STA SAVA; STX SAVX; EAX* 0; CXA; ETR =37777B; CNA; CAX; CLA
       CIO TOUT; BRX *-1; LDA SAVA; LDX SAVX; BRR 0
CR     POPD    104B5,2
       STA SAVA; STX SAVX; EAX* 0; CXA; ETR =37777B; CNA; CAX
       LDA =155B; CIO TOUT; BRX *-1; LDA SAVA; LDX SAVX; BRR 0

CINSW  DATA -1
START  STP CIN; SKG =0; STA CINSW; CBA; ADM FORMAT
       LDA =8; LDB =UNO; LDX =1; BRS 67; STA SUNO
       BRS 39; SKA =2B6; BRU SUPVR
USER   BRM DATE; BRM LF1
       MSGP M1 (TOTAL STORAGE)
       LDA SIZE; BRM PSTORG; CR 1; MSGP M2 (NO. OF FILES:)
       LDA UFIL; BRM PFILES; CR 1; BRS 10
PSTORG ZRO; BRS 51; STP POINT; LDA =256*3; BRS 51; FMP POINT; LDX FORMAT
       BRS 53; BRR PSTORG
PFILES ZRO; BRS 51; LDX FORMAT; BRS 53; BRR PFILES
*YES NO ROUTINE EXPECTS CALL TO BE IN FOLLOWING SEQUENCE
*MSG, BRM CLBUF, BRM YN  NO SKIP REPLY NO  SKIP REPLY YES

YN     ZRO; CIO CIN; SKE ='   Y'; BRU YN1; BRM YN4
       MIN YN; BRR YN
YN1    SKE ='   N'; BRU YN3; BRM YN4; BRR YN
YN3    LDA CIN; SKE =0
       BRU YNERR; LDX MINONE; BRS 11; LDA YN; SUB =3; STA YN; BRR YN
YNERR  MSG M38; BRS 10
YN4    ZRO; CIO CIN; SKE =155B; BRU *+2; BRU *+3; SKE =152B
       BRU YN4+1; STA TERCH; BRR YN4

M38    ASC '$INCORRECT REPLY TO YES OR NO INTEROGTORY'

SUPVR  LDA =31; LDB =ACCT; LDX =1; BRS 67
       SKN CINSW; MSG M3 (YOURS ONLY)
       BRM CLBUF; BRM YN; BRU *+4; BRM DATE; LDA SUNO; BRU USER+1
       SKN CINSW; MSG M5 (ITEMIZE BY USER)
       BRM CLBUF; BRM YN; BRU ACTSTO; SKR ITEMSW; BRU *-1
ACTSTO SKN CINSW; MSG M4 (USER NAMES)
       BRM CLBUF; LDA =377B; CIT CIN; BRU *+2; HLT
       SKE ='   .'; BRU FTEL; CIO CIN; LDA FILPT; STA FILPT1
ACTS1  CIO CIN; SKE =155B; BRU *+2; BRU ACTS3; SKE =152B
       BRU *+2; BRU ACTS3; SKE =141B; BRU ACTS2; TCO =77B
       LDA FILPT; SKE FILPT1; BRU *+2; BRU ACTS1
       LDA =-1; ADM FILPT1; BRU ACTS1
ACTS2  WCI FILPT; BRU ACTS1
ACTS3  STA TERCH; LDP FILPT; BRS 64; BRU ACTS4; STA FNI
       CBA; SKE =3; BRU *+2; BRU ACTS5; MSG ERR2 (FILE NOT SYMBOLIC)
       SKN CINSW; BRU *+2; BRU ENU1A; BRU ACTSTO
ACTS4  MSG ERR4 (CANNOT OPEN FILES)
       SKN CINSW; BRU ACTSTO; BRU ENU1A
ACTS5  SKR FILSW; BRU *-1
ACTS6  BRM CLBUF1; LDA NAMPTA; STA NAMPT; STA NAMPT1; SKN FNI
       BRU *+2; BRU ACTS7; BRM GUNAM; NOP 0 *(ERROR RET)
       LDX =-4; LDA NAMBF1+4,2; STA* LBUFPT; MIN LBUFPT; BRX *-3
       BRU ACTS6
ACTS7  LDA FNI; BRS 20; CLA; STA FILSW; LDA TERCH; SKE =152B
       BRU ACTS8; BRU ACTSTO+2

FTEL   BRM GTNS1; BRU EFTEL (ERROR IN NAME STRING)
       LDX =-4; LDA NAMBF1+4,2; STA* LBUFPT; MIN LBUFPT; BRX *-3
       LDA TERCH1; SKE =155B; BRU ACTSTO+2
ACTS8  LDA LBUFPT; SUB =1; STA EPTR
       LDA =31; LDB =ACCT; LDX =1; BRS 67
       BRM DATE; SKN ITEMSW; BRU TOTACT; MSGP H1; CR 1
       LDA =LBUF; STA LBUFPT
ACTS9  BRM GENTRY; BRU ERROR (NOT IN LUD)
       LDA 6,2; ETR =1777777B; SKE ACCT; BRU ERROR (NOT IN ACCT)
       LDA 4,2; STA UNO; SKN TOTSW; BRU *+3
       LDA LBUFPT; BRM PNAME; BRM LF1
       LDA UFIL; ADM AFIL; LDA SIZE; ADM ACSIZE
       SKN TOTSW; BRU ACTS9A; BRM PSTORG
       LDA UFIL; BRM PFILES; CR 1
ACTS9A CLA; STA SIZE; STA UFIL
       LDA LBUFPT; ADD =4; STA LBUFPT; SKG EPTR; BRU ACTS9
       CR 2; MSGP M12; LDA ACSIZE; BRM PSTORG; CR 1; MSGP M2
       LDA AFIL; BRM PFILES; CR 1; BRS 10
TOTACT STA TOTSW; BRU ACTS9-2

EFTEL  MSG ERR1; SKN CINSW; BRU FTEL; BRU ENU1A
ERROR  LDA LBUFPT; BRM PNAME; MSGP ERR7; CR 1; BRU ACTS9A+3
*PRINTS 12 DIGIT NAME STRING JUSTIFIED TO 18 SPACES

PNAME  ZRO; MUL =3; LSH 23; SUB =1; STA TLV21; ADD =12; STA TLV22
PNAME1 GCI TLV21; BRU PNRET; BRM PCHAR; BRU PNAME1
PNRET  LDA =18; SUB SCNT; CNA; CAX; SPACE 1; BRX *-1; CLA; STA SCNT
       BRR PNAME
PCHAR  ZRO; SKG =77B; BRU PCHAR1; STA TLV1; LDA AMPER; CIO TOUT
       MIN SCNT; LDA TLV1; ETR =77B
PCHAR1 CIO TOUT; MIN SCNT; BRR PCHAR

*OUTPUT A DECIMAL NUMBER TO A FILE

PDNO   ZRO; LDB =10; LDX TOUT; BRS 36; BRR PDNO

AMPER  DATA 6
CIN    ZRO
TOUT   ZRO
FDBUF   BSS 128
DISC   ZRO
GRP    ZRO
SAVA   ZRO
SAVX   ZRO
SCNT   ZRO
TERCH  ZRO
TERCH1 ZRO
TLV1   ZRO
TLV21  ZRO
TLV22  ZRO
UBUF   BSS 64
M1     ASC '$TOTAL STORAGE: /'
TEMP   ZRO
CLBUF  ZRO; LDA =155B; CIT CIN; BRR CLBUF; BRU *-2
LF1    ZRO; CLA; STA OFSW; LDA UNO
       BRM GFD
LF1A   LDA 0,2; SKG =0; BRU LF5; LDA 2,2; SKA =2B7; BRU *+2; BRU LF3+1
       STX ENTPT (SAVE STARTING LOCATION OF ENTRY)
       LDA 1,2; ETR =MAXSIZ; STA TEM; SKG =0 (CK IF LENGTH IN DIR)
       BRU *+3; MIN UFIL; BRU LF2A (YES, DO NOT CALCULATE)
       LDA 3,2; SKE =0; BRU *+2; BRU LF3 (ZERO INDEX BLOCK PTR)
       BRM CLEN; STA TEM
LF2A   LDX ENTPT; LDB =7B6; LDA 2,2; SKM =5B6; BRU *+2
       BRU LF3+1 (TEMP FILE DO NOT ADD TO TOTAL)
       LDA TEM; ETR =MAXSIZ; ADM SIZE (ACCUMULATE STORAGE FOR THIS USER)
LF3    LDX ENTPT; EAX 3,2; EAX 1,2; SKN 0,2; BRU *-2
       EAX 1,2; SKN 0,2; BRU LF1A
       BRM TOFL (TEST FOR OVERFLOW)
       BRU LF4 (NO OVERFLOW OR NO ADDITIONAL OVERFLOW)
       BRM GFD; BRU LF1A
LF4    BRR LF1

LF5    SKN OFSW; BRU LF6; BRM TOFL *1 OR MORE OVERFLOW ALREADY READ
       BRU LF4 *NO ADDITONAL OVERFLOW WRITE STORAGE
       BRM GFD *THERE ARE ADDITIONAL OVERFLOWS GET NEXT
       BRU LF1A
LF6    BRM TOFL; BRR LF1 *NO OVERFLOW DIRECTORIES
       BRM GFD *HAS 1 OR MORE OVERFLOW GET NEXT
       BRU LF1A
OFSW   ZRO
*OPEN FILE AND CALCULATE LENGTH

CLEN   ZRO; STA IBP; SKG =LIBP (CK IF VALID I.B. PTR.)
       SKG =0; BRU CLEN1 (ZERO I.B.P.)
       LDB =41B; BRS 1; BRU CLEN1 (COULD NOT OPEN FILE)
       STA TFIL; RCS TFIL; LDA TFIL; BRS 20; MIN UFIL
       CLA; DIV =256*3/2; BRR CLEN
CLEN1  MIN UFIL; CLA; BRR CLEN

*READS FILE DIRECTORY INTO BUFFER AND RETURNS PTR IN X

GFD    ZRO; STA GRP; BRM RDIR; LDX =FDBUF
       BRR GFD

RDIR   ZRO; LDA GRP; LRSH 23; DIV =64; SKG =3; BRU RDIR1
       SKG =35; BRU *+3; SUB =16; BRU RDIR1
       SUB =4; XAB; SUB =100B; XAB
RDIR1  STB TEMP; CLB; LSH 12; ADD TEMP; LSH 1; ADD =200B
       CAB; STB DISC (SAVE DISC ADR)
       LDA =FDBUF; LDX =200B; BRS 124; BRR RDIR

*TESTS FOR OVERFLOW DIR.  NO SKIP IF NONE, IF OFLOW SKIPS AND
*RETURNS GROUP NO. IN A

TOFL   ZRO; LDX =FDBUF; LDA 125,2; SKE =0; BRU *+5
       LDA 124,2; SKE =0; BRU *+2; BRR TOFL; SKE UNO
       BRU *+2; BRR TOFL; SKR OFSW; BRU *-1; MIN TOFL; BRR TOFL



ACSIZE ZRO
IBP    ZRO
MINONE DATA 77777777B
SIZE   ZRO
TEM    ZRO
UNO    ZRO
TFIL   ZRO
ENTPT  ZRO
LIBP   EQU 1B6-1

FORMAT DATA 30011000B
POINT  BSS 2
       ZRO 0
M12    ASC '$TOTAL SELECTED USERS STORAGE /'
M2     ASC '$NO. OF FILES:  /'
M3     ASC '$YOURS ONLY? /'
M4     ASC '$USER NAMES: /'
ITEMSW ZRO
M5     ASC '$ITEMIZED BY USER? /'
AFIL   ZRO
UFIL   ZRO
H1     ASC 'USER NAME            CHARS.    FILES/'
DATE   ZRO; LDA =-1; CLX; BRS 176; ABC; LSH 4; ADD =72; STA YR
       CLA; LSH 20; STA TLV1; LRSH 16; ADD =1; CR 1; BRM PDNO
       LDA SLASH; CIO TOUT; LDA TLV1; ETR =177777B; LRSH 23; DIV =1440
       STB TLV1; ADD =1; BRM PDNO; LDA SLASH; CIO TOUT
       LDA YR; BRM PDNO; SPACE 2; LDA TLV1; LRSH 23; DIV =60
       STB TLV1; BRM PDNO; LDA COLON; CIO TOUT; LDA TLV1; SKG =9
       BRM ZERO; BRM PDNO; CR 1; BRR DATE
ZERO   ZRO; STA SAVA; LDA =20B; CIO TOUT; LDA SAVA; BRR ZERO
COLON  DATA 32B
SLASH  DATA 17B
TLV11  ZRO
BLX    ZRO
BLNO   ZRO
MTEMP  ZRO
LBUF   EQU 14000B

GENTRY ZRO; LDA LBUFPT; STA LPOINT; MUL =3; LSH 23; SUB =1; CAB
       ADD =12; XAB
       STP GEPTR; CLA; LDX =-4; ADD* LPOINT; MIN LPOINT; BRX *-2
       LRSH 23; DIV =887; STB BLNO; CBA; SKG =895; BRU *+2; HLT
       BRM GE1
GE2    STX BLX; LDA 0,2; SKE =0; BRU *+3; LDX =1; BRR GENTRY (NOT IN LUD)
       SKE =-1; BRU *+3; MIN BLNO; BRU GE2-1; SKE =-2; BRU *+2
       BRU GE3; CXA; MUL =3; LSH 23; SUB =1; CAB; ADD =12; XAB
       SKSE GEPTR; BRU GE3; LDX BLX; MIN GENTRY; BRR GENTRY
GE3    LDX BLX; EAX 9,2; BRU GE2

GE1    ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB; LSH 13
       ADD =320200B; ADD MTEMP; CAB; LDA =UBUF; LDX =100B; BRS 124
       EAX UBUF; BRR GE1
ERR1   ASC '$ERROR: TYPE USER NAME /'
ERR2   ASC '$FILE NOT SYMBOLIC$/'
ERR4   ASC '$CANNOT OPEN FILE$/'
ERR7   ASC 'INVALID USER NAME/'
GTNS1  ZRO; BRM CLBUF1; BRM CLBUF; LDA NAMPTA; STA NAMPT; STA NAMPT1
       BRM GUNAM; BRR GTNS1; MIN GTNS1; BRR GTNS1
GUNAM  ZRO; CLA; STA NAMCNT
GUNAM1 SKN FILSW; BRU GUN1A; CIO FNI; SKE =137B; BRU GUN1A+1; BRU ACTS7
GUN1A  CIO CIN; SKE =155B; BRU *+2; BRU GUNAM4
       SKE =152B; BRU *+2; BRU GUNAM4
       SKE =141B; BRU GUNAM2; GCD NAMPT; NOP; TCO =77B
       LDA NAMCNT; SKG =0; BRU GUNAM1; SUB =1; STA NAMCNT; BRU GUNAM1
GUNAM2 SKG =77B (IGNORE CONTROL LETTERS); BRU *+2; BRU GUNAM1
       STA NAMTEM; LDA NAMCNT; SKE =12; BRU *+2; BRU GUNAM3
       LDA NAMTEM; WCI NAMPT; MIN NAMCNT; BRU GUNAM1
GUNAM3 CIO CIN; SKE =155B; BRU *+2; BRU GUN3A; SKE =152B; BRU GUNAM3
GUN3A  STA TERCH1; LDX =2; BRR GUNAM
GUNAM4 STA TERCH1; LDA NAMCNT; SKE =0; BRU *+3; LDX =1; BRR GUNAM
       MIN GUNAM; BRR GUNAM

NAMCNT ZRO
NAMTEM ZRO
NAMBF1 BSS 4
NAMPTA DATA 3*NAMBF1-1
NAMPT  ZRO
NAMPT1 ZRO
GEPTR  BSS 2
CLBUF1 ZRO; LDX =-4; CLA; STA NAMBF1+4,2; BRX *-1; BRR CLBUF1
EPTR   ZRO
ACCT   ZRO
ENU1A  MSG ERR3; BRS 10
ERR3   ASC '$ERROR IN COMMANDS FILE/'
YR     ZRO
FILSW  ZRO
LBUFPT DATA LBUF
SUNO   ZRO
FNI    ZRO
FILPT  DATA 3*FILBUF-1
FILPT1 ZRO
FILBUF BSS 20
LPOINT ZRO
TOTSW  DATA -1
       END
SUMMARY ** FICHE/FRAME BREAK *****
SUPVRSUMMARY8 SYMSORT

SYM.  PG.LN. IDENT.

ACCT    7 34 SUMSUP  ACSIZE  5 31 SUMSUP  ACTS1   2 20 SUMSUP  
ACTS2   2 24 SUMSUP  ACTS3   2 25 SUMSUP  ACTS4   2 28 SUMSUP  ACTS5   2 30 SUMSUP  
ACTS6   2 31 SUMSUP  ACTS7   2 35 SUMSUP  ACTS8   3  2 SUMSUP  ACTS9   3  6 SUMSUP  
ACTS9A  3 13 SUMSUP  ACTSTO  2 17 SUMSUP  AFIL    6 11 SUMSUP  AMPER   3 35 SUMSUP  
BLNO    6 26 SUMSUP  BLX     6 25 SUMSUP  CIN     3 36 SUMSUP  CINSW   1 26 SUMSUP  
CLBUF   4 13 SUMSUP  CLBUF1  7 32 SUMSUP  CLEN    5  3 SUMSUP  CLEN1   5  8 SUMSUP  
COLON   6 22 SUMSUP  CR      1 22 SUMSUP  DATE    6 14 SUMSUP  DISC    3 39 SUMSUP  
EFTEL   3 19 SUMSUP  ENTPT   5 38 SUMSUP  ENU1A   7 35 SUMSUP  EPTR    7 33 SUMSUP  
ERR1    7  5 SUMSUP  ERR2    7  6 SUMSUP  ERR3    7 36 SUMSUP  ERR4    7  7 SUMSUP  
ERR7    7  8 SUMSUP  ERROR   3 20 SUMSUP  FDBUF   3 38 SUMSUP  FILBUF  8  5 SUMSUP  
FILPT   8  3 SUMSUP  FILPT1  8  4 SUMSUP  FILSW   7 38 SUMSUP  FNI     8  2 SUMSUP  
FORMAT  6  2 SUMSUP  FTEL    2 38 SUMSUP  GE1     7  2 SUMSUP  GE2     6 35 SUMSUP  
GE3     6 39 SUMSUP  GENTRY  6 30 SUMSUP  GEPTR   7 31 SUMSUP  GFD     5 12 SUMSUP  
GRP     4  1 SUMSUP  GTNS1   7  9 SUMSUP  GUN1A   7 13 SUMSUP  GUN3A   7 21 SUMSUP  
GUNAM   7 11 SUMSUP  GUNAM1  7 12 SUMSUP  GUNAM2  7 17 SUMSUP  GUNAM3  7 20 SUMSUP  
GUNAM4  7 22 SUMSUP  H1      6 13 SUMSUP  IBP     5 32 SUMSUP  ITEMSW  6  9 SUMSUP  
LBUF    6 28 SUMSUP  LBUFPT  7 39 SUMSUP  LF1     4 14 SUMSUP  LF1A    4 16 SUMSUP  
LF2A    4 22 SUMSUP  LF3     4 25 SUMSUP  LF4     4 30 SUMSUP  LF5     4 32 SUMSUP  
LF6     4 36 SUMSUP  LIBP    5 39 SUMSUP  LPOINT  8  6 SUMSUP  M1      4 11 SUMSUP  
M12     6  5 SUMSUP  M2      6  6 SUMSUP  M3      6  7 SUMSUP  M38     2 10 SUMSUP  
M4      6  8 SUMSUP  M5      6 10 SUMSUP  MAXSIZ  1  7 SUMSUP  MINONE  5 33 SUMSUP  
MSG     1 14 SUMSUP  MSGP    1 16 SUMSUP  MTEMP   6 27 SUMSUP  NAMBF1  7 27 SUMSUP  
NAMCNT  7 25 SUMSUP  NAMPT   7 29 SUMSUP  NAMPT1  7 30 SUMSUP  NAMPTA  7 28 SUMSUP  
NAMTEM  7 26 SUMSUP  OFSW    4 39 SUMSUP  PCHAR   3 27 SUMSUP  PCHAR1  3 29 SUMSUP  
PDNO    3 33 SUMSUP  PFILES  1 36 SUMSUP  PNAME   3 23 SUMSUP  PNAME1  3 24 SUMSUP  
PNRET   3 25 SUMSUP  POINT   6  3 SUMSUP  PSTORG  1 34 SUMSUP  RCS     1  8 SUMSUP  
RDIR    5 15 SUMSUP  RDIR1   5 18 SUMSUP  SAVA    4  2 SUMSUP  SAVX    4  3 SUMSUP  
SCNT    4  4 SUMSUP  SIZE    5 34 SUMSUP  SLASH   6 23 SUMSUP  SPACE   1 19 SUMSUP  
START   1 27 SUMSUP  SUNO    8  1 SUMSUP  SUPVR   2 12 SUMSUP  TEM     5 35 SUMSUP  
TEMP    4 12 SUMSUP  TERCH   4  5 SUMSUP  TERCH1  4  6 SUMSUP  TFIL    5 37 SUMSUP  
TLV1    4  7 SUMSUP  TLV11   6 24 SUMSUP  TLV21   4  8 SUMSUP  TLV22   4  9 SUMSUP  
TOFL    5 25 SUMSUP  TOTACT  3 17 SUMSUP  TOTSW   8  7 SUMSUP  TOUT    3 37 SUMSUP  
UBUF    4 10 SUMSUP  UFIL    6 12 SUMSUP  UNO     5 36 SUMSUP  USER    1 30 SUMSUP  
YN      2  1 SUMSUP  YN1     2  3 SUMSUP  YN3     2  4 SUMSUP  YN4     2  7 SUMSUP  
YNERR   2  6 SUMSUP  YR      7 37 SUMSUP  ZERO    6 21 SUMSUP  
SUMMARY ** FICHE/FRAME BREAK *****
MAKESUMMARY8
NARP
SUPVRSUMMARY8
BSUPVRSUMMARY8

T
NARP
SUPSUM8
BSUPSUM8

T
RES
DDT
;TBSUPVRSUMMARY8
%FSAV 3 TO 3777 ON GSUMMARY8

240
RES
XDD
;TBSUPSUM8
%FSAV 3 TO 3777 ON G1SUMMARY8

240
"

       SUMMARY PROGRAMS  VERSION 8

       FILES AND CHECKSUMS


"
CHECKSUM
.
SUPVRSUMMARY8-SYMBOLICS-TO-SUMMARY
SUPSUM8-SYMBOLICS-TO-1SUMMARY
GSUMMARY8-GO-FILE-SUMMARY-PROGRAM
G1SUMMARY8-GO-FILE-1SUMMARY-PROGRAM
MAKESUMMARY8-COMMAND-FILE
.
COM T
 I	1XŽ