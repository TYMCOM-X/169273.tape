DEFER ** FICHE/FRAME BREAK *****
SDEFER26
DEFERF EQU     1

       IF      DEFERF
DEFER  IDENT   DEFER VERSION 1
       ELSE
REMOTE IDENT   REMOTE VERSION 1
       ENDF
DEBUG  EQU     -1              SET TO 1 FOR DEBUG MODE


SPTR   OPD     514B5
LIST   EQU     2000B
BUFF   EQU     3000B

       BRU     START

$GUSNM ZRO; LDA =48; LDB =NMT; LDX =6; BRS 67
       SPTR NMT; STA P; ADD NMP1; SUB NMP; STA P1; BRR GUSNM

GSYSNO ZRO; LDA =BUFF; LDB =200B; LDX =100B; BRS 124
       LDA BUFF+75B; LRSH 12; STA CPN; BRR GSYSNO

GPRJC  ZRO; LDA =59; LDB =BUFF; LDX =4; BRS 67
       SPTR BUFF; STP TP; ADD =12; STA TP+1
       GCI TP; ZRO; ETR =177B; SKE =0; BRU *+2; BRU GPRJCE
       STA T; LDA =33B; WCI P; LDA T
GPRJCL WCI P; GCI TP; BRU GPRJCE; ETR =177B; SKE =0; BRU GPRJCL
GPRJCE LDA =32B; WCI P; BRR GPRJC
GFILNM ZRO
GFNM1  CIO CIN; SKE =155B; BRU *+2; BRU *-3
       SKE =0; BRU *+2; BRU GFNM1
       WCI P; CIO CIN; WCI P; SKE =155B; BRU *-3
       LDP P; STP TP; CLA; LDX =-6; WCI TP; BRX *-1
       BRR GFILNM



TYPE   POPD    101B5
       STA T1; STB T2; STX T3
       CLX; EAX* 0; CXA; ETR =37777B; LDB =-1; LDX COUT; BRS 34
       LDA T1; LDB T2; LDX T3; BRR 0
DISTNO ZRO
USFLG  ZRO
START  STP CIN
       LDA     =MDSI
       LDX     =31
       BRS     7
       LDA     MDSI
       SKA     =1              MDSI?
       BRU     DONE            YES IT IS
       BRS     39
       LRSH    2
       ETR     =1777B
       STA     DISTNO
       SKG     =29             IN AREA 3?
       BRU     US                      NO LOWER
       SKG     =39             IN AREA 3?
       BRU     P14                     YES GO 14
       SKG     =69
       BRU     US
       SKG     =79
       BRU     P14
       SKG     =179
       BRU     US
       SKG     =199
       BRU     P14
US     CLA
       STA     USFLG
       LDA     =21
       STA     DTIME
       LDA     =14001304B
       BRU     DONE
P14    LDA     =-1
       STA     USFLG
       LDA     =12
       STA     DTIME
       LDA     =14000666B
DONE   STA     TMWD
       TYPE VERS
       LDA =4B4; LDX =-1; BRS 12
       BRM GUSNM
       BRM     GPRJC
       BRM GSYSNO
       BRM     GFILNM
       BRM     GNXD 
       BRM     XINS
       BRS     10



       IF DEFERF

DTIME  ZRO
GNXD   ZRO
       LDA =-1; LDX =300B; BRS 176
       STX UZ; CAB
       ETR =37B; SKG =21;  BRU GNXD1
       COPY BA,B; ADD =40; LDX =3000300B; BRS 176; BRU *+2
GNXD1  CBA; ETR =@37B; ADD =22; STA T
       LRSH 5; SUB =13777B; MRG =2B7
       CLB; STP LIST
       LDA T; LDX =3000120B; BRS 176; ADD =15
       STA NXD; BRR GNXD 


       ELSE

GNXD   ZRO
       LDA =-1; LDX =120B; BRS 176; STA NXD; STX UZ
       LDA =-1; LDX =300B; BRS 176; LRSH 5; SUB =13777B
       MRG =2B7; CLB; STP LIST; BRR GNXD

       ENDF

XINS   ZRO; LDA ="INS"; BRM OPCH (SETS IF & OF)
       LDA TMWD; BRM SWD
       LDA NXD; BRM SWD
       BRM XRQS (SEND RQS DATA); BRM SLIST
XINS1  BRM GWD; SKE ="EJB"; BRM SYSER (ERROR)
       TYPE EOJ; BRS 10

*OPEN CHANNEL AND ESTABLISH COMM.
OPCH   ZRO; STA CWD; SKN CHFL; BRU CSEND; LDA =INT5; STA 205B
       TYPE BUILD
       LDA =INT5; STA 205B
       LDA =1B5; LDB =180000 (3 MIN TIMOUT); BRS 135
       BRS 138; SKE =0; BRU NOSUP (NO SUPER.)
       LDA     MDSI
       SKE     =0
       BRU OPMDSI              MDSI
       BRS     39
       SKA     =4B7
       BRU     OPCHI
       LDP     LOGP14
       SKN     USFLG
       LDP     LOGPT
       BRU     OPCHZ
MDSI   ZRO
OPMDSI LDP     MDSIPT
       BRU     OPCHZ
OPCHI  LDP     LOGPTI          INTERNAL
OPCHZ  LDX =1; BRS 35; BRS 138; SKE =0; BRU NOSUP
       CXA; SKG =1; BRU NOSUP; STA CHFL
       STA IF; ADD =1; STA OF; CAX; LDP PJPT; BRS 35
       LDA =ISTR; LDB =-1; BRS 34; LDX OF; BRS 85 (SET 8 LEVEL OUT.)
*      ESTABLISH COMM. BY SENDING SEQ. AND GETTING SAME RETURN
       CLA; STA T3 (SENDING CHAR.)
ICM    LDX =-5; MIN T3; LDA T3; CIO OF; BRX *-3
*      WAIT FOR INPUT
       LDX IF
ICM1   LDA T2; STA T1; BRS 13; BRU *+3 (HAVE SOME)
       BRS 45 (WAIT 3 SEC); BRU ICM1-1
       CIO IF; STA T2; SUB T1; SKE =1; BRU ICM1 (NOT SEQUENTIAL)
       LDA T2 (FOR PREV. CHAR.)
ICM2   STA T1 (THIS IS PREV.)
       LDX IF; BRS 13; BRU ICM3 (HAVE A CHAR.)
       BRS 45; LDX IF; BRS 13; BRU ICM3; BRU ICM
ICM3   CIO IF; STA T2; SUB T1; SKE =1; BRU ICM1 (NOT SEQ.)
       LDA T2; SKE T3; BRU ICM2 (NOT LAST CHAR. SENT)
*      NOW SEND THE COMMAND WORD
       LDA CWD;
CSEND  BRM SWD
       BRM GWD; SKE CWD; BRM SYSER
       CLA; BRS 78 (CLEAR TIMER); BRR OPCH

*SEND WORD
SWD    ZRO; LRSH 16; CIO OF; LSH 8; CIO OF; LSH 8; CIO OF; BRR SWD

*GET WORD FROM REMOTE
GWD    ZRO; CIO IF; CLB; LSH 16; STA T1
       CIO IF; CLB; LSH 8; ADM T1
       CIO IF; MRG T1; BRR GWD

*DOES THE RQS SUBROUTINE SENDS THE DATA
XRQS   ZRO; LDA UZ; CLB; LSH 12; MRG CPN; BRM SWD
       LDA =NMT; BRM MVSUB; BRR XRQS

*MOVES A LIST TO REMOTE
*EXPECTS ADDRESS IN A AND LIST ENDS WITH ZERO WORD
MVSUB  ZRO; STA T1; LDA* T1; SKE =0; BRU MVSUB1
       BRM SYSER
MVSUB1 BRM SWD; MIN T1; LDA* T1; SKE =0; BRU MVSUB1
       BRM SWD; BRR MVSUB

*SENDS THE "LIST"
SLIST  ZRO; LDA =LIST; BRM MVSUB; BRR SLIST

*SEND USER NAME DATA

*COMMON ERROR
SYSER  ZRO; TYPE SYSEMG; BRS 10

NOSUP  TYPE NET; BRS 10

       ZRO
INT5   ZRO *-1
       BRS 156
       TYPE TIMEOUT; BRS 10

       IF DEBUG
LOGST  ASC     'CCARTER:2-'
LOGPT  DATA    3*LOGST-1,3*LOGST+9
LOGPTI EQU     LOGPT
LOGP14 EQU     LOGPT
MDSIPT EQU     LOGPT
       ELSE
LOGST  ASC 'DUPJOB:10-'
LOGPT  DATA 3*LOGST-1,3*LOGST+9
LOGMDS ASC     'DUPJOB:27-'
MDSIPT DATA    3*LOGMDS-1,3*LOGMDS+9
LOGSTI ASC     'DUPJOB:2-'
LOGPTI DATA    3*LOGSTI-1,3*LOGSTI+8
LOG14  ASC     'DUPJOB:14-'
LOGP14 DATA    3*LOG14-1,3*LOG14+9
       ENDF
PJST   ASC 'DEFER'
PJPT   DATA 3*PJPT-1,3*PJPT+4
ISTR   ASC '$1GPERP$/'

OF     ZRO
IF     ZRO
CWD    ZRO
$CHFL  DATA -1

       IF DEFERF
BUILD  ASC     '$BUILDING CIRCUIT TO MASTER DEFER SYSTEM$/'
       ELSE
BUILD  ASC     '$BUILDING CIRCUIT TO MASTER REMOTE SYSTEM$/'
       ENDF
NET    ASC     '$NETWORK DIFFICULTY$/'
TIMEOU ASC     '$SYSTEM TIME-OUT  PLEASE TRY LATER$/'
SYSEMG ASC     '$SYSTEM ERROR$/'
VERS   ASC     '$VERS 2.6'
FM     ASC     '$$COMMAND FILE: /'

       IF DEFERF
EOJ    ASC     '$DEFER JOB ENTERED$/'
       ELSE
EOJ    ASC     '$REMOTE JOB ENTERED$/'
       ENDF

CIN    ZRO     0
COUT   ZRO     1
T      ZRO
T1     ZRO
T2     ZRO
T3     ZRO
TP     BSS     2
P      ZRO
P1     ZRO
TMWD   DATA    14001304B
UZ     ZRO
NXD    ZRO
CPN    ZRO
NMT    BSS     4
NMP    ZRO
NMP1   ZRO
       BSS     130
       END
DEFER ** FICHE/FRAME BREAK *****
SDEFER26 SYMSORT

SYM.  PG.LN. IDENT.

BUFF    1 13 DEFERF  BUILD   6 33 DEFERF  BUILD   6 31 DEFERF  
CHFL    6 28 DEFERF  CIN     7  8 DEFERF  COUT    7  9 DEFERF  CPN     7 20 DEFERF  
CSEND   5  6 DEFERF  CWD     6 27 DEFERF  DEBUG   1  8 DEFERF  DEFER   1  4 DEFERF  
DISTNO  2  3 DEFERF  DONE    2 39 DEFERF  DTIME   3 15 DEFERF  EOJ     7  5 DEFERF  
EOJ     7  3 DEFERF  FM      6 39 DEFERF  GFILNM  1 29 DEFERF  GFNM1   1 30 DEFERF  
GNXD    3 30 DEFERF  GNXD    3 16 DEFERF  GNXD1   3 21 DEFERF  GPRJC   1 23 DEFERF  
GPRJCE  1 28 DEFERF  GPRJCL  1 27 DEFERF  GSYSNO  1 20 DEFERF  GUSNM   1 17 DEFERF  
GWD     5 14 DEFERF  ICM     4 31 DEFERF  ICM1    4 34 DEFERF  ICM2    4 38 DEFERF  
ICM3    5  2 DEFERF  IF      6 26 DEFERF  INT5    6  1 DEFERF  ISTR    6 23 DEFERF  
LIST    1 12 DEFERF  LOG14   6 18 DEFERF  LOGMDS  6 14 DEFERF  LOGP14  6 19 DEFERF  
LOGP14  6  9 DEFERF  LOGPT   6 13 DEFERF  LOGPT   6  7 DEFERF  LOGPTI  6 17 DEFERF  
LOGPTI  6  8 DEFERF  LOGST   6 12 DEFERF  LOGST   6  6 DEFERF  LOGSTI  6 16 DEFERF  
MDSI    4 21 DEFERF  MDSIPT  6 15 DEFERF  MDSIPT  6 10 DEFERF  MVSUB   5 24 DEFERF  
MVSUB1  5 26 DEFERF  NET     6 35 DEFERF  NMP     7 22 DEFERF  NMP1    7 23 DEFERF  
NMT     7 21 DEFERF  NOSUP   5 37 DEFERF  NXD     7 19 DEFERF  OF      6 25 DEFERF  
OPCH    4  6 DEFERF  OPCHI   4 24 DEFERF  OPCHZ   4 25 DEFERF  OPMDSI  4 22 DEFERF  
P       7 15 DEFERF  P1      7 16 DEFERF  P14     2 34 DEFERF  PJPT    6 22 DEFERF  
PJST    6 21 DEFERF  REMOTE  1  6 DEFERF  SLIST   5 30 DEFERF  SPTR    1 11 DEFERF  
START   2  5 DEFERF  SWD     5 11 DEFERF  SYSEMG  6 37 DEFERF  SYSER   5 35 DEFERF  
T       7 10 DEFERF  T1      7 11 DEFERF  T2      7 12 DEFERF  T3      7 13 DEFERF  
TIMEOU  6 36 DEFERF  TMWD    7 17 DEFERF  TP      7 14 DEFERF  TYPE    1 38 DEFERF  
US      2 28 DEFERF  USFLG   2  4 DEFERF  UZ      7 18 DEFERF  VERS    6 38 DEFERF  
XINS    3 37 DEFERF  XINS1   4  2 DEFERF  XRQS    5 19 DEFERF  
DEFER ** FICHE/FRAME BREAK *****
BUILDDEFER
NARP
SDEFER26
BDEFER



RES
XDD
;TBDEFER
;U%QSAV 3,3777 O DEFER

240
DUMP DUMPDEFER

"



"
CHECKSUM
.DEFER-GO-FILE
SDEFER26-SYMBOLICS
BUILDDEFER
.
COM T
   