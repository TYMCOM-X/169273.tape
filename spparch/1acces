1ACCESS ** FICHE/FRAME BREAK *****
ACCESS1
ACCESS IDENT   5-23-74   CONTROLS ACCESS OF SYSTEM
*ALLOWS UUN 122460 (ON LINE DECISIONS) TO CONTROL ACCESS TO SYSTEM ON
*WHICH VALIDATED

       BRU BEGIN; BRS 10
VERNO  ASC '1.00$/'; ASC 'BLOOD'
IDMSG  ASC '$SYSTEM ACCESS CONTROL, VERSION /'

MSG    POPD    100B5,2

       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BRS 34; BRR 0

BEGIN  STA CIN; MSG IDMSG; MSG VERNO
       LDA =ESCAPE; STA 201B; BRU ESCAPE+1
FRONT  CLAB; STA ERFLG; LDX =-25; STA CDBUF+25,2; BRX *-1
       LDP CDPTR; STP WKPTR; LDA =155B; CIO =1; LDA =32B; CIO =1
GTC    CIO CIN; SKE =141B; BRU *+2; BRU CONTA; SKG =77B; BRU *+2; BRU GTC1
       WCI WKPTR; BRU GTC
GTC1   LDA CDBUF; SKE =0; BRU *+2; BRU FRONT
       LDB =177777B; SKM =0; BRU *+2; BRU GTC1A
       LDB =377B; SKM =0; BRU *+2; BRU ERR; BRU GTC2
GTC1A  LRSH 16
       SKE =61B (Q); BRU *+2; BRU EN
       SKE =37B (?); BRU ERR; BRU HEL
GTC2   LDX =CTBL-CTBLND; SKE CTBLND,2
       BRX *-1; BRU* BTBLND,2
ERR    LDA ERFLG; SKE =0; BRU ERR1; MIN ERFLG; LDA =37B; CIO =1
       LDA =155B; CIO =1; CLAB; BRU FRONT+2
ERR1   MSG ERMSG1; BRU FRONT
ERMSG1 ASC '$FOR ASSISTANCE TYPE HELP$/'
CDBUF  BSS 25
CDPTR  DATA 3*CDBUF-1,3*CDBUF-1
WKPTR  BSS 2
CONTA  LDA =77B; CIO =1; GCD WKPTR; NOP; BRU GTC

CTBL   ASC 'STAREASETOVECLEANSSHURESQUIHEL'
CTBLND DATA 0,STAT,READ,SET,OVER,CLEAR,ANS,SHUT,RUB,EN,HEL
BTBLND DATA ERR

VER    ZRO; SKN VERSW; BRR VER; BRS 39; CBA; SKE =122460B
*USER O5WT555STAT - ON LINE DECISIONS TO ALLOW RETRICTING DEDICATED SYS
       BRU *+2; BRU VER1C
       BRS 71; BRU VER1A
VER1C  SKR STASW; BRU *-1; MIN VERSW; BRR VER
VER1A  BRM GETUN1; LDP SFPTR; BRS 48; BRU ERV1; LDB =SFBUF
       LDX =4; BRS 96; LDA =4; SKA SFBUF+3; BRU *+2; BRU ERV2
       LDP SFPTR; BRS 64; BRU OPFER; STA SFIL; CLA; SCP SFIL; HLT 0
VER1B  MSG VM1 (ENTER NAME)
       LDA VPTR1; STA VPTR1+1; LDA =VPTR1; LDB =155B; CLX; BRS 33
       LDA VPTR1+1; SUB VPTR1; STA VT1
VER1   CIO SFIL; SKE =137B; BRU *+2; BRU NONAME
       SKE =155B; BRU VER1; CIT SFIL; BRU *+2; BRU NONAME
       LDA VPTR2; STA VPTR2+1; LDA =VPTR2; LDB =155B
       LDX SFIL; BRS 33; LDA VPTR2+1; SUB VPTR2
       SKG =0; BRU NONAME; SKE VT1; BRU VER1
       LDP VPTR2; SKSE VPTR1; BRU VER1; GCI VPTR1; HLT 0
       SKR VPTR1; NOP; STA VXT8; BRM FONO; STA STAWD; SKA =40000B
       BRU *+2; BRU ERV3; SKA =1B3; BRU *+2; BRU ERV4
       BRM FONO; STA VT1; BRM FONO; STA VT2; LDA SFIL; BRS 20
       LDX =-1; LDA =3; BRS 12; MSG VM2 (ENTER YOUR PASSWORD PLEASE)
SGPS   LDP TERMWD; STP VXT1; STP VXT3; STB VXT5
       CLA; STA TLV1; LDA VXT8; ADM VXT3
GPS    LDA =20; STA TLV11
GPS1   LDX TLV1; LDA VXT1,2; MUL =6065B
       DIV =37777761B; CBA; ADD VXT2,2; CAB; ETR =3
       STA TLV1; LDA VXT1,2; XAB; STP VXT1,2; LDA VXT5
       XMA VXT1; STA VXT5; SKR TLV11
       BRU GPS1; CIO =0; SKE =155B; BRU *+2; BRU *+3
       ADM VXT1; BRU GPS
       LDX =-1; LDA =2; BRS 12
       LDA VXT1; ADD VXT2; SKE VT1; BRU NOPASS; LDA VXT3; ADD VXT4
       SKE VT2; BRU NOPASS; MIN VERSW; TCO =155B; BRR VER
VERSW  DATA -1

FONO   ZRO; LDB =8; LDX SFIL; BRS 38; BRR FONO

NOPASS MSG VME5; LDX =-1; LDA =3; BRS 12; BRU SGPS

NONAME MSG VME6; BRS 10


TERMWD DATA 27657537B,55555555B

SFPTR  DATA 3*SF-1,3*SF+5
SF     ASC '()PSWD'
VPTR1   DATA 3*OPNAM-1,3*OPNAM-1
OPNAM  BSS 20
VPTR2   DATA 3*BUF2-1,3*BUF2-1
BUF2   BSS 20
SFBUF  BSS 20
VT1    ZRO
VT2    ZRO
VXT1   ZRO
VXT2   ZRO
VXT3   ZRO
VXT4   ZRO
VXT5   ZRO
VXT8   ZRO
STAWD  ZRO
SFIL   ZRO
ERV1   MSG VME1
ERV    MSG VME; BRS 10
ERV2   MSG VME2; BRU ERV
OPFER  MSG VME3; BRS 10
ERV3   MSG VME4; BRS 10
ERV4   MSG VME7; BRU FRONT

VME1   ASC '$NO PASSWORD FILE/'
VME    ASC '$NOTIFY DIVISION OPNS MGR AND SYSTEMS GROUP TECH DIV/'
VME2   ASC '$PASSWORD FILE STATUS LOST/'
VME3   ASC '$PASSWORD FILE BUSY-TRY LATER$/'
VME4   ASC '$UNVALIDATED NAME/'
VM1    ASC '$YOUR NAME: /'
VM2    ASC '$YOUR PASSWORD: /'
VME5   ASC '$ERROR, TYPE PASSWORD /'
VME6   ASC '$NAME DOES NOT APPEAR IN PASSWORD FILE$/'
VME7   ASC '$MANAGERS LICENSE REQUIRED TO USE THIS COMMAND$/'
*GET CURRENT CONDITION OF CELL FLAGS

GTFLG  ZRO; LDX =4; CLA; BRS 22; BRS 136; STA HFLAG; BRS 136
       BRS 23; BRR GTFLG
HFLAG  ZRO
STAT   BRM AVAIL
       BRM GTFLG; LDA HFLAG; SKA =1B4; BRU STATR; MSG M1 (UNRESTRICTED)
       CLA; STA STATSW; BRU FRONT
STATR  MSG M2 (RESTRICTED); CLA; STA STATSW; BRU FRONT
M1     ASC '$SYSTEM UNRESTRICTED$/'
M2     ASC '$SYSTEM RESTRICTED$/'
M3     ASC '$EOJ$/'
M4     ASC '$VALID COMMANDS ARE:'
       ASC '$$STATUS      -    REPORTS THE CURRENT SYSTEM STATUS AS TO'
       ASC '$                 SYSTEM ACCESS AVAILABILITY, WHETHER SYSTEM'
       ASC '$                 IS ANSWERED OR SHUT, RESTRICTED OR UNRESTRICTED'
       ASC '$READ        -    READS CURRENT RESTRICTION PARAMETERS'
       ASC '$ANSWER      -    ANSWERS THE SYSTEM'
       ASC '$SHUT        -    SHUTS THE SYSTEM'
       ASC '$SET         -    CHANGES RESTRICTION PARAMETERS'
       ASC '$OVERRIDE    -    SETS SWITCH TO OVERRIDE CURRENT RESTRICTION'
       ASC '$                 PARAMETERS'
       ASC '$CLEAR OVERRIDE   CLEARS THE OVERRIDE SWITCH'
       ASC '$RESTRICT USER -  RESTRICTS OR UNRESTRICTS A SPECIFIED USER'
       ASC '$HELP OR ?   -    RETYPES THIS LIST'
       ASC '$QUIT OR Q   -    QUITS TO THE EXEC'
       ASC '$$ANY COMMAND MAY BE SHORTENED TO THREE CHARACTERS$$/'
M5     ASC '$NO PARAMETERS SET$/'
M6     ASC '$START RESTRICTION: /'
M7     ASC '$STOP RESTRICTION: /'
M8     ASC '$OVERRIDE SWITCH SET TO RESTRICTED$/'
M9     ASC '$OVERRIDE SWITCH SET TO UNRESTRICTED$/'
M10    ASC '$OVERRIDE SWITCH NOT SET$/'
M11    ASC '$$PARAMETERS NOW SET AS FOLLOWS:$/'
M12    ASC '$SYSTEM ANSWERED$/'
M13    ASC '$SYSTEM SHUT$/'
M14    ASC '$ENTER USER NAME: /'
M15    ASC '$SHALL THIS USER BE RESTRICTED? /'
M16    ASC '$ERROR,/'
M17    ASC '$THIS USER MAY NOT BE RESTRICTED BY THIS PROGRAM$/'
EN     MSG M3 (EOJ); BRS 10



HEL    MSG M4; BRU FRONT
GETUN1 ZRO; LDX =1; LDA =1; BRS 141; BRR GETUN1
CIN    ZRO
ERFLG  ZRO
STASW  ZRO
TLV1   ZRO
TLV11  ZRO
*READ DISC CONTROL BLOCK

RPAR   ZRO; LDA =PARBUF; LDB =200B; LDX =100B; BRS 124; BRR RPAR

*WRITE DISC CONTROL BLOCK

WPAR   ZRO; LDA =PARBUF; LDB =200B; LDX =100B; BRS 125; BRR WPAR

PARBUF BSS 100
START  EQU PARBUF+70B
STOP   EQU PARBUF+71B
ORIDE  EQU PARBUF+72B
CZONE  EQU PARBUF+74B

READ   BRM RPAR; LDA START; SKE =0; BRU READ1; LDA STOP; SKE =0
       BRU READ1; MSG M5; SKN STATSW; BRU FRONT; BRU STAT
READ1  MSG M6 (START RESTRICTION); LDA START; BRM TTIM
       MSG M7 (STOP RESTRICTION); LDA STOP; BRM TTIM
       LDA ORIDE; SKE =0; BRU READ2; MSG M10; SKN STATSW; BRU FRONT; BRU STAT
READ2  SKE =-1; BRU READ3; MSG M8; SKN STATSW; BRU FRONT; BRU STAT
READ3  MSG M9; SKN STATSW; BRU FRONT; BRU STAT
PDNO   ZRO; STP SREG; LDB =10; LDX =1; BRS 36; LDP SREG; BRR PDNO
SREG   BSS 2
ZERO   ZRO; TCO =20B; BRR ZERO
RDNO   ZRO; LDB =10; CLX; BRS 38; BRR RDNO

*SET SYSTEM PARAMETERS

SET    BRM VER; BRM RPAR; MSG M6; BRM GTIM; STA START
       MSG M7; BRM GTIM; STA STOP; BRM WPAR; MSG M11; BRU READ


*GET TIME EITHER AS HHMM OR AS HH:MM

GTIM   ZRO; LDX =-5; CLA; STA TBUF+5,2; BRX *-1; STA WORK
       LDA TPTR; STA TWKPTR; STA TWKPTR+1
GTIM1  CIO =0; SKE =141B; BRU *+3; BRM TCONTA; BRU GTIM1
       SKG =77B; BRU *+2; BRU GTIM2; SKG =31B; SKG =17B
       BRU GTIM1; WCI TWKPTR; BRU GTIM1
GTIM2  GCI TWKPTR; BRU GTIM3; SUB =20B; XMA WORK; MUL =10; LSH 23; ADM WORK
       BRU GTIM2
GTIM3  LDA WORK; BRR GTIM
TBUF   BSS 5
TPTR   DATA 3*TBUF-1
TWKPTR  BSS 2
WORK   BSS 1
TCONTA  ZRO; GCD TWKPTR; NOP; TCO =76B; BRR TCONTA


TTIM   ZRO; STA TEMP; LRSH 23; DIV =10; STB MIN1
       LRSH 23; DIV =10; STB MIN
       LRSH 23; DIV =10; STP HR
       LDA HR; SKE =0; BRM PDNO; LDA HR1; BRM PDNO
       LDA ='   :'; CIO =1; LDA MIN; BRM PDNO; LDA MIN1; BRM PDNO; BRR TTIM
HR     ZRO
HR1    ZRO
MIN    ZRO
MIN1   ZRO

*SET OVERRIDE SWITCH

OVER   BRM VER; BRM RPAR; BRM FTIM; SKR STATSW; BRU *-1
       SKN RPSW (NEG IF IN RESTRICTED PERIOD)
       BRU OVER1; LDA =-2; STA ORIDE; BRM WPAR; BRM CRESTR; BRU READ
OVER1  LDA =-1; STA ORIDE; BRM WPAR; BRM SRESTR; BRU READ

*CLEAR OVERRIDE SWITCH

CLEAR  BRM VER; BRM RPAR; BRM FTIM; SKR STATSW; BRU *-1; CLA; STA ORIDE
       BRM WPAR; SKN RPSW; BRU CLEAR1; BRM SRESTR; BRU READ
CLEAR1 BRM CRESTR; BRU READ

*SET SYSTEM TO UNRESTRICTED

CRESTR ZRO; BRS 22; CLA; LDX =4; BRS 136; ETR =@1B4; BRS 136; BRS 23
       BRR CRESTR

*SET SYSTEM TO RESTRICTED

SRESTR ZRO; BRS 22; CLA; LDX =4; BRS 136; MRG =1B4; BRS 136; BRS 23
       BRR SRESTR
STATSW ZRO
FTIM   ZRO; CLA; STA RPSW; LDA CZONE; CLB; LSH 12; MRG CZONE
       MRG FMATWD; STA XTIM
       CAX; LDA =-1; BRS 176; STP HDATE; ABC; LSH 14; ETR =17B; ADD =1
       STA MO; CLA; LSH 5; ADD =1; STA CDATE; CLA; LSH 5; CLB
       LRSH 23; DIV =10; STP FHR; CLA; LDB HDATE+1; LSH 6; CLB
       LRSH 23; DIV =10; STP FMIN; LDA FHR; STA HTIME
       LDA FHR+1; XMA HTIME; MUL =10; LSH 23; ADM HTIME
       LDA FMIN; XMA HTIME; MUL =10; LSH 23; ADM HTIME
       LDA FMIN+1; XMA HTIME; MUL =10; LSH 23; ADM HTIME
       BRM JULIAN; LRSH 23; DIV =7; LDA SYR
       SUB =10B; COPY AX,BA
       SKE SAT,2; BRU *+2; BRU FTIM5; SKE SUN,2; BRU *+2; BRU FTIM5
       LDA STOP; SUB START; STA TEMP; SKN TEMP; BRU *+2; BRU FTIM1
       LDA HTIME; SKG START; BRU FTIM5; SKG STOP; BRU *+2
       BRU FTIM5
FTIM3  SKR RPSW; BRU *-1; BRR FTIM
FTIM1  LDA HTIME; SKG STOP; BRU *+2; BRU FTIM2; SKG START
       BRU FTIM3; BRU FTIM5
FTIM2  LDA START; SKG HTIME; BRU FTIM3
FTIM5  CLA; STA RPSW; BRR FTIM
FHR     BSS 2
FMIN    BSS 2

CDATE  ZRO
MO     ZRO
FMATWD DATA 3000300B
HDATE  BSS 2
JULIAN ZRO; SKN JULSW; BRM LYR; LDX MO; LDA MTAB,2; ADD CDATE; STA JULDAT
       BRR JULIAN
LYR    ZRO; SKR JULSW; BRU *-1; BRS 42; CXA; STA SYR; ADD =1964; LRSH 23; DIV =4
       CBA; SKE =0; BRR LYR; LDX =-10; MIN MTABE,2; BRX *-1; BRR LYR
MTAB   DATA 0,0,31,59,90,120,151,181,212,243,273,304,334
MTABE  EQU *
JULSW  ZRO
HTIME  ZRO
SYR    ZRO
XTIM   ZRO
SAT    DATA 1,6,5,4,3
SUN    DATA 2,0,6,5,4
JULDAT ZRO
RPSW   ZRO
TEMP   ZRO
ESCAPE ZRO ESCPC; LDA =2B6; BRS 78; BRU FRONT
ESCPC  ZRO


*ANSWER THE SYSTEM

ANS    BRM VER; LDA =1; BRS 159; BRU FRONT
*SHUT THE SYSTEM

SHUT   BRM VER; CLA; BRS 159; BRU FRONT
AVAIL  ZRO; LDA =BRS7T; CLX; BRS 7; LDA UPSHUT; SKE =2; BRU *+3
       MSG M12 (SYSTEM ANSWERED); BRR AVAIL; MSG M13 (SYSTEM SHUT)
       BRR AVAIL
BRS7T  BSS 50
UPSHUT EQU BRS7T+36B
GUNAM  ZRO; CLA; STA NAMCNT
       LDX =-10; STA NAMBF+10,2; BRX *-1; LDP HNAMPT; STP NAMPT
GUNAM1 CIO CIN; BRM MAPLC; BRM TERMCH; SKE =141B
       BRU *+3; TCO =77B; BRU GUNAM1; SKG =0; BRU GUNAM1 (NO LEAD SP)
       SKE =16B; BRU *+2; BRU GUNAM1 (IGNORE LEADING PERIOD)
       SKG =77B; BRU *+2; BRU GUNAM1 (IGNORE CONTROL CHARS)
       BRM CKCHAR; WCI NAMPT; MIN NAMCNT
GUNAM2 CIO CIN; BRM MAPLC
       BRM TERMCH; SKE =141B; BRU GUNAM3; GCD NAMPT; NOP; TCO =77B
       LDA NAMCNT; SKG =0; BRU GUNAM1; SUB =1; STA NAMCNT; SKG =0
       BRU GUNAM1; BRU GUNAM2
GUNAM3 SKG =77B; BRU *+2; BRU GUNAM2 (IGNORE CONTROL CHARS)
GUN3A  BRM CKCHAR; STA NAMTEM; LDA NAMCNT; SKE =12
       BRU *+2; BRU GUNAM5; LDA NAMTEM; WCI NAMPT; MIN NAMCNT; BRU GUNAM2
GUNAM5 CIO CIN; SKE =155B; BRU GUNAM5; LDX =2; BRR GUNAM
GUNAM4 LDA NAMCNT; SKE =0; BRU *+3; LDX =1; BRR GUNAM
       MIN GUNAM; BRR GUNAM
MAPLC  ZRO; SKG =100B; BRR MAPLC; SKG =132B; SUB =40B; BRR MAPLC
TERMCH ZRO; SKE =155B; BRU *+2; BRU GUNAM4; SKE =33B; BRR TERMCH
       BRU GUNAM4
CKCHAR ZRO; SKG =32B; BRU CKC1; SKG =72B; SKG =37B; BRU CKC2
       BRR CKCHAR
CKC1   SKE =0; BRU *+2; BRR CKCHAR
       SKE =2; BRU *+2; BRR CKCHAR
       SKE =12B; BRU *+2; BRR CKCHAR
       SKG =13B; BRU *+2; BRR CKCHAR
CKC2   LDA =4; BRR CKCHAR
NAMBF  BSS 10
NAMPT  BSS 2
HNAMPT DATA 3*NAMBF-1,3*NAMBF-1
GENTRY ZRO; LDA POINT; MUL =3; LSH 23; SUB =1; CAB; ADD =12; XAB
       STP GEPTR; CLA; LDX =-4; ADD* POINT; MIN POINT; BRX *-2
       LRSH 23; DIV =887; STB BLNO; CBA; SKG =895; BRU *+2; HLT 0
       BRM GE1
GE2    STX BLX; LDA 0,2; SKE =0; BRU *+3; LDX =1; BRR GENTRY (NOT IN LUD)
       SKE =-1; BRU *+3; MIN BLNO; BRU GE2-1; SKE =-2; BRU *+2
       BRU GE3; CXA; MUL =3; LSH 23; SUB =1; CAB; ADD =12; XAB
       SKSE GEPTR; BRU GE3; LDX BLX
       MIN GENTRY; BRR GENTRY
GE3    LDX BLX; EAX 9,2; BRU GE2

GE1    ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB; LSH 13
       ADD =320200B; ADD MTEMP; CAB; STB LDISC; LDA =LUDBUF; LDX =100B
       BRS 124; EAX LUDBUF; BRR GE1
BLNO   ZRO
BLX    ZRO
GEPTR  BSS 2
LDISC  ZRO
LUDBUF BSS 100
MTEMP  ZRO
NAMCNT ZRO
POINT  ZRO
NAMTEM ZRO
YN     ZRO; BRM CLBUF
       CIO CIN; CAB; CIO CIN; SKE =155B; BRU *+2; BRU YN1
       SKE =152B; BRU *-5
YN1    STA YTERCH; CBA; SKE ='N'; BRU *+2; BRR YN; SKE ='Y'
       BRU *+2; MIN YN; BRR YN
       LDA CIN; SKE =0; BRU YNERR; LDA YN; SUB =2
       BRM CLINP; BRR YN
YTERCH  ZRO
YNERR  MSG M38; BRU EN
M38    ASC '$INCORRECT REPLY TO YES OR NO INTERROGATORY'
       ASC '$IN COMMAND FILE-PROGRAM ABORTED$$/'
CLINP  ZRO; LDX =-1; BRS 11; BRR CLINP

*WRITE LAST LUD BLOCK READ

WLUD   ZRO; LDA =LUDBUF; LDX =100B; LDB LDISC; BRS 125
       LDA =LUDBUF; LDX =100B; LDB =200B; BRS 124
       LDA =-1; STA LUDBUF+73B; LDA =LUDBUF; LDX =100B; LDB =200B
       BRS 125; BRR WLUD

*RESTRICT A USER

RUB    BRM VER; MSG M14 (ENTER USER NAME); BRM CLBUF; BRM GUNAM
       BRU RUBE1 (ERROR); LDA =NAMBF; STA POINT; BRM GENTRY
       BRU RUBE1 (NOT FOUND); STX BLX; LDA 8,2; STA CPARW
       LRSH 2; ETR =377B; SKE =33; BRU RUBE2 (NOT ON LINE DISTRICT)
       SKN 6,2; BRU *+2; BRU RUBE2 (NON-BILLABLE A NO NO FOR ON LINE)
       MSG M15; BRM YN; BRU RUB2; LDA =1B4; MRG CPARW
RUB1   LDX BLX; STA 8,2; BRM WLUD; BRU FRONT
RUB2   LDA CPARW; ETR =@1B4; BRU RUB1
RUBE1  LDA CIN; SKE =0; BRU YNERR; MSG M16 (ERROR,); BRU RUB+1
RUBE2  MSG M17; LDA CIN; SKE =0; BRU YNERR; BRU FRONT
CLBUF ZRO; LDA =377B; CIT CIN; SKE =155B; BRU *+2; BRU CLBUF+1
       SKE =37B; BRR CLBUF; CIO CIN; BRU FRONT
CPARW  ZRO
       END
1ACCESS ** FICHE/FRAME BREAK *****
ACCESS1 SYMSORT

SYM.  PG.LN. IDENT.

ANS     8 26 ACCESS  AVAIL   8 30 ACCESS  BEGIN   1 13 ACCESS  
BLNO   10  1 ACCESS  BLX    10  2 ACCESS  BRS7T   8 33 ACCESS  BTBLND  1 38 ACCESS  
BUF2    3 11 ACCESS  CDATE   8  1 ACCESS  CDBUF   1 31 ACCESS  CDPTR   1 32 ACCESS  
CIN     5  8 ACCESS  CKC1    9 18 ACCESS  CKC2    9 22 ACCESS  CKCHAR  9 16 ACCESS  
CLBUF  11  3 ACCESS  CLEAR   7  3 ACCESS  CLEAR1  7  5 ACCESS  CLINP  10 21 ACCESS  
CONTA   1 34 ACCESS  CPARW  11  5 ACCESS  CRESTR  7  9 ACCESS  CTBL    1 36 ACCESS  
CTBLND  1 37 ACCESS  CZONE   5 25 ACCESS  EN      5  2 ACCESS  ERFLG   5  9 ACCESS  
ERMSG1  1 30 ACCESS  ERR     1 27 ACCESS  ERR1    1 29 ACCESS  ERV     3 24 ACCESS  
ERV1    3 23 ACCESS  ERV2    3 25 ACCESS  ERV3    3 27 ACCESS  ERV4    3 28 ACCESS  
ESCAPE  8 20 ACCESS  ESCPC   8 21 ACCESS  FHR     7 37 ACCESS  FMATWD  8  3 ACCESS  
FMIN    7 38 ACCESS  FONO    2 36 ACCESS  FRONT   1 15 ACCESS  FTIM    7 17 ACCESS  
FTIM1   7 33 ACCESS  FTIM2   7 35 ACCESS  FTIM3   7 32 ACCESS  FTIM5   7 36 ACCESS  
GE1     9 37 ACCESS  GE2     9 30 ACCESS  GE3     9 35 ACCESS  GENTRY  9 26 ACCESS  
GEPTR  10  3 ACCESS  GETUN1  5  7 ACCESS  GPS     2 24 ACCESS  GPS1    2 25 ACCESS  
GTC     1 17 ACCESS  GTC1    1 19 ACCESS  GTC1A   1 22 ACCESS  GTC2    1 25 ACCESS  
GTFLG   4  3 ACCESS  GTIM    6  8 ACCESS  GTIM1   6 10 ACCESS  GTIM2   6 13 ACCESS  
GTIM3   6 15 ACCESS  GUN3A   9  8 ACCESS  GUNAM   8 35 ACCESS  GUNAM1  8 37 ACCESS  
GUNAM2  9  3 ACCESS  GUNAM3  9  7 ACCESS  GUNAM4  9 11 ACCESS  GUNAM5  9 10 ACCESS  
HDATE   8  4 ACCESS  HEL     5  6 ACCESS  HFLAG   4  5 ACCESS  HNAMPT  9 25 ACCESS  
HR      6 28 ACCESS  HR1     6 29 ACCESS  HTIME   8 12 ACCESS  IDMSG   1  7 ACCESS  
JULDAT  8 17 ACCESS  JULIAN  8  5 ACCESS  JULSW   8 11 ACCESS  LDISC  10  4 ACCESS  
LUDBUF 10  5 ACCESS  LYR     8  7 ACCESS  M1      4 10 ACCESS  M10     4 33 ACCESS  
M11     4 34 ACCESS  M12     4 35 ACCESS  M13     4 36 ACCESS  M14     4 37 ACCESS  
M15     4 38 ACCESS  M16     4 39 ACCESS  M17     5  1 ACCESS  M2      4 11 ACCESS  
M3      4 12 ACCESS  M38    10 19 ACCESS  M4      4 13 ACCESS  M5      4 28 ACCESS  
M6      4 29 ACCESS  M7      4 30 ACCESS  M8      4 31 ACCESS  M9      4 32 ACCESS  
MAPLC   9 13 ACCESS  MIN     6 30 ACCESS  MIN1    6 31 ACCESS  MO      8  2 ACCESS  
MSG     1  9 ACCESS  MTAB    8  9 ACCESS  MTABE   8 10 ACCESS  MTEMP  10  6 ACCESS  
NAMBF   9 23 ACCESS  NAMCNT 10  7 ACCESS  NAMPT   9 24 ACCESS  NAMTEM 10  9 ACCESS  
NONAME  3  1 ACCESS  NOPASS  2 38 ACCESS  OPFER   3 26 ACCESS  OPNAM   3  9 ACCESS  
ORIDE   5 24 ACCESS  OVER    6 35 ACCESS  OVER1   6 38 ACCESS  PARBUF  5 21 ACCESS  
PDNO    5 34 ACCESS  POINT  10  8 ACCESS  RDNO    5 37 ACCESS  READ    5 27 ACCESS  
READ1   5 29 ACCESS  READ2   5 32 ACCESS  READ3   5 33 ACCESS  RPAR    5 15 ACCESS  
RPSW    8 18 ACCESS  RUB    10 32 ACCESS  RUB1   10 38 ACCESS  RUB2   10 39 ACCESS  
RUBE1  11  1 ACCESS  RUBE2  11  2 ACCESS  SAT     8 15 ACCESS  SET     6  2 ACCESS  
SF      3  7 ACCESS  SFBUF   3 12 ACCESS  SFIL    3 22 ACCESS  SFPTR   3  6 ACCESS  
SGPS    2 22 ACCESS  SHUT    8 29 ACCESS  SREG    5 35 ACCESS  SRESTR  7 14 ACCESS  
START   5 22 ACCESS  STASW   5 10 ACCESS  STAT    4  6 ACCESS  STATR   4  9 ACCESS  
STATSW  7 16 ACCESS  STAWD   3 21 ACCESS  STOP    5 23 ACCESS  SUN     8 16 ACCESS  
SYR     8 13 ACCESS  TBUF    6 16 ACCESS  TCONTA  6 20 ACCESS  TEMP    8 19 ACCESS  
TERMCH  9 14 ACCESS  TERMWD  3  4 ACCESS  TLV1    5 11 ACCESS  TLV11   5 12 ACCESS  
TPTR    6 17 ACCESS  TTIM    6 23 ACCESS  TWKPTR  6 18 ACCESS  UPSHUT  8 34 ACCESS  
VER     2  1 ACCESS  VER1    2 12 ACCESS  VER1A   2  6 ACCESS  VER1B   2  9 ACCESS  
VER1C   2  5 ACCESS  VERNO   1  6 ACCESS  VERSW   2 34 ACCESS  VM1     3 35 ACCESS  
VM2     3 36 ACCESS  VME     3 31 ACCESS  VME1    3 30 ACCESS  VME2    3 32 ACCESS  
VME3    3 33 ACCESS  VME4    3 34 ACCESS  VME5    3 37 ACCESS  VME6    3 38 ACCESS  
VME7    3 39 ACCESS  VPTR1   3  8 ACCESS  VPTR2   3 1ESS  VT1     3 13 ACCESS  
VT2     3 14 ACCESS  VXT1    3 15 ACCESS  VXT2    3 16 ACCESS  VXT3    3 17 ACCESS  
VXT4    3 18 ACCESS  VXT5    3 19 ACCESS  VXT8    3 20 ACCESS  WKPTR   1 33 ACCESS  
WLUD   10 25 ACCESS  WORK    6 19 ACCESS  WPAR    5 19 ACCESS  XTIM    8 14 ACCESS  
YN     10 10 ACCESS  YN1    10 13 ACCESS  YNERR  10 18 ACCESS  YTERCH 10 17 ACCESS  
ZERO    5 36 ACCESS  
1ACCESS ** FICHE/FRAME BREAK *****
DOCACCESS

1ACCESS IS A PROGRAM WRITTEN FOR THE USE OF ON LINE DECISIONS ON THEIR
DEDICATED SYSTEM C13.  EXCEPT FOR THE READ ONLY COMMANDS, E.G. STATUS,
READ, AND HELP, USE IS LIMITED TO USER O5WT555STAT, OPERATIONS MANAGERS,
AND THOSE ABLE TO SET SYSTEM OR HIGHER LICENSE.

A SAMPLE RUN UTILIZING ALL OF THE COMMANDS IS ILLUSTRATED BELOW:

-1ACCESS


SYSTEM ACCESS CONTROL, VERSION 1.00

:HELP
VALID COMMANDS ARE: 

STATUS      -    REPORTS THE CURRENT SYSTEM STATUS AS TO  
                 SYSTEM ACCESS AVAILABILITY, WHETHER SYSTEM
                 IS ANSWERED OR SHUT, RESTRICTED OR UNRESTRICTED 
READ        -    READS CURRENT RESTRICTION PARAMETERS
ANSWER      -    ANSWERS THE SYSTEM
SHUT        -    SHUTS THE SYSTEM  
SET         -    CHANGES RESTRICTION PARAMETERS
OVERRIDE    -    SETS SWITCH TO OVERRIDE CURRENT RESTRICTION  
                 PARAMETERS  
CLEAR OVERRIDE   CLEARS THE OVERRIDE SWITCH 
RESTRICT USER -  RESTRICTS OR UNRESTRICTS A SPECIFIED USER
HELP OR ?   -    RETYPES THIS LIST 
QUIT OR Q   -    QUITS TO THE EXEC 

ANY COMMAND MAY BE SHORTENED TO THREE CHARACTERS


:STATUS
SYSTEM ANSWERED

SYSTEM UNRESTRICTED

:READ
START RESTRICTION: 0:01
STOP RESTRICTION: 0:02
OVERRIDE SWITCH NOT SET

:OVERRIDE
START RESTRICTION: 0:01
STOP RESTRICTION: 0:02
OVERRIDE SWITCH SET TO RESTRICTED

SYSTEM ANSWERED

SYSTEM RESTRICTED

:READ
START RESTRICTION: 0:01
STOP RESTRICTION: 0:02
OVERRIDE SWITCH SET TO RESTRICTED

:CLEAR
START RESTRICTION: 0:01
STOP RESTRICTION: 0:02
OVERRIDE SWITCH NOT SET

SYSTEM ANSWERED

SYSTEM UNRESTRICTED

:STATUS
SYSTEM ANSWERED

SYSTEM UNRESTRICTED

:SET
START RESTRICTION: 800
STOP RESTRICTION: 1500

PARAMETERS NOW SET AS FOLLOWS:

START RESTRICTION: 8:00
STOP RESTRICTION: 15:00
OVERRIDE SWITCH NOT SET

:READ
START RESTRICTION: 8:00
STOP RESTRICTION: 15:00
OVERRIDE SWITCH NOT SET


:SHUT
:STATUS
SYSTEM SHUT

SYSTEM UNRESTRICTED

:ANSWER
:STATUS
SYSTEM ANSWERED

SYSTEM UNRESTRICTED

:RESTRICT USER

ENTER USER NAME: JONES

SHALL THIS USER BE RESTRICTED? YES

:RESTRICT USER

ENTER USER NAME: SMITH

SHALL THIS USER BE RESTRICTED? NO

:QUIT
EOJ
-
   Ì