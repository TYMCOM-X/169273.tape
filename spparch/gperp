GPERP ** FICHE/FRAME BREAK *****
GP55
GPERP  IDENT   JUN 06, 1977    VERSION 5.5
*5.2 ADDS CHA COMMAND AND XLIST COMMAND FOR PERPRAY
*2.1 ALLOWS THE USE OF THE LIST COMMAND.

*BUILD GP BY ASSEMBLING WITH NARP, LOADING WITH XDDT
* AT 240 AND SAVING WITH STARTING ADR. OF 240

BUF    EQU 20000B

START  BRM SET8LV
S      BRM SIN; BRU S
       BRM SIN; BRU S
       BRM SIN; BRU S
       BRM SIN; BRU S
       BRM SIN; BRU *+2; BRU *-2
       LDA  T2; BRU *+2
CMD    CIO CI; CIO CO; CLB; LSH 16; STA T1; CIO CI; CIO CO
       CLB; LSH 8; ADM T1; CIO CI; CIO CO
       MRG T1; STA T1
       SKE CSTA; BRU *+2; BRU STATUS
       SKE CGET; BRU *+2; BRU GET
       SKE CDEL; BRU *+2; BRU DELETE
       SKE CINS; BRU *+2; BRU INSERT
       SKE CLIS; BRU *+2; BRU DOLIST
       SKE CCSC; BRU *+2; BRU CSC
       SKE CLSC; BRU *+2; BRU LSC
       SKE XLIS; BRU *+2; BRU XXLIST
       SKE CCEX; BRU *+2; BRU CEX (CHANGE EXCEPT)
       SKE CDEX; BRU *+2; BRU DEX (DELETE EXCEPT)
       BRM SYSER

XLIS   ASC     'XXL'
CCSC   ASC     'CSC'
CLSC   ASC     'LSC'
CSTA   ASC 'STA'
CGET   ASC 'GET'
CDEL   ASC 'DEL'
CINS   ASC 'INS'
CLIS   ASC 'LIS'
CEJ    ASC 'EJB'
CNJ    ASC 'NJB'
CCEX   ASC     'CEX'
CDEX   ASC     'DEX'


*
*      LIST THAT SENDS ONLY FOR FROMCP OVER WITH TMWD
*
*GETS COMPLET JOB STATUS FOR A USER
XXLIST CIO CI; STA FROMCP; BRM GUSNM (COPY NAME WORDS.)
       LDP FNP; BRS 64; BRM SYSER
       STA FL; LDA =BUF; STA T1
       WIO FL
XXL    SKE =-1; BRU XXL01; CLA; STA* T1; BRM CLFL; BRM SENBUF; BRU EJ
XXL01  SCP FL; BRM SYSER; WIO FL; STA NPOS
       LDP P; STP P2; LDA T1; STA XPOS (SAVE IF NOTTHIS USER)
       WIO FL; STA* T1; MIN T1
       WIO FL; STA* T1; MIN T1; ETR =7777B; STA TOCPU
       WIO     FL; STA* T1; MIN T1
       WIO     FL              5/16/75 TKB FLUSH
       WIO     FL              5/16/75 TKB FLUSH
       LDA T1; MUL =3; LSH 23; SUB =1; STA P4; ADD =13; STA P4+1
XXL0   WIO FL; STA* T1; MIN T1; SKA =4B7; BRU *+2; BRU XXL0
XXL6   WIO FL; STA* T1; MIN T1; SKA =4B7; BRU *+2; BRU XXL6
XXL1   GCI P4; BRM SYSER; ETR =177B;  STA T2
       GCI P2; BRU XXL4; SKE T2; BRU XXL5; BRU XXL1
XXL5   LDA XPOS; STA T1 (ERASE THIS ONE); LDA NPOS; BRU XXL
XXL3   LDA TOCPU; SKE FROMCP; BRU XXL5; LDA NPOS; BRU XXL
XXL4   LDA T2; SKE ='   :'; BRU *+2; BRU XXL3
       SKE ='   ;'; BRU XXL5; BRU XXL3

*
*      CHANGE SCHEDULE
*
TZCPU  ZRO
CPTZ   ZRO
FROMCP ZRO
TOCPU  ZRO
HRDN   ZRO     0               HOUR DOWN
HRUP   ZRO     0               HOUR UP
SCHLEN DATA    300             LENGTH OF SCHEDULE FILE
*
*
*      THE FORMAT OF //SCHEDULE FILE IS
*
*      WORD    BIT             MEANING
*      1       0               ON SCHEDULE CHANGE, USE WORD 2,3
*      1       0               OFF USE REG TIME WORD 1
*      1       1-12            HOUR TO GO DOWN (REG)
*      1       13-23          HOUR TO COME UP (REG)
*      2       0-23            EXCEPTION DMIN TIME DOWN
*      3       0-23            EXCEPTION DMIN TIME UP
*
*      THE BEGINING WORD IS FOUND BY COMPUTER NUMBER * 3
*      WORD 0 INFORMS PJ THAT THE FILE HAS BEEN CHANGED
*
*
CSC    CIO CI; STA TOCPU (SAVE CMPTR TO LIST)
       CIO CI; STA HRDN        (HOUR DOWN)
       CIO CI; STA HRUP (HOUR UP)
       BRM OPSCH (OPEN SCHEDULE FILE); BRM SYSER
       LDA TOCPU; MUL =9; RSH 1; STB T1 (GET  CURSOR POS)
       CBA; SCP FL; BRM SYSER; WIO FL (READ WORD ONE)
       RSH 23; CLB; LSH 11; MRG HRDN; LSH 12; MRG HRUP; STA T2 (MAKE WORD ONE)
       LDA T1; MRG =4B7; SCP FL; BRM SYSER (SET CP FOR WRITE)
       LDA T2; WIO FL; LDA SCHLEN; MUL =3; RSH 1; CBA; SSP FL
       BRM SYSER; LDA =4B7; SCP FL; BRM SYSER; LDA =-1; WIO FL; BRU INS2
*
*      LIST SCHEDULE
*
LSC    CIO CI; STA TOCPU (GET CPNO); MUL =9; RSH 1; STB XPOS
       BRM OPSCH (OPEN SCHEDULE FILE); BRM SYSER
       LDA XPOS; SCP FL; BRM SYSER; WIO FL; BRM SWD (SEND WORD 1)
       WIO FL; BRM SWD (SEND WORD 2)
       WIO FL; BRM SWD (SEND WORD 3)
       BRU INS2

*
*      DELETE EXCEPTION
*
DEX    CIO CI; STA TOCPU; MUL =9; RSH 1; STB XPOS; BRM OPSCH; BRM SYSER
       LDA XPOS; SCP FL; BRM SYSER; WIO FL; ETR =37777777B; STA T1
       LDA XPOS; MRG =4B7; SCP FL; BRM SYSER; LDA T1; WIO FL
       CLA; WIO FL; WIO FL; LDA =4B7; SCP FL; BRM SYSER; LDA =-1; WIO FL
       BRU INS2
*
*      CHANGE EXCEPTION
*
CEX    CIO CI; STA TOCPU; MUL =9; RSH 1; STB XPOS
  BRM GWD; STA HRDN; BRM GWD; CAB; LDA HRDN; LDX =3200020B; BRS 176; STA HRDN
  BRM GWD; STA HRUP; BRM GWD; CAB; LDA HRUP; LDX =3200020B; BRS 176; STA HRUP
       BRM OPSCH; BRM SYSER; LDA XPOS; SCP FL; BRM SYSER
       WIO FL; MRG =4B7; STA T1; LDA XPOS; MRG =4B7; SCP FL; BRM SYSER
       LDA T1; WIO FL; LDA HRDN; WIO FL; LDA HRUP; WIO FL; LDA =4B7
       SCP FL; BRM SYSER; LDA =-1; WIO FL; BRU INS2
*INSERTS REQUEST DATA INTO FILE
INSERT BRM GWD; STA TMWD; BRM GWD; STA NXD
       BRM RQS (GET REQ. DATA)
       BRM CPYLIS
       BRM OPFL; BRU NWFL
       BRM ISZ (GET SIZE OF DATA)
       BRM IHOLE (FIND HOLE) (SETS CRP)
       BRM IIN (INSERTS DATA)
       BRM IQ (ADJUSTS QUEUE FOR DATE)
INS2   BRM CLFL
EJ     LDA CEJ
EJ1    BRM SWD; BRU CMD

NWFL   LDA =4B7; SCP FL; BRM SYSER; LDA =3; STA CRP
       WIO FL; LDA =-1; WIO FL; BRM IIN; BRU INS2

*FINDS SIZE OF DATA AND STORES IN LSZ
ISZ    ZRO; LDA LIST; SKE =0; BRU *+2; BRM SYSER
       CLA; LDX =-100; SKE LIST+100,2; BRX *-1
       CXA; ADD =107; STA LSZ
       LDA =NMT; STA T1
ISZ1   LDA* T1; SKE =0; BRU *+2; BRR ISZ
       MIN LSZ; MIN T1; BRU ISZ1

*FINDS HOLE
IHOLE  ZRO; CLA; SCP FL; BRM SYSER
IH     CLA; STA CT
IH2    BRM IGWD; BRU IH1 (EOF)
       SKE =0; BRU IH2
       RCP FL; SUB =3; STA CRP
IH3    MIN CT; LDA CT; SKE LSZ; BRU *+2; BRR IHOLE
       BRM IGWD; BRR IHOLE (EOF)
       SKE =0; BRU IH; BRU IH3
IH1    RCP FL; STA CRP; BRR IHOLE

IGWD   ZRO; SKN FL; BRU *+2; BRR IGWD
       WIO FL; SKN FL; MIN IGWD; BRR IGWD


*INSERTS DATA INTO HOLE
IIN    ZRO; LDA CRP; ADD =3; MRG =4B7; SCP FL; BRM SYSER
       LDA NXD; WIO FL; COPY XA,B
       LDA CWORD; WIO FL
       LDA     TMWD            5/16/75 TKB
       WIO     FL              5/16/75 TKB
       CLA
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
       LDX =-18
IIN1   LDP NMT+18,2; SKB =-1; BRU *+2; BRU IIN2 (NEXT IS END)
       WIO FL; BRX IIN1
       BRX IIN1; BRM SYSER
IIN2   MRG =4B7; WIO FL; LDX =-100
IIN4   LDA LIST+100,2; SKE =0; BRU *+2; BRU *+3
       WIO FL; BRX IIN4
       LDA TMWD; MRG =4B7; WIO FL; BRR IIN

*INSERT IN QUEUE
IQ     ZRO; CLA; STA PPOS; SCP FL; BRM SYSER
       WIO FL; STA NPOS; SKE =-1; BRU IQ1
IQ2    LDA PPOS
IQ2A   MRG =4B7; SCP FL; BRM SYSER
       LDA CRP; WIO FL; MRG =4B7; SCP FL; BRM SYSER
       LDA NPOS; WIO FL; BRR IQ
IQ1    SCP FL; BRM SYSER; WIO FL; STA XPOS; WIO FL
       SKG NXD; BRU *+2; BRU IQ2 (INSERTR PREV. TO THIS)
       LDA XPOS; SKE =-1; BRU IQ3 (FOLLOW Q)
*END OF Q
       XMA NPOS; BRU IQ2A
IQ3    XMA NPOS; STA PPOS; LDA NPOS; BRU IQ1
*
*      OPEN SCHEDULE FILE
*
OPSCH   ZRO
OPSCH1  LDP SCPTR; MRG =516B5; LDX =2; BRS 65; BRU OPSE
       STA FL; RSP FL; SKG =0; BRR OPSCH; MIN OPSCH; BRR OPSCH
OPSE   BRS 45; BRU OPSCH1


OPFL   ZRO
OPFL1  LDP FNP; MRG =516B5; LDX =2; BRS 65; BRU OPFE
       STA FL; RSP FL; SKG =0; BRR OPFL; MIN OPFL; BRR OPFL
OPFE   BRS 45; BRU OPFL1
CLFL   ZRO; LDA FL; BRS 20; BRR CLFL

*TYPES STATUS OF SPECIFIED JOB
STATUS  BRM CMGET; BRM CLFL
       LDA T2; BRU EJ1
STATE  BRM CLFL; LDA CNJ; BRU EJ1

CMGET  ZRO; BRM RQS; LDP FNP; BRS 64; BRM SYSER
       STA FL; BRM FJOB; BRR CMGET

*GETS ENTRY BACK FROM DPLIST
GET    BRM CMGET; LDX =-100
       WIO FL; SKA =4B7; BRU GET1
       STA LIST+100,2; BRX *-4; BRM SYSER
GET1   CLA; STA LIST+100,2; BRM CLFL
       BRM SLIST; BRU CMD

*DELETES SPECIFIED JOB FROM LIST
DELETE  BRM RQS; BRM OPFL; BRU STATE; BRM FJOB
       LDA PPOS; MRG =4B7; SCP FL; BRM SYSER; LDA NPOS; WIO FL
       LDA CRP; SCP FL; BRM SYSER; LDX =LIST; LDA =100; BIO FL; NOP
       CLA; STA LIST; STA LIST+1; STA LIST+2
       STA     LIST+3          5/16/75 TKB
       STA     LIST+4          5/16/75 TKB
       STA     LIST+5          5/16/75 TKB
       LDX =-94                5/16/75 TKB
DEL    XMA LIST+100,2; SKA =4B7; BRU DEL1
       CLA; BRX DEL; BRM SYSER
DEL1   BRX *+2; BRM SYSER
DEL3   CLA; XMA LIST+100,2; SKA =4B7; BRU DEL2
       BRX DEL3; BRM SYSER
DEL2   LDA CRP; MRG =4B7; SCP FL; BRM SYSER
       LDX =LIST; LDA =100; BIO FL; NOP
       BRM CLFL; BRU EJ

*FINDS REQUESTED JOB IN LIST
FJOB   ZRO; CLA; STA PPOS; STA CRP; SCP FL; BRM SYSER
       WIO FL; STA NPOS
FJOB1  LDA NPOS; SKE =-1; BRU *+2; BRU STATE
       LDB CRP; STB PPOS; STA CRP; SCP FL; BRM SYSER
       WIO FL; STA NPOS; WIO FL; STA T2 (DATE WORD)
       WIO FL; LDB =777B; SKM CPN; BRU FJOB1 (NOT SAME COMPUTER)
       WIO     FL              5/16/75 TKB  FLUSH
       WIO     FL              5/16/75 TKB  FLUSH
       WIO     FL              5/16/75 TKB  FLUSH
       LDA =NMT; STA T1
FJOB3  WIO FL; SKA =4B7; BRU FJOB2
       SKE* T1; BRU FJOB1
       MIN T1; BRU FJOB3
FJOB2  ETR =37777777B; SKE* T1; BRU FJOB1; BRR FJOB

*GETS COMPLET JOB STATUS FOR A USER
DOLIST BRM GUSNM (COPY NAME WORDS.)
       LDP FNP; BRS 64; BRM SYSER
       STA FL; LDA =BUF; STA T1
       WIO FL
DOL    SKE =-1; BRU DOL01; CLA; STA* T1; BRU LEN
DOL01  SCP FL; BRM SYSER; WIO FL; STA NPOS
       LDP P; STP P2; LDA T1; STA XPOS (SAVE IF NOTTHIS USER)
       WIO FL; STA* T1; MIN T1
       WIO FL; STA* T1; MIN T1
       WIO     FL              5/16/75 TKB FLUSH
       WIO     FL              5/16/75 TKB FLUSH
       WIO     FL              5/16/75 TKB FLUSH
       LDA T1; MUL =3; LSH 23; SUB =1; STA P4; ADD =13; STA P4+1
DOL0   WIO FL; STA* T1; MIN T1; SKA =4B7; BRU *+2; BRU DOL0
DOL1   GCI P4; BRM SYSER; STA T2
       GCI P2; BRU DOL4; SKE T2; BRU DOL5; BRU DOL1
DOL5   LDA XPOS; STA T1 (ERASE THIS ONE)
DOL3   LDA NPOS; BRU DOL (EQUIV. STRINGS)
DOL4   LDA T2; SKE ='   :'; BRU *+2; BRU DOL3
       SKE ='   ;'; BRU DOL5; BRU DOL3

SET8LV ZRO; LDX =-1; LDA =4B7+2B6+400B; BRS 12; BRR SET8LV

*GETS USER NAME, FIRST CHAR IS COUNT
GUSNM  ZRO; CIO CI; SKG =12; BRU *+2; BRM SYSER
       STA T1; LDA PK; STA P; STA P1
GUS1   SKR T1; BRU *+2; BRR GUSNM
       CIO CI; WCI P; BRU GUS1

SYSER  ZRO; LDA ="***"; BRM SWD; LDA SYSER; BRM SWD; BRU CMD

SWD    ZRO; STA T3; LRSH 16; CIO CO
       LSH 8; CIO CO; LSH 8; CIO CO; LDA T3; BRR SWD

GWD    ZRO; CIO CI; CLB; LSH 16; STA T1
       CIO CI; CLB; LSH 8; ADM T1
       CIO CI; MRG T1; BRR GWD

RQS    ZRO; BRM GWD; STA CWORD; ETR =777B; STA CPN
       LDA =NMT; BRM MVSUB; BRR RQS

MVSUB  ZRO; STA T2; BRM GWD; STA* T2; MIN T2; SKE =0; BRU *-4
       BRR MVSUB

CPYLIS ZRO; LDA =LIST; BRM MVSUB; BRR CPYLIS

SLIST  ZRO; LDX =-100; LDA LIST+100,2; BRM SWD
       SKE =0; BRX *-3; BRR SLIST

SBUF   ZRO; LDA =BUF; STA T2
SBUF1  LDA* T2; BRM SWD; MIN T2; SKE =0; BRU SBUF1; BRR SBUF

SIN    ZRO; LDA T2; STA T1; CIO CI; STA T2; SUB T1
       SKE =1; BRR SIN
       LDA T2; CIO CO; MIN SIN; BRR SIN
LEN    BRM CLFL; BRM SBUF; BRU EJ
*
*      THIS IS THE SAME AS SBUF, EXCEPT IT STOPS AT T1 (SENDS 0'S)
*
SENBUF ZRO; LDA =BUF; STA T2; LDA T1; SUB =BUF; BRM SWD
       SKE =0; BRU *+2; BRR SENBUF
SEND1  LDA* T2; BRM SWD; MIN T2; LDA T1; SKE T2; BRU SEND1; BRR SENBUF

FLNM   ASC '(DUPJOB)//PPLIST'
FNP    DATA 3*FLNM-1,3*FLNM-1+16
SCFLN  ASC     '(DUPJOB)//SCHEDULE'
SCPTR  DATA    3*SCFLN-1,3*SCFLN-1+18
UZ     ZRO
CT     ZRO
CRP    ZRO
FL     ZRO
LSZ    ZRO
NXD    ZRO
PPOS   ZRO
NPOS   ZRO
XPOS   ZRO
P      ZRO
P1     ZRO
P2     ZRO
P3     ZRO
P4     BSS 2
PK     DATA 3*NMT-1
T1     ZRO
T2     ZRO
T3     ZRO
CO     ZRO 1
CI     ZRO 0
CWORD  ZRO
CPN    ZRO
TMWD   ZRO

NMT    BSS 20

LIST   BSS 100


       END
GPERP ** FICHE/FRAME BREAK *****
GP55 SYMSORT

SYM.  PG.LN. IDENT.

BUF     1  8 GPERP   CCEX    2  3 GPERP   CCSC    1 33 GPERP   
CDEL    1 37 GPERP   CDEX    2  4 GPERP   CEJ     2  1 GPERP   CEX     4 12 GPERP   
CGET    1 36 GPERP   CI     10  6 GPERP   CINS    1 38 GPERP   CLFL    6 24 GPERP   
CLIS    1 39 GPERP   CLSC    1 34 GPERP   CMD     1 17 GPERP   CMGET   6 31 GPERP   
CNJ     2  2 GPERP   CO     10  5 GPERP   CPN    10  8 GPERP   CPTZ    2 37 GPERP   
CPYLIS  9  3 GPERP   CRP     9 28 GPERP   CSC     3 20 GPERP   CSTA    1 35 GPERP   
CT      9 27 GPERP   CWORD  10  7 GPERP   DEL     7 11 GPERP   DEL1    7 13 GPERP   
DEL2    7 16 GPERP   DEL3    7 14 GPERP   DELETE  7  3 GPERP   DEX     4  4 GPERP   
DOL     8  2 GPERP   DOL0    8 11 GPERP   DOL01   8  3 GPERP   DOL1    8 12 GPERP   
DOL3    8 15 GPERP   DOL4    8 16 GPERP   DOL5    8 14 GPERP   DOLIST  7 37 GPERP   
EJ      4 29 GPERP   EJ1     4 30 GPERP   FJOB    7 21 GPERP   FJOB1   7 23 GPERP   
FJOB2   7 34 GPERP   FJOB3   7 31 GPERP   FL      9 29 GPERP   FLNM    9 22 GPERP   
FNP     9 23 GPERP   FROMCP  2 38 GPERP   GET     6 35 GPERP   GET1    6 38 GPERP   
GUS1    8 24 GPERP   GUSNM   8 22 GPERP   GWD     8 32 GPERP   HRDN    3  1 GPERP   
HRUP    3  2 GPERP   IGWD    5 15 GPERP   IH      5  6 GPERP   IH1     5 13 GPERP   
IH2     5  7 GPERP   IH3     5 10 GPERP   IHOLE   5  5 GPERP   IIN     5 20 GPERP   
IIN1    5 29 GPERP   IIN2    5 32 GPERP   IIN4    5 33 GPERP   INS2    4 28 GPERP   
INSERT  4 20 GPERP   IQ      5 38 GPERP   IQ1     6  5 GPERP   IQ2     6  1 GPERP   
IQ2A    6  2 GPERP   IQ3     6 10 GPERP   ISZ     4 36 GPERP   ISZ1    5  1 GPERP   
LEN     9 14 GPERP   LIST   10 13 GPERP   LSC     3 33 GPERP   LSZ     9 30 GPERP   
MVSUB   8 39 GPERP   NMT    10 11 GPERP   NPOS    9 33 GPERP   NWFL    4 32 GPERP   
NXD     9 31 GPERP   OPFE    6 23 GPERP   OPFL    6 20 GPERP   OPFL1   6 21 GPERP   
OPSCH   6 14 GPERP   OPSCH1  6 15 GPERP   OPSE    6 17 GPERP   P       9 35 GPERP   
P1      9 36 GPERP   P2      9 37 GPERP   P3      9 38 GPERP   P4      9 39 GPERP   
PK     10  1 GPERP   PPOS    9 32 GPERP   RQS     8 36 GPERP   S       1 11 GPERP   
SBUF    9  8 GPERP   SBUF1   9  9 GPERP   SCFLN   9 24 GPERP   SCHLEN  3  3 GPERP   
SCPTR   9 25 GPERP   SENBUF  9 18 GPERP   SEND1   9 20 GPERP   SET8LV  8 19 GPERP   
SIN     9 11 GPERP   SLIST   9  5 GPERP   START   1 10 GPERP   STATE   6 29 GPERP   
STATUS  6 27 GPERP   SWD     8 29 GPERP   SYSER   8 27 GPERP   T1     10  2 GPERP   
T2     10  3 GPERP   T3     10  4 GPERP   TMWD   10  9 GPERP   TOCPU   2 39 GPERP   
TZCPU   2 36 GPERP   UZ      9 26 GPERP   XLIS    1 32 GPERP   XPOS    9 34 GPERP   
XXL     2 15 GPERP   XXL0    2 24 GPERP   XXL01   2 16 GPERP   XXL1    2 26 GPERP   
XXL3    2 29 GPERP   XXL4    2 30 GPERP   XXL5    2 28 GPERP   XXL6    2 25 GPERP   
XXLIST  2 11 GPERP   
GPERP ** FICHE/FRAME BREAK *****
BUILDGPERP
NARP
GP55
BGP


RES
XDD
;TBGP
;U%FSAV 3,3777 ON GPERP

240
"


"
CHECKSUM
.GPERP
GP55
BUILDGPERP
.
COM TEL
    