PERP ** FICHE/FRAME BREAK *****
2P
2PERP7 IDENT   SEPT 26, 1977
*6/28/74  FIXED BUG THAT GAVE 'THE FIRST' FOR 'EVERY',
*FIXED BUG THAT CAUSED ENDLESS LOOP FOR IMPOSSIBLE 'DISPLAY' COMMANDS .

*CLEANED UP CODE IN EXPRESS, REMOVED HLTS, REMOVED UNUSED CODE 6/24/74TKB

LASTDT EQU     3286            DEC. 31, 1980

MSG    MACRO D
       LDA =*+3
       BRM PMSG
X      NARG
K      NCHR D(1)
K      EQU K/3
       IF 2-X
       BRU *+K+1
       ELSF 1
       BRU D(2)
       ENDF
       ASC D(1)
       ENDM

$EXP   BRM EX
       BRU BEG

EX     ZRO
       LDA =-1
       STA T3
       LDA LIST
       SKE =0
       BRU EX1
$EXER  MSG '$NO REQUEST HAS BEEN ENTERED$/',BEG
EX1    LDA SDATE
       STA     SDATE           CLC KLUDGE TO FIX STARTING WITH
       SKG =0
       BRU EX1A
       MSG '$STARTING /'
       LDA =SDATE
       STA LP
       BRM PDATE
EX1A   LDA =LIST
       STA LP
EX2    LDA* LP
       SKE =0
       BRU *+2
       BRU EXD3A
       LDB =7B7
       SKM =1B7
       BRU EXD
       SKA =17777B
       BRU EXFD (FOUND A DATE)
EX3    SKA =2B6
       BRU EXFS
       SKA =1B6
       BRU EXFL
       MSG 'THE EARLIEST OF:$/'
EXN    MIN LP
       BRU EX2
EXFL   MSG 'THE LATEST OF:$/',EXN
EXFS   SKA =1B6
       BRU EXFA
       MSG 'DO IN ORDER: (ONCE ONLY)$/',EXN
EXFA   MSG '$EXCEPT THE FOLLOWING:$/',EXN
EXFD   SKA =4B6
       BRU EXFD2
       STA T3 (REF. SW.)
       MSG 'USING THE REFERENCE DATE OF /'
EXFD1  BRM PDATE
       BRU EXN
EXFD2  SKA =1B6
       BRU EXFST
EXFU   MSG '$UNTIL /'
       BRM UPDATE
       LDA* LP
       BRU EX3
UPDATE ZRO
       LDA UPDATE
       STA PDATE
       LDA* LP
       ADD =1
       BRU PDATE+2
EXFST  MSG '$STARTING /',EXFD1

EXD    SKM =3B7
       BRU EXDN
EXD00  LDA* LP
       SKA =7
       BRU EXDW
       SKA =7770B
       BRU EXDY
EXD0   RSH 12
       ETR =17B
       SKG =0
       BRU EXDER
       LDA* LP
       ETR =3B6
       SKG =2B6
       SKG =0
       BRU EXD01 (0 OR 3)
       MSG 'THE /'
       LDA* LP
       RSH 12
       ETR =17B
       SKE =1
       BRU EXD02
EXD03  MSG 'MONTH$/',EXD04
EXD02  BRM EXNM
       BRU EXD03
EXD01  MSG 'EVERY /'
       LDA* LP
       RSH 12
       ETR =17B
       SKG =1
       BRU EXD03
       BRM PDNO
       MSG ' MONTHS$/'
EXD04  LDA* LP
       LDB =3B6
       SKM =3B6
       BRU EXD1A
       BRU EXD3B
EXD1   LDA* LP
EXD1A  SKA =2B5
       BRU EXD2 (ONCE ONLY)
EXD3   SKA =3B6
       BRU EXD4
EXD3B  MIN LP
       LDA* LP
       SKE =0
       BRU EXD5
EXD3A  LDA =155B
       CIO COUT
EXEX   BRR EX
EXD5   MSG '$OR /',EX2
EXD4   SKA =2B6
       BRU EXD7
EXD6   MSG '$AFTER /',EXN
EXD7   MSG '$BEFORE /',EXN
EXD2   MSG '(ONCE ONLY) /',EXD3B
EXDER  MSG '$NO DAY OR MONTH INDICATED$/',EXEX
EXDW   SKA =7770B
       BRU EXDW1
       MSG '$DAY OF WEEK WITH NO COUNT$/',EXEX
EXDW1  BRM EXNMS
       LDA* LP
       ETR =7
       CAX
       LDA DWKM,2
       BRM PMSG
EXDW2  LDA* LP
       SKA =17B4
       BRU EXDW2B
       BRU EXD1
EXDW2B SKA =4B6 (END)
       BRU EXDW3
       MSG 'OF /'
EXDW2A LDA* LP
       BRU EXD0
EXDW3  MSG 'BEFORE THE END OF /',EXDW2A
EXDY   BRM EXNMS
       MSG 'DAY /',EXDW2
EXDT2  BRM PDATE

EXDT2A MIN LP
       LDA* LP
       SKE =0
       BRU EX2
       BRU EXD3A
EXDN   SKM =2B7
       BRU *+2
       BRU EXDT2
       MSG '$INVALID DATE/',EXDT2A

EXNM   ZRO
       SKE =1
       BRU EXNM1
       MSG 'FIRST /'
EXNM9  BRR EXNM
EXNM1  SKE =2
       BRU EXNM2
       MSG '2ND /',EXNM9
EXNM2  SKE =3
       BRU EXNM3
       MSG '3RD /',EXNM9
EXNM3  BRM PDNO
       MSG 'TH /',EXNM9

EXNMS  ZRO
       LDB =3B6
       SKM =3B6
       BRU *+2
       BRU EXNMS0

       SKA =2B5
       BRR EXNMS
       SKA =17B4
       BRU EXNMS1
EXNMS0 MSG 'EVERY /'
       LDA* LP
       RSH 3
       ETR =777B
       SKG =1
       BRR EXNMS
EXNMS2 BRM EXNM
       BRR EXNMS
EXNMS1 SKN T3
       BRU EXNMS4
       SKA =4B5
       BRU EXNMS4
EXNMS3 MSG 'THE /'
       LDA* LP
       RSH 3
       ETR =777B
       BRU EXNMS2
EXNMS4 SKA =17B4
       BRU EXNMS3
       BRU EXNMS0

PDATE  ZRO
       LDA* LP
       STA T1
       RSH 5
       ETR =17B
       CAX
       LDA MONM,2
       BRM PMSG
       CLA
       CIO COUT
       LDA T1
       ETR =37B
       BRM PDNO
       LDA ='   ,'
       CIO COUT
       MSG ' 19/'
       LDA* LP
       RSH 9
       ETR =17B
       SKE =17B
       SUB =99-17B-72
       ADD =99-17B
       BRM PDNO
       LDX LP
       LDB =-1
       LDA ='   ,'
       SKB 1,2
       CIO COUT
       LDA =155B
       CIO COUT
       BRR PDATE

DWKM   DATA 0,DWM,DWM1,DWM2,DWM3,DWM4,DWM5,DWM6
DWM    ASC 'SUNDAY /'
DWM1   ASC 'MONDAY /'
DWM2   ASC 'TUESDAY /'
DWM3   ASC 'WEDNESDAY /'
DWM4   ASC 'THURSDAY /'
DWM5   ASC 'FRIDAY /'
DWM6   ASC 'SATURDAY /'

MONM   DATA MM1,MM2,MM3,MM4,MM5,MM6,MM7,MM8,MM9,MM10,MM11,MM12
MM1    ASC 'JAN/'
MM2    ASC 'FEB/'
MM3    ASC 'MAR/'
MM4    ASC 'APR/'
MM5    ASC 'MAY/'
MM6    ASC 'JUN/'
MM7    ASC 'JUL/'
MM8    ASC 'AUG/'
MM9    ASC 'SEP/'
MM10   ASC 'OCT/'
MM11   ASC 'NOV/'
MM12   ASC 'DEC/'

*DISPLAY ALL DATES
$DIS   BRM DS
       BRU BEG
$TEST  BRM EX
       BRU DIS

*DISPLAYS ALL DATES
DS     ZRO
       LDA LIST
       SKE =0
       BRU *+2
       BRU EXER (NO LIST)
       BRM DSX
       TCO =155B
       BRU DSST

$DSX   ZRO
       BRM GCRDT (PUTS CURRENT DATE IN T3
       LDA SDATE
       SKA =4B7
       BRU *+3
       BRM DATCV
       SKG T3
       LDA T3
       STA CURDT
       LDX =-100
       LDA LIST+100,2
       STA TLIST+100,2
       SKE =0
       BRX *-3
       BRR DSX
DSST   BRM DS0
DSST1  SKG =LASTDT
       SKG =0
       BRU DSER
       STA CURDT
       BRM PBNDT (PRINTS DATE FOUND IN A)
       LDA =155B
       CIO COUT
       BRU DSST

$DS0   ZRO
       LDA =TLIST
       STA LP
DS1    LDA =-1
       STA REFDT
       STA UNTDT
       STA XDATE
       STA LATSW
       STA ORDSW
       STA GRPDT
       STA TCRDT
DS2    LDA* LP
       LDB =7B7
       SKM =1B7
       BRU DSD
       BRM DSF
       BRU DS2

*FLAG WORD
DSF    ZRO
       SKA =17777B
       BRU DSFD (HAVE DATE)
DS3    SKA =2B6
       BRU DSFS
       SKA =1B6
       BRU DSFL
       LDA =-1
       BRU DSFL
DSN    MIN LP
       BRR DSF
DSFL   STA LATSW
       LDA =-1
       STA ORDSW
       BRU DSN
DSFS   SKA =1B6
       BRU DSFA
       STA ORDSW
       BRU DSN
DSFA   MSG '$EXCEPT USED WITHOUT A LIST OF DATES$/',BEG

*PROCESS FLAG WORD WITH DATE
DSFD   SKA =4B6
       BRU DSFD2 (UNLESS)
       BRM DATCV
       STA REFDT
       STA TSTDT
DSFD1  LDA* LP
       BRU DS3
DSFD2  SKA =1B6
       BRU DSFST
DSFU   SKE =14017000B
       BRU *+3
       LDA =-1
       BRU *+2
       BRM DATCV
       STA UNTDT
       STA TUNDT
       BRU DSN
DSFST  BRM DATCV
       SKG CURDT
       BRU DSN
       STA TCRDT
       BRU DSN

*PROCESS DATE LIST
DSD    LDA LP
       STA BGLIS
DSD0A  LDA =-1
       STA XDATE
       STA EXCSW
DSDS   BRM DSGD (GET A GROUP DATE)
       BRU DSDEN (END OF LIST)
       SKN ORDSW
       BRU DSDS2
       BRM DSDT
       BRU DSDS
       BRU DSDS
DSDS2  LDX LP
       LDB =-1
       STB -1,2
       BRM DSDT
       BRU DSDS
       BRU DSDEN

*ERROR RETURN IF FOUND NO VALID DATE
DSDT   ZRO
       SKN TUNDT
       SKG TUNDT
       BRU *+2
       BRR DSDT
       SKG TCRDT
       BRR DSDT
       SKG CURDT
       BRR DSDT
       SKN XDATE
       BRU DSDT2
DSDT1  STA XDATE
       LDA TLP
       STA XLP
       MIN DSDT
       BRR DSDT
DSDT2  SKN LATSW
       BRU *+4
       SKG XDATE
       BRU DSDT1
       BRR DSDT
       SKG XDATE
       BRR DSDT
       BRU DSDT1

*END OF LIST
DSDEN  LDA XDATE
       SKG =0
       BRU DSDEN1 (NO DATES FROM LIST)
       STA CURDT
       SKN EXCSW
       BRU *+9
       LDA* XLP
       SKA =2B5

       BRU *+2
       BRU *+3
       LDA =-1
       STA* XLP
       LDA XDATE
       BRR DS0


*PROCESS "EXCEPT"
       SKR CURDT (TO ALLOW THIS DATE)
DSDEN2 BRM DSGD
       BRU DSEXN (END OF EXCEPT LIST)
       SUB =1
       SKE CURDT
       BRU DSDEN2
       MIN CURDT
       LDA BGLIS
       STA LP
       BRU DSD0A
DSEXN  MIN CURDT
       LDA CURDT
       BRR DS0
DSDEN1 SKN TCRDT
       BRU DSDEN4
DSDEN3 CLA
       SKN EXCSW
       BRR DS0
       LDA* LP
       SKE =0
       BRU DS1
       BRR DS0
DSDEN4 LDA TCRDT
       SKG CURDT
       BRU DSDEN3
       STA CURDT
       BRU DS0+1

*GETS DATE FROM A GROUP
DSGD   ZRO
DSGD2  LDA LP
       STA BGGRP
       LDA REFDT
       SKG =0
       LDA CURDT
       STA TSTDT
       LDA UNTDT
       STA TUNDT
       LDA =-1
       STA GRPDT
       STA AFTSW
       STA AFTDT
       STA BEFDT
       STA ASW
       STA BSW
       LDA* LP
DSGD2A SKE =-1
       BRU *+3
DSGDN  MIN LP
       BRU DSGD2
       SKE =0
       BRU *+2
       BRR DSGD (END OF LIST)
       LDB =7B7
       SKM =1B7
       BRU DSGD3
       LDB =7B6
       SKM =3B6
       BRU DSGF
       MIN LP
       MIN EXCSW (EXCEPT)
       BRR DSGD
DSGF   BRM DSF
       BRU DSGD2
DSGD3  SKN TUNDT
       BRU D14
       LDA CURDT
       SKG =LASTDT
       BRU *+2
       BRU DSGDN
       LDA* LP
DSGD3A SKM =2B7
       BRU DSGD4
*DATE TYPE WORD
       BRM DATCV
       SKG CURDT
       BRU DSGDN
       LDB LP
       STB TLP
DEX    MIN LP
       MIN DSGD
       BRR DSGD
DSGD4  SKM =3B7
       BRU ER1 (NOT VALID WORD)
       ETR =3B6
       SKG =2B6
       SKG =0
       BRU D2 (NO CONTINUATION)
       MIN AFTSW (FOR AFTER OR BEFORE)
D1     MIN LP
       LDA* LP
       SKG =0
       BRU DSGD2A (POSSIBLE CANCELLED GRP)
       LDB =7B7
       SKM =1B7
       BRU *+2
       BRU ER2
       SKM =2B7
       BRU D3 (NOT DATE WORD)
*PROCESS DATE WORD
       SKN AFTSW
       BRU D7 (FOR AFTER SOME DATE)
D11    SKA =4B6
       BRU D8 (UNTIL)
       BRM DATCV
       STA TSTDT
D12    LDA* LP
       SKA =4B5
       BRU D9 (DATE CONTINUES)
       LDA LP
       STA ENGRP
*SEARCH BACK FOR LAST FOUND DAY WORD
D10    SKR LP
       LDA* LP
       LDB =7B7
       SKM =3B7
       BRU D10
       BRU *+2
       SKR LP
       BRM GTD
       LDA LP
       SKE BGGRP
       BRU *-4
       LDA ENGRP
       STA LP
       BRU DJ4

*FOR DATE AFTER "AFTER"
D7     SKA =4B6
       BRU ER3 (CANT HAVE UNTIL)
       BRM DATCV
       STA GRPDT
       BRU D12
*POSSIBLE CONTINUTION AFTER DATE
D9     MIN LP
       LDA* LP
       LDB =7B7
       SKM =2B7
       BRU ER3 (TROUBLE)
       BRU D11
D3     SKM =3B7
       BRU ER1 (NOT VALID WORD)
*DAY WORD AFTER CONTINUATION
       SKA =4B5
       BRU D4 (CONTINUES WITH AND)
       ETR =3B6
       SKG =2B6
       SKG =0
       BRU D5 (NO MORE CONT.)
*AFTER-BEFORE CONT
       MIN AFTSW
       BRU D1
D5     LDA =-1
       STA AFTSW
       LDA LP
       STA ENGRP
       BRM GTD
D6     SKR LP
D6A    BRM GTD
       LDA LP
       SKE BGGRP
       BRU D6
       LDA ENGRP
       STA LP
       BRM DJ
       BRU D6A

D8     BRM DATCV
       SKN TUNDT
       SKG TUNDT
       STA TUNDT
       BRU D12
D4     LDA =-1
       STA AFTSW
       BRU D1

D2     BRM GTD
       BRM DJ
       BRU D2

D14    LDA CURDT
       SKG TUNDT
       BRU *+2
       BRU DSGDN (CUR.PAST UNTIL)
       LDA* LP
       BRU DSGD3A

*DATE ADJUST
DJ     ZRO
       LDA GRPDT
       SKG CURDT
       BRU DJ1
       SKN BEFDT
       BRU DJ4 (HAVE BEFORE DATE)
       STA AFTDT
       MIN BSW (FORCE BACKUP)
DJ2    LDA =-1
       STA GRPDT
       BRR DJ
DJ1    SKN AFTDT
       BRU DJ3 (HAVE AFTER DATE)
       STA BEFDT
       MIN ASW
       BRU DJ2
DJ3    LDA AFTDT
       STA GRPDT
DJ4    LDB =-1
       LDA* LP
       SKA =2B5
       BRU DJ5
       LDA GRPDT
       BRU DEX
DJ5    LDA LP
       STA TLP
       LDA GRPDT
       BRU DEX
TLP    ZRO 0
XLP    ZRO 0

DSER   MSG '$NO FURTHER DATES$/'
       BRR DS
ER1    MSG '$INVALID WORD OR GARBAGE FOUND IN LIST$/',BEG
ER2    MSG '$CONTINUATION WITH "AND-AFTER-BEFORE-ETC." DOES NOT END$/',BEG
ER3    MSG '$CONTINUATION FROM DATE WORD WITH NO DATE WORD AFTER$/',BEG

*GETS DATE FROM DAY WORD - SETS GRPDT AND RETURNS IN A
GTD    ZRO
       LDA* LP
       LDB =7B7
       SKM =3B7
       BRM HLTER
* MUST BE DAY WORD
       STA ABSW
       SKA =3B6
       BRU G1 (AFTER-BEFORE)
G01    SKN GRPDT
       BRM HLTER
* CANT ALREADY HAVE DATE UNLESS AFTER-BEFORE
       MRG =1B6
       STA ABSW (FORCE AFTER)
       SKA =170000B
       BRU G12 (HAVE MONTH)
*NON-AFTER/BEVFORE LOGIC
G03    LDA ABSW
       SKA =7
       BRU G4 (HAVE DAY OF WEEK)
       SKA =7770B
       BRU *+2
       BRM HLTER
* MUST HAVE SOMETHING TO PROCEED
*PROCESS NO. OF DAY CYCLE
       RSH 3
       ETR =777B
       STA CYCLE
       LDA TSTDT
       SKE CURDT
       BRU *+3
       LDA CYCLE
       BRU G04A
       RSH 23
       DIV CYCLE
       STB RMNDR
G04    LDA CURDT
       RSH 23
       DIV CYCLE
       CBA
       CNA
       ADD RMNDR
       LDB ABSW
       SKB =2B6
       CNA
       SKG =0
       ADD CYCLE
       SKB =2B6
       CNA
G04A   ADD CURDT
       SKN ASW
       BRU G06
       SKN BSW
       BRU G07
G05    STA GRPDT
       BRR GTD
G06    LDB ASW
       STB T2
       ADD CYCLE
       SKR T2
       BRU *-2
       BRU G05
G07    LDB BSW
       STB T2
       SUB CYCLE
       SKR T2
       BRU *-2
       BRU G05

*POSSIBLE AFTER/BEFORE FLAGS
G1     LDB =3B6
       SKM =3B6
       BRU *+2
       BRM HLTER
* SPECIAL FLAGS NOT IMPLEMENTED
*START AFTER/BEFORE PROC.
G1B    SKA =170000B
       BRU G2 (HAVE MONTH)
G1A    SKA =7
       BRU G3 (HAVE DAY OF WEEK)
       SKA =7770B
       BRU *+2
       BRM HLTER
* NOTHING LEFT
       RSH 3
       ETR =777B
       STA CYCLE
       LDB ABSW
       LDA GRPDT
       SKB =1B6
       ADD CYCLE
       SKB =2B6
       SUB CYCLE
       SKA =4B7
       CLA
       STA GRPDT
       BRR GTD
*PROC. MONTH FOR AFTER/BEFORE
G2     RSH 12
       ETR =17B
       STA T1
       LDA GRPDT
       BRM FDMN (GETS YR/MONTH)
       LDA ABSW
       SKA =1B6
       BRU G6 (AFTER)
*BEFORE
       LDA MN
       SUB T1
       SKG =-1
       BRU G7
G8     STA MN
G8A    LDA ABSW
       SKA =7777B
       BRU G9 (HAVE SOMETING ELSE)
       LDA =1
       STA DY (SET FIRST OF MONTH)
       BRM GBNDT
       STA GRPDT
       BRR GTD
G7     ADD =12
       SKR YR
       NOP
       BRU G8
G9     SKA =4B6
       BRU G10 (FROM END OF MONTH)
       CLA
       STA DY
       BRM GBNDT
       STA GRPDT (FOR REFERENCE)
       LDA =1B6 (AFT)
G9A    STA ABSW
       LDA* LP
       BRU G1A

*MONTH AFTER
G6     LDA MN
       ADD T1
       SKG =11
       BRU G8
       SUB =12
       MIN YR
       BRU G8
*PREPARE FOR BEFORE END OF MONTH BY SETTING 1ST NEXT MONTH
G10    LDA =1
       STA DY
       MIN MN
       BRM GBNDT
       STA GRPDT
       LDA =2B6
       BRU G9A

*PROC. DAY OF WEEK FOR AFTER/BEFORE
G3     BRM GDWK
       LDA GRPDT
       RSH 23
       DIV =7
       CBA
       SUB RMNDR
       LDB ABSW
       SKB =2B6
       BRU G3A (AFTER)
       SKG =-1
       ADD =7
       CNA
       ADD CYCLE
G3B    ADD GRPDT
       STA GRPDT
       BRR GTD
G3A    SKG =0
       BRU *+2
       SUB =7
       ADD CYCLE
       CNA
       BRU G3B


*PROC. DAY OF WEEK, NOT AFTER/BEFORE
G4     BRM GDWK
       LDA TSTDT
       RSH 23
       DIV CYCLE
       CBA
       STA T1
       LDA RMNDR
G4A    SKE T1
       BRU G4C
G4B    STA RMNDR
       BRU G04
G4C    SKG T1
       BRU *+2
       BRU G4B
       ADD =7
       BRU G4A

GDWK   ZRO
       RSH 3
       ETR =777B
       SKG =0
       LDA =1
       MUL =7
       LSH 23
       STA CYCLE
       LDA* LP
       ETR =7
       SUB =1
       STA RMNDR
       BRR GDWK

*PROC. MONTH
G12    RSH 12
       ETR =17B
       STA T1
       LDA CURDT
       BRM FDMN
       LDP YR
       STP CRYR
       LDA TSTDT
       BRM FDMN
       BRU G13B
G13    LDA MN
G13A   ADD T1
       SKG =11
       BRU *+3
       SUB =12
       MIN YR
       STA MN
G13B   LDA YR
       SKG CRYR
       BRU G14
G15    SKN ASW
       BRU G17
       SKN BSW
       BRU *+2
       BRU G8A
       LDA BSW
       STA T2
       LDA MN
       SUB T1
       SKR T2
       BRU *-2
       SKG =-1
       BRU *+2
       BRU G16
       ADD =12
       SKR YR
       NOP
G16    STA MN
       BRU G8A
G17    LDA ASW
       STA T2
       LDA MN
       ADD T1
       SKR T2
       BRU *-2
       SKG =11
       BRU G16
       SUB =12
       MIN YR
       BRU G16
G14    SKE CRYR
       BRU G13
       LDA MN
       SKG CRMN
       BRU *+2
       BRU G15
       SKE CRMN
       BRU G13A
       BRU G15


*TAKES BINARY DATE AND CONVERTS TO MONTH DAY YEAR
$FDMN  ZRO
       CLX
       SKG YTB+1,2
       BRU *+3
       EAX 1,2
       BRU *-3
       STX YR (0-4)
       SUB YTB,2
       CLX
       LDB     =3              LEAP YEAR TEST    TKB 07/02/75
       SKB     YR                           T    TKB 07/02/75
       BRU     FDMN1           REGULAR YEAR      TKB 07/02/75
       SKG     MTB:LY+1,2      LEAP YEAR TABLE   TKB 07/02/75
       BRU     *+3             FOUND   R         TKB 07/02/75
       EAX     1,2                               TKB 07/02/75
       BRU     *-3                               TKB 07/02/75
       STX     MN                                TKB 07/02/75
       SUB     MTB:LY,2        CACULATE DAY      TKB 07/02/75
       STA     DY                                TKB 07/02/75
       BRR     FDMN                              TKB 07/02/75
FDMN1  SKG MTB+1,2
       BRU *+3
       EAX 1,2
       BRU *-3
       STX MN (0-11)
       SUB MTB,2
       STA DY
       BRR FDMN

*TAKES YR MN DY AND CONVERTS BACK TO BINARY (SEE FDMN)
GBNDT  ZRO
       SKN YR
       BRU *+3
       CLA
       BRR GBNDT
       LDX YR
       LDA YTB,2
       CXB
       LDX MN
       SKB     =3                           TKB 07/02/75
       BRU     *+3             REGULAR YR        TKB 07/02/75
       ADD     MTB:LY,2        LEAP YEAR         TKB 07/02/75
       BRU     *+2                               TKB 07/02/75
       ADD     MTB,2                             TKB 07/02/75
       ADD     DY                                TKB 07/02/75
       BRR     GBNDT                             TKB 07/02/75

*CONVERTS DATE WORD FROM TO BINARY FROM
DATCV  ZRO
       STA T1
       RSH 9
       ETR =17B
       CAX
       LDA YTB,2
       STA T2
       CXA                                       TKB 07/02/75
       SKA     =3              LEAP YEAR TEST    TKB 07/02/75
       BRU     DATCV1          REGULAR YEAR     TKB 07/02/75
       CLA                                       TKB 07/02/75
       LSH     4                                 TKB 07/02/75
       CAX                                       TKB 07/02/75
       LDA     MTB:LY,2                          TKB 07/02/75
       ADM     T2
       BRU     DATCV2
DATCV1 CLA
       LSH 4
       CAX
       LDA MTB,2
       ADM T2
DATCV2 CLA
       LSH 5
       ADD T2
       BRR DATCV
$YTB   DATA -1,364,729,1094,1459,1825,2190,2555,2920,3286
MTB    DATA 0,31,59,90,120,151,181,212,243,273,304,334,365
MTB:LY DATA 0,31,60,91,121,152,182,213,244,274,305,335,366

*GETS CURRENT DATE FOR THIS USER'S TIME ZONE
GCRDT  ZRO
       CLX
       LDA =-1
       BRS 176 (GETS IT)
       BRM GCRDTS
       BRR GCRDT
$GCRDTS ZRO
       RSH 20
       ETR =7B
       CAX
       LDA YTB,2
       STA T3
       CXA                                       TKB 07/02/75
       SKA     =3
       BRU     GCRDT1
       CLA                     
       LSH     4               
       CAX     4               
       LDA     MTB:LY,2        
       ADM     T3
       BRU     GCRDT2
GCRDT1 CLA
       LSH 4
       CAX
       LDA MTB,2
       ADM T3
GCRDT2 CLA
       RSH 7
       DIV =1440D
       ADD T3
       STA T3
       BRR GCRDTS

*TAKES BINARY DAY AND PRINTS
$PBNDT ZRO
       STA T1
       BRM FDMN
       LDX MN
       LDA MONM,2
       BRM PMSG
       CLA
       CIO COUT
       LDA DY
       SKG =9
       TCO =0
       BRM PDNO
       LDA ='   ,'
       CIO COUT
       CLA
       CIO COUT
       LDA YR
       ADD =1972
       BRM PDNO
       CLA
       CIO COUT
       LDA T1
       RSH 23
       DIV =7
       CBX
       LDA DWKM+1,2
       BRM PMSG
       BRR PBNDT

$HLTER ZRO
       LDA HLTER
       BRM PONO
       MSG 'HALT$/',BEG
*SET BY FDMN SUB.
$YR    ZRO
$MN    ZRO
$DY    ZRO
CRYR   ZRO
CRMN   ZRO

ASW    ZRO
* AFTER SWITCH FOR MOVING DATES FORWARD
BSW    ZRO
* BACKUP SWITCH
AFTDT  ZRO
* AFTER CURRENT DATE
BEFDT  ZRO
* BEFORE CURRENT DATE
ABSW   ZRO
* AFTER-BEFORE SW. INTERNAL TO GTD SUB.
AFTSW  ZRO
* AFTER/BEFORE SW. FOR GROUP DATES
BGGRP  ZRO
* LOC. OF GROUP BEG.
BGLIS  ZRO
* LOC. OF LIST BEG.
CURDT  ZRO
* TODAYS DATE OR STARTING DATE
CYCLE  ZRO
* LENGTH OF PERIODIC CYCLE IN DAYS
ENGRP  ZRO
* LOC. OF GROUP END
EXCSW  ZRO
* "EXCEPT" SWITCH
GRPDT  ZRO
* WORKING DATE FOR GROUP
LATSW  ZRO
* "LATEST" SWITCH
ORDSW  ZRO
* "ORDER" SWITCH
REFDT  ZRO
* REFERENCE DATE
RMNDR  ZRO
* REMAINDER FOR CYCLE CALC.
TCRDT  ZRO
* TEMP. CURRENT DATE FOR "STARTING" (NO DO PAST THIS)
TSTDT  ZRO
* TEMP. START DATE FOR GROUP
TUNDT  ZRO
* TEMP. UNTIL DATE FOR GROUP
UNTDT  ZRO
* "UNTIL" DATE
XDATE  ZRO
* TRIAL DATE FOR NEXT RUN
$TLIST BSS 100


       NOLIST EXT

       END
PERP ** FICHE/FRAME BREAK *****
2P SYMSORT

SYM.  PG.LN. IDENT.

ABSW   29 10 2PERP7  AFTDT  29  6 2PERP7  AFTSW  29 12 2PERP7  
ASW    29  2 2PERP7  BEFDT  29  8 2PERP7  BGGRP  29 14 2PERP7  BGLIS  29 16 2PERP7  
BSW    29  4 2PERP7  CRMN   28 39 2PERP7  CRYR   28 38 2PERP7  CURDT  29 18 2PERP7  
CYCLE  29 20 2PERP7  D1     14 26 2PERP7  D10    15 10 2PERP7  D11    14 39 2PERP7  
D12    15  4 2PERP7  D14    16 39 2PERP7  D2     16 35 2PERP7  D3     15 38 2PERP7  
D4     16 31 2PERP7  D5     16 11 2PERP7  D6     16 16 2PERP7  D6A    16 17 2PERP7  
D7     15 26 2PERP7  D8     16 26 2PERP7  D9     15 32 2PERP7  DATCV  26 15 2PERP7  
DATCV1 26 31 2PERP7  DATCV2 26 36 2PERP7  DEX    14 16 2PERP7  DIS     8  2 2PERP7  
DJ     17  8 2PERP7  DJ1    17 19 2PERP7  DJ2    17 16 2PERP7  DJ3    17 24 2PERP7  
DJ4    17 26 2PERP7  DJ5    17 32 2PERP7  DS      8  8 2PERP7  DS0     9  3 2PERP7  
DS1     9  6 2PERP7  DS2     9 14 2PERP7  DS3     9 25 2PERP7  DSD    10 29 2PERP7  
DSD0A  10 31 2PERP7  DSDEN  11 36 2PERP7  DSDEN1 12 28 2PERP7  DSDEN2 12 16 2PERP7  
DSDEN3 12 30 2PERP7  DSDEN4 12 37 2PERP7  DSDS   10 34 2PERP7  DSDS2  11  2 2PERP7  
DSDT   11 10 2PERP7  DSDT1  11 21 2PERP7  DSDT2  11 26 2PERP7  DSER   17 39 2PERP7  
DSEXN  12 25 2PERP7  DSF     9 22 2PERP7  DSFA   10  2 2PERP7  DSFD   10  5 2PERP7  
DSFD1  10 10 2PERP7  DSFD2  10 12 2PERP7  DSFL    9 33 2PERP7  DSFS    9 37 2PERP7  
DSFST  10 22 2PERP7  DSFU   10 14 2PERP7  DSGD   13  5 2PERP7  DSGD2  13  6 2PERP7  
DSGD2A 13 22 2PERP7  DSGD3  14  1 2PERP7  DSGD3A 14  8 2PERP7  DSGD4  14 19 2PERP7  
DSGDN  13 24 2PERP7  DSGF   13 38 2PERP7  DSN     9 31 2PERP7  DSST    8 32 2PERP7  
DSST1   8 33 2PERP7  DSX     8 17 2PERP7  DWKM    7 17 2PERP7  DWM     7 18 2PERP7  
DWM1    7 19 2PERP7  DWM2    7 20 2PERP7  DWM3    7 21 2PERP7  DWM4    7 22 2PERP7  
DWM5    7 23 2PERP7  DWM6    7 24 2PERP7  DY     28 37 2PERP7  ENGRP  29 22 2PERP7  
ER1    18  2 2PERP7  ER2    18  3 2PERP7  ER3    18  4 2PERP7  EX      1 26 2PERP7  
EX1     1 33 2PERP7  EX1A    2  2 2PERP7  EX2     2  4 2PERP7  EX3     2 13 2PERP7  
EXCSW  29 24 2PERP7  EXD     3  6 2PERP7  EXD0    3 13 2PERP7  EXD00   3  8 2PERP7  
EXD01   3 31 2PERP7  EXD02   3 29 2PERP7  EXD03   3 28 2PERP7  EXD04   3 39 2PERP7  
EXD1    4  5 2PERP7  EXD1A   4  6 2PERP7  EXD2    4 22 2PERP7  EXD3    4  8 2PERP7  
EXD3A   4 14 2PERP7  EXD3B   4 10 2PERP7  EXD4    4 18 2PERP7  EXD5    4 17 2PERP7  
EXD6    4 20 2PERP7  EXD7    4 21 2PERP7  EXDER   4 23 2PERP7  EXDN    5 13 2PERP7  
EXDT2   5  6 2PERP7  EXDT2A  5  8 2PERP7  EXDW    4 24 2PERP7  EXDW1   4 27 2PERP7  
EXDW2   4 33 2PERP7  EXDW2A  5  1 2PERP7  EXDW2B  4 37 2PERP7  EXDW3   5  3 2PERP7  
EXDY    5  4 2PERP7  EXER    1 32 2PERP7  EXEX    4 16 2PERP7  EXFA    2 24 2PERP7  
EXFD    2 25 2PERP7  EXFD1   2 29 2PERP7  EXFD2   2 31 2PERP7  EXFL    2 20 2PERP7  
EXFS    2 21 2PERP7  EXFST   3  4 2PERP7  EXFU    2 33 2PERP7  EXN     2 18 2PERP7  
EXNM    5 18 2PERP7  EXNM1   5 23 2PERP7  EXNM2   5 26 2PERP7  EXNM3   5 29 2PERP7  
EXNM9   5 22 2PERP7  EXNMS   5 32 2PERP7  EXNMS0  6  3 2PERP7  EXNMS1  6 11 2PERP7  
EXNMS2  6  9 2PERP7  EXNMS3  6 15 2PERP7  EXNMS4  6 20 2PERP7  EXP     1 23 2PERP7  
FDMN   25  6 2PERP7  FDMN1  25 26 2PERP7  G01    18 16 2PERP7  G03    18 24 2PERP7  
G04    19  4 2PERP7  G04A   19 17 2PERP7  G05    19 22 2PERP7  G06    19 24 2PERP7  
G07    19 30 2PERP7  G1     19 38 2PERP7  G10    21 34 2PERP7  G12    23 22 2PERP7  
G13    23 32 2PERP7  G13A   23 33 2PERP7  G13B   23 39 2PERP7  G14    24 33 2PERP7  
G15    24  3 2PERP7  G16    24 20 2PERP7  G17    24 22 2PERP7  G1A    20  7 2PERP7  
G1B    20  5 2PERP7  G2     20 27 2PERP7  G3     22  4 2PERP7  G3A    22 20 2PERP7  
G3B    22 17 2PERP7  G4     22 29 2PERP7  G4A    22 36 2PERP7  G4B    22 38 2PERP7  
G4C    23  1 2PERP7  G6     21 26 2PERP7  G7     21 10 2PERP7  G8     21  1 2PERP7  
G8A    21  2 2PERP7  G9     21 14 2PERP7  G9A    21 21 2PERP7  GBNDT  25 36 2PERP7  
GCRDT  27  6 2PERP7  GCRDT1 27 27 2PERP7  GCRDT2 27 32 2PERP7  GCRDTS 27 12 2PERP7  
GDWK   23  7 2PERP7  GRPDT  29 26 2PERP7  GTD    18  7 2PERP7  HLTER  28 30 2PERP7  
K       1 14 2PERP7  K       1 13 2PERP7  LASTDT  1  7 2PERP7  LATSW  29 28 2PERP7  
MM1     7 27 2PERP7  MM10    7 36 2PERP7  MM11    7 37 2PERP7  MM12    7 38 2PERP7  
MM2     7 28 2PERP7  MM3     7 29 2PERP7  MM4     7 30 2PERP7  MM5     7 31 2PERP7  
MM6     7 32 2PERP7  MM7     7 33 2PERP7  MM8     7 34 2PERP7  MM9     7 35 2PERP7  
MN     28 36 2PERP7  MONM    7 26 2PERP7  MSG     1  9 2PERP7  MTB    27  2 2PERP7  
MTB:LY 27  3 2PERP7  ORDSW  29 30 2PERP7  PBNDT  28  1 2PERP7  PDATE   6 24 2PERP7  
REFDT  29 32 2PERP7  RMNDR  29 34 2PERP7  TCRDT  29 36 2PERP7  TEST    8  4 2PERP7  
TLIST  30  7 2PERP7  TLP    17 36 2PERP7  TSTDT  29 38 2PERP7  TUNDT  30  1 2PERP7  
UNTDT  30  3 2PERP7  UPDATE  2 37 2PERP7  X       1 12 2PERP7  XDATE  30  5 2PERP7  
XLP    17 37 2PERP7  YR     28 35 2PERP7  YTB    27  1 2PERP7  
PERP ** FICHE/FRAME BREAK *****
3P
3PERP7 IDENT   MAY 16, 1975

$BUF   EQU 20000B


MSG    MACRO D
       LDA =*+3
       BRM PMSG
X      NARG
K      NCHR D(1)
K      EQU K/3
       IF 2-X
       BRU *+K+1
       ELSF 1
       BRU D(2)
       ENDF
       ASC D(1)
       ENDM


*INSERTS REQUEST DATA INTO FILE
$INS1  BRM GNXD                GET NEXT DATE
       SKN CPSW
       BRU XINS
       BRM OPFL
       BRU NWFL
       BRM ISZ (GET SIZE OF DATA)
       BRM IHOLE (FIND HOLE) (SETS CRP)
       BRM IIN (INSERTS DATA)
       BRM IQ (ADJUSTS QUEUE FOR DATE)
INS2   BRM CLFL
       BRU BEG

NWFL   LDA =4B7
       SCP FL
       HLT
       LDA =3
       STA CRP
       WIO FL
       LDA =-1
       WIO FL
       BRM IIN
       BRU INS2

*FINDS SIZE OF DATA AND STORES IN LSZ
ISZ    ZRO
       LDA LIST
       SKE =0
       BRU *+2
       BRU EXER
       CLA
       LDX =-100
       SKE LIST+100,2
       BRX *-1
       CXA
       ADD =107
       STA LSZ
       LDA P1
       SUB P
       ADD =2
       RSH 23
       DIV =3
       ADM LSZ
       BRR ISZ

*FINDS HOLE
IHOLE  ZRO
       CLA
       SCP FL
       HLT
IH     CLA
       STA CT
IH2    BRM GWD
       BRU IH1 (EOF)
       SKE =0
       BRU IH2
       RCP FL
       SUB =3
       STA CRP
IH3    MIN CT
       LDA CT
       SKE LSZ
       BRU *+2
       BRR IHOLE
       BRM GWD
       BRR IHOLE (EOF)
       SKE =0
       BRU IH
       BRU IH3
IH1    RCP FL
       STA CRP
       BRR IHOLE

GWD    ZRO
       SKN FL
       BRU *+2
       BRR GWD
       WIO FL
       SKN FL
       MIN GWD
       BRR GWD

*INSERTS DATA INTO HOLE
IIN    ZRO
       LDA CRP
       ADD =3
       MRG =4B7
       SCP FL
       HLT
       LDA NXD
       WIO FL
       LDA UZ
       CLB
       LSH 12
       MRG CPN
       WIO FL
       LDA     TMWD            5/16/75 TKB
       WIO     FL
       CLA
       WIO     FL
       WIO     FL
       LDX =-18
IIN1   LDP NMT+18,2
       SKB =-1
       BRU *+2
       BRU IIN2 (NEXT IS END)
       WIO FL
       BRX IIN1
       BRX IIN1
       HLT
IIN2   MRG =4B7
       WIO FL
       LDX =-100
IIN4   LDA LIST+100,2
       SKE =0
       BRU *+2
       BRU *+3
       WIO FL
       BRX IIN4
       LDA TMWD
       MRG =4B7
       WIO FL
       BRR IIN

*INSERT IN QUEUE
IQ     ZRO
       CLA
       STA PPOS
       SCP FL
       HLT
       WIO FL
       STA NPOS
       SKE =-1
       BRU IQ1
IQ2    LDA PPOS
IQ2A   MRG =4B7
       SCP FL
       HLT
       LDA CRP
       WIO FL
       MRG =4B7
       SCP FL
       HLT
       LDA NPOS
       WIO FL
       BRR IQ
IQ1    SCP FL
       HLT
       WIO FL
       STA XPOS
       WIO FL
       SKG NXD
       BRU *+2
       BRU IQ2 (INSERTR PREV. TO THIS)
       LDA XPOS
       SKE =-1
       BRU IQ3 (FOLLOW Q)
*END OF Q
       XMA NPOS
       BRU IQ2A
IQ3    XMA NPOS
       STA PPOS
       LDA NPOS
       BRU IQ1

*GET NEXT DATE
GNXD   ZRO
       LDA TMWD
       SKA =34000077B
       BRU GNX1 (SOMETHING BESIDES ANYTIME)
GNX0   CLA (FOR MIDNIGHT)
GNX    STA NXD
       BRM DSX
       BRM DS0
       SKG =0
       BRU GNXDE
       BRM FDMN
       LDA DY
       SUB =1
       MUL =1440
       LSH 23
       ADM NXD
       LDA MN
       CLB
       LSH 16
       ADM NXD
       LDA YR
       LSH 20
       ADD NXD
       LDX =120B
       BRS 176
       STX UZ
       STA NXD
       SKN     DEFSW           IF DEFER ADD 15 MIN
       BRR GNXD
       ADD     =15

       STA     NXD
       BRR     GNXD
GNX1   SKA =2B7
       BRU GNX2 (FOR NOT)
       SKA =4B6
       BRU GNX4 (FOR BETWEEN)
       SKA =1B7
       BRU GNX0 (BEFORE)
GNX3   ETR =37B
       MUL =60
       LSH 23
       BRU GNX
GNX2   SKA =1B7
       BRU GNX3
       BRU GNX0
GNX4   LRSH 5
       BRU GNX3
GNXDE  MSG '$ERROR IN DATE ROUTINE$/',CER1

OPFL   ZRO
       LDP FNP
       MRG =116B5
       LDX =2
       BRS 65
       BRU OPFLE
       STA FL
       RSP FL
       SKG =0
       BRR OPFL
       MIN OPFL
       BRR OPFL
OPFLE  MSG 'BUSY NOW, TRY LATER/',BEG
CLFL   ZRO
       LDA FL
       BRS 20
       BRR CLFL

*TYPES STATUS OF SPECIFIED JOB
$STATUS BRM RQS
       SKN CPSW
       BRU XSTAT
       BRM CMGET
       BRM CLFL
$RSTAT MSG '$JOB TO BE DONE ON: /'
       LDA T2
       BRM PDMDT
       BRU BEG
STATE  BRM CLFL                CLOSE FILE
       BRU     BEG
$RERR  MSG '$JOB NOT IN LIST.$/',BEG

CMGET  ZRO
       LDP FNP
       BRS 64
       BRU OPFLE
       STA FL
       BRM FJOB
       BRR CMGET

*GETS ENTRY BACK FROM DPLIST
$GET   BRM RQS
       SKN CPSW
       BRU XGET
       BRM CMGET
       LDX =-100
       WIO FL
       SKA =4B7
       BRU GET1
       STA LIST+100,2
       BRX *-4
       HLT
GET1   CLA
       STA LIST+100,2
       BRM CLFL
       BRU BEG

*DELETES SPECIFIED JOB FROM LIST
$DELETE BRM RQS
       SKN CPSW
       BRU XDEL
       BRM OPFL
       BRU STATE
       BRM FJOB
       LDA PPOS
       MRG =4B7
       SCP FL
       HLT
       LDA NPOS
       WIO FL
       LDA CRP
       SCP FL
       HLT
       LDX =TLIST
       LDA =100
       BIO FL
       NOP
       CLA
       STA TLIST
       STA TLIST+1
       STA TLIST+2
       STA     TLIST+3
       STA     TLIST+4
       STA     TLIST+5
       LDX =-94
DEL    XMA TLIST+100,2
       SKA =4B7
       BRU DEL1
       CLA
       BRX DEL
       HLT
DEL1   BRX *+2
       HLT
DEL3   CLA
       XMA TLIST+100,2
       SKA =4B7
       BRU DEL2
       BRX DEL3
       HLT
DEL2   LDA CRP
       MRG =4B7
       SCP FL
       HLT
       LDX =TLIST
       LDA =100
       BIO FL
       NOP
       BRM CLFL
       MSG '$JOB DELETED.$/',BEG

*FINDS REQUESTED JOB IN LIST
FJOB   ZRO
       CLA
       STA PPOS
       STA CRP
       SCP FL
       HLT
       WIO FL
       STA NPOS
FJOB1  LDA NPOS
       SKE =-1
       BRU *+2
       BRU STATE
       LDB CRP
       STB PPOS
       STA CRP
       SCP FL
       HLT
       WIO FL
       STA NPOS
       WIO FL
       STA T2 (DATE WORD)
       WIO FL
       LDB =777B
       SKM CPN
       BRU FJOB1 (NOT SAME COMPUTER)
       LDA =NMT
       STA T1
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
FJOB3  WIO FL
       SKA =4B7
       BRU FJOB2
       SKE* T1
       BRU FJOB1
       MIN T1
       BRU FJOB3
FJOB2  ETR =37777777B
       SKE* T1
       BRU FJOB1
       BRR FJOB

*GIVEN DMIN DATE-TIME WITH YEAR, TYPES ALL BY CONV. TO BINARY
PDMDT  ZRO
       LDX =12B5
       BRS 176
       STA T1
       BRM GCRDTS
       STB T2 (MIN OF DAY)
       ADD =1
       BRM PBNDT
       LDA T2
       RSH 23
       DIV =60
       STB T1
       SKG =9
       BRU     *+2
       BRU     *+5
       CAB                     SAVE A
       LDA     =0
       CIO     COUT
       CBA                     GET A BACK
       BRM PDNO
       LDA     ='  :'
       CIO     COUT
       LDA T1
       SKG =9
       BRU     *+2
       BRU     *+3
       LDA     ='  0'
       CIO     COUT
       LDA     T1
       BRM PDNO
       BRR PDMDT

$V1    BRS     39
       CBA
       SKE     =123723B
       BRU     *+2
       BRU     *+3
       BRS 71
       BRU CMER
       SKN CPSW
       BRU XV1
       LDP FNP
       BRS 64
       BRU OPFLE
       STA FL
       WIO FL
       STA NPOS
       BRU V1B
V1A    LDA NPOS
       BRM PDNO
       CLA
       CIO COUT
       WIO FL
       STA NPOS
       WIO FL
       BRM PDMDT
       CLA
       CIO COUT
       WIO FL
       BRM PONO
       LDA =155B
       CIO COUT
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
       WIO FL
       BRM PWD
       BRU *-2
       LDA NPOS
V1B    SKE =-1
       BRU *+2
       BRU STATE
       SCP FL
       HLT
       BRU V1A
PWD    ZRO
       ABC
       BRM PCH
       BRM PCH
       BRM PCH
       BRR PWD
PCH    ZRO
       LSH 8
       ETR =177B
       CIO COUT
       SKE =155B
       BRR PCH
       MIN PWD
       BRR PWD

*GETS COMPLET JOB STATUS FOR A USER
$DOLIST BRM GUSNM
       SKN CPSW
       BRU XDOL
       LDP FNP
       BRS 64
       BRU OPFLE
       STA FL
       LDA =BUF
       STA T1
       WIO FL
DOL    SKE =-1
       BRU *+2
       BRU LEN
       SCP FL
       HLT
       WIO FL
       STA NPOS
       LDP P
       STP P2
       LDA T1
       STA XPOS (SAVE IF NOTTHIS USER)
       WIO FL
       STA* T1
       MIN T1
       WIO FL
       STA* T1
       MIN T1
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
       WIO     FL              5/16/75 TKB
       LDA T1
       MUL =3
       LSH 23
       SUB =1
       STA P4
       ADD =13
       STA P4+1
DOL0   WIO FL
       STA* T1
       MIN T1
       SKA =4B7
       BRU *+2
       BRU DOL0
DOL1   GCI P4
       BRM SYSER
       STA T2
       GCI P2
       BRU DOL4
       SKE T2
       BRU DOL5
       BRU DOL1
DOL5   LDA XPOS
       STA T1 (ERASE THIS ONE)
DOL3   LDA NPOS
       BRU DOL (EQUIV. STRINGS)
DOL4   LDA T2
       SKE ='   :'
       BRU *+2
       BRU DOL3
       SKE =33B
       BRU DOL5
       BRU DOL3

LEN    BRM CLFL
       CLA
       STA* T1
$XDOLR LDA =BUF
       SKE T1
       BRU LEN1
       MSG 'JOB LIST EMPTY/',BEG
LEN1   STA XPOS
       LDA* XPOS
       SKE =0
       BRU LEN2
       MSG 'END OF JOB/',BEG
LEN2   BRM PDMDT
       LDA =LENM1
       BRM PMSG
       MIN XPOS
       LDA* XPOS
       MIN XPOS
       ETR =777B
       BRM PDNO (COMP. NO.)
       LDA ='   ,'
       CIO COUT
       CLA
       CIO COUT
       LDA* XPOS
       MIN XPOS
       BRM PWD
       BRU *-3
       BRU LEN1+1
LENM1  ASC ', CPN: /'
SYSER  ZRO
       MSG 'UNEXPECTED NAME COMPARISON ERROR/',BEG
FLNM   ASC '(DUPJOB)//PPLIST'
FNP    DATA 3*FLNM-1,3*FLNM-1+16
$UZ    ZRO
CT     ZRO
CRP    ZRO
FL     ZRO
LSZ    ZRO
$NXD   ZRO
PPOS   ZRO
NPOS   ZRO
XPOS   ZRO

       NOLIST EXT

       END
PERP ** FICHE/FRAME BREAK *****
3P SYMSORT

SYM.  PG.LN. IDENT.

BUF     1  3 3PERP7  CLFL    7 16 3PERP7  CMGET   7 35 3PERP7  
CRP    15 35 3PERP7  CT     15 34 3PERP7  DEL     9 10 3PERP7  DEL1    9 16 3PERP7  
DEL2    9 24 3PERP7  DEL3    9 18 3PERP7  DELETE  8 22 3PERP7  DOL    13 24 3PERP7  
DOL0   14 12 3PERP7  DOL1   14 18 3PERP7  DOL3   14 28 3PERP7  DOL4   14 30 3PERP7  
DOL5   14 26 3PERP7  DOLIST 13 14 3PERP7  FJOB    9 36 3PERP7  FJOB1  10  5 3PERP7  
FJOB2  10 34 3PERP7  FJOB3  10 27 3PERP7  FL     15 36 3PERP7  FLNM   15 31 3PERP7  
FNP    15 32 3PERP7  GET     8  5 3PERP7  GET1    8 16 3PERP7  GNX     5 36 3PERP7  
GNX0    5 35 3PERP7  GNX1    6 25 3PERP7  GNX2    6 35 3PERP7  GNX3    6 31 3PERP7  
GNX4    6 38 3PERP7  GNXD    5 31 3PERP7  GNXDE   7  1 3PERP7  GWD     3 16 3PERP7  
IH      2 32 3PERP7  IH1     3 12 3PERP7  IH2     2 34 3PERP7  IH3     3  2 3PERP7  
IHOLE   2 28 3PERP7  IIN     3 26 3PERP7  IIN1    4  6 3PERP7  IIN2    4 14 3PERP7  
IIN4    4 17 3PERP7  INS1    1 22 3PERP7  INS2    1 31 3PERP7  IQ      4 29 3PERP7  
IQ1     5 11 3PERP7  IQ2     4 38 3PERP7  IQ2A    4 39 3PERP7  IQ3     5 25 3PERP7  
ISZ     2  7 3PERP7  K       1 11 3PERP7  K       1 10 3PERP7  LEN    14 38 3PERP7  
LEN1   15  6 3PERP7  LEN2   15 11 3PERP7  LENM1  15 28 3PERP7  LSZ    15 37 3PERP7  
MSG     1  6 3PERP7  NPOS   16  1 3PERP7  NWFL    1 34 3PERP7  NXD    15 38 3PERP7  
OPFL    7  3 3PERP7  OPFLE   7 15 3PERP7  PCH    13  4 3PERP7  PDMDT  11  1 3PERP7  
PPOS   15 39 3PERP7  PWD    12 37 3PERP7  RERR    7 33 3PERP7  RSTAT   7 27 3PERP7  
STATE   7 31 3PERP7  STATUS  7 22 3PERP7  SYSER  15 29 3PERP7  UZ     15 33 3PERP7  
V1     11 33 3PERP7  V1A    12 10 3PERP7  V1B    12 31 3PERP7  X       1  9 3PERP7  
XDOLR  15  2 3PERP7  XPOS   16  2 3PERP7  
PERP ** FICHE/FRAME BREAK *****
4P
4PERP7 IDENT   JUN 04, 1976

MSG    MACRO D
       LDA =*+3
       BRM PMSG
X      NARG
K      NCHR D(1)
K      EQU K/3
       IF 2-X
       BRU *+K+1
       ELSF 1
       BRU D(2)
       ENDF
       ASC D(1)
       ENDM

*INSERT
$XINS  LDA ="INS"
       BRM OPCH (SETS IF & OF)
       LDA TMWD
       BRM SWD
       LDA NXD
       BRM SWD
       BRM XRQS (SEND RQS DATA)
       BRM SLIST
XINS1  BRM GWD
       SKE ="EJB"
       BRU XER (ERROR)
       BRU BEG

*STATUS
$XSTAT LDA ="STA"
       BRM OPCH
       BRM XRQS
       BRM GWD
       SKE ="***"
       BRU *+2
       BRM SYSER
       STA T2 (DATE WORD)
       SKE ="NJB"
       BRU RSTAT
       BRU RERR

*GET
$XGET  LDA ="GET"
       BRM OPCH
       BRM XRQS
       BRM GWD
       SKE ="NJB"
       BRU *+2
       BRU RERR
       LDB =LIST
       STB T2
XGET1  STA* T2
       SKE =0
       BRU *+2
       BRU BEG
       MIN T2
       BRM GWD
       BRU XGET1

*DELETE
$XDEL  LDA ="DEL"
       BRM OPCH
       BRM XRQS
       BRM     GWD
       SNE     ="NJB"          3/12/75 TKB
       BRU     RERR
       SKE     ="EJB"          3/12/75 TKB
       BRU     XER
       BRU     BEG

*DO THE LIST COMMAND
$XDOL  LDA ="LIS"
       BRM OPCH
       BRM SUSNM (SEND USER NAME DATA)
       LDA =BUF
       STA T2
XDOL1  BRM GWD
       STA* T2
       SKE =0
       BRU *+2
       BRU XDOL2
       MIN T2
       BRU XDOL1
XDOL2  LDA T2
       STA T1
       BRM GWD
       SKE ="EJB"
       BRU XER
       BRU XDOLR

$XV1   MSG '$THIS COMMAND IS NOT AVAILABLE ON THIS COMPUTER.$/',BEG


*OPEN CHANNEL AND ESTABLISH COMM.
OPCH   ZRO
       STA CWD
       SKN CHFL
       BRU CSEND
       LDA =INT5
       STA 205B
       MSG '$BUILDING CIRCUIT TO MASTER PERP SYSTEM$/'
       LDA =1B5
       LDB =180000 (3 MIN TIMOUT)
       BRS 135
       BRS 138
       SKE =0
       BRU NOSUP (NO SUPER.)
       BRS     39
       CAB
       LDA     =LOGFOR
       SKN     USFLG
       LDA =LOGST
       SKB     =4B7
       LDA     =LOGSTI
       LDB     MDSI
       SKB     =1              MDSI?
       BRU     *+2             YES
       BRU     *+2
       LDA     =LOGMDS
       LDB =-1
       LDX =1
       BRS 34
       BRS 138
       SKE =0
       BRU OPCHER
       CXA
       SKG =1
       BRU OPCHER
       STA CHFL
       STA IF
       ADD =1
       STA OF
       CAX
       LDP PJPT
       BRS 35
       LDA =ISTR
       LDB =-1
       BRS 34
       LDX OF
       BRS 85 (SET 8 LEVEL OUT.)
*      ESTABLISH COMM. BY SENDING SEQ. AND GETTING SAME RETURN
       CLA
       STA T3 (SENDING CHAR.)
ICM    LDX =-5
       MIN T3
       LDA T3
       CIO OF
       BRX *-3
*      WAIT FOR INPUT
       LDX IF
ICM1   LDA T2
       STA T1
       BRS 13
       BRU *+3 (HAVE SOME)
       BRS 45 (WAIT 3 SEC)
       BRU ICM1-1
       CIO IF
       STA T2
       SUB T1
       SKE =1
       BRU ICM1 (NOT SEQUENTIAL)
       LDA T2 (FOR PREV. CHAR.)
ICM2   STA T1 (THIS IS PREV.)
       LDX IF
       BRS 13
       BRU ICM3 (HAVE A CHAR.)
       BRS 45
       LDX IF
       BRS 13
       BRU ICM3
       BRU ICM
ICM3   CIO IF
       STA T2
       SUB T1
       SKE =1
       BRU ICM1 (NOT SEQ.)
       LDA T2
       SKE T3
       BRU ICM2 (NOT LAST CHAR. SENT)
*      NOW SEND THE COMMAND WORD
       LDA CWD

CSEND  BRM SWD
       BRM GWD
       SKE CWD
       BRU ICM4
       CLA
       BRS 78 (CLEAR TIMER)
       BRR OPCH
*      IMPROPER RESPONSE, MAYBE ERROR MSG.
ICM4   SKE ="***"
       HLT
* TEMPORARY STOP
       BRM SYSER

*SEND WORD
SWD    ZRO
       LRSH 16
       CIO OF
       LSH 8
       CIO OF
       LSH 8
       CIO OF
       BRR SWD

*GET WORD FROM REMOTE
GWD    ZRO
       CIO IF
       CLB
       LSH 16
       STA T1
       CIO IF
       CLB
       LSH 8
       ADM T1
       CIO IF
       MRG T1
       BRR GWD

*DOES THE RQS SUBROUTINE SENDS THE DATA
XRQS   ZRO
       LDA UZ
       CLB
       LSH 12
       MRG CPN
       BRM SWD
       LDA =NMT
       BRM MVSUB
       BRR XRQS

*MOVES A LIST TO REMOTE
*EXPECTS ADDRESS IN A AND LIST ENDS WITH ZERO WORD
MVSUB  ZRO
       STA T1
       LDA* T1
       SKE =0
       BRU MVSUB1
       MSG '$INCOMPLETE INPUT DATA$/',BEG
MVSUB1 BRM SWD
       MIN T1
       LDA* T1
       SKE =0
       BRU MVSUB1
       BRM SWD
       BRR MVSUB

*SENDS THE "LIST"
SLIST  ZRO
       LDA =LIST
       BRM MVSUB
       BRR SLIST

*SEND USER NAME DATA
SUSNM  ZRO
       LDA P1
       SUB P
       CIO OF (NO. OF CHARS.)
       GCI P
       BRR SUSNM
       BRU *-3

*COMMON ERROR
XER    SKE ="***"
       BRU XER1
       BRM SYSER
XER1   SKE ="NJB"
       BRU *+2
       BRU RERR
       BRM PONO
       BRS 10
SYSER  ZRO
       BRM GWD
       BRS 156
       BRM PONO
       BRS 10

NOSUP  MSG '$NETWORK DIFFICULTY$/',BEG
OPCHER MSG '$CHANNEL ERROR$/',BEG

       ZRO
INT5   ZRO *-1
       BRS 156
       LDA =-1
       STA CHFL
       STA IF
       STA OF
       MSG '$SYSTEM TIME-OUT - PLEASE TRY LATER$/',BEG

LOGST  ASC 'DUPJOB:10-/'
LOGSTI ASC     'DUPJOB:2-/'
LOGMDS ASC     'DUPJOB:20-/'
LOGFOR ASC     'DUPJOB:14-/'
LOGPT  DATA 3*LOGST-1,3*LOGST+9
PJST   ASC 'PERP'
PJPT   DATA 3*PJPT-1,3*PJPT+3
ISTR   ASC '$1GPERP$/'

OF     ZRO
IF     ZRO
CWD    ZRO
$CHFL  DATA -1

       NOLIST EXT

       END
PERP ** FICHE/FRAME BREAK *****
4P SYMSORT

SYM.  PG.LN. IDENT.

CHFL    8 30 4PERP7  CSEND   5 29 4PERP7  CWD     8 29 4PERP7  
GWD     6 14 4PERP7  ICM     4 29 4PERP7  ICM1    4 36 4PERP7  ICM2    5  9 4PERP7  
ICM3    5 18 4PERP7  ICM4    5 37 4PERP7  IF      8 28 4PERP7  INT5    8 10 4PERP7  
ISTR    8 25 4PERP7  K       1  8 4PERP7  K       1  7 4PERP7  LOGFOR  8 21 4PERP7  
LOGMDS  8 20 4PERP7  LOGPT   8 22 4PERP7  LOGST   8 18 4PERP7  LOGSTI  8 19 4PERP7  
MSG     1  3 4PERP7  MVSUB   7  1 4PERP7  MVSUB1  7  7 4PERP7  NOSUP   8  6 4PERP7  
OF      8 27 4PERP7  OPCH    3 19 4PERP7  OPCHER  8  7 4PERP7  PJPT    8 24 4PERP7  
PJST    8 23 4PERP7  SLIST   7 16 4PERP7  SUSNM   7 22 4PERP7  SWD     6  4 4PERP7  
SYSER   7 39 4PERP7  X       1  6 4PERP7  XDEL    2 24 4PERP7  XDOL    2 35 4PERP7  
XDOL1   3  1 4PERP7  XDOL2   3  8 4PERP7  XER     7 31 4PERP7  XER1    7 34 4PERP7  
XGET    2  6 4PERP7  XGET1   2 15 4PERP7  XINS    1 18 4PERP7  XINS1   1 26 4PERP7  
XRQS    6 28 4PERP7  XSTAT   1 32 4PERP7  XV1     3 15 4PERP7  
PERP ** FICHE/FRAME BREAK *****
PSYM
PERP7  IDENT   SEP 26, 1977



*$CPNO  EQU     11              COMPUTER NO. PERP SUPPOSED TO RUN ON.
*$ICPNO EQU     2               COMPUTER INTERNALS RUN FROM.

S      MACRO D
K      NCHR D(1)
L      EQU K/3
M      EQU L*3
       IF 4-K
       ASC 'D(1)'
       DATA 4B7+136B*400B
       ELSF 1-K+M
       ASC 'D(1$1,M-3)'
A      EQU '   D(1$M-2)'
B      EQU '   D(1$M-1)'
C      EQU '   D(1$M)'
       DATA 4B7+A*2B5+B*400B+C
       ELSF 2-K+M
       ASC 'D(1$1,M)'
A      EQU '   D(1$M+1)'
       DATA 4B7+A*2B5+136B*400B
       ELSF 1
       ASC 'D(1$1,M)'
A      EQU '   D(1$M+1)'
B      EQU '   D(1$M+2)'
       DATA 4B7+A*2B5+B*400B+136B
       ENDF
       ENDM

IT     MACRO   D
XX     NARG
       IF      2-XX
       ZRO     D(1)
       ELSF    1
       VALUE   D(2)
       ENDF
       S       D(1)
       ENDM

VALUE  MACRO   D
YY     NARG
       IF      2-YY
       DATA    D(1)
       ELSF    3-YY
       D(1)B   D(2)
       ELSF    1
       D(1)B   D(2),D(3)
       ENDF
       ENDM


START  BRU *+2
       BRS 10
       STP CIN
       LDA     =MDSI
       LDX     =31
       BRS     7               READ SERIAL NUMBER
       LDA     MDSI
       SKA     =1              MDSI?
       BRU     *+2             YES , MUST CHANGE THINGS
       BRU     *+4             NO,LEAVE CPNO,ICPNO ALONE
       LDA     =20
       STA     CPNO
       STA     ICPNO
       LDA =-1
       STA CPSW (STAYS NEG IF ON CPNO COMPUTER)
*      STA CHFL (GOES POSITIVE IF AUX CIRCUIT IS MADE)
       LDA =LIST
       LDB =200B
       LDX =100B
       BRS 124
       BRS     71              CHECK FOR STATUS
       BRU     *+2             NO, HAVE TO CHECK HIM OUT
       BRU     BEG1            HE LOOKS AT THIS SYSTEM
       BRS     39
       LRSH    2
       ETR     =1777B
       STA     DISTNO
       SKG     =39             LET LOOK FOR AREA 3 (BELGIUM)
       SKG     =29             LET LOOK FOR AREA 3 (BELGIUM)
       BRU     *+2             NOPE
       BRU     P14             MUST BE SO GO TO FOREIGN
       SKG     =69
       BRU     US
       SKG     =79
       BRU     P14
       SKG     =179
       BRU     US
       SKG     =199
       BRU     P14
US     CLA
       STA     USFLG
       BRU     *+5
P14    LDA     =-1
       STA     USFLG
       LDA     =14
       STA     CPNO
       BRS     39
       SKA     =4B7
       BRU     ICHECK          INTERNAL, SEND HIM TO INTERNAL SYSTEM
       LDA LIST+75B
       LRSH 12
       SKE  CPNO
       MIN CPSW
       BRU     BEG1
ICHECK LDA     LIST+75B
       LRSH    12
       SKE      ICPNO
       MIN     CPSW            NOT ON INTERNAL SYSTEM
BEG1   CLA
       STA LIST
       LDA =4B4
       LDX =-1
       BRS 12
$BEG   LDA =ESCAPE
       STA 201B
       LDA =2B6
       BRS 78
       LDA     =4B4            TKB 2/19/75
       LDX     =-1             TKB 2/19/75
       BRS     12              TKB 2/19/75
       LDA =-1
       STA SWLWD
       TCO =155B
       TCO ='   :'
       BRM GWD
       BRU CMER
       LDA =CTBL
       BRM LKUP
       BRU CMER

       SKE =NUM
       BRU 0,2
* LET IT FALL THROUGH TO CMER
$CMER  LDP CURWP
       LDX =1
       BRS 35

        LDA =CMERM
       BRM PMSG
       LDX =-1
       BRS 11
       BRU BEG
CMERM  ASC ' IS NOT A COMMAND, TRY HELP.$/'

VERS   LDA =V
       BRM PMSG
       BRU BEG
V      ASC '$VERSION 7.12$/'
$DISTNO        ZRO
$USFLG         ZRO
HELP   LDA =HMSG
       BRM PMSG
       BRU BEG
HMSG   ASC '$TYPE FIRST 3 CHARS OF ANY OF THE FOLLOWING COMMANDS:'
 ASC '$DATES:   TAKES A DATE EXPRESSION'
 ASC '$INSERT:  INSERTS PREVIOUS DATE EXPRESSION INTO JOB LIST'
 ASC '$DISPLAY: DISPLAYS ALL DATES ASSOCIATED WITH DATE EXPRESSION'
 ASC '$EXPRESS: REPHRASES THE DATE EXPRESSION'
 ASC '$DELETE:  DELETES SPECIFIED JOB'
 ASC '$STATUS:  TYPES STATUS OF SPECIFIED JOB BELONGING TO USER'
 ASC '$TEST:    DOES BOTH EXPRESS AND DISPLAY'
 ASC '$GET:     GETS ENTRY BACK FROM JOB LIST'
 ASC '$LIST:    LISTS ALL JOBS FOR THIS USER'
 ASC '$VERSION: TYPE VERSION NO.'
 ASC '$QUIT:    RETURNS TO EXEC.'
 ASC '$HELP:    TYPES THIS MESSAGE$/'

CTBL   IT HELP
       IT      RUN
       IT DATES,REQ
       IT INSERT,INS
       IT DISPLAY,DIS
       IT EXPRESS,EXP
       IT DELETE
       IT STATUS
       IT GET
       IT TEST
       IT Q,QUIT
       IT QUIT
STAV   IT STATV1,V1
       IT LIST,DOLIST
       IT VERSION,VERS
       IT      STATV1,V1
       ZRO

*      RUN-BOTH THE DATE AND INSERT COMMANDS

RUN    BRM     REQS
       LDA     =-1
       STA     SWLWD
       BRU     INS

*REQUEST - TAKES COMPLETE "DATE" REQUEST
REQ    BRM     REQS
       BRU     BEG
REQS   ZRO
       LDA =-2
       STA ESW
       LDA =-1
       STA SWLWD (NO PERIOD YET)
       STA ANDSW
       STA SDATE
       STA NUMSW
       CLA
       LDX =-100
       STA LIST+100,2
       BRX *-1
       STA NUMBER
       LDA =LIST
       STA LP
       STA GP
       LDA =RM
       BRM PMSG
R      BRM GWD
       BRU REN
       BRM SLKUP
       BRU R
       BRM 0,2 (PROCESS WORD)
       BRU R
REN    BRM ENGRP
       BRR REQS

INS    LDA LIST
       SKE =0
       BRU INS0
       LDA =INSM
       BRU CER
INSM   ASC '$MUST USE "DATE" COMMAND FIRST.$/'
INS0   BSS 0
RT     LDA =TM
       BRM PMSG
       LDA =4B7
       STA NUMBER
       CLA
       STA TMWD
       STA     DEFSW
RT00   BRM GWD
       HLT
       LDA =TT
       BRM LKUP
*      BRU RT00
       BRU RTN                 CLC 9/23/77
       BRM 0,2
       SKN NUMBER
       BRU RTN
RT0    BRU RQ

RTN    LDA NUMBER
       SKG =23
       SKG =-1
       BRU RTNER
       LDA TC
       SKE ='   -'
       BRU RTN2
       LDA NUMBER
       CLB
       LSH 5
       STA T1
       LDA TMWD
       SKA =1740B
       BRU RTNER1
       MRG T1
       MRG =14B6 (BETWEEN)
RTN1   STA TMWD
       STA NUMBER
       BRM GWD
       HLT
       LDA =TT
       BRM LKUP
       BRU RTER
       SKN NUMBER
       BRU *+2
       BRU RTNER2
       LDA TC
       SKE ='   -'
       BRU RTN2
       LDA =NERM
       BRM PMSG
       BRU RT
RTN2   LDA NUMBER
       SKG =23
       SKG =-1
       BRU RTNER
       LDA TMWD
       SKA =37B
       BRU RTNER1
       MRG NUMBER
       STA TMWD
       BRU RT0


NOT    ZRO
       LDA TMWD
       EOR =2B7
       STA TMWD
       BRU RT00
ANY    ZRO
       BRU RT0
TBEFR  ZRO
       LDA TMWD
       MRG =1B7
       BRU RTN1
TAFTER ZRO
       LDA TMWD
       MRG =4B6
       BRU RTN1
DFRD   ZRO
       LDA =14001304B DEF TIM = 2200-400
       STA TMWD
       LDA     =-1
       STA     DEFSW
       BRU RT0

RTNER  LDA =RTNERM
       BRM PMSG
       BRU RT
RTER   ZRO
       LDA =RTERM
       BRM PMSG
       BRU RT
RTNER1 LDA =RTNR1M
       BRM PMSG
       BRU RT
RTNER2 LDA =RTNR2M
       BRM PMSG
       BRU RT
RTNERM ASC '$TIME MUST BE HOUR BETWEEN 0 AND 23$/'
RTERM  ASC '$VALID HOUR EXPRESSIONS ARE:'
       ASC '$ANY  (TO RUN ANYTIME)'
       ASC '$DEFER (TO RUN AFTER 22:00)'
       ASC '$A NUMBER 0 TO 23'
       ASC '$A RANGE AS 4-16'
       ASC '$THE WORDS "AFTER" OR "BEFORE" FOLLOWED BY 0 TO 23'
       ASC '$ OR THE WORD "NOT" BEFORE THE RANGE, "AFTER" OR "BEFORE"$/'
RTNR1M ASC '$CONFUSING TIME EXPRESSION.$/'
RTNR2M ASC '$REQUIRE NUMBER AFTER DASH$/'
NERM   ASC '$DASH CAN ONLY BE USED ONCE AS SEPARATOR IN TIME EXPRESSIONS$/'
RQ     BRM RQS
       BRU INS1

$RQS   ZRO
       LDA =CPM
       BRM PMSG
       BRM RDNO
       XAB
       SKE =155B
       BRU RQE
       STB CPN
       BRM GUSNM (GET USER NAME)
       LDA =PJM
       BRM PMSG (ASK FOR PROJECT CODE)
       LDA =155B
       CIT CIN
       BRU *+2
       BRU RQS1 (NO PROJ.CODE)
       LDA =33B
       WCI P
       LDX =-12
       BRM CIO
       SKE =155B
       BRU *+2
       BRU RQS1
       WCI P
       BRX *-5
       BRU RQE
RQS1   LDA ='   :'
       WCI P
R0     LDA =FM
       BRM PMSG
       BRM CIO
       SKE  =155B
       BRU     *+3
       BRU R0
       BRM     CIO
       WCI P
       SKE =155B
       BRU *-3
       LDP P
       STP P2
       CLA
       LDX =-6
       WCI P2
       BRX *-1
       BRR RQS
RQE    LDA =REM
       BRM PMSG
       BRU RQS+1
$GUSNM ZRO
       BRS     71
       BRU     GUSNM1
       LDA     =UNMSG
       BRM     PMSG
       LDA     =155B
       CIT     =0
       BRU     *+2
       BRU     GUSNM1
       LDA     PK
       CAB
       STP     P
       LDA     =P
       LDB     =155B
       CLX
       BRS     33
       STP     P
       BRR     GUSNM
GUSNM1 LDA =48
       LDB =NMT
       LDX =6
       BRS 67
       LDA PK
       STA P
       ADD NMP1
       SUB NMP
       STA P1
       BRR GUSNM
UNMSG  ASC     '$USER NAME: /'
REM    ASC '$INVALID TERMINATOR/'

RM     ASC '$ENTER YOUR DATE PERIOD$/'
TM     ASC '$WHAT HOUR (0 TO 23)? /'
CM     ZRO
CPM    ASC '$COMPUTER? /'
FM     ASC '$COMMAND FILE NAME? /'

PJM    ASC '$PROJECT CODE? /'

$NMT   BSS 4
NMP    ZRO
NMP1   ZRO
       BSS 13
PK     DATA 3*NMT-1
$P     ZRO
$P1    ZRO
$P2    ZRO
P3     ZRO
$P4    BSS 2
CC     ZRO
TC     ZRO
$TMWD  ZRO
STBL   ZRO
$CPSW  DATA -1                 NEG. IF ON HOME COMP.
$CPNO  DATA    10              PERP RUN S ON 11 
$ICPNO DATA    2               PERP RUNS ON 2 FOR INTERNALS
$MDSI  DATA 0                  FOR MDSI IF =1 
$DEFSW DATA    0               -1=> DEFER TIME

GWD    ZRO
       SKN SWLWD
       BRU GWD4 (FOND PERIOD)
       MIN GWD
GWD0   CLA
       STA CURW
       LDA NCWP
       STA CURWP
       STA CURWP+1
GWD1   BRM CIO
       SKE =0
       BRU *+2
       BRU GWD1
       SKE =155B
       BRU *+2
       BRU GWD1
       SKE ='  .'
       BRU *+4
       SKR GWD
       NOP
       BRU GWD4
       STA FCH
GWD2   WCI CURWP
       BRM CIO
       SKE =0
       BRU *+2
       BRU GWD4
       SKE =155B
       BRU *+2
       BRU GWD4
       SKE =152B
       BRU *+2
       BRU GWD4
       SKE ='   A'+100B
       BRU *+2
       BRU GWD5
       SKE ='   W'+100B
       BRU *+3
       TCO =74B
       BRU GWD0
       SKE ='   ,'
       BRU *+2
       BRU GWD4
       SKE ='   -'
       BRU *+2
       BRU GWD4
       SKE ='   .'
       BRU *+2
       BRU GWD3
       SKE ='   D'+100B
       BRU GWD2
GWD3   STA SWLWD
       BRU GWD4
GWD4   STA TC
       BRR GWD
GWD5   TCO =77B
       GCD CURWP
       BRU GWD0
       LDA CURWP
       SKE CURWP+1
       BRU GWD2+1
       BRU GWD0

CURW   BSS 10
SWLWD  ZRO
NCWP   DATA 3*CURW-1
CURWP  BSS 2
FCH    ZRO

SLKUP  ZRO
       LDA =ST
       BRM LKUP
       BRR SLKUP
       MIN SLKUP
       BRR SLKUP

*LOOKUPS WORD IN STRING CONTROL TABLE AND RETURNS
* BRANCH VECTOR IN X FOR LATER BRM 0,2
*ERROR RETURN IF NOT STANDARD STRING
LKUP   ZRO
       STA STBL
       LDP CURWP
       STP SVCP
       LDA FCH
       SKG ='   9'
       SKG ='   0'-1
       BRU LK (NOT NUMBER)
*PROCESS NUMBER
       SUB ='   0'
       STA NUMBER
       GCI CURWP
       HLT
LK7    GCI CURWP
       BRU LK6
       SKG ='   9'
       SKG ='   0'-1
       BRU LK6
       SUB ='   0'
       XMA NUMBER
       MUL =10
       LSH 23
       ADM NUMBER
       BRU LK7
LK6    LDX =NUM
       CXA
       MIN LKUP
       BRR LKUP
LK     LDB =-1
       LDX STBL
       LDA CURW
LK1    SKB 0,2
       BRU *+2
       BRR LKUP (NOT VALID STRING)
       SKE 1,2
       BRU LK2
       CXA
       ETR =37777B
       ADD =1
       MUL =3
       LSH 23
       SUB =1
       STA TP
       LSH 6
       STA TP+1
       LDP CURWP
       STP TP1
       BRU *+2
LK3    BRM CMP
       GCI TP
       HLT
       SKG =177B
       BRU LK3
       EOR =200B
       BRM CMP
       GCI TP
       HLT
       SKE =136B
       BRU *+2
       BRU LK4
       BRM CMP
       GCI TP
       HLT
       SKE =136B
       BRM CMP
LK4    GCI TP1
       BRU *+2
       BRU LK2 (MORE CHARS, NO MATCH)
LK5    MIN LKUP
       LDA 0,2
       STA CCM
       ETR =37777B
       CAX
       BRR LKUP
LK8    LDA CURW
       LDB =-1
LK2    EAX 1,2
       SKN 0,2
       BRU *-2
       EAX 1,2
       BRU LK1
CMP    ZRO
       STA T2
       GCI TP1
       BRU LK5
       SKE T2
       BRU LK8
       BRR CMP


*DAY OF WEEK
DWK    ZRO
       BRM DWKS
       STA T1
       LDA CCM
       RSH 15
       MRG T1
       MRG =3B7
       STA* LP
       SKN NUMSW
       BRR DWK
       LDA ESW
       SKE =-2
       BRR DWK
       LDA* LP

       MRG =2B5
       STA* LP
       BRR DWK
DWKS   ZRO
       LDA* LP
       SKE =0
       BRM DWKS1
       XMA NUMBER
       SKG =0
       LDA =1
       CLB
       LSH 3
       BRR DWKS
DWKE   LDA =DWKEM
       BRU CER
DWKEM  ASC '$CONFLICTING DAYS IN YOUR EXPRESSION$/'
DWKS1  ZRO
       MIN LP
       CLA
       BRR DWKS1

*MONTH FOR DATE
SMON   ZRO
       LDA* LP
       SKE =0
       BRU SME
       BRM GD
       MRG =2B7
       STA* LP
       LDA TC
       SKE =152B
       BRU *+3
       BRM OR
       BRR SMON
       SKE ='  ,'
       BRR SMON
       BRM OR
       BRR SMON
SME    LDA =SMEM
       BRU CER
SMEM   ASC '$DATE USED INCORRECTLY OR WITHOUT A VALID SEPARATOR'
       ASC '$LIKE "OR", ETC.$/'
*GET DATE
GD     ZRO
       LDA CCM
       ETR =77B5
       RSH 10 (MONTH)
       SKE =740B
       BRU *+3
       LDA =17001B (FOREVER)
       BRR GD
       STA T1
       BRM GDS
       SKG =31
       SKG =0
       BRU GDE1
       ADM T1
       BRM GDS
       SKG =100
       BRU *+2
       SUB =1900
       SUB =72
       SKG =16B
       SKG =-1
       BRU GDE1A
       CLB
       LSH 9
       MRG T1
       BRR GD
GDS    ZRO
       BRM GWD
       BRU GDE1
       BRM SLKUP
       BRU GDE1
       SKE =NUM
       BRU GDE1
       CLA
       XMA NUMBER
       BRR GDS
GDE1   LDA =GDE1M
       BRU CER
GDE1M  ASC '$YOUR EXPRESSION MUST BE FOLLOWED BY DATE IN FORM'
       ASC '$"MONTH DAY,YEAR"$/'
GDE1A  LDA =GDE2M
       BRU CER
GDE2M  ASC '$DATE OUT OF RANGE, MUST BE BEFORE 1986./'
*GET A NUMBER
SNUM   ZRO
       MIN NUMSW
       LDA NUMBER
       SKE =0
       BRU SNUME
       LDA CCM
       RSH 15
       STA NUMBER
       BRR SNUM
SNUME  LDA =SNEM
       BRU CER
SNEM   ASC '$TWO NUMBER EXPRESSIONS TOGETHER WITH INVALID RELATIONSHIP$/'

DAYS   ZRO
       BRM DWKS
       MRG =3B7
       STA* LP
       BRR DAYS

MONTHS ZRO
       CLA
       XMA NUMBER
       SKG =0
       LDA =1
       CLB
       LSH 12
       STA T1
       LDA* LP
       SKE =0
       BRU MON1
       LDA T1
MON2   MRG =3B7
       STA* LP
       LDA =-2
       STA ESW
       BRR MONTHS
MON1   SKA =17B4
       BRU MONE
       MRG T1
       SKN ESW
       BRU MONE1 (EVERY - EVERY NOT IMP)
       BRU MON2
MONE   LDA =MONEM
       BRU CER
MONEM  ASC '$NO VALID SEPARATOR BETWEEN TWO MONTH EXPRESSIONS$/'
MONE1  LDA =MONE1M
       BRU CER
MONE1M ASC '$UNIMPLEMENTED SYNTAX (EVERY...EVERY)$/'

*REFERENCE
SREF   ZRO
       LDA CCM
       STA T4
       ETR =7B6
SREF1  STA T3
SREF02 LDA* LP
       SKE =0
       BRU SREF2
SREF0  BRM GDATW
       LDB T4
       SKB =1B5
       SUB =1
       MRG T3
       MRG =1B7
       STA* LP
       MIN LP
       CLA
       STA* LP
       BRR SREF
SREF2  MIN LP
       BRU SREF02


GDATW  ZRO
       BRM GWD
       BRU GDE1
       BRM SLKUP
       BRU *-3
       SKE =SMON
       BRU GDE1
       BRM GD
       BRR GDATW

*AFTER/BEFORE
RLDAY  ZRO
       LDA* LP
       SKE =0
       BRU RLD
       LDA =RLDEM
CER    BRM PMSG
$CER1  LDX =-1
       BRS 11
       BRU BEG
RLDEM  ASC '$AFTER-BEFORE MUST BE PRECEEDED BY A DAY EXPRESSION$/'
RLD    ETR =73B6
       SKE =3B7
       BRU *+2
       BRU *+3
       SKE =2B7
       BRU RLDE
       LDA CCM
       ETR =3B6
       ADM* LP
       MIN LP
       CLA
       STA* LP
       BRR RLDAY
RLDE   LDA =RLDEM1
       BRU CER
RLDEM1 ASC '$AFTER-BEFORE INCONSISTENCY$/'

*SETS INITIAL FLAG WORD
SFLAG  ZRO
       LDA* LP
       SKE =0
       BRU SFLGE
       LDA CCM
       ETR =7B6
       MRG =1B7
       STA* LP
       MIN LP
       CLA
       STA* LP
       BRR SFLAG
SFLGE  MIN LP
       BRU SFLAG+1

*STARTING
STARTI ZRO
       SKN SDATE
       BRU SST (ALREADY HAVE 1 SDATE)
       LDA LP
       SKE =LIST
       BRU SST
       BRM GDATW
       STA SDATE
       BRR STARTI
SST    LDA STARTI
       STA SREF
       LDA =5B6
       STA T4
       BRU SREF1

$CPN   ZRO
$LIST  BSS 100
SVCP   BSS 2
TP     BSS 2
TP1    BSS 2
NUMBER ZRO
CCM    ZRO
$LP    ZRO
GP     ZRO
$T1    ZRO
$T2    ZRO
$T3    ZRO
$T4    ZRO
ESW    DATA    -2
$SDATE ZRO
ANDSW  DATA -1

OR     ZRO
       LDA* LP
       SKE =0
       BRU OR1
ORE    LDA =OREM
       BRU CER
OREM   ASC '$CONNECTING WORD (OR-AND) WITHOUT A DAY OR DATE BEFORE$/'
OR1    BRM ENGRP
       BRR OR

ENGRP  ZRO
       LDA =-1
       STA NUMSW
       LDA =-2
       STA ESW
       LDA NUMBER
       SKE =0
       BRU ENGE1
       SKN ANDSW
       BRU ENGE
       MIN LP
       CLA
       STA* LP
       BRR ENGRP
ENGE   LDA =-1
       STA ANDSW
       LDA =ENGEM
       BRU CER
ENGEM  ASC '$CANNOT USE "AND" BETWEEN DAY EXPRESSIONS WITHOUT'
       ASC '$TERMINATING THE GROUP EXPRESSION WITH A REFERENCE DATE$/'
ENGE1  CLA
       STA NUMBER
       LDA =ENGE1M
       BRU CER
ENGE1M ASC '$NUMBER OF WHAT$ (INAPPROPIATE NUMBER USED)$/'

EVERY  ZRO
       MIN ESW
       BRR EVERY

AND    ZRO
       LDA =-2
       STA ESW
       LDA* LP
       SKE =0
       BRU *+2
       BRU ORE
       MRG =4B5
       MIN ANDSW
       STA* LP
       MIN LP
       CLA
       STA* LP
       BRR AND

END    ZRO
       LDA* LP
       SKE =0
       BRU ENDE
       LDX LP
       LDA -1,2
       SKA =17B4
       BRU END1 (GOT MONTH)
       SKR LP
       MRG =4B6
       SKA =2B6
       EOR =2B6 (REM.BEFORE)
       STA* LP
       BRR END
END1   LDA =3B7+4B6+10B (FOR END OF MONTH)
       STA* LP
       BRR END
ENDE   LDA =ENDEM
       BRU CER
ENDEM  ASC '$YOU CANNOT USE MONTHS IN RELATION TO END OF MONTH$/'

ONCE   ZRO
       LDA* LP
       SKE =0
       BRU ONCE1
       LDA LP
       SKE =LIST
       BRU ONCE2
ONCEE  LDA =ONCEEM
       BRU CER
ONCEEM ASC '$IMPROPER USE OF END AT BEGINNING OF EXPRESSION$/'
ONCE1  MRG =2B5
       STA* LP
       BRR ONCE
ONCE2  CAX
       LDA -1,2
       MRG =2B5
       STA -1,2
       BRR ONCE

WEK    ZRO
       LDX =-5
       LDA WEKTE,2
       STA* LP
       MIN LP

       BRX *-3
       BRR WEK
WEKT   DATA 3B7+12B,3B7+13B,3B7+14B,3B7+15B,3B7+16B,0
WEKTE  EQU     WEKT+5
*LAST (FOR LAST DAY) ETC.
LAST   ZRO
       BRM GWD
       BRU LASTE
       BRM SLKUP
       BRU LASTE
       SKE =DWK
       BRU *+2
       BRU LAST1
       SKE =DAYS
       BRU LASTE
LAST1  MIN NUMSW
       BRM 0,2
       MRG =4B6
       STA* LP
       BRR LAST
LASTE  LDA =LASTEM
       BRU CER
LASTEM ASC '$"LAST" MUST BE FOLLOWED BY A DAY OF WEEK OR "DAY"$/'
TODAY  ZRO
       LDA* LP
       SKE =0
       BRU SME
       LDA =-1
       LDX =300B
       BRM GTDT
       BRR TODAY

TOMRW  ZRO
       LDA* LP
       SKE =0
       BRU SME
       LDA =-1
       LDX =300B
       BRS 176
       ADD =40B (TO TOMORROW)
       LDX =3000300B
       BRM GTDT
       BRR TOMRW
GTDT   ZRO
       BRS 176
       LRSH 5
       SUB =13777B
       MRG =2B7
       STA* LP
       BRR GTDT

QUIT   LDA =-1
       STA CHFL
       BRS 156
       BRS 10

CIO    ZRO
       STB TEMPN
       CIO CIN
       LDB CIN
       SKB =-1
       CIO COUT
       LDB TEMPN
       BRR CIO
RDNO   ZRO
       LDX CIN
       LDB =10
       BRS 38
       STP     TEMPN
       LDB     CIN
       SKB     =-1
       BRM     PDNO
       LDP     TEMPN
       BRR     RDNO
TEMPN  BSS     2
$PDNO  ZRO
       LDX COUT
       LDB =10
       BRS 36
       BRR PDNO
$PONO  ZRO
       LDX COUT
       LDB =8
       BRS 36
       BRR PONO
$PMSG  ZRO
       LDB =-1
       LDX COUT
       BRS 34
       BRR PMSG

       ZRO
ESCAPE ZRO *-1
       BRS 17
       LDX =-1
       BRS 11
       BRS 29
       TCO =155B
       BRU BEG
CIN    ZRO
CO     ZRO     1
$COUT  EQU CO
NUMSW  ZRO

*STRING TABLE
ST     IT SUNDAYS,(1,DWK)
       IT MONDAYS,(2,DWK)
       IT TUESDAYS,(3,DWK)
       IT WEDNESDAYS,(4,DWK)
       IT THURSDAYS,(5,DWK)
       IT FRIDAYS,(6,DWK)
       IT SATURDAYS,(7,DWK)
       IT JANUARY,(SMON)
       IT FEBRUARY,(1,SMON)
       IT MARCH,(2,SMON)
       IT APRIL,(3,SMON)
       IT MAY,(4,SMON)
       IT JUNE,(5,SMON)
       IT JULY,(6,SMON)
       IT AUGUST,(7,SMON)
       IT SEPTEMBER,(10,SMON)
       IT OCTOBER,(11,SMON)
       IT NOVEMBER,(12,SMON)
       IT DECEMBER,(13,SMON)
       IT DAYS
       IT MONTHS
       IT DAILY,DAYS
       IT MONTHLY,MONTHS
       IT FIRST,(1,SNUM)
       IT SECOND,(2,SNUM)
       IT OTHER,(2,SNUM)
       IT THIRD,(3,SNUM)
       IT FOURTH,(4,SNUM)
       IT FIFTH,(5,SNUM)
       IT SIXTH,(6,SNUM)
       IT SEVENTH,(7,SNUM)
       IT EIGHTH,(10,SNUM)
       IT NINTH,(11,SNUM)
       IT TENTH,(12,SNUM)
       IT EARLIEST,(SFLAG)
       IT LATEST,(10,SFLAG)
       IT UNTIL,(41,SREF)
       IT THROUGH,(40,SREF)
       IT ORDER,(20,SFLAG)
       IT AFTER,(10,RLDAY)
       IT BEFORE,(20,RLDAY)
       IT END
       IT ENDING,(40,SREF)     SAME AS THROUGH
       IT REFERENCE,SREF
       IT BASE,SREF
       IT EXCEPT,(30,SFLAG)
       IT STARTING
       IT BEGINING,STARTI
       IT AND,OR
       IT OR
       IT EVERY
       IT EACH,EVERY
       IT FOREVER,(17,SMON)
       IT LAST
       IT TODAY
       IT TOMORROW,TOMRW
       IT WEEKDAYS,WEK
       ZRO
NUM    ZRO
       BRR NUM

TT     IT ANY
       IT NOT
       IT AFTER,TAFTER
       IT BEFORE,TBEFR
       IT HELP,RTER
       IT H,RTER
       IT DEFERRED,DFRD
       ZRO

       NOLIST EXT
       END
PERP ** FICHE/FRAME BREAK *****
PSYM SYMSORT

SYM.  PG.LN. IDENT.

A       1 27 PERP7   A       1 23 PERP7   A       1 17 PERP7   
AND    24 26 PERP7   ANDSW  23 23 PERP7   ANY     8 28 PERP7   B       1 28 PERP7   
B       1 18 PERP7   BEG     4  1 PERP7   BEG1    3 35 PERP7   C       1 19 PERP7   
CC     12 12 PERP7   CCM    23 14 PERP7   CER    21 32 PERP7   CER1   21 33 PERP7   
CIN    28 30 PERP7   CIO    27 26 PERP7   CM     11 35 PERP7   CMER    4 21 PERP7   
CMERM   4 30 PERP7   CMP    16 26 PERP7   CO     28 31 PERP7   COUT   28 32 PERP7   
CPM    11 36 PERP7   CPN    23  8 PERP7   CPNO   12 17 PERP7   CPSW   12 16 PERP7   
CTBL    5 16 PERP7   CURW   14  7 PERP7   CURWP  14 10 PERP7   DAYS   19 34 PERP7   
DEFSW  12 20 PERP7   DFRD    8 38 PERP7   DISTNO  4 36 PERP7   DWK    16 36 PERP7   
DWKE   17 25 PERP7   DWKEM  17 27 PERP7   DWKS   17 15 PERP7   DWKS1  17 28 PERP7   
END    25  2 PERP7   END1   25 16 PERP7   ENDE   25 19 PERP7   ENDEM  25 21 PERP7   
ENGE   24 10 PERP7   ENGE1  24 16 PERP7   ENGE1M 24 20 PERP7   ENGEM  24 14 PERP7   
ENGRP  23 35 PERP7   ESCAPE 28 23 PERP7   ESW    23 21 PERP7   EVERY  24 22 PERP7   
FCH    14 11 PERP7   FM     11 37 PERP7   GD     18 16 PERP7   GDATW  21 16 PERP7   
GDE1   19 13 PERP7   GDE1A  19 17 PERP7   GDE1M  19 15 PERP7   GDE2M  19 19 PERP7   
GDS    19  3 PERP7   GP     23 16 PERP7   GTDT   27 13 PERP7   GUSNM  11  2 PERP7   
GUSNM1 11 20 PERP7   GWD    12 22 PERP7   GWD0   12 26 PERP7   GWD1   12 31 PERP7   
GWD2   13  5 PERP7   GWD3   13 34 PERP7   GWD4   13 36 PERP7   GWD5   13 38 PERP7   
HELP    4 38 PERP7   HMSG    5  2 PERP7   ICHECK  3 31 PERP7   ICPNO  12 18 PERP7   
INS     6 32 PERP7   INS0    6 38 PERP7   INSM    6 37 PERP7   IT      1 33 PERP7   
K       1  9 PERP7   L       1 10 PERP7   LAST   26 14 PERP7   LAST1  26 24 PERP7   
LASTE  26 29 PERP7   LASTEM 26 31 PERP7   LIST   23  9 PERP7   LK     15 12 PERP7   
LK1    15 15 PERP7   LK2    16 21 PERP7   LK3    15 32 PERP7   LK4    16 10 PERP7   
LK5    16 13 PERP7   LK6    15  8 PERP7   LK7    14 36 PERP7   LK8    16 19 PERP7   
LKUP   14 23 PERP7   LP     23 15 PERP7   M       1 11 PERP7   MDSI   12 19 PERP7   
MON1   20 18 PERP7   MON2   20 13 PERP7   MONE   20 24 PERP7   MONE1  20 27 PERP7   
MONE1M 20 29 PERP7   MONEM  20 26 PERP7   MONTHS 20  1 PERP7   NCWP   14  9 PERP7   
NERM    9 29 PERP7   NMP    12  3 PERP7   NMP1   12  4 PERP7   NMT    12  2 PERP7   
NOT     8 23 PERP7   NUM    30 16 PERP7   NUMBER 23 13 PERP7   NUMSW  28 33 PERP7   
ONCE   25 23 PERP7   ONCE1  25 33 PERP7   ONCE2  25 36 PERP7   ONCEE  25 30 PERP7   
ONCEEM 25 32 PERP7   OR     23 25 PERP7   OR1    23 32 PERP7   ORE    23 29 PERP7   
OREM   23 31 PERP7   P      12  7 PERP7   P1     12  8 PERP7   P14     3 19 PERP7   
P2     12  9 PERP7   P3     12 10 PERP7   P4     12 11 PERP7   PDNO   28  6 PERP7   
PJM    11 39 PERP7   PK     12  6 PERP7   PMSG   28 16 PERP7   PONO   28 11 PERP7   
QUIT   27 21 PERP7   R       6 23 PERP7   R0     10 21 PERP7   RDNO   27 34 PERP7   
REM    11 31 PERP7   REN     6 29 PERP7   REQ     6  3 PERP7   REQS    6  5 PERP7   
RLD    21 37 PERP7   RLDAY  21 27 PERP7   RLDE   22 11 PERP7   RLDEM  21 36 PERP7   
RLDEM1 22 13 PERP7   RM     11 33 PERP7   RQ      9 30 PERP7   RQE    10 38 PERP7   
RQS     9 33 PERP7   RQS1   10 19 PERP7   RT      6 39 PERP7   RT0     7 16 PERP7   
RT00    7  7 PERP7   RTER    9  9 PERP7   RTERM   9 20 PERP7   RTN     7 18 PERP7   
RTN1    7 34 PERP7   RTN2    8 11 PERP7   RTNER   9  6 PERP7   RTNER1  9 13 PERP7   
RTNER2  9 16 PERP7   RTNERM  9 19 PERP7   RTNR1M  9 27 PERP7   RTNR2M  9 28 PERP7   
RUN     5 36 PERP7   S       1  8 PERP7   SDATE  23 22 PERP7   SFLAG  22 16 PERP7   
SFLGE  22 28 PERP7   SLKUP  14 13 PERP7   SME    18 11 PERP7   SMEM   18 13 PERP7   
SMON   17 34 PERP7   SNEM   19 32 PERP7   SNUM   19 21 PERP7   SNUME  19 30 PERP7   
SREF   20 32 PERP7   SREF0  21  1 PERP7   SREF02 20 37 PERP7   SREF1  20 36 PERP7   
SREF2  21 12 PERP7   SST    23  2 PERP7   ST     28 36 PERP7   START   2 16 PERP7   
STARTI 22 32 PERP7   STAV    5 28 PERP7   STBL   12 15 PERP7   SVCP   23 10 PERP7   
SWLWD  14  8 PERP7   T1     23 17 PERP7   T2     23 18 PERP7   T3     23 19 PERP7   
T4     23 20 PERP7   TAFTER  8 34 PERP7   TBEFR   8 30 PERP7   TC     12 13 PERP7   
TEMPN  28  5 PERP7   TM     11 34 PERP7   TMWD   12 14 PERP7   TODAY  26 32 PERP7   
TOMRW  27  2 PERP7   TP     23 11 PERP7   TP1    23 12 PERP7   TT     30 19 PERP7   
UNMSG  11 30 PERP7   US      3 16 PERP7   USFLG   4 37 PERP7   V       4 35 PERP7   
VALUE   2  4 PERP7   VERS    4 32 PERP7   WEK    26  3 PERP7   WEKT   26 11 PERP7   
WEKTE  26 12 PERP7   XX      1 34 PERP7   YY      2  5 PERP7   
PERP ** FICHE/FRAME BREAK *****
BUILDPERP
NARP
PSYM
BP


NARP
2P
B2P


NARP
3P
B3P


NARP
4P
B4P


RES
XDDT
;TBP
;TB2P
;TB3P
;TB4P
;U
STAV+1\34632041
STAV+2\55073021
%F
SAV 3,13777 O PERP

240
DUMP DUMPPERP

DUM DEBUG

"



"
CHECKSUM
.PERP
PSYM
2P
3P
4P
BUILDPERP
.
COM T
 m 1