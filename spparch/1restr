1RESTRICT ** FICHE/FRAME BREAK *****
RESTRICT5
SETRUB IDENT   3-18-75   CONTROLS RESTRICTED USE OF SYSTEM
*ALLOWS UUN 122460 (ON LINE DECISION TO RESTRICT SYSTEM ON WHICH VALIDATED)

       BRU BEGIN; BRS 10
       ASC '$5.00'; ASC 'BLOOD'

MSG    POPD    100B5,2

       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BRS 34; BRR 0

BEGIN  STA CIN; LDA =ESCAPE; STA 201B; BRU ESCAPE+1
FRONT  CLAB; STA ERFLG; LDX =-25; STA CDBUF+25,2; BRX *-1
       LDA =4B4; LDX =-1; BRS 12
       LDP CDPTR; STP WKPTR; LDA =155B; CIO =1; LDA =32B; CIO =1
GTC    CIO CIN; SKG =77B; BRU *+2; BRU GTC1
       WCI WKPTR; BRU GTC
GTC1   LDA CDBUF; SKE =0; BRU *+2; BRU FRONT
       LDB =177777B; SKM =0; BRU *+2; BRU GTC1A
       LDB =377B; SKM =0; BRU *+2; BRU ERR; BRU GTC2
GTC1A  LRSH 16
       SKE =61B (Q); BRU *+2; BRU EN
       SKE =37B (?); BRU ERR; BRU HEL
GTC2   LDX =CTBL-CTBLND; SKE CTBLND,2
       BRX *-1; BRU* BTBLND,2
ERR    LDA ERFLG; SKE =0; BRU ERR1; MIN ERFLG; LDA =37B; CIO =1
       LDA =155B; CIO =1; CLAB; BRU FRONT+2
ERR1   MSG ERMSG1; BRU FRONT
ERMSG1 ASC '$FOR ASSISTANCE TYPE HELP$/'
CDBUF  BSS 25
CDPTR  DATA 3*CDBUF-1,3*CDBUF-1
WKPTR  BSS 2

CTBL   ASC 'STAREASETOVECLEQUIHEL'
CTBLND DATA 0,STAT,READ,SET,OVER,CLEAR,EN,HEL
BTBLND DATA ERR

VER    ZRO; SKN VERSW; BRR VER; BRS 39; CBA; SKE =122460B
*USER O5WT555STAT - ON LINE DECISIONS TO ALLOW RETRICTING DEDICATED SYS
       BRU *+2; BRU VER1C
       BRS 71; BRU VER1A
VER1C  SKR STASW; BRU *-1; MIN VERSW; BRR VER
VER1A  BRM GETUN1; LDP SFPTR; BRS 48; BRU ERV1; LDB =SFBUF
       LDX =4; BRS 96; LDA =4; SKA SFBUF+3; BRU *+2; BRU ERV2
       LDP SFPTR; BRS 64; BRU OPFER; STA SFIL; CLA; SCP SFIL; HLT 0
VER1B  MSG VM1 (ENTER NAME)
       LDA VPTR1; STA VPTR1+1; LDA =VPTR1; LDB =155B; CLX; BRS 33
       LDA VPTR1+1; SUB VPTR1; STA VT1
VER1   CIO SFIL; SKE =137B; BRU *+2; BRU NONAME
       SKE =155B; BRU VER1; CIT SFIL; BRU *+2; BRU NONAME
       LDA VPTR2; STA VPTR2+1; LDA =VPTR2; LDB =155B
       LDX SFIL; BRS 33; LDA VPTR2+1; SUB VPTR2
       SKG =0; BRU NONAME; SKE VT1; BRU VER1
       LDP VPTR2; SKSE VPTR1; BRU VER1; GCI VPTR1; HLT 0
       SKR VPTR1; NOP; STA VXT8; BRM FONO; STA STAWD; SKA =40000B
       BRU *+2; BRU ERV3; SKA =1B3; BRU *+2; BRU ERV3
       BRM FONO; STA VT1; BRM FONO; STA VT2; LDA SFIL; BRS 20
       LDX =-1; LDA =3; BRS 12; MSG VM2 (ENTER YOUR PASSWORD PLEASE)
SGPS   LDP TERMWD; STP VXT1; STP VXT3; STB VXT5
       CLA; STA TLV1; LDA VXT8; ADM VXT3
GPS    LDA =20; STA TLV11
GPS1   LDX TLV1; LDA VXT1,2; MUL =6065B
       DIV =37777761B; CBA; ADD VXT2,2; CAB; ETR =3
       STA TLV1; LDA VXT1,2; XAB; STP VXT1,2; LDA VXT5
       XMA VXT1; STA VXT5; SKR TLV11
       BRU GPS1; CIO =0; SKE =155B; BRU *+2; BRU *+3
       ADM VXT1; BRU GPS
       LDX =-1; LDA =4B4; BRS 12
       LDA VXT1; ADD VXT2; SKE VT1; BRU NOPASS; LDA VXT3; ADD VXT4
       SKE VT2; BRU NOPASS; MIN VERSW; TCO =155B; BRR VER
VERSW  DATA -1

FONO   ZRO; LDB =8; LDX SFIL; BRS 38; BRR FONO

NOPASS MSG VME5; LDX =-1; LDA =3; BRS 12; BRU SGPS

NONAME MSG VME6; BRS 10


TERMWD DATA 27657537B,55555555B

SFPTR  DATA 3*SF-1,3*SF+5
SF     ASC '()PSWD'
VPTR1   DATA 3*OPNAM-1,3*OPNAM-1
OPNAM  BSS 20
VPTR2   DATA 3*BUF2-1,3*BUF2-1
BUF2   BSS 20
SFBUF  BSS 20
VT1    ZRO
VT2    ZRO
VXT1   ZRO
VXT2   ZRO
VXT3   ZRO
VXT4   ZRO
VXT5   ZRO
VXT8   ZRO
STAWD  ZRO
SFIL   ZRO
ERV1   MSG VME1
ERV    MSG VME; BRS 10
ERV2   MSG VME2; BRU ERV
OPFER  MSG VME3; BRS 10
ERV3   MSG VME4; BRS 10

VME1   ASC '$NO PASSWORD FILE/'
VME    ASC '$NOTIFY DIVISION OPNS MGR AND SYSTEMS GROUP TECH DIV/'
VME2   ASC '$PASSWORD FILE STATUS LOST/'
VME3   ASC '$PASSWORD FILE BUSY-TRY LATER$/'
VME4   ASC '$UNVALIDATED NAME/'
VM1    ASC '$YOUR NAME: /'
VM2    ASC '$YOUR PASSWORD: /'
VME5   ASC '$ERROR, TYPE PASSWORD /'
VME6   ASC '$NAME DOES NOT APPEAR IN PASSWORD FILE$/'
*GET CURRENT CONDITION OF CELL FLAGS

GTFLG  ZRO; LDX =4; CLA; BRS 22; BRS 136; STA HFLAG; BRS 136
       BRS 23; BRR GTFLG
HFLAG  ZRO
STAT   BRM GTFLG; LDA HFLAG; SKA =1B4; BRU STATR; MSG M1 (UNRESTRICTED)
       CLA; STA STATSW; BRU SSTAT
STATR  MSG M2 (RESTRICTED)
SSTAT  LDA HFLAG; SKA SFLAG; BRU SSTATR; MSG M12; CLA; STA STATSW; BRU FRONT
SSTATR MSG M13; CLA; STA STATSW; BRU FRONT
M1     ASC '$SYSTEM UNRESTRICTED$/'
M2     ASC '$SYSTEM RESTRICTED$/'
M3     ASC '$EOJ$/'
M4     ASC '$VALID COMMANDS ARE:'
       ASC '$$STATUS      -    REPORTS THE CURRENT SYSTEM STATUS AS'
       ASC '$                 RESTRICTED OR UNRESTRICTED AND WHETHER'
       ASC '$                 PRIORITY SCHEDULING IS IN EFFECT'
       ASC '$READ        -    READS CURRENT RESTRICTION AND PRIORITY'
       ASC '$                 SCHEDULING PARAMETERS'
       ASC '$SET         -    CHANGES RESTRICTION OR PRIORITY SCHEDULING'
       ASC '$                 PARAMETERS - REQUIRES CALLER HAVE MANAGERS'
       ASC '$                 LICENSE'
       ASC '$OVERRIDE    -    SETS SWITCH TO OVERRIDE CURRENT RESTRICTION'
       ASC '$                 OR PRIORITY SCHEDULING PARAMETERS - REQUIRES'
       ASC '$                 CALLER HAVE MANAGERS LICENSE'
       ASC '$CLEAR OVERRIDE   CLEARS RESTRICTION OR PRIORITY SCHEDULING'
       ASC '$                 OVERRIDE SWITCH - REQUIRES CALLER HAVE'
       ASC '$                 MANAGERS LICENSE'
       ASC '$HELP OR ?   -    RETYPES THIS LIST'
       ASC '$QUIT OR Q   -    QUITS TO THE EXEC'
       ASC '$$ANY COMMAND MAY BE SHORTENED TO THREE CHARACTERS$$/'
M5     ASC '$NO RESTRICTION PARAMETERS SET$/'
M6     ASC '$START RESTRICTION: /'
M7     ASC '$STOP RESTRICTION: /'
M8     ASC '$OVERRIDE SWITCH SET TO RESTRICTED$/'
M9     ASC '$OVERRIDE SWITCH SET TO UNRESTRICTED$/'
M10    ASC '$OVERRIDE SWITCH NOT SET$/'
M11    ASC '$$PARAMETERS NOW SET AS FOLLOWS:$/'
M12    ASC '$INTERNAL PRIORITY NOT IN EFFECT$/'
M13    ASC '$INTERNAL PRIORITY IN EFFECT$/'
M14    ASC '$NO PRIORITY SCHEDULING PARAMETERS SET$/'
M15    ASC '$START PRIORITY SCHEDULING /'
M16    ASC '$STOP PRIORITY SCHEDULING /'
M17    ASC '$OVERIDE SWITCH SET TO PRIORITY SCHEDULING$/'
M18    ASC '$OVERIDE SWITCH SET TO NO PRIORITY SCHEDULING$/'
M19    ASC '$RESTRICTION OR PRIORITY SCHEDULING? /'
M20    ASC '$START PRIORITY SCHEDULING: /'
M21    ASC '$STOP PRIORITY SCHEDULING: /'
EN     MSG M3 (EOJ); BRS 10


*FIND OUT IF HE WANTS TO CHANGE RESTRICTION OR PRIORITY SCHEDULING
*NO SKIP RESTRICTION SKIP PRIORITY

GRP    ZRO; MSG M19; BRM CLBUF; CIO CIN; CAB; CIO CIN; SKE =155B; BRU *-2
       CBA; SNE ='R'; BRR GRP; SKE ='P'; BRU *+3; MIN GRP; BRR GRP
       LDX =-1; BRS 11; BRU GRP+1

HEL    MSG M4; BRU FRONT
GETUN1 ZRO; LDX =1; LDA =1; BRS 141; BRR GETUN1
CIN    ZRO
ERFLG  ZRO
STASW  ZRO
TLV1   ZRO
TLV11  ZRO
SFLAG  DATA 77B6
*READ DISC CONTROL BLOCK

RPAR   ZRO; LDA =PARBUF; LDB =200B; LDX =100B; BRS 124; BRR RPAR

*WRITE DISC CONTROL BLOCK

WPAR   ZRO; LDA =PARBUF; LDB =200B; LDX =100B; BRS 125; BRR WPAR

PARBUF BSS 100
SSTART EQU PARBUF+56B
SSTOP  EQU PARBUF+57B
SORIDE EQU PARBUF+60B
START  EQU PARBUF+70B
STOP   EQU PARBUF+71B
ORIDE  EQU PARBUF+72B
CZONE  EQU PARBUF+74B

READ   BRM RPAR; LDA START; SKE =0; BRU READ1; LDA STOP; SKE =0
       BRU READ1; MSG M5
READ1A LDA SSTART; SKE =0; BRU READ4; LDA SSTOP; SKE =0
       BRU READ4; MSG M14; SKN STATSW; BRU FRONT; BRU STAT
READ1  MSG M6 (START RESTRICTION); LDA START; BRM TTIM
       MSG M7 (STOP RESTRICTION); LDA STOP; BRM TTIM
       LDA ORIDE; SKE =0; BRU READ2; MSG M10; BRU READ1A
READ2  SKE =-1; BRU READ3; MSG M8; BRU READ1A
READ3  MSG M9; BRU READ1A
READ4  MSG M15; LDA SSTART; BRM TTIM; MSG M16; LDA SSTOP; BRM TTIM
       LDA SORIDE; SKE =0; BRU READ5; MSG M10
READ4A SKN STATSW; BRU FRONT; BRU STAT
READ5  SKE =-1; BRU READ6; MSG M17; BRU READ4A
READ6  MSG M18; BRU READ4A
PDNO   ZRO; STP SREG; LDB =10; LDX =1; BRS 36; LDP SREG; BRR PDNO
SREG   BSS 2
ZERO   ZRO; TCO =20B; BRR ZERO
RDNO   ZRO; LDB =10; CLX; BRS 38; BRR RDNO

*SET SYSTEM PARAMETERS

SET    BRM VER; BRM RPAR
       BRM GRP; BRU *+2; BRU SSET; MSG M6; BRM GTIM; STA START
       MSG M7; BRM GTIM; STA STOP; BRM WPAR; MSG M11; BRU READ
SSET   MSG M20; BRM GTIM; STA SSTART; MSG M21
       BRM GTIM; STA SSTOP; BRM WPAR; MSG M11; BRU READ


*GET TIME EITHER AS HHMM OR AS HH:MM

GTIM   ZRO; LDX =-5; CLA; STA TBUF+5,2; BRX *-1; STA WORK
       LDA TPTR; STA TWKPTR; STA TWKPTR+1
GTIM1  CIO =0; SKE =141B; BRU *+3; BRM TCONTA; BRU GTIM1
       SKG =77B; BRU *+2; BRU GTIM2; SKG =31B; SKG =17B
       BRU GTIM1; WCI TWKPTR; BRU GTIM1
GTIM2  GCI TWKPTR; BRU GTIM3; SUB =20B; XMA WORK; MUL =10; LSH 23; ADM WORK
       BRU GTIM2
GTIM3  LDA WORK; BRR GTIM
TBUF   BSS 5
TPTR   DATA 3*TBUF-1
TWKPTR  BSS 2
WORK   BSS 1
TCONTA  ZRO; GCD TWKPTR; NOP; TCO =76B; BRR TCONTA

CLBUF  ZRO; LDA =155B; CIT =0; BRR CLBUF; BRU *-2

TTIM   ZRO; STA TEMP; LRSH 23; DIV =10; STB MIN1
       LRSH 23; DIV =10; STB MIN
       LRSH 23; DIV =10; STP HR
       LDA HR; SKE =0; BRM PDNO; LDA HR1; BRM PDNO
       LDA ='   :'; CIO =1; LDA MIN; BRM PDNO; LDA MIN1; BRM PDNO; BRR TTIM
HR     ZRO
HR1    ZRO
MIN    ZRO
MIN1   ZRO

*SET OVERRIDE SWITCH

OVER   BRM VER; BRM RPAR; BRM FTIM; SKR STATSW; BRU *-1
       BRM GRP; BRU *+2; BRU SOVER
       SKN RPSW (NEG IF IN RESTRICTED PERIOD)
       BRU OVER1; LDA =-2; STA ORIDE; BRM WPAR; BRM CRESTR; BRU READ
OVER1  LDA =-1; STA ORIDE; BRM WPAR; BRM SRESTR; BRU READ
SOVER  SKN PPSW; BRU SOVER1; LDA =-2; STA SORIDE; BRM WPAR
       BRM CPRIOR; BRU READ
SOVER1 LDA =-1; STA SORIDE; BRM WPAR; BRM SPRIOR; BRU READ

*CLEAR OVERRIDE SWITCH

CLEAR  BRM VER; BRM RPAR; BRM FTIM; SKR STATSW; BRU *-1
       BRM GRP; BRU *+2; BRU SCLEAR; CLA; STA ORIDE
       BRM WPAR; SKN RPSW; BRU CLEAR1; BRM SRESTR; BRU READ
CLEAR1 BRM CRESTR; BRU READ
SCLEAR CLA; STA SORIDE; BRM WPAR; SKN PPSW; BRU SCL1
       BRM SPRIOR; BRU READ
SCL1   BRM CPRIOR; BRU READ

*SET SYSTEM TO UNRESTRICTED

CRESTR ZRO; BRS 22; CLA; LDX =4; BRS 136; ETR =@1B4; BRS 136; BRS 23
       BRR CRESTR

*SET SYSTEM TO RESTRICTED

SRESTR ZRO; BRS 22; CLA; LDX =4; BRS 136; MRG =1B4; BRS 136; BRS 23
       BRR SRESTR

*SET SYSTEM TO NO PRIORITY SCHEDULING

CPRIOR ZRO; BRS 22; CLA; LDX =4; BRS 136; ETR =@77B6; BRS 136; BRS 23
       BRR CPRIOR

*SET SYSTEM TO PRIORITY SCHEDULING

SPRIOR ZRO; BRS 22; CLA; LDX =4; BRS 136; MRG =77B6; BRS 136; BRS 23
       BRR SPRIOR

STATSW ZRO
FTIM   ZRO; CLA; STA RPSW; STA PPSW; LDA CZONE; CLB; LSH 12; MRG CZONE
       MRG FMATWD; STA XTIM
       CAX; LDA =-1; BRS 176; STP HDATE; ABC; LSH 14; ETR =17B; ADD =1
       STA MO; CLA; LSH 5; ADD =1; STA CDATE; CLA; LSH 5; CLB
       LRSH 23; DIV =10; STP FHR; CLA; LDB HDATE+1; LSH 6; CLB
       LRSH 23; DIV =10; STP FMIN; LDA FHR; STA HTIME
       LDA FHR+1; XMA HTIME; MUL =10; LSH 23; ADM HTIME
       LDA FMIN; XMA HTIME; MUL =10; LSH 23; ADM HTIME
       LDA FMIN+1; XMA HTIME; MUL =10; LSH 23; ADM HTIME
       BRM JULIAN; LRSH 23; DIV =7; LDA SYR
       SUB =10B; COPY AX,BA
       SKE SAT,2; BRU *+2; BRU FTIM5; SKE SUN,2; BRU *+2; BRU FTIM5
       LDA STOP; SUB START; STA TEMP; SKN TEMP; BRU *+2; BRU FTIM1
       LDA HTIME; SKG START; BRU FTIM5; SKG STOP; BRU *+2
       BRU FTIM5
FTIM3  SKR RPSW; BRU *-1; BRU FTIM6
FTIM1  LDA HTIME; SKG STOP; BRU *+2; BRU FTIM2; SKG START
       BRU FTIM3; BRU FTIM5
FTIM2  LDA START; SKG HTIME; BRU FTIM3
FTIM5  CLA; STA RPSW
FTIM6  LDA SSTOP; SUB SSTART; STA TEMP; SKN TEMP; BRU *+2; BRU FTIM7
       LDA HTIME; SKG SSTART; BRU FTIM9; SKG SSTOP; BRU *+2; BRU FTIM9
FTIM6A SKR PPSW; BRU *-1; BRR FTIM
FTIM7  LDA HTIME; SKG SSTOP; BRU *+2; BRU FTIM8; SKG SSTART
       BRU FTIM6A; BRU FTIM9
FTIM8  LDA SSTART; SKG HTIME; BRU FTIM6A
FTIM9  CLA; STA PPSW; BRR FTIM
FHR     BSS 2
FMIN    BSS 2

CDATE  ZRO
MO     ZRO
FMATWD DATA 3000300B
HDATE  BSS 2
JULIAN ZRO; SKN JULSW; BRM LYR; LDX MO; LDA MTAB,2; ADD CDATE; STA JULDAT
       BRR JULIAN
LYR    ZRO; SKR JULSW; BRU *-1; BRS 42; CXA; STA SYR; ADD =1964; LRSH 23; DIV =4
       CBA; SKE =0; BRR LYR; LDX =-10; MIN MTABE,2; BRX *-1; BRR LYR
MTAB   DATA 0,0,31,59,90,120,151,181,212,243,273,304,334
MTABE  EQU *
JULSW  ZRO
HTIME  ZRO
SYR    ZRO
XTIM   ZRO
SAT    DATA 1,6,5,4,3
SUN    DATA 2,0,6,5,4
JULDAT ZRO
RPSW   ZRO
PPSW   ZRO
TEMP   ZRO
ESCAPE ZRO ESCPC; LDA =2B6; BRS 78; BRU FRONT
ESCPC  ZRO
       END
1RESTRICT ** FICHE/FRAME BREAK *****
RESTRICT5 SYMSORT

SYM.  PG.LN. IDENT.

BEGIN   1 11 SETRUB  BTBLND  1 35 SETRUB  BUF2    3  8 SETRUB  
CDATE   9 12 SETRUB  CDBUF   1 29 SETRUB  CDPTR   1 30 SETRUB  CIN     5 17 SETRUB  
CLBUF   7  6 SETRUB  CLEAR   7 31 SETRUB  CLEAR1  7 34 SETRUB  CPRIOR  8 12 SETRUB  
CRESTR  8  2 SETRUB  CTBL    1 33 SETRUB  CTBLND  1 34 SETRUB  CZONE   5 38 SETRUB  
EN      5  5 SETRUB  ERFLG   5 18 SETRUB  ERMSG1  1 28 SETRUB  ERR     1 25 SETRUB  
ERR1    1 27 SETRUB  ERV     3 21 SETRUB  ERV1    3 20 SETRUB  ERV2    3 22 SETRUB  
ERV3    3 24 SETRUB  ESCAPE  9 32 SETRUB  ESCPC   9 33 SETRUB  FHR     9  9 SETRUB  
FMATWD  9 14 SETRUB  FMIN    9 10 SETRUB  FONO    2 33 SETRUB  FRONT   1 12 SETRUB  
FTIM    8 21 SETRUB  FTIM1   8 37 SETRUB  FTIM2   8 39 SETRUB  FTIM3   8 36 SETRUB  
FTIM5   9  1 SETRUB  FTIM6   9  2 SETRUB  FTIM6A  9  4 SETRUB  FTIM7   9  5 SETRUB  
FTIM8   9  7 SETRUB  FTIM9   9  8 SETRUB  GETUN1  5 16 SETRUB  GPS     2 21 SETRUB  
GPS1    2 22 SETRUB  GRP     5 11 SETRUB  GTC     1 15 SETRUB  GTC1    1 17 SETRUB  
GTC1A   1 20 SETRUB  GTC2    1 23 SETRUB  GTFLG   3 37 SETRUB  GTIM    6 31 SETRUB  
GTIM1   6 33 SETRUB  GTIM2   6 36 SETRUB  GTIM3   6 38 SETRUB  HDATE   9 15 SETRUB  
HEL     5 15 SETRUB  HFLAG   3 39 SETRUB  HR      7 13 SETRUB  HR1     7 14 SETRUB  
HTIME   9 23 SETRUB  JULDAT  9 28 SETRUB  JULIAN  9 16 SETRUB  JULSW   9 22 SETRUB  
LYR     9 18 SETRUB  M1      4  6 SETRUB  M10     4 32 SETRUB  M11     4 33 SETRUB  
M12     4 34 SETRUB  M13     4 35 SETRUB  M14     4 36 SETRUB  M15     4 37 SETRUB  
M16     4 38 SETRUB  M17     4 39 SETRUB  M18     5  1 SETRUB  M19     5  2 SETRUB  
M2      4  7 SETRUB  M20     5  3 SETRUB  M21     5  4 SETRUB  M3      4  8 SETRUB  
M4      4  9 SETRUB  M5      4 27 SETRUB  M6      4 28 SETRUB  M7      4 29 SETRUB  
M8      4 30 SETRUB  M9      4 31 SETRUB  MIN     7 15 SETRUB  MIN1    7 16 SETRUB  
MO      9 13 SETRUB  MSG     1  7 SETRUB  MTAB    9 20 SETRUB  MTABE   9 21 SETRUB  
NONAME  2 37 SETRUB  NOPASS  2 35 SETRUB  OPFER   3 23 SETRUB  OPNAM   3  6 SETRUB  
ORIDE   5 37 SETRUB  OVER    7 20 SETRUB  OVER1   7 24 SETRUB  PARBUF  5 31 SETRUB  
PDNO    6 15 SETRUB  PPSW    9 30 SETRUB  RDNO    6 18 SETRUB  READ    6  1 SETRUB  
READ1   6  5 SETRUB  READ1A  6  3 SETRUB  READ2   6  8 SETRUB  READ3   6  9 SETRUB  
READ4   6 10 SETRUB  READ4A  6 12 SETRUB  READ5   6 13 SETRUB  READ6   6 14 SETRUB  
RPAR    5 25 SETRUB  RPSW    9 29 SETRUB  SAT     9 26 SETRUB  SCL1    7 37 SETRUB  
SCLEAR  7 35 SETRUB  SET     6 22 SETRUB  SF      3  4 SETRUB  SFBUF   3  9 SETRUB  
SFIL    3 19 SETRUB  SFLAG   5 22 SETRUB  SFPTR   3  3 SETRUB  SGPS    2 19 SETRUB  
SORIDE  5 34 SETRUB  SOVER   7 25 SETRUB  SOVER1  7 27 SETRUB  SPRIOR  8 17 SETRUB  
SREG    6 16 SETRUB  SRESTR  8  7 SETRUB  SSET    6 25 SETRUB  SSTART  5 32 SETRUB  
SSTAT   4  4 SETRUB  SSTATR  4  5 SETRUB  SSTOP   5 33 SETRUB  START   5 35 SETRUB  
STASW   5 19 SETRUB  STAT    4  1 SETRUB  STATR   4  3 SETRUB  STATSW  8 20 SETRUB  
STAWD   3 18 SETRUB  STOP    5 36 SETRUB  SUN     9 27 SETRUB  SYR     9 24 SETRUB  
TBUF    6 39 SETRUB  TCONTA  7  4 SETRUB  TEMP    9 31 SETRUB  TERMWD  3  1 SETRUB  
TLV1    5 20 SETRUB  TLV11   5 21 SETRUB  TPTR    7  1 SETRUB  TTIM    7  8 SETRUB  
TWKPTR  7  2 SETRUB  VER     1 37 SETRUB  VER1    2  9 SETRUB  VER1A   2  3 SETRUB  
VER1B   2  6 SETRUB  VER1C   2  2 SETRUB  VERSW   2 31 SETRUB  VM1     3 31 SETRUB  
VM2     3 32 SETRUB  VME     3 27 SETRUB  VME1    3 26 SETRUB  VME2    3 28 SETRUB  
VME3    3 29 SETRUB  VME4    3 30 SETRUB  VME5    3 33 SETRUB  VME6    3 34 SETRUB  
VPTR1   3  5 SETRUB  VPTR2   3  7 SETRUB  VT1     3 10 SETRUB  VT2     3 11 SETRUB  
VXT1    3 12 SETRUB  VXT2    3 13 SETRUB  VXT3    3 14 SETRUB  VXT4    3 15 SETRUB  
VXT5    3 16 SETRUB  VXT8    3 17 SETRUB  WKPTR   1 31 SETRUB  WORK    7  3 SETRUB  
WPAR    5 29 SETRUB  XTIM    9 25 SETRUB  ZERO    6 17 SETRUB  
1RESTRICT ** FICHE/FRAME BREAK *****
MAKERESTRICT5
NARP
RESTRICT5
BRESTRICT5



RES
XDDT
;TBRESTRICT5
;U%F
SAVE 3 T 3777 ON GRESTRICT5

240
"

       1RESTRICT   VERSION 5

"
CHECKSUM
.
(SS940UTIL)RESTRICT5-SYMBOLICS
(SS940UTIL)GRESTRICT5-GO-FILE
(SS940UTIL)MAKERESTRICT5-COMMAND-FILE
.
"

"
COM TEL
   