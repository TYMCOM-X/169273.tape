1LETTER ** FICHE/FRAME BREAK *****
LETTER69
LETTER IDENT  22-NOV-78 RKS
TEST   EQU    0;*IF 0,WRITE DISC,IF TEST=-1 NO WRITE
*      MODIFIED 11-78 RON SALTGAVER - JUST TABLE CHANGES ADD 15 SUB 11
*      MODIFIED 2-76 G BLOOD
*      REMOVED COMMAND COMPLETION AND ADDED COMMAND EDITING
*      INCREASED MAX DIST NUMBER TO 255 (FORMERLY 99)
*      MODIFIED 2-74 G BLOOD
*      CHANGES:
*      ADDED EXPIRATION DATE TO LETTERS
*      ADDED COMMANDS TO READ EXPIRATION DATE AND GIVE NEW EX. DATE
*      MODIFIED 7-72 M MARRAH
*      CHANGES:
*      READ LUD INSTEAD OF UAD
*      SEND TO DISTRICT,ACCOUNT   BILLABLE,NON-BILLABLE
*      RESERVE LETTERS 5,6 FOR DISTRICT ZERO
*      ADD A AGAIN COMMAND WHICH RESENDS A LETTER
*      PUT DATE-TIME STRING AT BEGINNING OF LETTER
*      ALLOW FOR CONTROL A AND CONTROL Q ON INPUT
*      FIX A FEW BUGS FOUND WHILE ADDING THE ABOVE
*
*




START  STP CIN; LDA 34000B
       IF TEST=-1
       BRU BEGIN1
       ELSE
       BRU BEGIN
       ENDF

SKNE   OPD     536B5,2
MSG    POPD 101B5,2
       STP SAVA; STX SAVX; EAX* 0; CXA; ETR =37777B; LDB =-1; LDX =1; BRS 34
       LDP SAVA; LDX SAVX; BRR 0
SAVA   ZRO
SAVB   ZRO
SAVX   ZRO

SNDFLG DATA 0  (-1=> SEND COMMAND)
VERNO  ASC '$LETTER VERSION 6.07$/'
FIX    POPD 100B5,2
       STX TEMX; EAX* 0; CXA; ETR =37777B; STA TEMP1; CLA
       XAB; SKG =3777B; SKG =0; BRU FIX1; XAB
       LSH 1; DIV =4; ADD TEMP1; STA TEMP1; MRG =76B5; STA *+1
       ZRO; CBX; STA TEMP2; LDA LNO; LDX STAB,2; CLB; LSH 0,2
       MRG TEMP2; CAX; LDA TEMP1; MRG =35B5; STA *+2; CXA; ZRO
FIX1   LDX TEMX; BRR 0

BEGIN  BRM VER; MSG VERNO (OUTPUT VERSION NO)
BEGIN1 LDA =INT1; STA 201B
       BRM RCBLK; LDA BUF+74B; ADM TIMX; CLB; LSH 12; ADM TIMX
BEGIN2 BRM ARM1
       CLA; LDX =BMBUF-ENBUF; STA ENBUF,2
       BRX *-1; STA ERFLG

INPUT  LDX =-1; LDA =4B4; BRS 12; CLA; STA SNDFLG
       TCO =155B; TCO =155B; TCO ='@'; BRM CLBUF; CIO CIN; CAB
       CIO CIN; SKE =155B; BRU *-2; CBA; LDX =CTBL-CTBLND
       SKE CTBLND,2; BRX *-1; BRU* BTBLND,2
CTBL   ASC '  A  C  D  E  H  N  R  S  Q  ?'
CTBLND DATA 0,AGAIN,COUNT,DISPL,EXPIRE,HELP,CEDATE,REMOV,SEND,QUIT,HELP
BTBLND DATA ERR
ERR    LDA ERFLG; SKE =0; BRU ERR1A; MIN ERFLG; MSG ERMSG
       LDX =-1; BRS 11; BRU INPUT
ERR1A  MSG ERMSG1; LDX =-1; BRS 11; BRU BEGIN2
ERFLG  ZRO
ERMSG  ASC '$?$/'
ERMSG1 ASC '$FOR ASSISTANCE TYPE HELP$/'

CLBUF  ZRO; LDA =155B; CIT CIN; BRR CLBUF; BRU *-2

QUIT   MSG EOJ; BRS 10


HELP   MSG M12 (VALID COMMANDS)
       BRU BEGIN2

COUNT  BRU *+2
COUN5  ZRO; CLAB; STP TEMP1; STP TEMP3; STP TEMP5; LDA =34000B
       LDB =160200B; LDX =512; BRS 124; LDX =-512

COUN2  LDB 35000B,2; STX TEMX; LDA =3; STA TEMP7

COUN3  LDX =-6; CLA; LSH 1; ADM TEMP7,2; BRX *-3; SKR TEMP7
       BRU COUN3; LDX TEMX; BRX COUN2; SKN EDCSW; BRU *+2; BRR COUN5
       LDA =1; STA TEMA; LDX =-6

COUN4  STX TEMP7; TCO =155B; LDA TEMA; BRM PON; MIN TEMA; TCO =0
       LDX TEMP7; LDA TEMP7,2; BRM PDN; LDX TEMP7; BRX COUN4
       BRU BEGIN2


DISPL  MSG M15 (LETTER NO); BRM CLBUF; BRM RDNO
       SKG =6; SKG =0; BRU ERRRT; SUB =1; CLB; LSH 1; ADD =160210B
       CAB; LDA =24000B; LDX =128; BRS 124; MUL =3; LSH 23
       SUB =1; STA LPTR; ADD =600B; STA LPTR1; GCI LPTR
       BRU *+6; SKE =144B; BRU *+2; BRU *+3; CIO =1
       BRU *-6; LDX =-200B; CLA; STA 24200B,2; BRX *-1
       BRU BEGIN2

REMOV  MSG M15 (LETTER NO); BRM CKNUM GET LETTER NUMBER
       SUB =1; STA TEMP; BRM REM2; BRM RCBLK; LDA TEMP; ADD =62B
       COPY AX,A; STA BUF,2; BRM WCBLK; BRU BEGIN2

       IF TEST=-1
REM2   ZRO; BRR REM2;*PATCH
       ELSE
REM2   ZRO; BRS 113; CAX; LDA =37373737B; CAB; RCY 0,2; STA TEMA
       CLX; LDA =1; BRS 136; SKE =1; BRU REM2A
       MSG M19 (LTR BIT MAP BUSY); BRS 114; BRR REM2
REM2A  LDX =512; LDA =34000B; LDB =160200B; BRS 124; LDX =-512
       LDA 35000B,2; ETR TEMA; STA 35000B,2; BRX *-3; LDX =512
       LDA =34000B; LDB =160200B; BRS 125; CLX; CLA; BRS 136
       BRS 114; BRR REM2
       ENDF


*PRINT EXPIRATION DATES

EXPIRE MSG M15 (LETTER NO)
       BRM RDNO; XAB; SKE ='A'
       BRU EXP3; CIO CIN; SKG =72B; BRU *-2
       LDA =155B; CIO =1; BRM RCBLK; LDA =1; STA EXPTEM
EXP2   LDA EXPTEM; BRM PDN; CLA; CIO =1; CIO =1; LDA EXPTEM; ADD =61B
       CAX; LDA BUF,2; SKG =0; BRU PNONE
       BRM DATE
EXP2A  MIN EXPTEM; LDA EXPTEM; SKE =7; BRU EXP2; BRU BEGIN2
EXP3   XAB; SKG =6; SKG =0; BRU ERRRT; STA EXPTEM; LDA =155B; CIO =1
       BRM RCBLK; LDA EXPTEM; ADD =61B; CAX; LDA BUF,2
       SKG =0; BRU PNONE1; BRM DATE; BRU BEGIN2

*ALLOW FOR CHANGING OF EXPIRATION DATE OF AN EXSISTING LETTER

CEDATE MSG M15 (LETTER NO); BRM RDNO; SKG =6; SKG =0; BRU ERRRT
       SUB =1; STA LNOW; SKR EDCSW; BRU *-1
       BRM COUN5; LDX LNOW; LDA TEMP1,2; SKG =0
       BRU CEDAT1; BRU SEN3A
CEDAT1 MSG M20 (LETTER NOT CURRENTLY ISSUED)
PNONE  MSG M21 (NONE)
       BRU EXP2A
PNONE1 MSG M21 (NONE)
       BRU BEGIN2

EXPTEM ZRO
PON    ZRO; LDB =8; LDX =1; BRS 36; BRR PON

PDN    ZRO; LDB =10; LDX =1; BRS 36; BRR PDN
* AGAIN-  RESEND A LETTER .ONLY THE DATE-TIME STRING IS CHANGED

AGAIN  MSG M15 (LETTER NO); BRM CKNUM; SUB =1; STA LNOW; SKNE =5; BRU ERRRT
       CLB; LSH 1; ADD =160210B; CAB; LDA =24000B; LDX =128
       BRS 124; BRM DATIM; LDA LPTR; ADD =600B
       STA LPTR3; GCI LPTR2; BRU ISSU2; SKE =144B; BRU *-3
ISSU2  LDA LPTR2; STA LPTR1; BRU SEN3



*      SEND SUBROUTINES. MODIFIED 7-72  M MARRAH
*      CHANGES ARE
*      1.READ THE LUD
*      2.ALLOW SELECTION BY TYMSHARE,CUSTOMER OR BOTH
*      3. IF DISCTRIT=ZERO THEN
*              ALLOW SELECTION BY DISTRICT AND/OR
*              ALLOW SELECTION BY ACCOUNT
*        IF DISTRICT=NON-ZERO
*              ALLOW SELECTION BY ACCOUNT
*      4.ACCOUNT(GAN) MAY BE SELECTED BY GAN NUMBER OR
*        USER NAME WITHIN THAT ACCOUNT
*      5.ALLOW CONTROL A AND CONTROL W ON INPUT OF SELECTION

SEND   LDX =-200B; CLA; STA 24200B,2; BRX *-1; LDA =-1; STA SNDFLG
       MSG M15 (LETTER NO); BRM CKNUM GET LETTER NUMBER
       SUB =1; STA LNOW; LDA =24000B; BRM DATIM
       MSG M24 (TYPE LETTER); MSG M25 ($$); BRU SEN1+1
SEN1   WCI LPTR; CIO CIN; SKE =144B; BRU SEN1


SEN2   WCI LPTR; LDA LPTR1; SUB LPTR2; SKG =577B; BRU SEN3
       MSG M26 (MORE THAN 128 WORDS); BRU ERRRT

SEN3   MSG M25 ($$); LDX =0; BRS 11
       GCI LPTR; BRU *+3; CIO =1; BRU *-3; CIO CIN; SKE =155B; BRU ERRRT
       LDA LNOW; BRM REM2 GET RID OF OLD BITS

*GET EXPIRATION DATE AND TIME

SEN3A  MSG M27 (EXPIRATION DATE AND TIME)
       BRM CLBUF; BRM RDNO
       SKG =12; SKG =0; BRU DATERR; SUB =1; CLB; LSH 16
       STA EDATE; BRM RDNO; SKG =31; SKG =0; BRU DATERR; SUB =1
       MUL =720; CBA; ADM EDATE; BRM RDNO; XAB; SKE =':'; BRU *+2; BRU GMINS
       COPY BA,B; LRSH 23; DIV =100; STB TEMP
       MUL =60; LSH 23; ADD TEMP; ADM EDATE
SEN3B  LDA EDATE; SKE =0; BRU *+2; BRU SEN3C
       LDA =155B; CIO =1; LDA EDATE
       BRM DATE; MSG M28 (OKAY); BRM CLBUF; CIO CIN
       CAB; CIO CIN; SKG =72B; BRU *-2
       XAB; SKE ='Y'; BRU *+2; BRU SEN3C
       SKE ='N'; BRU SEN3B+3; BRU SEN3A
GMINS  XAB; SKG =23; SKG =-1; BRU DATERR; MUL =60; LSH 23; ADM EDATE; BRM RDNO
       SKG =59; SKG =-1; BRU DATERR; ADM EDATE; BRU SEN3B
DATERR MSG M29 (CORRECT DATE FORMAT IS M-D-T); LDX =-1
       BRS 11; BRU SEN3A

*WRITE EXP DATE ON DISC AT ADDRESS 200B+62B+LETTER NO

SEN3C  BRM RCBLK; LDA LNOW; ADD =62B; CAX; LDA EDATE; STA BUF,2; BRM WCBLK
       SKN EDCSW; BRU SEN6; BRU BEGIN2

*      GET A LIST OF DISTRICT NUMBERS
SEN6   CLA; STA ALSW; SKN DISSW; BRU *+2; BRU SEN4Z
       MSG M30 (DISTRICTS); BRM GETSTR; BRU SEN6B  NO CHARACTERS
       BRM CKALL; BRU SEN6A; SKR ALSW; BRU *-1; LDX TERM
       EAX 2,2; BRU SEN6B+2,2
SEN6A  LDA =10; BRM CONV; BRU SEN6F; SKG =255; SKG =-1; BRU SEN6F
       BRM PDIST; BRU SEN6G
SEN6B  LDX TERM; BRU *+1,2; BRU SEN6A A COMMA; BRU SEN4 A LF
       BRU SEN6D A CR; BRU SEN7 A LF W-O CHARS
SEN6D  SKN ALSW; BRU *+2; BRU GETALL; SKN DISBUF; BRU SEN6E; BRU GETLUD
SEN6E  MSG M31 (NO DISTRICTS WERE SELECTED); BRU SEN6
SEN6F  MSG M32 (ERROR ON DIST NO); BRM PERROR; TCO =155B; BRU SEN6B
SEN6G  MSG M33 (NO MORE THAN 100 DIST MAY BE ENTERED)
       BRU SEN6







*      GET A LIST OF ACCT (GAN) NUMBERS
SEN4Z  LDA DIST; BRM PDIST; 0;*SHOULD NEVER HALT
SEN4   MSG M34 (ACCOUNTS); LDA =377B; CIT CIN; SNE =152B; BRU SEN5
       BRM GETSTR; BRU SEN4B; BRM CKALL; BRU SEN4A
       SKR ALSW; BRU *-1; LDX TERM; EAX 2,2; BRU SEN4B+2,2
SEN4A  LDA =8; BRM CONV; BRU SEN4F; SKG =1777777B; SKG =0; BRU SEN4F
       BRM PACT; BRU SEN4G
SEN4B  LDX TERM; BRU *+1,2; BRU SEN4A; BRU SEN7; BRU SEN4D; BRU SEN7
SEN4D  SKN ALSW; BRU *+2; BRU GETALL; SKN ACTBUF; BRU *+2; BRU GETLUD
       SKN DISBUF; BRU SEN4E; BRU GETLUD
SEN4E  MSG M35 (NO DIST. OR ACCTS. WERE SELECTED); BRU SEN6
SEN4F  MSG M36 (ERROR ON ACCOUNT); BRM PERROR; TCO =155B; BRU SEN4B
SEN4G  MSG M37 (MORE THAN 200 ACCTS. ENTERED); BRU SEN4

*      GET GAN FROM  A LIST OF USER NAMES
SEN5   CIO CIN (GET THE LF)
       MSG M1 (SELECT ACCOUNTS BY USER NAMES?)
       BRM CLBUF; CIO CIN; CAB; CIO CIN; SKE =155B; BRU *-2; CBA
       SNE ='Y'; BRU SEN5AA; SNE ='N'; BRU SEN8
       LDX =-1; BRS 11; BRU SEN5
SEN5AA MSG M38 (USER NAMES); BRM GETSTR; BRU SEN5B; BRM CKALL; NOP
SEN5A  BRM GETNAM; BRU SEN5B; BRM FDGAN; BRU SEN5F; LDA 6,2
       ETR =1777777B; BRM PACT; BRU SEN4G
SEN5B  LDX TERM; BRU *+1,2; BRU SEN5A; BRU SEN5K; EAX GETLUD
SEN5C  SKN DISSW; SKN DISBUF; BRU *+2; BRU 0,2; SKN ACTBUF; BRU SEN4E
       BRU 0,2
SEN5K  EAX SEN7; BRU SEN5C
SEN5F  MSG M39 (INVALID USER NAME); BRM PERROR; TCO =155B; BRU SEN5B

*      SEE IF TYMSHARE,CUSTOMER OR BOTH
*      TYMSHARE  TMSK=4B7 TCK=4B7
*      CUSTOMER TMSK=4B7 TCK=0
*      BOTH      TMSK=0  TCK=0
SEN7   CLA; STA TMSK; STA TCK
       MSG M44 (TYMSHARE, CUST. OR BOTH)
       BRM GETSTR; BRU SEN7; LDB =4B7; LDA STRBUF
       SKE =[64B*200000B]+[155B*400B]; BRU SEN7A; STB TCK; EAX GETLUD
SEN7G  STB TMSK
SEN7E  BRU 0,2
SEN7A  SKE =[42B*200000B]+[155B*400B]; BRU SEN7B
SEN7F  EAX GETALL; SKN ALSW; EAX GETLUD; BRU 0,2
SEN7B  SKE =[43B*200000B]+[155B*400B]; BRU SEN7; EAX GETLUD; BRU SEN7G

*SEND TO SPECIFIED USER NAMES ONLY

SEN8   LDA =UNBUF; STA UNBPT; MSG M38 (USER NAMES)
SEN8C  BRM GETSTR; BRU SEN8A
       BRM CKALL; NOP
SEN8A  BRM GETNAM; BRU SEN8B; BRM FDGAN; BRU SEN8F (INVALID NAME)
       SKN DISSW; BRU SEN8A1; LDX 8,2; RSH 2; ETR =377B
       SKE DIST; BRU SEN8F (NOT HIS DISTRICT INVALID NAME)
SEN8A1 LDA 4,2; ETR =3777B; STA* UNBPT; MIN UNBPT
SEN8B  LDX TERM; BRU *+1,2; BRU SEN8A; BRU SEN8C; LDA UNBPT
       SKE =UNBUF; BRU MMAP; MSG M40 (NO USERS FOUND); BRU BEGIN2
SEN8F  MSG M39 (INVALID USER NAME); BRM PERROR; TCO =155B; BRU SEN8B

*      TURN ON LETTER FOR ALL USERS
GETALL LDA DIST; SKE =0; BRU GETLUD; LDX LNOW; LDA =40404040B; CAB
       RCY 0,2; STA TEMP2; BRM WLET; LDX =512; LDA =34000B
       LDB =160200B; BRS 124; LDX =-512; LDA 35000B,2
       MRG TEMP2; STA 35000B,2; BRX *-3; BRM WMAP; LDP TEMPT
       STP NCTP; BRU BEGIN2


GETLUD LDA =UNBUF; STA UNBPT; BRM RLUD; LDA UNBPT; SKE =UNBUF
       BRU MMAP; MSG M40 (NO USERS FOUND WHICH MEET CRITERIA)
       BRU BEGIN2
TCHAR  ASC 'T  '
BCHAR  ASC 'B  '
CCHAR  ASC 'C  '


       IF TEST=-1
WLET   ZRO; BRR WLET;*PATCH
       ELSE
WLET   ZRO; CLX; LDA =-1; BRS 136; LDA LNOW; CLB
       LSH 1; ADD =160210B; CAB; LDA =24000B; LDX =128; BRS 125
       BRR WLET
       ENDF


       IF TEST=-1
WMAP   ZRO; BRR WMAP;*PATCH
       ELSE
WMAP   ZRO; LDX =512; LDA =34000B; LDB =160200B; BRS 125; CLX
       BRS 141; BRR WMAP
       ENDF


MMAP   CLB; LDA =40B; LDX LNOW; RSH 0,2; STA LNO; BRM WLET
       LDX =512; LDA =34000B; LDB =160200B; BRS 124; LDA UNBPT
       SUB =UNBUF; CAX; LDB UNBUF,2; FIX 34000B; EAX -1,2
       CXA; ETR =37777B; SKE =37777B; BRU *-6; BRM WMAP; LDB =10
MMAP3  LDX =-100; LDA =2B7; SKN DISBUF+100,2; BRU MMAP4
       SKA DISBUF+100,2; BRU MMAP3A; LDA DISBUF+100,2
       MSG M41 (NO USERS FOUND FOR DIST); BRM PUNCH
MMAP3A BRX MMAP3+1

MMAP4  LDX =-200; LDB =8; LDA =2B7; SKN ACTBUF+200,2; BRU BEGIN2
       SKA ACTBUF+200,2; BRU MMAP4A; LDA ACTBUF+200,2
       MSG M42 (NO USERS FOUND FOR ACCOUNT); BRM PUNCH
MMAP4A BRX MMAP4+2; BRU BEGIN2


*      WRITE NUMBER TO TELETYPE.
*      INPUT,A=NUMBER    B=BASE
*      EXIT,ALL REGS RESTORED

PUNCH  ZRO; STA PUNCHA; STB PUNCHB; STX PUNCHX; ETR =1777777B
       LDX =1; BRS 36; LDA PUNCHA; LDB PUNCHB; LDX PUNCHX; BRR PUNCH
PUNCHA ZRO
PUNCHB ZRO
PUNCHX ZRO


QUES   DATA 37B
LNO    ZRO
STAB   DATA 18,12,6,0
TEMA   ZRO;TEMB ZRO;TEMX ZRO
BMBUF  BSS 512
DISBUF BSS 100
ACTBUF BSS 200
STRBUF BSS 30
UNBUF  BSS 3777B
TCK    ZRO
TMSK   ZRO
EDCSW  ZRO
ENBUF  BSS 1

NCTBUF BSS 70B
NCTP   DATA 3*NCTBUF-1
       DATA 3*NCTBUF-1
TEMPT  BSS 2
ACNO   ZRO
ABPT   DATA ACTBUF
ALL    ASC 'ALL'
TERCH1 ZRO
LNOW   ZRO
SPTR   0; 0
LPTR   ZRO;LPTR1 ZRO
LPTR2  ZRO;LPTR3 ZRO
DTEMP  EQU 20000B
PGX    ZRO
RACT   ZRO
RPOS   ZRO
TEMPD  ZRO
COUN   ZRO
ALSW   ZRO
UDIST  ZRO
COT    ZRO
OVSW   ZRO
       
UNBPT  ZRO

VER    ZRO; BRS 71; BRU *+2; BRR VER
       LDA =1; CAX; BRS 141; LDP SFPTR; BRS 48; BRU ERV1; LDB =SFBUF
       LDX =4; BRS 96; LDA =4; SKA SFBUF+3; BRU *+2; BRU ERV2
       LDP SFPTR; BRS 64; BRU OPFER; STA SFIL; CLA; SCP SFIL; HLT
       BRM RETFD
       MSG M2 (NAME:)
       LDA PTR1; STA PTR1+1; LDA =PTR1; LDB =155B; CLX; BRS 33
       LDA PTR1+1; SUB PTR1; STA VT1
VER1   CIO SFIL; SKE =137B; BRU *+2; BRU NONAME
       SKE =155B; BRU VER1; CIT SFIL; BRU *+2; BRU NONAME
       LDA PTR2; STA PTR2+1; LDA =PTR2; LDB =155B
       LDX SFIL; BRS 33; LDA PTR2+1; SUB PTR2
       SKG =0; BRU NONAME; SKE VT1; BRU VER1
       LDP PTR2; SKSE PTR1; BRU VER1; GCI PTR1; HLT
       SKR PTR1; NOP; STA VXT8; BRM FONO; SKA =40000B
       BRU *+2; BRU ERV3; SKA =1B7; BRU *+2; BRU NONO
       ETR =377B; STA DIST; LDB =-1; SKE =0; STB DISSW
       BRM FONO; STA VT1; BRM FONO; STA VT2; LDA SFIL; BRS 20
       LDX =-1; LDA =3; BRS 12; MSG M3 (PASSWORD)
SGPS   LDP TERMWD; STP VXT1; STP VXT3; STB VXT5
       CLA; STA TEMP1; LDA VXT8; ADM VXT3
GPS    LDA =20; STA TEMP2
GPS1   LDX TEMP1; LDA VXT1,2; MUL =6065B
       DIV =37777761B; CBA; ADD VXT2,2; CAB; ETR =3
       STA TEMP1; LDA VXT1,2; XAB; STP VXT1,2; LDA VXT5
       XMA VXT1; STA VXT5; SKR TEMP2
       BRU GPS1; CIO =0; SKE =155B; BRU *+2; BRU *+3
       ADM VXT1; BRU GPS
       LDX =-1; LDA =4B4; BRS 12
       LDA VXT1; ADD VXT2; SKE VT1; BRU NOPASS; LDA VXT3; ADD VXT4
       SKE VT2; BRU NOPASS; BRR VER

FONO   ZRO; LDB =8; LDX SFIL; BRS 38; BRR FONO

NOPASS MSG M4 (ERROR, TYPE PASSWORD)
       LDX =-1; LDA =3; BRS 12; BRU SGPS

NONAME MSG M5 (INVALID NAME)
       BRS 10


TERMWD DATA 27657537B,55555555B

SFPTR  DATA 3*SF-1,3*SF+5
SF     ASC '()PSWD'
PTR1   DATA 3*OPNAM-1,3*OPNAM-1
OPNAM  BSS 20
PTR2   DATA 3*BUF2-1,3*BUF2-1
BUF2   BSS 20
SFBUF  BSS 20
TEMP1  ZRO;TEMP2 ZRO;TEMP3 ZRO;TEMP4 ZRO;TEMP5 ZRO;TEMP6 ZRO;TEMP7 ZRO
DIST   ZRO
VT1    ZRO
VT2    ZRO
VXT1   ZRO
VXT2   ZRO
VXT3   ZRO
VXT4   ZRO
VXT5   ZRO
VXT8   ZRO
SFIL   ZRO
ERV1   MSG M6 (NO PASSWORD FILE)
ERV    MSG M7 (NOTIFY OPNS AND SYS)
       BRM RETFD; BRS 10
ERV2   MSG M8 (PSWD FILE STATUS LOST)
       BRU ERV
OPFER  MSG M9 (PSWD FILE BUSY TRY LATER)
       BRM RETFD; BRS 10
ERV3   MSG M5 (INVALID NAME)
       BRS 10

NONO   TCO ='   ?'; BRS 10
RETFD  ZRO; BRS 39; CBA; ETR =7777B; LDX =1; BRS 141; BRR RETFD

*      FOLLOWING SUBROUTINES ADDED 7-72 M MARRAH
*      FOR THE FOLLOWING

*      PATCHES TO READ LUD
*      PATCHES TO SET LETTERS 5,6 ASIDE FOR DIST ZERO
*      PATCHES TO ALLOW SENDING TO DIST,CUSTOMERS,TYMSHARE,USER NAMES
*
*      READ LUD
RLUD   ZRO; LDX =-7; LDA =320200B-2B4; STA DISCA
RLUD1  STX RLUDX1; LDA =2B4; ADM DISCA; LDX =-4
RLUD2  STX RLUDX2; LDB DISCA; LDX =4000B; LDA =30000B; BRS 124
       LDA =30000B
RLUD3  CAX
RLUD3B LDA 0,2; SKE =0; BRU RLUD4
RLUD3A CXA; ADD =100B; ETR =37700B; SKG =33777B; BRU RLUD3
       LDA DISCA; ADD =40B; STA DISCA; LDX RLUDX2; BRX RLUD2
       ETR =777000B; ADD =200B; STA DISCA; LDX RLUDX1; BRX RLUD1
       BRR RLUD
RLUD4  SKE =-1; BRU RLUD5; BRU RLUD3A
RLUD5  SKE =-2; BRM PUN; EAX 9,2; BRU RLUD3B
DISCA  ZRO
RLUDX1 ZRO
RLUDX2 ZRO
*      FIND USER NAME IN LUD
*      NO SKIP=NOT FOUND
*      SKIP=FOUND.  X=POINTER TO LUD ENTRY IN DICBUF
FDGAN  ZRO; LDX =-4; CLA; ADD NAMBUF+4,2; BRX *-1
       LRSH 23; DIV =887; STB BLNO
       BRM FDGAN3
FDGAN1 LDA 0,2; SNE =-2; BRU FDGAN4; SNE =-1; BRU FDGAN2; SNE =0
       BRR FDGAN; BRU FDGAN5
FDGAN2 MIN BLNO; BRU FDGAN1-1
FDGAN3 ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB; LSH 13
       ADD =320200B; ADD MTEMP; CAB; STB DISCA; LDA =DISBF; LDX =100B
       BRS 124; LDX =DISBF; BRR FDGAN3
FDGAN4 EAX 9,2; BRU FDGAN1
FDGAN5 SKE NAMBUF; BRU FDGAN4; LDA 1,2; SKE NAMBUF+1; BRU FDGAN4
       LDA 2,2; SKE NAMBUF+2; BRU FDGAN4; LDA 3,2
       SKE NAMBUF+3; BRU FDGAN4; MIN FDGAN; BRR FDGAN
*
DISSW  ZRO;*NEG IF NOT DIST ZERO
TERM   ZRO
MTEMP  ZRO
BLNO ZRO
DISBF  BSS 64
NAMBUF BSS 4
NAMPTR DATA 3*NAMBUF-1,3*NAMBUF-1
*
*      CHECK STRBUF FOR THE WORD ALL AS THE FIRST THREE CHARACTERS
*      FOLLOWED BY A LF OR A CR.
*      ALSO SET UP STRING POINTERS IN SPTR AND SPTR+1 FROM STRTAB
*      IF THE WORD IS ALL,THEN SKIP RETURN

CKALL  ZRO; LDA =3*STRBUF-1; STA SPTR; LDA STRTAB; STA SPTR+1
       SUB SPTR; SKE =4; BRR CKALL; LDA STRBUF; SKE ALL; BRR CKALL
       MIN CKALL; BRR CKALL


*      GET USER NAME FROM STRBUF AND PUT INTO NAMBUF.
*      NO SKIP ON RETURN=BAD STRING

GETNAM ZRO; CLA; LDX =-4; STA NAMBUF+4,2; BRX *-1; STA TERM; LDA =-1
       STA CONVSW; LDP SPTR; STP LPTR; LDA =3*NAMBUF-1; STA WCHTAB
       ADD =13; STA WCHTAB+1
GETNM1 GCI SPTR; ZRO ; SKG =77B; BRU GETNM3; LDX =1; SKE =152B
       EAX 1,2; STX TERM
GETNM2 SKN CONVSW; MIN GETNAM; BRR GETNAM
GETNM3 SKE =','; BRU GETNM4; BRU GETNM2
GETNM4 WCH WCHTAB; MIN CONVSW; BRU GETNM1
GETNM9 SKR CONVSW; BRU *-1; BRU GETNM1
WCHTAB DATA 0,0
       BRU GETNM9


*
REGSW  DATA -1 (-1 => EXCEPTION SCHED, 0=> REG SCHED)
*      CHECK LETTMBER AGAINST DIST NUMBER.
*      SET ASIDE LETTERS 5,6 FOR DIST ZERO
CKNUM  ZRO; BRM CLBUF; BRM RDNO; STA LNOW; SKG =6; SKG =0; BRU ERRRT
       SKG =4; BRR CKNUM; SKN DISSW; BRU *+2; BRU ERRRT; SKE =6; BRR CKNUM
       SKN SNDFLG; BRR CKNUM
       MSG M45; CIO CIN; SKNE ='Y'; BRU CKNUM1; BRM CLCR; BRU ERRRT
CKNUM1 BRM CLCR; LDX =200B; BRS 127; STA CPNO
       LDB =-1; STB REGSW; MSG M49; CIO CIN; SKE ="Y"; BRU CKNUM2
       CLB; STB REGSW; STB MIN1; LDA =350B; STA UZ
       LDX CPNO; LDA TABLE,2; SKG =0; BRU CKNUM2; STA HR1; BRU CKNUM3; BRM CLCR
CKNUM2 BRM CLCR; BRM GETTZ;C1 LDA =M47; LDB =44; BRM MSG1; BRM GEXC; BRU C1
       STA WD1
       LDA MO0; STA MO1
       LDA DA; STA DA1
       LDA YR; STA YR1
       LDA MIN; STA MIN1
       LDA HR; STA HR1
C2 LDA =M48; LDB =42; BRM MSG1; BRM GEXC; BRU C2; STA WD2; SKG WD1; BRU ERRRT
CKNUM3 LDA =24000B; BRM DATIM; CLA; STA CNTHR
       LDA HR1; STA HRX; BRM CONVHR
       LDA CPNO; SKE =14; BRU CKNUM4; BRM P14SCH; BRU CKNUM5
CKNUM4 BRM SENREG
CKNUM5 BRM     WEXC
       LDA LNOW; SUB =1; STA LNOW
       BRU SEN3
*
*      CONVHR CONVERTS HR TO PACIFIC TIME
*
CONVHR ZRO
       SKN REGSW; BRR CONVHR (REGULAR)
       LDX =200350B
       LDA WD1; BRS 176; ETR =37B; STA HRX; BRR CONVHR
BUFF1  BSS     100
CHAFIL ASC     '(PJ)//CHANGE'
CHAPTR DATA 3*CHAFIL-1,3*CHAFIL+11
*
*      OPCHA OPENS CHA FILE AND WRITES CPNO TO IT
*
OPCHA  ZRO
       LDP CHAPTR; LDX =2; BRS 65; HLT; STA FL
       BRR OPCHA
*
*      WEXC WRITES OUT MO/DA/YR HR:MIN TIME ZONE TO (DUPJOB)//CHANGE
*
WEXC   ZRO; SKN REGSW; BRR WEXC; BRM OPCHA
       LDA WD1; LDX =200320B; BRS 176; WIO FL; CBA; WIO FL
       LDA WD2; LDX =200320B; BRS 176; WIO FL; CBA; WIO FL
       LDA FL; BRS 20
       BRR WEXC

*
*      SENREG SENDS REGULAR MSG
*
SENREG ZRO
       LDP CLCPT1; BRM MOVE
       LDP CLCPT2; BRM MOVEIT (PACIFIC)
       LDP CLCPT3; BRM MOVEIT (MOUNTAIN)
       LDP CLCPT4; BRM MOVEIT (CENTRAL)
       LDP CLCPT5; BRM MOVEIT (EASTERN)
       LDP CLCPT6; BRM MOVEIT (LONDON)
       LDP CLCPT7; BRM MOVEIT (WEST EUROPE)
       LDA =155B; WCI LPTR; WCI LPTR
       LDP CLCPT8; BRM MOVE
       LDA =144B; WCI LPTR
       BRR SENREG

*
*      P14SCH SEND SCHEDULE FOR P14
*
P14SCH ZRO
       LDP CLCP10; BRM MOVE; LDA HRX; BRM MOVEHR
       LDA =':'; WCI LPTR; LDA MIN1; BRM MOVEHR
       LDP CLCP11; BRM MOVE; LDA =144B; WCI LPTR; BRR P14SCH
*
* MOVEHR MOVES A REG TO LPTR
**
MOVEHR ZRO
       RSH 23; DIV =10; ADD =20B; WCI LPTR; CBA; ADD =20B; WCI LPTR; BRR MOVEHR
FL     ZRO
CPNO   ZRO
* THIS TABLE HAS TIMES IN PST THAT THE MACHINE COMES DOWN.
TABLE  ZRO
       DATA    21              1
       DATA    20              2
       DATA    01              3
       DATA    19              4
       DATA    24              5
       DATA    24              6
       DATA    24              7
       DATA    17              8
       DATA    2               9
       DATA    18              10
       DATA    21              11
       DATA    23              12
       DATA    24              13
       DATA    -1              14
       DATA    24              15
       DATA    20              16
       DATA    24              17
       DATA    19              18
       DATA    1               19
       DATA    24              20
       DATA    22              21
       DATA    -1              22
TABLN  ZRO
* IN THE ABOVE TABLE TIME ZONES SHOULD BE SUBRACTED FROM VALUES
* I.E. IF HOUSTON READS 21 IN PROGRAM THEN DOWN TIME IS 23 LOCAL HOUSTON.
*
*      MOVE MOVES FROM PTR IN A,B TO LPTR
*
WD1    ZRO
WD2    ZRO
MOVE   ZRO; STP T1; GCI T1; BRR MOVE; SKNE ='$'; LDA =155B; WCI LPTR; BRU *-5
*
*      MOVEIT PRINTS OUT THE TIME
*
MOVEIT ZRO; STP T1
       BRM ADDHR; RSH 23; DIV =10; ADD =20B; WCI LPTR; CBA; ADD =20B; WCI LPTR
       LDA MIN1; RSH 23; DIV =10; ADD =20B; WCI LPTR; CBA; ADD =20B; WCI LPTR
       LDP T1; BRM MOVE;
       LDP CLCPT9; BRM MOVE; LDA =155B; WCI LPTR; BRR MOVEIT
*
*      TABLE OF HRS TO ADD TO PAC TIME FOR LETTER
*
ADDTB  ZRO
       DATA 0,1,1,1,5,1
*
*      ADDD HR ADDS TABLE ENTRY ABOVE, MOD 24
*
CNTHR  ZRO
ADDHR  ZRO
       MIN CNTHR
       LDA     HRX
       LDX     CNTHR
       ADD     ADDTB,2
       SKG     =23
       BRU *+2
       SUB =24
       STA HRX
       BRR ADDHR
*
*      CONVERT STRING IN STRBUF TO A NUMBER
*      ON INPUT,A=BASE TO CONVERT TO.
*      SKIP RETURN,NUMBER IN A REG.
*      NO SKIP,BAD NUMBER
CONV   ZRO; STA CONVD; ADD =17B; STA CONVD1; LDP SPTR; STP LPTR
       CLA; STA CONVA; STA TERM; LDA =-1; STA CONVSW
CONV1  GCI SPTR; BRU CONV2; SKG =0; BRU *+5; SKG CONVD1; SKG =17B
       BRU CONV2; SUB =20B; XMA CONVA; MUL CONVD; SKE =0; BRR CONV
       LSH 23; ADM CONVA; MIN CONVSW; BRU CONV1
CONV2  SKG =77B; BRU CONV3; LDX =1; SKE =152B; EAX 1,2; STX TERM
CONV4  LDX TERM; LDA CONVA; SKN CONVSW; MIN CONV; BRR CONV
CONV3  SKE =','; BRU CONV5; SKN CONVSW; BRU CONV4; BRR CONV
CONV5  SKR CONVSW; BRU *-1; GCI SPTR; ZRO; BRU CONV2
CONVA  ZRO
CONVSW ZRO
CONVD  ZRO;*BASE NUMBER
CONVD1 ZRO;*UPPER LIMIT FOR CORRECT CHARACTER

*
*      GET CHARACTERS FROM TTY AND PUT INTO STRBUF. CONTROL
*      A AND Q FUNCTIONAL. ONLY ALLOW ONE LINE OF INPUT PER CALL.
*      TERMINATE ON LF OR CR. IF CR,THEN PUT A 4 IN TERM
*      ELSE IF A LF THEN A 3. IF NO CHARACTERS ARE INPUTED
*      EXCEPT TERMINATING CHARACTER,THEN NO SKIP ON EXIT
*      IF CHARACTERS ARE INPUTED,THEN SKIP. ON RETURN,X=TERM

GETSTR ZRO; CLA; LDX =-30; STA STRBUF+30,2; BRX *-1; LDA =3*STRBUF-1
       STA STRTAB; ADD =91; STA STRTAB+1
GETST1 CIO CIN; SKG =77B; BRU GETST7; BRU GETST4
GETST2 TCO =147B; BRU GETST1
GETST4 SKE =155B; BRU GETST6; LDX =2
GETST5 WCH STRTAB; STX TERM; LDA =3*STRBUF; SUB STRTAB; LDX TERM
       SKG =-1; MIN GETSTR; BRR GETSTR
GETST6 SKE =152B; BRU GETST2; LDX =1; BRU GETST5
GETST7 WCH STRTAB; BRU GETST1
GETST9 MSG M43 (MORE THAN 90 CHARS. INPUT); BRU GETST2

STRTAB 0; 0; BRU GETST9



*      PUT GAN ON ACTBUF
PACT   ZRO; LDX =-200; LDB =1777777B; SKN ACTBUF+200,2
       BRU PACT1; SKM ACTBUF+200,2; BRU *+2; BRU PACT2
       BRX *-5; BRR PACT
PACT1  MRG =4B7; STA ACTBUF+200,2
PACT2  MIN PACT; BRR PACT
*
*      PUT DISTRICT NUMBER ON DISBUF
PDIST  ZRO; LDX =-100; LDB =177B; SKN DISBUF+100,2; BRU PDIST1
       SKM DISBUF+100,2; BRU *+2; BRU PDIST2
       BRX *-5; BRR PDIST
PDIST1 MRG =4B7; STA DISBUF+100,2
PDIST2 MIN PDIST; BRR PDIST



*      PRINT THE STRING IN STRBUF UP TO THE COMMA OR LF OR CR
PERROR ZRO; LDA =-1; STA CONVSW SET SW FOR NULL STRING PLUS COMMA
PERR1  GCI LPTR; ZRO 0 TRAP SHOULD NOT HAPPEN; SKG =77B; BRU PERR3
PERR2  LDP LPTR; STP SPTR; BRR PERROR
PERR3  SKE =','; BRU PERR4; SKN CONVSW; BRU PERR2; CIO =1; BRU PERR2
PERR4  CIO =1; MIN CONVSW; BRU PERR1

*


*      GET LIST OF UNER NUMBERS (FD NUMBER) FROM
*      THE LUD BASED UPON THE DISSW,DISBUF,ACTBUF,TMSK,TCK
*      THE LUD USER ENTRY POINTED TO IN X REG. UPON ENTRY
PUN    ZRO; STX PUNX; LDA 6,2; LDB TMSK; SKM TCK; BRR PUN;*EXIT IF WRONG

* GET DISTRICT LIST AND SEE IF ANYTHING TO DO

PUN1   SKN DISBUF; BRU PUN2; LDX PUNX; LDA 8,2; RSH 2; ETR =177B
       LDX =-100; LDB =177B
PUN1B  SKN DISBUF+100,2; BRU PUN1C; SKM DISBUF+100,2; BRU PUN1D
       LDA DISBUF+100,2; MRG =2B7; STA DISBUF+100,2; SKN DISSW
       BRU PUN1F; BRU PUN2
PUN1C  SKN DISSW; BRU PUN2; BRU PUN4
PUN1F  LDX PUNX; LDA 4,2; SKG =3777B; SKG =0; BRR PUN; STA* UNBPT
       MIN UNBPT; BRR PUN
PUN1D  BRX PUN1B

*GET ACCT LIST AND SEE IF SOME THING TO DO
PUN2   SKN ACTBUF; BRU PUN3; LDX PUNX; LDA 6,2; ETR =1777777B
       LDX =-200; LDB =1777777B
PUN2A  SKN ACTBUF+200,2; BRU PUN4; SKM ACTBUF+200,2; BRU PUN2B
       LDA ACTBUF+200,2; MRG =2B7; STA ACTBUF+200,2; BRU PUN1F
PUN2B  BRX PUN2A; BRU PUN4

PUN3   SKN ALSW; BRU PUN4; BRU PUN1F

PUN4   LDX PUNX; BRR PUN

PUNX   ZRO

*
* PUT DATE TIME YEAR INTO BEGINNING OF LETTER BUFFER

DATIM  ZRO; MUL =3; LSH 23; SUB =1; STA LPTR; STA LPTR1; STA LPTR2
       LDA =155B; WCI LPTR; LDP LPTR; CBA; BRS 140; ADD =15; STA LPTR1
       LDA =155B; WCI LPTR; BRR DATIM


DATE   ZRO; LDX TIMX; BRS 176
       LSH 6; ETR =@74B6
       LRSH 16; ADD =1; STA MO; CLA; LSH 5; ADD =1; STA DAY
       CLA; LSH 5; STA HR; CLA; LSH 6; STA MINS
       LDA MO; BRM PDN; LDA ='-'; CIO =1; LDA DAY; BRM PDN
       CLA; CIO =1; CIO =1; LDA HR; BRM PDN; LDA =':'; CIO =1
       LDA MINS; SKG =9; BRM ZERO; BRM PDN; LDA =155B; CIO =1
       BRR DATE
ZERO   ZRO; STA SAVA; LDA =20B; CIO =1; LDA SAVA; BRR ZERO
MO     ZRO
DAY    ZRO
MINS   ZRO
TIMX   DATA 300B
BUF    BSS 100B
RCBLK  ZRO; LDA =BUF; LDB =200B; LDX =100B; BRS 124; BRR RCBLK
WCBLK  ZRO; LDA =BUF; LDB =200B; LDX =100B
       IF TEST=-1; BRR WCBLK; ELSE; BRS 125; BRR WCBLK; ENDF
EDATE  ZRO
TEMP   ZRO
ARM1   ZRO; LDA =2B6; BRS 78; BRR ARM1
INT1   ZRO INT1PC; BRU BEGIN2
INT1PC ZRO
RDNO   ZRO; LDX CIN; LDB =10; BRS 38; BRR RDNO
ERRRT  LDX =-1; BRS 11 (CLEAR INPUT); TCO ='?'; BRU BEGIN2
CLCM1  ASC     'REMINDER......THE SYSTEM WILL GO OFF THE AIR AT'
       ASC     ' .............$'
CLCM2  ASC     ' PACIFIC /'
CLCM3  ASC     ' MOUNTAIN /'
CLCM4  ASC     ' CENTRAL /'
CLCM5  ASC     ' EASTERN /'
CLCM6  ASC     ' LONDON '
CLCM7  ASC     ' WESTERN EUROPE /'
CLCM8  ASC     ' THANK YOU,$TYMSHARE.$$$$/'
CLCM9  ASC     'TIME$/'
*
* TO REVERSE CHANGES PLACE 'LE SYSTEME SERA COUPE CE JOUR A /'
* IN PLACE OF THE TWO LINES LABELED CLCM10, ALSO CHANGE THE 63
* ON THE LINE LABELED CLCP10 TO 32
*
CLCM10 ASC     'REMINDER......THE SYSTEM WILL GO OFF THE AIR AT'
       ASC     ' .............$'
CLCM11 ASC     ' *C-T OPERATIONS *$/'
CLCPT1 DATA    3*CLCM1-1,3*CLCM1+63-1
CLCPT2 DATA    3*CLCM2-1,3*CLCM2+9-1
CLCPT3 DATA    3*CLCM3-1,3*CLCM3+10-1
CLCPT4 DATA    3*CLCM4-1,3*CLCM4+9-1
CLCPT5 DATA    3*CLCM5-1,3*CLCM5+9-1
CLCPT6 DATA    3*CLCM6-1,3*CLCM6+9-1
CLCPT7 DATA    3*CLCM7-1,3*CLCM7+16-1
CLCPT8 DATA    3*CLCM8-1,3*CLCM8+25-1
CLCPT9 DATA    3*CLCM9-1,3*CLCM9+4-1
CLCP10 DATA    3*CLCM10-1,3*CLCM10+63-1
CLCP11 DATA    3*CLCM11-1,3*CLCM11+19-1
M1     ASC '$SELECT ACCOUNTS BY USER NAMES? /'
M2     ASC '$NAME: /'
M3     ASC '$PASSWORD: /'
M4     ASC '$ERROR, TYPE PASSWORD: /'
M5     ASC '$INVALID NAME$/'
M6     ASC '$NO PASSWORD FILE/'
M7     ASC '$NOTIFY DIVISION OPNS MGR AND SYSTEMS GROUP TECH DIV./'
M8     ASC '$PASSWORD FILE STATUS LOST/'
M9     ASC '$PASSWORD FILE BUSY-TRY LATER$/'
M12    ASC '$$VALID COMMANDS ARE:'
       ASC '$COUNT    -  DISPLAYS THE NUMBER OF ADDRESSEES FOR EACH LETTER'
       ASC '$DISPLAY  -  DISPLAYS THE CONTENTS OF A SPECIFIED LETTER'
       ASC '$EXPIRATION DATE  -  DISPLAYS THE EXPIRATION DATE OF A SPECIFIED'
       ASC '$                    LETTER'
       ASC '$HELP     -  DISPLAYS THIS LIST'
       ASC '$AGAIN    -  RESENDS A SPECIFIED LETTER WITH NEW DATE STRING'
       ASC '$NEW EXPIRATION DATE  -  CHANGES THE EXPIRATION DATE OF A SPECIFIED'
       ASC '$                        LETTER'
       ASC '$REMOVE   -  CANCELS A SPECIFIED LETTER'
       ASC '$SEND     -  SENDS A LETTER'
       ASC '$QUIT    -  QUITS TO THE EXEC'
       ASC '$$ALL COMMANDS MAY BE SHORTENED TO THE FIRST CHARACTER$$/'
M15    ASC '$LETTER NO: /'
M19    ASC '$LETTER BITMAP BUSY-TRY LATER$$/'
M20    ASC '$LETTER NOT CURRENTLY ISSUED$/'
M21    ASC 'NONE$/'
M24    ASC '$TYPE LETTER:/'
M25    ASC '$$/'
M26    ASC '$MORE THAN 128 WORDS$/'
M27    ASC '$EXPIRATION DATE AND TIME: /'
M28    ASC 'OKAY? /'
M29    ASC '$CORRECT DATE FORMAT IS M-D-T$/'
M30    ASC '$DISTRICTS: /'
M31    ASC '$NO DISTRICTS WERE SELECTED$/'
M32    ASC '$ERROR ON DISTRICT NO: /'
M33    ASC '$ERROR, NO MORE THAN 100 DISTRICTS MAY BE ENTERED$/'
M34    ASC '$ACCOUNTS: /'
M35    ASC '$NO DISTRICTS OR ACCOUNTS WERE SELECTED$$/'
M36    ASC 'ERROR ON ACCOUNT: /'
M37    ASC '$ERROR, MORE THAN 200 ACCOUNTS ENTERED$$/'
M38    ASC '$USER NAMES: /'
M39    ASC 'INVALID USER NAME: /'
M40    ASC '$NO USERS FOUND WHICH MEET CRITERIA$$/'
M41    ASC '$NO USERS FOUND FOR DISTRICT: /'
M42    ASC '$NO USERS FOUND FOR ACCOUNT: /'
M43    ASC '$ERROR, MORE THAN 90 CHARACTERS INPUT$/'
M44    ASC '$TYMSHARE, CUSTOMER OR BOTH (T,C, OR B): /'
M45    ASC     '$SCHEDULE? /'
M46    ASC     ' TIME$/'
M47    ASC     'ENTER EXCEPTION DOWN TIME MM/DD/YR HR:MIN : /'
M48    ASC     'ENTER EXCEPTION UP TIME MM/DD/YR HR:MIN : /'
M49    ASC     '$REGULAR? /'
EOJ    ASC '$E.O.J.$/'
*
*      ROUTINE TO GET EXCEPTION TIME MM/DD/YY HR:MIN
*
GEXC   ZRO
       BRM RDNO; STA MO0; SKG =12; SKG =0; BRU GEXC1; CBA; SKE ='/'; BRU GEXC1
       BRM RDNO; STA DA; SKG =31; SKG =0; BRU GEXC1; CBA; SKE ='/'; BRU GEXC1
       BRM RDNO; STA YR; SKG =82; SKG =76; BRU GEXC1; CBA; SKE =' '; BRU GEXC1
       BRM RDNO; STA HR; SKG =23; SKG =-1; BRU GEXC1; CBA; SKE =':'; BRU GEXC1
       BRM RDNO; STA MIN; SKG =59; SKG =-1; BRU GEXC1; CBA; SKE =155B; BRU GEXC1
       LDA UZ; CLB; LSH 12; MRG =20B; STA T3
       CLB; LDA MIN; RSH 6; LDA HR; RSH 5; LDA DA; SUB =1; RSH 5
       LDA MO0; SUB =1; RSH 4; LDA YR; SUB =60; LSH 14 ; LDX T3; BRS 176
       MIN GEXC; BRR GEXC
GEXC1  BRM CLCR; LDA =M221; LDB =31; BRM MSG1; BRR GEXC

TZTAB  ZRO
       ASC "TDP"
       ASC "TDC"
       ASC "TDE"
       ASC "TSP"
       ASC "TSC"
       ASC "TSE"
       ASC "RAP"
       ASC "TSB"
TZEND  ZRO
TZTAB1 ZRO
       DATA  350B
       DATA  352B
       DATA  353B
       DATA  350B
       DATA  352B
       DATA  353B
       DATA  361B
       DATA  361B
TA1END ZRO
TZSIZE DATA    -8
TZASC  ZRO
DST    ZRO
GETTZ  ZRO
       LDA =3; STA BUICPN
       LDA =-1; BRS 176; CXA; MRG =300B; STA UZ; BRR GETTZ
GETTZ1 MSG M181
       CIO CIN; SKNE =155B; BRU GETTZ3; CLB; RSH 8
       CIO CIN; SKNE =155B; BRU GETTZ3; RSH 8
       CIO CIN; SKNE =155B; BRU GETTZ3; RSH 8; STB TZASC; BRM CLCR
       LDX TZSIZE; LDA TZASC; SKNE ="HEL"; BRU GETTZ3
       SKNE TZEND,2; BRU GETTZ2; BRX *-2; BRU GETTZ3
GETTZ2 LDA TA1END,2; STA UZ; BRR GETTZ
GETTZ3 MSG M231; BRU GETTZ1
UZ     ZRO
BUICPN ZRO
T1     ZRO
T2     ZRO
CIN    ZRO
COUT   ZRO
HRX    ZRO
MO1    ZRO
DA1    ZRO
YR1    ZRO
MIN1   ZRO
HR1    ZRO
MO0    ZRO
DA     ZRO
HR     ZRO
MIN    ZRO
T3     ZRO
YR     ZRO
CLCR   ZRO
       CIO     CIN
       SKE     =155B
       BRU     *-2
       BRR     CLCR
M181   ASC     '$TIME ZONE: /'
M221   ASC     'MUST BE OF FORM MM/DD/YY HR:MIN/'
M231   ASC     '$VALID TIME ZONES ARE:'
       ASC     '$PST           PACIFIC STANDARD TIME'
       ASC     '$CST           CENTRAL STANDARD TIME'
       ASC     '$EST           EASTERN STANDARD TIME'
       ASC     '$PDT           PACIFIC DAYLIGHT TIME'
       ASC     '$CDT           CENTRAL DAYLIGHT TIME'
       ASC     '$EDT           EASTERN DAYLIGHT TIME'
       ASC     '$PAR           PARIS STANDARD TIME'
       ASC     '$BST           BRITISH SUMMER TIME$/'
MSG1   ZRO; CAX; LDA =155B; CIO COUT; CXA;  LDX COUT; BRS 34; BRR MSG1
MSG    ZRO
       LDB     =-1
       LDX     COUT
       BRS     34
       BRR     MSG
       END
1LETTER ** FICHE/FRAME BREAK *****
LETTER69 SYMSORT

SYM.  PG.LN. IDENT.

ABPT   10 16 LETTER  ACNO   10 15 LETTER  ACTBUF 10  3 LETTER  
ADDHR  18  4 LETTER  ADDTB  17 37 LETTER  AGAIN   4 35 LETTER  ALL    10 17 LETTER  
ALSW   10 29 LETTER  ARM1   21 22 LETTER  BCHAR   8 28 LETTER  BEGIN   2 12 LETTER  
BEGIN1  2 13 LETTER  BEGIN2  2 15 LETTER  BLNO   13 36 LETTER  BMBUF  10  1 LETTER  
BTBLND  2 25 LETTER  BUF    21 16 LETTER  BUF2   12  6 LETTER  BUFF1  15 23 LETTER  
BUICPN 25  3 LETTER  C2     15  8 LETTER  CCHAR   8 29 LETTER  CEDAT1  4 23 LETTER  
CEDATE  4 19 LETTER  CHAFIL 15 24 LETTER  CHAPTR 15 25 LETTER  CIN    25  6 LETTER  
CKALL  14  7 LETTER  CKNUM  14 32 LETTER  CKNUM1 14 36 LETTER  CKNUM2 15  1 LETTER  
CKNUM3 15  9 LETTER  CKNUM4 15 12 LETTER  CKNUM5 15 13 LETTER  CLBUF   2 33 LETTER  
CLCM1  21 27 LETTER  CLCM10 22  3 LETTER  CLCM11 22  5 LETTER  CLCM2  21 29 LETTER  
CLCM3  21 30 LETTER  CLCM4  21 31 LETTER  CLCM5  21 32 LETTER  CLCM6  21 33 LETTER  
CLCM7  21 34 LETTER  CLCM8  21 35 LETTER  CLCM9  21 36 LETTER  CLCP10 22 15 LETTER  
CLCP11 22 16 LETTER  CLCPT1 22  6 LETTER  CLCPT2 22  7 LETTER  CLCPT3 22  8 LETTER  
CLCPT4 22  9 LETTER  CLCPT5 22 10 LETTER  CLCPT6 22 11 LETTER  CLCPT7 22 12 LETTER  
CLCPT8 22 13 LETTER  CLCPT9 22 14 LETTER  CLCR   25 20 LETTER  CNTHR  18  3 LETTER  
CONV   18 19 LETTER  CONV1  18 21 LETTER  CONV2  18 24 LETTER  CONV3  18 26 LETTER  
CONV4  18 25 LETTER  CONV5  18 27 LETTER  CONVA  18 28 LETTER  CONVD  18 30 LETTER  
CONVD1 18 31 LETTER  CONVHR 15 19 LETTER  CONVSW 18 29 LETTER  COT    10 31 LETTER  
COUN   10 28 LETTER  COUN2   3  6 LETTER  COUN3   3  8 LETTER  COUN4   3 12 LETTER  
COUN5   3  3 LETTER  COUNT   3  2 LETTER  COUT   25  7 LETTER  CPNO   16 31 LETTER  
CTBL    2 23 LETTER  CTBLND  2 24 LETTER  DA     25 15 LETTER  DA1    25 10 LETTER  
DATE   21  3 LETTER  DATERR  6  9 LETTER  DATIM  20 37 LETTER  DAY    21 13 LETTER  
DISBF  13 37 LETTER  DISBUF 10  2 LETTER  DISCA  13 13 LETTER  DISPL   3 17 LETTER  
DISSW  13 33 LETTER  DIST   12  9 LETTER  DST    24 29 LETTER  DTEMP  10 23 LETTER  
EDATE  21 20 LETTER  EDCSW  10  8 LETTER  ENBUF  10  9 LETTER  EOJ    23 30 LETTER  
ERFLG   2 29 LETTER  ERMSG   2 30 LETTER  ERMSG1  2 31 LETTER  ERR     2 26 LETTER  
ERR1A   2 28 LETTER  ERRRT  21 26 LETTER  ERV    12 20 LETTER  ERV1   12 19 LETTER  
ERV2   12 22 LETTER  ERV3   12 26 LETTER  EXP2    4  9 LETTER  EXP2A   4 12 LETTER  
EXP3    4 13 LETTER  EXPIRE  4  5 LETTER  EXPTEM  4 29 LETTER  FDGAN  13 19 LETTER  
FDGAN1 13 22 LETTER  FDGAN2 13 24 LETTER  FDGAN3 13 25 LETTER  FDGAN4 13 28 LETTER  
FDGAN5 13 29 LETTER  FIX     2  4 LETTER  FIX1    2 10 LETTER  FL     16 30 LETTER  
FONO   11 29 LETTER  GETALL  8 17 LETTER  GETLUD  8 24 LETTER  GETNAM 14 15 LETTER  
GETNM1 14 18 LETTER  GETNM2 14 20 LETTER  GETNM3 14 21 LETTER  GETNM4 14 22 LETTER  
GETNM9 14 23 LETTER  GETST1 19  4 LETTER  GETST2 19  5 LETTER  GETST4 19  6 LETTER  
GETST5 19  7 LETTER  GETST6 19  9 LETTER  GETST7 19 10 LETTER  GETST9 19 11 LETTER  
GETSTR 19  2 LETTER  GETTZ  24 30 LETTER  GETTZ1 24 33 LETTER  GETTZ2 24 39 LETTER  
GETTZ3 25  1 LETTER  GEXC   23 34 LETTER  GEXC1  24  5 LETTER  GMINS   6  7 LETTER  
GPS    11 18 LETTER  GPS1   11 19 LETTER  HELP    2 38 LETTER  HR     25 16 LETTER  
HR1    25 13 LETTER  HRX    25  8 LETTER  INPUT   2 19 LETTER  INT1   21 23 LETTER  
INT1PC 21 24 LETTER  ISSU2   4 39 LETTER  LNO     9 37 LETTER  LNOW   10 19 LETTER  
LPTR   10 21 LETTER  LPTR2  10 22 LETTER  M1     22 17 LETTER  M12    22 26 LETTER  
M15    22 39 LETTER  M181   25 25 LETTER  M19    23  1 LETTER  M2     22 18 LETTER  
M20    23  2 LETTER  M21    23  3 LETTER  M221   25 26 LETTER  M231   25 27 LETTER  
M24    23  4 LETTER  M25    23  5 LETTER  M26    23  6 LETTER  M27    23  7 LETTER  
M28    23  8 LETTER  M29    23  9 LETTER  M3     22 19 LETTER  M30    23 10 LETTER  
M31    23 11 LETTER  M32    23 12 LETTER  M33    23 13 LETTER  M34    23 14 LETTER  
M35    23 15 LETTER  M36    23 16 LETTER  M37    23 17 LETTER  M38    23 18 LETTER  
M39    23 19 LETTER  M4     22 20 LETTER  M40    23 20 LETTER  M41    23 21 LETTER  
M42    23 22 LETTER  M43    23 23 LETTER  M44    23 24 LETTER  M45    23 25 LETTER  
M46    23 26 LETTER  M47    23 27 LETTER  M48    23 28 LETTER  M49    23 29 LETTER  
M5     22 21 LETTER  M6     22 22 LETTER  M7     22 23 LETTER  M8     22 24 LETTER  
M9     22 25 LETTER  MIN    25 17 LETTER  MIN1   25 12 LETTER  MINS   21 14 LETTER  
MMAP    9 10 LETTER  MMAP3   9 14 LETTER  MMAP3A  9 17 LETTER  MMAP4   9 19 LETTER  
MMAP4A  9 22 LETTER  MO     21 12 LETTER  MO0    25 14 LETTER  MO1    25  9 LETTER  
MOVE   17 25 LETTER  MOVEHR 16 28 LETTER  MOVEIT 17 29 LETTER  MSG    25 37 LETTER  
MSG     1 34 LETTER  MSG1   25 36 LETTER  MTEMP  13 35 LETTER  NAMBUF 13 38 LETTER  
NAMPTR 13 39 LETTER  NCTBUF 10 11 LETTER  NCTP   10 12 LETTER  NONAME 11 34 LETTER  
NONO   12 29 LETTER  NOPASS 11 31 LETTER  OPCHA  15 29 LETTER  OPFER  12 24 LETTER  
OPNAM  12  4 LETTER  OVSW   10 32 LETTER  P14SCH 16 21 LETTER  PACT   19 18 LETTER  
PACT1  19 21 LETTER  PACT2  19 22 LETTER  PDIST  19 25 LETTER  PDIST1 19 28 LETTER  
PDIST2 19 29 LETTER  PDN     4 32 LETTER  PERR1  19 35 LETTER  PERR2  19 36 LETTER  
PERR3  19 37 LETTER  PERR4  19 38 LETTER  PERROR 19 34 LETTER  PGX    10 24 LETTER  
PNONE   4 24 LETTER  PNONE1  4 26 LETTER  PON     4 30 LETTER  PTR1   12  3 LETTER  
PTR2   12  5 LETTER  PUN    20  7 LETTER  PUN1   20 11 LETTER  PUN1B  20 13 LETTER  
PUN1C  20 16 LETTER  PUN1D  20 19 LETTER  PUN1F  20 17 LETTER  PUN2   20 22 LETTER  
PUN2A  20 24 LETTER  PUN2B  20 26 LETTER  PUN3   20 28 LETTER  PUN4   20 30 LETTER  
PUNCH   9 29 LETTER  PUNCHA  9 31 LETTER  PUNCHB  9 32 LETTER  PUNCHX  9 33 LETTER  
PUNX   20 32 LETTER  QUES    9 36 LETTER  QUIT    2 35 LETTER  RACT   10 25 LETTER  
RCBLK  21 17 LETTER  RDNO   21 25 LETTER  REGSW  14 29 LETTER  REM2    3 32 LETTER  
REM2    3 30 LETTER  REM2A   3 35 LETTER  REMOV   3 25 LETTER  RETFD  12 30 LETTER  
RLUD   13  1 LETTER  RLUD1  13  2 LETTER  RLUD2  13  3 LETTER  RLUD3  13  5 LETTER  
RLUD3A 13  7 LETTER  RLUD3B 13  6 LETTER  RLUD4  13 11 LETTER  RLUD5  13 12 LETTER  
RLUDX1 13 14 LETTER  RLUDX2 13 15 LETTER  RPOS   10 26 LETTER  SAVA    1 37 LETTER  
SAVB    1 38 LETTER  SAVX    1 39 LETTER  SEN1    5 21 LETTER  SEN2    5 24 LETTER  
SEN3    5 27 LETTER  SEN3A   5 33 LETTER  SEN3B   6  1 LETTER  SEN3C   6 14 LETTER  
SEN4    7  1 LETTER  SEN4A   7  4 LETTER  SEN4B   7  6 LETTER  SEN4D   7  7 LETTER  
SEN4E   7  9 LETTER  SEN4F   7 10 LETTER  SEN4G   7 11 LETTER  SEN4Z   6 39 LETTER  
SEN5    7 14 LETTER  SEN5A   7 20 LETTER  SEN5AA  7 19 LETTER  SEN5B   7 22 LETTER  
SEN5C   7 23 LETTER  SEN5F   7 26 LETTER  SEN5K   7 25 LETTER  SEN6    6 18 LETTER  
SEN6A   6 22 LETTER  SEN6B   6 24 LETTER  SEN6D   6 26 LETTER  SEN6E   6 27 LETTER  
SEN6F   6 28 LETTER  SEN6G   6 29 LETTER  SEN7    7 32 LETTER  SEN7A   7 38 LETTER  
SEN7B   8  1 LETTER  SEN7E   7 37 LETTER  SEN7F   7 39 LETTER  SEN7G   7 36 LETTER  
SEN8    8  5 LETTER  SEN8A   8  8 LETTER  SEN8A1  8 11 LETTER  SEN8B   8 12 LETTER  
SEN8C   8  6 LETTER  SEN8F   8 14 LETTER  SEND    5 17 LETTER  SENREG 16  5 LETTER  
SF     12  2 LETTER  SFBUF  12  7 LETTER  SFIL   12 18 LETTER  SFPTR  12  1 LETTER  
SGPS   11 16 LETTER  SKNE    1 33 LETTER  SNDFLG  2  2 LETTER  SPTR   10 20 LETTER  
STAB    9 38 LETTER  START   1 26 LETTER  STRBUF 10  4 LETTER  STRTAB 19 13 LETTER  
T1     25  4 LETTER  T2     25  5 LETTER  T3     25 18 LETTER  TA1END 24 26 LETTER  
TABLE  16 33 LETTER  TABLN  17 17 LETTER  TCHAR   8 27 LETTER  TCK    10  6 LETTER  
TEMA    9 39 LETTER  TEMP   21 21 LETTER  TEMP1  12  8 LETTER  TEMPD  10 27 LETTER  
TEMPT  10 14 LETTER  TERCH1 10 18 LETTER  TERM   13 34 LETTER  TERMWD 11 38 LETTER  
TEST    1  2 LETTER  TIMX   21 15 LETTER  TMSK   10  7 LETTER  TZASC  24 28 LETTER  
TZEND  24 16 LETTER  TZSIZE 24 27 LETTER  TZTAB  24  7 LETTER  TZTAB1 24 17 LETTER  
UDIST  10 30 LETTER  UNBPT  10 34 LETTER  UNBUF  10  5 LETTER  UZ     25  2 LETTER  
VER    10 36 LETTER  VER1   11  5 LETTER  VERNO   2  3 LETTER  VT1    12 10 LETTER  
VT2    12 11 LETTER  VXT1   12 12 LETTER  VXT2   12 13 LETTER  VXT3   12 14 LETTER  
VXT4   12 15 LETTER  VXT5   12 16 LETTER  VXT8   12 17 LETTER  WCBLK  21 18 LETTER  
WCHTAB 14 24 LETTER  WD1    17 23 LETTER  WD2    17 24 LETTER  WEXC   15 35 LETTER  
WLET    8 35 LETTER  WLET    8 33 LETTER  WMAP    9  5 LETTER  WMAP    9  3 LETTER  
YR     25 19 LETTER  YR1    25 11 LETTER  ZERO   21 11 LETTER  
1LETTER ** FICHE/FRAME BREAK *****
BUILD1LETTER
NARP
LETTER69
BLETTER


RES
XDDT
;TBLETTER
;U1%L1%U%F
DUMP DUMP

SAVE 3 T 14777 ON 1LETTER

240
"

        1LETTER   VERSION 6.09

"
CHECKSUM
.
LETTER69-SYM-FILE
1LETTER-GO-FILE
BUILD1LETTER-BUILD-FILE
.
"

"
COM TEL
K!\ß