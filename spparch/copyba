COPYBATCH ** FICHE/FRAME BREAK *****
SCOPYBATCH10
COPBAT IDENT   4-27-77

*USE WITH EXEC 92E AND UP ONLY

*RETAIN A COPY IN HIS DIRECTORY

*
*   C O P Y B A T C H
*
* PROGRAM TO COPY USER FILES FROM HIS DIRECTORY TO
* THAT OF *1COPYBATCH
*

       BRU START; BRS 10
       ASC '10.00'; ASC 'BLOOD'
MSG    POPD 101B5,2
       STP SAVA; STX SAVX
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX TOUT; BRS 34
       LDP SAVA; LDX SAVX; BRR 0
SAVA   ZRO
SAVB   ZRO
SAVX   ZRO
START  STP CIN
RESET  CLA; STA ERFLG
       LDA =4B4; LDX =-1; BRS 12 (SET TO COMMAND EDIT)
GLTCH  LDA =32B; CIO TOUT  PRINT COLON
IN1    CIO CIN; SNE =0 (IGNORE SPACES); BRU IN1; SNE =155B (IF CR ONLY COLON)
       BRU GLTCH; LRSH 8
IN2    CIO CIN; SNE =0; BRU IN2; SNE =155B; BRU CHK (1 CHAR. SEE IF IS Q OR ?)
       LRSH 8
IN3    CIO CIN; SNE =0; BRU IN3; SNE =155B; BRU ERR (2 CHARS); LRSH 8
IN4    CIO CIN; SKE =155B; BRU IN4 (WAIT FOR CR); LDX =CTBL-CTBLND
       CBA (MOVE COMMAND INTO 'A' REGISTER); SKE CTBLND,2 (SEARCH)
       BRX *-1; BRU* BTBLND,2 (FALL THRU BRX MEANS COMMAND NOT FOUND)
CHK    CLA; LSH 8; SNE =61B (Q); BRS 10 (QUIT); SKE =37B (?)
       BRU ERR (NOT Q OR ?); BRU HEL
ERR    LDA ERFLG; SKE =0; BRU HEL; MIN ERFLG; MSG ERMSG (PRINT ?)
       BRU GLTCH
CTBL   ASC 'PACSNIREVAHCERCIUQLEHNUR'
CTBLND DATA 0,CAP,INS,VER,CHA,CRE,QUI,HEL,RUN
BTBLND DATA ERR
CAP    MSG MSG1; BRU GLTCH
INS    MSG MSG2; BRU GLTCH
VER    MSG MSG3; BRU GLTCH
CHA    MSG MSG4; BRU GLTCH
CRE    MSG MSG5; BRU GLTCH
QUI    BRS 10
HEL    MSG MSG6; BRU RESET
ERMSG  ASC ' ?$/'
MSG1   ASC 'CAPABILITIES'
       ASC '$$COPIES FILES FROM USERS DIRECTORY TO A UTILITY'
       ASC '$DIRECTORY WHICH IS AVAILABLE TO OPERATIONS PERSONNEL'
       ASC '$FOR BATCH PROCESSING.  THE UTILITY DIRECTORY IS CHECKED'
       ASC '$FOR AN EXISTING FILE BY THE SAME NAME, AND IF FOUND'
       ASC '$THE USERS FILE IS REJECTED TO PRECLUDE WRITING ON ANOTHER'
       ASC '$USERS FILE. A COMMENT CONTAINING THE USERS ACCOUNT NUMBER'
       ASC '$USER NAME AS WELL AS THE DATE AND TIME IS APPENDED'
       ASC '$TO THE FILE NAME.$$/'
MSG2   ASC 'INSTRUCTIONS'
       ASC '$$THE PROGRAM WILL ASK FOR THE NAME OF THE FILE TO BE'
       ASC '$COPIED AND IF YOU WISH TO ALSO RETAIN A COPY IN YOUR DIRECTORY.'
       ASC '$AFTER THE FILE IS COPIED THE PROGRAM WILL '
       ASC '$ASK IF ADDITIONAL FILES ARE TO BE COPIED. UPON RECEIVING'
       ASC '$A YES ANSWER, THE PROGRAM WILL AGAIN REQUEST THE NAME OF THE'
       ASC '$FILE TO BE COPIED.  UPON RECEIVING A NO ANSWER, IT WILL'
       ASC '$GO TO COMPLETION.$$/'
MSG3   ASC 'VERSION 10.00  MAY, 1977$$/'
MSG4   ASC 'NO ADDITIONAL CHARGE$$/'
MSG5   ASC 'WRITTEN BY TYMSHARE, INC.$$/'
MSG6   ASC 'LEGAL COMMANDS:'
       ASC '$$CAPABILITIES   DESCRIBES PROGRAM CAPABILITIES'
       ASC '$VERSION        LATEST UPDATE'
       ASC '$QUIT  (OR) Q   QUITS TO EXEC'
       ASC '$RUN            BEGINS EXECUTION'
       ASC '$INSTRUCTIONS   HOW TO RUN THE PROGRAM'
       ASC '$HELP  (OR) ?   REPRINTS THIS LIST'
       ASC '$CREDITS        THOSE RESPONSIBLE FOR THIS PROGRAM'
       ASC '$CHARGES        ADDITIONAL COST (IF ANY)$'
       ASC '$$ANY OF THESE COMMANDS MAY BE SHORTENED TO THE'
       ASC '$FIRST THREE LETTERS$$/'
ERFLG  BSS 1
RUN    BRM GBUN (GET FDPT OF *1COPYBATCH)
BEGIN1 MSG M1 
       BRM GFIL (GET INPUT FILE NAME AND OPEN)
       BRM GUSER (ADD COMMENT TO FILE NAME STRING AND USER NAME)
       BRM GTIME (ADD DATE-TIME TO COMMENT STRING)

*TEST FD OF *1COPYBATCH TO SEE IF FILE NAME ALREADY USED

       BRM GDIR

*DETERMINE IF HE WANTS TO KEEP COPY IN HIS OWN DIRECTORY IF NOT
*RENAME RATHER THAN COPY

BEGIN2 MSG M10 (DO YOU WISH TO ALSO RETAIN THE FILE IN YOUR DIRECTORY?)
       BRM CLBUF; CIO CIN; CAB; CIO CIN; SKE =155B; BRU *-2
       CBA; SKE ='   Y'; BRU *+2; BRU BEGIN4 (OKAY COPY IT)
       SKE ='   N'; BRU BEGIN2 (MAKE UP YOUR MIND)
       BRU RENAME

*GET FD OF *1COPYBATCH
BEGIN4 LDA BUNO; LDX =1; BRS 141


*OPEN OUTPUT FILE
       LDX TYPE; LDP PTR; BRS 65; BRU NOPEN
       STA FNO

CC     LDA =2B4; LDX =1B4; BIO FNI; NOP; LDB =2B5; SKB FNI; BRU CD
       LDA =2B4; LDX =1B4; BIO FNO; NOP; BRU CC
CD     SUB =1B4; LDX =1B4; BIO FNO; NOP
       RSP FNO; XMA CPTOP; MRG =4B7; SCP FNO; NOP
       LDA CPTOP; PCE FNO; NOP; MSG M9; BRU MORE
GUSSW  DATA -1


GUSER  ZRO; SKN GUSSW; BRU GUSER3; MIN GUSSW
       LDA =8; LDX =1; LDB =CFDPT; BRS 67; LDA =31; LDX =3
       LDB =GAN; BRS 67; BRM HDIR; LDA LUDL; ETR =1777B; STA BLNO
       LDA LUDL
       LRSH 10; ADD =NBUF; STA BPOS; BRM GE1; LDX BPOS
       LDA 4,2; SKE CFDPT; HLT; LDA BPOS; MUL =3
       LSH 23; SUB =1; CAB; ADD =12; XAB; STP NPTR
       GCD NPTR; HLT 0; SKG =0; BRU *-3; MIN NPTR+1
       LDP NPTR; STP HPTR
GUSER3 LDA =15B; WCI PTR; LDA =17B; WCI PTR
GUSER1 GCI NPTR; BRU GUSER2; WCI PTR; BRU GUSER1
GUSER2 CLA; WCI PTR; BRR GUSER
NODIR  MSG FERR4; BRU MORE
FERR4  ASC '$UNABLE OPEN FILE$/'
HDIR   ZRO; LDA FDLUDL; ETR =1777B; STA BLNO; LDA FDLUDL; LRSH 10
       ADD =NBUF; STA BPOS; BRM GE1; LDX BPOS; LDA 4,2; STA RFDPT
       BRR HDIR
RFDPT  ZRO
NOPEN  MSG FERR3; BRS 10
FERR3  ASC '$CANNOT OPEN OUTPUT FILE, PLEASE CALL THE OPERATOR/'
USED   LDP PTR2; LDX TOUT; BRS 35; MSG FERR2; BRS 10
NOGOOD MSG NOUSE; BRS 10
NOUSE  ASC '$PROGRAM CANNOT BE USED ON THIS SYSTEM/'
VCONT  CIO CIN; WCI PTR; BRU GFIL1
GFIL   ZRO
GFIL1  CIO CIN; SKE =155B; BRU *+2; BRU GFIL2
       SNE ='V'+100B; BRU VCONT
       WCI PTR; BRU GFIL1
GFIL2  LDP PTR; STP PTR2; BRS 48; BRU *+2; BRU GFIL3
       LDA =155B; CIO TOUT; LDP PTR; LDX TOUT
       BRS 35; MSG FERR1; LDA PTR; STA PTR1; BRU GFIL1
GFIL3  LDP PTR; BRS 64; BRU NODIR; STA FNI; STB TYPE; BRM SSTG; BRM GIMMIC
       RSP FNI; STA CPTOP; BRR GFIL

FERR1  ASC ' IS A BAD FILE NAME, PLEASE RE-ENTER: /'
FERR2  ASC '$IS A FILE NAME ALREADY USED IN THE BATCH PROCESSING'
       ASC '$DIRECTORY, PLEASE RENAME YOUR FILE AND RECALL THIS PROGRAM.'
       ASC '$THANK YOU./'
M1     ASC '$FILE NAME: /'

PTR    DATA 3*FILE-1
PTR1   DATA 3*FILE-1
PTR2   BSS 2

FILE   BSS 100
FNI    ZRO
FNO    ZRO
NBUF   BSS 64
CIN    ZRO
TOUT   ZRO
TEMP   ZRO
GBUN   ZRO; CLA; LDX =-4; ADD BNAME+4,2; BRX *-1
       LRSH 23; DIV =887; STB BLNO; CBA; SKG =895; BRU GBUN1
       BRU NOGOOD
GBUN1  BRM GE1; EAX NBUF
GBUN1A STX BLX; LDA 0,2; SKE =0; BRU *+2; BRU NOGOOD
       SKE =-1; BRU *+3; MIN BLNO; BRU GBUN1
       SKE =-2; BRU *+2; BRU GBUN2; CXA; MUL =3; LSH 23
       SUB =1; CAB; ADD =12; XAB; SKSE BNPT; BRU GBUN2
       LDA 4,2; STA BUNO; BRR GBUN
GBUN2  LDX BLX; EAX 9,2; BRU GBUN1A
BNPT   DATA 3*BNAME-1,3*BNAME-1+12
BNAME  ASC '*1C'
       ASC 'OPY'
       ASC 'BAT'
       ASC 'CH '
BUNO   ZRO
GDIR   ZRO; LDA BUNO; STA GRP; LDP COMPT; STP HCOMPT
       STP NAMPTR; STP HNPTR
S4A    BRM RDIR; LDA =BUF; STA BUFPTR
LOOK   LDX BUFPTR; LDA 0,2; SKE =0; BRU *+2
       BRU TESTOV; SKE =-1; BRU *+2; BRU TESTOV
       SKE =-2; BRU *+2; BRU TESTOV
LOOK1  EAX 1,2; LDA 0,2; SKE =-1; BRU *+2; BRU TESTOV; SKE =-2
       BRU *+2; BRU TESTOV; ETR =7B7; SKE =2B7; BRU LOOK1; EAX 1,2
LOOK2  EAX 1,2; LDB 0,2; SKB =4B7
       BRU ENDNM; CLA; LSH 8; WCI NAMPTR; CLA
       LSH 8; WCI NAMPTR; CLA; LSH 8; WCI NAMPTR; BRU LOOK2

ENDNM  EAX 1,2; CXA; STA BUFPTR; LDX =-3
ENDNM1 CLA; LSH 8; SKE =136B; BRU *+2; BRU TRIM; ETR =177B
       WCI NAMPTR; BRX ENDNM1

TRIM   LDA =-1; STA PROTC
TRIM1  GCI NAMPTR; BRU COMP; SNE ='/'; BRM STGPRO; SNE ="'"; BRM STGPRO
       SNE ='"'; BRM STGPRO; SKN PROTC; BRU TRIM2 (IN PROTECTED STRING)
       BRU HYPHEN
TRIM2  WCI COMPT; BRU TRIM1

LOOK1A MIN BUFPTR; BRU LOOK

HYPHEN  SKE ='   -'; BRU TRIM2; BRU COMP

TESTOV LDX =BUF; LDA 125,2; SKE =0; BRU *+2
       LDA 124,2; SKE =0; BRU *+2; BRR GDIR; SKE BUNO; BRU *+2
       BRR GDIR; STA GRP; BRU S4A


RDIR    ZRO; LDA GRP; CLB; LRSH 23; DIV =64
       SKG =3; BRU RDIR1; SKG =35; BRU *+3; SUB =16; BRU RDIR1
       SUB =4; XAB; SUB =100B; XAB
RDIR1   STB TEMP; CLB; LSH 12; ADD TEMP; LSH 1; ADD =200B; CAB; LDA =BUF
       LDX =200B; BRS 124; BRR RDIR

COMP   LDA =-1; STA PROTC; LDP COMPT; SKSE PTR2; BRU *+2; BRU HIT
       LDP HCOMPT; STP COMPT; LDP HNPTR; STP NAMPTR
       BRU LOOK
HIT    BRU USED

COMPT  DATA 3*CFILE-1,3*CFILE-1
CFILE  BSS 100
HCOMPT BSS 2
NAMPTR DATA 3*OFILE-1,3*OFILE-1
OFILE  BSS 100
HNPTR  BSS 2
GRP    ZRO
BUF    BSS 128
BUFPTR ZRO
GTIME  ZRO; LDP TMPTR; BRS 91; STP TMPTR; GCI TMPTR; NOP; SKE =20B
       WCI PTR; GCI TMPTR; BRU *+6; SKE =17B; BRU *+2; LDA =15B; WCI PTR
       BRU *-6; GCD PTR; NOP; SKG =0; BRU *-3; WCI PTR; LDA =17B
       WCI PTR; BRR GTIME
TMPTR  DATA 3*TIME-1,3*TIME-1
TIME   BSS 10
HTMPTR DATA 3*TIME-1,3*TIME-1
MORE   BRS 147
       LDA RFDPT; LDX =1; BRS 141
       MSG M2; CIO CIN; SKE =71B; BRU *+2; BRU MORE1; SKE =56B
       BRU MORE; BRU THRU
MORE1  CIO CIN; SKE =155B; BRU *-2; LDP HPTR; STP NPTR
       LDA PTR; STA PTR1
       LDP HCOMPT; STP COMPT; LDP HNPTR; STP NAMPTR
       LDP HTMPTR; STP TMPTR
       CLEAR; STP PTR2; BRU BEGIN1
THRU   CIO CIN; SKE =155B; BRU *-2; MSG M3; BRS 10
M2     ASC '$$MORE FILES TO BE COPIED? /'
M3     ASC '$$YOUR FILES ARE IN THE BATCH PROCESSING DIRECTORY, PLEASE'
       ASC '$USE THE BATCH PROCESSING REQUEST PROGRAM TO TRANSMIT'
       ASC '$YOUR INSTRUCTIONS TO THE OPERATOR.  THANK YOU.$/'
M7     ASC '$YOUR FILE NAME HAS BEEN MODIFIED TO /'
M8     ASC '$SO IT IS NOT MADE PUBLIC TO OTHER USERS$/'
M9     ASC     '$COPY COMPLETED/'
M10    ASC '$ALSO RETAIN THE FILE IN YOUR DIRECTORY? /'
M11    ASC '$TROUBLE COPYING THIS FILE - COPYBATCH DIRECTORY'
       ASC '$MAY BE BUSY.  COPY ABORTED$/'

* GIMMIC
* TAKES PTR, A FILE NAME, AND MAKES IT NON-PUBLIC
*
GIMMIC ZRO
       CLA; STA ZFLG (RESET GIMMIC FLAG); LDA =3*ZSTR-1; CAB
       STP ZPTR; GCI PTR; BRU *+3; WCI ZPTR; BRU *-3; LDA =3*FILE-1
       CAB; STP PTR
GIMM1  GCI ZPTR; BRU GIMME; SKE ='@'; BRU GIMM2; LDA ='A'; BRU GIMMW
GIMM2  SKG ='Z'+100B; SKG ='A'+100B-1; BRU GIMMO; STA ZFLG; LDA ='&'
       WCI PTR; LDA ZFLG; ETR =77B; BRU GIMMW
GIMMO  WCI PTR; BRU GIMM1
GIMMW  WCI PTR; LDA =-1; STA ZFLG; BRU GIMM1
GIMME  SKN ZFLG; BRR GIMMIC; MSG M7; LDP PTR; LDX TOUT; BRS 35
       MSG M8; BRR GIMMIC

GE1    ZRO; LDA BLNO; LRSH 23; DIV =128; STB MTEMP; CLB; LSH 13
       ADD =320200B; ADD MTEMP; CAB; LDA =NBUF; LDX =100B
       BRS 124; BRR GE1

*RENAME ROUTINE

RENAME BRS 147; LDA RFDPT; LDX =1; BRS 141
       LDP HLSPTR; STP LSPTR; LDP ONPTR; STP WKPTR
       GCI WKPTR; BRU REN1; WCI LSPTR; BRU *-3
REN1   LDP LSPTR; STP HOLDPT; LDP HBNPT; STP WKPTR
       GCI WKPTR; BRU REN2; WCI LSPTR; BRU *-3
REN2   LDP PTR; STP WKPTR; GCI WKPTR; BRU REN4
       WCI LSPTR; BRU *-3
REN4   LDX LSPTR+1; LDP HOLDPT; BRS 58; BRU REN5
       MSG M9; BRU MORE
REN5   MSG M11 (SYSTEM ERROR); BRU MORE
CLBUF  ZRO; LDA =155B; CIT CIN; BRR CLBUF; BRU *-2
DIRR   ZRO
       ETR =7777B; CLB; LRSH 23; DIV =64; SKG =3; BRU DIRR1
       SKG =35; BRU *+3; SUB =16; BRU DIRR1; SUB =4
       XAB; SUB =100B; XAB
DIRR1  STB TLV3; CLB; LSH 12; ADD TLV3; LSH 1; ADD =200B; CAB
       LDA =DBUF; LDX =128; STB DISC; BRS 124; BRR DIRR

DIRW   ZRO; LDA =DBUF; LDB DISC; LDX =128; BRS 125; BRR DIRW
TLV3   ZRO
DBUF   EQU 5000B
OFPTR  EQU DBUF+175B
LWORD  DATA DBUF+172B
DISC   ZRO
FILBUF BSS 100
FD1    EQU FILBUF+1
FD2    EQU FILBUF+2
FD3    EQU FILBUF+3
IBP    EQU FILBUF+4
FILBF  BSS 100
FGRP   ZRO
FGRP1  ZRO
IBP1   ZRO
RNPTR  BSS 2

*SAVE OLD STRING FOR POSSIBLE RENAME

SSTG   ZRO; LDP PTR; STP RNPTR; LDP XNPTR; STP ONPTR; GCI RNPTR
       BRR SSTG; WCI ONPTR; BRU *-3
XNPTR  DATA 3*FILE1-1,3*FILE1-1
FILE1  BSS 30
ONPTR  BSS 2
CFDPT  BSS 1
GAN    BSS 1
FDLUDL BSS 1
LUDL   BSS 1
BLNO   BSS 1
MTEMP  BSS 1
BLX    ZRO
BPOS   ZRO
CPTOP  ZRO
ZPTR   BSS 2 TEMP POINTERS FOR GIMMIC ROUTINE
ZSTR   BSS 100
ZFLG   ZRO 0 NEG IF GIMIIC FOUND ERROR
NPTR   BSS 2
TYPE   ZRO
HPTR   BSS 2
ONBUF  BSS 100
HLSPTR DATA 3*ONBUF-1,3*ONBUF-1
LSPTR  BSS 2
WKPTR  BSS 2
HOLDPT BSS 2
NAMEB  ASC '(*1COPYBATCH)'
HBNPT  DATA 3*NAMEB-1,3*NAMEB-1+13
STGPRO ZRO; SKN PROTC; BRU *+3; STA PROTC; BRR STGPRO (SET PROTECTED)
       SKE PROTC; BRR STGPRO (WITHIN PROTECTED STRING)
       CAB; LDA =-1; STA PROTC; CBA; BRR STGPRO (SET NOT PROTECTED)
PROTC  ZRO
       END
COPYBATCH ** FICHE/FRAME BREAK *****
SCOPYBATCH10 SYMSORT

SYM.  PG.LN. IDENT.

BEGIN1  3  5 COPBAT  BEGIN2  3 17 COPBAT  BEGIN4  3 24 COPBAT  
BLNO    9 18 COPBAT  BLX     9 20 COPBAT  BNAME   5 23 COPBAT  BNPT    5 22 COPBAT  
BPOS    9 21 COPBAT  BTBLND  2  2 COPBAT  BUF     6 38 COPBAT  BUFPTR  6 39 COPBAT  
BUNO    5 27 COPBAT  CAP     2  3 COPBAT  CC      3 31 COPBAT  CD      3 33 COPBAT  
CFDPT   9 14 COPBAT  CFILE   6 32 COPBAT  CHA     2  6 COPBAT  CHK     1 35 COPBAT  
CIN     5  9 COPBAT  CLBUF   8 21 COPBAT  COMP    6 26 COPBAT  COMPT   6 31 COPBAT  
CPTOP   9 22 COPBAT  CRE     2  7 COPBAT  CTBL    1 39 COPBAT  CTBLND  2  1 COPBAT  
DBUF    8 31 COPBAT  DIRR    8 22 COPBAT  DIRR1   8 26 COPBAT  DIRW    8 29 COPBAT  
DISC    8 34 COPBAT  ENDNM   6  1 COPBAT  ENDNM1  6  2 COPBAT  ERFLG   3  3 COPBAT  
ERMSG   2 10 COPBAT  ERR     1 37 COPBAT  FD1     8 36 COPBAT  FD2     8 37 COPBAT  
FD3     8 38 COPBAT  FDLUDL  9 16 COPBAT  FERR1   4 34 COPBAT  FERR2   4 35 COPBAT  
FERR3   4 19 COPBAT  FERR4   4 13 COPBAT  FGRP    9  2 COPBAT  FGRP1   9  3 COPBAT  
FILBF   9  1 COPBAT  FILBUF  8 35 COPBAT  FILE    5  5 COPBAT  FILE1   9 12 COPBAT  
FNI     5  6 COPBAT  FNO     5  7 COPBAT  GAN     9 15 COPBAT  GBUN    5 12 COPBAT  
GBUN1   5 15 COPBAT  GBUN1A  5 16 COPBAT  GBUN2   5 21 COPBAT  GDIR    5 28 COPBAT  
GE1     8  5 COPBAT  GFIL    4 24 COPBAT  GFIL1   4 25 COPBAT  GFIL2   4 28 COPBAT  
GFIL3   4 31 COPBAT  GIMM1   7 36 COPBAT  GIMM2   7 37 COPBAT  GIMME   8  2 COPBAT  
GIMMIC  7 32 COPBAT  GIMMO   7 39 COPBAT  GIMMW   8  1 COPBAT  GLTCH   1 26 COPBAT  
GRP     6 37 COPBAT  GTIME   7  1 COPBAT  GUSER   3 39 COPBAT  GUSER1  4 10 COPBAT  
GUSER2  4 11 COPBAT  GUSER3  4  9 COPBAT  GUSSW   3 36 COPBAT  HBNPT   9 35 COPBAT  
HCOMPT  6 33 COPBAT  HDIR    4 14 COPBAT  HEL     2  9 COPBAT  HIT     6 29 COPBAT  
HLSPTR  9 30 COPBAT  HNPTR   6 36 COPBAT  HOLDPT  9 33 COPBAT  HPTR    9 28 COPBAT  
HTMPTR  7  7 COPBAT  HYPHEN  6 13 COPBAT  IBP     8 39 COPBAT  IBP1    9  4 COPBAT  
IN1     1 27 COPBAT  IN2     1 29 COPBAT  IN3     1 31 COPBAT  IN4     1 32 COPBAT  
INS     2  4 COPBAT  LOOK    5 31 COPBAT  LOOK1   5 34 COPBAT  LOOK1A  6 11 COPBAT  
LOOK2   5 36 COPBAT  LSPTR   9 31 COPBAT  LUDL    9 17 COPBAT  LWORD   8 33 COPBAT  
M1      4 38 COPBAT  M10     7 25 COPBAT  M11     7 26 COPBAT  M2      7 18 COPBAT  
M3      7 19 COPBAT  M7      7 22 COPBAT  M8      7 23 COPBAT  M9      7 24 COPBAT  
MORE    7  8 COPBAT  MORE1   7 12 COPBAT  MSG     1 16 COPBAT  MSG1    2 11 COPBAT  
MSG2    2 20 COPBAT  MSG3    2 28 COPBAT  MSG4    2 29 COPBAT  MSG5    2 30 COPBAT  
MSG6    2 31 COPBAT  MTEMP   9 19 COPBAT  NAMEB   9 34 COPBAT  NAMPTR  6 34 COPBAT  
NBUF    5  8 COPBAT  NODIR   4 12 COPBAT  NOGOOD  4 21 COPBAT  NOPEN   4 18 COPBAT  
NOUSE   4 22 COPBAT  NPTR    9 26 COPBAT  OFILE   6 35 COPBAT  OFPTR   8 32 COPBAT  
ONBUF   9 29 COPBAT  ONPTR   9 13 COPBAT  PROTC   9 39 COPBAT  PTR     5  1 COPBAT  
PTR1    5  2 COPBAT  PTR2    5  3 COPBAT  QUI     2  8 COPBAT  RDIR    6 20 COPBAT  
RDIR1   6 23 COPBAT  REN1    8 14 COPBAT  REN2    8 16 COPBAT  REN4    8 18 COPBAT  
REN5    8 20 COPBAT  RENAME  8 11 COPBAT  RESET   1 24 COPBAT  RFDPT   4 17 COPBAT  
RNPTR   9  5 COPBAT  RUN     3  4 COPBAT  S4A     5 30 COPBAT  SAVA    1 20 COPBAT  
SAVB    1 21 COPBAT  SAVX    1 22 COPBAT  SSTG    9  9 COPBAT  START   1 23 COPBAT  
STGPRO  9 36 COPBAT  TEMP    5 11 COPBAT  TESTOV  6 15 COPBAT  THRU    7 17 COPBAT  
TIME    7  6 COPBAT  TLV3    8 30 COPBAT  TMPTR   7  5 COPBAT  TOUT    5 10 COPBAT  
TRIM    6  5 COPBAT  TRIM1   6  6 COPBAT  TRIM2   6  9 COPBAT  TYPE    9 27 COPBAT  
USED    4 20 COPBAT  VCONT   4 23 COPBAT  VER     2  5 COPBAT  WKPTR   9 32 COPBAT  
XNPTR   9 11 COPBAT  ZFLG    9 25 COPBAT  ZPTR    9 23 COPBAT  ZSTR    9 24 COPBAT  

COPYBATCH ** FICHE/FRAME BREAK *****
MAKECOPYBATCH10
NARP
SCOPYBATCH10
BSCOPYBATCH10



RES
XDDT
;TBSCOPYBATCH10
;U
%F
SAVE 3 TO 7777 ON GCOPYBATCH10

240
"


     COPYBATCH VERSION 10

"
CHECKSUM
.
(SS940UTIL)SCOPYBATCH10-SYMBOLICS
(SS940UTIL)GCOPYBATCH10-GO-FILE
(SS940UTIL)MAKECOPYBATCH10-BUILD-FILE
.
"

"
COM TEL
  