MAGCON ** FICHE/FRAME BREAK *****
SMAGCON23
MAGCON IDENT

*      MAGTAPE CONVERSION PROGRAM
*      VERSION  3.00
*      T'M B'S
*
*      MAGCON IS NOW SET UP TO MAKE ADDITIONS OF FURTHER CODES
*      EASY.  IN ORDER TO ADD NEW CODES THE FOLLOWING TABLES NEEDS
*      TO BE UPDATED.

*      CHCTAB  CONTAINS THE NUMBER OF CHARACTERS PER WORD
*      PARTAB -WHETHER THE PARITY IS EVEN (-1) OR ODD(1)
*      SHIFTC -NUMBER OF BITS PER CHARACTER
*      TRATAB -INDEXED POINTER TO THE TRANSLATION TABLE
*      CODEHM -HELP MESSAGE. ADD A DESCRIPTION OF THE CODE
*
*      A TRANSLATION TABLE MUST BE SET UP ALSO.  THIS MAY BE DOEN
*      BY TKAKING THE VALUE OF THE CHARACTER TO BE TRANSLATED AND
*      USING THAT AS AN INDEX INTO THE TRANSLATION TABLE PLACING
*      THE 940 INTERNAL VALUE OF THAT CHARACTER IN THE POSITION IN THE
*      TABLE.
*
*      IN ORDER TO USE THE FILE FEATURE THERE IS A UTILITY PROGRAM
*      THAT WILL PROVIDE AN OUTPUT FILE IN THE CORRECT FORMAT.
*      THIS PROGRAM IS
*      AND ASKS FOR THE CHARACTER SIZE AND PARITY FOR THE TAPE
*      IT THEN ASKS FOR THE OCTAL OR HEX REPRESENTATION OF THE CHARACTER
*      ON THE TAPE. AND CREATES AN OUTPUT FILE.
*
***    IT WOULD BE NICE TO HAVE A MECHANISM TO ALLOW TAPE
***    WRITING AT THE SAME TIME, BUT CONSIDERING THE NUMBER OF
***    DIFFERENT PROGRAMS AND FORMATS THIS IS MORE EASILY ACCOMPLISHED
***    THROUGH A PROGRAM LIBRARY
*
       BRU BEGIN

OSA    EQU     14000B          OUTPUT CORE BUFFER STARTING ADDRESS
NOW    EQU     40000B-OSA      OUTPUT CORE BUFFER SIZE

*      OUTPUT CODE

*      OUTPUT CHARACTER TO WORD

OCR    EQU *
OCW    ZRO; STA OCWA; STB OCWB; CLB; LDA CWD; LSH 8; MRG OCWA
       STA CWD; SKR OCC; BRU *+2; BRU OCW1
OCWR   LDA OCWA; LDB OCWB; BRR OCW
OCW1   STX OCWX; BRM OWM; LDA =2; STA OCC; LDX OCWX; BRU OCWR

OCWA   ZRO       0       SAVE A REGISTER
OCWB   ZRO       0       SAVE B REGISTER
OCWX   ZRO       0       SAVE X REGISTER
CWD    ZRO       0       CURRENT WORD BEING MADE UP
OCC    DATA 2       0       OUTPUT CHARACTER COUNTDOWN

*      OUTPUT WORD TO MEMORY

OWM    ZRO; LDA CWD; STA* OWP; MIN OWP; SKR OWC; BRR OWM
       BRM OPR; LDA =NOW-1; STA OWC; LDA =OSA
       STA OWP; BRR OWM

OWP    DATA OSA       0       OUTPUT WORD POINTER
OWC    DATA NOW-1       0       OUTPUT COUNTDOWN

*      OUTPUT DATA IN CORE OUTPUT BUFFER TO DISC

OPR    ZRO; LDA OWP; SUB =OSA; LDX =OSA; BIO OUT; NOP; TCO =141B
       LDA =NOW-1; STA OWC; LDA =OSA; STA OWP; BRR OPR

*      PUSH ANY LEFT OVER CHARACTERS THROUGH TO DISC
*      ERASE ANY BLANKS LEFT ON FILE BY THE FINAL BIO

COF    ZRO; LDA OCC; SKA =6; BRU COF1; CLA; BRM OCR; MIN NBE; BRU COF+1
COF1   BRM OPR; LDA NBE; SKG =0; BRR COF; RSP OUT; CAB; SUB NBE; MRG =4B7
       SCP OUT; ZRO; CBA; PCE OUT; ZRO; CLA; STA NBE; BRR COF

NBE    ZRO       0       NUMBER OF BLANKS TO ERASE, IF ANY


BUFF   EQU 10000B              TAPE DATA BUFFER STARTING ADDRESS
LMAX   EQU 3000000             MAXIMUM SIZE FILE IF LINE ORIENTED
HMAX   EQU 3100000             MAXIMUM SIZE FILE IF STRAIGHT DATA - NO RETURNS

*      MESSAGE DRIVER

PRINT  MACRO D,G,2
       BRU G(1)
G(2)   ASC 'D(1)/'
G(1)   LDA =G(2)
       BRM MSG
       ENDM

BEGIN  STA COMFIL; BRS 39; SKA =1B7; BRU START
       PRINT ($MIN. OF OPER STATUS REQUIRED$); BRS 10

MSG    ZRO; LDB =-1; LDX =1; BRS 34; BRR MSG

*      GENERAL PROGRAM FLOW (MAIN SECTIONS SEPRATED BY ------)

START  BRM ASSIGN     ASSIGN TAPE UNIT 0 TO CONTROLING TELETYPE STA.
       BRM ARMINT     ARM SOFTWARE INTERRUPTS
       BRM OPTION     SELECT CONVERSION OPTIONS
       BRM OUTPUT     SELECT OUTPUT FILE NAME AND FILE TYPE AND OPEN
       BRM CONVRT     READ DATA FROM TAPE TO CORE TO DISC
       BRM QUIT       CLOSE OUTPUT FILE, RELEASE DEVICE
       BRS 10         RETURN TO EXEC

*-----------------------------------------------------------------------

*      ASSIGN TAPE UNIT 0 TO THE CONTROLLING TELETYPE STATION

ASSIGN ZRO
ASSN   BRS 104; SKE =-1; BRU DEVASS; BRM VERS
       CLA; LDX =-1; BRS 100; BRU ERRASS; BRS 110; BRU *+2; BRU ASSN1
       PRINT ($PUT TAPE DRIVE IN READY$); BRS 110; BRU *-1
ASSN1  TCO =155B; BRS 108; CAX; LDA DTY,2; LDB =10; LDX =1; BRS 36; PRINT ( BPI$)
       LDA =17777777B; BRS 92; BRR ASSIGN (MAX FILE QUANTUM BRS 92)

*      NO DEVICE ASSIGNED - BUT CANNOT ASSIGN

ERRASS PRINT ($CANNOT ASSIGN DEVICE$); BRS 10

*      A DEVICE IS CURRENTLY ASSIGNED TO A CHANNEL

DEVASS STA DEV; STX CHAN; PRINT ($CHANNEL ); LDA CHAN; LDB =10; LDX =1
       BRS 36; PRINT ( HAS); LDX DEV; BRU *+1,2; BRU M0; BRU M1; BRU M2
M0     PRINT ( TAPE DRIVE 0); BRU R
M1     PRINT ( TAPE DRIVE 1); BRU R
M2     PRINT ( THE PRINTER)
R      PRINT ( ASSIGNED$); BRS 10

VERS   ZRO; PRINT ($VERSION 2.03$); BRR VERS

*-----------------------------------------------------------------------

*      ARM SOFTWARE INTERRUPTS

ARMINT ZRO; LDA =ESC; STA 201B; LDA =2B6; BRS 78; BRR ARMINT

*      PROCESS ALTMODE INTERRUPT - GO TO THE OPTION SUBROUTINE

ESC    ZRO SIRETN; LDX =-1; BRS 11; LDX =-1; BRS 29
       BRM ARMINT; LDA =6; BRS 105; BRM CLEAR; BRU OPTION+1

*-----------------------------------------------------------------------

*      SELECT OPTIONS

OPTION ZRO
       BRM CLEAR      INITIALIZE SWITCHES AND COUNTERS
       BRM CODE       SELECT ONE OF THE CONVERSION CODES
*      BRM     ADVANCE         GET INFO ABOUT ADVANCE BACKSPACE
       BRM LABEL      SPECIFY LENGTH AND NATURE OF THE TAPE HEADER
       BRM BLOCK      GENERATE A RETURN AFTER EACH BLOCK
       BRM RETURN     GENERATE A RETURN AFTER EACH RECORD
       BRM PARITY     MANUAL SELECTION OF MAG TAPE PARITY
       BRM SUBST      MAKE SUBSTITUTIONS
       BRM TRAIL      DELETE TRAILING BLANKS
       BRM RECORD     CONVERT ONLY THIS NUMBER OF CHARACTERS PER RECORD
       BRM CMPMB      COMPRESS MULTIPLE BLANKS (SYMBOLIC FILES ONLY)
       BRM LOWER      CONVERT LOWER TO UPPER CASE
       BRM IGNORE     IGNORE TAPE READ ERRORS
       BRR OPTION

*      CLEAR OUT SWITCHES AND COUNTERS

CLEAR  ZRO; LDA =-1; STA LAB; STA BLK; STA PAR; STA TRA; STA LABTYP
       STA RECTOT; STA CMP; STA RET; BRM CLSUB; STA LOW; STA IGN
       CLA;*STA BAK;*STA ADV; STA LINES; STA COUNT; STA TOTAL; BRR CLEAR
CLSUB  ZRO; LDX =-377B; LDA =-1; STA SUB; STA SUBTAB+377B,2
       BRX *-1; BRR CLSUB

*      SELECT CONVERSION CODE - Q FOR QUIT

CODE   ZRO; PRINT ($CODE: ); BRM RDNO; XAB; SKE ='   Q'; BRU *+2
       BRU START+5
       SKE ='  ?'; BRU *+2; BRU CODEH
       XAB; SKA =4B7; CLA; SKG =NUMBER; BRU *+2; BRU CODE+1
       STA CNVCDE; CAX; LDA TRATAB,2; STA TABPTR
       LDA CNVCDE; SKE =7; BRU *+2; BRU CODESP
       BRM CRLF; BRR CODE; BRU CODE+1
CODEH  LDA =CODEHM; BRM MSG; BRU CODE+1
CODEHM ASC     '$CODES AVAILABLE'
       ASC     '$0     DUMP MODE'
       ASC     '$1     EBCDIC'
       ASC     '$2     BCD'
       ASC     '$3     UNIVAC 1108'
       ASC     '$4     HONEYWELL'
       ASC     '$5     GENERAL ELECTRIC'
       ASC     '$6     370 TRTCH ET FORMAT'
       ASC     '$7     SPECIAL FORMAT FROM FILE'
       ASC     '$8     370 TRTCH E FORMAT'
       ASC     '$/'

CODESP STB TEMPB; PRINT ($FROM FILE: ); LDA COMFIL; BRS 62
       BRU CDSPE; STA TEMP
       SKG =2; BRU CDSPE
       CBA; SKE =2; BRU CDSPE
       CIO COMFIL
       WIO TEMP; STA CHCTAB+7
       WIO TEMP; STA PARTAB+7
       WIO TEMP; STA SHIFTC+7
       LDA =256; LDX =SPTAB; BIO TEMP
       NOP; LDA TEMP; BRS 20
       LDB TEMPB; BRM CRLF; BRR CODE; BRU CODE+1

CDSPE  PRINT ($CANNOT OPEN FILE ); BRU CODE+1

*      NUMBER OF FILES TO ADVANCE

*ADVANCE ZRO
*      PRINT   (ADVANCE: )
*      BRM     RDNO
*      STA     ADV
*      SKG     =0
*      BRU     BACKSPACE
*      BRM     CRLF
*      BRR     ADVANCE
*      BRU     ADVANCE+1
*
*      NUMBER OF FILES TO BACK OVER
*
*ACKSPACE PRINT (BACKSPACE: )
*      BRM     RDNO
*      STA     BAK
*      BRM     CRLF
*      BRR     ADVANCE
*      BRU     ADVANCE+1
*
*      SPECIFY TAPE LABEL OR HEADER

LABEL  ZRO; PRINT (LABEL: ); BRM RDNO; STA LAB; BRM CRLF; BRR LABEL
       SKE ='   R'; BRU *+2; BRU *+3; SKE ='   F'; BRU LABEL+1
       STA LABTYP; BRM CRLFYN; BRR LABEL; BRU LABEL+1

*      NUMBER OF CHARACTERS PER BLOCK

BLOCK  ZRO; PRINT (RECORD: ); BRM RDNO; STA BLK; BRM CRLF; BRR BLOCK
       BRU BLOCK+1

*      CONVERT ONLY THIS MANY CHARACTERS PER RECORD

RECORD ZRO; PRINT (NUMBER OF CHARACTERS IN A BLOCK: ); BRM RDNO; STA RECTOT; MIN RECTOT
       BRM CRLF; BRR RECORD; BRU RECORD+1


*      USE THIS MACRO IN THE NEXT 5 ROUTINES

BRN    MACRO D; PRINT D(2); BRM CRLFYN; BRR D(1); BRM YESNO; BRU *+2
       BRU D(1)+1; STA D(3); BRM CRLFYN; BRR D(1); BRU D(1)+1; ENDM

*      SET SWITCH FOR RETURN AFTER EACH RECORD

RETURN ZRO; BRN RETURN,((RETURN? )),RET

*      SWITCH FOR MANUAL SETTING OF TAPE PARITY

PARITY ZRO; BRN PARITY,((ODD PARITY? )),PAR

*      ALLOW USER TO COMPRESS MULTIPLE BLANKS ON SYMBOLIC FILES

CMPMB  ZRO; BRN CMPMB,((COMPRESS? )),CMP

*      DELETE TRAILING BLANKS

TRAIL  ZRO; BRN TRAIL,((TRAIL? )),TRA

*      CONVERT LOWER TO UPPER CASE IF ANSWERED WITH AN N

LOWER  ZRO; BRN LOWER,((LOWER? )),LOW

*      IGNORE TAPE READ ERRORS

IGNORE ZRO; BRN IGNORE,((IGNORE? )),IGN

*      MAKE CHARACTER BY CHARACTER SUBSTITUTIONS

SUBST  ZRO; PRINT (SUBSTITUTIONS? ); BRM CRLFYN; BRR SUBST; BRM YESNO
       BRU *+2; BRU SUBST+1; STA SUB; CIO COMFIL; SKE =155B
       BRU *+2; BRU MAKSUB; SKE =152B; BRU SUBST+1
MAKSUB STA MAKTER; PRINT ($MAKE SUBSTITUTIONS:$); LDX =-1; CLA; BRS 12
MAK1   CIO COMFIL; STA SUB1; PRINT ( FOR ); CIO COMFIL; CAX
       LDA SUB1; STA SUBTAB,2; CIO COMFIL; SKE =152B; BRU *+2
       BRU MAK1; SKE =155B; BRU MAKERR; LDX =-1; LDA =2; BRS 12
       PRINT $
       LDA MAKTER; SKE =155B; BRR SUBST; BRR OPTION
MAKERR PRINT ($MAKE SUBSTITUTIONS ON A ONE FOR ONE BASIS.)
       PRINT ($FOLLOW EACH PAIR WITH A LF TO MAKE ANOTHER SUBSTITUTION)
       PRINT ($OR A CR TO CONTINUE WITH THE CONVERSION$)
       BRU MAKSUB+1

*      SUBROUTINES ASSOCIATED WITH ABOVE ROUTINES

*      READ A DECIMAL NUMBER FROM THE COMMAND INPUT FILE

RDNO   ZRO; LDB =10; LDX COMFIL; BRS 38; SKG =0; LDA =-1; BRR RDNO

*      CHECK FOR A RETURN OR LINE FEED AFTER READING A NUMBER (RDNO)

CRLF   ZRO; CBA; SKE =155B; BRU *+2; BRR OPTION; SKE =152B; BRU *+2
       BRR CRLF; MIN CRLF; BRR CRLF

*      CRLFYN CHECKS FOR A RETURN OR LINE FEED AFTER A YES OR NO ANS.

CRLFYN ZRO; CIO COMFIL; SKE =155B; BRU *+2; BRR OPTION; SKE =152B
       MIN CRLFYN; BRR CRLFYN

*      YESNO CHECKS FOR A YES OR NO RESPONSE

YESNO  ZRO; SKE ='   Y'; BRU *+2; BRR YESNO; SKE ='   N'; BRU *+2
       BRR YESNO; MIN YESNO; BRR YESNO

*-----------------------------------------------------------------------

*      INPUT THE INPUT FILE NAME AND FILE TYPE AND OPEN THE OUTPUT FILE

OUTPUT ZRO
       BRM NAME        INPUT THE FILE NAME
       BRM OPEN        OPEN THE FILE FOR OUTPUT WITH STRING POINTERS
       BRR OUTPUT

NAME   ZRO; PRINT ($OUTPUT TO: ); LDA NAMEPT; STA NAMEPT+1 (INIT PTRS.)
C1     CIO COMFIL; SKE =141B; BRU *+2; BRU GCD (CAN USE CONTROL A'S)
       SKE =155B; BRU *+2; BRU C2; SKE =152B; BRU *+2; BRU C3
       WCI NAMEPT; BRU C1
C2     LDA =3; STA TYPE; LDA =-1; STA BINSW; BRR NAME 
C3     LDA =2; STA TYPE; CLA; STA BINSW; BRR NAME

*      CAN USE CONTROL A TO DELETE THE LAST CHARACTER TYPED

GCD    GCD NAMEPT; CLA; LDA =77B; CIO =1; BRU C1

*      SET UP BRS 65 TO PRINT OLD OR NEW FILE MESSAGE

OPEN   ZRO; LDA TYPE; CLB; LSH 12; MRG COMFIL; CAX; LDP NAMEPT
       SKSE ENDFPT; BRU *+2; BRR READ; BRS 65; BRU OPNERR; STA OUT
       LDA =-1; STA TRAPSW; LDA =OPEN1; STA PANIC
       LDA =PANIC+514B5; BRS 9
       LDA PANIC+6; SKG =0; BRU CD2E; BRM COF; BRS 147
       PRINT ($INSTRUCTION TRAP AT: )
       LDA PANIC; ETR =37777B; LDX =1; LDB =8; BRS 36
       BRS 101; BRS 10
OPEN1  LDA =OSA; STA OWP; LDA =NOW-1; STA OWC; LDA =2; STA OCC; BRR OPEN

*      ERROR ON OPENING OUTPUT FILE

OPNERR PRINT (CANNOT OPEN FILE ); LDP NAMEPT; LDX =1; BRS 35
       PRINT ($); BRU NAME+1

*-----------------------------------------------------------------------

*      ROUTINE TO READ A FILE FROM MAG TAPE TO THE DISC

CONVRT ZRO
       BRM PRELIM      SET UP THE NUMBER OF CHARACTERS PER WORD
*                      ALSO TAKE CARE OF THE TAPE LABEL
*                      AND THE MAG TAPE PARITY
       BRM READ        THE ACTUAL CONVERSION IS DONE HERE
       BRR CONVRT

*      PRELIMINARY ROUTINE

PRELIM ZRO
       BRM CHRWRD      SET UP THE NUMBER OF CHARACTERS PER WORD
       BRM SKPLAB      SKIP THE TAPE HEADER
       BRM STPAR       SET THE MAG TAPE PARITY
       BRR PRELIM

CHRWRD ZRO; LDX CNVCDE; LDA CHCTAB,2; STA CHRCNT; BRR CHRWRD

SKPLAB ZRO; SKN LAB; BRU *+2; BRR SKPLAB; SKN LABTYP
       BRU LAB1; LDA LAB; BRR SKPLAB
LAB1   LDA LABTYP; SKE ='   F'
       BRU REC; LDA =3; SKR LAB; BRU *+2; BRR SKPLAB; BRS 105; BRU *-4
REC    LDA =BUFF; LDB =3777B; LDX =1; SKR LAB; BRU *+2; BRR SKPLAB
       BRS 102; BRU *-4

STPAR  ZRO; SKN PAR; BRU MAN; LDX CNVCDE (PROGRAM SELECTS PARITY)
       LDA PARTAB,2; BRS 107; BRR STPAR

*      MANUAL SELECTION BY USER

MAN    LDA PAR; SKE ='   Y'; BRU *+4; LDA =1; BRS 107; BRR STPAR
       LDA =-1; BRU *-3

*      SUBROUTINE TO CONVERT TAPE DATA TO 940

READ   ZRO; CLA; STA RECNT;*FIX BUG 10/20/76 CLC
        LDA =BUFF; LDB =3777B; LDX =1; BRS 102; BRM CODES (ERR CK)
       LDA BUFF; ETR =37777B; STA WORDS (NUMBER WORDS THIS RECORD)
       LDA =BUFF; STA ADR (ADDRESS OF THE CURRENT WORD BEING CONVERTED)
NXTWRD SKR WORDS; BRU *+2; BRU ENDREC (END OF THIS RECORD)
       MIN ADR; LDB* ADR; LDA CHRCNT; STA CURCNT (WHEN NEG DO NEXT WRD)
NXTCHR SKR CURCNT; BRU *+2; BRU NXTWRD; SKN BINSW; BRU BIN; BRM SHIFT
       BRM LOWCK; BRM SUBSTI; BRM RECK; BRM MB; BRM COUT
MBR    BRM TEST; BRM TESTBK; BRU NXTCHR

*      SET UP PROPER SHIFT AND INDEX THE RIGHT TABLE

SHIFT  ZRO; CLA; LDX CNVCDE; LDX SHIFTC,2; LSH 0,2; CAX
       LDA* TABPTR; BRR SHIFT

*      WIO IF A BINARY DUMP, CHAR OPTION SKIPS THE FIRST X WORDS

BIN    CBA; WIO OUT; MIN LINES; LDA LINES; SKG =HMAX/3; BRU NXTWRD
       BRM MAXFIL; CLA; STA LINES; BRU NXTWRD

*      TEST FOR BLOCKING FACTOR AND IF RECORD IS TO BE TRUNCATED

TESTBK ZRO; SKN BLK; BRU *+2; BRR TESTBK
       MIN COUNT (CHARACTERS OUTPUT SO FAR FOR THIS BLOCK)
       LDA COUNT; SKE BLK; BRR TESTBK (NOT YET); BRM BLCK
       CLA; STA COUNT; LDA =155B; BRM COUT
       BRR TESTBK

*      TEST IF THIS IS A RETURN WITH CODE 1 IN EFFECT
*      IF YES THEN IT IS THE LAST CHARACTER IN THIS RECORD

TEST   ZRO; SKE =155B; BRR TEST; LDA CNVCDE; SKE =1; BRR TEST; BRU NXTWRD

*      CONVERT ONLY THE FIRST X CHARACTERS OF EACH RECORD

RECK   ZRO; SKN RECTOT; BRU *+2; BRR RECK; MIN RECNT; BRM SAVE
       LDA RECNT; SKE RECTOT; BRU *+4; CLA; STA RECNT; BRU READ+1
       BRM RESREG; BRR RECK

*      USER MAY WANT TO OUTPUT A RETURN AT THE END OF EACH RECORD

ENDREC SKN RET; BRU *+2; BRU READ+1; LDA RET; SKE ='   Y'
       BRU READ+1; BRM BLCK; LDA =155B (PUT OUT A RETURN AFTER RECORD)
       BRM COUT; LDA CNVCDE; SKE =0; BRU READ+1; LDA =155B; BRM COUT
       BRU READ+1

*      ROUTINE TO HANDLE THE COMPRESSION OF MULTIPLE BLANKS
*      APPLYS TO SYMBOLIC FILES ONLY

MB     ZRO; SKE =0; BRU *+3; MIN MBC; BRU MBR+1; STA CSAVE
       BRM MB1; LDA CSAVE; BRR MB

MB1    ZRO; LDA MBC; SKG =0; BRR MB1; LDA TYPE; SKE =3; BRU SBLOOP
       SKN CMP; BRU *+2; BRU SBLOOP; LDA CMP; SKE ='   Y'; BRU SBLOOP
MBRT   LDA MBC; SKG =1; BRU *+5; LDA =135B; BRM COUT; LDA MBC; BRU *+2
       CLA; BRM COUT; BRM CLMBSW; BRU MBRET

SBLOOP CLA; SKR MBC; BRU *+2; BRU MBRET; BRM COUT; BRU *-5

MBRET  BRM CLMBSW; LDA CSAVE; BRR MB1

CLMBSW ZRO; CLA; STA MBC; BRR CLMBSW

BLCK   ZRO; SKN TRA; BRU *+3; BRM MB1; BRR BLCK; LDA TRA; SKE ='   Y'
       BRU *-4; BRM CLMBSW; BRR BLCK

*      MAKE SUBSTITUTIONS

SUBSTI ZRO; SKN SUB; BRU *+2; BRR SUBSTI; BRM SAVE; LDA SUB
       SKE ='   Y'; BRU SRET; LDX SA; LDA SUBTAB,2; SKE =-1
       BRU *+2; BRU SRET; SKE =172B; BRU *+2; BRU NXTCHR
       LDB SB; LDX SX; BRR SUBSTI
SRET   BRM RESREG; BRR SUBSTI

*      CONVERT LOWER TO UPPER CASE UNLESS LOWER ANSWERED NO

LOWCK  ZRO; SKN LOW; BRU *+2; BRU *+8; STA SA; LDA LOW; SKE ='   N'
       BRU *+3; LDA SA; BRR LOWCK; LDA SA; SKG =132B; SKG =100B
       BRR LOWCK; SUB =40B; BRR LOWCK

*      ERROR CODE ROUTINE

CODES  ZRO; LDA BUFF; LRSH 15
       CAX; BRU *,2; BRU CD1; BRU CD2; BRU CD3; BRU CD4; BRR CODES
       BRU CD6; BRU CD7; BRU CD10; BRU CD11; BRU CD12; BRU CD13

CD1    SKN IGNORE; BRU *+2; BRU CD1A; LDA IGN; SKE ='   Y'; BRU CD1A; BRR CODES
CD1A   PRINT ($POSSIBLE TAPE ERROR AT LINE ); BRU LINNO (LINE NUMBER)
CD2    CLA; STA TRAPSW; PRINT ($END FILE$); BRS 10
CD2E   BRM COF; BRS 147; CLA; STA LINES; STA MBC
       STA COUNT; STA TOTAL; BRU OUTPUT+1
CD3    SKN MSGSW; BRU *+2; BRR CODES
       PRINT ($RECORD SIZE EXCEEDS 2047 WORDS)
       PRINT ($RECORDS ARE THEREFORE TRUNCATED$$); LDA =-1; STA MSGSW
       BRR CODES
CD4    PRINT ($OVERRUN OF 3 SECOND TIME LIMIT$$ABORT$); BRU EXIT
CD6    CLA; STA TRAPSW; PRINT ($END OF TAPE$); BRS 10 (SEE CD2)
CD7    PRINT ($TAPE DRIVE NOT READY$$ABORT$); BRU EXIT
CD10   PRINT ($PAGE BOUNDARY ERROR$$ABORT$); BRU EXIT
CD11   PRINT ($BRS 102 FAILURE$$ABORT$); BRU EXIT
CD12   PRINT ($BRS 102 FAILURE$$ABORT$); BRU EXIT
CD13   PRINT ($DEVICE NO LONGER ASSIGNED$); BRU EXIT

*      PRINT OUT THE LINE NUMBER OF THE ERROR AND PROCEED

LINNO  LDA LINES; LDB =10; LDX =1; BRS 36; PRINT ($); BRR CODES

*-----------------------------------------------------------------------

*      SAVE AND RESTORE THE REGISTERS

SAVE   ZRO; STA SA; STB SB; STX SX; BRR SAVE
RESREG ZRO; LDA SA; LDB SB; LDX SX; BRR RESREG

*      EXIT FROM PROGRAM UNDER NORMAL CONDITIONS

QUIT   ZRO; BRS 147; LDA =6; BRS 105; BRS 101
       LDA =1364000B; BRS 92; BRR QUIT

*      EXIT FROM PROGRAM UNDER UNUSUAL CONDITIONS (NO TAPE REWIND)

EXIT   BRS 147; BRS 101; LDA =1364000B; BRS 92; BRS 10

COUT   ZRO; BRM; LDA OUT; SKE =1; BRU *+4; LDA SA; CIO OUT; BRR COUT
       LDA SA; BRM OCR; MIN TOTAL; SKE =155B; BRU COUT1; MIN LINES
       LDA TOTAL; SKG =LMAX; BRU *+2; BRU COUT3
COUT1  LDA TOTAL; SKG =HMAX; BRU COUT2
COUT3  CLA; STA TOTAL; BRM COF; BRM MAXFIL
COUT2  BRM RESREG; BRR COUT

*      OPEN ANOTHER FILE IF PRESENT ONE TOO BIG

MAXFIL ZRO; PRINT ($MAX FILE SIZE REACHED - OPEN ANOTHER FILE$)
       LDA OUT; BRS 20; BRM OUTPUT; BRR MAXFIL

*      TEMPORARY STORAGE

TEMP   ZRO       0        TEMP STORAGE
TEMPB  ZRO       0        MORE OF THE SAME
TABPTR ZRO       0        POINTER TO THE TRANSLATION TABLE
BINSW  ZRO       0        -1 IF SYMBOLIC FILE, 0 IF BINARY
TRAPSW ZRO       0       -1 WHILE FILE OPEN, USED FOR TRAP LOGIC
PANIC  BSS 10       0       PANIC TABLE FOR BRS 9 IN "OPEN" ROUTINE
SIRETN ZRO       0       RETURN CELL FOR SOFTWARE INTERRUPT RETURN
SA     ZRO       0       SAVE A REGISTER
SB     ZRO       0       SAVE B REGISTER
SX     ZRO       0       SAVE X REGISTER
DEV    ZRO       0       THE DEVICE ASSIGNED IF THE W CHANNEL IS BUSY
CHAN   ZRO       0       THE CHANNEL WHICH HAS A DEVICE ASSIGNED
COUNT  ZRO       0       CHARACTERS OUTPUT SO FAR FOR THE CURRENT BLOCK
ADR    ZRO       0       ADDRESS OF THE CURRENT WORD BEING CONVERTED
CNVCDE ZRO       0       THE CONVERSION CODE PICKED BY THE USER
LAB    ZRO       0       THE NUMBER OF RECORDS OR FILES IN THE TAPE LABEL OR HEADER
OUT    ZRO       0       THE OUTPUT FILE NUMBER
COMFIL ZRO       0       THE COMMAND INPUT FILE
LABTYP ZRO       0       THE TAPE HEADER MAY BE EITHER RECORDS OF FILES
CURCNT ZRO       0       THE NUMBER OF CHARACTERS IN A WORD YET TO BE CONVERTED
LINES  ZRO       0       THE NUMBER OF LINES OUTPUT TO THE OUTPUT FILE
PAR    ZRO       0       EVEN OR ODD PARITY
BLK    ZRO       0       THE NUMBER OF CHARACTERS PER BLOCK
TYPE   ZRO       0       OUTPUT FILE TYPE
CHRCNT ZRO       0       THE NUMBER OF CHARACTERS PER WORD 
RET    ZRO       0       SWITCH FOR A RETURN AFTER EACH RECORD
WORDS  ZRO       0       NUMBER OF WORDS IN THIS RECORD
CMP    ZRO       0       SWITCH FOR THE COMPRESSION OF MULTIPLE BLANKS
MBC    ZRO       0       NUMBER OF MULTIPLE BLANKS
TRA    ZRO       0       SWITCH FOR DELETING TRALING BLANKS
RECTOT ZRO       0       NUMBER OF CHARACTERS PER RECORD TO BE CONVERTED
RECNT  ZRO       0       NUMBER OF CHARACTERS OUTPUT SO FAR THIS RECORD
SUB    ZRO       0       SWITCH FOR SUBSTITUTIONS
MAKTER ZRO       0       TERMINATING CHARACTER IN SUBSTITUTION ROUTINE
SUB1   ZRO       0       HOLD CELL FOR SUBSTITUTION ROUTINE
MSGSW  ZRO       0       ONLY WANT TO TYPE THIS MESSAGE ONCE
CSAVE  ZRO       0       SAVE THE CURRENT CHARACTER IN THE MULTIPLE BLANK ROUTINE
LOW    ZRO       0       LOWER TO UPER CASE CONVERSION SWITCH
IGN    ZRO       0       IGNORE TAPE READ ERROR OPTION
*BAK    ZRO     0               NUMBER OF FILES TO BACKSPACE
*ADV    ZRO     0               NUMBER OF FILES TO ADVANCE

*      OTHER STORAGE

SUBTAB RPT 377B TABLE FOR SUBSTITUTIONS
       DATA -1
       ENDR
TOTAL  ZRO       0       OUTPUT FILE SIZE - SEE LMAX AND HMAX
DTY    DATA 200,556,800 TAPE DENSITY
FILE   BSS 24 FILE NAME UP TO 72 CHARACTERS INCLUDING COMMENT
NAMEPT DATA 3*FILE-1 STRING POINTERS FOR THE OUTPUT FILE NAME
       DATA 3*FILE-1
ENDF   ASC 'E'
ENDFPT DATA 3*ENDF-1,3*ENDF ALLOW TERMINATION WITH AN E AT OUTPUT TO:


*      CHARACTERS PER WORD

CHCTAB DATA    8
       DATA    3               EBCDIC
       DATA    4               BCD
       DATA    4               1108
       DATA    4               HONEYWELL
       DATA    4               GENERAL ELECTRIC
       DATA    4               ET FORMAT 370
       DATA    0               SPECIAL MODE
       DATA    4               E FORMAT 370

*      PARITY  1=ODD, -1=EVEN

PARTAB DATA    1
       DATA    1               EBCDIC
       DATA    -1              BCD
       DATA    -1              1108
       DATA    1               HONEYWELL
       DATA    0               GENERAL ELECTRIC
       DATA    -1              ET FORMAT 370
       DATA    0               SPECIAL MODE
       DATA    -1              E FORMAT 370

*      SHIFT COUNT TABLE
SHIFTC DATA    3
       DATA    8               EBCDIC
       DATA    6               BCD
       DATA    6               1108
       DATA    6               HONEYWELL
       DATA    6               GENERAL ELECTRIC
       DATA    6               ET FORMAT 370
       DATA    0               SPECIAL MODE
       DATA    6               E FORMAT 370

*      TABLE FOR POINTER TO TRANSLATION TABLES

TRATAB ZRO     BCDTAB,2
       ZRO     EBCDIC,2        EBCDIC
       ZRO     BCDTAB,2        BCD
       ZRO     1108TA,2        1108
       ZRO     HONEY,2         HONEYWELL
       ZRO     GETAB,2         GENERAL ELECTRIC
       ZRO     ET370,2         ET FORMAT 370
       ZRO     SPTAB,2         SPECIAL MODE
       ZRO     370E,2          E FORMAT 370


*      CONVERSION TABLES

*      EBCDIC TO 940 INTERNAL CODE

EBCDIC DATA 000B,141B,142B,143B,144B,151B,146B,147B
       DATA 000B,145B,165B,153B,154B,152B,156B,157B
       DATA 160B,161B,162B,163B,164B,155B,166B,167B
       DATA 170B,171B,172B,000B,000B,000B,000B,000B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,074B,016B,034B,010B,013B,073B
       DATA 006B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,001B,004B,012B,011B,033B,075B
       DATA 015B,017B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,076B,014B,005B,077B,036B,037B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,032B,003B,040B,007B,035B,002B
       DATA 000B,101B,102B,103B,104B,105B,106B,107B
       DATA 110B,111B,000B,000B,000B,000B,000B,000B
       DATA 000B,112B,113B,114B,115B,116B,117B,120B
       DATA 121B,122B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,123B,124B,125B,126B,127B,130B
       DATA 131B,132B,000B,000B,000B,000B,000B,000B
       DATA 000B,074B,073B,075B,073B,075B,000B,000B
       DATA 000B,000B,000B,000B,000B,000B,000B,000B
       DATA 000B,041B,042B,043B,044B,045B,046B,047B
       DATA 050B,051B,000B,000B,000B,000B,000B,000B
       DATA 000B,052B,053B,054B,055B,056B,057B,060B
       DATA 061B,062B,000B,000B,000B,000B,000B,000B
       DATA 000B,000B,063B,064B,065B,066B,067B,070B
       DATA 071B,072B,000B,000B,000B,000B,000B,000B
       DATA 020B,021B,022B,023B,024B,025B,026B,027B
       DATA 030B,031B,000B,000B,000B,000B,000B,000B
   
*      BCD CODE TO 940 INTERNAL CODE

BCDTAB DATA 20B,21B,22B,23B,24B,25B,26B,27B
       DATA 30B,31B,40B,35B,07B,32B,36B,76B
       DATA 13B,41B,42B,43B,44B,45B,46B,47B
       DATA 50B,51B,37B,16B,11B,73B,34B,03B
       DATA 15B,52B,53B,54B,55B,56B,57B,60B
       DATA 61B,62B,01B,04B,12B,75B,33B,02B
       DATA 00B,17B,63B,64B,65B,66B,67B,70B
       DATA 71B,72B,06B,14B,10B,77B,74B,05B

*      UNIVAC 1108 CODE TO 940 INTERNAL CODE

1108TA DATA 41B,73B,75B,03B,06B,00B,41B,42B
       DATA 43B,44B,45B,46B,47B,50B,51B,52B
       DATA 53B,54B,55B,56B,57B,60B,61B,62B
       DATA 63B,64B,65B,66B,67B,70B,71B,72B
       DATA 11B,15B,13B,34B,35B,36B,06B,04B
       DATA 12B,10B,05B,32B,37B,01B,14B,00B
       DATA 20B,21B,22B,23B,24B,25B,26B,27B
       DATA 30B,31B,07B,33B,17B,16B,00B,76B

*      HONEYWELL 200 SERIES

HONEY  DATA 20B,21B,22B,23B,24B,25B,26B,27B
       DATA 30B,31B,07B,35B,32B,00B,36B,06B
       DATA 13B,41B,42B,43B,44B,45B,46B,47B
       DATA 50B,51B,33B,16B,11B,05B,00B,37B
       DATA 15B,52B,53B,54B,55B,56B,57B,60B
       DATA 61B,62B,03B,04B,12B,02B,03B,01B
       DATA 34B,17B,63B,64B,65B,66B,67B,70B
       DATA 71B,72B,40B,14B,10B,155B,0B,43B

*      GENERAL ELECTRIC MARK II TYPE

GETAB  DATA 20B,21B,22B,23B,24B,25B,26B,27B
       DATA 30B,31B,40B,03B,07B,32B,36B,37B
       DATA 06B,41B,42B,43B,44B,45B,46B,47B
       DATA 50B,51B,37B,16B,11B,10B,34B,03B
       DATA 15B,52B,53B,54B,55B,56B,57B,60B
       DATA 61B,62B,01B,04B,12B,11B,33B,07B
       DATA 00B,17B,63B,64B,65B,66B,67B,70B
       DATA 71B,72B,06B,14B,05B,35B,02B,05B

ET370  DATA 20B,21B,22B,23B,24B,25B,26B,27B
       DATA 30B,31B,40B,35B,07B,07B,35B,02B
       DATA 06B,41B,42B,43B,44B,45B,46B,47B
       DATA 50B,51B,37B,16B,34B,10B,13B,76B
       DATA 15B,52B,53B,54B,55B,56B,57B,60B
       DATA 61B,62B,01B,04B,12B,11B,33B,74B
       DATA 00B,17B,63B,64B,65B,66B,67B,70B
       DATA 71B,72B,06B,14B,05B,77B,36B,37B

*      TRRANSLATION TABLE FROM IBM 370 E FORMAT

370E   DATA    00B,41B,42B,43B,44B,45B,46B,47B
       DATA    50B,51B,00B,16B,34B,10B,13B,76B
       DATA    20B,21B,22B,23B,24B,25B,26B,27B
       DATA    30B,31B,32B,03B,40B,07B,35B,02B
       DATA    15B,17B,63B,64B,65B,66B,67B,70B
       DATA    71B,72B,00B,14B,05B,77B,36B,37B
       DATA    00B,52B,53B,54B,55B,56B,57B,60B
       DATA    61B,62B,01B,04B,12B,11B,33B,74B

SPTAB  BSS 256

NUMBER EQU     PARTAB-CHCTAB-1
       END
MAGCON ** FICHE/FRAME BREAK *****
SMAGCON23 SYMSORT

SYM.  PG.LN. IDENT.

1108TA 18 16 MAGCON  370E   19 19 MAGCON  ADR    14 31 MAGCON  
ARMINT  4 21 MAGCON  ASSIGN  3 34 MAGCON  ASSN    3 35 MAGCON  ASSN1   3 38 MAGCON  
BCDTAB 18  5 MAGCON  BEGIN   3 15 MAGCON  BIN    11 15 MAGCON  BINSW  14 21 MAGCON  
BLCK   12 22 MAGCON  BLK    15  1 MAGCON  BLOCK   7  4 MAGCON  BRN     7 15 MAGCON  
BUFF    3  2 MAGCON  C1      9 11 MAGCON  C2      9 14 MAGCON  C3      9 15 MAGCON  
CD1    13  6 MAGCON  CD10   13 18 MAGCON  CD11   13 19 MAGCON  CD12   13 20 MAGCON  
CD13   13 21 MAGCON  CD1A   13  7 MAGCON  CD2    13  8 MAGCON  CD2E   13  9 MAGCON  
CD3    13 11 MAGCON  CD4    13 15 MAGCON  CD6    13 16 MAGCON  CD7    13 17 MAGCON  
CDSPE   6 12 MAGCON  CHAN   14 29 MAGCON  CHCTAB 15 37 MAGCON  CHRCNT 15  3 MAGCON  
CHRWRD 10 18 MAGCON  CLEAR   5 11 MAGCON  CLMBSW 12 20 MAGCON  CLSUB   5 14 MAGCON  
CMP    15  6 MAGCON  CMPMB   7 28 MAGCON  CNVCDE 14 32 MAGCON  CODE    5 19 MAGCON  
CODEH   5 26 MAGCON  CODEHM  5 27 MAGCON  CODES  13  2 MAGCON  CODESP  5 39 MAGCON  
COF     2 34 MAGCON  COF1    2 35 MAGCON  COMFIL 14 35 MAGCON  CONVRT 10  3 MAGCON  
COUNT  14 30 MAGCON  COUT   14  4 MAGCON  COUT1  14  7 MAGCON  COUT2  14  9 MAGCON  
COUT3  14  8 MAGCON  CRLF    8 27 MAGCON  CRLFYN  8 32 MAGCON  CSAVE  15 15 MAGCON  
CURCNT 14 37 MAGCON  CWD     2 14 MAGCON  DEV    14 28 MAGCON  DEVASS  4  8 MAGCON  
DTY    15 27 MAGCON  EBCDIC 17  9 MAGCON  ENDF   15 31 MAGCON  ENDFPT 15 32 MAGCON  
ENDREC 11 39 MAGCON  ERRASS  4  4 MAGCON  ESC     4 25 MAGCON  ET370  19  8 MAGCON  
EXIT   14  2 MAGCON  FILE   15 28 MAGCON  G(1)    3 11 MAGCON  G(2)    3 10 MAGCON  
GCD     9 19 MAGCON  GETAB  18 38 MAGCON  HMAX    3  4 MAGCON  HONEY  18 27 MAGCON  
IGN    15 17 MAGCON  IGNORE  8  1 MAGCON  LAB    14 33 MAGCON  LAB1   10 22 MAGCON  
LABEL   6 37 MAGCON  LABTYP 14 36 MAGCON  LINES  14 38 MAGCON  LINNO  13 25 MAGCON  
LMAX    3  3 MAGCON  LOW    15 16 MAGCON  LOWCK  12 35 MAGCON  LOWER   7 36 MAGCON  
M0      4 10 MAGCON  M1      4 11 MAGCON  M2      4 12 MAGCON  MAK1    8  9 MAGCON  
MAKERR  8 14 MAGCON  MAKSUB  8  8 MAGCON  MAKTER 15 12 MAGCON  MAN    10 32 MAGCON  
MAXFIL 14 13 MAGCON  MB     12  8 MAGCON  MB1    12 11 MAGCON  MBC    15  7 MAGCON  
MBR    11  6 MAGCON  MBRET  12 18 MAGCON  MBRT   12 13 MAGCON  MSG     3 18 MAGCON  
MSGSW  15 14 MAGCON  NAME    9 10 MAGCON  NAMEPT 15 29 MAGCON  NBE     2 38 MAGCON  
NOW     1 38 MAGCON  NUMBER 19 30 MAGCON  NXTCHR 11  4 MAGCON  NXTWRD 11  2 MAGCON  
OCC     2 15 MAGCON  OCR     2  5 MAGCON  OCW     2  6 MAGCON  OCW1    2  9 MAGCON  
OCWA    2 11 MAGCON  OCWB    2 12 MAGCON  OCWR    2  8 MAGCON  OCWX    2 13 MAGCON  
OPEN    9 23 MAGCON  OPEN1   9 31 MAGCON  OPNERR  9 35 MAGCON  OPR     2 28 MAGCON  
OPTION  4 32 MAGCON  OSA     1 37 MAGCON  OUT    14 34 MAGCON  OUTPUT  9  5 MAGCON  
OWC     2 24 MAGCON  OWM     2 19 MAGCON  OWP     2 23 MAGCON  PANIC  14 23 MAGCON  
PAR    14 39 MAGCON  PARITY  7 24 MAGCON  PARTAB 16 10 MAGCON  PRELIM 10 12 MAGCON  
PRINT   3  8 MAGCON  QUIT   13 36 MAGCON  R       4 13 MAGCON  RDNO    8 23 MAGCON  
READ   10 37 MAGCON  REC    10 24 MAGCON  RECK   11 33 MAGCON  RECNT  15 10 MAGCON  
RECORD  7  9 MAGCON  RECTOT 15  9 MAGCON  RESREG 13 32 MAGCON  RET    15  4 MAGCON  
RETURN  7 20 MAGCON  SA     14 25 MAGCON  SAVE   13 31 MAGCON  SB     14 26 MAGCON  
SBLOOP 12 16 MAGCON  SHIFT  11 10 MAGCON  SHIFTC 16 21 MAGCON  SIRETN 14 24 MAGCON  
SKPLAB 10 20 MAGCON  SPTAB  19 28 MAGCON  SRET   12 31 MAGCON  START   3 22 MAGCON  
STPAR  10 27 MAGCON  SUB    15 11 MAGCON  SUB1   15 13 MAGCON  SUBST   8  5 MAGCON  
SUBSTI 12 27 MAGCON  SUBTAB 15 23 MAGCON  SX     14 27 MAGCON  TABPTR 14 20 MAGCON  
TEMP   14 18 MAGCON  TEMPB  14 19 MAGCON  TEST   11 29 MAGCON  TESTBK 11 20 MAGCON  
TOTAL  15 26 MAGCON  TRA    15  8 MAGCON  TRAIL   7 32 MAGCON  TRAPSW 14 22 MAGCON  
TRATAB 16 33 MAGCON  TYPE   15  2 MAGCON  VERS    4 15 MAGCON  WORDS  15  5 MAGCON  
YESNO   8 37 MAGCON  
MAGCON ** FICHE/FRAME BREAK *****
BUILDMAGCON
NARP
SMAGCON23
B


RES
XDD
;TB
;U%FSAV 3,7777 O MAGCON

240
"


"
CHECKSUM
.MAGCON
SMAGCON23
BUILDMAGCON
.
COM T
 A@xé