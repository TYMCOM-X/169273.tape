SUPCHECK ** FICHE/FRAME BREAK *****
/CUPSUP/
SUPCK  IDENT   VERSION 14  LAST UPDATE   5-19-76
*MUST BE SAVED FROM 3 TO SEMIF


SNE    OPD 536B5
SKL    OPD 545B5
SIC    OPD 541B5

ASCP   MACRO   D
X      NCHR    D(1)
       DATA    [*+2]*3-1,**3+X
       ASC     D(1)
       ENDM
       BRU START

VERMSG ASC '$SUPCHECK - VERSION 15$/'


MSG    POPD 101B5,2
       STP MSGA; STX MSGX
       EAX* 0; CXA; ETR =37777B; LDB =-1; LDX TOUT; BRS 34
       CLA; STA CTT
       LDP MSGA; LDX MSGX; BRR 0
MSGA   BSS 2
MSGX   BSS 1


COM    ZRO
TOUT   ZRO

* STORE COM AND TOUT FILE NUMBERS AND PRINT VERSION NUMBER

START  STP COM
       MSG VERMSG

* CHECKS TO MAKE SURE YOU ARE LOGGED INTO GAN 1 AND GET HOME SYSTEM NUMBER
*AND SYSTEM LETTER

GETSYS LDA =HOMSYS; LDX =11; BRS 7; LDB HOMSYS
GETSY1 CLA; LSH 8; SKG =0; BRU GETSY1 (GET RID OF LEADING SPACE IF ANY)
       SKG =31B; BRU GETSY3 (NO LETTER?); STA SYSLTR
GETSY2 CLA; LSH 8
GETSY3 SUB =20B; STA HOMSYS; CLA; LSH 8; SKG =0; BRU CKGAN (1DIGIT SYS NO)
       SUB =20B; XMA HOMSYS; MUL =10; LSH 23; ADM HOMSYS

CKGAN  LDA =31; LDB =GAN; LDX =1; BRS 67; LDA GAN; SNE =1; BRU OPNPAS
       MSG M22 (MUST BE RUN FROM GAN 1)
       MSG M20 (ABORT); BRS 10

SYSFIX ZRO; LDA =3*HOMSYS+1; MIN SYSFIX; BRR SYSFIX

GAN    ZRO
HOMSYS ZRO
SYSLTR ZRO

* OPEN PASSWORD FILE

OPNPAS LDP PASPTR; BRS 64; BRU OPNERR; STA PASFN; CLA; SCP PASFN
       BRU SCPERR; BRU DSNAME

PASPTR DATA 3*PASSTR-1,3*PASSTR+5
PASSTR ASC '()PSWD'
PASFN  ZRO

OPNERR MSG M23 (PSWD BUSY)
       MSG M1 (EXIT); BRS 10

SCPERR BRS 17; MSG M24 (UNABLE SET PSWD READ MODE)
       MSG M20 (ABORT); BRS 10

* INPUT DS NAME

DSNAME MSG M2 (NAME)
       LDA =3*NAMSTR-1; STA COMPTR; STA COMPTR+1
       BRM TTYIN; STA TYMFLG; LDA COMPTR+1; STA NAMPTR+1; SUB COMPTR
       STA NAMLEN; LDA COMPTR; STA NAMPTR; BRU CKNAME

COMPTR DATA 0,0
NAMSTR BSS 10
NAMPTR BSS 2
NAMLEN ZRO
TEMP   ZRO

* TTY INPUT ROUTINE - CHECKS FOR CONTROL A

TTYIN  ZRO; CIO COM; SNE =141B; BRU CNTRLA; SNE =155B; BRU TTYIN1; SNE =152B
       BRU TTYIN1; WCI COMPTR; BRU TTYIN+1
TTYIN1 STA TERCH; BRR TTYIN

* CONTROL A EDITING ROUTINE

CNTRLA GCD COMPTR; BRU BELL; STA TEMP; TCO ='  <'; TCO TEMP
       LDA =3; LDX =-1; BRS 12

INPUT  CIO COM; SKE =141B; BRU *+5; GCD COMPTR; BRU BELL+2; BRM CTOUT
       BRU INPUT; STA TEMP; TCO ='  >'; TCO TEMP; CLA; LDX =-1
       BRS 12; LDA TEMP; SNE =155B; BRU TTYIN1; SNE =152B
       BRU TTYIN1; WCI COMPTR; BRU TTYIN+1

BELL   TCO =147B; BRU TTYIN+1; TCO =147B; BRU INPUT

* CHECK NAME ENTERED WITH NAMES IN PASSWORD FILE

CKNAME CIO PASFN; SNE =137B; BRU ERRNAM; SKE =155B; BRU CKNAME; CIT PASFN
       BRU *+2; BRU ERRNAM; LDA OPNPTR; STA OPNPTR+1; LDA =OPNPTR; LDB =155B
       LDX PASFN; BRS 33; LDA OPNPTR+1; SUB OPNPTR; SKG =0; BRU ERRNAM
       SKE NAMLEN; BRU CKNAME; LDP OPNPTR; SKSE NAMPTR; BRU CKNAME; GCI COMPTR
       BRU GCIERR; STA TMP8; BRM RDSTAT; SKA =4B4; BRU *+2; BRU NOTVAL
       ETR =777B; STA DIST; BRM RDSTAT; STA TMP1; BRM RDSTAT; STA TMP2
       LDA PASFN; BRS 20; BRU PSWD

TMP1   ZRO
TMP2   ZRO
TMP3   ZRO
TMP4   ZRO
TMP5   ZRO
TMP6   ZRO
TMP7   ZRO
TMP8   ZRO
TMP9   ZRO
TMP10  ZRO
TMP11  ZRO
TMP12  ZRO
TMP13  ZRO

OPNPTR DATA 3*OPNSTR-1,3*OPNSTR-1
OPNSTR BSS 10

DIST   ZRO

ERRNAM BRS 17
       MSG M25 (NAME NOT IN PSWD FILE)
       MSG M20 (ABORT); BRS 10

GCIERR BRS 17
       MSG M26 (UNABLE DO GCI)
       MSG M20 (ABORT); BRS 10

RDSTAT ZRO; LDB =8; LDX PASFN; BRS 38; BRR RDSTAT

NOTVAL BRS 17
       MSG M27 (NAME NOT VALIDATED)
       MSG M20 (ABORT); BRS 10

* INPUT AND VERIFY PASSWORD

PSWD   LDX =-1; LDA =3; BRS 12
       MSG M3 (PASSWORD)

CKPAS  LDP TERMWD; STP TMP3; STP TMP5; STB TMP7; CLA; STA TMP9; LDA TMP8
       ADM TMP5

CKPAS1 LDA =20; STA TMP10

CKPAS2 LDX TMP9; LDA TMP3,2; MUL =6065B; DIV =37777761B; CBA; ADD TMP4,2
       CAB; ETR =3; STA TMP9; LDA TMP3,2; XAB; STP TMP3,2; LDA TMP7; XMA TMP3
       STA TMP7; SKR TMP10; BRU CKPAS2; CIO COM; STA TMP11; LDB TMP12
       STX TMP13; WCI PWPTR; LDA TMP11; LDB TMP12; LDX TMP13; SNE =155B
       BRU *+3; ADM TMP3; BRU CKPAS1; LDX =-1; LDA =2; BRS 12; TCO =155B
       LDA TMP3; ADD TMP4; SKE TMP1; BRU ERRPAS; LDA TMP5
       ADD TMP6; SKE TMP2; BRU ERRPAS; LDX =-13; CLA; STA TMP13+1,2; BRX *-1
       BRU CKDIST

TERMWD DATA 27657537B,55555555B
PWPTR  DATA 3*PWSTR-1,3*PWSTR-1
PWSTR  BSS 10

ERRPAS LDX =-1; LDA =3; BRS 11; BRS 12; LDA PWPTR; STA PWPTR+1
       MSG M28 (ERROR TYPE PSWD); BRU CKPAS

* CHECK FOR DISTRICT ZERO STATUS

CKDIST LDA DIST; SNE =0; BRU OKMSG
       MSG M29 (MUST BE DIST ZERIO)
       MSG M1 (EXIT); BRS 10

OKMSG  MSG M4 (THANK YOU)
       BRM RECORD; LDA =ERRTAB; STA SYSTAB+1

* INPUT BUILDING

GETBLD LDA SYSLTR; SNE ='N'; BRU ECO; SNE ='V'; BRU ECO
       SNE ='H'; BRU SCO
GBLD1  LDA BLDPTR; STA COMPTR; STA COMPTR+1; CLA; STA BLDSTR
       MSG M5 (INPUT BUILDING); BRM TTYIN; STA TERCH
       LDA COMPTR+1; STA BLDPTR+1; LDA BLDSTR; LRSH 16; STA BLDSTR; SNE ='A'
       BRU BLDGA; SNE ='B'
       BRU BLDGB; SNE ='C'; BRU BLDGC; CLA; STA BLDSTR; LDX =-1; BRS 11
       BRU GBLD1

BLDPTR DATA 3*BLDSTR-1,3*BLDSTR-1
BLDSTR BSS 5
TERCH ZRO
PFNO   ZRO
CHGSW  ZRO

* INITIALIZE SYSTEM POINTER FOR BUILDING A

BLDGA  LDA =TABLEA; STA SYSTAB; BRM CKSYS; BRM CKTIME
       BRM AMSG; BRU BLDCIR

* INITIALIZE SYSTEM POINTER FOR BUILDINGS A AND C

BLDGB  LDA =TABLEB; STA SYSTAB; BRM CKSYS; BRM CKTIME
       BRM BMSG; BRU BLDCIR

* INITIALIZE SYSTEM POINTER FOR BUILDING C

BLDGC  LDA =TABLEC; STA SYSTAB; BRM CKSYS; BRM CKTIME
       BRM CMSG; BRU BLDCIR

*INITIALIZE SYSTEM POINTER FOR EASTERN COMPUTER CENTER

ECO    LDA =ETABLE; STA SYSTAB; BRM CKSYS; BRM CKTIME; BRM EMSG; BRU BLDCIR

*INITIALIZE SYSTEM POINTER FOR SOUTHERN COMPUTER CENTER

SCO    LDA =STABLE; STA SYSTAB; BRM CKSYS; BRM CKTIME; BRM SMSG; BRU BLDCIR

*SYSTEM TABLES

*WCO TABLES

TABLEA DATA 1,2,3,4,5,7,9,10,11,13,14,16,18,19,20,999,0,0,0,0,0
       DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
TABLEB DATA 1,2,5,7,9,10,11,13,14,15,16,17,18,19,26,27,31
       DATA 32,33,34,35,36,37,38,999,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
TABLEC DATA 33,34,35,36,37,38,39,999,0,0,0,0,0,0,0,0,0,0,0,0
       DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

*ECO TABLE

ETABLE DATA 3,4,24,25,999,0,0,0,0,0,0,0,0,0,0,0,0,0,0
       DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

*SCO TABLE

STABLE DATA 6,8,12,17,23,31,32,999,0,0,0,0,0,0,0,0
       DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0

ERRTAB DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
       DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
SYSTAB DATA 0,0

* CHECK FOR MODIFICATION OF SYSTEM TABLE

CKSYS  ZRO; BRM LISSYS; MSG M77 (OKAY); BRM YN; BRU *+2; BRU CKCHG
CKSYS4 MSG M6 (REMOVE SYS?); BRM YN; BRU CKSYS1; BRU REMSYS

TTYPTR DATA 3*TTYBUF-1,3*TTYBUF-1
TTYBUF BSS 5

CKSYS1 MSG M7 (ADD SYS?); BRM YN; BRU CKSYS+1; BRU ADDSYS

* REMOVE SYSTEM FROM SYSTEM TABLE

REMSYS LDA SYSPTR; STA COMPTR; STA COMPTR+1; CLA; STA SYSNUM
       MSG M8 (ENTER SYS NO)
       BRM TTYIN; STA TEMP; LDA COMPTR+1; STA SYSPTR+1; LDX =1B7; SIC COMPTR
       STA SYSNUM; LDX SYSTAB; LDA 0,2; SNE SYSNUM; BRU PUTZRO; SNE =999
       BRU ERRSYS; EAX 1,2; BRU *-6

SYSPTR DATA 3*SYSSTR-1,3*SYSSTR-1
SYSSTR BSS 5
SYSNUM ZRO

* ZERO OUT SYSTEM NUMBER

PUTZRO SKR CHGSW; BRU *-1
PZ1    LDA 1,2; STA 0,2; SNE =999; BRU *+3; EAX 1,2; BRU PZ1
       CLA; STA 1,2; LDA TEMP; SNE =152B; BRU REMSYS; BRU CKSYS1

ERRSYS TCO =155B; LDP SYSPTR; LDX TOUT; BRS 35
       MSG M30 (NOT PRESENTLY ASSIGNED)
       BRU CKSYS4

* ADD SYSTEM TO SYSTEM TABLE

ADDSYS LDA SYSPTR; STA COMPTR; STA COMPTR+1; CLA; STA SYSNUM
       MSG M8 (ENTER SYS NO)
       BRM TTYIN; STA TEMP; LDA COMPTR+1; STA SYSPTR+1; LDX =1B7; SIC COMPTR
       STA SYSNUM; BRM VALSYS; LDX SYSTAB; LDA 0,2; SKG SYSNUM; BRU *+10
       CAB; LDA SYSNUM; STA 0,2; CBA; SNE =999; BRU CKLF
       EAX 1,2; LDB 0,2; BRU *-6; EAX 1,2; BRU *-13

CKLF   EAX 1,2; STA 0,2; LDA TEMP; SNE =152B; BRU ADDSYS; BRU CKSYS+1

CKCHG  SKN CHGSW; BRR CKSYS; MSG M78 (ARE CHANGES PERMANENT)
       BRM YN; BRR CKSYS
       BRM MKCHG; BRR CKSYS

*YES NO ROUTINE NO NO SKIP  YES SKIP MUST BE CALLED
*IMMEDIATELY AFTER MESSAGE AS IF NEITHER NO OR YES WILL CAUSE MESSAGE
*TO BE REPEATED
YN     ZRO; BRM CLBUF; CIO COM; CAB
YN1    CIO COM; SNE =155B; BRU YN2; SNE =152B; BRU YN2; BRU YN1
YN2    STA TERCH; CBA; SNE ='N'; BRR YN; SNE ='Y'; BRU YN3
       LDA YN; SUB =2; STA YN; LDX =-1; BRS 11; BRR YN
YN3    MIN YN; BRR YN
CLBUF  ZRO; LDA =155B; CIT COM; BRR CLBUF; BRU *-2
LISSYS ZRO; MSG M84 (CURRENT SYSTEMS ARE)
       LDA =155B; CIO TOUT; LDA SYSTAB; STA COMPTR
LISS1  LDX COMPTR; LDA 0,2; EAX 1,2; STX COMPTR
       SNE =999; BRR LISSYS; SNE =0; BRU LISS1
       LDX TOUT; LDB =10; BRS 36; CLA
       CIO TOUT; BRU LISS1

*CHANGE GO FILE OF SUPCHECK TO REFLECT SYSTEM ADDS AND DELETIONS

MKCHG  ZRO; LDP SUPPTR; MRG =116B5; LDX =1; BRS 65; BRU NOSUP
       STA PFNO; LDA =TABLEA; MUL =3; LSH 23
       MRG =4B7; SCP PFNO; BRU SUPSCP; LDA =200
       LDX =TABLEA; BIO PFNO; BRU SUPBIO; LDA PFNO; BRS 20
       MSG M79 (SUPCHECK UPDATED); BRR MKCHG
NOSUP  MSG M80 (UNABLE OPEN (UTIL)SUPCHECK)
NOSUP1 MSG M81 (CHANGES NOT MADE PERMANENT); BRR MKCHG

SUPSCP MSG M82 (UNABLE SET CURSOR (UTIL)SUPCHECK MAY BE CLOBBERED)
       BRU NOSUP1
SUPBIO MSG M83 (BIL ERROR UPDATING (UTIL)SUPCHECK MAY BE CLOBBERED)
       BRU NOSUP1
SUPPTR ASCP    '(UTIL)SUPCHECK'

* CHECK TO SEE IF SYSTEM ENTERED IS VALID

VALSYS ZRO; SKR CHGSW; BRU *-1
       LDA SYSNUM; SNE =14; BRU INVAL
       SNE =128; BRR VALSYS
       SKL =40; BRU INVAL; BRR VALSYS

AMSG   ZRO; MSG M9 (CLEARING A)
       BRM DATE; LDA =155B; BRM CTOUT; BRM CTOUT; BRR AMSG

BMSG   ZRO; MSG M10 (CLEARING BLDG A AND C)
       BRM DATE; LDA =155B; BRM CTOUT; BRM CTOUT; BRR BMSG

CMSG   ZRO; MSG M11 (CLEARING BLDG C)
       BRM DATE; LDA =155B; BRM CTOUT; BRM CTOUT; BRR CMSG

EMSG   ZRO; MSG M75 (CLEARING EASTERN)
       BRM DATE; LDA =155B; BRM CTOUT; BRM CTOUT; BRR EMSG

SMSG   ZRO; MSG M76 (CLEARING SOUTHERN)
       BRM DATE; LDA =155B; BRM CTOUT; BRM CTOUT; BRR SMSG

* OUTPUT DATE AND TIME TO TTY

DATE   ZRO; LDA DATPTR; STA DATPTR+1; LDP DATPTR; BRS 140; LDX TOUT; BRS 35
       TCO =155B; BRR DATE

DATPTR DATA 3*DATSTR-1,3*DATSTR-1
DATSTR BSS 10

INVAL  TCO =155B; LDP SYSPTR; LDX TOUT; BRS 35
       MSG M31 (NOT VALID SYS); BRU CKSYS+4

* CHECK FOR RESTART PAUSE

CKTIME ZRO; LDA TYMFLG; SKE =152B; BRR CKTIME; LDA TYMPTR; STA COMPTR
       STA COMPTR+1; CLA; STA PAUSE
       MSG M13 (ENTER RESTART INTERVAL)
       BRM TTYIN; LDA COMPTR+1; STA TYMPTR+1; LDX =1B7; SIC COMPTR
       SKG     =4
       BRU     TYMERR
       SKG     =20
       BRU     *+2
       LDA     =20
       STA     PAUSE
       BRR     CKTIME


TYMERR MSG M32 (MUST BE BETWEEN 5 AND 60)
       BRU CKTIME+4

TYMFLG ZRO
TYMPTR DATA 3*PAUSE-1,3*PAUSE-1
PAUSE  DATA 15
       BSS 4

* BUILD AUXILIARY CIRCUIT

BLDCIR LDA SYSTAB; CAX; EAX 1,2; STX SYSTAB; CAX; LDA 0,2; SNE =0; BRU BLDCIR
       SNE =999; BRU DONE; STA SYSTEM; BRM CKHOME

BUILD  BRM ARM8; BRM ARM10; BRM ARM5; LDA SYSTEM
       BRS 173; BRU CIRERR; SKE =0; BRU CIRERR; STX IPORT; EAX 1,2; STX OPORT
       LDA =PCODE; LDB =8; BRS 34; LDA =105B; CIO OPORT
       CLA; STA CIRLST; STA CIRLST+1; STA CIRLST+2; LDX =CIRLST
       LDA =3; BRS 137; LDA CIRLST+1; SKE IPORT; BRU AUXCHN
       CIO IPORT; SNE =73B; BRU *+3; SKE =273B
       BRM ERRLOG; BRM DISAR5; CLA; STA COUNT; BRU CKMAIL

SYSTEM ZRO
CIRLST BSS 5

AUXCHN CLA; BRS 78; MIN COUNT2; LDA COUNT2; SNE =4; BRU NOCHAN
       BRS 156; TCO =155B; BRM OUTSYS
       MSG M33 (UNABLE ACTIVATE AUX CIRCUIT)
       BRM RING; BRU BUILD

ERRLOG ZRO; SNE =123B; BRR ERRLOG; SNE =323B; BRR ERRLOG; CLA; BRS 78
       MIN COUNT1; LDA COUNT1; SNE =4; BRU NOLOG; BRS 156; TCO =155B; BRM OUTSYS
       MSG M34 (CIR BUILT UNABLE LOG IN)
       BRM RING; BRU BUILD

* ARM SOFTWARE INTERRUPT NUMBER 10 - ZAPPED CIRCUIT

ARM10  ZRO; LDA =ZAPCIR; STA 212B; BRS 49; MRG =2B3; BRS 78; BRR ARM10

* ARM SOFTWARE INTERRUPT NUMBER 5 - LOG IN TIMEOUT

ARM5   ZRO; LDA =TYMOUT; STA 205B; BRS 49; MRG =1B5; BRS 78; LDB =180000
       BRS 135; BRR ARM5

NOLOG  TCO =155B; BRM OUTSYS
       MSG M35 (UNABLE LOG IN AFTER FOUR TRYS)
       BRM RING; BRM RESET; BRM SYSLOG; BRU BLDCIR

NOCHAN TCO =155B; BRM OUTSYS
       MSG M36 (UNABLE ACT AUX CIR AFTER FOUR TRYS)
       BRM RING; BRM RESET; BRM SYSLOG; BRU BLDCIR

IPORT  ZRO
OPORT  ZRO
COUNT  ZRO
COUNT1 ZRO
COUNT2 ZRO

* TRIES TO BUILD CIRCUIT TWICE IF ERROR OCCURS

CIRERR STA CERFLG; MIN COUNT; LDA COUNT; SNE =2; BRU CKERR; BRU BUILD

CERFLG ZRO

* DISARM SOFTWARE INTERRUPT NUMBER 5 - TIMEOUT

DISAR5 ZRO; BRS 49; ETR =77677777B; BRS 78; BRR DISAR5

* TIMEOUT ROUTINE FOR LOG IN

TYMOUT ZRO; BRM RESET; TCO =155B; BRM OUTSYS
       MSG M37 (LOGIN TIMEOUT)
       BRM RING; BRM SYSLOG; BRU BLDCIR

* ZAPPED CIRCUIT ROUTINE

ZAPCIR ZRO; BRM RESET; TCO =155B; BRM OUTSYS
       MSG M38 (CIR ZAPPED)
       BRM RING; BRM SYSLOG; BRU BLDCIR

* CIRCUIT ERROR ROUTINE

CKERR  LDA CERFLG; SNE =1; BRU CERR1; SNE =2; BRU CERR2
       SNE =3; BRU CERR3; SNE =4; BRU CERR4; BRU CERR5

CERR1  BRM RESET; TCO =155B; BRM OUTSYS
       MSG M45 (FORMAT ERROR USER NAME)
       BRM RING; BRU BLDCIR

CERR2  BRM RESET; TCO =155B; BRM OUTSYS
       MSG M46 (USER NOT IN MUD)
       BRM RING; BRU BLDCIR

CERR3  BRM RESET; TCO =155B; BRM OUTSYS
       MSG 47 (BAD MUD)
       BRM RING; BRU BLDCIR

CERR4  BRM RESET; TCO =155B; BRM OUTSYS
       MSG M48 (SYS UNAVAIL)
       BRM RING1; BRM SYSLOG; BRU BLDCIR

CERR5  BRM RESET; TCO =155B; BRM OUTSYS
       MSG M49 (UNDETERMINED AUX CIR ERROR)
       BRM RING; BRM SYSLOG; BRU BLDCIR

* ACTIVATE SUPSLV

ACTSLV BRM BUFCLR; CLA; STA COUNT; BRM ON8; BRM ARM8; BRM ARM10; BRM ARM5A
       LDA =SUPSLV; BRM AUXSEN; BRM SYNC; BRU RECEV

* SET EIGHT LEVEL MODE FOR AUXILIARY OUTPUT PORT

ON8    ZRO; LDA =42B6+400B; LDX OPORT; BRS 12; BRR ON8

* RESET TO NORMAL MODE FOR AUXILIARY OUTPUT PORT

OFF8   ZRO; LDX OPORT; BRS 86; BRR OFF8

* ARM SOFTWARE INTERRUPT NUMBER 8 - BUFFER ZAPPED

ARM8   ZRO; LDA =BUFZAP; STA 210B; BRS 49; MRG =1B4; BRS 78; BRR ARM8

* ARM SOFTWARE INTERRUPT NUMBER 5 - CIO TIMEOUT (SLAVE INACTIVE)

ARM5A  ZRO; LDA =TIMOUT; STA 205B; BRS 49; MRG =1B5; BRS 78; LDB =120000
       BRS 135; BRR ARM5A

* ROUTINE TO SEND CHARACTERS VIA AUXILIARY PORT IN EIGHT LEVEL MODE
* A REGISTER CONTAINS ADDRESS OF FIRST WORD OF ASCII STRING
* $ = CARRIAGE RETURN , / = END OF MESSAGE

AUXSEN ZRO; STA AUXSTR
AUX1   LDX =-3; LDB* AUXSTR
AUX2   STX INDEX; LSH 8; ETR =377B; SNE ='  /'; BRR AUXSEN; SNE ='  $'
       BRU AUX4; ADD =40B; ETR =177B; CIO OPORT
AUX3   LDX INDEX; BRX AUX2; MIN AUXSTR; BRU AUX1
AUX4   LDA =215B; CIO OPORT; BRU AUX3

AUXSTR ZRO
INDEX  ZRO
SUPSLV ASC 'GO (UTIL)SLVSUP$/'
MAISTR ASC 'MAIL$/'
PSPSTR ASC 'PSP$/'
PCODE  ASC 'SUPCHECK'

* TIMEOUT ROUTINE FOR CIO - SUPSLV INACTIVE

TIMOUT ZRO; BRM RESET; TCO =155B; BRM OUTSYS
       MSG M50 (SUPSLV INACTIVE)
       BRM RING; BRM SYSLOG; BRU BLDCIR

* BUFFER ZAPPED ROUTINE

BUFZAP ZRO; BRM RESET; TCO =155B; BRM OUTSYS
       MSG M51 (BUFFER ZAPPED)
       BRM RING; BRM SYSLOG; BRU BLDCIR


* ROUTINE TO RING BELL

RING   ZRO; LDX =-5; TCO =147B; BRX *-1; BRR RING
RING1  ZRO; LDX =-20; TCO =147B; BRX *-1; BRR RING1

* OUTPUT SYSTEM NUMBER TO TTY

OUTSYS ZRO; TCO =155B; TCO SYSLTR; LDA SYSTEM; LDB =10
       LDX TOUT; BRS 36; SKG =9; TCO =0; TCO =0; BRR OUTSYS
OUTSY1 ZRO; TCO SYSLTR; LDA SYSTEM; LDB =10; LDX TOUT; BRS 36
       SKG =9; TCO =0; TCO =0; BRR OUTSY1

* READ CODE TELLING MASTER THAT SLAVE IS ACTIVE

RECEV  CIO IPORT; SKE =141B; BRU ERRCOD; CIO IPORT; SKE =142B; BRU ERRCOD
       CIO IPORT; SKE =143B; BRU ERRCOD; CIO IPORT; SKE =144B; BRU ERRCOD

* SEND CODE TELLING SLAVE THAT MASTER IS ACTIVE

SEND   LDA =141B; CIO OPORT; LDA =142B; CIO OPORT; LDA =143B; CIO OPORT
       LDA =144B; CIO OPORT; BRM VERIFY; BRM DISAR5; BRU GSPOOL

* CLEARS INPUT AND OUTPUT BUFFERS AND TRIES TO RE-ESTABLISH COMMUNICATIONS

ERRCOD SKE =140B; BRU *+3; CIO OPORT; BRU RECEV; LDA =140B; CIO OPORT
       CIO IPORT; SKE =140B; BRU *-2; BRU RECEV

* CLEAR INPUT BUFFER FROM REMOTE SYSTEM

SYNC   ZRO; CIO IPORT; SKE =160B; BRU *-2; CIO IPORT; SKE =160B
       BRU ERRCOD; BRR SYNC

* RECEIVE VERIFICATION CODE FROM SLAVE - 151

VERIFY ZRO; CIO IPORT; SNE =151B; BRR VERIFY; BRU ERRCOD

* COMMUNICATION ERROR - NOT RECEIVING CORRECT CODE FROM SLAVE

TERM   BRM RESET; TCO =155B; BRM OUTSYS
       MSG M52 (BAD CODE REC FROM SLAVE)
       BRM RING; BRM SYSLOG; BRU BLDCIR

* ARM SOFTWARE INTERRUPT NUMBER 5 - CIO TIMEOUT

ARM5B  ZRO; LDA =COMERR; STA 205B; BRS 49; MRG =1B5; BRS 78; LDB =60000
       BRS 135; BRR ARM5B

COMERR ZRO; BRM RESET; TCO =155B; BRM OUTSYS
       MSG M53 (CIO TIMOUT)
       BRM RING; BRM SYSLOG; BRU BLDCIR

*GET SPOOLING INFORMATION AND DISPLAY ON TELETYPE

GSPOOL CIO IPORT; SNE =150B; BRU BAERR; SKE =153B; BRU GSPOOL; CIO OPORT
       CIO IPORT; STA T1;            LDA T1; SNE =0; BRU GSP2
       LDA T1
       LDB =10; LDX =1
       CLA; CIO =1
GSP1   BRU GETREQ
GSP2   BRU GSP1
TSTARS ZRO; TCO =154B; BRM CR; LDX =-70; TCO ='*'; BRX *-1; TCO =155B; BRR TSTARS
ESTARS ZRO; LDX =-70; TCO ='*'; BRX *-1; BRM CJ; TCO =154B; BRR ESTARS
T1     ZRO


* CHECK FOR MAIL WAITING

CKHOME ZRO; SKE HOMSYS; BRR CKHOME; BRS 39; SKA =2; BRU *+2; BRR CKHOME
       BRM CR; BRR CKHOME
CKMAIL BRM ON8; BRM ARM8; BRM ARM10; LDA =215B; CIO OPORT
       LDA =MAISTR; BRM AUXSEN; BRM ARM5C; BRM GETMSG
       BRM DISAR5; BRM CLRBUF; BRM TSTARS; BRM OUTSYS
       MSG M14 (MAIL); BRM DATE;            BRM RECMAI
       BRM ESTARS; CLA; STA COUNT; BRU ACTSLV

* INPUT LOG IN MESSAGE FROM REMOTE SYSTEM

GETMSG ZRO; CIO IPORT; SKE =115B; SNE =315B; BRU GTMSG1; BRU GETMSG+1

GTMSG1 CIO IPORT; SKE =101B; SNE =301B; BRU GTMSG2; BRU GETMSG+1

GTMSG2 CIO IPORT; SKE =111B; SNE =311B; BRU GTMSGU GETMSG+1

GTMSG3 CIO IPORT; SKE =114B; SNE =314B; BRU GTMSG4; BRU GETMSG+1

GTMSG4 CIO IPORT; SKE =15B; SNE =215B; BRR GETMSG; BRU GETMSG+1

* CLEAR AUXILIARY INPUT PORT OF CHARACTERS UP TO MAIL MESSAGE

CLRBUF ZRO; LDX IPORT; BRS 13; BRU *+2; BRU AGAIN1; CIO IPORT; SKE =106B
       SNE =306B; BRU MAIL; SKE =116B; SNE =316B; BRU NOMAIL
       SKE =115B; SNE =315B; BRU BUSY; BRU CLRBUF+1

MAIL   STA TEMP; CLA; STA COUNT; BRR CLRBUF

NOMAIL CLA; STA COUNT; BRU ACTSLV

BUSY   CLA; STA COUNT; BRU ACTSLV

* NO CHARACTERS IN AUXILIARY INPUT BUFFER - WAIT 60 SECONDS

AGAIN1 MIN COUNT; LDA COUNT; SNE =2; BRU CIOERR; LDA =60000; BRS 81
       BRU CLRBUF+1

BUFCLR ZRO; LDX IPORT; BRS 13; BRU *+2; BRU AGAIN2; CIO IPORT
       SKE =55B; SNE =255B; BRR BUFCLR; SKE =56B; SNE =256B
       BRR BUFCLR; BRU BUFCLR+1

AGAIN2 MIN COUNT; LDA COUNT; SNE =2; BRU CIOERR; LDA =60000; BRS 81
       BRU BUFCLR+1

* CONVERT CHARACTERS FROM REMOTE SYSTEM AND OUTPUT TO TTY - MAIL

RECMAI ZRO; LDA TEMP; SUB =40B; ETR =177B; BRM CTOUT
       BRM ARM8; BRM ARM10; LDA =PSPSTR; BRM AUXSEN; LDX IPORT
       BRS 13; BRU *+2; BRU IOWAIT; CIO IPORT; SKE =55B; SNE =255B
      BRU CHREND; SKE =56B; SNE =256B; BRU CHREND; SUB =40B; ETR =177B
       BRM CTOUT; BRU RECMAI+5

* ENDING SEQUENCE - HYPHEN OR PERIOD FOLLOWED BY PSP

CHREND STA TMP1; BRM ARM5B; CIO IPORT; STA TEMP; BRM DISAR5; LDA TEMP
       SKE =120B; SNE =320B; BRU CHEND1; STA TMP2; LDA TMP1; SUB =40B; ETR =177B
       BRM CTOUT; LDA TMP2; SUB =40B; ETR =177B; BRM CTOUT; BRU RECMAI+5

CHEND1 STA TMP2; BRM ARM5B; CIO IPORT; STA TEMP; BRM DISAR5; LDA TEMP
       SKE =123B; SNE =323B; BRU CHEND2; STA TMP3; LDA TMP1; SUB =40B; ETR =177B
       BRM CTOUT; LDA TMP2; SUB =40B; ETR =177B; BRM CTOUT; LDA TMP3
       SUB =40B; ETR =177B; BRM CTOUT; BRU RECMAI+5

CHEND2 STA TMP3; BRM ARM5B; CIO IPORT; STA TEMP; BRM DISAR5; LDA TEMP
       SKE =120B; SNE =320B; BRR RECMAI; STA TMP4; LDA TMP1; SUB =40B; ETR =177B
       BRM CTOUT; LDA TMP2; SUB =40B; ETR =177B; BRM CTOUT; LDA TMP3
       SUB =40B; ETR =177B; BRM CTOUT; LDA TMP4; SUB =40B; ETR =177B
       BRM CTOUT; BRU RECMAI+5

* NO CHARACTERS IN AUXILIARY INPUT BUFFER - WAIT 60 SECONDS

IOWAIT MIN COUNT; LDA COUNT; SNE =2; BRU CIOERR; LDA =60000; BRS 81
       BRU RECMAI+5

* WAITED FOR 120 SECONDS WITHOUT RECEIVING CHARACTERS FROM REMOTE SYSTEM

CIOERR BRM RESET; TCO =155B; BRM OUTSYS
       MSG M54 (CIO TIMEOUT FROM SLAVE)
       BRM RING; BRM SYSLOG; BRU BLDCIR

* RESET FLAGS TO ZERO

RESET  ZRO; CLA; STA CERFLG; STA COUNT; STA COUNT1; STA COUNT2; BRS 78
       BRS 156; BRR RESET

* IF REQUEST WAITING THEN GET IT

GETREQ BRM AUXREC; BRM DISAR5; LDA =153B; CIO OPORT; BRM CR; BRM ENDSLV
       BRM RESET; BRU BLDCIR

CLRMSG ZRO; BRM TSTARS; BRM OUTSY1
       MSG M15 (BATCH REQUEST)
       BRM DATE; BRR CLRMSG

* NO BATCH REQUEST WAITING

CLEAR  CIO OPORT;              BRM ENDSLV; BRM RESET; BRU BLDCIR

* HAVE CHECKED ALL SYSTEMS - CHECK FOR WAIT

* HAVE CHECKED ALL SYSTEMS - CHECK SYSTEM RETRY LIST

DONE   BRS 156; LDA RETRY; SKE =0; BRU DONE1; MIN RETRY; LDA =999
       LDX SYSTAB+1; STA 0,2; LDA =ERRTAB; STA SYSTAB; CAX
       LDA 0,2; SKE =999; BRU BLDCIR

DONE1  BRM RESET; LDA PAUSE; SNE =0; BRU REINIT
       CLB; MUL =60; LSH 23; MUL =1000; LSH 23; STA WTGFLG
                                    MSG M17 (BEGINNING; LDA PAUSE
       LDB =10; LDX TOUT; BRS 36; MSG M18 (MIN WAIT); BRM DATE
       LDA WTGFLG; BRS 81; BRM RING; BRU REINIT

RETRY  ZRO
WTGFLG ZRO

* ARM SOFTWARE INTERRUPT NUMBER 5 - CIO TIMEOUT (MAIL ROUTINE)

ARM5C  ZRO; LDA =MAILER; STA 205B; BRS 49; MRG =1B5; BRS 78; LDB =120000
       BRS 135; BRR ARM5C;

* TIMEOUT ROUTINE FOR CIO - MAIL

MAILER ZRO; TCO =155B; BRM OUTSYS
       MSG M40 (MAIL ROUTINE FAIL)
       BRM RING; BRU ACTSLV

* RE-INITIALIZE BUILDING POINTER

REINIT CLA; STA RETRY; LDA =ERRTAB; STA SYSTAB+1
       LDA BLDSTR; SNE ='A'; BRU INITA; SNE ='B'
       BRU INITB; SNE ='C'; BRU INITC
       LDA SYSLTR; SNE ='N'; BRU INITE; SNE ='V'; BRU INITE
       SNE ='H'; BRU INITS; BRU BLDERR

* RE-INITIALIZE SYSTEM POINTER FOR BUILDING A

INITA  LDA =TABLEA; STA SYSTAB; BRM AMSG; BRU BLDCIR

* RE-INITIALIZE SYSTEM POINTER FOR BUILDINGS A AND C

INITB  LDA =TABLEB; STA SYSTAB; BRM BMSG; BRU BLDCIR

* RE-INITIALIZE SYSTEM POINTER FOR BUILDING C

INITC  LDA =TABLEC; STA SYSTAB; BRM CMSG; BRU BLDCIR

*RE-INITIALIZE SYSTEM POINTER FOR EASTERN COMPUTER CENTER

INITE  LDA =ETABLE; STA SYSTAB; BRM EMSG; BRU BLDCIR

*RE-INITIALIZE SYSTEM POINTER FOR SOUTHERN COMPUTER CENTER

INITS  LDA =STABLE; STA SYSTAB; BRM SMSG; BRU BLDCIR


BLDERR MSG M44 (ERR TRYING TO REINIT SYS PTRS)
       MSG M20 (ABORT); BRS 10

* SUPSLV TELLING MASTER HE IS QUITTING

ENDSLV ZRO; BRM ARM5B; CIO IPORT; STA TEMP; BRM DISAR5; LDA TEMP; SNE =145B
       BRR ENDSLV; TCO =155B; BRM OUTSYS
       MSG M55 (UNABLE TERMINATE SUPSLV)
       BRM RING; BRR ENDSLV

* OUTPUT DATE AND TIME BATCH FILE WAS LAST CLEARED

OUTDAT ZRO; MSG M19 (FILE LAST CLEARED)
       LDP TIMPTR; LDX TOUT; BRS 35; TCO =155B; BRR OUTDAT

TIMPTR DATA 3*TIMSTR-1,3*TIMSTR-1
TIMSTR BSS 10

* CONVERT CHARACTERS FROM REMOTE SYSTEM AND OUTPUT TO TTY - BATCH REQUEST

AUXREC ZRO; CLA; STA COUNT; BRM ARM8; BRM ARM10; LDX IPORT; BRS 13; BRU *+2
       BRM CIOWT1; CIO IPORT; BRM CKCHR2; BRM CKCHR1; CIO OPORT
       BRM CR;             BRM CLRMSG; CLA; STA COUNT; BRM GETDAT; BRM OUTDAT

AUXA   LDX IPORT; BRS 13; BRU *+2; BRM CIOWT3; CIO IPORT
       BRM CKCHR;             BRM CTOUT; BRU AUXA

*      READ SPOOL HARD COPY AND PUT TO THE THERMINAL

SPLHRD CIO     OPORT
       BRM     SPLMSG
       CLA
       STA     COUNT
SPLH1  LDX     IPORT
       BRS     13
       BRU     *+2
       BRM     CIOWT2
       CIO     IPORT
       SNE     =167B           END
       BRU     SPLEND
       SNE     =165B           ERROR
       BRU     SPLERR
       SNE     =170B
       BRU     SPLH2
       BRM     CTOUT
       BRU     SPLH1
SPLH2  CIO     OPORT
       BRU     SPLH1
SPLEND CIO     OPORT
       BRM ESTARS
       BRU     AUXREC+5

SPLERR CIO     OPORT
       BRM     CR
       BRM     OUTSYS
       MSG     M88             ERROR IS SPOOL HARDCOPY
       BRU     AUXREC+5

SPLMSG ZRO; BRM TSTARS
       BRM     OUTSYS
       MSG     M89             SPOOL HARDCOPY
       BRM     DATE
       BRR     SPLMSG


* GET DATE AND TIME BATCH FILE WAS LAST CLEARED

GETDAT ZRO; CLA; STA COUNT; LDA TIMPTR; STA TIMPTR+1; LDX IPORT; BRS 13
       BRU *+2; BRM CIOWT2; CIO IPORT; BRM CKCHR; SNE =155B
       BRR GETDAT; WCI TIMPTR; CLA; STA COUNT; BRU GETDAT+5

* CHECK FOR CONTROL CODE

CKCHR  ZRO; SNE =156B; BRU NXTREQ; SNE =150B; BRU BAERR; SNE =153B
       BRU ENDCHR; SKE =170B; BRR CKCHR; CIO OPORT; BRU AUXA

CKCHR1 ZRO; SNE =146B; BRU CLEAR; SNE =147B; BRR CKCHR1; SNE =150B
       BRU BAERR; SKE =170B; BRU AGAIN; CIO OPORT; BRU AUXREC+5

CKCHR2 ZRO; SNE =155B; BRU ERR1;
       SNE     =166B
       BRU     SPLHRD
       SKE     =170B
       BRR     CKCHR2
       CIO     OPORT
       BRU     AUXREC+5

AGAIN  MIN COUNT; LDA COUNT; SNE =2; BRU TERM; CLA; CIO OPORT; BRU AUXREC+5

* NO CHARACTERS IN AUXILIARY INPUT BUFFER - WAIT 60 SECONDS

CIOWT1 ZRO; BRM ARM5B; LDX IPORT; BRS 13; BRU *+2; BRU *-2; BRM DISAR5
       BRR CIOWT1

CIOWT2 ZRO; BRM ARM5B; LDX IPORT; BRS 13; BRU *+2; BRU *-2; BRM DISAR5
       BRR CIOWT2

CIOWT3 ZRO; BRM ARM5B; LDX IPORT; BRS 13; BRU *+2; BRU *-2; BRM DISAR5
       BRR CIOWT3

* OUTPUT HEADING BEFORE NEXT BATCH REQUEST

NXTREQ CIO OPORT;                     BRM CLRMSG; CLA; STA COUNT; BRU AUXA-1

* ERROR ON REMOTE SYSTEM WHILE OPENING BATCH FILE - SUPSLV

BAERR  CIO OPORT; BRM ARM5B; CIO IPORT; STA TEMP; BRM DISAR5; LDA TEMP
       SKG =0; BRU AGAIN3; SKL =21; BRU AGAIN3; BRM RESET
       LDA TEMP; SNE =1; BRU ERR1; SNE =2; BRU ERR2; SNE =3; BRU ERR3
       SNE =4; BRU ERR4; SNE =5; BRU ERR5; SNE =6; BRU ERR6; SNE =7
       BRU ERR7; SNE =8; BRU ERR8; SNE =9; BRU ERR9; SNE =10; BRU ERR10
       SNE =11; BRU ERR11; SNE =12; BRU ERR12; SNE =13; BRU ERR13; SNE =14
       BRU ERR14; SNE =15; BRU ERR15; SNE =16; BRU ERR16; SNE =17
       BRU ERR17; SNE =18; BRU ERR18; SNE =19; BRU ERR19; BRU 20ERR

* INVALID CODE RECEIVED FROM SLAVE, TRY AGAIN

AGAIN3 MIN COUNT; LDA COUNT; SNE =2; BRU TERM; BRU BAERR+1

* ENDING SEQUENCE - 153 FOLLOWED BY ANOTHER 153

ENDCHR STA TMP1; BRM ARM5B; CIO IPORT; SNE TMP1; BRM ESTARS; BRR AUXREC; CLA
       CIO OPORT; CIO IPORT; BRM ESTARS; STA TMP1; BRU ENDCHR+2

* SYSTEM FAILURE - ENTER SYSTEM IN SYSTEM RETRY LIST

SYSLOG ZRO; LDA RETRY; SKE =0; BRR SYSLOG
       LDA SYSTAB+1; SUB =1; CAX; LDA 0,2; SNE SYSTEM; BRR SYSLOG
       LDA SYSTAB+1; CAX; EAX 1,2; STX SYSTAB+1; CAX; LDA SYSTEM
       STA 0,2; BRR SYSLOG

* ERROR ROUTINE FOR OPENING FILES - SUPSLV

ERR1   TCO =155B; BRM OUTSYS
       MSG M56 (BATCH FILE BUSY)
       BRM RING; BRM SYSLOG; BRU BLDCIR

ERR2   TCO =155B; BRM OUTSYS
       MSG M57 (UNABLE TO OPEN @BA)
       BRM RING; BRU BLDCIR

ERR3   TCO =155B; BRM OUTSYS
       MSG M58 (ERROR INPUT @BA)
       BRM RING; BRU BLDCIR

ERR4   TCO =155B; BRM OUTSYS
       MSG M59 (UNABLE DO LOOKUP @BA)
       BRM RING; BRU BLDCIR

ERR5   TCO =155B; BRM OUTSYS
       MSG M60 (UNABLE DELETE @BA)
       BRM RING; BRU BLDCIR

ERR6   TCO =155B; BRM OUTSYS
       MSG M61 (UNABLE CREATE @BA)
       BRM RING; BRU BLDCIR

ERR7   TCO =155B; BRM OUTSYS
       MSG M62 (UNABLE DECLARE @BA)
       BRM RING; BRU BLDCIR

ERR8   TCO =155B; BRM OUTSYS
       MSG M63 (UNABLE DO LOOKUP ON BAHOLD)
       BRM RING; BRU BLDCIR

ERR9   TCO =155B; BRM OUTSYS
       MSG M64 (UNABLE DECLARE BAHOLD)
       BRM RING; BRU BLDCIR

ERR10  TCO =155B; BRM OUTSYS
       MSG 67 (UNABLE OPEN BAHOLD)
       BRM RING; BRU BLDCIR

ERR11  TCO =155B; BRM OUTSYS
       MSG M65 (UNABLE OPEN PPRTMP.DAT)
       BRM RING; BRU BLDCIR

ERR12  TCO =155B; BRM OUTSYS
       MSG M66 (ERROR INPUT PPRTMP.DAT)
       BRM RING; BRU BLDCIR

ERR13  TCO =155B; BRM OUTSYS
       MSG M68 (UNABLE RENAME PPRTMP.DAT TO PPRCLR.DAT)
       BRM RING; BRU BLDCIR

ERR14  TCO =155B; BRM OUTSYS
       MSG M69 (UNABLE TO CREATE NEW PPRTMP.DAT)
       BRM RING; BRU BLDCIR

ERR15  TCO =155B; BRM OUTSYS
       MSG M70 (ERROR ON OUTPUT PPRTMP.DAT)
       BRM RING; BRU BLDCIR

ERR16  TCO =155B; BRM OUTSYS
       MSG M71 (UNABLE OPEN PPRCLR.DAT)
       BRM RING; BRU BLDCIR

ERR17  TCO =155B; BRM OUTSYS
       MSG M72 (ERROR ON INPUT PPRCLR.DAT)
       BRM RING; BRU BLDCIR

ERR18  TCO =155B; BRM OUTSYS
       MSG M73 (FORMAT INCORRECT PPRCLR.DAT)
       BRM RING; BRU BLDCIR

ERR19  TCO =155B; BRM OUTSYS
       MSG M74 (UNABLE TO DELETE PPRCLR.DAT)
       BRM RING; BRU BLDCIR

20ERR  TCO =155B; BRM OUTSYS; MSG M86 (UNABLE OPEN SPOOL CONTROL FILE)
       BRM RING; BRU BLDCIR


* WHO USED THIS PROGRAM AND WHEN

RECORD ZRO; LDP WHEN; BRS 91; LDP CONPTR; BRS 48; BRU ERR20; LDX =21402140B
       BRS 6; BRU ERR21; LDX =3; LDP CONPTR; BRS 65; BRU ERR22; CAX
       STA CONFN; LDP NAMPTR; BRS 35; CLA; CIO CONFN; CIO CONFN
       LDP WHEN; LDX CONFN; BRS 35; LDA =155B; CIO CONFN; LDA CONFN
       BRS 20; BRR RECORD

* ERROR ROUTINE FOR CONTROL FILE UPDATING

ERR20  MSG M41 (CONTROL FILE MISSING)
       MSG M20 (ABORT); BRS 10

ERR21  MSG M42 (UNABLE DECLARE CONTROL FILE)
       MSG M20 (ABORT); BRS 10

ERR22  MSG M43 (CONTROL FILE BUSY)
       MSG M1 (EXIT); BRS 10

CJ     ZRO; LDX =-10; TCO =154B; LDA =155B; BRM CTOUT; BRX *-1; BRR CJ
CR     ZRO; LDX =-7; TCO =154B; LDA =155B; BRM CTOUT; BRX *-1; BRR CR

WHEN   DATA 3*STRING-1,3*STRING+12
STRING BSS 5
CONPTR ASCP    '(UTIL)/SUP-CONTROL/'
CONFN  ZRO

*      KEEP FROM OVER-PRINTING

CTOUT  ZRO; STA CTA; SKE =155B; BRU *+2; BRU *+3; SKE =152B; BRU C1; CLA
C3     STA CTT; LDA CTA; CIO TOUT; BRR CTOUT
C1     LDA CTT; SKE =72; BRU C2; LDA =155B; CIO TOUT; LDA =1; BRU C3
C2     ADD =1; BRU C3
CTA    ZRO
CTT    ZRO
*MSG STRINGS

M1     ASC '$EXIT$/'
M2     ASC '$NAME: /'
M3     ASC '$PASSWORD: /'
M4     ASC '$THANK YOU!$/'
M5     ASC '$INPUT BUILDING A, C, OR B [BOTH]: /'
M6     ASC '$REMOVE SYSTEM (Y OR N)? /'
M7     ASC '$ADD SYSTEM (Y OR N)? /'
M8     ASC '$ENTER SYSTEM NUMBER (OMIT LETTER): /'
M9     ASC '$$$CLEARING BUILDING A  /'
M10    ASC '$$$CLEARING BUILDINGS A AND C  /'
M11    ASC '$$$CLEARING BUILDING C  /'
M13    ASC '$ENTER RESTART INTERVAL [MINUTES]: /'
M14    ASC '- MAIL  /'
M15    ASC '- BATCH REQUEST  /'
M16    ASC '- CLEAR  /'
M17    ASC 'BEGINNING /'
M18    ASC ' MINUTE WAIT  /'
M19    ASC '$FILE LAST CLEARED/'
M20    ASC '$ABORT$/'
M22    ASC '$THIS PROGRAM MUST BE RUN FROM GAN 1$/'
M23    ASC '$PASSWORD FILE BUSY - PLEASE TRY LATER$/'
M24    ASC '$UNABLE TO SET PASSWORD FILE TO READ MODE$/'
M25    ASC '$NAME DOES NOT APPEAR IN PASSWORD FILE$/'
M26    ASC '$UNABLE TO EXECUTE A GCI INSTRUCTION$/'
M27    ASC '$NAME NOT VALIDATED$/'
M28    ASC '$ERROR, TYPE PASSWORD: /'
M29    ASC '$MUST HAVE DISTRICT ZERO OPERATOR STATUS$/'
M30    ASC ' IS NOT PRESENTLY ASSIGNED FOR CLEARING$/'
M31    ASC ' IS NOT A VALID SYSTEM NUMBER$/'
M32    ASC '$TIME ENTERED MUST BE BETWEEN 5 AND 60 INCLUSIVE$/'
M33    ASC '- UNABLE TO ACTIVATE AUXILIARY CIRCUIT, WILL TRY AGAIN!$$/'
M34    ASC '- CIRCUIT BUILT BUT UNABLE TO LOG IN, WILL TRY AGAIN!$$/'
M35    ASC '- UNABLE TO LOG IN AFTER FOUR ATTEMPTS$$/'
M36    ASC '- UNABLE TO ACTIVATE AUXILIARY CIRCUIT AFTER FOUR ATTEMPTS$$/'
M37    ASC '- LOG IN TIMEOUT$$/'
M38    ASC '- CIRCUIT ZAPPED$$/'
M39    ASC '- MAIL BOX BUSY$$/'
M40    ASC '- MAIL ROUTINE FAILURE$$/'
M41    ASC '$CONTROL FILE MISSING$/'
M42    ASC '$UNABLE TO DECLARE CONTROL FILE PROPERLY$/'
M43    ASC '$CONTROL FILE BUSY - PLEASE TRY LATER$/'
M44    ASC '$ERROR WHILE TRYING TO RE-INITIALIZE SYSTEM POINTER$/'
M45    ASC '- FORMAT ERROR ON USER NAME$$/'
M46    ASC '- USER NAME NOT IN MUD$$/'
M47    ASC '- BAD MUD$$/'
M48    ASC '- SYSTEM UNAVAILABLE$$/'
M49    ASC '- UNDETERMINED ERROR WHILE BUILDING AUXILIARY CIRCUIT$$/'
M50    ASC '- SUPSLV INACTIVE$$/'
M51    ASC '- BUFFER ZAPPED$$/'
M52    ASC '- INVALID CODE RECEIVED FROM SLAVE$$/'
M53    ASC '- COMMUNICATION ERROR WITH SLAVE [CIO TIMOUT]$$/'
M54    ASC '- CIO TIMEOUT FROM SLAVE, CIRCUIT ZAPPED BY MASTER$$/'
M55    ASC '- UNABLE TO TERMINATE SUPSLV, CIRCUIT ZAPPED$$/'
M56    ASC '- BATCH FILE BUSY$$/'
M57    ASC '- UNABLE TO OPEN @BA, FILE MAY BE MISSING$$/'
M58    ASC '- ERROR ON INPUT, @BA$$/'
M59    ASC '- UNABLE TO DO A LOOKUP ON @BA$$/'
M60    ASC '- UNABLE TO DELETE @BA$$/'
M61    ASC '- UNABLE TO CREATE NEW @BA$$/'
M62    ASC '- UNABLE TO DECLARE @BA PROPERLY$$/'
M63    ASC '- UNABLE TO DO A LOOKUP ON BAHOLD$$/'
M64    ASC '- UNABLE TO DECLARE BAHOLD PROPERLY$$/'
M65    ASC '- UNABLE TO OPEN PPRTMP.DAT, FILE MAY BE MISSING$$/'
M66    ASC '- ERROR ON INPUT, PPRTMP.DAT$$/'
M67    ASC '- UNABLE TO OPEN BAHOLD$$/'
M68    ASC '- UNABLE TO RENAME PPRTMP.DAT TO PPRCLR.DAT$$/'
M69    ASC '- UNABLE TO CREATE NEW PPRTMP.DAT$$/'
M70    ASC '- ERROR ON OUTPUT, PPRTMP.DAT$$/'
M71    ASC '- UNABLE TO OPEN PPRCLR.DAT$$/'
M72    ASC '- ERROR ON INPUT, PPRCLR.DAT$$/'
M73    ASC '- FORMAT INCORRECT, PPRCLR.DAT$$/'
M74    ASC '- UNABLE TO DELETE PPRCLR.DAT$$/'
M75    ASC '$$$CLEARING EASTERN COMPUTER CENTER /'
M76    ASC '$$$CLEARING SOUTHERN COMPUTER CENTER /'
M77    ASC '$OKAY? /'
M78    ASC '$ARE CHANGES PERMANENT? /'
M79    ASC '$SUPCHECK UPDATED$/'
M80    ASC '$UNABLE OPEN (UTIL)SUPCHECK/'
M81    ASC '$CHANGES WERE NOT MADE PERMANENT$/'
M82    ASC '$UNABLE SET CURSOR (UTIL)SUPCHECK'
       ASC '$FILE MAY BE BAD/'
M83    ASC '$BIO ERROR UPDATING (UTIL)SUPCHECK'
       ASC '$PROGRAM FILE MAY BE CLOBBERED/'
M84    ASC '$CURRENT SYSTEMS ARE: /'
M85    ASC ' SPOOLED REQUESTS WAITING PRINTING ON /'
M86    ASC '- UNABLE TO OPEN SPOOL CONTROL FILE "SPLCNT"$$/'
M87    ASC 'NO SPOOLED FILES ON /'
M88    ASC     '$ERROR IN READING SPOOL HARDCOPY$/'
M89    ASC     '$$SPOOL REQUEST HARDCOPY$$/'
M90    ASC ' '

       END
SUPCHECK ** FICHE/FRAME BREAK *****
/CUPSUP/ SYMSORT

SYM.  PG.LN. IDENT.

20ERR  25 24 SUPCK   ACTSLV 13 10 SUPCK   ADDSYS  8  6 SUPCK   
AGAIN  22 19 SUPCK   AGAIN1 17  6 SUPCK   AGAIN2 17 13 SUPCK   AGAIN3 23 10 SUPCK   
AMSG    9 19 SUPCK   ARM10  11 18 SUPCK   ARM5   11 22 SUPCK   ARM5A  13 27 SUPCK   
ARM5B  15 27 SUPCK   ARM5C  19 11 SUPCK   ARM8   13 23 SUPCK   ASCP    1  9 SUPCK   
AUX1   13 35 SUPCK   AUX2   13 36 SUPCK   AUX3   13 38 SUPCK   AUX4   13 39 SUPCK   
AUXA   20 34 SUPCK   AUXCHN 11  6 SUPCK   AUXREC 20 30 SUPCK   AUXSEN 13 34 SUPCK   
AUXSTR 14  2 SUPCK   BAERR  22 38 SUPCK   BELL    3 22 SUPCK   BLDCIR 10 31 SUPCK   
BLDERR 20 10 SUPCK   BLDGA   6  4 SUPCK   BLDGB   6  9 SUPCK   BLDGC   6 14 SUPCK   
BLDPTR  5 35 SUPCK   BLDSTR  5 36 SUPCK   BMSG    9 22 SUPCK   BUFCLR 17  9 SUPCK   
BUFZAP 14 17 SUPCK   BUILD  10 34 SUPCK   BUSY   17  2 SUPCK   C1     26 20 SUPCK   
C2     26 21 SUPCK   C3     26 19 SUPCK   CERFLG 12  4 SUPCK   CERR1  12 27 SUPCK   
CERR2  12 31 SUPCK   CERR3  12 35 SUPCK   CERR4  12 39 SUPCK   CERR5  13  4 SUPCK   
CHEND1 17 30 SUPCK   CHEND2 17 35 SUPCK   CHGSW   5 39 SUPCK   CHREND 17 26 SUPCK   
CIOERR 18  9 SUPCK   CIOWT1 22 23 SUPCK   CIOWT2 22 26 SUPCK   CIOWT3 22 29 SUPCK   
CIRERR 12  2 SUPCK   CIRLST 11  4 SUPCK   CJ     26  8 SUPCK   CKCHG   8 15 SUPCK   
CKCHR  22  5 SUPCK   CKCHR1 22  8 SUPCK   CKCHR2 22 11 SUPCK   CKDIST  5 17 SUPCK   
CKERR  12 24 SUPCK   CKGAN   2  7 SUPCK   CKHOME 16 11 SUPCK   CKLF    8 13 SUPCK   
CKMAIL 16 13 SUPCK   CKNAME  3 26 SUPCK   CKPAS   4 33 SUPCK   CKPAS1  4 36 SUPCK   
CKPAS2  4 38 SUPCK   CKSYS   7 13 SUPCK   CKSYS1  7 19 SUPCK   CKSYS4  7 14 SUPCK   
CKTIME 10  8 SUPCK   CLBUF   8 27 SUPCK   CLEAR  18 29 SUPCK   CLRBUF 16 33 SUPCK   
CLRMSG 18 23 SUPCK   CMSG    9 25 SUPCK   CNTRLA  3 14 SUPCK   COM     1 28 SUPCK   
COMERR 15 30 SUPCK   COMPTR  2 39 SUPCK   CONFN  26 14 SUPCK   CONPTR 26 13 SUPCK   
COUNT  11 35 SUPCK   COUNT1 11 36 SUPCK   COUNT2 11 37 SUPCK   CR     26  9 SUPCK   
CTA    26 22 SUPCK   CTOUT  26 18 SUPCK   CTT    26 23 SUPCK   DATE    9 36 SUPCK   
DATPTR  9 39 SUPCK   DATSTR 10  1 SUPCK   DISAR5 12  8 SUPCK   DIST    4 12 SUPCK   
DONE   18 35 SUPCK   DONE1  18 39 SUPCK   DSNAME  2 34 SUPCK   ECO     6 19 SUPCK   
EMSG    9 28 SUPCK   ENDCHR 23 14 SUPCK   ENDSLV 20 15 SUPCK   ERR1   23 26 SUPCK   
ERR10  24 23 SUPCK   ERR11  24 27 SUPCK   ERR12  24 31 SUPCK   ERR13  24 35 SUPCK   
ERR14  24 39 SUPCK   ERR15  25  4 SUPCK   ERR16  25  8 SUPCK   ERR17  25 12 SUPCK   
ERR18  25 16 SUPCK   ERR19  25 20 SUPCK   ERR2   23 30 SUPCK   ERR20  25 38 SUPCK   
ERR21  26  2 SUPCK   ERR22  26  5 SUPCK   ERR3   23 34 SUPCK   ERR4   23 38 SUPCK   
ERR5   24  3 SUPCK   ERR6   24  7 SUPCK   ERR7   24 11 SUPCK   ERR8   24 15 SUPCK   
ERR9   24 19 SUPCK   ERRCOD 15  7 SUPCK   ERRLOG 11 11 SUPCK   ERRNAM  4 14 SUPCK   
ERRPAS  5 12 SUPCK   ERRSYS  7 39 SUPCK   ERRTAB  7  7 SUPCK   ESTARS 16  5 SUPCK   
ETABLE  6 38 SUPCK   GAN     2 13 SUPCK   GBLD1   5 28 SUPCK   GCIERR  4 18 SUPCK   
GETBLD  5 26 SUPCK   GETDAT 21 38 SUPCK   GETMSG 16 21 SUPCK   GETREQ 18 20 SUPCK   
GETSY1  2  1 SUPCK   GETSY2  2  3 SUPCK   GETSY3  2  4 SUPCK   GETSYS  1 39 SUPCK   
GSP1   16  2 SUPCK   GSP2   16  3 SUPCK   GSPOOL 15 36 SUPCK   GTMSG1 16 23 SUPCK   
GTMSG2 16 25 SUPCK   GTMSG3 16 27 SUPCK   GTMSG4 16 29 SUPCK   HOMSYS  2 14 SUPCK   
INDEX  14  3 SUPCK   INITA  19 30 SUPCK   INITB  19 34 SUPCK   INITC  19 38 SUPCK   
INITE  20  3 SUPCK   INITS  20  7 SUPCK   INPUT   3 17 SUPCK   INVAL  10  3 SUPCK   
IOWAIT 18  4 SUPCK   IPORT  11 33 SUPCK   LISS1   8 30 SUPCK   LISSYS  8 28 SUPCK   
M1     26 26 SUPCK   M10    26 35 SUPCK   M11    26 36 SUPCK   M13    26 37 SUPCK   
M14    26 38 SUPCK   M15    26 39 SUPCK   M16    27  1 SUPCK   M17    27  2 SUPCK   
M18    27  3 SUPCK   M19    27  4 SUPCK   M2     26 27 SUPCK   M20    27  5 SUPCK   
M22    27  6 SUPCK   M23    27  7 SUPCK   M24    27  8 SUPCK   M25    27  9 SUPCK   
M26    27 10 SUPCK   M27    27 11 SUPCK   M28    27 12 SUPCK   M29    27 13 SUPCK   
M3     26 28 SUPCK   M30    27 14 SUPCK   M31    27 15 SUPCK   M32    27 16 SUPCK   
M33    27 17 SUPCK   M34    27 18 SUPCK   M35    27 19 SUPCK   M36    27 20 SUPCK   
M37    27 21 SUPCK   M38    27 22 SUPCK   M39    27 23 SUPCK   M4     26 29 SUPCK   
M40    27 24 SUPCK   M41    27 25 SUPCK   M42    27 26 SUPCK   M43    27 27 SUPCK   
M44    27 28 SUPCK   M45    27 29 SUPCK   M46    27 30 SUPCK   M47    27 31 SUPCK   
M48    27 32 SUPCK   M49    27 33 SUPCK   M5     26 30 SUPCK   M50    27 34 SUPCK   
M51    27 35 SUPCK   M52    27 36 SUPCK   M53    27 37 SUPCK   M54    27 38 SUPCK   
M55    27 39 SUPCK   M56    28  1 SUPCK   M57    28  2 SUPCK   M58    28  3 SUPCK   
M59    28  4 SUPCK   M6     26 31 SUPCK   M60    28  5 SUPCK   M61    28  6 SUPCK   
M62    28  7 SUPCK   M63    28  8 SUPCK   M64    28  9 SUPCK   M65    28 10 SUPCK   
M66    28 11 SUPCK   M67    28 12 SUPCK   M68    28 13 SUPCK   M69    28 14 SUPCK   
M7     26 32 SUPCK   M70    28 15 SUPCK   M71    28 16 SUPCK   M72    28 17 SUPCK   
M73    28 18 SUPCK   M74    28 19 SUPCK   M75    28 20 SUPCK   M76    28 21 SUPCK   
M77    28 22 SUPCK   M78    28 23 SUPCK   M79    28 24 SUPCK   M8     26 33 SUPCK   
M80    28 25 SUPCK   M81    28 26 SUPCK   M82    28 27 SUPCK   M83    28 29 SUPCK   
M84    28 31 SUPCK   M85    28 32 SUPCK   M86    28 33 SUPCK   M87    28 34 SUPCK   
M88    28 35 SUPCK   M89    28 36 SUPCK   M9     26 34 SUPCK   M90    28 37 SUPCK   
MAIL   16 37 SUPCK   MAILER 19 16 SUPCK   MAISTR 14  5 SUPCK   MKCHG   8 37 SUPCK   
MSG     1 19 SUPCK   MSGA    1 24 SUPCK   MSGX    1 25 SUPCK   NAMLEN  3  3 SUPCK   
NAMPTR  3  2 SUPCK   NAMSTR  3  1 SUPCK   NOCHAN 11 29 SUPCK   NOLOG  11 25 SUPCK   
NOMAIL 16 39 SUPCK   NOSUP   9  3 SUPCK   NOSUP1  9  4 SUPCK   NOTVAL  4 24 SUPCK   
NXTREQ 22 34 SUPCK   OFF8   13 19 SUPCK   OKMSG   5 21 SUPCK   ON8    13 15 SUPCK   
OPNERR  2 26 SUPCK   OPNPAS  2 19 SUPCK   OPNPTR  4  9 SUPCK   OPNSTR  4 10 SUPCK   
OPORT  11 34 SUPCK   OUTDAT 20 22 SUPCK   OUTSY1 14 31 SUPCK   OUTSYS 14 29 SUPCK   
PASFN   2 24 SUPCK   PASPTR  2 22 SUPCK   PASSTR  2 23 SUPCK   PAUSE  10 26 SUPCK   
PCODE  14  7 SUPCK   PFNO    5 38 SUPCK   PSPSTR 14  6 SUPCK   PSWD    4 30 SUPCK   
PUTZRO  7 35 SUPCK   PWPTR   5  9 SUPCK   PWSTR   5 10 SUPCK   PZ1     7 36 SUPCK   
RDSTAT  4 22 SUPCK   RECEV  14 36 SUPCK   RECMAI 17 18 SUPCK   RECORD 25 30 SUPCK   
REINIT 19 22 SUPCK   REMSYS  7 23 SUPCK   RESET  18 15 SUPCK   RETRY  19  6 SUPCK   
RING   14 24 SUPCK   RING1  14 25 SUPCK   SCO     6 23 SUPCK   SCPERR  2 29 SUPCK   
SEND   15  2 SUPCK   SIC     1  7 SUPCK   SKL     1  6 SUPCK   SMSG    9 31 SUPCK   
SNE     1  5 SUPCK   SPLEND 21 19 SUPCK   SPLERR 21 23 SUPCK   SPLH1  21  4 SUPCK   
SPLH2  21 17 SUPCK   SPLHRD 20 39 SUPCK   SPLMSG 21 29 SUPCK   STABLE  7  4 SUPCK   
START   1 33 SUPCK   STRING 26 12 SUPCK   SUPBIO  9  8 SUPCK   SUPPTR  9 10 SUPCK   
SUPSCP  9  6 SUPCK   SUPSLV 14  4 SUPCK   SYNC   15 12 SUPCK   SYSFIX  2 11 SUPCK   
SYSLOG 23 19 SUPCK   SYSLTR  2 15 SUPCK   SYSNUM  7 31 SUPCK   SYSPTR  7 29 SUPCK   
SYSSTR  7 30 SUPCK   SYSTAB  7  9 SUPCK   SYSTEM 11  3 SUPCK   T1     16  6 SUPCK   
TABLEA  6 29 SUPCK   TABLEB  6 31 SUPCK   TABLEC  6 33 SUPCK   TEMP    3  4 SUPCK   
TERCH   5 37 SUPCK   TERM   15 21 SUPCK   TERMWD  5  8 SUPCK   TIMOUT 14 11 SUPCK   
TIMPTR 20 25 SUPCK   TIMSTR 20 26 SUPCK   TMP1    3 34 SUPCK   TMP10   4  4 SUPCK   
TMP11   4  5 SUPCK   TMP12   4  6 SUPCK   TMP13   4  7 SUPCK   TMP2    3 35 SUPCK   
TMP3    3 36 SUPCK   TMP4    3 37 SUPCK   TMP5    3 38 SUPCK   TMP6    3 39 SUPCK   
TMP7    4  1 SUPCK   TMP8    4  2 SUPCK   TMP9    4  3 SUPCK   TOUT    1 29 SUPCK   
TSTARS 16  4 SUPCK   TTYBUF  7 17 SUPCK   TTYIN   3  8 SUPCK   TTYIN1  3 10 SUPCK   
TTYPTR  7 16 SUPCK   TYMERR 10 21 SUPCK   TYMFLG 10 24 SUPCK   TYMOUT 12 12 SUPCK   
TYMPTR 10 25 SUPCK   VALSYS  9 14 SUPCK   VERIFY 15 17 SUPCK   VERMSG  1 16 SUPCK   
WHEN   26 11 SUPCK   WTGFLG 19  7 SUPCK   X       1 10 SUPCK   YN      8 22 SUPCK   
YN1     8 23 SUPCK   YN2     8 24 SUPCK   YN3     8 26 SUPCK   ZAPCIR 12 18 SUPCK   

SUPCHECK ** FICHE/FRAME BREAK *****
BUILDSUPCHECK
I'VE GOT THE SYMBOLICS OF VERSION 15 OF SUPCHECK TO WORK.
THE PROBLEM WITH THE PROGRAM IS THAT IT MUST BE RUN IN THE
UTIL DIRECTORY AND VERY IMPORTANTLY IT MUST BE NAMED
'SUPCHECK'.  I AM GOING TO SHOW YOU HOW TO BUILD THE
PROGRAM FROM THE SYMBOLICS.

;THE SYMBOLIC FILE IS /CUPSUP/

;FIRST YOU CALL NARP

NARP
SOURCE FILE:  /CUPSUP/
OBJECT FILE:  /./              ;THIS CAN BE ANY NAME
 NEW FILE
TEXT FILE:  TER

SUPCK IDENT                            ;PRINTED BY NARP


23 SEC   0 ERR   6225 (3221) WRD    (S:376,O:4,L:133,M:18,U:414)


-XDDT

;T  /./                        ;INPUT THE FILE INTO XDDT

IDENT. SUPCK   6465            ;PRINTED BY XDDT (6465 IS THE LAST LOCATION)

%Q                             ;THIS IS HOW YOU QUIT.

-SAVE 3 TO 6465 ON SUPCHECK    ;USE THE MONITOR SAVE COMMAND TO SAVE CORE IMAGE
 NEW FILE <LF>                 ;USE A LINE FEED SO YOU CAN ENTER A STARTING LOC
STARTING LOCATION 273          ;YOU HAVE TO ENTER THE 273 AS THE STARTING LOC

-

;AND THERE YOU HAVE IT.

 D §