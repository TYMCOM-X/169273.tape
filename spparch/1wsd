1WSD ** FICHE/FRAME BREAK *****
SS9403WSD
WSD    IDENT  06-05-75 EXEC 96 AND UP ONLY WITH MDSI, XFORTRAN, AND
*              P. 22 OF EXEC WHICH COMES FROM EXECD1.
*VERSION 94.03 RUNS 1WSD FROM LOC 2 OR 4 ONLY AND 2WSD FROM LOC 12 ONLY
*VERSION 94.02 EXPECTS THE SUMSUB FILE TO BE IN *6UTIL OR *6WSD2
* FOR 1WSD OR 2WSD RESPECTIVELY, IN CASE THERE IS A DIFFERENT EXEC
* ON LOC 12.  NOTE:  1SETMAP MUST START UP 2WSD WHEN SYSTEM IS
* BROUGHT UP OFF LOC 12, OR ELSE THE PROPER EXECD1 CANNOT BE WRITTEN.

2WSD   EQU 0 (ASSEMBLE 2WSD IF =1)
       IF 2WSD
       REM ASSEMBLING 2WSD TO REFERENCE FILES IN *6WSD2
       ELSE
       REM ASSEMBLING 1WSD TO REFERENCE FILES IN *6UTIL
       ENDF
MSG    OPD  100B5,2

SUBSYS MACRO A,G,1;CHAR NCHR A(1)
       LDP *+6; IF A(5); BRM SET; ELSE; BRM WRITE; ENDF
       DATA A(2)B,A(3)B,A(4)B; BRU G(1)
       DATA 3**+6,3**+CHAR+11; DATA 4040B; IF 2WSD; ASC '6WSD2)'; ELSE
       ASC '6UTIL)'; ENDF
       ASC 'A(1)'
G(1)   BSS 0; ENDM

       BRU START; BRS 10
       ASC  'S94.03'
       ASC  'KAHRS'
       ASC  'HARDY'
       ASC  'BLOOD'
       ASC     %C'KY'R%

START  LDA TV100; STA 100B; BRS 39; SKA =1B7; BRU START1
       MSG MSTAT; BRS 10
BEGIN  LDA =MDSI; LDX =31; BRS 7

       TCO =155B
       LDA MDSI; ETR =77B; SKE =1; BRU SER0

*      MDSI MACHINE - SERIAL 1

       MSG MMDSI; MSG MVERS
       BRM EXECD1
       SUBSYS EXECD2,170000,0,0,-1
       SUBSYS XDDT,353637,40000000,0,-1
       SUBSYS EDITOR,262730,0,0,-1
       SUBSYS COMMON,323334,0,100,-1
       SUBSYS SBASIC,10203,4050607,100,-1
       SUBSYS BFOR1,313233,34250000,0,-1
       SUBSYS BFOR2,10203,4050600,200,-1
       SUBSYS BFOR3,71011,12131400,200,-1
       SUBSYS XFOR1,240000,0,0,-1
       SUBSYS XFOR2,100000,0,100,-1
       SUBSYS XFOR3,112627,71727374,100,1
       BRU FIN

*      NORMAL MACHINE - SERIAL 0

SER0   MSG MVERS
       BRM EXECD1
       SUBSYS EXECD2,170000,0,0,-1
       SUBSYS DDT,414200,0,0,-1
       SUBSYS XDDT,353637,40000000,0,-1
       SUBSYS EDITOR,262730,0,0,-1
       SUBSYS COMMON,323334,0,100,-1
       SUBSYS SFOR3,454600,0,100,-1
       SUBSYS SBASIC,10203,4050607,100,-1
       SUBSYS SFOR1,353637,40000000,100,-1
       SUBSYS SFOR2,414243,44000000,100,-1
       SUBSYS BFOR1,313233,34250000,0,-1
       SUBSYS BFOR2,10203,4050600,200,-1
       SUBSYS BFOR3,71011,12131400,200,-1
       SUBSYS CAL,121314,15160000,100,-1
       SUBSYS FOS,505152,0,100,1
       SUBSYS F2OS,656667,0,100,1
       SUBSYS FTC,303100,0,100,-1
       SUBSYS F2C,636400,0,100,-1
       SUBSYS NBASIC,172021,22232425,100,-1
       SUBSYS NBASIC1,565760,61000000,100,-1
       SUBSYS XFOR1,240000,0,0,-1
       SUBSYS XFOR2,100000,0,100,-1
       SUBSYS XFOR3,112627,71727374,100,1

FIN    BRS 43; ETR =77B6; CLB; BRS 44; SKN BADSW2; BRU *+2
       MSG EXMSG; SKN BADSW; BRU *+2; BRU BELL
       SKN BADSW1; BRU *+2; BRU BELL2; MSG MEOJ; BRS 10

* READ AND RELABEL ONE SUBSYSTEM

WRITE  ZRO; STP NAME; TCO =155B; ADD =8; LDX =1; BRS 35
       LDA NAME+1; SUB NAME; SUB =18; CAX; TCO =0; BRX *-1
       MIN WRITE; LDP NAME; BRS 64 (OPEN FILE); BRU BADFIL
       STA FILE; CBA; SKE =1 (GO FILE?); BRU WFT; LDA* WRITE
       ETR =777777B; STA BYTE; MIN WRITE; LDA* WRITE
       STA BYTES; MIN WRITE; LDA =7; STA BLKCNT; LDP BYTE

RELPAG EQU *  RELABEL PAGE INTO CORE
       ETR =77B; SKG =0; BRU CLEAR; STA CPAGE; ADD* WRITE
       BRS 3 (MAKE POINTER INDIRECT AND READ-WRITE)
       STA* PAGTBL; MIN PAGTBL; SUB CPAGE; ADM BYTE
       SKN ROSW; BRU CLEAR; LDA CPAGE; ADD* WRITE
       BRS 57 (MAKE PAGE VISIBLE TO USERS)

CLEAR  EQU *  CLEAR PAGES FROM PMT AND BIO FILE INTO CORE
       LDP BYTE; LCY 6; STP BYTE; SKR BLKCNT; BRU RELPAG
       CLA; STA CKSUM; WIO FILE; STA FIRST; BRM WRDSUM
       WIO FILE; STA LAST; BRM WRDSUM; WIO FILE; BRM WRDSUM
       BRS 43; ETR =77B6; MRG BYTE; LDB BYTES; BRS 44
       LDA FIRST; ETR =3777B; MRG =4000B (PAGE 1); CAX
       STA SRTADR; LDA LAST; SUB FIRST; STA WRDCNT; ADD =1
       BIO FILE; BRU IOERR; LDA CKSUM; CLB; LDX SRTADR

SUMPAG EQU *  CHECKSUM PAGE
       LCY 7; ADD 0,2; STB PAGE; ADD PAGE; CLB; EAX 1,2
       SKR WRDCNT; BRU SUMPAG; STA CKSUM; LDA =TABLE
       STA PAGTBL

ZERO   CLA; XMA* PAGTBL; SKG =0; BRU NEXT; BRS 121 (KILL PAGE)
       MIN PAGTBL; BRU ZERO
NEXT   LDA =TABLE; STA PAGTBL; LDA CKSUM; CLB; LDX SRTADR

FINISH WIO FILE; SKN FILE; BRU *+2; BRU END; BRM WRDSUM
       BRU FINISH
END    LDA FILE; BRS 20 (CLOSE FILE); LDA CKSUM; LDB =8
       LDX =1; BRS 36 (TYPE CHECKSUM); SKN BADSW1; BRM VCKSUM
       LDP BYTE; RCY 12
       STP BYTE; CLA; STA ROSW; BRR WRITE

SET    ZRO;* SET ROSW NEGATIVE
       LDX =-1; STX ROSW; LDX SET; STX WRITE; BRU WRITE+1

EXMSG  ASC '$$SOMETHING IS WRONG WITH EXECD1.  REVENUE WILL BE'
       ASC '$LOST IF CORRECTIVE ACTION IS NOT TAKEN.$/'
EXECD1 ZRO;*   CHECK TO SEE IF PG 22 HAS BEEN WRITTEN AND WRITE IF NOT
       BRS 22; BRS 46; LDA =22B; BRS 80; SKA =4B7; BRU XD1A (OK, WAS RO)
       BRS 47; BRS 23; BRR EXECD1
XD1A   LDA =22B+4B7; BRS 80; BRS 47; BRS 23
       SUBSYS EXECD1,220000,0,0,-1
       SKN BADSW; BRU XD1C
       SKR BADSW2; BRU *-1; BRR EXECD1
XD1C   BRS 22; BRS 46
       LDA =-1; LDX =3; BRS 136 (SET MASTER NEG.)
       LDA =22B; BRS 80; BRS 47; BRS 23; BRR EXECD1

WRDSUM ZRO;*  WORD SUM
       XMA CKSUM; CLB; LCY 7; ADD CKSUM; STB CKSUM
       ADM CKSUM; BRR WRDSUM

BADFIL EQU *  BAD INPUT FILE
       CXA; ETR =777B; SKE =12B; BRU *+2; BRU MISS; SKE =51B
       BRU *+2; BRU BAD; MSG FTROUB
BFIL1  SKR BADSW; BRU *-1; LDA =2; ADM WRITE; BRR WRITE
MISS   MSG MBADFL; BRU BFIL1
BAD    MSG BFILE; BRU BFIL1
WFT    MSG MWFT; LDA FILE; BRS 20; BRU BFIL1
BELL   LDX =-1; BRS 11; MSG DOSOME
BELL1  LDA =147B; CIO =1; LDA =1000; BRS 81; BRU BELL1

IOERR  MSG MIOERR; BRS 147; SKR BADSW; BRU *-1; BRR WRITE

MSGOUT EAX* 0; LDA =1; XXA; ETR =37777B; LDB =-1; BRS 34
       BRR 0

NAME   BSS 2   FILE NAME
FILE   ZRO 0   FILE NUMBER
FIRST  ZRO 0   FIRST LOCATION OF FILE
LAST   ZRO 0   LAST LOCATION OF FILE
BYTE   ZRO 0   FIRST RELABELING BYTE
BYTES  BSS 2   LAST RELABELING BYTE
SRTADR ZRO 0   STARTING ADDRESS IN CORE
CPAGE  ZRO 0   CURRENT PAGE
PAGE   ZRO 0   PAGE SUM
MDSI   ZRO 0 (SERIAL NO. FOR CUSTOM MACHINES)
ROSW   ZRO 0   BRS 57 SWITCH
TV100  BRU MSGOUT
CKSUM  ZRO 0   FILE CHECKSUM
WRDCNT ZRO 0   WORD COUNT
BLKCNT ZRO 0   BLOCK COUNT
PAGTBL DATA TABLE
TABLE  BSS 8   PAGE TABLE

MSTAT  ASC  '$MIN. OF OPER STATUS REQUIRED.$/'
MMDSI  ASC     'MDSI /'
MVERS   ASC  'WSD VERSION S94.03$/'
MIOERR ASC  '$I-O ERROR./'
MBADFL ASC  'MISSING./'
MWFT   ASC  '$WRONG FILE TYPE./'
MEOJ   ASC  '$$END JOB.$/'
FTROUB ASC '$FILE TROUBLE./'
BFILE  ASC '$FILE BAD./'
DOSOME ASC '$$CHECK PRINTOUT ABOVE.'
       ASC '$$SOMETHING IS WRONG WITH A SUBSYSTEM FILE.'
       ASC '$TAKE CORRECTIVE ACTION BEFORE ANSWERING LINES.'
       ASC '$$TYPE ALTMODE TO STOP BELLS$$/'
BADSW  ZRO 0 (SUBSYSTEM FILE MISSING, BAD FILE, ETC.)
BADSW1 ZRO 0 (CHECKSUM OR CHECKSUM FILE MISSING)
BADSW2 ZRO 0 (TROUBLE WITH EXECD1)
ADRSMT EQU 1
DISCN EQU ADRSMT+53B
START1 LDA =ADRSMT; LDX =19; BRS 7 (READ MISC MONITOR TABLE)
       LDA DISCN; IF 2WSD; SKE =12B; ELSE
       SKE =4; SNE =2; BRU *+2; ENDF; BRU GOOF
       LDP FILPTR; BRS 64; BRU NOSUBF; STA FILE
       LDA =4000B; LDX =4000B; BIO FILE; NOP; STA SUBE
       LDA FILE; SKA =4B5; BRU SUBBAD; SKA =2B5; BRU *+2; BRU SUBBAD
       LDA FILE; BRS 20; BRS 43; ETR =77B4; STA CKREL; BRS 43
       ETR =77B6; BRS 44; BRU BEGIN
GOOF   MSG WRGDSC; BRS 10
       IF 2WSD
WRGDSC ASC '$ATTEMPT TO RUN 2WSD FROM DISC LOC. OTHER THAN 12./'
       ELSE
WRGDSC ASC '$ATTEMPT TO RUN 1WSD FROM DISC LOC. OTHER THAN 2 OR 4./'
       ENDF
NOSUBF CXA; ETR =777B; SKE =1; BRU *+2; BRU SUBBUS; SKE =12B; BRU *+2
       BRU SUBMIS; SKE =51B; BRU SUBTRO; BRU SUBBAD
SUBBUS MSG SUB1; BRS 10
SUBMIS MSG SUB2; MSG SUB3; SKR BADSW1; BRU *-1; BRU BEGIN
SUBBAD MSG SUB4; BRU SUBMIS+1
SUBTRO MSG SUB5; BRU SUBMIS+1
SUB1   ASC '$SUBSYSTEM CHECKSUM FILE BUSY. PLEASE RERUN WSD$/'
SUB2   ASC '$SUBSYSTEM CHECKSUM FILE MISSING/'
SUB3   ASC '$CHECKSUMS TYPED BELOW HAVE NOT BEEN VERIFIED/'
SUB4   ASC '$SUBSYSTEM CHECKSUM FILE IS BAD/'
SUB5   ASC '$TROUBLE WITH THE SUBSYSTEM CHECKSUM FILE/'
SUB6   ASC '$ENTRY FOR THE ABOVE FILE NOT'
       ASC '$FOUND IN THE SUBSYSTEM CHECKSUM'
       ASC '$FILE.  THIS CHECKSUM AS WELL AS THE/'
VCKSUM ZRO; BRS 43; STP NREL; ETR =77007777B; MRG CKREL; BRS 44
       LDP NAME; ADD =8; STP NAME1
       LDA =SUB
VCKS1  STA SPTR; STA SPTR1; SKG SUBE; BRU *+2; BRU VCKSER; CLA
       STA CHARCT; LDX =-6
VCKS2  STX LOOPX; LDB* SPTR1; LDX =-3
VCKS3  CLA; LSH 8; SKA =37B; BRU *+2; BRU VCKS4
       MIN CHARCT; BRX VCKS3; MIN SPTR1; LDX LOOPX; BRX VCKS2; HLT
VCKS4  LDA SPTR; MUL =3; LSH 23; SUB =1; CAB; ADD CHARCT; XAB; SKSE NAME1
       BRU *+2; BRU VCKS5; LDA SPTR; ADD =7; BRU VCKS1
VCKS5  LDA SPTR; ADD =6; STA SPTR; LDA* SPTR; SKE CKSUM; BRU *+3
       BRM SNREL; BRR VCKSUM; MSG BCKSUM; SKR BADSW; BRU *-1; BRM SNREL
       BRR VCKSUM
BCKSUM ASC '   CHECKSUM BAD/'
SUBE   ZRO
FILPTR DATA 3*F-1,3*F+13
F      DATA 2020026B (@6
       IF 2WSD
       ASC 'WSD2)SUMSUB'
       ELSE
       ASC 'UTIL)SUMSUB'
       ENDF
VCKSER MSG SUB6; MSG SUB3; SKR BADSW1; BRU *-1; BRR VCKSUM
BELL2  MSG MBELL2; LDX =-1; BRS 11; BRU BELL1
MBELL2 ASC '$$PLEASE READ ABOVE PRINTOUT'
       ASC '$NOTIFY YOUR SUPERVISOR'
       ASC '$$TYPE ALTMODE TO STOP BELLS$$/'
SPTR   ZRO
SPTR1  ZRO
CHARCT ZRO
LOOPX  ZRO
SUB    EQU 4000B
SNREL  ZRO; LDP NREL; BRS 44; BRR SNREL
NREL   BSS 2
CKREL  ZRO
NAME1  BSS 2
       END
1WSD ** FICHE/FRAME BREAK *****
SS9403WSD SYMSORT

SYM.  PG.LN. IDENT.

2WSD    1  9 WSD     ADRSMT  6  2 WSD     BAD     4 36 WSD     
BADFIL  4 31 WSD     BADSW   5 38 WSD     BADSW1  5 39 WSD     BADSW2  6  1 WSD     
BCKSUM  7  6 WSD     BEGIN   1 34 WSD     BELL    4 38 WSD     BELL1   4 39 WSD     
BELL2   7 16 WSD     BFIL1   4 34 WSD     BFILE   5 33 WSD     BLKCNT  5 21 WSD     
BYTE    5 11 WSD     BYTES   5 12 WSD     CHARCT  7 22 WSD     CKREL   7 27 WSD     
CKSUM   5 19 WSD     CLEAR   3 25 WSD     CPAGE   5 14 WSD     DISCN   6  3 WSD     
DOSOME  5 34 WSD     END     4  6 WSD     EXECD1  4 16 WSD     EXMSG   4 14 WSD     
F       7  9 WSD     FILE    5  8 WSD     FILPTR  7  8 WSD     FIN     3  5 WSD     
FINISH  4  4 WSD     FIRST   5  9 WSD     FTROUB  5 32 WSD     G(1)    1 23 WSD     
GOOF    6 12 WSD     IOERR   5  2 WSD     LAST    5 10 WSD     LOOPX   7 23 WSD     
MBADFL  5 29 WSD     MBELL2  7 17 WSD     MDSI    5 16 WSD     MEOJ    5 31 WSD     
MIOERR  5 28 WSD     MISS    4 35 WSD     MMDSI   5 26 WSD     MSG     1 15 WSD     
MSGOUT  5  4 WSD     MSTAT   5 25 WSD     MVERS   5 27 WSD     MWFT    5 30 WSD     
NAME    5  7 WSD     NAME1   7 28 WSD     NEXT    4  2 WSD     NOSUBF  6 18 WSD     
NREL    7 26 WSD     PAGE    5 15 WSD     PAGTBL  5 22 WSD     RELPAG  3 18 WSD     
ROSW    5 17 WSD     SER0    2 19 WSD     SET     4 11 WSD     SNREL   7 25 WSD     
SPTR    7 20 WSD     SPTR1   7 21 WSD     SRTADR  5 13 WSD     START   1 32 WSD     
START1  6  4 WSD     SUB     7 24 WSD     SUB1    6 24 WSD     SUB2    6 25 WSD     
SUB3    6 26 WSD     SUB4    6 27 WSD     SUB5    6 28 WSD     SUB6    6 29 WSD     
SUBBAD  6 22 WSD     SUBBUS  6 20 WSD     SUBE    7  7 WSD     SUBMIS  6 21 WSD     
SUBSYS  1 17 WSD     SUBTRO  6 23 WSD     SUMPAG  3 34 WSD     TABLE   5 23 WSD     
TV100   5 18 WSD     VCKS1   6 35 WSD     VCKS2   6 37 WSD     VCKS3   6 38 WSD     
VCKS4   7  1 WSD     VCKS5   7  3 WSD     VCKSER  7 15 WSD     VCKSUM  6 32 WSD     
WFT     4 37 WSD     WRDCNT  5 20 WSD     WRDSUM  4 27 WSD     WRGDSC  6 16 WSD     
WRGDSC  6 14 WSD     WRITE   3 11 WSD     XD1A    4 19 WSD     XD1C    4 23 WSD     
ZERO    3 39 WSD     
1WSD ** FICHE/FRAME BREAK *****
MAKEWSD
EDI
R SS9403WSD
:2WSD:S
 0 1N
./
WTEMP1

:2WSD:S
 1 0N
./
W TEMP2

Q
NARP
TEMP1
BTEMP1


NARP
TEMP2
BTEMP2


RES
XDDT
;TBTEMP1
;U%F
SAVE 240 T 2501 ON G1WSD9403

240
RES
XDDT
;TBTEMP2
;U%F
SAVE 240 T 2474 ON G2WSD9403

240
"

       1WSD  2WSD  VERSION 94.03

"
CHECKSUM
.
(SS940UTIL)SS9403WSD-SYMBOLICS
(SS940UTIL)G1WSD9403-GO-FILE-1WSD
(SS940UTIL)G2WSD9403-GO-FILE-2WSD
(SS940UTIL)WSDDOC-DOC-FILE
(SS940UTIL)MAKEWSD-BUILD-FILE
.
"

"
COM TEL
1WSD ** FICHE/FRAME BREAK *****
WSDDOC
                               WSD  VERSION 94.03

CHANGES:

1WSD AND 2WSD NOW ONLY LOOK FOR (*6UTIL)SUMSUB AND (*6WSD2)SUMSUB
RESPECTIVELY.

1WSD CHECKS FOR SYSTEM TO BE UP FROM DISC 2 FOR DISC 4 OTHERWISE
WILL NOT RUN
2WSD CHECKS FOR SYSTEM TO BE UP FROM DISC 12 OTHERWISE WILL NOT RUN
  