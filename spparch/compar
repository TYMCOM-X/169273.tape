COMPARE ** FICHE/FRAME BREAK *****
SCOMPARE3
SRCCOM IDENT

ARRAY  MACRO NAME
XQ     NARG
       RPT (YQ=1,XQ-1)
NAME(YQ) BSS NAME(XQ)
       ENDR; ENDM

VARB   MACRO NAME
XQ     NARG
       RPT (YQ=1,XQ)
NAME(YQ) BSS 1
       ENDR; ENDM

MSG    MACRO MES
       LDA =MES(1); LDX =1; LDB =-1; BRS 34
       ENDM

MSGP   MACRO MES
       LDA =MES(1); LDX =1; LDB =-1; SKN NPRMPT; BRS 34
       ENDM

MSG1   MACRO MES
       LDA =MES(1); LDB =-1; LDX OFL; BRS 34
       ENDM

SALL   MACRO CELL
XQ     NARG
       RPT (YQ=1,XQ)
       RPT (ZQ=0,NFIL-1)
       STA CELL(YQ)+ZQ
       ENDR; ENDR; ENDM

SETAC0 MACRO D
       LDA 2,2; STA AC.&D(1).&0; LDA 3,2; STA AC.&D(1).&1; ENDM

SETACM MACRO; SETAC0 M; ENDM

CMPAC0 MACRO DADR
       LDA 2,2; SKE AC.&DADR(1).&0; DADR(2)
       LDA 3,2; SKE AC.&DADR(1).&1; DADR(2); ENDM

CMPAC  MACRO ADR; CMPAC0 M,(BRU ADR(1)); ENDM

GET    MACRO A
       LDA A(2); LDX A(1); BRM RTRV1
XQ     NARG
       IF XQ=3; BRU A(3); ENDF; ENDM

DUMP   MACRO ARGS
       LDA ARGS(2); LDX ARGS(1); BRM DUMPIT; ENDM

STRARN MACRO D
       MSG1 FILE.&D(1).&S; LDX =D(1)-1; BRM LINPR; ENDM

DFMLMT EQU 1
NFIL   EQU 2   NUMBER OF FILES INVOLVED
BUFSIZ EQU 67

       RPT (XQ=1,NFIL)
SMBF($XQ) BSS 256
       ENDR

SMBFP  RPT (XQ=1,NFIL)
       DATA SMBF($XQ)
       ENDR

       ARRAY CRCNT,CPSM,CWSM,SMBPT,SBIOC,EFL,NFIL

       VARB BIOFIL,SIMSB,SIMSX,SIMR,SIMSA

*      SIMCIO SIMULATES A CIO WITH BIO. CALL
*              BRM SIMCIO
*              EOF RETURN
*              NORMAL RETURN
*      ON CALL, X HAS INDEX TO FILE TO HAVE A CIO DONE ON IT.
*      ROUTINE AUTOMATICALLY UPDATES CPOS

SIMCIO ZRO; SKN EOF,2; BRR SIMCIO
SIMCI2 SKR CRCNT,2; BRU SIMGCH
       LDA SBIOC,2; ADD =256*3; XMA EFL,2; SKG EFL,2; BRU SIMSEF
       XMA EFL,2; STA SBIOC,2
       LDA FIL,2; STA BIOFIL; COPY A; STA CPSM,2
       STX SIMSX; LDA SMBFP,2; STA SMBPT,2; LDX =256; COPY AX,XA
       BIO BIOFIL; NOP; LDX SIMSX; LDA EFL,2; SUB SBIOC,2
       SKG =256*3; BRU *+2; LDA =256*3; STA CRCNT,2; BRU SIMCI2

SIMSEF XMA EFL,2; MIN EOF,2; BRR SIMCIO

SIMGCH STB SIMSB; MIN SIMCIO; SKR CPSM,2; BRU SIMPKW
       LDB* SMBPT,2; MIN SMBPT,2; LDA =2; STA CPSM,2; BRU *+2
SIMPKW LDB CWSM,2; COPY A; LSH 8; STB CWSM,2
       LDB SIMSB; MIN CPOS,2; BRR SIMCIO

*      SIMSCP SIMULATEA AN SCP. X HAS INDEX TO FILE NUMBER. A HAS
*      NEW POSITION. UPDATES EOF AND CPOS.
*              BRM SIMSCP
*              EOF RETURN
*              NORMAL RETURN

SIMSCF XMA EFL,2; MIN EOF,2; BRR SIMSCP

SIMSCP ZRO; XMA EFL,2; SKG EFL,2; BRU SIMSCF; XMA EFL,2; MIN SIMSCP
       SKE CPOS,2; BRU *+2; BRR SIMSCP; STA CPOS,2
       STX SIMSX; STA SIMSA; STB SIMSB; COPY B; RSH 23; DIV =256*3
       STB SIMR; MUL =256*3; LSH 23; SKE SBIOC,2; BRU *+2; BRU SIMSCD
       LDB FIL,2; STB BIOFIL; STA SBIOC,2
       SCP BIOFIL; BRU LBIG; LDA =256; LDX SMBFP,2; BIO BIOFIL; NOP
       LDX SIMSX
SIMSCD LDA =-1; STA EOF,2; COPY A; LDB SIMR; LSH 1
       DIV =3; ADD SMBFP,2; STA SMBPT,2
       LDA EFL,2; SUB SBIOC,2; SKG =256*3; BRU *+2; LDA =256*3
       SUB SIMR; STA CRCNT,2; STB SIMR
       LDA =3; SUB SIMR; STA CPSM,2; LSH 3
       LDA* SMBPT,2; MIN SMBPT,2; COPY BX; LSH 0,2; LDX SIMSX
       STA CWSM,2; LDB SIMSB; LDA SIMSA; BRR SIMSCP

*      SBLNK SKIPS BLANKS IN THE INPUT STREAM. IF IT
*      ENCOUNTERS EOF, IT RETURNS TO CALL -2. EXPECTED USE IS
*              BRM SIMCIO  BRU  END OF FILE  SKN BLNFLG  BRM SBLNK

SBLNK  ZRO; BRR SBRT           COROUTINE CALL
SBRT   ZRO; BRR SBLNK
SKBL2  BRM SBRT; SKE =0; BRU SKBL4; BRM SBRT; BRU SKBL1
SKBL4  SKE =135B; BRU SKBL2; COPY A; BRM SBRT
SKBL3  BRM SIMCIO; BRU SBLEOF
SKBL1  SKG =0; BRU SKBL3; SKE =135B; BRU SKBL2
       BRM SIMCIO; BRU SBLEOF; BRU SKBL3
SBLEOF LDA =-3; ADM SBLNK; BRU SKBL2

*      GLINC IS JUST LIKE GLINE EXCEPT THAT IT PRODUCES A HASH OF THE
*      LINE INSTEAD OF FILLING THE BUFFER. THERE ARE TWO SUCH ROUTINES
*      TO GET A SLIGHT INCREASE IN SPEED.
*      GLINC ALSO DOES NOT SET BUFPTR WITH A SIZE.

GLINC  ZRO; SKN EOF,2; BRR GLINC; MIN GLINC
GLN1C  LDA CPOS,2; STA LPOS,2; LDA =-1; STA CCNT,2
       LDA ARG0; STA ACZ0; LDA ARG1; STA ACZ1; MIN LNUM,2
CGLPC  BRM SIMCIO; BRU GLEOFC; SKN BLNFLG; BRM SBLNK
NTEOFC SKE =155B; BRU NTEOLC
GEOLC  SKN BLNFLG; SKN CCNT,2; BRR GLINC; BRU GLN1C
NTEOLC XMA ACZ0; MUL CON0; LSH 23; XMA ACZ0; ADM ACZ0
       XMA ACZ1; MUL CON1; LSH 23; ADM ACZ1; MIN CCNT,2
       BRU CGLPC
GLEOFC LDA =SKBL2; STA SBRT; SKN CCNT,2; BRU GEOLC; BRU* GLINC

TLMS   ASC '$LINE TOO LONG FOR BUFFER/'

       ARRAY LNUM,CPOS,LPOS,EOF,FIL,BUFPTR,CCNT,NFIL

BUFP   RPT (YQ=1,NFIL)
       DATA BUFR($YQ)
       ENDR

       RPT (YQ=1,NFIL)
BUFR($YQ) BSS BUFSIZ
       ENDR

       VARB BUFCNT,ELCNT,SVA,ACM0,ACM1,BLNFLG,ACZ0,ACZ1
CON0   DATA 101
CON1   DATA 131
ARG0   DATA 1010101B
ARG1   DATA 3030303B

*      MORCOR GETS ANOTHER BLOCK OF CORE AND LINKS IT INTO THE
*      FREE CHAIN. SINCE IT IS GENERALLY CALLED WITH
*              LDA FREPNT; SKG =0; BRM MORCOR
*      IT MUST REMEMBER TO COPY FREPNT INTO A BEFORE RETURN.
*      ROUTINE IS CAREFUL NOT TO DESTROY X, BUT DOES DESTROY A AND B.
*      ROUTINE USES ELCNT.
*
*      CORE IS DIVIDED INTO 4 WORD UNITS AS FOLLOWS
*              LINK TO NEXT ELEMENT
*              CURSOR POS FOR START OF LINE
*              WORD 1 OF HASH
*              WORD 2 OF HASH
*
*      FREE CORE IS LINKED AND POINTED TO BY FREPNT. NCORE CONTAINS
*      THE LOWEST LOCATION NOT YET USED.
*      USEDCR IS COUNT OF BLOCKS USED FOR RELEASE CODE.

MORCOR ZRO; STX SVX; LDA FDCORE; ADD =4000B
       SKG =4000B*7; BRU *+2; BRU NOCORE    OUT OF ROOM
       STA FDCORE; SUB NCORE; COPY B; RSH 23; DIV =5; SUB =1
       STA ELCNT; LDA NCORE; STA FREPNT
SCLP   COPY AX; ADD =5; STA 0,2
       SKR ELCNT; BRU SCLP
       STA NCORE; COPY A; STA 0,2      ZERO LAST LINK
       LDX SVX; LDA FREPNT; BRR MORCOR
NOCORE MSG NCMS; BRS 10        FATEL ERROR FOR NOW

NCMS   ASC 'INSUFFICIENT CORE FOR COMPARISON/'


       VARB FREPNT,NCORE,SVX,FDCORE

*      STOR1 TAKES THE HASH CODE IN ACM0, ACM1 AND PUTS IT IN THE
*      PROPER LINE LIST. IT INCLUDES THE LPOS WORD. X IS THE
*      NUMBER OF THE FILE WHOSE INFORMATION TO USE.
*      THE LINE LISTS HAVE TWO POINTERS. LINK POINTS TO THE HEAD
*      OF THE LIST AND ELN TO THE END SO NEW ELEMENTS CAN BE ADDED
*      SINCE THIS ROUTINE MAY CALL MORCOR, IT USES A,B, AND ELCNT.
*      NUM1 HAS COUNT OF NUMBER OF LINES IN THIS LIST (0 MEANS 1).

STOR1  ZRO; MIN NUM1,2
       LDA FREPNT; SKG =0; BRM MORCOR  GET FREE LIST ELEMENT
       LDB LPOS,2; COPY AX,XB,BA       PRESERVE EVERYTHING IN REGS
       STA 1,2; LDA ACZ0; STA 2,2; LDA ACZ1; STA 3,2
       COPY BX,XB; LDA LNUM,2; COPY BX,XB; STA 4,2
       COPY BX,A               NOW HAVE FILE INC IN X, 0 IN A
       XMA* FREPNT; XMA FREPNT LINK EL TO 0 AND FREPNT TO NEXT FREE
       STA* ELN,2; STA ELN,2; BRR STOR1

       ARRAY LINK,NUM1,ELN,NFIL

*      RTRV1 IS CALLED WITH A FILE INDEX IN X AND A LINE NUMBER IN A.
*      IF THE LINE NUMBER IS NOT IN THE LINE LIST IT IS PUT THERE.
*      ON RETURN X HAS A POINTER TO THE LIST ELEMENT FOR THIS LINE.
*      RTRV1 SKIP RETURNS IF LINE EXISTS. NON-SKIP RETURN INDICATES
*      EOF WAS ENCOUNTERED. CALLS GLINC AND STOR1 TO GET LINE IN LIST.
*      DESTROYS A,B,X, AND ELCNT

RTRV1  ZRO; SKE NUM1,2; BRU *+2; BRU LSTRT  LINE IS LAST ONE
       SKG NUM1,2; BRU JRTV    LINE IS PRESENT
       BRM GLINC; BRR RTRV1; BRM STOR1 MUST BE PRESENT NOW
LSTRT  LDX ELN,2; MIN RTRV1; BRR RTRV1
JRTV   STA ELCNT; LDA LINK,2; BRU INTRLP
NTRLP  COPY AX; LDA 0,2
INTRLP SKR ELCNT; BRU NTRLP
       COPY AX; MIN RTRV1; BRR RTRV1

*      PUT OUT A LINE NUMBER AFTER *'S

LINPR  ZRO
       LDX LINK,2; LDA 4,2; LDB =10
       LDX OFL; BRS 36
OFLDOL MSG1 DLMS; BRR LINPR

DLMS   ASC '$/'

*      A PRINT ROUTINE. X HAS FILE INDEX. PRINTS FROM HERE TO EOF

PEND   ZRO
PENDL1 BRM SIMCIO; BRR PEND
       STA SVA; STX SVX; LDA FMS,2; LDB =-1; LDX OFL; BRS 34
       LDA SVA; LDX SVX
PENDL  CIO OFL; SKE =155B; BRU *+2; BRU PENDL1   NEW LINE
       BRM SIMCIO; BRR PEND; BRU PENDL

FMS    RPT (XQ=1,NFIL)
       DATA FMSG($XQ)
       ENDR

       RPT (XQ=1,NFIL)
FMSG($XQ) ASC '($XQ)) /'
       ENDR

*      ANOTHER PRINT ROUTINE. X HAS FILE INDEX. PRINT LINES IN HASH
*      BUFFER FROM FIRST TO NUMBER IN A INCLUSIVE.

PLINS  ZRO; STX SVX; ADD =1; BRM RTRV1; BRU PLINE     GET POS OF NEXT
       LDA 1,2
PLINE1 STA ELCNT; LDX SVX; LDB CPOS,2; STB RCPOS; LDX LINK,2
       LDA 1,2                 FIRST POS
       LDX SVX; BRM SIMSCP; BRU LBIG; SUB ELCNT; COPY N
       SUB =1; STA ELCNT
PLIN1  LDA FMS,2; LDB =-1; LDX OFL; BRS 34; LDX SVX
PLIN2  BRM SIMCIO; BRU LBIG; CIO OFL; SKR ELCNT; BRU *+2; BRU PFLN
       SKE =155B; BRU PLIN2; BRU PLIN1
PLINE  LDX SVX; LDA CPOS,2; BRU PLINE1 CPOS HAS EOF POSITION
PFLN   LDX SVX; LDA RCPOS; BRM SIMSCP; NOP; BRR PLINS

       VARB RCPOS

*      HERE IS THE FIND A MATCH ROUTINE. IT IS CALLED AFTER A DIFFERENCE
*      IS FOUND. THE FILES INVOLVED ARE ASSUMED TO HAVE HAD AN SCP
*      DONE TO GET BACK TO THE START OF THE MISMATCH. THE CALL IS
*              BRM FMTCH
*              BOTH FILE HIT EOF BEFORE MATCH
*              MATCH  A HAS LINE NUM FOR FIRST FILE B FOR SECOND
*      AT CALLING TIME, A AND B HAVE THE FILE INDICES

FMTCH  ZRO; STA MF1; STB MF2; COPY A; STA FM1; STA FM2     POSITIONS
       MIN FMTCH               FOR RETURN
FMTLP  LDA FM1; LDX MF1; BRM RTRV1; BRU FMEOF1
       LDA FM2; LDX MF2; BRM RTRV1; SKR FM2 JUST 1 LESS LINE HERE
MULCOM LDA FM1; LDX MF1; BRM RTRV1; BRU LBIG     SHOULD BE THERE
       LDA 2,2; STA ACM0; LDA 3,2; STA ACM1
       LDA FM2; STA WDCNT              LINE COMPARE NUMBERS
FCMS1  LDA WDCNT; LDX MF2; BRM RTRV1; BRU LBIG
       LDA 2,2; SKE ACM0; BRU FNM1
       LDA 3,2; SKE ACM1; BRU FNM1
       LDA FM1; LDB WDCNT; BRM MULMTC  FOUND MATCH, CHECK MUL LINE
FNM1   SKR WDCNT; BRU FCMS1
       LDA FM2; LDX MF2; BRM RTRV1; BRU LBIG
       LDA 2,2; STA ACM0; LDA 3,2; STA ACM1
       LDA FM1; SKG =0; BRU INCFMS     ONLY ONE LINE THERE
       SUB =1; STA WDCNT       AVOID REDOING COMPARE
FCMS2  LDA WDCNT; LDX MF1; BRM RTRV1; BRU LBIG
       LDA 2,2; SKE ACM0; BRU FNM2
       LDA 3,2; SKE ACM1; BRU FNM2
       LDA WDCNT; LDB FM2; BRM MULMTC
FNM2   SKR WDCNT; BRU FCMS2
INCFMS MIN FM1; MIN FM2; BRU FMTLP

FMEOF1 LDA FM2; LDX MF2; BRM RTRV1; BRU* FMTCH   BOTH AT EOF
       SKR FM1; BRU MULCOM

*      THIS ROUTINE CHECKS FOR A MULTI-LINE MATCH. ON ENTRY MF1 AND
*      MF2 CONTAIN THE FILE INDICES. A AND B CONTAIN THE LINE NUMBERS
*      ON WHICH A MATCH WAS FOUND. THIS ROUTINE CHECKS FOR THE CORRECT
*      NUMBER OF HASH MATCHES AND THEN GOES BACK AND CHECKS THE LINES
*      THEMSELVES JUST TO MAKE SURE. THE NUMBER OF EXTRA LINES
*      TO MATCH (-1) IS FOUND IN MXCMP
*      IF A MATCH IS FOUND, ROUTINE DOES A RETURN FROOM FMTCH
*      ORTHERWISE A RETURN THROUGH MULMTC.

MULMTC ZRO; STA OM1; STA OM1S; STB OM2; STB OM2S
       LDA MXCMP; SKA =40000000B; BRU RTMLM; STA MXCM1
MLCMP  MIN OM1; LDA OM1; LDX MF1; BRM RTRV1; BRU MLEOF1
       LDA 2,2; STA ACMA; LDA 3,2; STA ACMB GET HASHES
       MIN OM2; LDA OM2; LDX MF2; BRM RTRV1; BRR MULMTC    NO MATCH
       LDA 2,2; SKE ACMA; BRR MULMTC; LDA 3,2; SKE ACMB; BRR MULMTC
       SKR MXCM1; BRU MLCMP
RTMLM  LDA OM1S; LDB OM2S; BRR FMTCH

MLEOF1 MIN OM2; LDA OM2; LDX MF2; BRM RTRV1; BRU RTMLM
       BRR MULMTC

       VARB SCP1,SCP2,OM1S,OM2S,MXCM1,OM1,OM2,ACMA,ACMB,NMIS
       VARB MF1,MF2,FM1,FM2,WDCNT,OFL,CIN,NPRMPT

MXCMP  DATA 2

LBIG   MSG LBMS; BRS 10

LBMS   ASC '$$INTERNAL ERROR$$/'

*      THIS ROUTINE REMOVES LINES FROM THE LINE LIST. IT IS THE OPPOSITE
*      OF STORE1. LINES ARE REMOVED FROM THE FRONT OF THE LIST.
*      A HAS THE NUMBER OF LINES TO REMOVE, X HAS THE FILE INDEX.
*      LIST SPACE RECOVERED IS PLACED BACK ON THE FREE STORAGE LIST.

DUMPIT ZRO; SUB =1; STA ELCNT; COPY XB; LDX LINK,2; STX SVX
DMPLP  LDA 0,2; COPY BX,XB; SKR NUM1,2; NOP; STA LINK,2
       COPY XB,AX,BA; SKR ELCNT; BRU DMPLP
       COPY AX; LDA SVX; XMA FREPNT; STA 0,2; COPY BX; LDA LINK,2
       SKE =0; BRR DUMPIT; LDA RSE,2; STA ELN,2; BRR DUMPIT

RSE    RPT (XZ=0,NFIL-1); DATA LINK+XZ; ENDR

CMPLEN ZRO
       STA SVA; ADD =4; COPY AX; BRX *+1
       SKN 0,2; BRU *-2; LDA 0,2; ETR =37777777B; STA 0,2
       COPY XA,B; SUB =4; SUB SVA; STA* SVA
       LSH 1; ADM* SVA         GOT SIZE ALMOST
       LDB 0,2; LDX =-3        CHECK CHARACTERS
       COPY A; LSH 8; SKE =136B; BRX *-3
       COPY XA; ADM* SVA; BRR CMPLEN

       ARRAY NMBUF1,NMBUF2,60

       VARB INDEX
PRTD   ZRO
       SKN NMIS; BRR PRTD     ONLY PUT OUT ONCE
       LDA =DBUF*3-1; COPY AB; BRS 140; XAB; SUB =4; XAB 0 GET DATE
       LDX OFL; BRS 35 AND PRINT
       MSG1 EQ1MS; LDA =NMBUF1+5; LDB NMBUF1; LDX OFL; BRS 34
       MSG1 EQ2MS; LDA =NMBUF2+5; LDB NMBUF2; LDX OFL; BRS 34
       MSG1 DOL2; BRR PRTD

EQ1MS  ASC '$1)=/'
EQ2MS  ASC '$2)=/'
DOL2   ASC '$$/'

DBUF   BSS 7

START  BRU MAIN; BRS 12

       ASC 'WEIHER'
       ASC 'A03.00'

MAIN   STA CIN; COPY N; STA NPRMPT; BRS 147
       LDA =SKBL2; STA SBRT
       RPT (XQ=1,NFIL)
MN($XQ) MSGP INP($XQ); LDA CIN; BRS 62; BRU MN($XQ); STA FIL+XQ-1
       STX EFL+XQ-1
       LDA =64; LDB =INDEX; LDX =1; BRS 67  GET DIR POINTER
       LDA INDEX; LDB =NMBUF($XQ); LDX =60; BRS 96    GET NAME
       LDA =NMBUF($XQ); BRM CMPLEN
       ENDR
OUTN   MSGP OUTP; LDA CIN; LDX =3; BRS 63; BRU OUTN; STA OFL
       LDA =-1; SALL EOF,NUM1; STA NMIS
       LDA =-256*3; SALL SBIOC
       COPY A; STA BLNFLG; SALL CPOS; STA FREPNT; SALL LINK,CRCNT,LNUM
       RPT (XQ=0,NFIL-1)
       LDA =LINK+XQ; STA ELN+XQ
       ENDR
       LDA =DFMLMT; STA MXCMP
       MSGP NUMQST; LDA =155B; CIT CIN; BRU *+2; BRU NOBLQ
       LDX CIN; LDB =10; BRS 38; SKA =40000000B; BRU NOMNM
       SUB =1; STA MXCMP
NOMNM  COPY BA; SKE =152B; BRU NOBLQ
       MSGP BLQMS; LDX =-1; COPY A; BRS 12
       CIO CIN; SKE ='Y'; SKR BLNFLG; NOP; LDA =155B; SKN NPRMPT; CIO =1
NOBLQ  LDA =8; STA NCORE; BRS 43
LCORE  SKB =77B; BRU FCORE
       LRSH 6; SKR NCORE; BRU LCORE
FCORE  LDA NCORE; COPY B; LSH 11; STA NCORE; STA FDCORE

       LDA =155B; CIO =1

MAINL  GET =0,=0,EOF1; SETACM
       GET =1,=0,EOF2; CMPAC NMATCH
       DUMP =1,=1; DUMP =0,=1; BRU MAINL

EOF1   GET =1,=0,DONE; BRM PRTD; MSG1 FILE1S; MSG1 DLMS
       LDX LINK+1; LDA 1,2; LDX =1; BRM SIMSCP; BRU LBIG
       BRM PEND; STRARN 2; BRS 10

EOF2   BRM PRTD; LDX LINK; LDA 1,2; COPY X; BRM SIMSCP; BRU LBIG
       BRM PEND; STRARN 1; MSG1 FILE2S; MSG1 DLMS; BRS 10

FILE1S ASC '****/'
FILE2S ASC '********/'

       RPT (XQ=1,NFIL)
INP($XQ) ASC 'INPUT FILE ($XQ): /'
       ENDR

OUTP   ASC 'OUTPUT TO: /'

NUMQST ASC 'NUMBER OF LINES TO MATCH = /'

BLQMS  ASC 'SUPRESS BLANKS? /'

DONE   SKN NMIS; BRS 10; MSGP NODIF; LDA OFL; SKN NPRMPT; SKE =1
       BRU *+2; BRS 10; MSG1 NODIF; BRS 10

NODIF  ASC 'NO DIFFERENCES ENCOUNTERED/'

NMATCH BRM PRTD; MIN NMIS
       COPY A; LDB =1; BRM FMTCH; BRU EOFDM
       STA FM1; STB FM2
       COPY X; BRM PLINS; STRARN 1
       LDX =1
       LDA FM2; BRM PLINS; STRARN 2
       MIN FM1; MIN FM2; DUMP =0,FM1; DUMP =1,FM2; BRU MAINL

EOFDM  BRM PRTD; LDX LINK; LDA 1,2; COPY X; BRM SIMSCP; BRU LBIG
       BRM PEND; STRARN 1
       LDX LINK+1; LDA 1,2; LDX =1; BRM SIMSCP; BRU LBIG
       BRM PEND; STRARN 2; BRS 10

       END MAIN
COMPARE ** FICHE/FRAME BREAK *****
SCOMPARE3 SYMSORT

SYM.  PG.LN. IDENT.

ARG0    5  6 SRCCOM  ARG1    5  7 SRCCOM  ARRAY   1  3 SRCCOM  
BLQMS  12  6 SRCCOM  BUFP    4 34 SRCCOM  BUFSIZ  2 19 SRCCOM  CGLPC   4 22 SRCCOM  
CMPAC   2  4 SRCCOM  CMPAC0  1 39 SRCCOM  CMPLEN 10  1 SRCCOM  CON0    5  4 SRCCOM  
CON1    5  5 SRCCOM  DBUF   10 25 SRCCOM  DFMLMT  2 17 SRCCOM  DLMS    7  5 SRCCOM  
DMPLP   9 33 SRCCOM  DOL2   10 23 SRCCOM  DONE   12  8 SRCCOM  DUMP    2 11 SRCCOM  
DUMPIT  9 32 SRCCOM  EOF1   11 27 SRCCOM  EOF2   11 31 SRCCOM  EOFDM  12 21 SRCCOM  
EQ1MS  10 21 SRCCOM  EQ2MS  10 22 SRCCOM  FCMS1   8 17 SRCCOM  FCMS2   8 26 SRCCOM  
FCORE  11 19 SRCCOM  FILE1S 11 34 SRCCOM  FILE2S 11 35 SRCCOM  FMEOF1  8 33 SRCCOM  
FMS     7 16 SRCCOM  FMTCH   8 10 SRCCOM  FMTLP   8 12 SRCCOM  FNM1    8 21 SRCCOM  
FNM2    8 30 SRCCOM  GEOLC   4 24 SRCCOM  GET     2  6 SRCCOM  GLEOFC  4 28 SRCCOM  
GLINC   4 19 SRCCOM  GLN1C   4 20 SRCCOM  INCFMS  8 31 SRCCOM  INP($X 11 38 SRCCOM  
INTRLP  6 34 SRCCOM  JRTV    6 32 SRCCOM  KUFR($  4 39 SRCCOM  LBIG    9 23 SRCCOM  
LBMS    9 25 SRCCOM  LCORE  11 17 SRCCOM  LINPR   6 39 SRCCOM  LSTRT   6 31 SRCCOM  
MAIN   10 32 SRCCOM  MAINL  11 23 SRCCOM  MLCMP   9  8 SRCCOM  MLEOF1  9 15 SRCCOM  
MN($XQ 10 35 SRCCOM  MORCOR  5 26 SRCCOM  MSG     1 15 SRCCOM  MSG1    1 23 SRCCOM  
MSGP    1 19 SRCCOM  MULCOM  8 14 SRCCOM  MULMTC  9  6 SRCCOM  MXCMP   9 21 SRCCOM  
NAME(Y  1 12 SRCCOM  NAME(Y  1  6 SRCCOM  NCMS    5 36 SRCCOM  NFIL    2 18 SRCCOM  
NMATCH 12 13 SRCCOM  NOBLQ  11 16 SRCCOM  NOCORE  5 34 SRCCOM  NODIF  12 11 SRCCOM  
NOMNM  11 13 SRCCOM  NTEOFC  4 23 SRCCOM  NTEOLC  4 25 SRCCOM  NTRLP   6 33 SRCCOM  
NUMQST 12  4 SRCCOM  OFLDOL  7  3 SRCCOM  OMSG($  7 21 SRCCOM  OUTN   11  2 SRCCOM  
OUTP   12  2 SRCCOM  PEND    7  9 SRCCOM  PENDL   7 13 SRCCOM  PENDL1  7 10 SRCCOM  
PFLN    7 37 SRCCOM  PLIN1   7 33 SRCCOM  PLIN2   7 34 SRCCOM  PLINE   7 36 SRCCOM  
PLINE1  7 29 SRCCOM  PLINS   7 27 SRCCOM  PRTD   10 13 SRCCOM  RSE     9 38 SRCCOM  
RTMLM   9 13 SRCCOM  RTRV1   6 28 SRCCOM  SALL    1 27 SRCCOM  SBLEOF  4 12 SRCCOM  
SBLNK   4  5 SRCCOM  SBRT    4  6 SRCCOM  SCLP    5 30 SRCCOM  SETAC0  1 34 SRCCOM  
SETACM  1 37 SRCCOM  SIMCI2  3  2 SRCCOM  SIMCIO  3  1 SRCCOM  SIMGCH  3 12 SRCCOM  
SIMPKW  3 14 SRCCOM  SIMSCD  3 32 SRCCOM  SIMSCF  3 23 SRCCOM  SIMSCP  3 25 SRCCOM  
SIMSEF  3 10 SRCCOM  SKBL1   4 10 SRCCOM  SKBL2   4  7 SRCCOM  SKBL3   4  9 SRCCOM  
SKBL4   4  8 SRCCOM  SMBFP   2 25 SRCCOM  START  10 27 SRCCOM  STOR1   6 10 SRCCOM  
STRARN  2 14 SRCCOM  TLMS    4 30 SRCCOM  VARB    1  9 SRCCOM  XQ      2  8 SRCCOM  
XQ      1 28 SRCCOM  XQ      1 10OM  XQ      1  4 SRCCOM  [MBF($  2 22 SRCCOM  

7dQQÞ