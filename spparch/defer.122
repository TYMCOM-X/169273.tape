        TITLE   DEFER	9 Dec 79 MCH
;Ver 7.18 made 1980 fixes by rewriting FDMN, GBNDT, DATCV, and CUBS and
;by extending YTB, DSTAB, DSTAB1, and YRDAY through 1982.  Also added
;code and tables so that it is no longer necessary to transmit
;special leap year versions.  /MCH

;DO NOT FORGET TO MAKE A LOCKHEED VERSION!!
;(See LochSw below)

VERSION==1
VERPAT==22
R0=     0
R1=     1
R2=     2
R3=     3
CH=     4
I=      10
P=      17
        OPDEF   ERROR [01000000000]
        LOC     41
        PUSHJ   P,ERUUO
        LOC     137
        XWD     VERSION,VERPAT
        RELOC
DEFER:  MOVE    P,STACK
        OUTSTR  [ASCIZ '
VERSION ']
        IFDEF   LOCHSW,<OUTCHR [114]>
        HLRZ    R0,137
        PUSHJ   P,PDNUM
        OUTCHR  [56]
        HRRZ    R0,137
        PUSHJ   P,PDNUM
        OUTCHR  [15]
        OUTCHR  [12]
        PUSHJ   P,BRS176
        OR      R1,[2B14]
        MOVEM   R1,LIST
        SETZM   LIST+1
        MOVEI   5,11
        GETTAB  5,
        JFCL
        LDB     6,[POINT 7,5,13]
        SUBI    6,"0"
        IMULI   6,^D10
        LDB     5,[POINT 7,5,20]
        ADDI    6,-"0"(5)
        MOVEM   6,CPN
        JRST    RQ

STACKL= 100
STACK:	XWD     -STACKL,.
        BLOCK   STACKL
STACKE: 0

PDNUM:  IDIVI   R0,12           ;DIVIDE FOR THE NEXT DIGIT
        HRLM    R1,(P)          ;PUT THE DIGIT ON THE STACK
        SKIPE   R0              ;SKIP IF THAT IS ALL
        PUSHJ   P,PDNUM         ;GET THE NEXT DIGIT
        HLRZ    R0,(P)          ;RETRIEVE DIGIT FROM THE STACK
        ADDI    R0,"0"          ;CONVERT TO A CHARACTER
        OUTCHR  R0              ;PRINT THE CHARACTER
        POPJ    P,              ;AND RETURN

BRS176:  DATE    R1,             ;GET THE DATE
        MOVEM   R1,5
        ADDI    R1,1            ;TO GET THE RIGHT NUMBER
        TIMER   6,
        MOVSI   7,-1
        HRRI    7,6
        GETTAB  7,              ;GET THE JBTPRV WORD FOR TIME ZONE
        JFCL
        LDB     7,[POINT 6,7,7] ;GET THE TIME ZONE
        MOVSS   7,7             ;IN LH
        IORI    7,50            ;GET TO CALIF TIME ZONE
        MOVEI   2,5
        DATUUO  2,
        JFCL
        CAML    6,[74*74*74*25] ;AT 2100 HOURS
        ADDI    R1,1            ;GO TO THE NEXT DAY
        IDIVI   R1,^D31
        MOVEM   R2,DANO#
        IDIVI   R1,^D12
        MOVEM   R2,MTH#
        SUBI    R1,10
        LSH     R1,4
        OR      R1,MTH
        LSH     R1,5
        OR      R1,DANO
        POPJ    P,

;TAKES BINARY DATE AND CONVERTS TO MONTH DAY YEAR
FDMN:	Z
        SETZ    R3,
        CAMG    R1,YTB+1(R3)
        JRST    .+3
        AOS     R3
        JRST    .-3
        MOVEM   R3,YR
        SUB     R1,YTB(R3)
	trnn	r3,3		;test to see if year divisible by 4
	jrst	FdmnLy		;yes, so use month table for leap years
        SETZ    R3,
        CAMG    R1,MTB+1(R3)
        JRST    .+3
        AOS     R3
        JRST    .-3
        MOVEM   R3,MN
        SUB     R1,MTB(R3)
        JRST    fdmnrt
fdmnly: setz	r3,
	camg	r1,MtbLy+1(r3)
        jrst    .+3
	aos	r3	
	jrst	.-3	
        movem   r3,mn
        sub     r1,MtbLy(r3)
fdmnrt: MOVEM   R1,DY
        JRST    @FDMN

;TAKES YR MN DY AND CONVERTS BACK TO BINARY
GBNDT:  Z
        SKIPL   YR
        JRST    .+3
        SETZ    R1,
        JRST   @GBNDT
	move	r3,mn
	move	r1,yr
	trne	r1,3
	jrst	.+3		; not a leap year so use regular table
	move	r1,MtbLy(r3)
	skipa
	move	r1,mtb(r3)
	move	r3,yr
	add	r1,ytb(r3)	; add in no. of days up to begin of year
        ADD     R1,DY		; add in no. of days since 1st of month
        JRST    @GBNDT

;CONVERTS DATE WORD FROM TO BINARY FORM

DATCV:  MOVEM   R1,T1
        LSHC    R1,-11
        ANDI    R1,17
        MOVE    R3,R1
        MOVE    R1,YTB(R3)
        MOVEM   R1,T2
	move	r1,r3
	trne	r1,3
	jrst	datcv1		;not a leap year so use regular table
        SETZ    R1,
        LSHC    R1,4
        MOVE    R3,R1
        MOVE    R1,MTBly(R3)
	jrst	datcv2
datcv1: setz	r1,
	lshc	r1,4
	move	r3,r1
	move	r1,mtb(r3)
datcv2: ADDM    R1,T2
        SETZ    R1,
        LSHC    R1,5
        ADD     R1,T2
        POPJ    P,

YTB:    DEC     -1,364,729,1094,1459,1825,2190,2555,2920,3286,3651,4016
MTB:    DEC     0,31,59,90,120,151,181,212,243,273,304,334,365
MtbLy:	DEC     0,31,60,91,121,152,182,213,244,274,305,335,366

USFLG:  0               ;0=> US -1=> FOREIGN
RQ:     PUSHJ   P,RQS
        PUSHJ   P,XTUZ
INS1:   PUSHJ   P,GNXD
        MOVE    R1,.INS
        PUSHJ   P,OPCH
        MOVE    R1,TMWD
        PUSHJ   P,SWD
        MOVE    R1,NXD
        PUSHJ   P,SWD
        PUSHJ   P,XRQS          ;SENDS RQS DATA
        PUSHJ   P,SLIST
        PUSHJ   P,GWDA
        CAME    R1,.EJB
        ERROR   [ASCIZ 'SYSTEM ERROR']
        OUTSTR  [ASCIZ '
DEFER JOB ENTERED
']
QUIT:   MOVE    1,[-1]
        ZAPCIR  1,
        EXIT    1,

RQS:    PUSHJ   P,GUSNM
        OUTSTR  [ASCIZ '
PROJECT CODE ']
        INCHWL  CH
        CAIN    CH,15
        JRST    RQS1            ;NO PROJECT CODE
        MOVEI   R0,33
        PUSHJ   P,WCHI
        MOVE    R0,CH
        ADDI    R0,140
        ANDI    R0,177
        PUSHJ   P,WCHI
        MOVNI   R3,14
        INCHWL  CH
        CAIN    CH,15
        JRST    RQS1
        MOVE    R0,CH
        ADDI    R0,140
        ANDI    R0,177
        PUSHJ   P,WCHI
        AOJL    R3,.-7
        OUTSTR [ASCIZ '
PROJECT CODE TOO LONG
']
        CLRBFI
        JRST    RQS
RQS1:   INCHWL  CH
        CAIE    CH,12
        ERROR   [ASCIZ 'MISSING LINE FEED']
        MOVEI   R0,32           ;COLON
        PUSHJ   P,WCHI
CO0:    CLRBFI
        OUTSTR  [ASCIZ '
COMMAND FILE NAME ']
CO1:    INCHWL  CH
        CAIN    CH,40
        JRST CO1
        CAIN    CH,15
        JRST    CO0
        JRST    CO2
COM1:   INCHWL  CH
CO2:    CAIN    CH,15
        JRST    RQS2
        CAIE    CH,40
        JRST    COM2
        CLRBFI
        JRST    RQS2A
COM2:   MOVE    R0,CH
        ADDI    R0,140
        ANDI    R0,177
        PUSHJ   P,WCHI
        JRST    COM1
RQS2:   INCHWL  CH
RQS2A:  MOVEI   R0,155
        PUSHJ   P,WCHI
        MOVNI   R3,6
        SETZ    R0,
        PUSHJ   P,WCHI
        AOJL    R3,.-1
        POPJ    P,
GUSNM:	MOVE    R1,[XWD -1,-22]
        GETTAB  R1,
        JFCL
        MOVEM   R1,UN1#
        MOVE    R1,[XWD -1,-21]
        GETTAB  R1,
        JFCL
        MOVEM   R1,UN2#
        MOVE    I,NMP
        MOVE    R1,UN1
        MOVNI   R3,6
        SETZM   NCOUNT
        SETZ    R0,
        LSHC    R0,6
        SKIPG   R0
        POPJ    P,
        PUSHJ   P,WCHI
        AOS     NCOUNT#
        AOJL    R3,.-6
        MOVNI   R3,6
        MOVE    R1,UN2
        SETZ    R0
        LSHC    R0,6
        SKIPG   R0
        POPJ    P,
        PUSHJ   P,WCHI
        AOS     NCOUNT
        AOJL    R3,.-6
        POPJ    P,

WCHI:   DPB     R0,I
        IBP     I
        HLRZ    R2,I
        CAIN    R2,341000
        HRLI    I,201000
        POPJ    P,
NMT:	BLOCK   20
NMP:    POINT   8,NMT,19

GNXD:   MOVE    R1,TMWD
GNX0:   MOVEI   R1,25*74+15
        SKIPE   USFLG
        MOVEI   R1,15*74+15
GNX:    MOVEM   R1,NXD
        MOVE    R1,LIST
        PUSHJ   P,DATCV
        SKIPG   R1
        ERROR   [ASCIZ 'ERROR IN DATE ROUTINE']
        JSR     FDMN  
        MOVE    R1,DY
        SOS     R1
        IMULI   R1,^D1440
        ADDM    R1,NXD
        MOVE    R1,MN
        LSH     R1,20
        ADDM    R1,NXD
        MOVE    R1,YR
        LSH     R1,24
        ADD     R1,NXD
        PUSHJ   P,CUBS
        MOVEM   R1,NXD
        POPJ    P,


YR:	Z
MN:	Z
DY:	Z
NXD:	Z
LIST:   BLOCK   100
TMWD:   4000025
.INS:   12227063
.EJB:	11225042
CPN:	Z
OPCH:	MOVEM   R1,CMD
        SKIPLE  AUX
        JRST    SCMD
        MOVEI   R1,INTTAB
        HRLI    R1,6
        INTADR  R1,
        JFCL
        SETO    R1,
        HRLI    R1,4
        TINASS  R1,
        JFCL
        MOVSI   R1,42000
        INTENB  R1,
        JFCL
        MOVEI   R1,6
        HRLI    R1,4
        INTASS  R1,
        JFCL
        MOVEI   R1,^D180
        HRLI    R1,1
        SETTIM  R1,
        JFCL
        OUTSTR  [ASCIZ '
BUILDING CIRCUIT TO MASTER DEFER SYSTEM
']
        PUSHJ   P,SETIM
        PUSHJ   P,GETSNO
        IFNDEF  LOCHSW,<
        SKIPL   USFLG
        JRST    US1
P141:   MOVEI   R1,[ASCIZ 'DUPJOB:14-;']
US1:    HRLZI   R2,-1
        HRRI    R2,6
	GETTAB  R2,
        JFCL
        TRNE    R2,40000
        MOVEI   R1,[ASCIZ 'DUPJOB:2-;']>
        CREAUX  R1,
        JRST    OPCHER
        MOVEM   R1,AUX#
        MOVEI   R1,4
        HRL     R1,AUX
        AUXCAL  R1,15
        AUXCAL  R1,15
        JRST    INIT
OPCHER: MOVS    R2,R1
        JRST    ER(R2)
ER:     JRST    SUPERR          ;SUPERVISOR ERROR
        ERROR   SYSER
        ERROR   SYSER
        ERROR   [ASCIZ '
UNABLE TO BUILD CIRCUIT, TRY LATER
']
        ERROR   SYSER
        ERROR   SYSER
        ERROR   SYSER
        ERROR   SYSER
SUPERR: ANDI    R1,17
        CAIN    R1,4
        ERROR   [ASCIZ '
JOB LIST UNAVAILABLE
']
        ERROR   SYSER
SYSER:	ASCIZ '
SYSTEM ERROR
'


;
;       THIS ROUTINE SETS UP R1 FOR CORRECT COMPUTER NUMBER
;       DUPJOB:10 IF USER IS ON  SYSTEMS 35 OR 36
;       DUPJOB:8  IF USER IS ON  SYSTEMS 31,32,33,34,37,38,88.
;        ALL IX USERS GO TO SYSTEM 10
;


GETSNO: HRRZI   R1,11
        GETTAB  R1,
        JFCL
        MOVEM   R1,SYSNO#
        MOVE    R1,[POINT 7,SYSNO,6]
        MOVEM   R1,PTR#
        SETZM   WHEFLG#
        ILDB    R1,PTR
        CAIE    R1,"3"
        JRST    GET1
        ILDB    R1,PTR
        CAIGE   R1,"5"
        JRST    GET1
        CAILE   R1,"6"
        JRST    GET1
        SETOM   WHEFLG
        MOVEI   R1,[ASCIZ 'DUPJOB:10-;']
        POPJ    P,
GET1:   MOVEI   R1,[ASCIZ 'DUPJOB:8-;']
        POPJ    P,
INIT:   MOVE    R1,[POINT 7,GOMES]
        MOVEI   R2,5
        HRL     R2,AUX
        AUXCAL  R2,R1
        PUSHJ   P,GCHA
        CAIE    R1,377
        JRST    .-2
ICM:    SETZ    R1,
        MOVEM   R1,T3
        MOVNI   R3,5
        AOS     T3
        MOVEI   R1,3
        HRL     R1,AUX
        AUXCAL  R1,T3
        AOJL    R3,.-4
ICM1:   MOVE    R1,T2#
        MOVEM   R1,T1#
        PUSHJ   P,GCHA
        MOVE    CH,R1
        MOVEM   CH,T2
        SUB     CH,T1
        CAIE    CH,1
        JRST    ICM1
        MOVE    R1,T2
ICM2:   MOVEM   R1,T1
ICM3:   PUSHJ   P,GCHA
        MOVE    CH,R1
        MOVEM   CH,T2
        SUB     CH,T1
        CAIE    CH,1
        JRST    ICM1
        MOVE    R1,T2
        CAME    R1,T3#
        JRST    ICM2
;
;       SEND COMMAND WORD
;
        MOVE    R1,CMD#
SCMD:   PUSHJ   P,SWD
        PUSHJ   P,GWDA
        CAME    R1,CMD
        ERROR   SYSER
        PUSHJ   P,CLTIM
        POPJ    P,

SWD:	MOVEI   R2,3
        HRL     R2,AUX
        SETZ    R0,
        LSHC    R0,24
        AUXCAL  R2,R0

        SETZ    R0,
        LSHC    R0,10
        AUXCAL  R2,R0

        SETZ    R0,
        LSHC    R0,10
        AUXCAL  R2,R0

        POPJ    P,

GWDA:	HRLZ    R2,AUX
        SETZ    R0,
        PUSHJ   P,GCHA
        OR      R0,R1
        LSH     R0,10
        PUSHJ   P,GCHA
        OR      R0,R1
        LSH     R0,10
        PUSHJ   P,GCHA
        OR      R1,R0
        POPJ    P,

GCHA:   MOVEI   R2,10
        HRL     R2,AUX
        HRRI    R2,2
        AUXCAL  R2,R1
        ERROR   SYSER
        POPJ    P,
GCHA1:  MOVEI   R2,1000
        SLEEP   R2,
        JRST    GCHA
MVSUB:  MOVEM   R1,T1
        MOVE    R1,@T1
        SKIPE   R1
        JRST    MVSUB1
        ERROR   [ASCIZ 'INCOMPLETE INPUT DATA']
MVSUB1: PUSHJ   P,SWD
        AOS     T1
        MOVE    R1,@T1
        SKIPE   R1
        JRST    MVSUB1
        PUSHJ   P,SWD
        POPJ    P,

SLIST:	MOVEI   R1,LIST
        PUSHJ   P,MVSUB
        POPJ    P,

XRQS:	MOVE    R1,UZ
        LSH     R1,14
        OR      R1,CPN
        PUSHJ   P,SWD
        MOVEI   R1,NMT
        PUSHJ   P,MVSUB
        POPJ    P,
SETIM:  MOVEI   R1,0
        SETTIM  R1,
        JFCL
        MOVEI   R1,6
        HRLI    R1,4
        INTASS  R1,
        JFCL
        MOVEI   R1,^D180
        HRLI    R1,1
        SETTIM  R1,
        JFCL
        POPJ    P,

CLTIM:  MOVEI   R1,0
        SETTIM  R1,
        JFCL
        POPJ    P,
TIMOUT:	ERROR  [ASCIZ '
SYSTEM TIMEOUT- TRY AGAIN LATER
']
GOMES:  ASCIZ /
1GPERP
/

;this routine converts a DMIN date (see the 940 BRS manual) to CUBS
;assumes the date is in R1 and returns the CUBS date (in GMT!) in R1

CUBS:   MOVEM   R1,TEMP#	; save the DMIN date for later
        PUSHJ   P,GTUZ		; look up user's time zone
        MOVE    r3,UZ		; put time zone in R3
        MOVE    R2,TEMP		; get the date we are converting
	trzn	r3,40		; test for DST bit and turn off if on
	jrst	cubs1		; user not subject to DST
        setz    r1,		; clear r1 to use as an index
	caml	r2,dstab(r1)	; find next day that DST stops
	aoja	r1,.-1
	camle	r2,dstab1(r1)	; see if DST has already started
	aos	r3		; yes, so adjust the time zone by 1
cubs1:	movn	r3,r3		; get negative of tz
	addi	r3,20		; and make it the offset to GMT
	imuli	r3,^d60		; convert it to minutes
	movem	r3,zmin#	; and save the correction for later
	setz	r1,		; clear r1 before shifting
	lshc	r1,^d16		; right-justify year field in r1
	lshc	r2,-^d32	; right-justify month field in r2
	lsh	r3,-^d20	; right-justify minutes of month in r3
	addm	r3,zmin		; add the offset to GMT
	move	r3,zak(r2)	; fetch no. of days since begin of year
	add	r3,yrday(r1)	; add in no. of days UP TO begin of year
	trne	r1,3		; test for leap year
	jrst	cubs2		; no leap year fix needed
	trne	r2,16		; skip if mon < 2 (i.e., Jan or Feb)
	aos	r3		; add a day for leap year
cubs2:	subi	r3,^d59		; make it days since Feb 29, 1972
	imuli	r3,^d1440	; convert days to minutes
	move	r1,r3		; use r1 so as not to zap r4 with ASHC
	add	r1,zmin		; add minutes since 1st of month
	mul	r1,[^d234375]	; 1st part of conversion to CUBS
	ashc	r1,^d18		; finish conversion
	popj	p,

; Zak is the number of days from beginning of year to begin of month

zak:	DEC	  0		; Jan
	DEC	 31		; Feb
	DEC	 59		; Mar
	DEC	 90		; April
	DEC	120		; May
	DEC	151		; June
	DEC	181		; July
	DEC	212		; Aug
	DEC	243		; Sep
	DEC	273		; Oct
	DEC	304		; Nov
	DEC	334		; Dec

; DsTab gives the DMIN date for END of daylight savings time figured
; 	as 1 AM of last Sunday in October of the indicated year.

DSTAB:	EXP	   2316674	; 29 OCT 72
	EXP	   6314034	; 28 OCT 73
	EXP	  12311174	; 27 OCT 74
	EXP	  16306334	; 26 OCT 75
	EXP	  22324374	; 31 OCT 76
	EXP	  26321534	; 30 OCT 77
	EXP	  32316674	; 29 OCT 78
	EXP	  36314034	; 28 OCT 79
	EXP	  42306334	; 26 OCT 80
	EXP	  46303474	; 25 OCT 81
	EXP	  52324374	; 31 OCT 82
	EXP	  56321534	; 30 OCT 83
	EXP	  62314034	; 28 OCT 84
	EXP	  66311174	; 27 OCT 85
	EXP	  72306334	; 26 OCT 86
	EXP	  76303474	; 25 OCT 87
	EXP	3777777777	; dummy entry to indicate end of table

; DsTab1 gives the DMIN date for START of daylight savings time figured
;	 as 2 AM of last Sunday in April of the indicated year.

DSTAB1: EXP	  721630	; 30 APR 72
	EXP	 4716770	; 29 APR 73
 	EXP	10016230	;  6 JAN 74 (special date this year)
 	EXP	14276070	; 23 FEB 75 (again a special date)
 	EXP	20703570	; 25 APR 76 (no special law this year)
 	EXP	24700730	; 24 APR 77
  	EXP	30721630	; 30 APR 78
	EXP	34716770	; 29 APR 79
	EXP	40711270	; 27 APR 80
	EXP	44706430	; 26 APR 81
	EXP	50703570	; 25 APR 82
	EXP	54700730	; 24 APR 83
	EXP	60716770	; 29 APR 84
	EXP	64714130	; 28 APR 85
	EXP	70711270	; 27 APR 86
	EXP	74706430	; 26 APR 87

; the following table gives no. of days up to BEGINNING of the year

YrDay:	DEC	   0		;1972
	DEC	 366		;1973
	DEC	 731		;1974
	DEC	1096		;1975
	DEC	1461		;1976
	DEC	1827		;1977
	DEC	2192		;1978
	DEC	2557		;1979
	DEC	2922		;1980
	DEC	3288		;1981
	DEC	3653		;1982

GTUZ:	MOVEI   R1,50
        MOVEM   R1,UZ
        POPJ    P,

;       THE FOLLOWING CODE IS LEFT OVER FROM THE DAYS THAT
;       DEFER USED THE USERS TIME ZONE FOR THE TIME ZONE
;       FOR PERP
;       THIS INCLUDES DOWN TO BUT NOT INCLUDING THE LABEL UZ:

XTUZ:	MOVE    R1,[XWD -1,-23]
        GETTAB  R1,
        JFCL
        MOVEM   R1,AUN#
        MOVE    R1,[XWD -1,-22]
        GETTAB  R1,
        JFCL
        MOVEM   R1,UN1#
        MOVE    R2,[XWD -1,-21]
        GETTAB  R2,
        JFCL
        MOVEM   R2,UN2#
        PUSHJ   P,HASH          ;GET THE HASH FUNCTION
        MOVEM   R1,HNAME#
        MOVEM   R2,BLK#
        INIT    1,17
        SIXBIT/SYS/
        0
        JRST    ERR
        LOOKUP  1,LBLK
        JRST    ERR
RDLD:   USETI   1,@BLK          ;POSITION
        INPUT   1,LUDLST
        SETZ    R1,
        MOVE    R2,AUN
LOP:     CAMN    R2,LUDBLK(R1)
        JRST    FOUND
        SKIPN   LUDBLK(R1)
        JRST    ERR
        SKIPG   LUDBLK(R1)
        JRST    OVFL
        MOVE    R3,LUDBLK+2(R1)
        ANDI    R3,177
        ADD     R1,R3
        JRST    LOP
OVFL:   HRRZ    R2,LUDBLK(R1)
        MOVEM   R2,BLK
        JRST    RDLD
FOUND:  MOVE    R1,LUDBLK+1(R1)
        ANDI    R1,377
        MOVEM   R1,DISTNO#
        CLOSE   1,
        RELEASE 1,
        MOVE    R2,DISTNO
        CAIG    R2,^D29         ; LET CHECK FOR AREA 3
        JRST    US
        CAIG    R2,^D39 
        JRST    P14             ; THATS IT
        CAIG    R2,^D69
        JRST    US
        CAIG    R2,^D79
        JRST    P14
        CAIGE   R2,^D180
        JRST    US
        CAIL    R2,^D200
        JRST    US
P14:    SETOM   USFLG
        POPJ    P,
US:     SETZM   USFLG
        POPJ    P,
ERR:    ERROR   [ASCIZ 'TROUBLE IN READING TIME ZONE
        CALL QUALITY ASSURANCE']
LBLK:   SIXBIT/LUD/
        SIXBIT/SYS/
        0
        0
LUDBLK: BLOCK   200
LUDLST: IOWD    200,LUDBLK
        0

HASH:   MOVEI   6,0
        MOVE    3,[555555555555]
        MOVE    4,[361275431652]     MOVE    5,[612754316523]
        PUSHJ   P,RND
        PUSHJ   P,RND
        PUSHJ   P,RND
        PUSHJ   P,RND
        XOR     5,4
        MOVE    R1,5
        TLZ     R1,400000
        IDIVI   R1,^D887
        ADDI    R2,1
        XOR     3,5
        MOVE    R1,3
        POPJ    P,

RND:    ADD     4,R1
        ROTC    R1,-22
        MOVEI   7,5
RND1:   MOVE    10,4(6)
        MUL     10,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
        ADDM    11,5(6)
        AOJE    6,RND2
        MOVNI   6,1
        TRNE 4,1
        SKIPL   5
        MOVEI   6,0
        EXCH    3,5
RND2:   SOJG    7,RND1
        POPJ    P,
UZ:	Z
ERUUO:  MOVE    R0,40
        OUTSTR  @R0
        CLRBFI
        MOVE    1,[-1]
        ZAPCIR  1,
        EXIT    1,
ESCR:   JRST    @ESCRT
INTTAB: BLOCK   6
ESCRT:  EXP     0,ESCR
        EXP     0,0
TIMRT:  EXP     0,TIMOUT
        BLOCK   ^D62
        END     DEFER
  t@È