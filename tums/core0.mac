	title	CORE0	multilanguage callable routine to clear core and exit

	entry	CORE0

; INTADR flag bits
IA.DIS=	1
IA.CLR=	4

CORE0:	JFCL			; in case called by JSR

; The following RELEAS's due to bug in RESET UUO whereby it does not
; cleanup buffered I/O channels completely before returning causing
; ADRESS CHECK error under some circumstances:
CHN=	0
    repeat 20,<
	RELEAS	CHN,
CHN=	CHN + 1
    >

; Close up all I/O channels (see caveat above).
; Make page 0 private and writable.
; Turn off APRENB interrupt system.
	RESET

; Turn off and clear INTADR interrupt system.
	HRLZI	17,IA.DIS+IA.CLR
	INTADR	17,		; disable and clear INTADR system
	 JFCL

; Move program to AC's for execution
	HRLZI	17,AC0
	BLT	17,ACn		; copy program to ACs
	JRST	0		; start it

AC0:	phase	0		; assemble for ACs
	CORE	A,		; clear above location 140 (set UPTREL)
	 JFCL
	VCLEAR	B,		; unmap pages 1:777, if any mapped
	 JFCL
	EXIT	1,		; slip away silently
	EXIT			; in case CONT command
A:	XWD	0,140		; argument for CORE, clear above location 140
B:	XWD	777,1		; argument for VCLEAR, clear pages 1:777
	dephase
ACN=	.-AC0-1			; last register used

	end
