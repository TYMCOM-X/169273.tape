	SUBTTL	MA355/JDS/DRH/MP/RCM/VB		20-AUG-76

; COPYRIGHT 1972,1973,1976,1977 STEVENS INSTITUTE OF TECHNOLOGY
; HOBOKEN, NEW JERSEY 07030
; ALL RIGHTS RESERVED.

  ;UNIVERSALS

	SEARCH	SNOPRM		;SITBOL PARAMETERS
	SEARCH	JOBDAT		;JOB DATA AREA DEFINITIONS
	SEARCH	MACTEN		;DEC-10 MACRO DEFINITIONS
IFN FTOPS10,SEARCH	UUOSYM		;[244] TOPS10 UUO SYMBOLS
IFN FTOPS20,SEARCH	MONSYM		;[244] TOPS-20 JSYS definitions

  ;DIRECTIVES

	.DIREC	.XTABM		;EXCLUDE TABS FROM MACRO CALLS
	SALL			;SUPPRESS MACRO EXPANSIONS
	TWOSEG	400K		;TWOSEGMENT CODE

  ;VERSION NUMBER

	FIOVER==5		;MAJOR VERSION NUMBER
	FIOMIN==0		;MINOR VERSION NUMBER
	FIOEDT==303		;EDIT LEVEL
	FIOWHO==0		;CUSTOMER ARGUMENT

 ;TITLE

	TITLE.	FIO,SNOFIO,<I/O and System Interface>

  ;SHOW UNIVERSAL'S VERSION NUMBERS

	SHOW.	%%SNOP		;VERSION NUMBER OF SNOPRM
	SHOW.	%%MACT		;VERSION NUMBER OF MACTEN
IFN FTOPS10,SHOW.	%%UUOS		;[244] VERSION NUMBER OF UUOSYM
IFN FTOPS20,SHOW.	%%MONS		;[244] MONSYM's version number
	SHOW.	%%JOBD		;VERSION NUMBER OF JOBDAT

	LOC	.JBVER		;VERSION NUMBER
	VERSION			;SITBOL VERSION NUMBER

	RELOC	400K		;BACK TO THE HIGH SEG

	FIOTTL			; Title
	FIOPTX			; PASS2 assembly message
	SUBTTL	Table of Contents


;		Table of Contents for SNOFIO - SITBOL I/O and System Interface
;
;
;			   Section			      Page
;   1. Table of Contents. . . . . . . . . . . . . . . . . . .    2
;   2. REVISION HISTORY . . . . . . . . . . . . . . . . . . .    3
;   3. Dispatch table . . . . . . . . . . . . . . . . . . . .    4
;   4. STORAGE INITIALIZATION . . . . . . . . . . . . . . . .    5
;   5. OPENING THE COMMAND STRING . . . . . . . . . . . . . .    9
;   6. FIRST PASS OVER THE COMMAND. . . . . . . . . . . . . .   11
;   7. RECONFIGURATION OF STORAGE . . . . . . . . . . . . . .   13
;   8. 2ND PASS OVER THE COMMAND STRING . . . . . . . . . . .   14
;   9. STORAGE CONTROL. . . . . . . . . . . . . . . . . . . .   17
;  10. P (PRIMARY) ROUTINES FOR I/O . . . . . . . . . . . . .   24
;  11. Primary routines
;       11.1.   DATE. . . . . . . . . . . . . . . . . . . . .   25
;       11.2.   DELETE. . . . . . . . . . . . . . . . . . . .   26
;       11.3.   DETACH. . . . . . . . . . . . . . . . . . . .   27
;       11.4.   ENDFILE . . . . . . . . . . . . . . . . . . .   28
;       11.5.   EXIT. . . . . . . . . . . . . . . . . . . . .   29
;       11.6.   FILE. . . . . . . . . . . . . . . . . . . . .   31
;       11.7.   INPUT . . . . . . . . . . . . . . . . . . . .   33
;       11.8.   MTAPE . . . . . . . . . . . . . . . . . . . .   34
;       11.9.   OUTPUT. . . . . . . . . . . . . . . . . . . .   35
;       11.10.  RENAME. . . . . . . . . . . . . . . . . . . .   36
;       11.11.  RESCAN. . . . . . . . . . . . . . . . . . . .   37
;       11.12.  REWIND. . . . . . . . . . . . . . . . . . . .   38
;       11.13.  RUN . . . . . . . . . . . . . . . . . . . . .   39
;       11.14.  TIME. . . . . . . . . . . . . . . . . . . . .   40
;       11.15.  TTY . . . . . . . . . . . . . . . . . . . . .   41
;  12. TYPE S (SECONDARY) UTILITIES . . . . . . . . . . . . .   42
;  13. Secondary routines
;       13.1.   CVTFMT. . . . . . . . . . . . . . . . . . . .   43
;       13.2.   CVTIO . . . . . . . . . . . . . . . . . . . .   45
;       13.3.   CVTSIO. . . . . . . . . . . . . . . . . . . .   46
;       13.4.   GETOCT. . . . . . . . . . . . . . . . . . . .   47
;       13.5.   IOASSN. . . . . . . . . . . . . . . . . . . .   48
;       13.6.   NONBLK. . . . . . . . . . . . . . . . . . . .   49
;       13.7.   STFILS. . . . . . . . . . . . . . . . . . . .   50
;       13.8.   TVIN & TVOUT. . . . . . . . . . . . . . . . .   52
;  14. TYPE T (TERTIARY) I/O UTILITIES. . . . . . . . . . . .   58
;  15. Tertiary routines
;       15.1.   CLSALL. . . . . . . . . . . . . . . . . . . .   59
;       15.2.   CVTDW . . . . . . . . . . . . . . . . . . . .   60
;       15.3.   CVTWD . . . . . . . . . . . . . . . . . . . .   62
;       15.4.   DTCH. . . . . . . . . . . . . . . . . . . . .   63
;       15.5.   FILECH. . . . . . . . . . . . . . . . . . . .   64
;       15.6.   PEJECT. . . . . . . . . . . . . . . . . . . .   65
;       15.7.   READ. . . . . . . . . . . . . . . . . . . . .   66
;       15.8.   MAKEIO. . . . . . . . . . . . . . . . . . . .   75
;       15.9.   STRIO . . . . . . . . . . . . . . . . . . . .   75
;       15.10.  WRITE . . . . . . . . . . . . . . . . . . . .   80
;  16. TYPE Q (QUATERNARY) I/O UTILITIES. . . . . . . . . . .   85
;  17. Quaternary routines
;       17.1.   ALBUF . . . . . . . . . . . . . . . . . . . .   86
;       17.2.   CLRFSV. . . . . . . . . . . . . . . . . . . .   88
;       17.3.   CLS . . . . . . . . . . . . . . . . . . . . .   89
;       17.4.   LKP . . . . . . . . . . . . . . . . . . . . .   90
;       17.5.   ENTR. . . . . . . . . . . . . . . . . . . . .   91
;       17.6.   MODE. . . . . . . . . . . . . . . . . . . . .   92
;       17.7.   MSTIME. . . . . . . . . . . . . . . . . . . .   93
;       17.8.   OPN . . . . . . . . . . . . . . . . . . . . .   94
;       17.9.   WMPROP. . . . . . . . . . . . . . . . . . . .   95
	SUBTTL	REVISION HISTORY

COMMENT	\
122	RECONFIGURE THE SYSTEM AND RENAME THE MODULES.
130	DELETE THE TMP FILE FROM DISK.
134	Add PORTALs for SNODEB and SNOPAV.
140	FIX PROBLEM WITH FILE SPECIFICATION SCANNER.
141	REMEMBER WERE WE CAME FROM (SAVE .SGNAM, .SGDEV, .SGPPN)
142	Add a third argument to the EXIT function.
143	FIX EDIT 130.
147	Use Y.???? instead of X.????, and use the correct macros
	to handle them.
157	Fix a mistake in the dispatch table. 
160	Fix a mistake in edit 143.
166	Finish SFD handling.
172	Fix a slight problem in the command scanner
175	Remove SETDDT UUO from EXIT2
176	FIX PROBLEM WITH SEGDEV & SEGNAM IN SNOFIO
177	FIX SNOFIO SO ^Z'S WORK CORRECTLY
200	TOUCH UP SLIGHT BUG WITH SFD'S.
202	ADD APPEND MODE I/O.
204	Add new keyword &VERSION.
210	Fix a fix to the file spec. scanner.
213	Fix problems with the output associations file.
223	Remove the rewind from the close routine (CLS).
230	Don't bomb if no  line feed in the TMPCOR file.
	Also RELEAS channel 17 all  the time.
232	Make absolute errors give real errors (i.e. Experation
	date has expired.  NOT ill mem ref).
235	Use correct defaults for proj. and prog. no. if not supplied.
240	Redefine FMT.AP
243	Get the processor type.
244	Add a little support for the FTOPS10 and FTOPS20 feature tests.
245	Add missing PORTAL
246	Redefine some definitions from this module to SNOPRM
251	Add another missing PORTAL
255	Add a Low Segment version number.
260	Fix ENDFILE so that it will close files correctly.
264	Use defaults for SITBOL's name, prefix and extension.
266	Put a JSP in .JB41 instead of a JRST.
273	Don't do the error 15  on a fail from DEVNAM at STIO7.
274	In STRIO a reference to IOPPN should actually be to IOSFD.
275	Append to the end of the listing file after requesting a listing
	following an EXIT().
276	Don't die if the nnnSNO.TMP file is protected when deleteing it.
277	Fix a ridiculous bug where TIME(1) returns the value that
	its given (1).  typo.
300	Allow EXIT(-1) to work on a KI-KL on EXO SITBOL.
301	In STRIO also check for identical PATH against other files.  This
	lack of a check caused two files with different PATH's but the same
	name to be treated as one.
303	Another bug in STRIO, PPN is checked in the wrong order, messing up
	check of files.
\
	SUBTTL	Dispatch table

;THE FOLLOWING DISPATCH TABLE HAS BEEN ADDED SO THAT .SAV (OR .EXE)
;FILES WILL REMAIN COMPATIBLE WITH FUTURE VERSIONS OF SITBOL.


	JOBHGH==:DSPTBL-.JBHDA	;[270]	MAKE SYMBOL JOBHGH

DSPTBL::			;START OF THE DISPATCH TABLE
REENTR:	PORTAL	$REENT		;REENTER ADDRESS
	FUNC	DATE$,1,E,DATES
	FUNC	DELET$,1,ES,DEL
	FUNC	DTCH$,1,E,DETCH
	FUNC	ENDFL$,1,E,ENDFIL
	FUNC	EXIT$,3,ES,EXIT0
	FUNC	FILE$,1,ES,FILE
	FUNC	INPUT$,3,ES,INPUT0
	FUNC	MTAPE$,3,ES,MTAPE0
	FUNC	OUTPT$,3,ES,OUTPT0
	FUNC	RENAM$,2,ES,RENAM
	FUNC	RSCN$,1,ES,RESCN
	FUNC	RWND$,1,ES,RWND
	FUNC	RUN$,2,ES,RUN0
	FUNC	TIME$,1,ES,TIME0
	FUNC	TTY$,1,ES,TTY
	FUNC	LOAD$,2,ES,LOAD##
EFCAL$::PORTAL	EFCALL##		;Call an external function
	FUNC	UNLD$,1,ES,UNLD##	;UNLOAD - unload an external function
	FUNC	KEYW$,1,,KEYW##		;Function KEYW
	FUNC	DATA$,1,E,DATA##	;DATA - PRIMARY FUNCTION
	FUNC	FIELD$,2,ES,FIELD##	;FIELD - PRIMARY FUNCTION
FLDRF$::PORTAL	FLDREF##		;REF TO FLD OF DEFINED DATA OBJ
	FUNC	DFN$,2,ES,DFN##		;DEFINE
	FUNC	APPLY$,,A,APPLY##	;APPLY
	FUNC	ARG$,2,ES,ARG##		;ARG
	FUNC	LOCAL$,2,ES,LOCAL##	;LOCAL
DFF$::	PORTAL	DFF##			;CALL TO DEFINED FUNCTION
	FUNC	STPTR$,2,ES,STOPTR##	;STOPTR
	FUNC	TRACE$,4,E,TRACE##	;TRACE FUNCTION
	FUNC	ASGN$,2,S,ASGN##	;ASSIGN FUNCTION
	FUNC	DUMP$,1,ES,DUMP##	;DUMP FUNCTION
ERRA$::	PORTAL	ERRA##			;APR ENTRY POINT
	FUNC	STXT$,1,E,STXT##	;SETEXIT FUNCTION
	FUNC	CLLCT$,2,ES,CLLCT##	;COLLECT FUNCTION
	FUNC	ALT$,2,ES,ALT##		;ALTERATIONS OF PATTTERNS
	FUNC	ARBNO$,1,E,ARBNO##	;ARBNO FUNCTION
	FUNC	IMVA$,2,S,IMVA##	;
	FUNC	CNVA$,2,S,CNVA##	;CONDITIONAL VARIABLE ASSOC.
	FUNC	MTCH$,2,S,MTCH##	;
	FUNC	ANY$,1,E,ANY##		;ANY
	FUNC	NTNY$,1,E,NTNY##	;NOTANY
	FUNC	BREAK$,1,E,BREAK##	;BREAK
	FUNC	BRKX$,1,E,BRKX##	;BREAKX
	FUNC	SPAN$,1,E,SPAN##	;SPAN
	FUNC	LEN$,1,E,LEN##		;LEN
	FUNC	POS$,1,E,POS##		;POS
	FUNC	RPOS$,1,E,RPOS##	;RPOS
	FUNC	RTAB$,1,E,RTAB##	;RTAB
	FUNC	TAB$,1,E,TAB##		;TAB
	FUNC	CURS$,1,,CURS##		;
	FUNC	CLEAR$,2,ES,CLEAR##	;CLEAR
	FUNC	DTTP$,1,E,DTTP##	;DATATYPE
	FUNC	FUNCT$,1,E,FUNCT##	;FUNCTION
	FUNC	TRIM$,1,E,TRIM##	;TRIM
IFE FTDDT,<
	FUNC	LABEL$,1,E,LABEL##	;LABEL
>
IFN FTDDT,<
	FUNC	LABEL$,2,ES,LABEL##	;LABEL
>
	FUNC	OPSYN$,3,ES,OPSYN##	;OPSYN
	FUNC	UNDEF$,,,UNDEF##	;[157] Undefined
	FUNC	CODE$,1,ES,CODE##	;Code function
	FUNC	ASCII$,1,E,ASCI.0##	;ASCII(n)
	FUNC	CAT$,2,ES,CAT##		;CAT - CONCATENATION
	FUNC	DFFR$,2,ES,DFFR##	;DIFFER
	FUNC	IDENT$,2,ES,IDENT0##	;IDENT - IDENTICAL
	FUNC	DUPL$,2,ES,DUPL##	;DUPL(s,n)
	FUNC	LEQ$,2,ES,LEQ##		;LEQ
	FUNC	LGE$,2,ES,LGE##		;LGE
	FUNC	LGT$,2,ES,LGT##		;LGT
	FUNC	LLE$,2,ES,LLE##		;LGE
	FUNC	LLT$,2,ES,LLT##		;LLT
	FUNC	LNE$,2,ES,LNE##		;LNE
	FUNC	LPAD$,,E,LPAD##		;LPAD
	FUNC	RPAD$,,E,RPAD##		;RPAD
	FUNC	RPLC$,3,ES,RPLC##	;REPLACE
	FUNC	RVRS$,1,E,RVRS##	;REVERSE
	FUNC	SIZE$,1,E,SIZE##	;SIZE
	FUNC	SBSTR$,3,E,SBSTR##	;SUBSTR
	FUNC	TABLE$,2,ES,TABLE##	;TABLE
	FUNC	ARRAY$,2,ES,ARRAY##	;ARRAY
	FUNC	ITEM$,,E,ITEM##		;ITEM
	FUNC	COPY$,1,E,COPY0##	;COPY
	FUNC	SORT$,2,ES,SORT##	;SORT
	FUNC	RSORT$,2,ES,RSORT##	;RSORT
	FUNC	EVAL$,1,E,EVAL##	;EVAL
	FUNC	NEG$,1,,NEG##		;NEGATE
	FUNC	QUEST$,1,E,RETNL1	;INTEROGATION ??
	FUNC	IND$,1,E,IND##		;IND
	FUNC	NAME$,1,,NAME##		;NAME
	FUNC	CNVRT$,3,E,CNVRT##	;CONVERT
	FUNC	GE$,2,SE,GE##		;.GE.
	FUNC	LE$,2,SE,LE##		;.LE.
	FUNC	GT$,2,SE,GT##		;.GT.
	FUNC	LT$,2,SE,LT##		;.LT.
	FUNC	EQ$,2,SE,EQ##		;.EQ.
	FUNC	NE$,2,SE,NE##		;.NE.
	FUNC	REMDR$,2,SE,REMDR##	;REMDR
	FUNC	INTGR$,1,SE,INTGR##	;INTEGER
	FUNC	RAND$,1,ES,RAND##	;RANDOM
	FUNC	SUM$,2,SE,SUM##	;SUM
	FUNC	DIFF$,2,SE,DIFF##	;DIFFER
	FUNC	MUL$,2,SE,MUL##		;MUL
	FUNC	DIV$,2,SE,DIV##		;DIV
	FUNC	EXP$,2,SE,EXP##		;EXP
	FUNC	PLUS$,1,SE,PLUS##	;PLUS
	FUNC	MINUS$,1,SE,MINUS##	;MINUS
	FUNC	PROTO$,1,E,PROTO##	;PROTOTYPE
CRTDT$::PORTAL	CRTDAT##		;[245]
TRCFN4::PORTAL	TRCFN3##		;[251]
IFN FTDDT,<
	FUNC	IBP$,3,ES,IBP##
	FUNC	RBP$,3,ES,RBP##
	FUNC	WHERE$,2,ES,WHERE0##
	FUNC	CNCT$,2,ES,CNCT##
	FUNC	DCNCT$,2,ES,DCNCT##
$DDCMD::PORTAL	DDTCMD##		;Entry for SNODDT trap

>;End of IFN FTDDT

IFN FTPAV,<
	FUNC	ASSC$,2,ES,ASSC##
	FUNC	DASSC$,2,ES,DASSC##
>;END OF IFN FTPAV

	SUBTTL	STORAGE INITIALIZATION

	 ; HERE FOR INCOMPATIBLE HIGH-LOW SEGMENTS

INITXX:	CLEAR	P2,		; NO &STNO OR &FNCLEVEL
	JRST	INITXY		; ESTABLISH MEMORY

	 ; HERE TO SIGNAL AN ERROR WHEN CORE IS IN A
	 ; CHAOTIC STATE. P1 CONTAINS THE NEGATIVE OF
	 ; AN ERROR NUMBER IN THE FORM 
	 ; MAJOR * 1000 + MINOR 

ABSERR::
	HRR	P2,$STNO##	; SAVE &STNO
	HRL	P2,$FNCLV##	; AND &FNCLEVEL
	 
	JSP	TLINK,CLSALL	; CLOSE ALL I/O
	JRST	INITXY		; LEAP INTO FORAY

	;[300]	Test high segment version here instead of in low
	;[300]	segment so concelled programs work.

INITR:	PORTAL	.+1		;[300] EXO ENTRY
	SKIPN	LISTF##		;[275] IS THERE A LISTING FILE?
	 JRST	HTEST		;[275] NO -- DON'T WORRY ABOUT IT.
	MOVX	Z,FMT.AP	;[275] APPEND MODE
	MOVEM	Z,LISTF+1	;[275] FOR LISTING FILE.

HTEST:	HLLZ	P1,HGHVER##	;[300] HIGH SEG VER
	HLLZ	P2,.JBHGH+.JBHVR	;[300] REAL HIGH SEG VERSION NO.
	CAMN	P1,P2		;[300] ARE THEY THE SAME?
	 JRST	INIT		;[300] IT'S OK.
	JRST	EXERRV(SURF)	;[300] NO GOOD, REPORT ERROR.

	 ; REENTER POINT - HERE WHEN A REENTER IS TYPED
	 ; AT THE TERMINAL FOLLOWING A ^C INTERRUPT

$REENT:	SKIPE	RSFLAG##
	JRST	$REE
	AOS	$TRACE##		; GIVE A TRACE MESSAGE
	EXCH	Q3,$STCNT##+1	; SAVE Q3
	SKIPN	Q3		; TOUCH NOT IF ALREADY SET
	MOVEI	Q3,NULLST##+1	; POINT TO 0
	EXCH	Q3,$STCNT+1	; FLAG TRACE AND RESTORE Q3
	JRSTF	@.JBOPC		; RESUME PROCESSING

$REE:	CLEARM	RSFLAG

	 ; HERE IS THE MAIN ENTRY POINT!
	 ; FLAG ENTRY

INIT:	PORTAL	.+2		;[300] TO CONCEAL THE HIGH SEG
	PORTAL	.+2		;[300] SAME
	TDZA	P1,P1		; ZERO OUT P1 AND SKIP
	MOVEI	P1,1		; ONE-IZE P1

	RESET			; RESET ALL I/O

	SKIPE	STFLAG##		; HAVE WE BEEN HERE BEFORE?
	 JRST	INITT		;  YES - SKIP OVER NEXT.

IFN FTOPS10,<			;[244]

	SKIPN	.SGDEV		;HAVE A DEVICE ?
	MOVSI	.SGDEV,'SYS'	;NO - ASSUME SYS:
	MOVEM	.SGDEV,SEGDEV##	;AND SAVE IT
	SKIPN	.SGNAM		;HAVE A NAME 
	 MOVE	.SGNAM,[D$NAME]	;[264] NO - ASSUME DEFAULT
	MOVEM	.SGNAM,SEGNAM##	;STORE IT TOO
	MOVEM	.SGPPN,SEGPPN##	;STORE THE PPN TOO

>;[244] End of IFN FTOPS10

	SETOM	STFLAG		; FLAG ENTRY.
INITT:	SKIPE	RSFLAG		; SAVE FILE?
	JRST	INITXW		; YES

	MOVE	Z,.JBHGH+.JBHVR	; Get high seg version no.
	MOVEM	Z,HGHVER##	; So the EXIT code will be able to test
				; segment compatabilty.

INITXY:	CLEARM	FID##		; ZERO FIRST WORD
	MOVE	T1,[FID,,FID+1]	; LOAD FROM-TO ADDR
	BLT	T1,AID-1	; ZERO OUT REST OF FID

	JSP	QLINK,INIMP##	; INITIALIZE IMPURE DATA

	JSP	SLINK,SETMEM	; ESTABLISH THE REST OF MEMEORY

INITXW:	 ; COMPUTE JOBNO IN 6-BIT, LEFT JUSTIFIED

	PJOB	T1,		; ESTABLISH ...
	MOVEM	T1,$JOBNO##	; &JOBNO

	MOVEI	T4,3		; 3 DIGITS
INIT1:	IDIVI	T1,^D10		; GET DIGIT TO T2
	ADDI	T2,'0'		; MAKE IT 6-BIT
	LSHC	T2,-6		; SHIFT INTO T3
	SOJG	T4,INIT1	; LOOP ON T4

	HLLZM	T3,JOBNO6##	; SAVE

	MOVEI	T1,REENTR	; ESTABLISH
	MOVEM	T1,.JBREN	; REENTER ADDR

	MOVE	T1,[JSP	Z,ERRA$] ;[266] PLANT
	MOVEM	T1,.JB41	; UUO HANDLER

	MOVEI	T1,INTRPT##	; POINT TO INTERRUPT HANDLER
	MOVEM	T1,.JBAPR	; PLUG JOB DATA AREA

	SKIPE	RSFLAG		; VALID SAVE FILE?
	JRST	FIREUP		; YES, OFF TO RESUME PROCESSING

	 ; CHECK FOR HIGH-LOW SEGMENT INCOMPATIBILITY

	JUMPGE	P1,INITXZ	; JUMP UNLESS AN ERROR ...
	SETOM	RSFLAG		; IS TO BE SIGNALLED.
	MOVN	P1,P1		; MAKE INTO A PROPER ERROR
	HRRZ	P1,P1		;[232] FOR ERROR PROCESS.
	JRST	ERRABS##		;   SIGNAL ERROR.
INITXZ:	 ; SET UP TRAP HANDLING

;[243] Here to get the processor type that we are executing on

	SETZ	T4,		;[243] Clear for the value
	JUMPPT	T1,CPDP6,CKA10,CKI10,CKL10 ;[243] Jump on the CP type


CPDP6:	ERROR	15		;[243] PDP-6 ??

CKL10:	ADDI	T4,1		;[243] Increment
CKI10:	ADDI	T4,1		;[243] Increment
CKA10:	ADDI	T4,1		;[243] Increment

;[243] At this point the number in T4 is the same that is found in the
;[243] name REL file block.
;[243]
;[243]	1 - KA10
;[243]  2 - KI10
;[243]  3 - KL10


	MOVEM	T4,CPUTYP##	;[243] Save the CPU number

	MOVX	Q3,AP.POV	; PUSH-OVERFLOW BIT
	MOVEM	Q3,TRFLGS##	; SAVE FLAG
	APRENB	Q3,		; ESTABLISH OVERFLOW


	CALLI	P2,14		; GET WHEN WE IS
	MOVEM	P2,DIM4##	; PRESERVE IT FOR PROSPERITY

DEFINE	EXPIRE(DAY,MONTH,YEAR),<
	RADIX	10
	EXPDAT==<<<YEAR-1964>*12+MONTH-1>*31+DAY-1>
	IFL	<31-DAY>!<12-MONTH>!<2001-YEAR>,<
	PRINTX	? Expiration date messed up>
	IFE	DAY!MONTH!YEAR,	EXPDAT==RH.ALF
	RADIX	8
	>

	EXPIRE	31,12,1999

IFE FTDEBUG,<
	CAIL	P2,EXPDAT	; ARE WE TOO LATE DOCTOR?
	ERROR	12,8		; YES, NOW DON'T BE TOO UPSET MISS.
>; End of IFN FTDEBUG
	SUBTTL	OPENING THE COMMAND STRING


	 ; OPEN XXXSNO.TMP (IF NECESSARY)

	JUMPE	P1,INIT2	; AVOID THIS FOR 0 ENTRY POINT

	SETOM	RSFLAG		; SET RESTART FLAG

	OPEN	17,CMSPEC	; OPEN IT ON CHANNEL 17
	ERROR	12,1		; NO DSK?

	HRRM	SURF,.JBFF	; ALLOCATE BUFFER IN FLID
	INBUF	17,1		; ONLY 1 BUFFER NEEDED

	MOVE	Q1,[1,,T1]	; TRY TMPCOR
	MOVSI	T1,D$PRE	;[264] NAME OF SITBOL
	MOVEI	T2,-1(SURF)		; BUFFER LOCATION
	HRLI	T2,-^D128	; NOMINAL BUFFER LENGTH
	TMPCOR	Q1,		; ISSUE TMPCOR UUO
	JRST	INIT1A		; TRY THE DISK

	 ; SET UP POINTER LENGTH

	IMULI	Q1,5		; CONVERT TO BYTE COUNT
	MOVEM	Q1,IGNOS##+2	; AND STORE IN BYTE COUNT

	MOVE	Q2,[POINT 7,0]	; SET ...
	HRR	Q2,SURF		; UP ...
	MOVEM	Q2,IGNOS##+1	; POINTER

	RELEAS	17,		;[230] Release this channel
	JRST	INIT3		; JOIN REGULAR PROCESSING

	
INIT1A:
	 ; COMPUTE NAME OF FILE

	MOVE	T3,JOBNO6	; GET JOBNO IN LEFT HALF
	HRRI	T3,D$PRE	;[264] COMPLETE THE FILE

	 ; NAME IS IN T3, COMPLETE FILE DESCRIPTION

	HRLZI	T4,'TMP'	; EXTENSION TO T4
	SETZB	Q1,Q2		;[276] Zero these words (don't use 2 setz's)

	LOOKUP	17,T3		; LOOK THIS FILE UP
	AOSA	RSFLAG		; NOT FOUND - CLEAR RSFLAG AND SKIP

	SOSA	TMPOPN##	; FLAG TMP FILE OPEN

	JRST	INIT		; TMP FILE NOT THERE, JUST RESTART
	JRST	INIT3		; CONTINUE

	 ; SPECIFIER TO OPEN COMMAND-STRING

CMSPEC:	EXP	1		; ASCII MODE
	SIXBIT/DSK/		; FROM THE DISK
	XWD	0,IGNOS##	; INPUT RINGS AT IGNOS

	 ; TTY INITIALIZATION

INIT2:	OUTCHR	["*"]		; OUTPUT A STAR

	 ; PREPARE DEPOSITORY FOR COMMAND STRING

INIT3:	HRRZ	T1,NSTACK##	; WILL BEGIN IN STACK REGION
	ADD	T1,[POINT 7,SCHARS+1] ; PREPARE DEPOSITION POINTER
	CLEAR	S3,		; BYTE COUNT
	CLEARM	IGNOP##		; FLAG TO INDICATE '=' SIGN


	SUBTTL	FIRST PASS OVER THE COMMAND


INIT4:	JSP	QLINK,CM.C	; PICK UP COMMAND CHAR IN Q3
	CAIN	Q3,"/"		; BEGIN SWITCH?
	JRST	IN.SW		; YES

	TXNE	Z,Y.EQ!Y.LA	; CHECK FOR '=' OR '_'
	SETOM	IGNOP##		; YES - SET FLAG

	TXNE	Z,Y.CZ!Y.VM!Y.ALT	; TEST FOR LINE DELIMETER
	JRST	INIT6		; YES - FINISH UP

	CAIN	Q3,15		; IGNORE ...
	 JRST	INIT4		; <CR>'S

INIT5:	IDPB	Q3,T1		; SAVE BYTE
	AOJA	S3,INIT4	; BUMP COUNT AND LOOP



	 ; SWITCH DETECTED

IN.SW:	JINK,CM.C	; GET NEXT CHAR
	CLEAR	T3,		; FLAG
	CAILE	Q3,"_"		; LOWER CASE?
	SUBI	Q3,40		; YES

	CAIN	Q3,"C"		; IS IT CORE?
	MOVE	T3,[^D4000,,C.PAR] ; LENGTH-LOCATION

	CAIN	Q3,"S"		; IS IT STACK?
	MOVE	T3,[^D1000,,S.PAR] ; LENGTH LOCATION

	CAIN	Q3,"V"		; IS IT VTABLE
	MOVE	T3,[^D131,,V.PAR] ; LENGTH-LOCATION

	CAIN	Q3,"H"		; HISTOGRAM?
	JRST	IN.SWH		; YES

	SKIPN	T3		; DID WE FIND ONE?
	ERROR	12,11		; NO - BAD SWITCH

	 ; SWITCH PARAMETER PROCESSING

	CLEAR	T4,		; WILL CONTAIN PARAMETER

IN.SW1:	JSP	QLINK,CM.C	; PICK UP NEXT CHARACTER

	TXNE	Z,Y.EQ!Y.LA!Y.CM!Y.ALT!Y.CZ!Y.VM!Y.BL!Y.TAB!Y.SLSH ; SWITCH DELIMETER
	JRST	IN.SW2		; YES

	TXNN	Z,Y.NUM		; NUMBER ?
	JRST	IN.SW1		; NO - JUST LOOP

	IMULI	T4,^D10		; PLACE FORCES MULTIPLICATION
	ADDI	T4,-"0"(Q3)	; ADD IN NUMERIC VALUE OF CHAR.

	JRST	IN.SW1		; LOOP BACK

	 ; END OF SWITCH

IN.SW2:	SKIPN	T4		; IF T4 HAS NOT BEEN PLUGGED...
	HLRZ	T4,T3		; USE DEFAULT VALUE

	ADDM	T4,0(T3)	; T3 ALSO HAS ADDRESS OF PARAM.

	JRST	INIT4+1		; RESUME SWITCH SEARCH

	 ; HISTOGRAM SWITCH

IN.SWH:	SETOM	AUTOHS##	; AUTO-HISTOGRAM FLAG
	JRST	IN.SW1		; GO BACK FOR MORE

	SUBTTL	RECONFIGURATION OF STORAGE

INIT6:	SKIPL	TMPOPN		; TMP FILE OPEN ?
	  JRST	INIT.7		; NO -- THEN CAN'T DELETE IT
	SETZ	T1,		;CLEAR
	RENAME	17,T1		;DELETE THE FILE
	 JFCL			;[276] We really don't care
	RELEAS	17,		;RELEASE CHANNEL
INIT.7:	HRRZ	P2,NSTACK##	; SAVE LOCATION OF
	ADDI	P2,1		; COMMAND STRING
	JSP	SLINK,SETMEM	; REESTABLISH MEMORY

	 ; MAKE COMMAND STRING AN SBLOK

	CLEARM	GCFLD(P2)	; CLEAR GC FIELD OF SBLOK
	MOVEI	T1,5*SCHARS+4(S3) ; COMPUTE # OF ...
	IDIVI	T1,5		; WORDS IN SBLOK
	HRLI	T1,SBLOK	; COMPLETE HEADER WORD
	MOVEM	T1,BTCODE&BLENG(P2)	; HEADER COMPLETE

	 ; MOVE SBLOK TO FLOATING STORAGE

	HRR	Q1,SURF		; TO ADDRESS 
	HRL	Q1,P2		; FROM ADDRESS
	MOVE	Q2,SURF		; SAVE SURFACE
	ADDI	SURF,0(T1)	; UPDATE SURFACE
	BLT	Q1,-1(SURF)	; MOVE	SBLOK

	 ; CLEAR VTABLE AND STACKS

	MOVE	T3,VTABLE##	; LOAD ADDRESS OF VTABLE
	CLEARM	0(T3)		; ZERO OUT FIRST WORD

	HRLI	T3,0(T3)	; THIS WILL BE THE FROM ADDRESS
	ADDI	T3,1		; MAKE TO ADDRESS
	MOVE	T4,GRID##		; LOAD GRID ADDRESS
	BLT	T3,-1(T4)	; ZERO OUT ALL UP TO GRID

	 ; PREPARE DESCRIPTOR FOR COMMAND STRING

	HRLI	Q2,SDT		; STRING DT
	PUSH	STACK,Q2	; PUSH FIRST WORD
	PUSH	STACK,S3	; AND 2ND WORD

	PUSH	STACK,Q2	; REPEAT FOR
	PUSH	STACK,S3	; &COMMAND KEYWORD

	JUMPN	S3,INIT.8	;[244]
	SKIPN	RSFLAG		;[244]
	 JRST	INIT		;[244] TRY AGAIN
IFN FTOPS10,EXIT		;[244] Return to the monitor
IFN FTOPS20,<
	HALTF			;[244] Return to the EXEC
	JRST	.-1		;[244] HALTF can continue
>; End of IFN FTOPS20
	SUBTTL	2ND PASS OVER THE COMMAND STRING


;	COMMAND STRING MINUS SWITCHES IS ON STACK.
;	INPUT FILES ARE PROCESSED FIRST SINCE THIS LEADS 
;	TO FEWER DIFFICULTIES. 


INIT.8:	JSP	TLINK,STINIT##	;[244] INITIALIZE STREAMING
	ERROR	15		; MUST BE STRING

	MOVE	S2,SYSCT##	; SEARCH FOR ...
	MOVX	S3,Y.EQ!Y.LA	; INPUT ...
	JSP	TLINK,STR.##	; FILES
	TDZA	P2,P2		; FLAG RUNNOUT
	JRST	INIT21		; NORMAL BRANCH

	PUSH	STACK,SUBJ##	; RELOAD ...
	PUSH	STACK,SUBJ##+1	; SUBJECT
	JSP	TLINK,STINIT	; BEGIN AT BEGINNING
	ERROR	15		; IMPOSSIBLE
	JRST	INIT22		; JOIN NORMAL PROCESSING

	 ; HERE IF = WAS DETECTED

INIT21:	SOS	CURSOR		; GET OVER ...
	IBP	STPTR		; CHARACTER

	MOVEI	P2,1		; FLAG CHAR.

	 ; MERGE HERE FROM NON = CASE

INIT22:	SETOM	IGNOS##		; YES WE WNAT FILE CHECKING
	JSP	SLINK,STFILS	; PROCESS INPUT
	ERROR	12,13		; FILES
	ERROR	12,30		; COULDN'T FIND FILE

	MOVE	S1,-1(STACK)	; LOAD STREAM
	MOVEM	S1,COMPIN##	; SET AS COMPILER INPUT
	MOVEM	S1,INFILE	; AND ALOS AS DATA INPUT
	SUB	STACK,XWD22##	; POP STACK

	MOVX	S1,FMT.N	; LET COMPILER ...
	MOVEM	S1,COMPIN##+1	; READ LINE NOS.



	 ; NOW PROCESS THE OUTPUT LIST

INIT23:	MOVE	T1,TTYBLK	; LOAD DEFAULT
	MOVEM	T1,OUTF		; OUTPUT FILE

	MOVEI	P1,OUTF-2	; POINT JUST BEFORE FIRST FILE
	JUMPE	P2,INIT25	; LEAP OVER IF NONE

	PUSH	STACK,SUBJ##	; RENEW ...
	PUSH	STACK,SUBJ##+1	; STREAMING ...
	JSP	TLINK,STINIT	; FROM ...
	ERROR	15		; BEGINNING

	MOVEI	P2,DFEXTS	; POINT TO DEFAULT EXT'S

	 ; LOOP TO PROCESS OUTPUT FILES

INIT24:	MOVE	T1,0(P2)	; LOAD DEFAULT EXT.
	MOVEM	T1,DFTEXT##	; SET IT UP
	ADDI	P2,1		; BUMP CURRENT DEFAULT

	JSP	TLINK,STRIO	; GET NEXT DESCR.
	ERROR	12,13		; BAD FILE

	ADDI	P1,2		; POINT TO NEXT BIN

	CAILE	P1,XERRF##	; BEYOND LAST?
	ERROR	12,14		; YES - GIVE MESSAGE

	SUB	STACK,XWD22##	; POP STACK
	SKIPE	T1,1(STACK)	; GET RETURNED IOBLOK
	MOVEM	T1,0(P1)	; BUT USE ONLY IF NONZERO

	CAIE	Z,"="		; LOOK FOR ASSIGNMENT CHAR
	CAIN	Z,"_"		; AND IF FOUND
	JRST	INIT25		; TAKE THIS BRANCH

	JRST	INIT24		; GET NEXT FILE

	 ; DEFAULT EXTENSIONS

DFEXTS:	SIXBIT	/LST/	; FILE 1
	SIXBIT	/LST/		; LISTING FILE
	EXP	0		; ERROR FILE

INIT25:	CLEARM	DFTEXT		; DONE WITH SETTING UP IO


	 ; IF /H SWITCH WAS SET ESTABLISH A LISTF FILE ANYWAY

	SKIPE	AUTOHS		; TEST SWITCH
	SKIPE	LISTF		; IF SWITCH OFF OR LISTF THERE
	JRST	INIT29		; TAKE THIS BRANCH

	JSP	QLINK,CLRFSV	; CLEAR OUT THE VARS.
	MOVSI	S2,'DSK'	; SET
	MOVEM	S2,DEVICE	; VARIOUS
	MOVE	S2,JOBNO6	; PAR-
	HRRI	S2,'HST'	;
	MOVEM	S2,FILNAM	; AMETERS
	MOVSI	S2,'TMP'	; AND
	MOVEM	S2,EXT		; GO
	JSP	TLINK,MAKEIO	; CREATE DSK:TMP.LST
	ERROR	15
	POP	STACK,LISTF+1	; MAKE
	POP	STACK,LISTF	; THE LISTING FILE


	 ; ESTABLISHING DEFAULT ASSOCIATIONS FOR
	 ;		 INPUT & OUTPUT


INIT29:	PUSH	STACK,SYSTR##	; STRING
	PUSH	STACK,S.INP##	; 'INPUT'

	PUSH	STACK,INFILE	; IO
	PUSH	STACK,[0]	; DESCRIPTOR

	MOVE	Z,[IABLOK,,TVIN] ; TELL IT INPUT
	JSP	SLINK,IOASSN	; MAKE THE CALL


	PUSH	STACK,SYSTR	; STRING
	PUSH	STACK,S.OUT##	; 'OUTPUT'

	PUSH	STACK,OUTF	; IO
	PUSH	STACK,[0]	 ; DESCRIPTOR

	MOVE	Z,[OABLOK,,TVOUT] ; TELL IT OUTPUT
	JSP	SLINK,IOASSN	; MAKE THE CALL
	
	 ; IO ASSOCIATIONS FOR TTY

	PUSH	STACK,SYSTR	; STRING
	PUSH	STACK,S.TTY##	; 'TTY'
	PUSH	STACK,TTYBLK	; IOBLOK
	PUSH	STACK,[0]	; FOR TTY
	CLEAR	Z,		; NO CHECK NEEDED
	JSP	SLINK,CVTSIO	; MAKE A STREAM OUT OF IT
	ERROR	15		; THIS CAN'T FAIL
	ERROR	15		; THIS CAN'T FAIL EITHER
	MOVE	Z,[IABLOK,,TVIN] ; TELL IT INPUT
	JSP	SLINK,IOASSN	; MAKE THE CALL

	PUSH	STACK,SYSTR	; STRING
	PUSH	STACK,S.TTY##	; 'TTY'
	PUSH	STACK,TTYBLK	; IOBLOK
	PUSH	STACK,[0] 	; FOR TTY
	MOVN	Z,[OABLOK,,TVOUT]	; TELL IT OUTPUT
	JSP	SLINK,IOASSN	; MAKE THE CALL

	JRST	ISTART##	; LET INT HAVE CONTROL


	 ; AFTER THE PROGRAM IS DONE, CONTROL COMES HERE


CLOSIO::MOVSI	Z,(CLOSE)	; LOAD UP A CLOSE
	MOVSI	Q1,(RELEAS)	; AND LOAD UP A RELEASE
	MOVSI	Q3,(1B12)	; LOAD A 1 IN THE AC FIELD
	MOVEI	Q2,20		; # OF CHANS
CLOSLP:
	XCT	Z		; DO A CLOSE
	ADD	Z,Q3		; DO THE NEXT CHAN

	XCT	Q1		; THEN RELEASE IT
	ADD	Q1,Q3		; INCREMENT FOR NEXT

	SOJG	Q2,CLOSLP	; LOOP FOR MORE

	 ; NOW GO BACK OR EXIT


	SKIPN	RSFLAG##		; DID HE RESTART HIMSELF
	JRST	INIT		; NO - GIVE A STAR

IFN FTOPS10,EXIT		;[244] Give an EXIT

IFN FTOPS20,<
	HALTF			;[244] Give an EXIT
	JRST	.-1		;[244] This is continable
>;[244] End of IFN FTOPS20


	SUBTTL	STORAGE CONTROL

;	STORAGE IS ORDERED AS FOLLOWS:
;	FID - FIXED IMPURE DATA
;	AID - ADJUSTABLE IMPURE DATA
;		VTABLE - VARIABLE BLOCK TABLE
;		STACKS
;			NAME STACK
;			HISTORY STACK
;			SYSTEM STACK
;	GRID - GROWING STORAGE
;	FLID - FLOATING STORAGE

	 ; THE FOLLOWING TABLE GIVES EXCESS PERCENTAGES
	 ; AND MINIMUM VALUES.

EMTBL:	RADIX	10
EM.VT:	XWD	14,37		; VARIABLE BLOCK TABLE
EM.ST:	XWD	10,550		; STACKS
EM.GR:	XWD	20,550		; GROWING REGION
EM.FL:	XWD	56,550		; FLOATING REGION

	 ; THE STACK BREAK-DOWN FOLLOWS

	SS%==60		; SYSTEM STACK
	HS%==25		; HISTORY STACK
	NS%==15		; NAME STACK

	RADIX	8


;	JSP	TLINK,SETMEM
;	RETURN
;
;	WILL SET UP MEMORY INITIALIZING 
;	VARIABLES LIKE SURF, CEIL, ETC.	AFTER THIS
;	CALL THE AID, GRID AND FLID REGION WILL BE
;	READY TO USE.
;
;	THROUGH IT ALL THE AID AND FLID REGIONS ARE
;	LEFT UNDISTURBED. AN ENTRY IS MADE IN THE GRID
;	REGION, VIZ. AN IOBLOK FOR TTY (SETTING UP AN 
;	IO DESCRIPTOR FOR XERRF. THUS ERROR N,M WILL WORK
;	AFTER SETMEM IS CALLED.

	 ; THE SIZES OF THE VARIOUS REGIONS ARE 
	 ; COMPUTED IN REGISTERS. AS FOLLOWS
	 ; VTABLE (T2), STACKS(T3), GRID(T4) AND FLID(Q1)

SETMEM:	SKIPN	T2,V.PAR##	; USE V PARAMETER FOR VTABLE
	HRRZ	T2,EM.VT	; ELSE USE MINIMUM SIZE

	SKIPN	T3,S.PAR##	; USE S PARAMETER FOR STACKS
	HRRZ	T3,EM.ST	; ELSE USE DEFAULT SIZE
	CAIGE	T3,^D256	; BUT IF LESS THAN 256
	IMULI	T3,2000		; HE MUST MEAN K

	HRRZ	T4,EM.GR	; DEFAULT SIZE OF GRID

	HRRZ	Q1,EM.FL	; DEFAULT SIZE OF FLID



	 ; REGISTERS NOW CONTAIN MINIMUM VALUES
	 ; WHAT TO DO WITH THE EXCESS?
	 ; FIRST COMPUTE TOTAL

	MOVEI	Q2,1(T2)	; LOAD VTBL SIZE
	ADD	Q2,T3		; ADD STACK SIZE
	ADD	Q2,T4		; ADD GRID SIZE
	ADD	Q2,Q1		; ADD FLID SIZE
	MOVEM	Q2,IGNOT##	; REQUIRED CORE TO IGNOT

	MOVE	Q3,C.PAR##	; LOAD THE C PARAMETER
	CAIG	Q3,^D256	; IF LESS THAN 256 ...
	IMULI	Q3,2000		; USE K


	 ; IF Q3 IS GREATER THAN Q2 DISTRIBUTE EXCESS

	CAMG	Q3,Q2		; GREATER?
	JRST	SETMM1		; NO - USE WHAT WE HAVE

	MOVEM	Q3,TOOMCH##	; UP THE SLACK VALUE
	MOVEM	Q3,IGNOT##	; REQUIRED CORE TO IGNOT
	SUB	Q3,Q2		; FIND EXCESS
	MOVS	Q3,Q3		; INTO LEFT FOR GREATER PRECISION
	IDIVI	Q3,^D100	; CONVERT TO % UNITS
	MOVEM	Q3,IGNOQ##	; SAVE THIS QUANTITY

	MOVEI	Q2,3		; # OF SLICES MINUS 1

	 ; LOOP TO DISTRIBUTE EXCESS

SETMM2:	HLRZ	Q3,EMTBL(Q2)	; LOAD EXCESS %
	IMUL	Q3,IGNOQ##	; MULTIPLY BY EXCESS
	HLRZ	Q3,Q3		; MOVE TO RIGHT HALF
	ADDM	Q3,T2(Q2)	; INCREMENT
	SOJGE	Q2,SETMM2	; LOOP ON Q2

	 ; ALLOCATE ADDITIONAL CORE IF NEEDED

SETMM1:	MOVE	Q2,IGNOT##	; GET TOTAL LENGTH OF REGION
	ADDI	Q2,AID##	; ADD LENGTH OF FID
	MOVEM	Q2,IGNOT##	; SAVE TOTAL
	CORE	Q2,		; ORDER THIS MUCH CORE
	ERROR	12,10		; TOO MUCH


	 ; ESTABLISH FLID

	MOVE	Q2,IGNOT##	; RELOAD LAST ADDRESS
	SUBI	Q2,MARGIN	; POINT JUST BELOW SKY
	MOVEM	Q2,CEIL##		; ESTABLISH AS CEILING
	SUBI	Q2,-MARGIN(Q1)	; DEDUCT SIZE OF FLID
	MOVEM	Q2,FLOOR##	; ESTABLISH AS FLOOR
	MOVEM	Q2,SURF		; AND SURFACE OF FLOATING REG.

	 ; ESTABLISH GRID

	SUB	Q2,T4		; DEDUCT SIZE OF GRID
	MOVEM	Q2,GRID		; BASE OF GRID
	MOVEM	Q2,GSURF##	; ALSO SURFACE OF GRID

	 ; ESTABLISH STACKS

	MOVEI	Z,SS%		; % THAT IS SYSTEM STACK
	JSP	QLINK,SETSTK	; GO CREATE A STACK
	MOVEM	Z,STACK		; THIS IS SYSTEM STACK
	MOVEM	Z,STBASE##	; SAVE BASE FOR GC

	MOVEI	Z,HS%		; HISTORY STACK CUT
	JSP	QLINK,SETSTK	; GO MAKE STACK
	MOVEM	Z,HSTACK##	; GO ESTABLISH HISTORY STACK
	MOVEM	Z,HSBASE##	; AND BASE
	MOVEM	Z,HSR		; SET UP HISTORY STACK REG.

	MOVEI	Z,NS%		; NAME STACK PERCENT
	JSP	QLINK,SETSTK	; GO SET UP A STACK
	MOVEM	Z,NSTACK##	; ESTABLISH AS ...
	MOVEM	Z,NSBASE##	; NSTACK

	 ; ESTABLISH VTABLE##

	MOVEM	T2,LVTBL##	; INSERT LENGTH OF VTABLE
	SUBI	Q2,0(T2)	; POINT TO START OF VTABLE
	MOVEM	Q2,VTABLE	; INSERT ADDRESS
	MOVEM	T2,-1(Q2)	; INSERT LENGTH AT TOP OF VTABLE


	 ; ESTABLISH TTY FOR ERRORS DURING
	 ; COMMAND STRING PROCESSING


	CLEARM	IOBLKS##		; START CHAIN ANEW
	CLEARM	TTYBLK##		; ZERO OUT OLD BLOK

	JSP	QLINK,CLRFSV	; CLEAR VARIABLES
	MOVSI	S2,'TTY'	; SET ...
	MOVEM	S2,DEVICE	; DEVICE

	JSP	TLINK,MAKEIO	; MAKE THE FILE
	ERROR	15		; THIS CAN'T HAPPEN

	POP	STACK,Z		; POP JUNK
	POP	STACK,T4	; POP IOBLOK ADDRESS

	MOVEM	T4,TTYBLK	; ESTABLISH AS TTY
	MOVEM	T4,XERRF##	; AND DEFAULT ERROR FILE

	AOS	IOMODE(T4)	; INSERT ASCII MODE
	MOVEI	Z,-1		; SET
	HRLM	Z,IOTTY(T4)	; FLAG INDICATING TTY

	JRST	0(SLINK)	; RETURN


;	SETSTK - USED BY SETMEM TO SET UP A STACK POINTER
;
; CALLING SEQUENCE:
;	LOAD Z WITH PERCENTAGE OF TOTAL STACK LENGTH
;	Q2 POINTS JUST NORTH OF THE STACK
;	T3 CONTAINS TOTAL ALLOTMENT FOR STACKS
;	JSP	QLINK,SETSTK
;	RETURN - Z WILL CONTAIN CORRECT VALUE
;		- Q2 IS UPDATED

SETSTK:	IMUL	Z,T3		; MULTIPLY % BY LENGTH
	MOVE	Q3,Z		; MOVE TO AVOID CLOBBERING REG. 1
	IDIVI	Q3,^D100	; DIVIDE TO OBTAIN LENGTH

	MOVN	Z,Q3		; NEED NEG. OF LENGTH
	HRL	Z,Z		; TO Z

	SUBI	Q2,0(Q3)	; UPDATE Q2
	HRRI	Z,-1(Q2)	; AND POINT TO BASE - 1

	JRST	0(QLINK)	; RETURN


;	CM.C - COMMAND CHARACTER, READ A
;
; CALLING SEQUENCE:
;	JSP	QLINK,CM.C
;	RETURN - Q3 CONTAINS NON ZERO CHARACTER
;		 - Z CONTAINS MASK FROM SYSCT

CM.C:	JUMPE	P1,CM.C2	; JUMP IF TTY INPUT
	SOSGE	IGNOS+2		; DECREMENT THE BUFFER COUNT
	JRST	CM.C3		; FILL BUFFER IF EMPTY
	ILDB	Q3,IGNOS+1	; RETRIEVE NEXT BYTE FROM BUFFER

	 ; HERE AFTER TTY INPUT OF CHARACTER

CM.C1:	JUMPE	Q3,CM.C		; IGNORES 0'S

	CAIN	Q3,32		;A ^Z ???
	SETOM	RSFLAG		;YUP EXIT  WHEN DONE
	MOVE	Q2,SYSCT##	; LOAD ADDRESS OF CHAR. TABLE
	ADDI	Q2,CTBITS(Q3)	; POINT TO MASK
	MOVE	Z,0(Q2)		; LOAD Z WITH MASK

	JRST	0(QLINK)	; RETURN

	 ; TTY IN

CM.C2:	INCHWL	Q3		; LOAD CHAR.
	JRST	CM.C1		; JOIN PROCESSING
	 
	 ; HERE TO FILL BUFFER IF EMPTY

CM.C3:	SKIPL	TMPOPN##	;[230] TMP file open ?
	 JRST	CM.C4		;[230] No - Force a line feed
	IN	17,		; INPUT A BUFFER
	JRST	CM.C		; NORMAL

	MOVEI	Q3,12		; LINEFEED IF EOF
	GETSTS	17,Q2		; TEST FOR ...
	TXNN	S2,IO.EOF	;[230] End Of File ?
	 ERROR	12,		;[230] Yes - Serious error

CM.C4:	MOVX	Q3,.CHLFD	;[230] Get the line feed
	JRST	CM.C1		;[230] Return
	SUBTTL P (PRIMARY) ROUTINES FOR I/O

;	DATE() - MM/DD/YY

;	DETACH(NAME)

;	ENDFILE(FILE-SPEC)

;	EXIT()

;	FILE(FILE-SPEC-SEQUENCE)

;	INPUT(NAME, FILE-SPEC-SEQUENCE, FORMAT)

;	MTAPE(FUNCTION, FILE-SPEC)

;	OUTPUT(NAME, FILE-SPEC, FORMAT)

;	RENAME(FILE-SPEC, FILE-SPEC)

;	RESCAN()

;	REWIND(FILE-SPEC-SEQUENCE)

;	RUN(FILE-SPEC, OFFSET)

;	TIME(N)

	SUBTTL	Primary routines -- DATE

;	DATE()

DATES::	SUB	STACK,XWD22##	; POP USELESS ARGUMENT
DATEE::
IFN FTOPS10,DATE	P2,		;[244] PLACE CALLI IN P2

	IDIVI	P2,^D31		; DAYS ...
	AOJ	S1,		; TO S1
	MOVEM	S1,IGNOP	; SAVE DAYS

	IDIVI	P2,^D12		; MONTHS ...
	AOJ	S1,		; TO S1
	MOVEM	S1,IGNOP+1	; SAVE

	ADDI	P2,^D64		; ADD '64 TO PRODUCE YEARS

	JSP	SLINK,LINE.##	; PREPARE STRING DATE
	XWD	0,5		; 5 ARGS
	XWD	IDT,IGNOP+1	; MONTH
	XWD	SDT,["/",,1]	; /
	XWD	IDT,IGNOP	; DAY
	XWD	SDT,["/",,1]	; /
	XWD	IDT,P2		; YEAR

	JRST	1(PLINK)	; SUCCESSFUL RETURN

	SUBTTL	Primary routines -- DELETE

;	DELETE(FILE-SPEC)

DEL:	SETZ	Z,		; USE ASCII FORMAT
	JSP	P2,FCHECK	; DOES THE FILE EXIST?
	JRST	0(PLINK)	; NO - FAIL

	JSP	QLINK,CLRFSV	; CLEAR AN AREA
	MOVE	Q3,IOCHAN(T4)	; LOAD A CHANNEL
	IOR	Q3,[RENAME FILNAM] ; COMPLETE THE INSTRUCTION
	XCT	Q3		; DELETE THE FILE
	ERROR	12,16		; TELL USER IF TROUBLE DEVELOPS

	JSP	QLINK,CLS	; RELEASE THE CHANNEL

	JRST	RETNUL##		; AND RETURN


	SUBTTL	Primary routines -- DETACH

;	DETACH(FILE-SPEC)

DETCH:	JSP	TLINK,CVTNAM	; ENSURE A NAME
	ERROR.	1,62		; BAD ARG TO DETACH

	JSP	TLINK,DTCH	; CALL SERVICE ROUTINE
	JRST	RETNL1##	; POP DESCR. AND RETURN NULL


	SUBTTL	Primary routines -- ENDFILE

;	ENDFILE (FILE-SPEC)

ENDFIL:	JSP	SLINK,CVTIO	; GET THE IOBLOK
	ERROR	12,13		; BAD SPEC

	POP	STACK,Z		; POP
	POP	STACK,T4	; ARG
	JSP	QLINK,CLS	; AND CLOSE THE FILE

	JRST	RETNUL##		; RETURN THE NULL STRING

	SUBTTL	Primary routines -- EXIT

EXIT0:	MOVX	Z,FMT.S!FMT.WM	;CONVERT TO SIXBIT
	JSP	TLINK,CVTDW	;CONVERT THE THIRD ARG TO SIXBIT
	  JFCL			; STRING TOO LONG - BUT WHO CARES
	SKIPE	Z		;DON'T SET IT IF ZERO
	SETNAM	Z,		;SET THE NAME
	JSP	TLINK,CVTINT##	; INSURE AN INTEGER
	ERROR.	1,59		; WRONG TYPE

	POP	STACK,T1	; GET THE SECOND ARGUMENT
	POP	STACK,Z		; AND GET RID OF DATATYPE
	JUMPE	T1,EXIT1	; ZERO SECOND ARGUMENTS DO NOTHING

	 ; HERE FOR NON-ZERO SECOND ARGUMENT.  BREAK THE BACKWARDS
	 ; LINK IN THE CURRENT CODE BLOCK TO FLUSH SOME CODE.

	MOVE	T1,ILOC##	; GET CURRENT CODE BLOCK ADDRESS
	CLEARM	LCLINK(T1)	; AND CLEAR THE LCLINK FIELD

	 ; MERGE HERE TO PROCESS THE NORMAL FIRST ARGUMENT

EXIT1:	JSP	TLINK,CVTINT	; ENSURE AN INTEGER FIRST ARGUMENT
	ERROR.	1,59

	JSP	SLINK,CGCOL##	; COMPLETE GC

	JSP	QLINK,MSTIME	; SAVE TIME OFF
	MOVEM	Z,TIME.E##	; HERE

	MOVE	Z,[1,,IGNOP]	; SAVE
	BLT	Z,IGNOP+3	; KEY REGISTERS

	SKIPG	0(STACK)	; EXIT 1, TYPE OF EXIT?
	JRST	EXITLE		; NO, MAKE FURTHER TESTS

	 ; EXIT(1) 

IFN FTOPS10,EXIT	1,	;[244] YES, MAKE QUICK EXIT!
IFN FTOPS20,HALTF		;[244] Yes - Make quick exit
	JRST	FIRE.1	; BACK AGAIN? GO RECOVER
		
	 ; HERE FOR EXIT(-1) AND EXIT(0)

EXITLE:	JSP	TLINK,CLSALL	; CLOSE OUT ALL I/O
	
EXIT2:	SETOM	RSFLAG		; AVOID RECYCLING

	 ; COMPRESS STORAGE

	CLEARM	0(SURF)		; BEGIN TO CLEAR ...
	HRLI	Z,0(SURF)	; UNALLOCATED REGION
	HRRI	Z,1(SURF)	; SO THAT ZERO-COMPRESS
	MOVE	T1,CEIL		; IS EFFECTIVE
	BLT	Z,MARGIN(T1)	; CLEAR IT OUT
	 ; NOW SET JOB DATA AREA

	ADDI	T1,MARGIN	; POINT TO (CEIL)+MARGIN
	HRLM	T1,.JBSA	;[175]  set up .JBSA so reset works
	MOVEM	T1,.JBFF	;[175] set .JBFF for save

	SKIPE	0(STACK)	; WAS ARG NEGATIVE?
	JRST	EXITL		; YES

	 ; EXIT(0)

	EXIT			; ISSUE THE EXIT

	;-----;
	
	 ; HERE FOR RECOVERY FROM EXIT(0) AND EXIT(-1)

FIREUP:	RESET			; RESET I/O

FIRE.1:	MOVE	Q1,TRFLGS	; GET THE TRAPPING FLAGS
	APRENB	Q1,		; ENABLE THE TRAPS

	MOVE	Z,[IGNOP,,1]	; RESTORE
	BLT	Z,4		; KEY REGISTERS

	JSP	QLINK,MSTIME	; AUGMENT ALL
	SUB	Z,TIME.E	; TIMES BY AMOUNT SPENT
	ADDM	Z,TIME.C##	; IN HIBERNATION
	ADDM	Z,TIME.S##	;

	JRST	RETNL1##	; NOW RETURN

	 ; EXIT(-1)

EXITL:	MOVEI	Z,0(SURF)	; 'TO' ADDRESS
	HRLI	Z,ADDRN		; 'FROM' ADDRESS
	BLT	Z,RNLENG(SURF)	; MOVE NUCLEUS PROTOTYPE

	HRRM	SURF,EXRECO+1(SURF)	; SAVE RECOVERY ADDRESS

	MOVEI	T1,EXRECO(SURF)	; STARTUP ADDRESS
	HRRM	T1,.JBSA	; SET ENTRY POINT

	MOVSI	P2,1		; HIGH SEG DELETE WORD
	JRST	0(SURF)		; JUMP INTO LOWER CORE 

	 ; RESTART NUCLEUS

ADDRN:	PHASE	0		; ADDRESS OF RN PROTOTYPE
	CORE	P2,		; DELETE HIGH SEGMENT
	ERROR	15
	EXIT			; AND EXIT
EXRECO:	JFCL			; NOOP
	MOVEI	SURF,.-.	; SELF-BASING INSTRUCTION
	MOVEM	SURF,IGNOQ	;SAVE THE SURF
	MOVEI	P1,SEGDEV	;POINT TO SYSTEM DESIRED
	GETSEG	P1,		; PULL HIGH SEGMENT IN
	ERROR	15		; SOME SORT OF ERROR
	MOVE	SURF,IGNOQ	;RETRIEVE THE SURF
	MOVEI	P1,LOWVER	;[255] LOW SEGMENT VERSION NUMBER
	CAME	P1,LWVRSN##	;[255] IS IT THE SAME?
	 JRST	EXERRV(SURF)	;[255] REPORT AN ERROR

	JRST	INITR		;[300] LET THE HIGH SEG TEST VERSION
				;[300] COMPATABILITY.

EXERRV:	HRROI	P1,-^D12007	; FLASH THIS ERROR
	RESET			; AND RESET IO
	JRST	INITXX		; MESSAGE

	DEPHASE
RNLENG==.-ADDRN		;LENGTH OF THE BLOCK
	SUBTTL	Primary routines -- FILE

;	FILE(FILE-SPEC)

FILE:	SETOM	Z		; WE WANT FILES CHECKED
	JSP	SLINK,CVTSIO	; MAKE A STREAM
	JRST	0(PLINK)	; FAIL FOR BAD SPEC
	JRST	FRET1##		; FAIL FOR NO FIND
	JRST	RETNL1		; SUCCEED IF ALL GOES WELL


	 ; FCHECK - FILE CHECK
	 ; CALLING SEQUENCE:

	 ; MOVE FORMAT WORD TO Z
	 ; PUSH POTENTIAL STRING ONTO STACK
	 ; JSP	P2,FCHECK
	 ; FILE-NOT-FOUND-RETURN, T4 POINTS TO IOBLOK
	 ; FILE-FOUND-RETURN - T4 POINTS TO IOBLOK
	 ;		(FILE IS LEFT OPEN)

	 ; STACK POPPED IN ALL CASES

FCHECK:	MOVEM	Z,IGNOP+3	; SAVE  FORMAT WORD
	SKIPE	0(STACK)	; NULL STRING?
	JRST	FILE2		; NO, GO ON
	SUB	STACK,XWD22##	; YES, POP AND
	JRST	0(P2)		; AND FAIL

FILE2:	JSP	SLINK,CVTIO	; OBTAIN INDICATED FILE
	ERROR	13,69		; BAD ARGUMENT

	POP	STACK,Z		; FORMAT WORD
	POP	STACK,T4	; IOBLOK POINTER
	SKIPN	T4		; WAS A FILE FOUND
	ERROR	13,69		; BAD SPEC

	SKIPE	IOCHAN(T4)	; IS IT OPEN NOW?
	JRST	1(P2)		; YES - RETURN

	MOVE	Z,IGNOP+3	; RETRIEVE FORMAT WORD
	JSP	QLINK,MODE	; DETERMINE THE MODE
	JSP	QLINK,OPN	; OPEN THE FILE
	ERROR	12,28		; NO OPEN? HIS ERROR

	JSP	QLINK,LKP	; LOOK THE FILE UP
	JRST	FILE1		; COULDN'T FIND IT


	JRST	1(P2)		; AND RETURN

	 ; HERE IF THE FILE COULDN'T BE FOUND

FILE1:	JSP	QLINK,CLS	; RELEASE THE CHANNEL
	JRST	0(P2)		; AND FAIL

	SUBTTL	Primary routines -- INPUT

;	INPUT(NAME, FILE-SPEC-SEQUENCE, FORMAT)

INPUT0:	JSP	SLINK,CVTFMT	; CONVERT CHARACTERS TO BITS
	MOVEM	Z,P2		; SAVE FORMAT

	CLEAR	Z,		; WE DON'T WANT A FILE CHECK
	JSP	SLINK,CVTSIO	; CONVERT 2ND ARG TO STREAM
	ERROR	12,13		; BAD SPECIFICATION
	ERROR	15		; CAN'T TAKE THIS ONE
	MOVEM	P2,0(STACK)	; INSERT FORMAT

	MOVE	P2,INFILE	; INSERT
	SKIPN	-1(STACK)	; DEFAULT FILE
	MOVEM	P2,-1(STACK)	; IF NECESSARY

	MOVE	Z,[IABLOK,,TVIN]; ARGUMENTS FOR ...
	JSP	SLINK,IOASSN	; ASSOCIATION ROUTINE

	JRST	RETNUL##		; RETURN NULL STRING
	SUBTTL	Primary routines -- MTAPE

;	MTAPE(FUNCTION, FILE-SPEC, FORMAT)
;		<FORMAT IS OPTIONAL, ASCII ASSUMED IF NOT SPECIFIED>




MTAPE0::JSP	SLINK,CVTFMT		; GET FORMAT WORD
	JSP	P2,FCHECK		; GET A CHANNEL #
	JFCL				; WE DONT GIVE A DAMN
	MOVE	S1,T4			; SAVE IOBLOK ADDR
	JSP	TLINK,CVTINT##		; RETRIEVE FUNCTION #
	ERROR	13,9			; BAD 1ST ARG
	POP	STACK,P2		; SAVE VALUE OF FUNCTION
	CAIL	P2,0			;IF LT 0
	CAILE	P2,6			;OR GT 6
	ERROR	13,9			;THEN 1ST ARG OUT OF RANGE
	MOVE	S2,IOCHAN(S1)		; GET CHANNEL IN BITS 9-12
	LSH	S2,-27			; RIGHT JUSTIFY THEM
	DEVCHR	S2,			; WHAT DEVICE
	TLNN	S2,20			; DO WE HAVE A MAGTAPE
	JRST	0(PLINK)			; NO - FAIL
	MOVE	S2,MTABLE(P2)		; GET MTAPE # FROM TABLE
	IOR	S2,IOCHAN(S1)		; OR IN THE CHANNEL #
	IOR	S2,[MTAPE 0,0]		; COMPLETE THE INSTRCTION
	XCT	S2			; DO IT
	HLLZ	S2,S2			; CLEAR OUT FUNCTION
	XCT	S2			; WAIT FOR COMPLETION
	JRST	RETNUL##		;BRING NULL-STRING HOME TO MOMMA


; MTAPE FUNCTION TABLE FOLLOWS.  ITS USED TO CONVERT THE SITBOL
; MTAPE FUNCTION NUMBERS TO STANDARD DEC MONITOR CODES

MTABLE:	EXP	1			; REWIND
	EXP	3			; WRITE A EOF MARK
	EXP	6			; SKIP A RECORD
	EXP	7			; BACKSPACE A RECORD
	EXP	10			; SPACE TO LOGICAL EOT
	EXP	16			; SKIP 1 FILE
	EXP	17			; BACKSPACE 1 FILE

	SUBTTL	Primary routines -- OUTPUT

;	OUTPUT(NAME, FILE-SPEC, FORMAT)

OUTPT0:	JSP	TLINK,CVTSTR##	; ENSURE 3RD ARGUMENT
	ERROR.	1,71		; IS A STRING

	JSP	SLINK,CVTFMT	; CONVERT 3RD ARG TO FORMAT
	MOVEM	Z,P2		; SAVE RESULT IN P2

	JSP	SLINK,CVTIO	; CONVERT 2ND ARG TO FILE
	ERROR	12,13		; BAD 2ND ARG
	MOVEM	P2,0(STACK)	; INSERT FORMAT TO COMPLETE DESCR.

	MOVE	Z,[OABLOK,,TVOUT]	; LOAD ARGS

	MOVE	P2,OUTF##	; INSERT DEFAULT
	SKIPN	-1(STACK)	; OUTPUT FILE
	MOVEM	P2,-1(STACK)	; IF NEEDED
	JSP	SLINK,IOASSN	; FOR COMMON ASSN FUNCTION

	JRST	RETNUL##		; RETURN

	SUBTTL	Primary routines -- RENAME

;	RENAME(FILE-SPEC,FILE-SPEC)

RENAM:	SETZ	Z,		;  CLEAR FORMAT WORD
	JSP	P2,FCHECK	; CHECK 2ND FILE
	JRST	FRET1##		; NOT THERE? FAIL
	MOVEM	T4,IGNOP##	; SAVE RETURNED VALUE

	MOVE	Q1,-1(STACK)	; IS THE FIRST
	CAMN	Q1,NULLST	; ARGUMENT NULL?
	JRST	FRET1##		; YES, FAIL

	SETZ	Z,		; CLEAR FORMAT TO ASSUME ASCII
	JSP	P2,FCHECK	; IS FIRST FILE THERE?
	JRST	RENAM1		; NO - GOOD
	JSP	QLINK,CLS	; CLOSE IT UP
	JRST	0(PLINK)	; AND FAIL
	
	 ; HERE IF FIRST FILE IS NOT THERE
	 ; AND 2ND FILE IS THERE AND OPEN

RENAM1:	MOVE	P2,IGNOP##	; OBTAIN
	MOVE	T3,IOCHAN(P2)	; CHANNEL THAT'S OPEN
	IOR	T3,[RENAME IONAME(T4)] ; OR IN INSTR
	XCT	T3		; RENAME THE FILE
	ERROR	12,3		; COULDN'T RENAME

	MOVE	T4,P2		; PICK UP OLD BLOK
	JSP	QLINK,CLS	; AND CLOSE IT OUT

	JRST	RETNUL##		; AND RETURN

	SUBTTL	Primary routines -- RESCAN

;	RESCAN()

RESCN:	RESCAN	1		; RESCAN THE INPUT LINE
	SKPINL			; IS ANYTHING THERE ?
	JRST	FRET1##		; NOPE, JUST FAIL

	SUB	STACK,XWD22##	; PRUNE STACK

	MOVEI	Z,^D300		; LARGEST STRING FROM TTY
	JSP	QLINK,ASBLOK##	; ALLOCATE SBLOK
	ERROR	11,3		; TOO BIG

	PUSH	STACK,-1(STACK)	; DUPLICATE THE...
	PUSH	STACK,-1(STACK)	; STRING DESCRIPTOR

	JSP	QLINK,CVTPTR##	; CONVERT TO POINTER
	0			; NO BASING
	ERROR	15		; CAN'T FAIL

	MOVSI	S1,-^D300	; S1 WILL HOLD CHARACTER COUNT
	MOVE	S2,SYSCT##	; S2 HAS SYSTEM CHARACTER TABLE

	 ; LOOP TO MAKE THE LAST LINE TYPED A STRING BLOK

RSCNL:	INCHWL	Q1		; INPUT A CHARACTER TO Q1
	IDPB	Q1,Z		; DEPOSIT IN SBLOK
	ADDI	Q1,CTBITS(S2)	; POINT TO ITS MASK IN SYSCT
	MOVE	Q1,0(Q1)	; AND LOAD SAID MASK
	TXNE	Q1,Y.ALT!Y.VM!Y.CR	; INPUT TERMINATOR ?
	JRST	RSCNE		; YES, FINISH UP
	AOBJN	S1,RSCNL	; INCREMENT COUNT AND LOOP
	ERROR	12,21		; STRING IS TOO LONG

	 ; HERE WHEN A TERMINATOR IS FOUND

RSCNE:	HRRM	S1,0(STACK)	; STORE LENGTH IN DESCRIPTOR

	MOVEI	Q2,^D14(S1)	; COMPUTE TOTAL LENGTH OF SBLOK
	IDIVI	Q2,5		; CONVERT TO WORDS
	MOVE	Q1,-1(STACK)	; GET ADDRESS OF SBLOK
	HRRZ	Q3,BLENG(Q1)	; AND GET THE CURRENT LENGTH
	HRRM	Q2,BLENG(Q1)	; STORE NEW LENGTH
	SUB	Q3,Q2		; FIND THE DIFFERENCE
	SUB	SURF,Q3		; AND PRUNE SURFACE POINTER

	JRST	1(PLINK)	; RETURN SUCCESS

	SUBTTL	Primary routines -- REWIND

;	REWIND(FILE-SPEC-SEQWUENCE)


RWND:	SETZ	Z,		; No file check
	JSP	SLINK,CVTSIO	; CONVERT ARG TO STREAM
	ERROR	12,13		; BAD SYNTAX
	ERROR	15		; CAN'T FAIL THIS TEST

	MOVE	T3,-1(STACK)	; LOAD ADDRESS OF STBLOK
	MOVEI	T2,STIOBL(T3)	; POINT TO FIRST IOBLOK
	HRRZ	T1,BLENG(T3)	; LOAD LENGTH
	ADDI	T1,0(T3)	; UPPER BOUND

	MOVEI	S3,STIOBL	; MAKE FIRST IOBLOK
	MOVEM	S3,STCF(T3)	; THE CURRENT FILE

	 ; LOOP TO CLOSE OUT FILES IN STREAM

RWND1:	CAML	T2,T1		; ARE WE STILL BELOW BOUND
	JRST	RETNL1##	; NO - RETURN

	MOVE	T4,0(T2)	; MOVE IOBLOK POINTER
	JSP	QLINK,CLS	; CLOSE THE FILE
	AOJA	T2,RWND1	; KEEP LOOPING

	SUBTTL	Primary routines -- RUN

	 ; RUN(FILE,INC)


RUN0:	JSP	TLINK,CVTINT##	; ENSURE AN INTEGER
	ERROR.	1,90		;
	MOVS	P2,0(STACK)	; SAVE IN P2-LEFT
	SUB	STACK,XWD22##	; REMOVE INTEGER

	MOVE	S1,-1(STACK)	; LOOK AT FIRST ARGUMENT
	CAMN	S1,NULLST##	; COMPARE WITH NULL STRING
	ERROR	13,52		; BAD FILE SPEC

	JSP	SLINK,CVTIO	; GET THE FILE
	ERROR	13,52		; BAD FILE SPEC

	JSP	TLINK,CLSALL	; CLOSE ALL I/O

	MOVEM	SURF,IGNOP##	; SAVE CRITICAL
	MOVEM	STACK,IGNOP##+1	; REGISTERS

	MOVE	T4,-1(STACK)	; POINT TO RETRIEVED IOBLOK
	MOVE	Q1,IODEV(T4)	; TRANSFER
	MOVEM	Q1,IONAME-1(T4)	; DEVICE
	HRRI	P2,IONAME-1(T4)	; POINT TO GOODIES
	RUN	P2,		; ISSUE UUO

	MOVE	SURF,IGNOP##	; RESTORE
	MOVE	STACK,IGNOP##+1	; CRITICAL REG.
	ERROR	12,25		; AND SIGNAL ERROR

	SUBTTL	Primary routines -- TIME

;	TIME(N)
;	RETURNS RUNTIME IF N=0	AND ABSOLUTE TIME IF N = 1

TIME0:	JSP	TLINK,CVTINT##	; ENSURE INTEGER
	ERROR.	1,91		; BAD ARG

	SKIPE	0(STACK)	; TEST N
	JRST	TIME1		; BRANCH IF NOT 0

	JSP	QLINK,MSTIME	; GET TIME SINCE ...
	SUB	Z,TIME.C##	; END OF COMPILATION
	JRST	TIME2		; JOIN COMMON RETURN

	 ; ABSOLUTE TIME

TIME1:	MSTIME	Z,		; ABSOLUTE TIME

TIME2:	MOVEM	Z,0(STACK)	; RETURN ...
	JRST	1(PLINK)	; Z

	SUBTTL	Primary routines -- TTY

	 ; TTY()

TTY:	JSP	TLINK,CVTSTR##	; ENSURE A STRING
	ERROR.	1,100		; GIVE ERROR.

	JSP	QLINK,CVTPTR##	; TRY TO MAKE A PTR
	0			; NO BASING
	JRST	TTY1		; WHOOPS - NULL STRING

	ILDB	Q1,Z		; CHECK
	CAIE	Q1,"C"		; FOR 'C'
	ERROR	13,61		; GIVE ERROR IF NOT

	SKPINC	0		; CHECK FOR CHAR
	JRST	0(PLINK)	; NO
	JRST	RETNUL##	; YES

	 ; HERE TO TEST FOR A FULL LINE

TTY1:	SKPINL	0		; LINE?
	JRST	FRET1##		; NO
	JRST	RETNL1##	; YES

	SUBTTL	TYPE S (SECONDARY) UTILITIES

;

;	CVTFMT - CONVERT TO FORMAT (STRING TO BITS)

;	CVTIO - CONVERT TO IOBLOK (FROM STRING)

;	CVTSIO - CONVERT TO STREAM (OF IOBLOKS)

;	GETOCT - GET OCTAL NUMBER (FROM THE STREAM)

;	IOASSN - DO AN I/O ASSOCIATION

;	NONBLK - STREAM TO A NONBLANK

;	STFILS - STREAM FOR FILES

;	TVIN & TVOUT - TRAPPED VARIABLES FOR I/O


	SUBTTL	Secondary routines -- CVTFMT

;	CVTFMT - CONVERTS A DESCRIPTOR TO AN INTERNAL (ONE-WORD)
;	FORMAT. THE DESCRIPTOR MAY BE STRING OR INTEGER.
;	IF INTEGER, THE VALUE IS PLACED IN THE RIGHT HALF
;	OF THE FORMAT. IF STRING, IT MAY CONSIST OF CHARACTERS
;	CHOSEN FROM THE SET 'TCN' (ASCII MODE) OR BE ONE OF 
;	'BOSUD' (WORD MODE) OR SIMPLY BE ANY OTHER SINGLE 
;	CHARACTER (CHARACTER PREFIX). THE LATTER 
;	IS SO AS TO BE COMPATIBLE WITH SPITBOL.

;	BYTE TABLE
;	CONTAINS CONVERSION CHARACTERISTICS

	BTTBL==.-1		; ADDRESS MINUS 1 OF THIS TABLE

	XWD	"0",1		; BASE-LENGTH
	XWD	"0",3		; BASE - LENGTH
	XWD	" ",6		; BASE - LENGTH
	XWD	0,7		; BASE - LENGTH

; CALLING SEQUENCE:
;	PUSH DESCRIPTOR ONTO STACK
;	JSP	SLINK,CVTFMT
;	RETURN - DESCRIPTOR IS POPPED FROM STACK
;		(Z CONTAINS THE FORMAT)


CVTFMT::HLRZ	T2,-1(STACK)	; GET DATATYPE
	CAIE	T2,IDT		; IS IT INTEGER?
	JRST	CVTFM1		; NO, GO HANDLE STRINGS

	 ; HERE FOR AN INTEGER FORMAT

CVTFM4:	HRRZ	Z,0(STACK)	; LIFT INTEGER TO Z
	SUB	STACK,XWD22##	; REMOVE DESCRIPTOR
	JRST	0(SLINK)	; AND RETURN

	 ; HERE IF STRING OR SOMETHING ELSE

CVTFM1:	JSP	TLINK,CVTSTR##	; MUST BE A STRING HERE
	ERROR.	1,71		; WRONG DATATYPE

	HRRZ	T4,0(STACK)	; LOAD LENGTH
	JSP	QLINK,CVTPTR	; CONVERT TO POINTER
	0			; NO BASING NEEDED
	JRST	CVTFM4		; MUST BE NULL STRING

	MOVE	Q1,T4		; SAVE ORIGINAL LENGTJ

	 ; EXAMINE EACH CHARACTER

	CLEAR	T1,		; FLAGS ARE ACCUMULATED HERE

CVTFM2:	CLEAR	T2,		; FLAGS FOR THIS PASS

	ILDB	T3,Z		; NEXT BYTE TO T3

	CAIN	T3,"T"		; T?
	MOVX	T2,FMT.T	;[246] TERMINAL CHARACTER FORMAT

	CAIN	T3,"N"		; N?
	MOVX	T2,FMT.N	;[246]

	CAIN	T3,"C"		; C?
	MOVX	T2,FMT.C	;[246]

	CAIN	T3,"D"		; D?
	MOVX	T2,FMT.D!FMT.WM	;[246]

	CAIN	T3,"B"		; B?
	MOVX	T2,FMT.B!FMT.WM	;[246]

	CAIN	T3,"O"		; O?
	MOVX	T2,FMT.O!FMT.WM	;[246]

	CAIN	T3,"S"		; S?
	MOVX	T2,FMT.S!FMT.WM	;[246]

	CAIN	T3,"U"		; U?
	MOVX	T2,FMT.U!FMT.WM	;[246]

	CAIN	T3,"A"		; A?
	MOVX	T2,FMT.AP	;[246]

	SKIPE	T2		; DID WE HIT ANYTHING?
	JRST	CVTFM3		; YES, GO ON

	MOVX	T2,FMT.CP	;[246] SINGLE CHAR FORMAT 
	HRR	T2,T3		; GET CHARACTER TOO.
	CAIE	Q1,1		; LENGTH = 1, OK
	ERROR	12,29		; BAD FORMAT

CVTFM3:	IOR	T1,T2		; OR IN THE BITS
	SOJG	T4,CVTFM2	; CONTINUE WITH LOOP

	 ; ON LOOP TERMINATION, CHECK COMPATIBILITY

	MOVE	Z,T1		; LOAD RESULT
	TXNE	Z,FMT.WM!FMT.CP	;[246] IF NOT ASCII MODE
	CAMN	T1,T2		; SEE IF THERE'S ONLY ONE
	JRST	0(SLINK)	; IF SO - RETURN
	ERROR	13,62		; ELSE BAD ARGUMENT

	SUBTTL	Secondary routines -- CVTIO

;	CVTIO - CONVERT A DESCRIPTOR TO AN IO DESCRIPTOR.
;	NULL STRING IMPLIES TTY.
;
; CALLING SEQUENCE:
;	PUSH DESCRIPTOR ONTO STACK
;	JSP	SLINK,CVTIO
;	FAIL		; IMPROPER SYNTAX (DESCR POPPED)
;	SUCCESS		; I/O DESCRIPTOR REPLACES ORIGINAL

CVTIO:	JSP	TLINK,CVTSTR##	; CONVERT TO STRING
	ERROR.	1,108		; BAD DATATYPE

CVTIO1:	JSP	TLINK,STINIT##	; INITIAIZE STREAMING
	JRST	CVTIO2		; MUST BE NULL

	JSP	TLINK,STRIO	; GO GET NEXT I/O DESCR
	JRST	0(SLINK)	; BAD VALUE

	JUMPE	Z,1(SLINK)	; NORMAL RETURN (IF NO BREAK)
	JRST	0(SLINK)	; OTHERWISE FAIL

	 ; NULL STRING

CVTIO2:	SUB	STACK,XWD22##	; POP DESCRIPTOR
	PUSH	STACK,OUTF##	; RETURN ...
	PUSH	STACK,OUTF+1	; OUTPUT ...
	JRST	1(SLINK)	; FILE

	SUBTTL	Secondary routines -- CVTSIO

;	CVTSIO - CONVERT TO STREAM I/O 
;	USED BY INPUT, REWIND AND IN GENERAL WHEREVER A STREAM IS
;	REQUIRED AS OPPOSED TO JUST AN IOBLOK.
;
; CALLING SEQUENCE:
;	PUSH DESCRIPTOR (STRING OR IOBLOK) ONTO STACK
;	SET Z TO -1 IF FILES SHOULD BE 
;	CHECKED AND TO 0 OTHERWISE
;	JSP	SLINK,CVTSIO
;	BAD-SYNTAX (STACK POPPED)
;	FILES-NOT-PRESENT (STREAM RETURNED)
;	NORMAL RETURN (STREAM RETURNED)


CVTSIO::MOVEM	Z,IGNOS		; SAVE FLAG
	JSP	TLINK,CVTSTR##	; MAKE SURE IT'S STRING
	JRST	CVTST3		; TRY IOBLOK

CVTSI2:	JSP	TLINK,STINIT##	; INITIALIZE STREAMING
	JRST	CVTSI1		; MUST BE NULL STRING
	JRST	STFILS		; JOIN STREAMING

CVTSI1:	SUB	STACK,XWD22##	; POP NULL STRING OFF
	PUSH	STACK,INFILE##	; SUBSTITUTE ...
	PUSH	STACK,[0]	; DEFAULT FILE
	JRST	2(SLINK)	; RETURN SUCCESSFULLY

	 ; HERE IF NOT A STRING

CVTST3:	HLRZ	S2,-1(STACK)	; LOAD DT
	CAIN	S2,PDT		; PATTERN?
	JRST	CVTST4		; YES
	CAIE	S2,IODT		; I/O ?
	ERROR.	1,70		; INCORRECT DATATYPE

	MOVEI	S2,1		; ONE IOBLOK
	JRST	STFLS2		; JOIN FILE STREAMING

	 ; THERE'S ONLY ONE GUY WHO WOULD WANT TO READ FROM 
	 ; A PATTERN

CVTST4:	SUB	STACK,XWD22##	; POP MEANINGLESS PATTERN
	PUSH	STACK,LISTF##	; AND PUSH THE 
	PUSH	STACK,LISTF+1	; LISTING FILE ON
	JRST	CVTST3		; LOOP BACK AND TRY AGAIN.

	SUBTTL	Secondary routines -- GETOCT

;	GETOCT - WILL STREAM ACROSS BLANKS, CONVERT A
;	SEQUENCE OF DIGITS TO OCTAL AND EAT UP THE NEXT
;	BREAK CHARACTER. THE BREAK CHARACTER IS RETURNED IN Q2.
;
; CALLING SEQUENCE:
;	STINIT HAS PREVIOUSLY BEEN CALLED
;	JSP	SLINK,GETOCT
;	RETURN	; Z CONTAINS OCTAL NUMBER

GETOCT:	MOVEM	SLINK,GETOSV##	; SAVE RETURN

	JSP	SLINK,NONBLK	; ENSURE A NONBLANK
	JRST	STBOMB		; RUNNOUT MEANS ERROR

	MOVX	S3,Y.NUM	; MASK FOR 0-9
	JSP	TLINK,STRMC##	; FIND END
	SKIPA			; NO BREAK CHAR

	JSP	SLINK,NONBLK	; SKIP PAST BLANKS
	CLEAR	Q2,		; NO BREAK CHAR

	MOVE	S1,Q2		; SAVE BREAK CHAR.

	MOVX	Z,FMT.O		;[246] CONVERT TO ...
	JSP	TLINK,CVTDW	; OCTAL
	JRST	STBOMB		; ERROR RETURN

	MOVE	Q2,S1		; RESTORE Q2
	JUMPE	Q2,@GETOSV	; QUICK RETURN IF NO BRK

	SOS	CURSOR##	; BUMP PAST ...
	IBP	STPTR##		; BREAK CHAR.

	JRST	@GETOSV		; AND RETURN

	SUBTTL	Secondary routines -- IOASSN

;	IOASSN - ASSOCIATES AN IO DESCRIPTOR WITH A NAME.
;	AN ASSOCIATION BLOK IS ALLOCATED (EITHER IABLOK
;	OR OABLOK) WHICH CONTAINS THE ORIGINAL VALUE 
;	PLUS THE IO DESCRIPTOR.
;
; CALLING SEQUENCE:
;	LOAD Z WITH [BLOK TYPE,,TV ADDRESS]
;	PUSH NAME DESCRIPTOR 
;	PUSH IO DESCRIPTOR
;	JSP	SLINK,IOASSN
;	RETURN		; BOTH DESCRIPTORS CLEARED
;			; NOTHING RETURNED
;	IF Z IS NEG. THEN PREVIOUS ASSOCIATIONS ARE NOT REMOVED

IOASSN:	MOVEM	Z,IGNOS+1	; SAVE ARGUMENT

	SKIPG	Z		; IF ARG IS ...
	MOVN	Z,Z		; NEGATIVE
	MOVEM	Z,IGNOS		; POSIFY IT

	 ; ALLOCATE AN ASSOCIATION BLOK

	HRRI	Z,ALENG		; LENGTH
	JSP	QLINK,BLOK##	; ALLOCATE A BLOK
	ERROR	15		; CAN'T FAIL

	POP	STACK,FLAS##+1	; SAVE ASSOCIATION ...
	POP	STACK,FLAS##	; BLOK

	 ; INSERT GIVEN IO DESCRIPTOR

	MOVE	S3,FLAS		; GET ADDRESS OF BLOK
	POP	STACK,AFILE+1(S3) ; POP 2ND ...
	POP	STACK,AFILE(S3)	; ARGUMENT

	 ; FIND ADDRESS OF NAME GIVEN BY FIRST ARG

	JSP	TLINK,CVTNAM##	; CONVERT TO NAME
	ERROR.	1,69		; BAD FIRST ARGUMENT

	SKIPL	IGNOS+1		; SKIP IF Z WAS NEG.
	JSP	TLINK,DTCH	; REMOVE PREVIOUS ASSNS

	POP	STACK,Q1	; OFFSET
	POP	STACK,Q2	; + BLOK HEAD
	ADD	Q1,Q2		; = ADDRESS

IFN FTPAV,<

	; FIND CORRECT POSITION IN TRAP CHAIN

	MOVE	T2,Q1		;POINT TO HEAD OF CHAIN
	HLRZ	Z,IGNOS		;GET BLOCK TYPE (IABLOCK OR OABLOK)
	CAIN	Z,IABLOK	;INPUT ?
	 MOVEI	T3,IALVL	;YES - GET DEFAULT LEVEL
	CAIN	Z,OABLOK	;OUTPUT ?
	 MOVEI	T3,OALVL	;YES - GET DEFAULT LEVEL
	JSP	TLINK,FNDPOS##	;FIND THE CORRECT SPOT
	MOVE	Q1,T2		;MOVE POINTER TO GOOD SPOT INTO Q1

>;END OF IFN FTPAV

	 ; EXCHANGE VALUE IN NAME WITH TVAR

	MOVE	Z,FLAS		; ADDRESS OF ASSOCIATION BLOK
	HRLI	Z,TVAR		; TRAPPED VARIABLE DATATYPE
	EXCH	Z,0(Q1)		; SWITCH THIS WITH FIRST VALUE WORD
	MOVEM	Z,AVAL(S3)	; OLD VALUE TO ASSN BLOK

	HRRZ	Z,IGNOS		; TVAR ADDRESS
	EXCH	Z,1(Q1)		; INTO TVAR DESCR
	MOVEM	Z,AVAL+1(S3)	; OLD 2ND WORD TO ASSN BLOK

	JRST	0(SLINK)	; RETURN

	SUBTTL	Secondary routines -- NONBLK

;	NONBLK - STREAM FOR A NONBLANK
;
; CALLING SEQUENCE:
;	JSP	SLINK,NONBLK
;	RUNNOUT
;	NORMAL

NONBLK:	MOVX	S3,Y.BL!Y.TAB
	JSP	TLINK,STR.C##
	JRST	0(SLINK)
	JRST	1(SLINK)

	SUBTTL	Secondary routines -- STFILS

;	STFILS - A ROUTINE TO PROCESS A SEQUENCE OF 
;	INPUT FILES. THESE ARE COMBINED INTO A STREAM BLOK.
;
; CALLING SEQUENCE:
;	STINIT HAS PREVIOUSLY BEEN CALLED
;	SET IGNOS WITH FLAG (FILE CHECK)
;	JSP	SLINK,STFILS
;	BAD SYNTAX, STACK UNCHANGED
;	FILE NOT FOUND (STREAM RETURNED ANYWAY)
;	NORMAL

	 ; ENTRY POINT - PUSH IOBLOK POINTERS ONTO STACK

STFILS:	MOVEI	S2,1		; USED TO COUNT DESCRIPTORS
	MOVEM	STACK,IGNOS+2	; SAVE STACK
	CLEARM	IGNOS+3		; FLAG TO INDICATE ERROR

STFLS1:	JSP	TLINK,STRIO	; STREAM FOR IO DESCRIPTOR
	JRST	STFL99		; BAD FILE SPEC

	MOVE	S3,Z		; SAVE BREAK CHAR
	SKIPN	IGNOS		; TEST FILES?
	JRST	STFLS9		; NO
	JSP	TLINK,FILECH	; CHECK THE FILE
	SETOM	IGNOS+3		; BAD FILE FLAG

STFLS9:	MOVE	Q3,TTYBLK	; DEFAULT FILE
	SKIPN	-1(STACK)	; IS
	MOVEM	Q3,-1(STACK)	; TTY

	JUMPE	S3,STFLS2	; NO MORE FILES
	AOJA	S2,STFLS1	; INCREMENT & LOOP

	 ; HERE WHEN STREAMING IS DONE

STFLS2:	SKIPN	IGNOS+3		; ANY BAD FILES?
	ADDI	SLINK,1		; YES, SKIP INCREMENT

	MOVEI	Z,STIOBL(S2)	; LENGTH OF REQUIRED STBLOK
	JSP	QLINK,GCOLG##	; ENSURE SPACE
	MOVE	T1,GSURF##	; POINT TO STBLOK BEING FORMED

	HRLI	Z,STBLOK	; INSERT TYPE OF BLOK
	MOVEM	Z,BTCODE&BLENG(T1)	; STUFF INTO BLOK HEADER
	CLEARM	GCFLD(T1)	; SATISFY GC

	MOVEI	T2,STIOBL	; OFFSET TO FIRST IOBLOK
	MOVEM	T2,STCF(T1)	; MAKE CURRENT FILE
	MOVEI	T2,STIOBL-1(T1)	; COMPUTE ADDRESS OF ...
	ADDI	T2,0(S2)	; LAST IOBLOK
	MOVEI	S3,0(S2)	; LOAD COUNT

STFLS3:	POP	STACK,Z		; JUNK WORD
	POP	STACK,0(T2)	; POP FILE WORD
	SUBI	T2,1		; DECREMENT FOR NEXT
	SOJG	S3,STFLS3	; COUNT DOWN ON S3



	 ; COMPARE WITH PREVIOUS STBLOKS 
	 ; T1 POINTS TO NEWLY FORMED STBLOK

	SKIPA	T2,STBLKS##	; LOAD HEAD OF LIST

STFLS4:	MOVE	T2,STNXT(T2)	; TRY NEXT STBLOK
	JUMPE	T2,STFLS6	; END-OF-LINE

	MOVE	Z,BLENG(T1)	; LOAD LENGTH WORD
	CAME	Z,BLENG(T2)	; COMARE LENGTH WORDS
	JRST	STFLS4		; LOOP BACK FOR MORE

	MOVEI	S3,0(S2)	; LOAD COUNT AGAIN
	MOVEI	Q1,STIOBL(T1)	; FIRST IOBLOK OF NEW
	MOVEI	Q2,STIOBL(T2)	; FIRST IOBLOK OF OLD

	 ; LOOP TO COMPARE STBLOK'S

STFLS5:	MOVE	Z,0(Q1)		; COMPARE ...
	CAME	Z,0(Q2)		; IOBLOK'S
	JRST	STFLS4		; DIFFERENT - NEXT STBLOK

	AOJ	Q1,		; BUMP IOBLOK ...
	AOJ	Q2,		; POINTERS ...
	SOJG	S3,STFLS5	; AND LOOP

	PUSH	STACK,T2	; RETURN ...
	PUSH	STACK,[0]	; FOUND STBLOK ...
	JRST	1(SLINK)	; NORMALLY

	 ; NO MATCH WAS FOUND, LINK NEW STBLOK INTO LIST

STFLS6:	HRRZ	Z,BLENG(T1)	; LENGTH OF STBLOK
	ADDM	Z,GSURF##	; MAKE IT OFFICIAL

	HRLI	T1,IODT		; THIS DATATYPE IS OK
	PUSH	STACK,T1	; RETURN THE ...
	PUSH	STACK,[0]	; DESCRIPTOR

	MOVE	T2,STBLKS	; LOAD FIRST STBLOK
	MOVEM	T2,STNXT(T1)	; INSERT AS OUR NEXT
	MOVEM	T1,STBLKS	; MAKE OURS THE FIRST

	JRST	1(SLINK)

	 ; ERROR - BAD SPECIFICATION

STFL99:	MOVE	STACK,IGNOS+2	; RETRIEVE STACK
	JRST	0(SLINK)	; GIVE ERROR RETURN

	SUBTTL	Secondary routines -- TVIN & TVOUT

;	TVIN AND TVOUT - ARE THE S TYPE ROUTINES 
;	USED TO IMPLEMENT THE TRAPPED VARIABLES ASSOCIATED 
;	WITH INPUT AND OUTPUT RESPECTIVELY.

TVIN:	JRST	TVREAD		; VALUE
	JRST	TVNAME		; NAME
	JRST	TVASGN		; ASSIGNMENT

TVOUT:	JRST	TVFTCH		; VALUE
	JRST	TVNAME		; NAME
	JRST	TVWRIT		; ASSIGNMENT


;	TVREAD - TRAPPED VARIABLE ON TOP OF STACK -
;	TO BE REPLACED BY READ-IN VALUE

TVREAD:	MOVE	S2,-1(STACK)	; ADDRESS OF ASSN BLOK

	DMOVE	T2,AFILE(S2)	; LOAD FILE WORD
				; FORMAT WORD
	MOVE	S3,T3		; SAVE FORMAT WORD
	JSP	TLINK,READ	; DO THE READ
	ERROR	12,4		; INCORRECTIBLE READ
	JRST	0(SLINK)	; EOF - TAKE FAIL

	AOS	NINS##		; INCREMENT READ COUNT

	TXNN	S3,FMT.WM	;[246] DID WE READ IN WORD MODE?
	JRST	TVRD1		; NO - TAKE BRANCH

	MOVE	T2,Z		; MOVE WORD FOR CONVERSION
	MOVE	Z,S3		; FORMAT
	JSP	TLINK,CVTWD	; CONVERT WORD TO DATA
	ERROR	15		; CAN'T FAIL
	JRST	TVRD2		; JUMP OFF TO INSERT VALUE

	 ; HERE IF READ WAS UNDER ASCII MODE

TVRD1:	SKIPE	$TRIM##		; WAS &TRIM SET?
	JSP	TLINK,TRIMT##	; YES - DO IT

	 ; HERE TO PLACE VALUE IN VALUE FIELD

TVRD2:	MOVE	S2,-3(STACK)	; POINT TO ASSN BLOK
	MOVEI	Q2,AVAL(S2)	; POINT TO VALUE FIELD
	JSP	QLINK,LOCVAL##	; POINT TO REAL VALUE FIELD
	POP	STACK,1(Q2)	; POP VALUE ...
	POP	STACK,0(Q2)	; INTO THIS FIELD

	SUB	STACK,XWD22##	; POP TRAPPED VARIABLE
	PUSH	STACK,0(Q2)	; PLACE VALUE ...
	PUSH	STACK,1(Q2)	; BACK INTO STACK
	JRST	1(SLINK)	; AND RETURN


TVNAME:	MOVE	S2,-1(STACK)	; GET ADDRESS OF ASSN BLOK
	SUB	STACK,XWD22##	; POP DESCRIPTOR

	HRLI	S2,NDT		; INSERT DATATYPE
	PUSH	STACK,S2	; THIS IS FIRST WORD
	MOVEI	S3,AVAL		; THIS IS OFFSET
	PUSH	STACK,S3	; PUSH IT

	JRST	1(SLINK)	; AND RETURN


TVASGN:	POP	STACK,S3	; POP ...
	POP	STACK,S2	; VALUE

	MOVE	Z,-1(STACK)	; ADDRESS OF ASSN BLOK
	HRLI	Z,VAR		; MAKE IT A VARIABLE
	PUSH	STACK,Z		; PUSH VARIABLE ...
	PUSH	STACK,[AVAL]	; ONTO STACK

	PUSH	STACK,S2	; PUSH VALUE ...
	PUSH	STACK,S3	; BACK ON

	JSP	TLINK,ASGNVL##	; DO THE ASSIGNMENT
	JRST	0(SLINK)	; PASS ANY ERRORS BACK

	SUB	STACK,XWD22##	; GET RID OF VARIABLE

	JRST	1(SLINK)	; AND RETURN


TVFTCH:	MOVE	S2,-1(STACK)	; GET ADDRESS	OF BLOK
	SUB	STACK,XWD22##	; REMOVE DESCRIPTOR

	PUSH	STACK,AVAL(S2)	; PUSH CURRENT
	PUSH	STACK,AVAL+1(S2) ; VAL

	JSP	TLINK,CVTVAL##	; MAKE IT A VALUE
	JRST	0(SLINK)	; PASS SUCCESS BACK
	JRST	1(SLINK)	; AND RETURN SUCCESS


TVWRIT:	SKIPN	$OUTPT##	; CHECK &OUTPUT
	JRST	TVASGN		; OFF - JUST ASSIGN

	PUSH	STACK,-1(STACK)	; DUPLICATE OBJECT...
	PUSH	STACK,-1(STACK)	; FOR ULTIMATE ASSIGNMENT

	MOVE	S2,-5(STACK)	; ASSN BLOK PTR
	MOVE	S3,AFILE+1(S2)	; FORMAT WORD
	TXNN	S3,FMT.D	;[246] DIRECT?
	JRST	TVWRT1		; NO -

	JSP	TLINK,CVTNUM##	; ENSURE NUMERIC
	ERROR.	1,109		; BAD VALUE
	POP	STACK,Z		; BITS TO BE TRANSMITTED
	POP	STACK,0(STACK)	; PRUNE STACK
	JRST	TVWRT2		; JUMP TO WRITE

	 ; HERE FOR NON DIRECT WRITING

TVWRT1:	JSP	TLINK,CVTSTR##	; CONVERT TO STRING IF POSSIBLE
	JSP	QLINK,DTNAME##	; NO - JUST USE NAME

	TXNN	S3,FMT.WM	;[246] WORD MODE?
	JRST	TVWRT2		; NO

	MOVE	Z,S3		; LOAD FORMAT
	JSP	TLINK,CVTDW	; CONVERT TO WORD
	ERROR	13,64		; BAD VALUE

	 ; HERE WITH ITEM READY TO BE OUTPUT

TVWRT2:	MOVE	T2,AFILE(S2)	; LOAD FILE WORD
	MOVE	T3,S3		; LOAD FORMAT WORD
	JSP	TLINK,WRITE	; WRITE IT OUT
	ERROR	12,5		; INCORRECTIBLE

	AOS	NOUTS##		; INCREMENT # OF WRITES
	JRST	TVASGN		; DO AN ASSIGNMENT

	SUBTTL	TYPE T (TERTIARY) I/O UTILITIES

;	CLSALL - CLOSE ALL I/O FILES

;	CVTDW - CONVERT FROM DATA TO WORD

;	CVTWD - CONVERT FROM WORD TO DATA

;	DTCH - DETACH ALL ASSOCIATIONS

;	FILECH - FILE CHECK

;	PEJECT - PAGE EJECT

;	READ

;	STRIO - STREAM FOR IO

;		MAKEIO - (ALTERNATE ENTRY FOR STRIO)

;	WRITE

	SUBTTL	Tertiary routines -- CLSALL

	 ; CLSALL WILL CLOSE ALL FILES
	 ; CALLING SEQUENCE:
	 ;	 JSP TLINK,CLSALL
	 ;	 RETURN

CLSALL:: HRRZ	T4,IOBLKS	; PICK UP FIRST

CLSAL1:	JUMPE	T4,0(TLINK)	; TEST FOR DONE
	JSP	QLINK,CLS	; CLOSE IT
	HRRZ	T4,IONEXT(T4)	; GET NEXT
	JRST	CLSAL1		; LOOP BACK

	SUBTTL	Tertiary routines -- CVTDW

;	CVTDW - CONVERT FROM DATA TO WORD.
;
; CALLING SEQUENCE -
;	PUSH DESCRIPTOR ONTO STACK
;	LOAD Z WITH FORMAT
;	JSP	TLINK,CVTDW
;	ALTERNATE RETURN - INPUT STRING TOO LONG
;	NORMAL RETURN - Z CONTAINS CONVERTED VALUE
;
;	ON THE ALTERNATE RETURN, Z WILL CONTAIN THE 
;	CONVERTED REPRESENTATION OF AN INITIAL SUBSTRING
;	OF THE INPUT STRING.	STACK IS POPPED IN EITHER CASE

CVTDW::	MOVEM	TLINK,CDWSV	; SAVE RETURN POINT

	TXNN	Z,FMT.D		;[246] IS IT D FORMAT?
	JRST	CVTDW1		; NO - BRANCH

	JSP	TLINK,CVTNUM	; MAKE A NUMBER OUT OF IT
	ERROR.	1,105		; FORMAT-DATATYPE INCOMPATABILITY

	POP	STACK,Z		; THE REAL VALUE
	POP	STACK,T2	; JUNK

	MOVE	TLINK,CDWSV##	; RESTORE LINK
	JRST	1(TLINK)	; NORMAL RETURN

	 ; HERE ON ANYTHING BUT 'D' FORMAT

CVTDW1:	JSP	QLINK,WMPROP	; SET 'BT-WORDS' FROM FORMAT

	JSP	TLINK,CVTSTR##	; ENSURE A STRING
	ERROR.	1,106		; BAD DATATYPE
	MOVE	TLINK,CDWSV	; RESTORE TLINK

	HRRZ	T4,0(STACK)	; LOAD LENGTH

	JSP	QLINK,CVTPTR##	; CONVERT TO ...
	0			; POINTER
	JRST	CVTDW9		; NULL STRING EXIT

	SETZB	Q2,T2		; LENGTH RECORDED IN Q2,
				;  RESULT ACCUMLULATED IN T2

	 ; TOP OF LOOP - WORD IS FORMED IN T3

CVTDW2:	ILDB	T3,Z		; GET 7-BIT BYTE FROM STRING
	SUB	T3,BTBASE	; SUBTACT OFF BASE

	SKIPGE	T3		; TOO LOW?
	ERROR	13,63		; YES

	CAMGE	T3,BTMAX	; TOO HIGH?
	JRST	CVTDW4		; NO - 0K

	MOVE	Q3,BTSIZE	; MAYBE ...
	CAIE	Q3,6		; SIXBIT?
	ERROR	13,63		; NO
	SUBI	T3,40		; YES, LOWER-UPPER CASE

CVTDW4:	IDPB	T3,BTPTR	; INSERT BYTE

	AOJ	Q2,		; INCREMENT LENGTH
	CAML	Q2,T4		; GO ON IF WE HAVE NOT EXHAUSTED STR.
	JRST	CVTDW3		; BRANCH IF DONE
	CAMGE	Q2,BTLENG	; SKIP IF WE HAVE FILLED A WORD
	JRST	CVTDW2		; OTHERWISE LOOP
	MOVE	Z,T2		; VALUE TO RETURN REGISTER
	JRST	0(TLINK)	; ALTERNATE RETURN

	 ; INPUT STRING EXHAUSTED

CVTDW3:	MOVE	Z,T2		; MOVE VALUE TO RETURN REG.

	SKIPE	BTLJFL		; TEST LEFT-JUSTIFY FLAG
	JRST	1(TLINK)	; ON - JUST RETURN

	IMUL	Q2,BTSIZE	; GET LENGTH OF INFO IN BITS
	ROT	Z,0(Q2)		; SHIFT INFO INTO LOW ORDER

	JRST	1(TLINK)	; NORMAL RETURN

	 ; HERE ON NULL STRINGS

CVTDW9:	SUB	STACK,XWD22##	; POP STACK
	CLEAR	Z,		; HE WANTS A 0
	JRST	1(TLINK)	; NORMAL RETURN

	SUBTTL	Tertiary routines -- CVTWD

;	CVTWD - CONVERT WORD TO DATA (UNDER FORMAT CONTROL).
;
; CALLING SEQUENCE:
;	LOAD Z WITH FORMAT
;	LOAD T2 WITH WORD
;	JSP	TLINK,CVTWD
;	FAIL - SPARE EXIT NOT CURRENTLY TAKEN
;	SUCCESS - DESCRIPTOR RETURNED ON STSACK

CVTWD::	TXNN	Z,FMT.D		;[246] 'D' FORMAT?
	JRST	CVTWD1		; NO

	PUSH	STACK,[IDT,,0]	; EASY CASE -
	PUSH	STACK,T2	; MAKE INTEGER ...
	JRST	1(TLINK)	; OUT OF IT

	 ; HERE IF NOT 'D' FOMRAT

CVTWD1:	JSP	QLINK,WMPROP	; WORD-MODE PROPERTIES

	MOVE	Z,BTLENG	; ALLOCATE STRING ...
	JSP	QLINK,ASBLOK##	; OF APPROPRIATE LENGTH.
	ERROR	11,13		; EXCEEDED &MAXLNGTH

	MOVE	T4,BTLENG	; TRANSFER LENGTH ...
	MOVEM	T4,0(STACK)	; INTO DESCR.

CVTWD2:	ILDB	T3,BTPTR	; EXTRACT BYTE FROM T2
	ADD	T3,BTBASE	; ADD BYTE CORR. TO 0
	IDPB	T3,Q3		; TOSS BYTE INTO STRING
	SOJG	T4,CVTWD2	; LOOP ON T4

	JRST	1(TLINK)	; RETURN

	SUBTTL	Tertiary routines -- DTCH

;	DTCH - A TYPE T ROUTINE TO DETACH ALL I/O
;	ASSOCIATIONS FOR A GIVEN VARIABLE
;
; CALLING SEQUENCE:
;	PUSH NAME OF VARIABLE ONTO STACK
;	JSP 	TLINK,DTCH
;	RETURN	(STACK UNPOPPED)

DTCH:	MOVE	T2,-1(STACK)	; GET ADDRESS ...
	ADD	T2,0(STACK)	; OF VALUE DESCRIPTOR

	 ; HERE AFTER DESCRIPTOR HAS BEEN REPLACED

DTCH2:	HLRZ	T4,0(T2)	; LOAD DATATYPE
	CAIE	T4,TVAR		; IS IT TRAPPED
	JRST	0(TLINK)	; NO - RETURN

	MOVE	T4,0(T2)	; LOAD BLOK ...
	HLRZ	Q1,BTCODE(T4)	; TYPE AND TEST ...
	CAIE	Q1,IABLOK	; INPUT OR ...
	CAIN	Q1,OABLOK	; OUTPUT
	JRST	DTCH1		; YES

	 ; NOT IO TRAP, MUST BE TRACE

	MOVEI	T2,LHDR(T4)	; POINT TO NEW VALUE
	JRST	DTCH2		; LOOP BACK

	 ; HERE TO REMOVE ASSOCIATION

DTCH1:	PUSH	STACK,AVAL(T4)	; TRANSFER VALUE
	PUSH	STACK,AVAL+1(T4)	; FROM ASSN BLOK
	POP	STACK,1(T2)	; BACK INTO
	POP	STACK,0(T2)	; THE VARIABLE
	JRST	DTCH2		; AND LOOP

	SUBTTL	Tertiary routines -- FILECH

;	FILECH - CHECKS FOR THE EXISTENCE OF AN INPUT 
;	FILE.  IT THE EXT IS NULL AND THE FILE DOES NOT
;	EXIST, IT TRIES AN EXTENSION OF SNO.
;	IF THE FILE IS 0 THEN TTY IS SUPPLIED.
;	THESE THINGS ARE VALID ONLY DURING COMMAND STRING
;	EVALUATION
;
;	CALLING SEQUENCE:
;	PUSH IO DESCR. ONTO STACK
;	JSP	TLINK,FILECH
;	ERROR - FILE DOES NOT EXIST
;	NORMAL RETURN, STACK UNPOPPED

FILECH:	MOVE	T4,-1(STACK)	; LOAD IOBLOK
	JUMPE	T4,1(TLINK)	; RETURN IF FILE IS NULL

	SKIPE	IOFLGS(T4)	; CHECK IF BUFFERS EXIST
	JRST	1(TLINK)	; YES - RETURN

	MOVE	Z,0(STACK)	; COMPUTE ...
	JSP	QLINK,MODE	; MODE

	JSP	QLINK,OPN	; OPEN THE FILE
	ERROR	12,23		; CAN'T OPEN

FILCH2:	JSP	QLINK,LKP	; LOOK THE FILE UP
	JRST	FILCH1		; COULDN'T FIND FILE

FILCH4:	JSP	QLINK,CLS	; MIGHT AS WELL CLOSE IT

	JRST	1(TLINK)	; AND RETURN

	 ; HERE IF WE COULDN'T LOCATE THE FILE AS GIVEN

FILCH1:	SKIPN	IOEXT(T4)	; NULL EXTENSION
	SKIPE	COMPIN##	; TRY DEFAULT ONLY IF COMPIN IS 0
	SOJA	TLINK,FILCH4	; GO BACK AND FAIL

	MOVSI	Q1,D$EXT	;[264] TRY USING THE DEFAULT EXTENSION
	MOVEM	Q1,IOEXT(T4)	; PLACE THIS IN IOBLOK

	JRST	FILCH2		; TRY AGAIN
	

	SUBTTL	Tertiary routines -- PEJECT

;	PEJECT - GENERAL UTILITY TO EJECT A PAGE
;
; CALLING SEQUENCE:
;	LOAD T2 WITH FILE WRD
;	LOAD T3 WITH FORMAT WORD
;	JSP 	TLINK,PEJECT
;	ERROR
;	NORMAL - STACK UNCHANGED


PEJECT::MOVX	T3,FMT.C	;[246] SINGLE CHAR. FORMAT
	PUSH	STACK,$ALPHA##	; SEND ...
	PUSH	STACK,[.CHFFD,,1]	; FORM-FEED ...
	JRST	WRITE		; TREAT LIKE A WRITE

	SUBTTL	Tertiary routines -- READ

;	READ - READS A RECORD (OR A CHARACTER, OR A WORD)
;	FROM A STREAM ACCORDING TO A FORMAT.
;
; CALLING SEQUENCE:
;	LOAD T2 WITH POINTER TO STREAM
;	LOAD T3 WITH FORMAT
;	JSP	TLINK,READ
;	ERROR
;	END-OF-FILE
;	NORMAL
;
;	ON NORMAL RETURN EITHER A 36-BIT WORD IS RETURNED IN
;	Z (WORD MODE FORMAT) OR A STRING IS RETURNED ON THE STACK.
;	IN ASCII MODE (NON C FORMAT) Z IS SET WITH FLAGS
;	F.FLG IF A FORM FEED WAS ENCOUNTERED.
;	N.FLG IF THE LINE WAS SEQUENCE NUMBERED

	 ; ENTRY POINT

READ::	JUMPL	T3,1(TLINK)	; END-OF-FILE FLAG
	TXNE	T3,FMT.CP	;[246] CHARACTER-PREFIX FORMAT?
	CLEAR	T3,		; YES - IGNORE FORMAT ON INPUT

	MOVE	T4,STCF(T2)	; CURRENT FILE OFFSET
	HRRZ	Q1,BLENG(T2)	; GET LENGTH OF BLOK
	CAML	T4,Q1		; IS CURRENT FILE WITHIN BOUNDS?
	JRST	1(TLINK)	; NO - END-OF-FILE

	ADDI	T4,0(T2)	; POINT TO POINTER
	MOVE	T4,0(T4)	; LOAD IOBLOK POINTER

READ1:	CAMN	T4,TTYBLK	; IS IT TTY?
	JRST	RDTTY		; YES - TRANSFER BEYOND READ6

	MOVE	Q1,IOFLGS(T4)	; GET FLAGS
	TLNE	Q1,IOINFL	; ALREADY OPEN FOR INPUT?
	JRST	READ2		; YES

	 ; PREPARE FILE FOR READING

READ1A:	JSP	QLINK,CLS	; CLOSE FILE IN CASE

	MOVE	Z,T3		; FORMAT WORD
	JSP	QLINK,MODE	; MODE TO REG Z

	JSP	QLINK,OPN	; OPEN THE FILE
	ERROR	12,23		; COULDN'T OPEN

	JSP	QLINK,LKP	; LOOK UP THE FILE
	ERROR	12,30		; NOT FOUND

	MOVSI	Z,IOINFL	; ALLOCATE
	JSP	QLINK,ALBUF	; INPUT BUFFERS
	 ; ASCERTAIN MODE-FORMAT COMPATABILITY

READ2:	MOVE	Z,T3		; CONVERT FORMAT ...
	JSP	QLINK,MODE	; TO MODE

	CAME	Z,IOMODE(T4)	; MODES INCOMPATIBLE?
	ERROR	12,19		; YES - GIVE ERROR

	 ; SAVE REGISTERS

	MOVEM	T1,RDLINK	; SAVE
	MOVEM	T2,RDSTBL	; REGS.
	CLEARM	RDFLGS		; CLEAR FLAGS

	N.FLG==1
	F.FLG==2

	 ; WORD MODE READING

	TXNN	T3,FMT.WM	;[246] WORD MODE?
	JRST	READ3		; NO

	SOSGE	IOIBHD+2(T4)	; DECREMENT WORD COUNT
	JSP	QLINK,FINBUF	; FILL BUFFER IF 0
	ILDB	Z,IOIBHD+1(T4)	; EXTRACT WORD

	JRST	2(TLINK)	; NORMAL RETURN
	 ; 'C' FORMAT

READ3:	TXNN	T3,FMT.C	;[246] 'C' FORMAT?
	JRST	READ4		; NO

	HRRZ	Q2,IOLAST(T4)	; GET LAST CHAR
	HLRM	Q2,IOLAST(T4)	; SET TO 0
	JRST	READ3B		; ENTER WITHIN

READ3A:	SOSGE	IOIBHD+2(T4)	; DECREMENT CHAR. COUNT
	JSP	QLINK,FINBUF	; FILL BUFFER ON 0 COUNT
	ILDB	Q2,IOIBHD+1(T4)	; GET NEXT BYTE
READ3B:	JUMPE	Q2,READ3A	; REPEAT IF 0

	CAIE	Q2,.CHCNZ	; ^Z ?
	JRST	READ3C		; NO - GO AROUND
	SKIPGE	IOTTY(T4)	; TTY?
	JRST	FINBU1		; YES - EOF

	 ;	HERE FROM TTY 'C' FORMAT

READ3C:	MOVS	Q2,Q2		; CHAR TO LEFT HALF
	HRRI	Q2,1		; ONE TO RIGHT HALF
	PUSH	STACK,$ALPHA	; FIRST DESCRIPTOR WORD
	PUSH	STACK,Q2	; SECOND WORD
	JRST	2(TLINK)

	 ; ALLOCATE SBLOK

READ4:	TXNE	T3,FMT.AP	;[246] Is it Append Mode?
	ERROR	12,39		; That wasn't very bright.

	MOVE	Z,IOIBHD+2(T4)	; SIZE OF SBLOK
	AOJ	Z,		; MAKE IT AT LEAST 1
	MOVE	T1,Z		; SAVE THE VALUE

	JSP	QLINK,ASBLOK	; ALLOCATE ONE
	ERROR	11,3

	MOVEM	T1,0(STACK)	; SET LENGTH
	PUSH	STACK,-1(STACK)	; DUPLICATE ...
	PUSH	STACK,-1(STACK)	; VALUE

	JSP	QLINK,CVTPTR	; GET POINTER
	Q3			; BASE ON Q3
	ERROR	15		; CAN'T FAIL

	MOVEM	Z,DEPTR##		; SAVE POINTER
	CLEAR	T2,		; DEPOSITION-CHAR COUNT

	HRRZ	Q2,IOLAST(T4)	; LOAD LAST BYTE

	 ; PHASE I - LOOK FOR A MARKED WORD 
	 ; (INDICATING SEQUENCE NUMBERING)

	JRST	READ5A		; ENTER THE FORAY

	 ; PHASE I LOOP - CHECK FOR SEQUENCE NUMBERING 
	 ; WITHIN FILE

READ5:	SOSGE	IOIBHD+2(T4)	; DECREMENT COUNT
	JSP	QLINK,FINBF1	; FILL BUFFER IF NECESSARY
	ILDB	Q2,IOIBHD+1(T4)	; GET NEXT BYTE

	 ; HERE IF BYTE ALREADY EXISTS

READ5A:	MOVE	Q1,IOIBHD+1(T4)	; GET POINTER
	LSH	Q1,-^D30	; BYTE POSITION TO Q1 RIGHT
	CAIN	Q1,^D29		; LEFT-MOST BYTE?
	JRST	READ5B		; YES
	JUMPE	Q2,READ5	; IF 0 - LOOP
	JRST	READ6		; OTHERWISE IT CAN'T BE NUMBERED

	 ; HERE IF BYTE IS FIRST BYTE OF A WORD

READ5B:	MOVE	Q1,IOIBHD+1(T4)	; GET POINTER AGAIN
	MOVE	Q1,0(Q1)	; LOAD POINTED-TO WORD

	MOVEI	Z,N.FLG		; SET ...
	TRNE	Q1,1		; SEQUENCE # FLAG IF ...
	IORM	Z,RDFLGS##	; FILE IS SEQUENCED #ED

	TRNE	Q1,1		; IF NOT SEQUENCED #'ED
	TXNE	T3,FMT.N	;[246] OR IF N FORMAT
	JRST	READ6		; THEN GET THE WHOLE THING

	 ; FILE IS SEQUENCE NUMBERED & NOT N FORMAT
	 ; IGNORE FIRST SIX CHARACTERS OF STRING

	MOVEI	T1,6		; NUMBER OF CHARACTES TO OMIT
READ5C:	SOSGE	IOIBHD+2(T4)	; DEDUCT COUNT
	JSP	QLINK,FINBF1	; FILL BUFF IF NECESSARY
	ILDB	Q2,IOIBHD+1(T4)	; LOAD NEXT BYTE
	SOJG	T1,READ5C	; KEEP LOOPING UNTIL CHARS ARE EATEN


;	READ6 - PHASE II OF THE GENERALIZED LINE INPUT.
;	CHARACTERS ARE TRANSFERED FROM THE BUFFER TO THE
;	DEPOSITION STRING. WHILE THIS IS DONE, A COUNT IS 
;	KEPT IN T2 AND THE CHARACTERS ARE EXAMINED FOR
;	AN OCCURRENCE OF A TERMINAL. Y.ALT!Y.VM!Y.CR DESCRIBES THE 
;	SET OF ALL TERMINALS WHEREAS Y.ALT!Y.VM DESCRIBES THE 
;	SUBSET OF TERMINALS CALLED BREAKS.

;	A LINE ENDS WITH ANY SUCCESSION OF BREAKS AND THE LONGEST
;	SUFFIX OF TERMINALS IS CALLED THE TCC (TERMINAL 
;	CONTROL CHARACTER) SEQUENCE.
;
;	ENTER WITH Q2 CONTAINING A CHARACTER. T1 HOLDS THE STATE
;	 STATE 0 :	NO TERMINAL YET
;	 STATE 1 :	THE LAST CHAR WAS A TERMINAL BUT NO BREAK YET
;	 STATE 2 :	THE LAST CHAR WAS A BREAK

READ6:	MOVEI	T1,RD.ST0	; INITIAL STATE IS 0
	CLEAR	T2,		; BYTE COUNT
	SETOM	IGNOT##		; LENGTH OF MEAT
	JRST	READ6B		; JUMP INTO THE LOOP

	 ; LOOP BACK HERE FOR EACH CHAR

READ6A:	SOSGE	IOIBHD+2(T4)	; BUMP COUNT
	JSP	QLINK,FINBF1	; REFRESH BUFFER IF 0
	ILDB	Q2,IOIBHD+1(T4)	; GET NEXT BYTE

READ6B:	JUMPE	Q2,READ6A	; IGNORE NULL CHARS
	MOVE	Q1,SYSCT##	; ADDRESS OF SYSTEM CHAR TABLE
	ADDI	Q1,CTBITS(Q2)	; POINT TO MASK
	MOVE	Q1,0(Q1)	; LOAD MASK
	JRST	@T1		; TRANSFER TO CURRENT STATE

	 ; CONTROL RETURNS EITHER HERE OR AT READ6D.
	 ; THE LATTER IS THE END-OF-RECORD LOCATION

READ6C:	IDPB	Q2,DEPTR	; DEPOSIT BYTE
	AOJA	T2,READ6A	; BUMP COUNT AND LOOP

	 ; STATE 0

RD.ST0:	TXNN	Q1,Y.ALT!Y.VM!Y.CR!Y.CZ	; A TERMINAL?
	JRST	READ6C		; NO

	MOVEM	T2,IGNOT	; SAVE CURRENT CURSOR
	MOVEI	T1,RD.ST1	; CHANGE TO STATE 1

	 ; STATE 1

RD.ST1:	MOVEI	Z,F.FLG		; LOAD FORM FEED FLAG
	CAIN	Q2,14		; FORM FEED?
	IORM	Z,RDFLGS	; YES - SET FORM FEED FLAG

	SKIPL	IOTTY(T4)	; IS IT A TTY?
	JRST	RD.S1A		; NO

	CAIN	Q2,.CHCNZ		; EOF?
	JRST	FINBU2		; YES

	TXNN	Q1,Y.ALT!Y.VM		; BREAK CHAR?
	JRST	RD.S1A		; NO - TREAT LIKE NORMAL CASE

	IDPB	Q2,DEPTR	; DEPOSIT VALUE
	AOJA	T2,READ6G	; INC COUNT AND JUMP TO END GAME

RD.S1A:	TXNN	Q1,Y.ALT!Y.VM!Y.CR	; SOME KIND OF TERMINAL?
	MOVEI	T1,RD.ST0	; NO - BACK TO STATE 0

	TXNE	Q1,Y.ALT!Y.VM		; A BREAK?
	MOVEI	T1,RD.ST2	; YES - UP TO STATE 2

	JRST	READ6C		; GO AND DEPOSIT BYTE

	 ; STATE 2

RD.ST2:	MOVEI	Z,F.FLG		; LOAD FORM FEED FLAG
	CAIN	Q2,14		; FORM FEED CHAR?
	IORM	Z,RDFLGS	; YES - SET FORM FEED FLAG

	TXNE	Q1,Y.ALT!Y.VM		; IS IT A BREAK?
	JRST	READ6C		; YES - OK


	 ; THE SEARCH FOR A LOGICAL RECORD IS COMPLETE
	 ; PREPARE TO RETURN.

	 ; HERE ON END-OF-FILE WITHIN FINBF1

READ6D:	HRRM	Q2,IOLAST(T4)	; SAVE CHAR.

READ6G:	 ; HERE FROM TTY END-OF-LINE

	SKIPGE	IGNOT		; IGNORE MEAT LENGTH ...
	JRST	READ6F		; IF NOT SET
	TXNN	T3,FMT.T	;[246] 'T' FORMAT?
	MOVE	T2,IGNOT	; NO - USE SHORTER LENGTH

READ6F:	TXNN	T3,FMT.CP	;[246] IF CHAR PREFIX
	TRNN	T3,-1		; OR IF RIGHT SIDE 0
	JRST	READ6E		; IGNORE LENGTH FORMAT

	CAILE	T2,0(T3)	; IF T2 IS GREATER THAN FORMAT-RIGHT
	MOVEI	T2,0(T3)	; USE FORMAT RIGHT

READ6E:	MOVEM	T2,0(STACK)	; INSERT LENGTH IN DESCRIPTOR

	 ; SNIP OFF EXCESS FROM SBLOK

	MOVEI	Q2,^D14(T2)	; FIND REQUIRED ...
	IDIVI	Q2,5		; NEW LENGTH OF SBLOK TO Q2
	MOVE	Q1,-1(STACK)	; POINT TO SBLOK
	HRRZ	Q3,BLENG(Q1)	; CURRENT LENGTH
	HRRM	Q2,BLENG(Q1)	; INSERT COMPUTED LENGTH
	ADDI	SURF,0(Q2)	; ADJUST SURFACE ...
	SUBI	SURF,0(Q3)	; POINTER

	MOVE	TLINK,RDLINK##	; RESTORE LINK
	MOVE	Z,RDFLGS	; LOAD FLAGS
	JUMPN	T2,2(TLINK)	; AND RETURN (UNLESS NULL)

	MOVE	T2,NULLST	; BUT RETURN
	MOVEM	T2,-1(STACK)	; A REAL NULL STRING
	JRST	2(TLINK)	; IF LENGTH WAS 0


	 ; HERE TO DO READING FROM THE TTY

RDTTY:	MOVEM	T1,RDLINK	; SAVE LINK REG.
	MOVEM	T2,RDSTBL##	; SAVE STREAM BLOK

	TXNE	T3,FMT.WM	;[246] WORD MODE?
	ERROR	12,20		; YES-INCOMPATIBLE

	SKIPN	IOFLGS(T4)	; OPEN FOR OUTPUT?
	JRST	RDTTY1		; NO - JUMP

	MOVE	Q3,IOCHAN(T4)	; LOAD CHANNEL #
	IOR	Q3,[OUT 0-0,0]	; FLUSH OUT ...
	XCT	Q3		; CURRENT BUFFERS
	SKIPA			; GODD FLUSH
	ERROR	12,31		; BAD FLUSH

RDTTY1:	TXNN	T3,FMT.C	;[246] SINGLE CHARACTER?
	JRST	RDTTYL		; NO - GET LINE

	 ; 'C' FORMAT FOR TTY

	INCHRW	Q2		; LOAD Q2 WITH NEXT CHAR
	CAIN	Q2,32		; CHECK FOR ^Z
	JRST	FINBU1		; YES-JOIN LATTER PART OF FINBUF
	JRST	READ3C		; JOIN OTHER 'C' PROCESSING

	 ; NON-'C' FORMATS FOR TTY

RDTTYL:	MOVEI	Z,^D300		; MAX. LINE WIDTH
	JSP	QLINK,ASBLOK	; ALLOCATE BLOK
	ERROR	11,3		; EXCEEDED &MAXLNGTH
	MOVEM	Q3,DEPTR	; ESTABLISH DEP POINTER
	CLEARM	IOIBHD+2(T4)	; SET TRAP
	CLEAR	Q2,		; INITIAL CHARACTER
	MOVEI	T1,RD.ST0	; INITIALIZE STATE
	CLEAR	T2,		; INITIALIZE CHARACTER COUNT
	JRST	READ6B		; JUMP TO ENTRY POINT
				; IN GENERALIZED LINE HANDLER
	


;	FINBUF AND FINBF1 SERVE TO FILL THE NEXT INPUT
;	BUFFER IF POSSIBLE.	
;	FINBUF IS USED FOR  WORDS OR SINGLE CHARACTERS
;	FINBF1 IS USED WITH STRINGS.
; 
; CALLING SEQUENCE:
;	RETURN
;	JSP	QLINK,FINBUF [OR FINBF1]
;
;	NOTE THE NON-STANDARD CALLING SEQUENCE

FINBF1:	CAME	T4,TTYBLK	; TTY?
	JRST	FINBF4		; NO
	INCHWL	Q2		; GET NEXT CHAR
	CAIE	Q2,32		; ^Z ?
	JRST	READ6B		; NO - RETURN

	JRST	FINBU2		; JOIN LATTER PART OF FINBUF

FINBF4:	MOVE	Q2,IOCHAN(T4)	; LOAD Q2 WITH ...
	IOR	Q2,[IN 0-0]	; AN IN UUO
	XCT	Q2		; AND GO FILL BUFFERS
	JRST	FINBF2		; GOOD READ

	 ; UNUSUAL CONDITIONS

	MOVE	Q2,IOCHAN(T4)	; GET ...
	IOR	Q2,[GETSTS 0-0,Q3] ; STATUS ...
	XCT	Q2		; TO Q3

	TRNN	Q3,1B22		; END-OF-FILE?
	ERROR	12,4		; NO - INCORRECTIBLE

	JUMPN	T2,FINBF5	; JUMP IF ANY CHAR SO FAR
	JRST	FINBU2		; JOIN NEXT-FILE PROCESSING

FINBF5:	JSP	QLINK,CLS	; FIRST CLOSE OUT THE FILE
	CLEAR	Q2,		; NULL CHAR
	MOVE	Q3,RDSTBL	; LOAD CURRENT STREAM BLOK
	AOS	STCF(Q3)	; BUMP CURRENT FILE
	JRST	READ6D		; JOIN END GAME

	 ; HERE IF BUFFER WAS FILLED PROPERLY

	 ; WE WANT TO ALLOCATE ADDITONAL STORAGE FOR SBLOK

FINBF2:	MOVEM	QLINK,RDQLNK##	; SAVE LINK


	MOVE	Q2,IOIBHD+2(T4)	; # OF CHARACTERS
	IDIVI	Q2,5		; # OF WORDS TO Q2
	MOVEI	Z,1(Q2)		; 1 EXTRA FOR GOOD MEASURE
	JSP	QLINK,GCOLF##	; ENSURE ADDITIONAL STORAGE

	MOVE	Q2,-1(STACK)	; POINT TO SBLOK
	ADDM	Z,BLENG(Q2)	; BUMP WORD COUNT
	ADDM	Z,SURF		; ALSO BUMP SURFACE

	MOVE	Q2,IOIBHD+2(T4)	; GET CHAR. COUNT AGAIN
	ADDM	Q2,0(STACK)	; ADD TO STRING LENGTH
	MOVE	Q3,-1(STACK)	; RENEW BASE FOR DEPTR
	MOVE	QLINK,RDQLNK##	; RESTOR QLINK
	JRST	-2(QLINK)	; RETURN BACKWARD

;	FINBUF - FILL INPUT BUFFER

FINBUF:	MOVE	Q2,IOCHAN(T4)	; LOAD Q2 WITH IN ...
	IOR	Q2,[IN 0]	; UUO
	XCT	Q2		; EXECUTE SAME
	JRST	-2(QLINK)	; SUCCESSFUL FILL

	 ; UNUSUAL CONDITION ENCOUNTERED

	MOVE	Q2,IOCHAN(T4)	; GET
	IOR	Q2,[GETSTS Q3]	; STATUS ...
	XCT	Q2		; INTO Q3

	TRNN	Q3,1B22		; END-OF-FILE?
	ERROR	12,4		; INCORRECTIBLE

	 ; HERE FROM ^Z ON TTY
	 ; MOVE ON TO NEXT FILE IN STREAM

	SKIPA
FINBU2:	SUB	STACK,XWD22##	; POP STRING

FINBU1:	JSP	QLINK,CLS	; CLOSE FILE
	MOVE	T2,RDSTBL	; RELOAD STBLOK ADDRESS
	AOS	STCF(T2)	; INCREMENT CURR FILE
	MOVE	TLINK,RDLINK	; RELOAD LINK
	JRST	READ		; START OVER WITH NEW FILE

	SUBTTL	Tertiary routines -- MAKEIO
	SUBTTL	Tertiary routines -- STRIO

;	STRIO - STREAM FOR I/O
;
; CALLING SEQUENCE:
;	GIVEN THAT A STRING BEARING A FILE SPEC HAS
;	PREVIOUSLY BEEN PASSED TO STINIT...
;	JSP	TLINK,STRIO
;	ERROR
;	NORMAL - FILE DESCRIPTOR ON STACK AND 
;		 Z CONTAINS BREAK CHARACTER (OR 0 ON 
;		 RUNNOUT) CURSOR POSITIONED AFTER BREAK CHAR

;	MAKEIO (ALTERNATE ENTRY POINT TO STRIO)
;
; CALLING SEQUENCE:
;
;	SET FILE SPEC VARIABLES TO DESIRED STATE
;
;	JSP	TLINK,MAKEIO
;	ERROR
;	NORMAL - I/O DESCRIPTOR ON STACK, Z ZEROED

	 ; ENTRY POINTS

STRIO:	MOVEI	T2,STIO1	; FIRST LOCATION
	PUSHR	PST		; ELEVATE STATUS
	JRST	0(T2)		; START


MAKEIO:	MOVEI	T2,STIO99	; ENTER BY BACK DOOR
	JRST	STRIO+1		; JOIN STRIO


	 ; INITIALIZE FILE-SPEC VARIABLES

STIO1:	JSP	QLINK,CLRFSV	; CLEAR FILE-SPEC VARIABLES

	MOVSI	S1,'DSK'	; DEFAULT ...
	MOVEM	S1,DEVICE##	; DEVICE

	 ; CHECK FOR LEADING ALPHANUMERICS

	MOVE	S2,SYSCT	; BASE ALL STREAMING

	JSP	SLINK,NONBLK	; ZIP ACROSS BLANKS
	JRST	STIONL		; NO FILE

STIO2:	MOVX	S3,Y.UPS!Y.LOWS!Y.NUM	; DID WE FIND AN ...
	JSP	TLINK,CHECK##	; ALPHANUMERIC?
	JRST	STIO35		; NO

	JSP	TLINK,STRMC##	; LOOK FOR END OF IT
	JRST	STIO3		; RUNNOUT (WILL USE NAME)
	JSP	SLINK,NONBLK	; SKIP TO NEXT NONBLANK
	JRST	STIO3		; SAME AS RUNNOUT

	CAIE	Q2,":"		; COLON?
	JRST	STIO3		; NO

	IBP	STPTR		; INCREMENT ...
	SOS	CURSOR		; CURSOR

	MOVX	Z,FMT.S!FMT.WM	;[246]CONVERT
	JSP	TLINK,CVTDW	; TO SIXBIT
	JRST	STBOMB		; ERRONEOUS FILE SPEC
	MOVEM	Z,DEVICE	; ESTABLISH AS DEVICE

	JSP	SLINK,NONBLK	; SEEK NEXT NON BLANK
	JRST	STIO99		; NONE MEANS DONE
	JRST	STIO2		; LOOP FOR NAME
	
	 ; HERE TO PROCESS NAME

STIO3:	MOVX	Z,FMT.S!FMT.WM	;[246] CONVERT TO ...
	JSP	TLINK,CVTDW	; SIXBIT
	JFCL			; NOOP - IGNORE LONG STRING
	MOVEM	Z,FILNAM##	; ESTABLISH AS FILE NAME
	SKIPN	FRSTNM##		; FIRST NAME?
	MOVEM	Z,FRSTNM	; YES, SAVE IT


	SKIPG	CURSOR		; AT END?
	JRST	STIO99		; YES



	 ; STREAM FOR AND PROCESS EXTENSION

STIO35:	MOVX	S3,Y.PER	; IS NONBLANK ...
	JSP	TLINK,CHECK##	; A PERIOD?
	JRST	STIO4		; NO

	CLEARM	DFTEXT##		; YES, CLEAR DEFAULT EXTENSION

	IBP	STPTR		; INCREMENT PAST ...
	SOSG	CURSOR		; PERIOD
	JRST	STIO99		; JUMP IF NO CHARS LEFT

	JSP	SLINK,NONBLK	; SKIP TO FIRST NONBLANK
	JRST	STIO99		; NONE MEANS DONE

	MOVX	S3,Y.UPS!Y.LOWS!Y.NUM	; ALPHA-
	JSP	TLINK,CHECK	; NUMERIC?
	JRST	STIO4		; NO - TRY PPN

	JSP	TLINK,STRMC	; LOOK FOR END OF THEM
	JFCL			; ON RUNNOUT - NOOP

	MOVX	Z,FMT.S!FMT.WM	;[246] CONVERT STRING
	JSP	TLINK,CVTDW	; TO SIXBIT
	JFCL			; IGNORE OVERFLOW
	HLLM	Z,EXT##		; ESTABLISH AS EXTENSION

	 ; HERE TO PROCESS PPN

STIO4:	JSP	SLINK,NONBLK	; FIND SOMETHING?
	JRST	STIO99		; NOTHING

	CAIE	Q2,"["		; START OF PPN?
	JRST	STIO5		; NO - TRY PRIV

	IBP	STPTR		; GET PAST ...
	SOS	CURSOR		; THE BRACKET

	JSP	SLINK,GETOCT	; GET OCTAL
	HRLM	Z,PPN##		; THIS IS PROJECT NUMBER

	CAIN	Q2,","		; WAS BREAK A COMMA?
	JRST	STIO40	; YES, SKIP THIS

	CAIE	Q2,"-"		; WAS IT A HYPHEN?
	 JRST	STBOMB		; CAN'T BE ANYTHING ELSE

	CLEARM	PPN		; Use default path

	JSP	SLINK,NONBLK	; GET NEXT CHAR
	 JRST	STIO99		; NOTHING ELSE

	IBP	STPTR		;[210]  eat that character
	SOS	CURSOR		;[210]   ..
	JRST	STIO5		;]210]  go get rest

STIO40:	JSP	SLINK,GETOCT	; GET NEXT ...
	HLL	Z,PPN		; PROGRAMMER NUMBER
	GETPPN	Q3,		; GET USER'S PPN
	TLNN	Z,-1		; IS THERE A PROJ. NUMBER?
	 HLL	Z,Q3		;  NO - USE USER'S
	TRNN	Z,-1		; IS THERE A PROG. NUMBER?
	 HRR	Z,Q3		;  NO - USE HIS
	MOVEM	Z,PPN


	 ; START PROCESSING OF THE SFD'S

	CAIE	Q2,"]"		; IS THIS THE END?
	CAIE	Q2,","		; WELL IS THERE A COMMA?
	JRST	STIO4B		;[210]  go see what else lurks about

	MOVEI	Q3,3		; SET UP FOR SFD'S
	MOVEM	Q3,PTHPTR##	; SET PONTER TO FIRST SFD
	JRST	SFDFIR		; ENTER WERE IT DOESN'T COUNT

	 ; HERE TO PROCESS THE SFD'S

STNSFD:	MOVEM	Q3,PTHPTR	; KEEP Q3 OUT OF TROUBLE
	JSP	SLINK,NONBLK	; GET NEXT CHAR
	JRST	STIO4A		; NOTHING

	CAIE	Q2,"]"		; HAS THE SFD SPEC ENDED?
	CAIE	Q2,","		; IS THERE A COMMA NEXT?
	JRST	STIO4A		; GO SEE WHAT ELSE AND DO OTHER THINGS

	IBP	STPTR		; EAT THE ...
	SOS	CURSOR		; COMMA

SFDFIR:	MOVX	S3,Y.UPS!Y.LOWS!Y.NUM	; CHECK FOR ...
	JSP	TLINK,CHECK##	; A ALPHA-NUMERIC
	JRST	STBOMB		; NOPE - BOMB OUT

	JSP	TLINK,STRMC##	; FIND THE END
	JFCL			; DON'T WORRY ABOUT IT

	MOVX	Z,FMT.S!FMT.WM	;[246] CONVERT TO ...
	JSP	TLINK,CVTDW	; SIXBIT
	JFCL			; TOO LONG BUT WHO CARES?

	MOVE	Q3,PTHPTR	; GET THE POINTER BACK
	MOVEM	Z,PTH##(Q3)	; PUT SFD IN PATH

	AOJ	Q3,		; INCREMENT
	CAIN	Q3,PTHEND##-PTH-3	; ARE THERE TOO MANY SFD'S?
	JRST	STBOMB		; WELL HE BLEW IT
	JRST	STNSFD		; GO BACK FOR MORE

STIO4A:	IBP	STPTR		;[210]  get past
	SOS	CURSOR		;[210]  character

STIO4B:	MOVEI	Q1,1		;[210]  put the path dummy arg
	MOVEM	Q1,PTH+1	; TO KEEP PATH. HAPPY

	MOVE	Q1,PPN		; PPN GOES A WELL
	MOVEM	Q1,PTH+2	;

	JSP	SLINK,NONBLK	; GET WIDGIT FOR PRIV
	 JRST	STIO99		; NOTHING THERE.

	 ; HERE TO PROCESS <PRIVILEGE>

STIO5:	CAIE	Q2,"<"		; ONE OF THESE?
	JRST	STIO6		; NO
	IBP	STPTR		; GET PAST
	SOS	CURSOR		; CHARACTER

	JSP	SLINK,GETOCT	; CONVERT TO OCTAL
	LSH	Z,^D27		; MOVE INTO FIELD 0-8
	MOVEM	Z,PRIV##		; OF PRIV

	JRST	STIO4		; LOOK FOR MORE THINGS

	 ; HERE WHEN STRANGE UNIDENTIFIED CHARACTER IS ENCOUNTERED

STIO6:	MOVEM	Q2,TERMCH##	; LAST CHAR PICKED UP
	SOS	CURSOR		; AND STEP ...
	IBP	STPTR		; PAST HIM

	 ; MERGE HERE FROM RUNNOUT CONDITIONS
	 ; LOOK FOR IDENTICAL FILE PREVIOUSLY INSTALLED

STIO99:	MOVE	T1,DEVICE	; LOAD ...
	MOVE	T2,FILNAM	; PARAMETERS ...
	MOVE	T3,EXT		; INTO ...
	MOVE	T4,PPN		; REGISTERS ...
	MOVE	Q1,PRIV		; FOR FAST COMPARISON

	 ; CHECK FOR UFD

	MOVS	Q2,EXT		; IS EXTENSION ...
	CAIN	Q2,'UFD'	; UFD ?
	SKIPE	FILNAM		; AND IS NAME NULL?
	JRST	STIO98		; THEN AVOID THIS BRANCH

	MOVE	T2,PPN		; AND SET NAME FROM PPN
	MOVEI	T4,16		; AND ...
	GETTAB	T4,		; RETRIEVE ...

	MOVE	T4,[1,,1]	; MPD PPN
	MOVEM	T4,PTH+2	; PUT IT IN PTH.

	MOVEI	Q2,1		; PLACE DUMMY IN PTH ...
	MOVEM	Q2,PTH+1	; SO IT WILL NOT LOOKUP DEFAULT.

	 ; CHECK FOR NULL FILE

STIO98:	MOVS	Q2,DEVICE	; IF DEVICE IS ...
	CAIN	Q2,'DSK'	; DSK AND ...
	JUMPE	T2,STIONL	; NAME IS NULL, REPORT NULL FILE

	 ; DEFAULT EXTENSION

	CAIN	T3,0		; IS THE EXTENSION 0?
	CAIN	T2,0		; AND IS THE NAME NONZERO
	SKIPA			; THEN
	MOVE	T3,DFTEXT	; USE THE DEFAULT

	 ; If there was no path - Just clear the PTH area

	MOVE	Q2,PTH+1	; FIRST LOOK IN PTH ...
	JUMPN	Q2,STIO9I	; TO SEE IF IT HAS BEEN DEFINED

	CLEARM	PTH+2		; Use default.

STIO9I:	MOVEI	S1,IOBLKS##	; POINT TO LIST OF IOBLOKS

	 ; SPECIAL TTY TEST

	MOVE	S2,TTYBLK	; LOAD TTYBLK
	JUMPE	S2,STIO7	; IF STILL 0, MOVE ON
	CAMN	T1,[SIXBIT /TTY/] ; IS OUR DEVICE TTY?
	JRST	STIO11		; YES - AVOID SEARCH

	 ; TOP OF SEARCH LOOP

STIO7:	SKIPN	S2,0(S1)	; LOAD ADDR OF NEXT IOBLOK
	JRST	STIO9		; NONE LEFT

	MOVE	Z,T1		; GET THE DEVICE
	DEVNAM	Z,		; 'REAL' NAME
	 JFCL			;[273] WILL DETECT THIS LATER.
	MOVE	Q2,IODEV(S2)	; OTHER DEVICE
	DEVNAM	Q2,		; OTHER 'REAL' NAME
	 JFCL			;[273] DON'T CARE.
	CAMN	Q2,Z		; ARE THEY THE SAME?

	CAME	T2,IONAME(S2)	; NAME?
	JRST	STIO8		; JUMP IF UNSAME

	CAME	T3,IOEXT(S2)	; EXTENSION?
	JRST	STIO8		; JUMP IF DIFF

	SKIPN	T4		;[303] Is ppn zero?
	SKIPN	IOSFD(S2)	;[303] ..
	 JRST	STIO7A		;[260] No -- avoid this.
	MOVEI	Q2,.PTFRD	;[260] Lookup default path.
	MOVEM	Q2,PTH		;[260] This is an argument.
	MOVE	Q2,[XWD PTHEND-PTH,PTH]	;[260] Argument for path.
	PATH.	Q2,		;[260] Do the UUO.
	 ERROR	15		;[260] Error.
	MOVE	Z,IOSFD(S2)	;[260] NO USE THIS TO COMPARE
	CAME	Z,PTH+2		;[260] ARE THESE THE SAME PPN?
	 JRST	STIO8		;[260] NO - BRANCH
	
STIO7A:	CAME	Q1,IOPRIV(S2)	;[301] Same priv?
	JRST	STIO8		;[301] Jump if different.

	MOVSI	Q2,-<PTHEND-PTH-3>	;[301] Set up neg count.
	HRRI	Q2,1		;[301] Start looking at SFD's
	HRRZ	Q3,S2		;[301] Another pointer.

STIO7B:	MOVE	Z,PTH+2(Q2)	;[301] Compare this specs PATH
	ADDI	Q3,1		;[301] Increment.
	CAME	Z,IOSFD(Q3)	;[301] with this previous spec.
	 JRST	STIO8		;[301] They are different.
	AOBJN	Q2,STIO7B	;[301] Is there more?
	 JRST	STIO11		;[301] Go away - it is identical

STIO8:	MOVEI	S1,IONEXT(S2)	; POINT TO NEXT POINTER
	JRST	STIO7		; AND LOOP BACK


	 ; HERE IF NO MATCH WAS FOUND

STIO9:	MOVEI	Z,IOLENG	; ENSURE ...
	JSP	QLINK,GCOLG##	; ENOUGH GROWING STORGE

	MOVE	S2,GSURF##	; INSERT ADDRESS OF ...
	MOVEM	S2,0(S1)	; FRESH BLOK
	ADDM	Z,GSURF		; UPDATE GSURF

	HRLI	Z,IOBLOK	; INSERT CODE
	MOVEM	Z,BTCODE&BLENG(S2) ; AND LENGTH

	CLEARM	GCFLD(S2)	; CLEAR ...
	HRRI	Z,GCFLD+1(S2)	; ALL ...
	HRLI	Z,GCFLD(S2)	; OF NEW ...
	BLT	Z,IOLENG-1(S2)	; IOBLOK

	MOVEM	T1,IODEV(S2)	; TRANSFER
	MOVEM	T2,IONAME(S2)	; THE ...
	MOVEM	T3,IOEXT(S2)	; GOODIES

	 ; PUT SFD'S FROM PTH INTO IOSFD

	HRLI	Q2,PTH+2	; START WERE THE PPN GOES
	HRRI	Q2,IOSFD(S2)	; AND PUT THAT IN AT THE START OF IOSFD
	BLT	Q2,IOSFD+7(S2)	;  FILL IT UP.

STIO9B:	MOVE	Q1,PRIV	; RELOAD PRIVILEGE

	 ; HERE FROM DETECTION OF MATCH

STIO11:	MOVEM	Q1,IOPRIV(S2)	; SET PRIVILEGE

	MOVE	Q2,S2		; SAVE ADDR OF IOBLOK
	HRLI	Q2,IODT		; FILL DATATYPE FIELD



	 ; HERE FROM NULL FILE CASE

STIO12:	HLLZ	T1,IODEV(S2)	;GET THE DEVICE NAME
	CAMN	T1,[SIXBIT /TTY/] ;IS IT TTY ?
	  JRST	STIO13		;YES - DON'T CHECK FOR NULL FILE NAME
	SKIPN	IONAME(S2)	;NOT A TTY - MUST NOT BE NULL
	  JRST	STBOMB		;IT IS -- BOMB
STIO13:	POPR	PST		; RESTORE REGS

	PUSH	STACK,Q2	; PUSH ...
	PUSH	STACK,[0]	; DESCRIPTOR

	MOVE	Z,TERMCH	; SET BREAK CHAR
	JRST	1(TLINK)	; AND RETURN

	 ; HERE IF NO CHARACTERS IN STRING AT ALL

STIONL:	CLEAR	Q2,		; RETURN 0-0
	JRST	STIO13		;[210]  join common end
	

	 ; HERE IF THE FILE SPEC HAS POOR SYNTAX

STBOMB:	POPR	PST		; RESTORE ALL REGS.
	JRST	0(TLINK)	; TAKE HUMBLE EXIT

	SUBTTL	Tertiary routines -- WRITE

;	WRITE - EXTERNAL UTILITY
;
; CALLING SEQUENCE:
;	LOAD Z WITH WORD (FOR WORD MODE)
;	PUSH STRING DESCRIPTOR ONTO STACK0(OTHERWISE)
;	LOAD T2 WITH ADDRESS OF IOBLOK
;	LOAD T3 WITH FORMAT
;	JSP	TLINK,WRITE
;	ERROR
;	NORMAL ; ARG POPPED FROM STACK

	 ; ENTRY POINT

WRITE::	JUMPL	T3,WRIT99	; NO-OP FLAG
	JUMPE	T2,WRIT99	; OR NO FILE - RETURN

	MOVE	T4,T2		; SET UP ARG FOR Q-CALLS
	MOVEM	Z,IGNOT		; SAVE WORD ARGUMENT
	MOVE	Q3,IOFLGS(T2)	; GET FLAGS
	TLNN	Q3,IOOUFL	; CURRENTLY OPENED OUT?
	JRST	WRDONT
	TXNN	T3,FMT.AP	;[246]
	JRST	WRIT1

	 ; CLOSE THE FILE AND OPEN IT FOR OUTPUT

WRDONT:	JSP	QLINK,CLS	; CLOSE THE FILE (IF OPENED)

	MOVE	Z,T3		; DETERMINE THE ...
	JSP	QLINK,MODE	; MODE

	JSP	QLINK,OPN	; OPEN THE FILE ACCORDING TO THE WORD
	ERROR	12,22		; COULD NOT OPEN
	TXNE	T3,FMT.AP	;[246]
	JRST	WRDNT		;[275]


	JSP	QLINK,ENTR	; ENTER THE NAME
	ERROR	12,22		; BAD OPEN

WRDNT:	MOVSI	Z,IOOUFL	;[275] ALLOCATE OUTPUT ...
	JSP	QLINK,ALBUF	; BUFFERS

	 ; HERE FROM ALREADY-OPEN CASE

WRIT1:	MOVE	Z,T3		; DETERMINE ...
	JSP	QLINK,MODE	; MODE
	XOR	Z,IOMODE(T4)	; COMPARE WITH CURRENT FOR ...
	TRNE	Z,10		; WORD - CHAR INCOMPATIBILITY
	ERROR	12,19		; MODE - INCOMPATIBLE

	TXNN	T3,FMT.AP	;[246] APPEND MODE?
	JRST	WRIT11		; NO.

	JSP	QLINK,LKP
	ERROR	12,39

	JSP	QLINK,ENTR
	ERROR	12,39

	MOVE	Q3,IOCHAN(T4)
	HRRI	Q3,-1
	IOR	Q3,[USETI 0-0,0]
	XCT	Q3

WRIT11:	TXNN	T3,FMT.WM	;[246] WORD MODE?
	JRST	WRIT2		; NO - ASCII



	 ; WORD MODE

	MOVE	Q2,IGNOT	; LOAD WORD FOR OUTPUT

WRIT1A:	SOSGE	IOOBHD+2(T4)	; DECREMENT COUNT
	JSP	QLINK,EMPBUF	; EMPTY BUFFER
	IDPB	Q2,IOOBHD+1(T4)	; OUT HE GOES
	JRST	1(TLINK)	; RETURN
 	 ; ASCII OUTPUT

WRIT2:	HRRZ	T2,0(STACK)	; GET LENGTH
	JSP	QLINK,CVTPTR	; MAKE POINTER
	0			; NO BASING
	SUB	STACK,XWD22##	; NULL STRING

	MOVEM	Z,EXPTR##	; ESTABLISH EXTRACTION PTR

	 ; 'C' FORMAT

WRIT3:	TXNN	T3,FMT.C	;[246] 'C' ?
	JRST	WRIT4		; NO - ON TO OTHER THINGS

	JUMPE	T2,1(TLINK)	; WAS LENGTH 0?

	MOVEI	T2,1		; MAKE SURE ITS ONE CHAR
	TXO	T3,FMT.T	;[246] SIMULATE TERMINAL FORMAT
	JRST	WRIT6		; PROCESS LIKE NORMAL WRITE


	 ; 
	 ; 'N' FORMAT

WRIT4:	TXNN	T3,FMT.N	;[246] 'N' FORMAT?
	JRST	WRIT5		; NO - MOVE ON

	CAIGE	T2,6		; ENOUGH CHARACTERS?
	ERROR	12,24		; MUST BE AT LEAST 6

	 ; LOOP FOR BYTE AT BEGINNING OF WORD

WRIT4A:	SOSGE	IOOBHD+2(T4)	; ANOTHER LEFT?
	JSP	QLINK,EMPBUF	; EMPTY IF NECESSARY
	IBP	IOOBHD+1(T4)	; INCREMENT POINTER
	MOVE	Q3,IOOBHD+1(T4)	; LOAD POINTER
	LSH	Q3,-^D30	; POSITION OF BYTE
	CAIE	Q3,^D29		; LEFT-MOST = 29
	JRST	WRIT4A		; KEEP LOOPING IF NOT LEFT

	 ; LEFT MOST BYTE HAS BEEN FOUND

	CAMGE	T2,IOOBHD+2(T4)	; ENOUGH LEFT
	JRST	WRIT4B		; YES

	CLEARM	IOOBHD+2(T4)	; PRETEND WE'VE WRITTEN ALL
	JRST	WRIT4A		; JUST LOOP


	 ; HERE WHEN WE'RE READY TO START WRITING

WRIT4B:	MOVE	Q2,IOOBHD+1(T4)	; GET ACTUAL POINTER
	MOVEI	Z,1		; RIGHT-MOST BIT
	IORM	Z,0(Q2)		; SET BIT IN BUFFER

	ILDB	Q2,EXPTR	; GET FIRST BYTE
	DPB	Q2,IOOBHD+1(T4)	; DEPOSIT W/O INCREMENTING
	SOJA	T2,WRIT5	; DECREMENT COUNT AND LEAP


	 ; CHARACTER PREFIX

WRIT5:	TXNN	T3,FMT.CP	;[246] PREFIX REQUIRED?
	JRST	WRIT6		; NO - ONWARD

	SOSGE	IOOBHD+2(T4)	; DECREMENT COUNT
	JSP	QLINK,EMPBUF	; EMPTY BUFFER IF NECESSARY
	IDPB	T3,IOOBHD+1(T4)	; DEPOSIT BYTE
	JRST	WRIT6		; JOIN REST OF PROCESSING

	 ; BASIC WRITE LOOP

WRIT6:	SOJL	T2,WRIT7	; LOOP ON STRING LENGTH
	SOSGE	IOOBHD+2(T4)	; DECREMENT BUFFER LENGTH
	JSP	QLINK,EMPBUF	; EMPTY BUFFER IF NECESSARY

	ILDB	Q2,EXPTR	; GET NEXT CHAR
	IDPB	Q2,IOOBHD+1(T4) ; DEPOSIT SAME
	JRST	WRIT6		; AND LOOP

	 ; FINISHING UP

WRIT7:	TXOE	T3,FMT.T	;[246] TERMINAL CHARACTERS?
	JRST	WRIT9		; JUST RETURN 

	MOVE	Q2,[POINT 7,[BYTE (7)15,12]] ; POINTER TO ...
	MOVEM	Q2,EXPTR	; <CR> <LF>
	MOVEI	T2,2		; LENGTH IS 2
	JRST	WRIT6		; BACK FOR MORE WRITING

	 ; HERE TO DO FINAL RETURN

WRIT9:	SKIPL	IOTTY(T4)	; TTY?
	JRST	1(TLINK)	; NO - JUST RETURN
	JSP	QLINK,EMPBUF	; YES, EMPTY BUFFER FIRST

	 ; QUICK RETURN

WRIT99:	SUB	STACK,XWD22##	; POP ARG
	JRST	1(TLINK)	; NORMAL RETURN


; EMPBUF IS CALLED ONLY FROM WITHIN WRITE
; TO EMPTY A BUFFER (AND SWITCH TO THE NEXT)
; CALLING SEQUENCE
;	RETURN
;	JSP	QLINK,EMPBUF

EMPBUF:	MOVE	Q3,IOCHAN(T4)	; LOAD CHANNEL #
	IOR	Q3,[OUT 0-0,0]	; COMPLETE THE INSTRUCTION
	XCT	Q3		; ADVANCE BUFFERS
	JRST	-2(QLINK)	; OK - EXIT
	ERROR	12,5		; OUTPUT ERROR

	SUBTTL	TYPE Q (QUATERNARY) I/O UTILITIES

;	ALBUF	ALLOCATE BUFFERS

;	CLRFSV - CLEAR FILE SPEC VARIABLES

;	CLS - CLOSE A FILE

;	ENTR - ENTER A FILE NAME

;	LKP - LOOKUP A FILE NAME

;	MODE - DETERMINE MODE FROM FORMAT

;	MSTIME - RETURN RUN TIME

;	OPN - OPEN A FILE

;	WMPROP - COMPUTE WORD MODE PROPERTIES

	SUBTTL	Quaternary routines -- ALBUF

; ALBUF WILL ALLOCATE BUFFERS (2) FOR A FILE.
; THE FILE MUST NOT BE CURRENTLY BUFFERED (UNLESS TTY).
;
; CALLING SEQUENCE:
;	LOAD T4 WITH ADDRESS OF IOBLOK
;	LOAD Z LEFT WITH IOINFL OR IOOUFL
;	JSP	QLINK,ALBUF
;	RETURN

ALBUF:	MOVEM	QLINK,ALBFSV##	; SAVE LINK
	CAME	T4,TTYBLK	; TTY ?
	JRST	ALBUF0		; NO - JUMP

	TLNN	Z,IOINFL	; IF ITS INPUT THEY WANT
	SKIPE	IOFLGS(T4)	; OR IF BUFFERS ARE THERE
	JRST	0(TLINK)	; RETURN IMMEDIATELY

ALBUF0:	MOVEM	Z,IOFLGS(T4)	; SET FLAGS
	MOVEI	Q2,IOMODE(T4)	; POINT TO OPEN BLOCK
	DEVSIZ	Q2,		; GET BUFFER SIZE
	ERROR	12,28		; BAD DEVICE
	LSH	Q2,1		; DOUBLE SIZE
	ADDI	Q2,BUBITS	; ADD TO DETERMINE BLENG
	HRLI	Q2,OBBLOK	; LOOKING FOR AN OLD BUFFER
	MOVE	Q3,GRID		; POINT TO BEGINNING OF GRID



	 ; SEARCH LOOP FOR OLD BUFFER

ALBUF1:	CAML	Q3,GSURF	; ARE WE STILL BELOW SURFACE
	JRST	ALBUF4		; NO - MOVE ON

	MOVEI	Q1,VARSIZ	; VARIABLE BLOCK SIZE
	MOVE	Z,BTCODE&BLENG(Q3) ; LOAD FIRST WORD
	TLNE	Z,BLOKF		; A BLOK ?
	HRRZ	Q1,Z		; YES - USE BLENG
	ADDI	Q3,0(Q1)	; INCREMENT POINTER
	CAME	Q2,Z		; IS CURRENT BLOK USEFUL
	JRST	ALBUF1		; NO - JUMP

	SUBI	Q3,0(Q1)	; YES - FIND OLD ADDRESS
	HRRM	Q3,IOBUBL(T4)	; ESTABLISH AS BUFFER
	MOVE	Q2,Q3		; HAVE Q2 POINT TO BUBLOK
	JRST	ALBUF6		; JOIN COMMON PROCESSING
	
	 ; COULDN'T FIND AN OLD ONE

ALBUF4:	MOVEI	Z,0(Q2)	; GET SIZE REQUIRED
	JSP	QLINK,GCOLG##	; ENSURE THIS MUCH GRID

	MOVE	Q2,GSURF	; POINT TO NEW AREA
	HRRM	Q2,IOBUBL(T4)	; INSERT AS BUFFER
	ADDM	Z,GSURF		; BUMP SURFACE POINTER

ALBUF6:	CLEARM	GCFLD(Q2)	; MAKE GC HAPPY
	HRLI	Z,BUBLOK	; INSERT BTCODE
	MOVEM	Z,BTCODE&BLENG(Q2) ; FILL FIRST WORD
	 ; 
	 ; NOW TELL THE SYSTEM ABOUT OUR BUFFER

ALBUF5:	MOVEI	Q3,BUBITS(Q2)	; POINT TO MEAT OF BUFFER
	HRRM	Q3,.JBFF	; INSERT FOR SYSTEM SAKE

	MOVE	Q2,[INBUF 2]	; LOAD INSTRUCTION (INPUT)
	MOVE	Q1,IOFLGS(T4)	; GET FLAGS
	TLNE	Q1,IOOUFL	; BUT IS IT REALLY OUTPUT
	MOVE	Q2,[OUTBUF 2]	; YES

	IOR	Q2,IOCHAN(T4)	; COMPLETE INSTRUCTION
	XCT	Q2		; EXECUTE INSTRUCTION
	JRST	@ALBFSV		; AND RETURN

	SUBTTL	Quaternary routines -- CLRFSV

;	CLRFSV - CLEAR FILE SPEC VARIABLES
;	(SEE STRIO PROLOGUE FOR DECLARATIONS)

CLRFSV:	CLEARM	DEVICE		; ZERO FIRST WORD
	MOVE	Z,[DEVICE,,FILNAM] ; FROM - TO
	BLT	Z,TERMCH	; BLIITT
	JRST	0(QLINK)	; RETURN

	SUBTTL	Quaternary routines -- CLS

;	CLS - CLOSE A FILE. RELEASE ANY BUFFERS AND ISSUE 
;	A CLOSE UUO
; CALLING SEQUENCE:
;	LOAD T4 WITH ADDRESS OF IOBLOK
;	JSP	QLINK,CLS
;	RETURN

CLS::	SKIPN	Q3,IOCHAN(T4)	; IF ALREADY CLOSED
	JRST	0(QLINK)	; RETURN

	IOR	Q3,[CLOSE 0,0]	; COMPLETE THE UUO
	XCT	Q3		; AND EXECUTE IT

	MOVE	Q3,IOCHAN(T4)	; CHECK
	IOR	Q3,[STATZ 0-0,740000] ; FOR
	XCT	Q3		; AN 
	ERROR	12,9		; ERROR IN CLOSING

	MOVE	Q3,IOCHAN(T4)	; NOW
	IOR	Q3,[RELEAS 0-0,0] ; RELEASE
	XCT	Q3		; IT

	CLEARM	IOCHAN(T4)	; CLEAR CHANNEL INDICATOR

	 ; RELEASE BUFFERS

	SKIPN	Q3,IOBUBL(T4)	; LOAD AND TEST BUFFER
	JRST	0(QLINK)	; ALREADY 0 - JUST RETURN

	MOVEI	Q2,OBBLOK	; OLD BUFFER CODE
	HRLM	Q2,BTCODE(Q3)	; INSERTED IN OLD BUFFER
	CLEARM	IOFLGS&IOBUBL(T4) ; ZAP OUT BUFF INDICATORS

	JRST	0(QLINK)	; RETURN

	SUBTTL	Quaternary routines -- LKP

;	LKP - LOOKUP A FILE
;	CALLS LOOKUP UUO
;	FILE MUST HAVE PREVIOUSLY BEEN OPENED
;
; CALLING SEQUENCE:
;	LOAD T4 WITH IOBLOK ADDR
;	JSP	QLINK,LKP
;	FAIL
;	RETURN


LKP:	SKIPA	Q3,[LOOKUP 0-0,FILNAM] ; LOAD UUO
;	JRST	ENTR+1		; JOIN ENTR PROCESSING

	SUBTTL	Quaternary routines -- ENTR

;	ENTR - ENTER A FILE NAME IN THE DIRECTORY
;	(CALLS ENTER UUO)
;
; CALLING SEQUENCE:
;	LOAD T4 WITH IOBLOK
;	JSP	QLINK,ENTR
;	ERROR (REFLECTED ERROR)
;	RETURN
;	FILE MUST HAVE BEEN PREVIOUSLY OPENED


ENTR:	MOVE	Q3,[ENTER 0-0,FILNAM] ; LOAD UUO

	 ; HERE FROM LKP

	IOR	Q3,IOCHAN(T4)	; CHANNEL #

ENTR2:	HRLI	Q2,IONAME(T4)	; FROM ADDRESS
	HRRI	Q2,FILNAM	; TO ADDRESS
	BLT	Q2,PPN		; TRANSFER GOODIES

	HRLI	Q2,IOSFD(T4)	; FILL UP PTH USING ...
	HRRI	Q2,PTH+2	;  WHATS IN IOSFD
	BLT	Q2,PTHEND	; TO THE END

	MOVE	Q2,[XWD	0,PTH]
	SKIPN	PTH+2		; Do we want the default path?
	CLEARM	Q2		; YES.
	MOVEM	Q2,PPN		; PATH

	XCT	Q3		; EXECUTE THE UUO
	JRST	0(QLINK)	; COULDN'T ENTER OR LOCATE
	JRST	1(QLINK)	; OK - COULD
	SUBTTL	Quaternary routines -- MODE

;	MODE - RETURNS THE I/O MODE GIVEN THE FORMAT WORD
;	OF AN I/O DESCRIPTOR.
;
; CALLING SEQUENCE:
;	LOAD Z WITH FORMAT WORD
;	JSP	QLINK,MODE
;	RETURN WITH MODE IN Z

MODE:	MOVEI	Q2,.IOASL	; ASCII MODE (TENTATIVELY)
	TXNE	Z,FMT.WM	;[246] BUT IF WORD MODE ...
	MOVEI	Q2,.IOIBN	; USE IMAGE BINARY

	MOVE	Z,Q2		; PASS TO RESULT REG.
	JRST	0(QLINK)	; RETURN

	SUBTTL	Quaternary routines -- MSTIME

;	MSTIME - EXTERNAL UTILITY

MSTIME::CLEAR	Z,		; CLEAR Z TO ...
	RUNTIM	Z,		; GET THE TIME
	JRST	0(QLINK)	; AND RETURN IT

	SUBTTL	Quaternary routines -- OPN

;	OPN WILL OPEN A FILE ON THE NEXT AVAILABLE
;	CHANNEL. IT SHOULD NO ALREADY BE OPEN.
;
; CALLING SEQUENCE:
;	LOAD Z WITH MODE
;	LOAD T4 WITH IOBLOK ADDRESS
;	JSP	QLINK,OPN
;	ERROR (COULDN'T OPEN)
;	NORMAL

OPN:	SKIPE	IOCHAN(T4)	; ALREADY OPEN?
	JRST	1(QLINK)	; YES

	MOVEM	Z,IOMODE(T4)	; SET THE MODE

	MOVEI	Q3,IOIBHD(T4)	; INBUF HEADER
	HRLI	Q3,IOOBHD(T4)	; OUTBUF HEADER
	MOVEM	Q3,IOBUFS(T4)	; OUTBUF,,INBUF WORD

	MOVE	Q3,IODEV(T4)	; DETERMINE DEVICE
	DEVCHR	Q3,		; CHARACTERISTICS

	TRNN	Q3,200000	; IF ITS NOT ALREADY INITED?
	JRST	OPN2		; THEN LET THE OPEN OCCUR
	TLNN	Q3,200010	; ALSO IF ITS DISK OR TTY LET IT OPEN
	ERROR	12,17		; NO - CAN'T OPEN DEVICE AGAIN.
OPN2:	CLEARM	IOLAST(T4)	; LAST CHAR READ IS VOID

	MOVEI	Z,-1		; SETUP FOR TTY FLAG
	TLNE	Q3,10		; IS DEVICE TTY?
	HRLM	Z,IOTTY(T4)	; YES, SET FLAG

	 ; FIND THE NEXT AVAILABLE CHANNEL (EXCEPT 0)

	MOVEI	Q3,1		; DENOTES CHANNEL FOUND

OPN1:	MOVE	Q2,Q3		; COPY CHANNEL
	CAIL	Q2,20		; WITHIN RANGE OF CHANNEL #'S
	ERROR	12,15		; NO?

	DEVCHR	Q2,		; ASK MONITOR ABOUT CHANNEL
	SKIPE	Q2		; SKIP IF AVAILABLE
	AOJA	Q3,OPN1		; TRY NEXT

	LSH	Q3,^D23		; SHIFT NO. INTO AC POSITION
	MOVEM	Q3,IOCHAN(T4)	; REMEMBER CHANNEL

	IOR	Q3,[OPEN IOMODE(T4)]	; COMPLETE THE UUO
	XCT	Q3		; EXECUTE THE OPEN UUO
	JRST	0(QLINK)	; COULDN'T OPEN
	JRST	1(QLINK)	; COULD OPEN

	SUBTTL	Quaternary routines -- WMPROP

;	WMPROP - WORD MODE PROPERTIES
;
;	SETS PROPERTIES FOR CONVERSION TO TAKE PLACE. IS CALLED
;	BY CVTWD AND CVTDW. IT IS ASSUMED THAT FMT.WM BIT IS ON.
;

; CALLING SEQUENCE:
;
;	LOAD Z WITH A FORMAT
;	JSP	QLINK,WMPROP
;	RETURN - Z IS UNCHANGED

WMPROP:	CLEARM	BTLJFL##		; LEFT-JUSTIFY IS OFF
	TXNE	Z,FMT.LJ	;[246] UNLESS BIT IS SET
	SETOM	BTLJFL		; IN WHICH CASE WE SET IT ON

	HLRZ	Q2,Z		; COPY FORMAT INTO Q2 RIGHT
	ANDI	Q2,7		; LOW ORDER 3 BITS
	MOVE	Q3,BTTBL(Q2)	; GET BASE-SIZE WORD ...
	 ;			; FROM TABLE (SEE CVTFMT)
	HRRZM	Q3,BTSIZE##	; ESTABLISH SIZE OF BYTE
	HLRZM	Q3,BTBASE##	; BASE (LOWEST POSSIBLE CHAR)

	MOVEI	Q2,1		; COMPUTE ...
	LSH	Q2,0(Q3)	; 2 ** BTSIZE
	MOVEM	Q2,BTMAX##	; = MAXIMUM BYTE VALUE

	LSH	Q3,^D24		; MOVE SIZE INTO BITS 6-11
	IOR	Q3,[POINT 0-0,T2] ; COMPLETE THE POINTER
	MOVEM	Q3,BTPTR##	; (THE WORD IS EXPECTED IN T2)

	MOVEI	Q2,^D36		; COMPUTE LENGTH
	IDIV	Q2,BTSIZE	; OF STRING
	MOVEM	Q2,BTLENG##	; CORRESPONDING TO WORD.

	JRST	0(QLINK)	; RETURN


END	INIT
 `n