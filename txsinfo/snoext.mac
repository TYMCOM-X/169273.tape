	SUBTTL	D.R. HANSON/DRH/RCM/VB		16-JUN-76

; COPYRIGHT 1972,1973,1976,1977 STEVENS INSTITUTE OF TECHNOLOGY
; HOBOKEN, NEW JERSEY, 07030
; ALL RIGHTS RESERVED.


  ;DIRECTIVES

	.DIRECT	.XTABM		;EXCLUDE TABS FROM MACRO CALLS
	SALL			;SUPPRESS MACRO EXPANSIONS
	TWOSEG	400K		;TWO SEGMENTS
  ;UNIVERSALS

	SEARCH	JOBDAT		;JOBDATA ARE DEFINITIONS
	SEARCH	MACTEN		;DEC-10 COMMON MACROS
	SEARCH	SNOPRM		;SITBOL PARAMETER FILE

  ;VERSION NUMBER

	EXTVER==5		;MAJOR VERSION NUMBER
	EXTMIN==0		;MINOR VERSION NUMBER
	EXTEDT==302		;EDIT LEVEL
	EXTWHO==0		;CUSTOM ARGUMENT

  ;TITLE

	TITLE.	EXT,SNOEXT,<External Functions>

  ;SHOW UNIVERSAL VERSION NUMBERS

	SHOW.	%%JOBD		;JOBDAT'S  VERSION NUMBER
	SHOW.	%%MACT		;MACTEN'S VERSION NUMBER
	SHOW.	%%SNOP		;SNOPRM'S VERSION NUMBER

	EXTTTL			; Title
	EXTPTX			; PASS2 assembly message
	SUBTTL	Table of Contents

;		Table of Contents for External Functions
;
;
;			   Section			      Page
;   1. Table of Contents. . . . . . . . . . . . . . . . . . .    2
;   2. REVISION HISTORY . . . . . . . . . . . . . . . . . . .    3
;   3. Macro Definitions. . . . . . . . . . . . . . . . . . .    4
;   4. Data Definitions . . . . . . . . . . . . . . . . . . .    5
;   5. LOAD
;        5.1.   Load an external function . . . . . . . . . .    7
;        5.2.   Parse list of datatypes . . . . . . . . . . .    8
;        5.3.   Set up EFBLOK . . . . . . . . . . . . . . . .    9
;        5.4.   Fill the EFBLOK . . . . . . . . . . . . . . .   10
;        5.5.   Load the external function. . . . . . . . . .   11
;   6. EFCALL
;        6.1.   Call an external function . . . . . . . . . .   12
;        6.2.   Set up arguments for external function. . . .   13
;        6.3.   Enter an external function. . . . . . . . . .   14
;        6.4.   Return value from external function . . . . .   15
;   7. UNLOAD
;        7.1.   Unload an external function . . . . . . . . .   16
;   8. GETDT
;        8.1.   Determine datatype from string descriptor . .   17
;   9. LNKCOD
;        9.1.   Process Code Link Item Types. . . . . . . . .   18
;  10. LNKEND - Process End Link Item Types . . . . . . . . .   20
;  11. LNKENT - Process Entry Link Item Types . . . . . . . .   21
;  12. LNKERR - Process invalid Link Item Types . . . . . . .   22
;  13. LNKNAM - Process Name Link Item Types. . . . . . . . .   23
;  14. LNKSYM - Process Symbol Link Item Types. . . . . . . .   24
;  15. LNKJNK - Process ignorable block types . . . . . . . .   26
;  16. LNKHSG - PROCESS HIGH SEGMENT LINK ITEM-TYPE . . . . .   27
;  17. GETDAT - Routine to get data from a REL file . . . . .   28
;  18. CODBLK
;       18.1.   Allocate block for external function code . .   29
;  19. CVTR50
;       19.1.   Convert ASCII string to RADIX-50. . . . . . .   30
;  20. PNTSTR
;       20.1.   Point to ASCIZ string . . . . . . . . . . . .   32
;  21. ADJSTK
;       21.1.   Adjust the stack. . . . . . . . . . . . . . .   33
	SUBTTL	REVISION HISTORY

COMMENT	\

START OF VERSION 5.
124	ADD THIS MODULE.
126	REMOVE DMOVE & DMOVEM MACROS TO THE PARAMETER FILE.
130	Remove unused code.
134	Add PORTAL entry points for execute only.
137	FIX PROBLEM WITH CVTR50. Y.PER ==> Y.PCNT
147	Use Y.???? instead of X.???? and use the correct macros.
242	Don't use the POPR and PUSHR routines for just the P registers
243	Start improved REL file handling.
250	Fix typos in edit 243 and other errors
256	Fix some more errors in edit 243 & add more code.
302	Add a missing PORTAL where we return from an external function.
\
	SUBTTL	Macro Definitions


;	GET	LOC
;
;  This  Macro  produces code to read the next word from the file
;  whose I/O descriptor is at the top of  the  stack.   The  word
;  read is stored in location LOC.
;
	DEFINE	GET(LOC)  <
	DMOVE	T2,-1(STACK)		;; Pointer to stream block & fmt
	JSP	TLINK,READ##
	ERROR	12,4			;; Input error
	ERROR	12,6			;; EOF
	IFDIF	<Z><LOC>
      <	MOVEM	Z,LOC >	  >
	SUBTTL	Data Definitions
;		----------------

;
;  Define DEC datatypes (Fortran-10 conventions)
;
DANYDT==000000				; Unspecified
DIDT==	000100				; INTEGER
DRDT==	000200				; REAL
DSDT==	000740				; STRING
;
;  Symbol link item type constants (all shifted to RH of word)
;
GDEF==4B<5+^D18>			; Global definition bits
GSYM==6B<2+^D18>			; Global reference bits
LSYM==1B<2+^D18>			; Local symbol bits
;
;  Pure data
;
	RELOC	400000

	 ;  Dispatch table for Link item types

LNKDSP:	XWD	0,LNKERR		; Link item type 0
	XWD	0,LNKCOD		; Link item type 1
	XWD	0,LNKSYM		; Link item type 2
	XWD	0,LNKHSG		; Link item type 3
	XWD	0,LNKJNK		; Link item type 4
	XWD	0,LNKEND		; Link item type 5
	XWD	0,LNKNAM		; Link item type 6
	XWD	0,LNKERR		;[243] Item type 7
	XWD	0,LNKERR		;[243] Item type 10 - Local references
	XWD	0,LNKERR		;[243] Item type 11 - Polish
	XWD	0,LNKERR		;[243] Item type 12
	XWD	0,LNKERR		;[243] Item type 13
	XWD	0,LNKJNK		;[243] Item type 14 - Index

LSTLKD==<.-LNKDSP-1>			; Last entry in Link dispatch


	 ;  Dispatch table for SITBOL datatypes
	 ;   (INTEGER, STRING, REAL)

	 ;  Left half is corresponding DEC datatype
	 ;  Right half is pointer to return code

DTDSP:	XWD	DANYDT,A.RET		; For any other non-null string
	XWD	DIDT,I.RET		; For string INTEGER
	XWD	DRDT,R.RET		; For string REAL
	XWD	DSDT,ST.RET		; For string STRING
;
LSTDTD==<.-DTDSP-1>			; Last entry in datatype dispatch


	 ;  Dispatch table to convert datatypes of
	 ;  arguments to external functions

CDTDSP:	XWD	0,CVTINT##		; Convert to INTEGER
	XWD	0,CVTRL##		; Convert to REAL
	XWD	0,PNTSTR		; Point to ASCIZ string
;
LSTCDD==<.-CDTDSP-1>			; Last entry in convert datatype
					;  dispatch
	SUBTTL	LOAD -- Load an external function
;
;
;    Primary routine, unstable
;  
;  Control transfers  to  this  routine  to  execute  the  built-in
;  function LOAD.  The syntax of this function call is of the form
;  
;  	LOAD("F(dt1,dt2,...dtn)retdt","filename")
;  
;  where F is the external function name,  dt1  ...   dtn  are  the
;  datatypes  of  the  arguments  to  be  passed,  and retdt is the
;  datatype the function will return.  The second argument  to  the
;  LOAD function is the filename of the REL file to be loaded.
;  
;  This routine parses the prototype, sets up an EFBLOK, and  links
;  the  variable  block  for  the  function  to  this  EFBLOK.  The
;  function is then loaded into the GRID.   The  following  storage
;  areas are used:
;
;	P2		Points to the EFBLOK
;	FLAP, FLAP+1	Descriptor for filename
;	IGNOP		Address of variable block for function
;	IGNOP+1		Start of descriptor pairs in EFBLOK
;	IGNOP+2		PLINK save area
;

LOAD::	MOVEM	PLINK,IGNOP##+2		; Save return address
	POP	STACK,FLAP##+1		; Save descriptor for second
	POP	STACK,FLAP##		;  argument (filename)

	JSP	TLINK,STINIT##		; Initialize for parsing
					;  first argument
	 ERROR.	1,111			; Not a string?
	MOVE	S2,SYSCT##		; Use system character table
	MOVX	S3,Y.LP		; Break on "("
	JSP	TLINK,STRM##
	 ERROR	13,71			; What! No "(" ?
	JSP	TLINK,INSTAL##		; Enter function name in symbol
					;  table
	 ERROR	13,73			; Funny function name?
	MOVEM	Z,IGNOP##		; Save address of symbol table
					;  entry for function
	SUBTTL	LOAD -- Parse list of datatypes


	 ;  Now pick up argument datatypes.
	 ;  The number of arguments for this function is the
	 ;  number of commas + 1.

	MOVEI	P1,1			; Initialize argument counter
	MOVX	S3,Y.CM!Y.RP	; BREAK(",)")
PARS.1:	IBP	STPTR##			; Hop over last
	SOS	CURSOR##		;  break character.
	JSP	TLINK,STRM		; String descriptor for this
					;  arg is pushed on stack.
	 ERROR	13,73
	CAIN	Q2,","			; If break char a comma,
	 AOJA	P1,PARS.1		;  bump counter and get next arg

	 ;  Terminating ")" has been found.  String descriptors for
	 ;  datatype of each argument have been pushed on the stack.

	 ;  Pickup the return datatype.

	IBP	STPTR			; Hop over ")"
	SOS	CURSOR
	JSP	TLINK,STRM		; Break on ")" will fail
	JFCL				;  REM is pushed on stack
	SUBTTL	LOAD -- Set up EFBLOK


	 ;  Allocate space for EFBLOK at GSURF in the GRID.
	 ;  Need EFARGS + 3 * (num of args) words.
	 ;  P1 contains the number of arguments.

	MOVE	Z,P1			; Get number of args
	IMULI	Z,3			; 3 * (number of args)
	ADDI	Z,EFARGS		;  + EFARGS

	JSP	QLINK,GCOLG##		; Expand GRID
	MOVE	P2,GSURF##		; Pointer to block
	ADDM	Z,GSURF##		; Update GSURF
	 ;	
	HRLI	Z,EFBLOK		; Get BTCODE
	MOVEM	Z,BTCODE&BLENG(P2)	; Set up block header
	CLEARM	GCFLD(P2)
	SUBTTL	LOAD -- Fill the EFBLOK


	 ;  Now begin filling in EFBLOK

	HRLI	S1,FEAFL!FAAFL		; The FFLAGS
	HRR	S1,P1			;  and the FNARGS
	MOVEM	S1,FFLAGS&FNARGS(P2)

	MOVE	S1,[ JSP  P2,EFCAL$## ]
	MOVEM	S1,FCODE(P2)		; The FCODE field
	CLEARM	EFFUNC(P2)		; Entry point pointer

	MOVN	S1,P1			; Negative of number of args
	HRLZM	S1,EFARGS-1(P2)		;  preceeds list of arguments

	 ;  At the top of the stack is a string descriptor for the
	 ;  return datatype.  

	JSP	SLINK,GETDT		; What is return datatype?
	MOVEM	S3,EFRET(P2)		; Plug EFRET field

	 ;  String descriptors giving the datatypes of the arguments
	 ;  are next on the stack. (In reverse order of course)

	 ;  For each of these, plug the argument list entry
	 ;  and the DT field of the corresponding descriptor.

	MOVEI	Z,EFARGS(P2)		; Set up pointer to first
	ADD	Z,P1			;  descriptor pair in EFBLOK
	MOVEM	Z,IGNOP+1		; Save it

FILL.1:	SOJL	P1,FILL.2		; Quit when all args done
	JSP	SLINK,GETDT		; What is datatype of this arg?
	HRRZ	Q1,P2			; Pointer to EFBLOK
	ADD	Q1,P1			;  + offset for this arg
	MOVE	Q2,P1			; Get offset to descriptor
	LSH	Q2,1			;  for this argument
	ADD	Q2,IGNOP+1		; Add offset to pointer
	HRLZM	S2,(Q2)			; Plug DT field of descriptor
	CAME	S2,[DANYDT,,0]		; For REAL, STRING, and INTEGER
	AOS	Q2			;  point to 2nd descriptor word
	HRR	S2,Q2			; Insert pointer
	MOVEM	S2,EFARGS(Q1)		; Plug entry in argument list
	 JRST	FILL.1			; Do remaining arguments


	 ;  Point the VFUNC field in the variable block for
	 ;  this function to this EFBLOK.

FILL.2:	MOVE	Q2,IGNOP		; Retrieve address of variable
					;  block
	MOVEI	Q2,VFUNC(Q2)		; Add offset to VFUNC field
	HRRZ	Q3,P2			; Pointer to EFBLOK
	JSP	QLINK,IFBLOK##
	SUBTTL	LOAD -- Load the external function


	 ;  Retrieve descriptor for second argument to LOAD
	 ;  function (file name).  Set up a stream block for
	 ;  this file and process the Link Item types in the
	 ;  file.

	MOVE	Z,FLAP			; From where we stashed it
	CAME	Z,NULLST##		; If this is the null string
	 JRST	NOTNUL
	MOVE	S1,SYSTR##		;  use the default external
	MOVE	S2,S.XLIB##		;   library file name
	DMOVEM	S1,FLAP

NOTNUL:	PUSH	STACK,FLAP		; String descriptor to stack
	PUSH	STACK,FLAP+1
	CLEAR	Z,			; No file checking
	JSP	SLINK,CVTSIO##		; Convert to stream
	 ERROR	12,13			; Bad file spec
	 ERROR	15			; Can't ever happen

	MOVX	Z,FMT.WM	;[246] Plug "word mode" format
	MOVEM	Z,(STACK)		;  into I/O descriptor

	CLEARM	ECBPTR##			; External code block pointer

READ.0:	GET	Z,			; Read a word (Should be a
					;  LINK-10 Item type header)
	HLRZ	P1,Z			; So put item type in P1
	HRRZ	Z,Z			;  and num of data words in Z
	JUMPG	Z,READ.1		; YES THERE ARE DATA WORDS

	 GET	Z,			; No data words, get rel bits
	 JRST	READ.0			;  and go for something more
					;   interesting.

READ.1:	MOVEM	Z,LCOUNT##		;[256] STORE THE COUNT
	SETOM	LCOUNT+1		;[256] INITIALIZE THE WORD

	CAIL	P1,0			; Do we know what to do with
	CAILE	P1,LSTLKD		;  this item type?
	 ERROR	12,32			; If not, give error.

	 ;  Call routine to process this link item type

	JSP	SLINK,@LNKDSP(P1)
	 ERROR	12,32			; Couldn't do it?
	 JRST	READ.0			; Successful return
					;  Go for next item type

	 ;  Successful return from END item type

	MOVE	T2,-1(STACK)		; Pointer to stream block
	MOVE	T4,STCF(T2)		; Current file offset
	ADDI	T4,0(T2)		; Point to pointer
	MOVE	T4,0(T4)		; Load I/O block pointer
	JSP	QLINK,CLS##		; Close the file

	MOVE	PLINK,IGNOP+2		; Restore PLINK
	 JRST	RETNL1##		; POP I/O descriptor and
					;  return the null string
	SUBTTL	EFCALL -- Call an external function
;
;    Primary routine, unstable
;
;  This routine transfers control to an external function.   It  is
;  invoked via the FCODE field in the EFBLOK for the function.  The
;  arguments to be passed are converted  to  the  proper  type,  if
;  required,  and  then  loaded into the EFBLOK.  Before control is
;  passed to the external function, all registers containing active
;  data are saved.  A flag (EF.FLG) is also set to indicate that an
;  external function is being executed.  An error condition such as
;  stack  overflow  could  prematurely return control to the SITBOL
;  system  with  the  registers  in  an  unpredictable  state.   By
;  checking  this  flag,  SITBOL can determine if the registers are
;  valid and recover.
;
;  The standard Fortran-10 calling conventions are used for passing
;  parameters and linking to the function.  AC  16  points  to  the
;  argument list in the EFBLOK.  A
;
;	PUSHJ  17,entry
;  
;  transfers to the function entry point.
;  
;  When  the  function  returns,  the  previous state of the SITBOL
;  system is restored.  On normal return from an external function,
;  the  value returned is converted to the proper SITBOL descriptor
;  and pushed onto the system stack.
;

EFCALL::MOVEI	P2,-FCODE-1(P2)		; Get pointer to EFBLOK
	HRRZ	S1,FNARGS(P2)		; Get formal num of parameters
	SUB	Z,S1			; Compare with actual num passed
	CAIE	Z,0			; If necessary,
	JSP	QLINK,ADJSTK		;  adjust stack
	SUBTTL	EFCALL -- Set up arguments for external function


	 ;  Get offset to last descriptor pair in EFBLOK
	 ;    EFARGS + 3 * (num args) - 2

	MOVE	S2,S1
	IMULI	S2,3
	ADDI	S2,EFARGS-2(P2)		; S2 points to descriptor
	HRRZ	S3,STACK		; S3 points to top of stack

	 ;  All arguments with declared datatype of 'STRING' are
	 ;  converted to ASCIZ strings.  This must be done first
	 ;  since no garbage collections can occur after a descriptor
	 ;  is moved from the stack to the EFBLOK.

CVTST0:	HLRZ	T1,(S2)			; Get datatype
	CAIE	T1,SDT			; String?
	 JRST	CVTST1			; No.  On to the next.
	PUSH	STACK,-1(S3)		; This argument to
	PUSH	STACK,0(S3)		;  top of stack
	JSP	TLINK,CVTSTZ		; Convert to ASCIZ string
	 ERROR.	1,112			; Won't convert
	POP	STACK,0(S3)		; Replace original argument
	POP	STACK,-1(S3)		;  with new string
CVTST1:	SUBI	S2,2			; Adjust
	SUBI	S3,2			;  pointers
	 SOJG	S1,CVTST0		; Check all arguments

	 ;  Convert arguments and load into EFBLOK

	HRRZ	S1,FNARGS(P2)		; Get num of parameters
	MOVE	S2,S1
	IMULI	S2,3			; S2 points to last descriptor
	ADDI	S2,EFARGS-2(P2)		;  pair in EFBLOK

CVTARG:	HLRZ	T1,(S2)			; Get datatype
	ANDI	T1,DTNUM		; Get datatype number
	SOSL	T1
	CAILE	T1,LSTCDD		; In dispatch table?
	 JRST	CVTAR1			; No conversion for this DT
	JSP	TLINK,@CDTDSP(T1)	; Convert to proper DT
	 ERROR.	1,112			; Can't convert that one

	 ;  Descriptor at top of stack is plugged into
	 ;  EFBLOK

CVTAR1:	POP	STACK,1(S2)
	POP	STACK,0(S2)
	SUBI	S2,2			; Point to next descriptor
	 SOJG	S1,CVTARG		; Do all args
	SUBTTL	EFCALL -- Enter an external function


	 ; Batten down the hatches and enter function

	$PUSH	<P1,P2>		;[242] Save the prinmary registers
	MOVEM	HSR,IGNOP		; History stack
	MOVEM	STACK,IGNOP+1		; "THE" stack
	MOVEM	SURF,IGNOP+2		; The SURF
	MOVEM	SURF,.JBFF		; Point JBFF to free area

	MOVE	17,STACK		; Use system stack
	MOVEI	16,EFARGS(P2)		; Reg 16 points to arg list
	SETOM	EF.FLG##		; Set flag to indicate ext func
	AOS	$FNCLV##		; Bump &FNCLEVEL
	PUSHJ	17,@EFFUNC(P2)		; Enter function
	PORTAL	.+1			;[302] EXO entry

	SKIPA	S1,S.RET##		; Successful return
	MOVEI	S1,S.FRET##		; Failure return
	MOVEM	S1,$RTNTP##		; Set &RETURN
	DMOVEM	Z,S1			; Save returned values

	 ;  Restore the world as we remembered it

	MOVE	HSR,IGNOP
	MOVE	STACK,IGNOP+1
	MOVE	SURF,IGNOP+2
	$POP	<P2,P1>		;[242] Restore the primary registers
	CLEARM	EF.FLG			; Everything is back to normal
	SOS	$FNCLV			; Back one level

	MOVE	Z,$RTNTP
	CAIN	Z,S.FRET		; If function failed,
	JRST	0(PLINK)		;  return failure

	JRST	@EFRET(P2)		; Return proper datatype
	SUBTTL	EFCALL -- Return value from external function


	 ;  Return STRING

ST.RET:	MOVE	S3,[POINT 7,0(S1)]	; Set up byte pointer
	CLEAR	Z,			; Counter
ST.R1:	ILDB	T1,S3			; Count up characters
	CAIE	T1,0			; Zero byte?
	 AOJA	Z,ST.R1			; Try again

	JUMPE	Z,RETNUL##		; No chars?  Return null string
	MOVE	T1,Z			; Keep count of characters

	JSP	QLINK,ASBLOK		; Get a string block
	 ERROR	11,13			; Must be a biggie!
	HRRM	T1,0(STACK)		; Add length

	IDIVI	T1,5
	SKIPE	T2
	ADDI	T1,1			; T1 is num of words for string

	HRRZ	T2,-1(STACK)		; Pointer to SBLOK
	ADDI	T2,LHDR			; Point to string in SBLOK
	ADDI	T1,-1(T2)		; Point to last word in block
	HRL	T2,S1			; Beginning of string

	BLT	T2,0(T1)		; Move string
	JRST	1(PLINK)		; Return


	 ;  Return null string

	N.RET==	RETNUL##	;[250] Return the null string


	 ;  Return INTEGER

I.RET:	MOVSI	S2,IDT
	 JRST	EF.RET			; Join common code


	 ;  Return REAL

R.RET:	MOVSI	S2,RDT
	 JRST	EF.RET			; Join common code


	 ;  Return descriptor

A.RET:	EXCH	S1,S2

EF.RET:	PUSH	STACK,S2		; First word of descriptor pair
	PUSH	STACK,S1		; Second word
	JRST	1(PLINK)		; Success
	SUBTTL	UNLOAD -- Unload an external function
;
;
;    Primary routine, unstable
;
;  Control transfers to this routine  when  the  built-in  function
;  UNLOAD  is  executed.   The  UNLOAD function takes 1 argument, a
;  string giving the name of the function  to  be  unloaded.   This
;  routine  removes  the  link  between  the variable block and the
;  function block.  No storage is recovered.
;

UNLD::	JSP	TLINK,CVTNAM##		; Get name
	 ERROR.	1,113			; Can't?
	HRRZ	Q2,-1(STACK)		; Pointer to var block
	ADDI	Q2,VFUNC		; Pointer to VFUNC field
	MOVEI	Q3,UNDEF$##		; "New" function
	JSP	QLINK,IFBLOK

	JRST	RETNUL			; Return null string
	SUBTTL	GETDT--Determine datatype from string descriptor
;		-----  ------------------------------------
;
;    Secondary routine, unstable
;
;  This  routine  takes  a string descriptor and returns the SITBOL
;  datatype  associated  with  this  string,   the   DEC   datatype
;  associated  with this string (F10 conventions), and a pointer to
;  the code for processing this  datatype  when  returned  from  an
;  external function.
;
;	String		SITBOL DT	DEC DT		Return code
;
;	any others	0		DANYDT		A.RET
;	'INTEGER'	IDT (1)		DIDT		I.RET
;	'REAL'		RDT (2)		DRDT		R.RET
;	'STRING'	SDT (3)		DSDT		ST.RET
;	null string	0		DANYDT		N.RET
;
;    Calling Sequence:
;    ----------------
;
;	PUSH string descriptor on stack
;	JSP	SLINK,GETDT
;	normal return	
;
;    The stack is POPed in all cases.  On normal return
;    S2(right) contains SITBOL datatype
;    S2(left) contains DEC datatype
;    S3 contains return code
;

GETDT:	JSP	TLINK,INSTAL##		; Find variable block for
					;  this string.
	 JRST	GET.NL			; Must be null string
	MOVE	S2,Z			; Get address of var block
	HLRZ	S2,VBDT(S2)		; Get datatype field
	MOVE	T2,S2
	ANDI	T2,DTNUM		; Get datatype number
	CAILE	T2,LSTDTD		; In dispatch table?
	CLEARB	T2,S2			; No, then use first entry

	HLL	S2,DTDSP(T2)		; Get DEC datatype
	HRRZ	S3,DTDSP(T2)		;  and pointer to return code
	JRST	0(SLINK)		; Return


	 ;  For null string

GET.NL:	HRLZI	S2,DANYDT
	MOVEI	S3,N.RET
	JRST	0(SLINK)		; Return
	SUBTTL	LNKCOD -- Process Code Link Item Types
;
;    Secondary routine, unstable
;
;  This routine relocates the data items as required and loads  the
;  code  into  the GRID.  CODBLK is called to create and expand the
;  JKBLOK containing  the  code.   Absolute  or  symbolic  starting
;  addresses for code items are not allowed.
;
;    Calling Sequence:
;    ----------------
;
;	IGNOP points to variable block for function being loaded
;	PUSH I/O stream descriptor on stack
;	Load Z with count of data words
;	JSP	SLINK,LNKCOD
;	error return
;	normal return
;

LNKCOD:	GET	S2		;[243] Get relocation bits
	GET	S3		; Get starting address

	TLNE	S3,400000
	 ERROR	12,36		; Can't do symbolic start address
	TLNN	S2,200000
	 ERROR	12,37		; Can't do absolute start address
	LSH	S2,2		; Done with these relocation bits
	CAIL	S3,400000
	 ERROR	12,38		; Can't do high seg code

	ADD	Z,LCOUNT##	;[250] Starting address + number of
				;  words to load
	SUBI	Z,1		; Don't count data word
				;  containing starting address
	MOVE	T2,ECBPTR	; Get pointer to ext code block
	JSP	TLINK,CODBLK	; Expand block for this code
	MOVEM	T2,ECBPTR	; Save pointer

	ADDI	S3,LHDR(T2)	; St address + block address
	MOVEM	S3,LOCNTR##	;  + block header offset
				;   = location counter.
	MOVEI	S3,1		; Initialize counter to process
				;  data words.

COD.1:	GET	Z,			; Get data word
	MOVEI	Q2,LHDR			; Block header offset
	ADD	Q2,ECBPTR		;  + address of ext code block
					;   = RH relocation factor
	HRLZ	Q1,Q2			; Q1 is LH relocation factor

	TLNE	S2,400000		; Ck LH relocation bit
	ADD	Z,Q1			; Relocate LH
	TLNE	S2,200000		; Ck RH relocation bit
	ADD	Z,Q2			; Relocate RH
	LSH	S2,2			; 2 less bits to worry about

	MOVEM	Z,@LOCNTR		; Put in code block
	AOS	LOCNTR			; Bump location counter
	ADDI	S3,1			;  and loop counter
	CAML	S3,LCOUNT##	; When done
	JRST	1(SLINK)		;  return 

	MOVE	T1,S3
	IDIVI	T1,^D18			; Every 18 data words
	  JUMPN	T2,COD.1		;  get a new set of rel bits
	GET	S2
	  JRST	COD.1			;   and then continue loop.
	SUBTTL	LNKEND - Process End Link Item Types
;
;    Secondary routine, stable
;
;  This routine expands the code block as  required  by  the  first
;  data word.  It also checks for references to absolute addresses,
;  and verifies that an entry point has been extablished.
;
;    Calling Sequence:
;    ----------------
;
;	IGNOP points to variable block for function being loaded
;	PUSH I/O stream descriptor on stack
;	Load Z with count of data words
;	JSP	SLINK,LNKEND
;	error return
;	unused return (normal return for other LNK--- routines)
;	normal return (for LNKEND only)
;

LNKEND:	JSP	TLINK,GETDAT	;[243] Get a data word
	 ERROR	15		;[243]
	MOVE	S2,Z		;[243] Copy the data word
	JSP	TLINK,GETDAT	;[243] Get the next data word
	 ERROR	15		;[243]
	EXCH	Z,S2		;[250] Num of locations required by
				;  the subprogram just loaded
	MOVE	T2,ECBPTR		; Expand code block
	JSP	TLINK,CODBLK		;  if necessary

	CAIE	S2,0		;[243]
	 ERROR	12,34			; Absolute address in program

	HRRZ	T2,EFFUNC(P2)		; External function entry point
	CAIN	T2,0
	 ERROR	12,35			; Entry point never established

	JRST	2(SLINK)		; Normal return
	SUBTTL	LNKENT - Process Entry Link Item Types
;
;    Secondary routine, stable
;
;  All data items in this item type are ignored.
;
;    Calling Sequence:
;    ----------------
;
;	PUSH I/O stream descriptor on stack
;	Load Z with count of data words
;	JSP	SLINK,LNKENT
;	error return
;	normal return
;


LNKENT:	ERROR	12,32		;[256] THIS IS TEMPORARY.
;LNKENT:	MOVE	S2,LCOUNT	;[250] Get the address of the var block
	PUSH	STACK,VNAME(S2)	;[250] Get the descriptor
	PUSH	STACK,VNAME+1(S2) ;[250] On the stack
	JSP	TLINK,CVTR50	;[250] Convert to a radix 50 symbol
	MOVEM	Z,IGNOS##+1	;[250] Store it
	SUBTTL	LNKERR - Process invalid Link Item Types
;
;    Secondary routine, stable
;
;  This  routine  is invoked when a link item type not supported by
;  the external  function  loader  is  encountered.   This  routine
;  always fails.
;
;    Calling Sequence:
;    ----------------
;
;	PUSH I/O stream descriptor on stack
;	Load Z with count of data words
;	JSP	SLINK,LNKERR
;	error return
;	normal return
;

LNKERR:	ERROR	12,32			; Fail in style
	SUBTTL	LNKNAM - Process Name Link Item Types
;
;    Secondary routine, stable
;
;  All data items in this link item type are ignored.
;
;    Calling Sequence:
;    ----------------
;
;	PUSH I/O stream descriptor on stack
;	Load Z with count of data words
;	JSP	SLINK,LNKNAM
;	error return
;	normal return
;

	NM$CPU==770000,,0	;[243] MASK FOR THE CPU TYPE IN THE BLOCK
	NM$TRA==007777,,0	;[243] MASK FOR THE TRANSLATOR NUMBER


LNKNAM:	JSP	TLINK,GETDAT	;[243] Get a data word
	 JRST	1(SLINK)	;[243] SHORT BLOCK ?
	JSP	TLINK,GETDAT	;[243] Get the info
	 ERROR	15		;[256] ON WELL.
	TRNE	Z,-1		;[243] Require a common area ?
	 error	12,40		;[243] yes - Error

	ldb	s2,[pointr (Z,nm$cpu)] ;[243] get the cpu type
	JUMPE	S2,LNAM.0	;[243] ZERO - ALL IS OK
	CAME	S2,CPUTYP##	;[243] Same cpu ?
	  ERROR	12,41		;[243] Different CPUs, it will not run


LNAM.0:	LDB	S2,[POINTR (Z,NM$TRA)] ;[243] Get the translator that produced this
	CAILE	S2,TRALEN	;[243] In the table ?
	 ERROR	12,42		;[243] No - Unknown translator
	XCT	TRADSP(S2)	;[243] Issue error or do nothing
	 JRST	1(SLINK)	;[243] All is ok


;[243] the following is the translator table
;[243] An entry in this table either validates a translator or
;[243] declairs it illegal

tradsp:	jfcl			;[243] 0 - unknown
	ERROR	12,43		;[243] 1 - F40 (HAHA)
	ERROR	12,44		;[243] 1 - COBOL
	ERROR	12,45		;[243] 3 - ALGOL
	ERROR	12,46		;[243] 4 - NELIAC
	ERROR	12,47		;[243] 5 - PL/1
	JFCL			;[243] 6 - BLISS-10
	ERROR	12,48		;[243] 7 - SAIL
	JFCL			;[243] 10 - FORTRAN-10
	JFCL			;[243] 11 - MACRO
	JFCL			;[256] 12 - FAIL
	ERROR	12,49		;[256] 13 - BCPL
	JFCL			;[256] 14 - MIDAS
	ERROR	12,50		;[256] 15 - SIMULA
	ERROR	12,51		;[256] 16 - CBL74
	ERROR	12,52		;[256] 17 - CBL79
	JFCL			;[256] 20 - BLS36
	ERROR	12,53		;[256] 21 - BASIC (PLUS)
TRALEN==.-TRADSP		;[250] LENGTH OF THE TABLE
	SUBTTL	LNKSYM - Process Symbol Link Item Types
;		------  ------------------------------
;
;    Secondary routine, stable
;
;  Only global references and definitions are of interest.   Global
;  references  cannot  be satisfied and result in an error message.
;  Global definitions are checked to establish the entry  point  of
;  the external function.
;
;    Calling Sequence:
;    ----------------
;
;	IGNOP points to variable block for function being loaded
;	PUSH I/O stream descriptor on stack
;	Load Z with count of data words
;	JSP	SLINK,LNKSYM
;	error return
;	normal return
;

LNKSYM:	MOVE	S2,IGNOP		;[243] Get address of variable block
	PUSH	STACK,VNAME(S2)		;[243] String descriptor to
	PUSH	STACK,VNAME+1(S2)	;[243]  stack

	JSP	TLINK,CVTR50		; Convert function name to
					;  RADIX-50
	TLO	Z,GDEF			; Set global definition bits
	MOVEM	Z,IGNOS+1		; Save function name
	CLEAR	S3,			; Initialize counter

SYM.1:	GET	Z,			; Get relocation bits

SYM.2:	GET	S2			; Get first data word
	GET	Z,			; Get second data word

	 ;  If this is a global symbol reference, the external
	 ;  function loader can't properly handle it.

	TLNE	S2,GSYM			; Any global bits set
	TLNE	S2,LSYM			; OK if local bit set too
	TRNA				; SKIP quickly
	 ERROR	12,33			; Can't do global refs

	 ;  Is this symbol the entry point we are looking for?

	CAME	S2,IGNOS+1
	 JRST	SYM.3		; No, On to the next entry.

	 ;  This is the entry point for the function.  Plug
	 ;  the EFFUNC field in the EFBLOK for this function.

	MOVE	T1,ECBPTR		; Relocate entry point to
	ADDI	Z,LHDR(T1)		;  external code block
	HRRM	Z,EFFUNC(P2)		; Plug EFBLOK

SYM.3:	ADDI	S3,2			; Increment counter
	CAML	S3,LCOUNT	;[250] When all data words done
	JRST	1(SLINK)		;  normal return

	MOVE	Q1,S3
	IDIVI	Q1,^D18			; Every 18 words,
	 JUMPN	Q2,SYM.2
	 JRST	SYM.1			;  do something with those
					;   overhead words of rel bits
	SUBTTL	LNKJNK - Process ignorable block types

; the following routine will process all block types that can be safely
; ignored.  These block types include: 0, and 14.
;
; usage:
;
;	ignos - containing the number of data words
;
;	jsp	slink,lnkjnk
;	(error return)		; not used
;	(Normal Return)


LNKJNK:	JSP	TLINK,GETDAT	;[243] Get the data word
	 JRST	1(SLINK)	;[243] Return
	JRST	.-2		;[243] Loop for all words
	SUBTTL	LNKHSG - PROCESS HIGH SEGMENT LINK ITEM-TYPE

	; THE FOLLOWING ROUTINE WILL PROCESS THE LINK HIGH SEG
	; ITEM TYPE FOR THE SPECIAL CASE OF SOME F10 PROGRAMS
	; WHICH WILL NOT REFERENCE THE HIGH SEGMENT (I.E. NO FORTRAN-10 I/O).
	;
	; USAGE:
	;
	;	LCOUNT - CONTAINING THE COUNT OF DATA WORDS
	;
	;	JSP	SLINK,LNKHSG
	;	 (ERROR RETURN)
	;	 (NORMAL RETURN)


LNKHSG:	ERROR	12,32		;[256] TEMPORARY.
;LNKHSG:	JSP	TLINK,GETDAT	;[256] GET DATA
	 ERROR	15		;[256] CAN'T HAPPEN
	MOVE	Z,LCOUNT	;[256]	GET COUNT OF WORDS
	CAIE	Z,2		;[256] ARE THERE TWO WORDS?
	 ERROR	12,38		;[256] NOT FROM F10
	JSP	TLINK,GETDAT	;[256]	GET THE NEXT WORDS
				;[256] AND IGNOR EVERYTHING.
	 ERROR	15		;[256] WOOPS.
	JRST	1(SLINK)	;[256] RETURN TO CALLER.
	SUBTTL	GETDAT - Routine to get data from a REL file

; The following routine will return the next data word from the REL file
; all relocations words will be ignored.


GETDAT:	AOS	T2,LCOUNT+1	;[243] Increment the count
	CAMLE	T2,LCOUNT##	;[243] Finished ?
	 JRST	GETD.1		;[243] Yes - Clean up and return
	IDIVI	T2,^D18		;[243] Relocation word next ?
	DMOVEM	TLINK,TSAVE##	;[256] SAVE TLINK AND T2
	JUMPN	T3,GETD.0	;[243] No - Ignore it
	GET	Z		;[243] Get the relocation word

GETD.0:	GET	Z		;[243] Get the data word
	DMOVE	TLINK,TSAVE	;[256] GET BACK TLINK AND T2
	JRST	1(TLINK)	;[243] Give a good return

; Here at the end of the data block


GETD.1:	SETOM	LCOUNT+1		;[243] Reinitialize the count
	JRST	(TLINK)		;[243] Return to the caller
	SUBTTL	CODBLK--Allocate block for external function code
;		------  -----------------------------------------
;
;    Tertiary routine,  unstable
;
;  This routine allocates a JKBLOK  into  which  the  code  for  an
;  external  function will be loaded.  The block begins at GSURF in
;  the GRID.  The first call to this routine creates the block  and
;  subsequent  calls  expand  it as needed.  (It is assumed that no
;  other storage allocation in the GRID  will  take  place  between
;  calls.)
;
;    Calling Sequence:
;    ----------------
;
;	T2 points to the start of the block (0 on first call)
;	Z is the number of words needed (not counting header)
;	JSP	TLINK,CODBLK
;	normal return
;
;    On return T2 points to the start of the block
;

CODBLK:	JUMPN	T2,CODB.1		; On first call
	MOVE	T2,GSURF		;  set pointer to block
	 JRST	CODB.2
;
CODB.1:	HRRZ	T3,BLENG(T2)		; Get current length
	SUB	Z,T3			; How much do we need?

CODB.2:	ADDI	Z,LHDR			; Don't forget header words
	JUMPLE	Z,(TLINK)		; Normal return if we already
					;  have it.
					; Otherwise,
	JSP	QLINK,GCOLG##		; Expand GRID

	MOVE	Q1,GSURF		; Points to beginning of new ext
	ADDB	Z,GSURF			; Update GSURF

	CLEARM	(Q1)			; Clear first word

	HRL	Q1,Q1
	ADDI	Q1,1
	MOVE	Q2,GSURF
	BLT	Q1,-1(Q2)		; Get them others

	SUB	Z,T2			; New length of block
	HRLI	Z,JKBLOK		; Plug datatype field
	MOVEM	Z,BTCODE&BLENG(T2)	; Insert block header
	CLEARM	GCFLD(T2)		;  and a little something for GC
	JRST	(TLINK)			; And another successful return
	SUBTTL	CVTR50--Convert ASCII string to RADIX-50
;		------  --------------------------------
;
;    Tertiary routine, stable
;
;  This  routine  produces  a  RADIX-50  string   from   a   string
;  descriptor.  If the string is longer that 6 characters, only the
;  first 6 characters are used.
;
;    Calling Sequence:
;    ----------------
;
;	PUSH string descriptor on stack
;	JSP	TLINK,CVTR50
;	normal return
;
;    On return Z contains the RADIX-50 string
;

CVTR50:	HRRZ	T2,(STACK)		; Get length of string
	JSP	QLINK,CVTPTR##		; Get byte pointer
	 0				; Zero
	 ERROR	15			; "Can't ever happen," he said

	MOVEM	Z,T4			; Save byte pointer
	CAILE	T2,6			; If length > 6
	MOVEI	T2,6			;  only look at first 6 chars
	CLEAR	Z,			; Z will contain RADIX-50 string

CVTR.1:	ILDB	T3,T4			; ASCII char to T3
	MOVE	Q1,SYSCTB##(T3)		; Char set bits for this char

	TDNN	Q1,X.R50##		; Valid RADIX-50 char?
	CLEAR	T3,			; No, use null

	TXNE	Q1,Y.UPS	; A-Z ?
	SUBI	T3,"A"-13

	TXNE	Q1,Y.LOWS	; a-z ?
	SUBI	T3,"a"-13

	TXNE	Q1,Y.NUM	; 0-9 ?
	SUBI	T3,"0"-1

	TXNE	Q1,Y.PER	; A period?
	MOVEI	T3,45

	TXNE	Q1,Y.DOLR!Y.PCNT	; A dollar or % symbol?
	MOVEI	T3,46-"$"(T3)

	IMULI	Z,50			; Shift string
	ADD	Z,T3			; Tack on this character
	 SOJG	T2,CVTR.1		; Do entire string

	JRST	0(TLINK)		; Then a normal return
;    	SUBTTL	CVTSTZ--Convert to ASCIZ string
;		------  -----------------------
;
;    Tertiary routine, unstable
;
;  This routine converts a SITBOL datatype to an ASCIZ string.  Any
;  datatype  that  can  be  converted  to  STRING  datatype  can be
;  converted to an ASCIZ string.
;
;    Calling Sequence:
;    ----------------
;
;	PUSH descriptor on stack
;	JSP	TLINK,CVTSTZ
;	error return
;	normal return
;
;    On  normal  return  a  string descriptor pointing to the SBLOK
;    containing an ASCIZ string will be on the top  of  the  stack.
;    The old value is only removed from the stack if the conversion
;    is successful.
;

CVTSTZ:	DMOVEM	TLINK,TSAVE##		; Save link and T2

	JSP	TLINK,CVTSTR##		; Convert to string
	JRST	@TSAVE			; Won't convert
	DMOVE	TLINK,TSAVE
	HRRZ	T3,0(STACK)		; Length of string
	JUMPE	T3,1(TLINK)		; If null, we are done

	JSP	QLINK,CVTPTR		; Byte pointer for string
	Z				; Don't need base register
	 ERROR	15			; It WILL be non-null string
	MOVE	T2,Z			; Save byte pointer
	MOVEI	Z,1(T3)			; Num chars for ASCIZ string

	JSP	QLINK,ASBLOK##		; Get block for ASCIZ string
	 ERROR	11,13			; String too long
	HRRZ	T4,-1(STACK)		; Point to block
	MOVEI	Z,1(T3)			; Num chars for ASCIZ string
	HRRZM	Z,0(STACK)		; Plug length
	MOVE	Q1,[POINT 7,LHDR(T4)]	; Byte pointer for new string

CVTMOV:	ILDB	Q2,T2			; Move characters
	IDPB	Q2,Q1			;  to new block
	 SOJG	T3,CVTMOV

	CLEAR	Z,
	IDPB	Z,Q1			; Add zero byte
	JRST	1(TLINK)		; Return
	SUBTTL	PNTSTR--Point to ASCIZ string
;		------  ---------------------
;
;    Tertiary routine, stable
;
;  This  routine sets the pointer in an argument list for passing a
;  STRING datatype.
;
;    Calling Sequence:
;    ----------------
;
;	Load P2 with pointer to EFBLOK containing argument list
;	Load S1 with the argument number
;	PUSH the descriptor pointing to ASCIZ SBLOK on stack
;	JSP	TLINK,PNTSTR
;	normal return
;
;    On  return, the entry in the argument list will point directly
;    to the ASCIZ string.
;

PNTSTR:	HRRZ	T2,-1(STACK)		; Pointer to SBLOK with ASCIZ
					;  string
	JUMPE	T2,1(TLINK)		; Return if null
	ADDI	T2,LHDR			; Point directly to ASCIZ string

	MOVE	T3,P2			; Point to EFBLOK
	ADDI	T3,EFARGS-1(S1)		; Point to argument in list
	HRRM	T2,(T3)			; Set pointer to string
	JRST	1(TLINK)		; All done
	SUBTTL	ADJSTK--Adjust the stack
;		------  ----------------
;
;    Quaternary routine, stable
;
;  This routine adjusts the stack by Z descriptors.
;    If Z > 0, POP Z descriptors off stack.
;    If Z < 0, PUSH -Z null string descriptors on stack.
;
;    Calling Sequence:
;    ----------------
;
;	Load Z with adjustment count
;	JSP	QLINK,ADJSTK
;	normal return
;

ADJSTK:	JUMPE	Z,0(QLINK)		; Return
	JUMPL	Z,ADJ.2


	 ;  POP stack

ADJ.1:	SUB	STACK,XWD22##
	 SOJN	Z,ADJ.1
	JRST	0(QLINK)		; Return


	 ;  PUSH null string on stack

ADJ.2:	PUSH	STACK,NULLST
	PUSH	STACK,NULLST+1
	 AOJN	Z,ADJ.2
	JRST	0(QLINK)		; Return

	END
*gOZ™