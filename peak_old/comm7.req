!! title COMM7 - The Runtime Help System ;
! *************************************************************************
*									  *
*				   COMM7				  *
*			  The Runtime Help System			  *
*									  *
***************************************************************************


	   This file is required as a source!file in COMAND.SAI.


**************************************************************************;
!! title Rtn ShowFileInBox ;
! The routine to run a file through the box.
;

! <<  This approach to showing help should be abandoned in favor of a
!     "special Help Buffer" approach in 2.0 and better.
;

procedure ShowFileInBox (string F);
begin "show file in box"
  ifcr TymcomX thenc
    string L;
    integer C, SaveILP;
    external integer T!ILP;	! scroll flag in terminal module ;

    if ( not F!Lookup( F ) ) then
      begin
	W!Msg( "% Can't lookup "&F&" for box display" );
	return;
      end;

    SaveILP := T!ILP;		! turn off scrolling for smooth ;
    T!ILP := false;		! box display ;

    while ( true ) do
      begin "each page"

	while ( true ) do
	  begin "this page"
	    L := NULL;
	    while (( C := F!GetC ) >= " " ) do L := L & C;
	    W!BAdd( L );
	    if (( C = FF ) or ( C <= '0 )) then done "this page";
	    if ( C = CR ) then C := F!GetC;
	  end "this page";

	if ( C > 0 ) then
	  begin
	    W!Msg( "-More-" );  C := C!GetC;
	    if ( C!Tab[ C ] land (IsAbort+IsNo)) then done "each page";
	    W!BClr;
	  end
	else
	  begin
	    W!Msg( "-No More-" );  C := C!GetC;
	    done "each page"
	  end;
	
      end "each page";

    T!ILP := SaveILP;

    F!Release;
  endc

  ifcr Tops20 thenc
    string
      L;
    integer
      C,
      F!Chan,
      SaveILP;
    external integer
      T!ILP;                            ! Scroll flag in terminal module.  
                                        ;

    F!Chan _ openfile(F, "NOE");

    if (!skip!) then
    begin
      W!Msg("% Can't lookup " & F & " for box display");
      return;
    end;

    SaveILP _ T!ILP;		! turn off scrolling for smooth ;
    clear(T!ILP);		! box display ;

    while (true) do
    begin "each page"
      while (true) do
      begin "this page"
        L _ null;

        while ((C _ charin(F!Chan)) >= " ") do
          appnd(L, C);

        W!BAdd(L);

        if ((C = FF) or (C = '0)) then
          done "this page";

        if (C = CR) then
          C _ charin(F!Chan);
      end "this page";

      if (C neq 0) then
      begin
        W!Msg("-More-");  C _ C!GetC;

        if (C!Tab[C] land (IsAbort+IsNo)) then
          done "each page";

        W!BClr;
      end
      else
      begin
        W!Msg("-No More-");
        C _ C!GetC;
        done "each page"
      end;
    end "each page";

    T!ILP _ SaveILP;

    cfile(F!Chan);
  endc
end "show file in box";
!! title Rtn C!Help ;
! The main routine for help.
;

simple procedure C!Help;
begin "help"
  integer
    C,
    RunAUN;
  string
    AUNStr;

  ifcr TymcomX thenc
    C!ArgV _ 0;
    W!BSet(true);
    W!MsgF _ true;

    if (not C!Debug) then
    begin
      W!BClr;
      W!BAdd("                    PEAK Online Help");
      W!BAdd(null);
      W!BAdd("                  Type N or ^G to quit");
      W!BAdd("          Type any other character to continue");
      W!BAdd(null);
      W!BAdd("                     Open Buffers:");
      W!BAdd(null);
      B!BufStat;

      W!Msg("-More-");
      C _ C!GetC;

      if (not(C!Tab[C] land (IsAbort+IsNo))) then
      begin
        W!BClr;
        RunAUN _ call(xwd(-1, -'25), "gettab");		! "file" aun ;
        AUNStr _ "[" & cvos(RunAUN lsh -18) &
                  "," & cvos(RunAUN land '777777) & "]";
        ShowFileInBox("PEAK.HLP" & AUNStr);
      end;
    end
    else
    begin
      W!Msg("-Type any character to clear-");
      C!GetC;
    end;

    W!Msg(null);
    W!MsgF _ false;
    W!BSet(false);
  endc

  ifcr Tops20 thenc
    T!Bell;
    W!Msg("Help is not availible at this time");
  endc
end "help";


! ************************** End of COMM7.REQ ****************************;
