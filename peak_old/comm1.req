!! title COMM1 - Simple Commands ;
! *************************************************************************
*									  *
*				   COMM1				  *
*			      Simple Commands				  *
*									  *
***************************************************************************


	   This file is required as a source!file in COMAND.SAI.


**************************************************************************;
!! title Rtn C!Insert ;
! Insert some number of the character in C!Cmd into the buffer, obeying
! all prevailing modes and deviations. (Code duplication is to improve
! runtime efficiency.)
!
! Note that the boolean InsertLiterally is usually false, indicating normal
! character insertion.  It is only true when the character wants to be
! inserted literally.  (This is only a concern when the character is a CR
! or a TAB, both of which would noramally be interpreted.)
!
! Note that this routine is called by other insert routines, as well as
! by the Command Dispatcher.
;

simple procedure C!Insert;
begin "C!Insert"
  own integer
    Col;                                ! The column number into which
                                        ! the next character will be
                                        ! inserted.  (This must survive
                                        ! between invocations.)
                                        ;
  integer
    I;                                  ! Iteration variable
                                        ;


  if (B!Lock) then
    PuntCommand;

  C!ArgV _ C!ArgV max 1;

! ------------------------------------------------------------------------;

  ! Perform the overwrite mode delete operation.
  ;

  if (G!OverWriteP and SP <= C!Cmd < DEL) then
  begin
    for I _ 1 step 1 while (I <= C!ArgV and not AtEOL) do
    begin
      if (B!GetC = TAB) then
      begin
        if (GetColumn mod C!TabWidth = 0) then
          B!Delete(FORWARDS);
      end
      else
        B!Delete(FORWARDS);
    end;
  end;

! ------------------------------------------------------------------------;

  ! Perform character insertion.
  ;

  if (G!TextMode) then
  begin
    ! Do Text Mode insertion.
    ;

    if (C!Addr neq C!LastAddr) then
      ! If this command was not the last comand issued.
      ;

      Col _ GetColumn;

    if (C!Cmd = CR) then
    begin
      Col _ 1;

      for I _ 1 step 1 until C!ArgV do
      begin
        B!Insert(CR);

        if (not InsertLiterally) then
          B!Insert(LF);
      end;
    end
    else if (C!Cmd = TAB) then
    begin
      for I _ 1 step 1 until C!ArgV do
      begin
        integer
          Width;

        incr(Col, (Width _ CharWidth(TAB, Col)));

        if (C!UseTabs or InsertLiterally) then
          B!Insert(TAB)
        else
          GenerateWS(Width);
      end;
    end
    else
    begin
      if (C!Cmd = BS) then
        decr(Col, C!ArgV)
      else
        incr(Col, C!ArgV);

      for I _ 1 step 1 until C!ArgV do
        B!Insert(C!Cmd);
    end;

    if (Col > C!RightMargin + 1) then
    begin
      ! Perform line fill.
      ;

      integer
        EndMk,
        SavMk;

      B!SetM((SavMk _ B!AllocateMark), B!GetP);
      MoveToEOL;
      B!SetM((EndMk _ B!AllocateMark), B!GetP);
      B!SetP(B!GetM(SavMk));

      if (Col > C!RightMargin + 2) then
        TryColumn(C!RightMargin + 1);

      SkipWS(BACKWARDS);
      SkipNWS(BACKWARDS);
      SkipWS(BACKWARDS);
      SkipNWS(BACKWARDS);

      Col _ GetColumn;
      DoFill(Col, EndMk, C!LeftMargin, C!RightMargin);
      B!SetP(B!GetM(SavMk));

      if ((C!Cmd = SP or C!Cmd = TAB) and AtEol) then
      begin        
        MoveForwards;
        SkipWS(FORWARDS);
      end;

      Col _ GetColumn;
      B!DeallocateMark(SavMk);
      B!DeallocateMark(EndMk);
    end;
  end
  else
  begin
    ! Do Normal Mode insertion.
    ;

    if (C!Cmd = CR) then
    begin
      for I _ 1 step 1 until C!ArgV do
      begin
        B!Insert(CR);

        if (not InsertLiterally) then
          B!Insert(LF);
      end;
    end
    else if (C!Cmd = TAB) then
    begin
      integer
        Col;

      Col _ GetColumn;

      for I _ 1 step 1 until C!ArgV do
      begin
        integer
          Width;

        incr(Col, (Width _ CharWidth(TAB, Col)));

        if (C!UseTabs or InsertLiterally) then
          B!Insert(TAB)
        else
          GenerateWS(Width);
      end;
    end
    else
    begin
      for I _ 1 step 1 until C!ArgV do
        B!Insert(C!Cmd);
    end;
  end;

  C!ArgV _ 0;
end "C!Insert";
!! title Rtn C!InsertCRLF ;
! Insert a number of <crlf> pairs into the buffer.
;

simple procedure C!InsertCRLF;
begin "C!InsertCRLF"
  integer
    CAddr,
    Cmd;

  if (B!Lock) then
    PuntCommand;

  CAddr  _ C!Addr;
  C!Addr _ location(C!Insert);
  Cmd    _ C!Cmd;

  C!Cmd  _ CR;
  C!Insert;

  C!Cmd  _ Cmd;
  C!Addr _ CAddr;
end "C!InsertCRLF";
!! title Rtn C!InsertTAB ;
! Insert some number of TAB equivalents into the buffer.
;

simple procedure C!InsertTAB;
begin "C!InsertTab"
  integer
    CAddr,
    Cmd;

  if (B!Lock) then
    PuntCommand;

  CAddr  _ C!Addr;
  C!Addr _ location(C!Insert);
  Cmd    _ C!Cmd;

  C!Cmd  _ TAB;
  C!Insert;

  C!Cmd _ Cmd;
  C!Addr _ CAddr;
end "C!InsertTab";
!! title Rtn C!OverWrite ;
! Toggle the overwrite flag. ;

simple procedure C!OverWrite;
begin "C!OverWriteP"
  toggle(G!OverWriteP);
  W!NewS;
  C!ArgV _ 0;
end "C!OverWriteP";
!! title Rtn C!Quote ;
! "Quote" the next character typed.
;

simple procedure C!Quote;
begin "C!Quote"
  integer
    CAddr,
    Cmd;

  if (B!Lock) then
    PuntCommand;

  W!Msg("Quote");

    ! A friendly reminder that we're quoting.
    ;

  CAddr  _ C!Addr;
  C!Addr _ location(C!Insert);
  Cmd    _ C!Cmd;
  set(InsertLiterally);

  C!Cmd  _ C!GetC;
  C!Insert;

  clear(InsertLiterally);
  C!Addr _ CAddr;
  C!Cmd  _ Cmd;
  W!Msg(null);
end "C!Quote";
!! title Rtn C!Open ;
! "Open" a line by inserting <crlf> and backing up to before it. ;

simple procedure C!Open;
begin "C!Open"
  integer
    CAddr,
    Cmd,
    SavePt;

  if (B!Lock) then
    PuntCommand;

  SavePt _ B!GetP;
  CAddr  _ C!Addr;
  C!Addr _ location(C!Insert);
  Cmd    _ C!Cmd;

  C!Cmd  _ CR;
  C!Insert;

  C!Addr _ CAddr;
  C!Cmd  _ Cmd;

  B!SetP(SavePt);
end "C!Open";
!! title Rtn C!IndentUp ;
! Break the line at the point and give the new line the same
! indentation as the old. ;

procedure C!IndentUp;
begin "C!IndentUp"
  integer
    I;

  if (B!Lock) then
    PuntCommand;

  C!ArgV _ C!ArgV max 1;
  I      _ GetIndent;

  while (C!ArgV) do
  begin
    UpLine;
    B!Insert(CR);
    B!Insert(LF);
    ForceIndent(I);
    decr(C!ArgV);
  end;
end "C!IndentUp";
!! title Rtn C!IndentDown ;
! Break the line at the point and give the new line the same
! indentation as the old. ;

procedure C!IndentDown;
begin "C!IndentDown"
  integer
    I;

  if (B!Lock) then
    PuntCommand;

  C!ArgV _ C!ArgV max 1;
  I      _ GetIndent;

  while (C!ArgV) do
  begin
    B!Insert(CR);
    B!Insert(LF);
    ForceIndent(I);
    decr(C!ArgV);
  end;
end "C!IndentDown";
!! title Rtn C!ColumnDown ;
! Create a new line indented to the current column ;

procedure C!ColumnDown;
begin "C!ColumnDown"
  integer
    I;

  if (B!Lock) then
    PuntCommand;

  C!ArgV _ C!ArgV max 1;
  I      _ GetColumn - 1;

  while (C!ArgV) do
  begin
    MoveToEOL;
    B!Insert(CR);
    B!Insert(LF);
    ForceIndent(I);
    decr(C!ArgV);
  end;
end "C!ColumnDown";
!! title Rtn C!MoveF ;
! *************************  MOVEMENT COMMANDS  *********************** ;


! Move one character forward ;

simple procedure C!MoveF;
begin "C!MoveF"
  C!ArgV _ C!ArgV max 1;

  while (C!ArgV) do
  begin
    if (B!EndP) then
    begin
      set(C!MAbort);
      done;
    end;

    MoveForwards;
    decr(C!ArgV);
  end;
end "C!MoveF";
!! title Rtn C!MoveB ;
! Move one character backward ;

simple procedure C!MoveB;
begin "C!MoveB"
  C!ArgV _ C!ArgV max 1;

  while (C!ArgV) do
  begin
    if (B!BegP) then
    begin
      set(C!MAbort);
      done;
    end;

    MoveBackwards;
    decr(C!ArgV);
  end;
end "C!MoveB";
!! title Rtn C!MoveBOL ;
! Move to beginning of line ;

simple procedure C!MoveBOL;
begin
  MoveToBOL;
  C!ArgV _ 0;
end;
!! title Rtn C!MoveEOL ;
! Move to the end of a line ;

simple procedure C!MoveEOL;
begin
  MoveToEOL;
  C!ArgV _ 0;
end;
!! title Rtn C!MoveUp ;
! Move up a line.
;

forward simple procedure C!MoveDown;

simple procedure C!MoveUp;
begin "C!MoveUp"
  C!ArgV _ C!ArgV max 1;

  if (C!LastAddr neq location(C!MoveUp) and
      C!LastAddr neq location(C!MoveDown)) then
    C!StickyColumn _ GetColumn;

  while (C!ArgV) do
  begin "move"
    MoveToBOL;

    if (B!BegP) then
    begin
      set(C!MAbort);
      done "move";
    end;

    MoveBackwards;
    MoveToBOL;
    decr(C!ArgV);
  end "move";

  if (C!ArgV = 0) then
    TryColumn(C!StickyColumn);

  C!ArgV _ 0;
end "C!MoveUp";
!! title Rtn C!MoveDown ;
! Move down to the next line.
;

simple procedure C!MoveDown;
begin "down"
  C!ArgV _ C!ArgV max 1;

  if (C!LastAddr neq location(C!MoveUp) and
      C!LastAddr neq location(C!MoveDown)) then
    C!StickyColumn _ GetColumn;

  while (C!ArgV) do
  begin "move"
    MoveToEOL;

    if (B!EndP) then
    begin
      set(C!MAbort);
      done "move";
    end;

    MoveForwards;
    decr(C!ArgV);
  end "move";

  if (C!ArgV = 0) then
    TryColumn(C!StickyColumn);

  C!ArgV _ 0;
end "down";
!! title Rtn C!ScrollUp ;
! "Scroll" the screen up a line.
;

simple procedure C!ScrollUp;
begin
  integer
    SavePoint;

  C!ArgV    _ C!ArgV max 1;
  SavePoint _ B!GetP;
  B!SetP(W!GetS);

  while (C!ArgV) do
  begin
    MoveToEOL;
    MoveForwards;
    decr(C!ArgV);
  end;

  W!SetS(B!GetP);
  B!SetP(SavePoint max B!GetP);
  W!Frame;
end;
!! title Rtn C!ScrollDown ;
! "Scroll" the screen down a line.
;

simple procedure C!ScrollDown;
begin
  integer
    SavePoint;

  C!ArgV    _ C!ArgV max 1;
  SavePoint _ B!GetP;
  B!SetP(W!GetS);

  while (C!ArgV) do
  begin
    MoveBackWards;
    MoveToBOL;
    decr(C!ArgV);
  end;

  W!SetS(B!GetP);

  if (G!WrapP) then
  begin
    ! <<  This junk is intended to circumvent a sticky window "Fill"
    !     inconsistency having to do with the fact that the cursor cannot
    !     appear under the vertical bar at the end of a wrapped line.
    !     When the end of the screen appears after the end of a line that
    !     wraps around, the current (circa 3.6.84) window fill logic
    !     forces the cursor to appear at the beginning of the next line,
    !     and this causes a reframe to occur, and THIS nicely changes the
    !     beginning (and end) end of screen.
    ;
    integer
      Tmp;

    B!SetP(Tmp _ FindEOS);

    if (not AtEOL) then
      decr(Tmp);

    B!SetP(SavePoint min Tmp);
  end
  else
    B!SetP(SavePoint min FindEOS);

  W!Frame;
end;
!! title Rtn C!MovTOS ;
! Move to the top of the current screen ;

simple procedure C!MovTOS;
  begin "top"

    W!Frame;
    B!SetP( W!GetS );
    W!FixS;
    C!ArgV := 0;

  end "top";
!! title Rtn C!LineTOS ;
! Move the current line to the top of the screen.
;

simple procedure C!LineTOS;
begin "C!LineTOS"
  integer
    SavePoint;

  W!Frame;
  SavePoint _ B!GetP;
  MoveToBOL;
  W!SetS(B!GetP);
  B!SetP(SavePoint);
  C!ArgV _ 0;
end "C!LineTOS";
!! title Rtn C!LineBOS ;
! Move the current line to the end of the screen.
;

simple procedure C!LineBOS;
begin "C!LineBOS"
end "C!LineBOS";
!! title Rtn C!MovBOS ;
! Move to the bottom of the current screen.
;

simple procedure C!MovBOS;
begin "C!MovBOS"
  W!Frame;

  if (G!WrapP) then
  begin
    ! <<  This junk is intended to circumvent a sticky window "Fill"
    !     inconsistency having to do with the fact that the cursor cannot
    !     appear under the vertical bar at the end of a wrapped line.
    !     When the end of the screen appears after the end of a line that
    !     wraps around, the current (circa 3.6.84) window fill logic
    !     forces the cursor to appear at the beginning of the next line,
    !     and this causes a reframe to occur, and THIS nicely changes the
    !     beginning (and end) end of screen.
    ;
    integer
      Tmp;

    B!SetP(Tmp _ FindEOS);

    if (not AtEOL) then
      decr(Tmp);

    B!SetP(Tmp);
  end
  else
    B!SetP(FindEOS);

  W!FixS;
  C!ArgV _ 0;
end "C!MovBOS";
!! title Rtn C!NextScreen ;
! Move to the next "screen" ;
! ** This needs a rewrite to allow for wrapped lines ** ;

simple procedure C!NextScreen;
  begin "next screen"
    own integer C, Count;

    if ( C!ArgV <= 0 ) then C!ArgV := 1;

    W!Frame;
    B!SetP( W!GetS );
    while ( C!ArgV ) do
      begin "argv times"
	Count := W!Size - 1;	! want to skip this many lines ;
	while ( Count ) do
	  begin "count times"
	    if ( B!EndP ) then done "argv times";
	    C := B!GetC;
	    if ( C = FF ) then
	      Count := 0
	    else if ( B!GetC = CR ) then
	      Count := Count - 1;
	    MoveForwards;
	  end "count times";
	if ( B!GetC = FF ) then MoveForwards;
	if ( not B!EndP ) then W!SetS( B!GetP );
	C!ArgV := C!ArgV - 1;
      end "argv times";

    W!FixS;
    C!ArgV := 0;

  end "next screen";
!! title Rtn C!PriorScreen ;
! Move to the prior "screen" ;
! ** This needs to be rewrittne to allow for wrapped lines ** ;

simple procedure C!PriorScreen;
  begin "prior screen"
    own integer C, Count;

    if ( C!ArgV <= 0 ) then C!ArgV := 1;

    W!Frame;
    B!SetP( W!GetS );

    while ( C!ArgV ) do
      begin "argv times"
	MoveBackwards;
	if ( B!GetC = FF ) then MoveBackwards;
	Count := W!Size;
	C := NULL;
	while ( true ) do
	  begin "count times"
	    C := B!GetC;
	    if ( B!BegP ) then done "argv times";
	    if ( C = FF ) then Count := 0
	    else if ( C = CR ) then Count := Count - 1;
	    if ( Count = 0 ) then done "count times";
	    MoveBackwards;
	  end "count times";
	if ( Count = 0 ) then MoveForwards;
	W!SetS( B!GetP );
	C!ArgV := C!ArgV - 1;
      end "argv times";

    W!FixS;
    C!ArgV := 0;

  end "prior screen";
!! title Rtn C!MovBOP ;
! Move to the beginning of the current page ;

simple procedure C!MovBOP;
  begin
    integer Temp1;

    MoveBackwards;
    if ( not B!BegP ) then
      begin
	forever do
	  begin "a page"
	    Temp1 := B!GetC;
	    if ( Temp1 = FF ) then
	      begin
		MoveForwards;
		done "a page";
	      end;
	    if ( B!BegP ) then done "a page";
	    MoveBackwards;
	  end "a page";
      end;
    W!FixS;
    C!ArgV := 0;
  end;
!! title Rtn C!MovEOP ;
! Move to the end of the current page ;

simple procedure C!MovEOP;
  begin
    integer Temp1;

    W!FixS;

    forever do
      begin "a page"
	if ( B!EndP ) then done "a page";
	Temp1 := B!GetC;
	if ( Temp1 = FF ) then done "a page";
	MoveForwards;
      end "a page";
    C!ArgV := 0;
  end;
!! title Rtn C!MovBOB ;
! Move to the beginning of the buffer ;

simple procedure C!MovBOB;
  begin
    B!SetP( 0 );
    W!FixS;
    C!ArgV := 0;
  end;


!! title Rtn C!MovEOB ;
! Move to the end of the buffer ;

simple procedure C!MovEOB;
  begin
    B!SetP( '377777777777 );	! this *should* get us to the end ;
    W!FixS;
    C!ArgV := 0;
  end;
!! title Rtn C!DelF ;
! **************************  DELETE COMMANDS  ***************************;

! Delete one character forward ;

simple procedure C!DelF;
begin
  if (C!ArgV <= 0) then
    C!ArgV _ 1;

  while (C!ArgV) do
  begin
    integer
      Char;

    Char _ B!GetC;
    B!Delete(FORWARDS);

    if (Char = CR and B!GetC = LF) then
      B!Delete(FORWARDS);

    decr(C!ArgV);
  end;
end;
!! title Rtn C!DelB ;
! Delete one character backward.  The reason we backup, look at the
!  character, then move forward and backdelete is to avoid gap shifts. ;

simple procedure C!DelB;
begin
  boolean
    ReFrameP;

  if (C!ArgV <= 0) then
    C!ArgV _ 1;

  ReFrameP _ false;

  while (C!ArgV) do
  begin
    if (B!BegP) then
    begin
      C!ArgV _ 0;
      return;
    end;

    ReFrameP _ W!GetS = B!GetP;
    MoveBackwards;

    if (B!GetC = CR) then
    begin
      B!Delete(FORWARDS);

      if (B!GetC = LF) then
	B!Delete(FORWARDS);
    end
    else
      B!Delete(FORWARDS);

    decr(C!ArgV);
  end;

  if (ReFrameP) then
  begin
    MoveBackwards;
    W!Disp(true);
    MoveForwards;
  end;

    ! Reframe if the point is at the beginning of the window ;
end;
!! title Rtn C!DelWhiteF ;
! Delete whitespace forwards ;

simple procedure C!DelWhiteF;
begin
  while (C!IsWhiteP) do
    B!Delete(FORWARDS);

  C!ArgV _ 0;
end;
!! title C!DelWhiteB ;
! Delete whitespace backwards ;

simple procedure C!DelWhiteB;
begin
  while (not B!BegP) do
  begin
    B!Move(BACKWARDS);

    if (C!IsWhiteP) then
      B!Delete(FORWARDS)
    else
    begin
      B!Move(FORWARDS);
      done;
    end;
  end;

  C!ArgV _ 0;
end;
!! title Rtn C!DelNULLs ;
! delete all nulls in a file ;

procedure C!DelNULLs;
begin
  integer
    SavePoint;

  if (B!Lock) then
  begin
    W!Msg("Buffer must be unlocked to delete nulls.");
    PuntCommand;
  end;

  C!ArgV _ 0;
  B!SetM((SavePoint _ B!AllocateMark), B!GetP);
  B!SetP(0);

  while (not B!EndP) do
  begin
    if (B!GetC = null) then
      B!Delete(FORWARDS)
    else B!Move(FORWARDS);
  end;

  B!SetP(B!GetM(SavePoint));
  B!DeallocateMark(SavePoint);
end;
!! title Rtn C!SetMark ;
! Set a mark at the current point ;

procedure C!SetMark;
begin
  if (0 <= C!ArgV <= 9) then
  begin
    B!SetM(C!ArgV, B!GetP);
    W!Msg("Mark " & cvs(C!ArgV) & " Set");
  end
  else
  begin
    W!Msg("Bad Number - " & cvs(C!ArgV));
    T!Bell;
  end;

  C!ArgV _ 0;
end;
!! title Rtn C!GotoMark ;
! Go to a specified Mark ;

procedure C!GotoMark;
begin
  integer
    M;

  if (0 <= C!ArgV <= 9) then
  begin
    M _ B!GetM(C!ArgV);

    if (M < 0) then
      W!Msg("Mark " & cvs(C!ArgV) & " isn't set")
    else
      B!SetP(M);
  end
  else
  begin
    W!Msg("Bad Mark Number - " & cvs(C!ArgV));
    T!Bell;
  end;

  C!ArgV _ 0;
end;
!! title Rtn C!ClearMark ;
! Deallocate a user mark.
;

simple procedure C!ClearMark;
begin "C!ClearMark"
  if (0 <= C!ArgV <= 9) then
  begin
    B!DeallocateMark(C!ArgV);
    W!Msg("Mark " & cvs(C!ArgV) & " cleared");
  end
  else
  begin
    W!Msg("Bad Mark Number - " & cvs(C!ArgV));
    T!Bell;
  end;

  C!ArgV _ 0;
end "C!ClearMark";
!! title Rtn C!SwapPM ;
! Swap the point and the mark ;

procedure C!SwapPM;
  begin
    integer T, M;
    if (( C!ArgV < 0 ) or ( C!ArgV > 9 )) then
      begin
	W!Msg( "Bad Mark Number - " & cvs( C!ArgV ));
      end
    else
      begin
	T := B!GetP;
	M := B!GetM( C!ArgV );
	if ( M < 0 ) then
	  W!Msg( "Mark " & cvs( C!ArgV )& " isn't set" )
	else
	  begin
	    B!SetP( M );
	    B!SetM( C!ArgV, T );
	  end;
	W!FixS;
      end;
    C!ArgV := 0;
  end;

!! title Rtn C!Refresh ;
! ************************  MISCELLANEOUS COMMANDS  ********************* ;


! Routine to refresh the screen. ;

simple procedure C!Refresh;
  begin
    C!ArgV := 0;
    T!RSet;		! reset the terminal (just in case) ;
    R!Init;
    W!NewS;
    W!Disp( false );
  end;


!! title Rtn C!Redislpay ;
! Force a non-interruptible redisplay ;

simple procedure C!Redisplay;
  begin
    C!ArgV := 0;
    W!NewS;
    W!Disp( false );
  end;
!! title Rtn C!Twiddle ;
! Routine to "twiddle" the last two characters ;
;

simple procedure C!Twiddle;
begin "C!Twiddle"
  boolean
    CrlfP;
  own integer
    C,
    SavePoint;

  if ((SavePoint _ B!GetP) >= 2) then
  begin
    B!Move(BACKWARDS);

    C _ B!GetC;
    B!Delete(FORWARDS);
    B!Move(BACKWARDS);

    clear(CrlfP);

    if (C = LF and B!GetC = CR) then
    begin
      B!Delete(FORWARDS);
      B!Move(BACKWARDS);
      set(CrlfP);
    end;

    if (B!GetC = LF) then
    begin
      B!Move(BACKWARDS);

      if (B!GetC neq CR) then
	B!Move(FORWARDS);
    end;

    if (CrlfP) then
    begin
      B!Insert(CR);
      B!Insert(LF);
      clear(CrlfP);
    end
    else
      B!Insert(C);

    B!SetP(SavePoint);
  end;

  C!ArgV _ 0;
end "C!Twiddle";
!! title Rtn C!Toggle ;
! Routine to toggle the case of a character ;

simple procedure C!Toggle;
  begin "toggle"
    own integer C;

    if ( C!ArgV <= 0 ) then C!ArgV := 1;

    while ( C!ArgV ) do
      begin
	C := B!GetC;
	if ( "A" <= C <= "Z" ) then
	  C := C + '40
	else if ( "a" <= C <= "z" ) then
	  C := C - '40;
	if ( C neq B!GetC ) then
	  begin
	    B!Delete( FORWARDS );
	    B!Insert( C );
	  end
	else
	  B!Move( FORWARDS );
	C!ArgV := C!ArgV - 1;
      end;

  end "toggle";
!! title Rtn C!IncrChar ;
! Increment (modulo 128) the ascii value of the character following the
point. ;

simple procedure C!IncrChar;
begin "C!IncrChar"
  integer
    Char;

  if (B!EndP) then
    PuntCommand;

  if (not C!ArgV) then
    C!ArgV _ 1;

  Char _ (B!GetC + C!ArgV) land !mask(7);
  B!Delete(FORWARDS);
  B!Insert(Char);
  B!Move(BACKWARDS);
  C!ArgV _ 0;
end "C!IncrChar";
!! title Rtn C!DecrChar ;
! Increment (modulo 128) the ascii value of the character following the
! point. ;

simple procedure C!DecrChar;
begin "C!DecrChar"
  integer
    Char;

  if (B!EndP) then
    PuntCommand;

  if (not C!ArgV) then
    C!ArgV _ 1;

  Char _ (B!GetC - C!ArgV) land !mask(7);
  B!Delete(FORWARDS);
  B!Insert(Char);
  B!Move(BACKWARDS);
  C!ArgV _ 0;
end "C!DecrChar";
!! title Rtn C!ForceLeftMargin ;
! Force the current line to have a specified (or the default) left margin.
;

simple procedure C!ForceLeftMargin;
begin "C!ForceLeftMargin"
  ForceIndent((if (C!ArgV) then C!ArgV else C!LeftMargin) - 1);
  C!ArgV _ 0;
end "C!ForceLeftMargin";
!! title Rtn C!ShowColumn ;
! Display the indentity of the column to the right of the point.
;

simple procedure C!ShowColumn;
begin "C!ShowColumn"
  W!Msg("Column: " & cvs(GetColumn));
  C!ArgV _ 0;
end "C!ShowColumn";
!! title Rtn C!ShowOctal ;
! Display the octal for the character following the point.
;

simple procedure C!ShowOctal;
begin "C!ShowOctal"
  W!Msg("Octal of next character: '" & cvos(B!GetC));
  C!ArgV _ 0;
end "C!ShowOctal";
!! title Rtn C!InsertOctal ;
! Insert the character whose octal value is input.
;

simple procedure C!InsertOctal;
begin "C!InsertOctal"
  integer
    C,
    Oct;
  string
    Prompt;

  Prompt _ "Enter Octal of the character to be inserted: '";

  while (true) do
  begin
    if (C!ReadString(Prompt, ESC, CR)) then
    begin
      Oct _ cvo(C!String);
      W!Msg(Prompt & C!String);

      if (Oct <= DEL and kequ(cvos(Oct), C!String)) then
      begin
        C _ C!Cmd;
        C!Cmd _ Oct;
        C!Insert;
        C!Cmd _ C;
        done;
      end;
    end;
  end;

  W!Msg(null);
end "C!InsertOctal";
!! title Rtn C!SetAppendKill ;
! Make the next kill append to the kill buffer.
;

simple procedure C!SetAppendKill;
begin "C!SetAppendKill"
  set(G!AppendKillP);
  C!ArgV _ 0;
end "C!SetAppendKill";
!! title Rtn C!FillLine ;
! Force the current line to be filled within the defined margins.
;

simple procedure C!FillLine;
begin "C!FillLine"
  integer
    Col,
    SavMk;

  B!SetM((SavMk _ B!AllocateMark), B!GetP);
  MoveToEOL;

  if (GetColumn > C!RightMargin + 1) then
  begin
    integer
      EndMk;

    B!SetM((EndMk _ B!AllocateMark), B!GetP);

    TryColumn(C!RightMargin + 1);
    SkipWS(BACKWARDS);
    SkipNWS(BACKWARDS);
    SkipWS(BACKWARDS);
    SkipNWS(BACKWARDS);

    Col _ GetColumn;
    DoFill(Col, EndMk, C!LeftMargin, C!RightMargin);
    B!SetP(B!GetM(SavMk));
    B!DeallocateMark(EndMk);
  end
  else
    B!SetP(B!GetM(SavMk));

  B!DeallocateMark(SavMk);
end "C!FillLine";
!! title Rtn C!InsStrToCol ;
! Insert characters from an input string until you reach the right-most
! position less than or equal to a specified column, depending upon the
! width of the string.  (Remember that the width of a string can vary
! contingent upon various factors).
;

simple procedure C!InsStrToCol;
begin "C!InsStrToCol"
  integer
    C,
    CAddr,
    NewCol,
    SavCmd,
    ThisCol,
    Width;
  string
    Prompt,
    Str;

  ThisCol _ GetColumn;
  NewCol  _ C!ArgV;
  C!ArgV  _ 0;
  Prompt  _ "String to Insert:";

  if (C!ReadString(Prompt, CR, ESC) and NewCol > ThisCol) then
  begin
    SavCmd _ C!Cmd;
    CAddr  _ C!Addr;
    C!Addr _ location(C!Insert);

    Str    _ C!String;

    while ((ThisCol +
        (Width _ CharWidth((C _ lop(Str)), ThisCol))) <= NewCol) do
    begin
      C!Cmd _ C;
      C!Insert;

      incr(ThisCol, Width);

      if (not length(Str)) then
        Str _ C!String;
    end;

    C!Cmd  _ SavCmd;
    C!Addr _ CAddr;
  end
  else
    W!Msg(null);
end "C!InsStrToCol";


! **************************  End of COMM1.REQ **************************;
@0æ