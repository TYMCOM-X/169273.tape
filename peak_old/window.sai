Entry;
begin "DED - window manager"
  require "DED.DEF" source!file;
!! title Peak Window Manager ;
! *************************************************************************
*                                                                         *
*                               Window.Sai                                *
*                                                                         *
***************************************************************************

        The Peak Window Manager.

**************************************************************************;


!                         From the Command Module
;

  external boolean procedure
    C!ChrP;                             ! Is there a character ready
                                        ! anywhere?
                                        ;
  external integer
    C!Debug;                            ! True if we are debugging.
                                        ;
  external boolean
    G!CRCtl;            		! true if lonely CRs appear as
                                        ! "^m"
                                        ;

!                         From the Buffer Module
;

  external boolean
    B!BegP,                             ! True if point is at the
                                        ! beginning of the buffer.
                                        ;
    B!EndP,                             ! True if point is at the end of
                                        ! the buffer.
                                        ;
    B!ModP;                             ! True if the buffer has been
                                        ! modified. 
                                        ;
  external integer
    B!Lock;                             ! State of the Buffer Lock.
                                        ;
  external string
    B!Mode,                             ! Current editing mode.
                                        ;
    B!File,                             ! The name of the file we are
                                        ! editing. 
                                        ;
    B!Alias;                            ! An alternate display name for
                                        ! the current buffer.
                                        ;
  external integer
    B!WS0,                              ! Index of the Mark indicating
                                        ! the start of Window 0.
                                        ;
    B!WS1;                              ! Index of the Mark indicating
                                        ! the start of Window 1.
                                        ;

  external simple integer procedure
    B!GetM (integer M);                 !  Return the location of a
                                        !  certain Mark. 
                                        ;
  external simple procedure
    B!SetM (integer M, P);              ! Set the location associated
                                        ! with a certain Mark.
                                        ;
  external simple procedure
    B!SetB (integer BufNum);            ! Go to a particular buffer.
                                        ;
  external simple integer procedure
    B!Cnt;                              ! Return the number of buffers in
                                        ! the chain.
                                        ;
  external simple procedure
    B!Move (integer Direction);         ! Move one buffer in some
                                        ! direction. 
                                        ;
  external simple integer procedure
    B!GetP;                             ! Get the location of the point.  
                                        ;
  external simple integer procedure
    B!GetC;                             ! Get the character after the
                                        ! point. 
                                        ;
  external simple procedure
    B!SetP (integer Position);          ! Move the point to a particular
                                        ! location. 
                                        ;
  external simple integer procedure
    B!GetL;                             ! Get the location of the "last"
                                        ! point. 
                                        ;
  external simple procedure
    B!SetL (integer Position);          ! Set the location of the "last"
                                        ! point. 
                                        ;
  external simple integer procedure
    B!Pcnt;                             ! Return the distance in percent
                                        ! of the point into the buffer.
                                        ;

!                        From the Redisplay Module
;

  external integer
    T!Lines,
    T!Width;
  external string
    T!Name;

  external procedure
    T!Bell;

  external procedure
    R!Disp (integer array Buffer;
        boolean PuntIfKeyHit;
        integer CursorRow, CursorCol);

  ! global state stuff ;

  external integer
    G!ESCF,
    G!OverStrikeP,
    G!WrapP,
    C!TABWidth,
    G!FFBreak;
!! title Internal Data Structures ;
! Internal data and structures ;

  ! The screen buffer is available to the redisplay, and *must* be declared
  !  the same size and shape.  ;

  own trusted integer array W!Buf[ 1 : 25, 1 : 27 ];

  ! the help "box" is overlaid on W!Buf iff W!BoxP is true ;

  own internal boolean W!BoxP;
  own trusted integer array W!Box[ 1 : 20, 1 : 12 ];
  own integer W!BoxPnt;

  ! information about the window(s) ;

  internal boolean
    W!IntP;                             ! True iff the window module has
                                        ! been initialized.
                                        ;
  own boolean W!SplitP;			! true iff split window ;
  own integer W!Num;			! window we're in [ 0..1 ] ;
  own internal integer W!Buf0, W!Buf1;	! corresponding buffer # ;
  own integer W!BufNum;			! current buffer # ;
  own integer W!Size0, W!Size1;		! size of the windows ;
  own internal string W!Bar;		! bar to use when split ;

  ! some handy byte pointers and lengths that we want to keep around ;

  own integer W!WindowBP;
  own integer W!StatusLine, W!MsgLine;
  own integer W!StatusBP, W!FixStatusBP, W!FixStatusLen;
  own integer W!MsgBP, W!MsgLen;

  ! for remembering where to put the cursor ;

  own integer SavedCursorRow, SavedCursorColumn;

  ! for forcing the cursor to the end of the message line ;

  internal own boolean W!MsgF;
!! title Rtn W!Init ;
! Initialize the Window module by doing whatever. ;

  forward simple internal procedure W!Badd( string Text );
  forward simple internal procedure W!BClr;

internal simple procedure W!Init;
  begin "init"

    T!Lines := 24;	! ** kludge ** ;

    ! start out with one clean window and one clean box ;

    arrclr( W!Buf, cvasc( "     " ));
    W!BClr;

    ! set up some fresh byte pointers ;

    W!WindowBP := point( 7, W!Buf[ 1, 1 ], -1 );
    W!StatusLine := T!Lines - 1;
    W!StatusBP := point( 7, W!Buf[ W!StatusLine, 1 ], -1 );
    W!MsgLine := T!Lines;
    W!MsgBP := point( 7, W!Buf[ W!MsgLine, 1 ], -1 );

    W!BoxP := false;
    W!SplitP := false;
    W!Num := 0;
    W!Size0 := T!Lines - 2;
    W!Size1 := 0;
    W!Bar := "+-------";

    set(W!IntP);

      ! Indicate to the world that the window has been initialized.
      ;
  end "init";
!! title Rtn W!SetB ;
! Routine used by the buffer module to tell us what buffer #
! corresponds to the current window. ;

internal simple procedure W!SetB( integer BufNum );
  begin

    if ( C!Debug ) then
      W!BAdd( "W!SetB(" & cvs(BufNum) & "), W!Num=" & cvs(W!Num) );

    if ( W!Num ) then
      W!Buf1 := BufNum
    else
      W!Buf0 := BufNum;

    W!BufNum := BufNum;

  end;
!! title Rtn AppendStr ;
! Routines to build and update the status line ;

simple procedure AppendStr( string S; reference integer BP, Len );
  while ( length( S )) do
    begin
      idpb( lop( S ), BP );
      Len := Len + 1;
    end;
!! title Rtn W!FixS ;
! This routine fixes the fixable part of a status line.  (Where we are,
!  if the buffer has been modified, if we have mail). ;

internal simple procedure W!FixS;
begin
  own boolean
    NewMailBit,
    OldMailBit;
  own integer
    Bp,
    Len;

  Bp  _ W!FixStatusBP;
  Len _ W!FixStatusLen;

  if (B!BegP and B!EndP) then
    AppendStr("Empty", Bp, Len)
  else if (B!BegP) then
    AppendStr("Top", Bp, Len)
  else if (B!EndP) then
    AppendStr("Bottom", Bp, Len)
  else
  begin
    AppendStr(cvs(B!Pcnt), Bp, Len);
    AppendStr("%", Bp, Len);
  end;

  AppendStr("-", Bp, Len);

  if (B!ModP) then
    AppendStr(" *", Bp, Len);

  ifcr TymcomX thenc
    begin!code
      setzm	NewMailBit;
      setzm	OldMailBit;

      hrroi	t1, '6;
      calli	t1, '41;
       setz	t1, ;

      trne	t1, '10000;		! jp.mal (old mail) ;
        setom	OldMailBit;
      tlne	t1, '001000;		! jp.mai (new mail) ;
      	setom	NewMailBit;
    end;

    if (OldMailBit or NewMailBit) then
      AppendStr(" Mail", Bp, Len);

    if (NewMailBit) then
      AppendStr("!", Bp, Len);

    if (OldMailBit) then
      AppendStr(".", Bp, Len);
  endc

  while (Len <= T!Width) do
    AppendStr(" ", Bp, Len);
end;
!! title Rtn W!NewS ;
! Routine to build the more permanent part of the status line.
;

internal simple procedure W!NewS;
begin
  own integer
    Bp,
    Len,
    L,
    R;

  Bp  _ W!StatusBP;
  Len _ 0;

  AppendStr(DED!Alias, Bp, Len);
  AppendStr(" " & DED!Major!Version, Bp, Len);
  AppendStr("." & DED!Minor!Version, Bp, Len);
  AppendStr(" (" & DED!Edit!Version & ")", Bp, Len);

  AppendStr(" (", Bp, Len);
  AppendStr(T!Name, Bp, Len);
  AppendStr(" ", Bp, Len);
  AppendStr(B!Mode, Bp, Len);

  if (G!WrapP) then
    AppendStr(",Wrap", Bp, Len);

  if (G!OverStrikeP) then
    AppendStr(",Over", Bp, Len);

  AppendStr(") ", Bp, Len);

  L _ B!Cnt;
  R _ rh(L);
  L _ lh(L);

  if (L) then
  begin
    integer
      I;

    for I _ 1 step 1 until L do
      AppendStr("<", Bp, Len);

    AppendStr(" ", Bp, Len);
  end;

  if (B!Alias) then
    AppendStr(B!Alias, Bp, Len)
  else
    AppendStr(B!File, Bp, Len);

  if (B!Lock) then
    AppendStr(" $", Bp, Len);

  if (R) then
  begin
    integer
      I;

    AppendStr(" ", Bp, Len);

    for I _ 1 step 1 until R do
      AppendStr(">", Bp, Len);
  end;

  AppendStr(" -", Bp, Len);

  ! the remainder of the status line is more readily changeable ;

  W!FixStatusBP _ Bp;
  W!FixStatusLen _ Len;
  W!FixS;
end;
!! title Rtn W!Msg ;
! If the Window Module has been initialized,
!   put something in the message line, and remember how long it is
! otherwise
!   print the message using a standard SAIL print statement.
;

internal simple procedure W!Msg(string S);
begin "W!Msg"
  own integer
    len,
    c,
    bp;

  if (W!IntP) then
  begin
    ! The Window Module has been initialized.
    ;

    W!FixS;

      ! While we're here, insure current status.
      ;

    for len _ 1 upto 27 do
      W!Buf[W!MsgLine, len] _ cvasc("     ");

    bp  _ W!MsgBP;
    len _ 0;

    while (length(S)) do
    begin
      c _ lop(s);

      if ((c >= " ") and (c < DEL)) then
        idpb(c, bp)
      else if (c = ESC) then
      begin
        if (G!ESCF) then
          idpb("$", bp)
        else
        begin
          idpb("^", bp);
          idpb("[", bp);
          incr(len);
        end;
      end
      else
      begin
        idpb("^", bp);

        if (c = NULL) then
          idpb("@", bp)
        else if (c = DEL) then
          idpb("#", bp)
        else
          idpb(c + '100, bp);

        incr(len);
      end;

      incr(len);

      if (len >= T!Width) then
        done;
    end;

    W!MsgLen _ len;
  end
  else
    ! The Window Module has not been initialized.
    ;

    print(S);
end "W!Msg";
!! title Rtn W!Msg2 ;
! Put something at the right margin of the message line.
! by convention, the string *must* be printable ascii ;

internal procedure W!Msg2( string S );
  begin
    integer BP, L, I;
    BP := W!MsgBP;
    L := length( S );
    for I := 1 upto ( T!Width - L - 1 ) do ibp( BP );
    while ( length( S )) do idpb( lop( S ), BP );
  end;

!! title Rtn W!GetS ;
! Return the start of the current window ;

internal simple integer procedure W!GetS;
begin
  return
  (
    if (W!Num = 1) then
      B!GetM(B!WS1)
    else
      B!GetM(B!WS0)
  );
end;
!! title Rtn W!SetS ;
! Set the start of the current window ;

internal simple procedure W!SetS (integer Position);
begin
  if (W!Num = 1) then
    B!SetM(B!WS1, Position)
  else
    B!SetM(B!WS0, Position);
end;
!! title Rtn W!Size ;
! Return the size of the current window ;

internal simple integer procedure W!Size;
  begin
    if ( W!Num = 1 ) then
      return( W!Size1 )
    else
      return( W!Size0 );
  end;
!! title Rtn ReFrame ;
! Reframe a window whose point has gone wandering off.  Try to put the
!  point in the middle of the current window, unless we run into the
!  beginning of the buffer, or a <FF>, first.  Allow for line wrap,
!  if set.  Unfortunately, TABs are very hard to allow for at this point.
!
! 10/27/82/DWS  CharCount is a foolish attempt to keep reframe from
!  failing (resulting in the 'W!BSet lost big' errors).
;

simple procedure ReFrame;
  begin "reframe"
    own integer SavePoint, C, CharCount, LineCount, SizeDiv2;

    SizeDiv2 := W!Size div 2;
    LineCount := 1;
    CharCount := 1;

    SavePoint := B!GetP;

    while ( LineCount < SizeDiv2 ) do
      begin "line"

        C := NULL;
	if ( B!BegP ) then done "line";

	B!Move( BACKWARDS );
	C := B!GetC;

	if ( C = FF ) then
	  done "line"
	else if ( C = LF ) then
	  begin
	    LineCount := LineCount + 1;
	    CharCount := 1;
	  end
	else
	  CharCount := CharCount + 1;

	if ( CharCount = T!Width ) then
	  begin
	    Linecount := Linecount + 1;
	    CharCount := 1;
	  end;

      end "line";

    if (( C = LF ) or ( C = FF )) then
      B!Move( FORWARDS )
    else if ( not B!BegP ) then
      B!Move( FORWARDS );

    W!SetS( B!GetP );
    B!SetP( SavePoint );

  end "reframe";
!! title Rtn BuildBar ;
! Put up the bar in the window ;

procedure BuildBar;
  begin
    integer P, C, W;  string Bar;

    P := point( 7, W!Buf[ ( W!Size0 + 1 ), 1 ], -1 );
    Bar := NULL;

    W := 0;
    while ( W < T!Width ) do
      begin
	if ( not length( Bar )) then
	  Bar := W!Bar;
	C := lop( Bar );
	if (( C < " " ) or ( C > '176 )) then C := "?";
	idpb( C, P );
	W := W + 1;
      end;

  end;
!! title Rtn W!BSet ;
! Routine to turn on/off the box ;

internal procedure W!BSet( boolean OnFlag );
  begin
    W!BoxP := OnFlag;
    if (( not OnFlag ) and W!SplitP ) then
      BuildBar;
  end;
!! title Rtn W!BClr ;
! Routine to rebuild the box ;

simple internal procedure W!BClr;
  begin "clear the box"
    integer I;

    arrclr( W!Box, cvasc( "     " ));

    for I := 2 upto 11 do
      W!Box[1,I] := W!Box[20,I] := cvasc( "-----" );

    for I := 2 upto 19 do
      begin
	W!Box[I,1] := cvasc( "!    " );
	W!Box[I,12] := cvasc( "    !" );
      end;

    W!Box[1,1] := W!Box[20,1] := cvasc( "+----" );
    W!Box[1,12] := W!Box[20,12] := cvasc( "----+" );

    W!BoxPnt := 1;

  end "clear the box";
!! title Rtn W!BMsg ;
! Routine to put text into the box.  Note that to the outside
! world the inside of the box is 18x56, not 20x60. ;

simple internal procedure W!BMsg( integer Line; string Text );
  begin "put a message into the box"
    integer Width, C, BP;

    if (( Line < 1 ) or ( Line > 18 )) then
      usererr( 0,0,"Bad call to W!BMsg, Line="&cvs(Line),"x" );

    BP := point( 7, W!Box[ 1 + Line, 1 ], -1 );
    ibp( BP ); ibp( BP );

    Width := 1;
    while (( Width <= 56 ) and length( Text )) do
      begin
	C := lop( Text );
	if (( C < " " ) or ( C > '176 )) then C := "?";	! ** hack ** ;
	idpb( C, BP );
	Width := Width + 1;
      end;

    W!BoxPnt := Line;

  end "put a message into the box";
!! title Rtn W!BAdd ;
! Routine to "append" text to the bottom of the box, scrolling
! other text up. ;

internal simple procedure W!BAdd( string Text );
  begin
    own integer I, J;

    W!BoxPnt := W!BoxPnt + 1;
    if ( W!BoxPnt < 19 ) then
      W!BMsg( W!BoxPnt, Text )
    else
      begin
	for I := 2 upto 18 do
	  for J := 1 upto 12 do
	    W!Box[ I, J ] := W!Box[ 1 + I, J ];
	for J := 2 upto 11 do W!Box[ 19, J ] := cvasc( "     " );
	W!Box[ 19, 1 ] := cvasc( "!    " );
	W!Box[ 19,12 ] := cvasc( "    !" );
	W!BMsg( 18, Text );
      end;
  end;

!! title Rtn W!Fill ;
! Rebuild the pertinent part of the window.  Return true iff we've seen
! the point while rebuilding.
;

simple boolean procedure W!Fill (integer WindowToFill);
begin "W!Fill"
  own boolean
    PointSeen;
  own integer
    C,
    Column,
    CurrentLine,
    I,
    J,
    Line!BP,
    LinesToFill,
    SavePoint;

  PointSeen _ false;
  SavePoint _ B!GetP;

  if (WindowToFill = 0) then
  begin
    B!SetP(B!GetM(B!WS0));
    CurrentLine _ 1;
    LinesToFill _ W!Size0;
  end
  else
  begin
    B!SetP(B!GetM(B!WS1));
    CurrentLine _ W!Size0 + 2;
    LinesToFill _ W!Size1;
  end;

  for I _ CurrentLine upto (CurrentLine + LinesToFill - 1) do
    for J _ 1 upto 27 do
      W!Buf[I, J] _ cvasc("     ");

  Line!BP _ point(7, W!Buf[CurrentLine, 1], -1);
  Column  _ 1;

  while (true) do
  begin "fill a window"
    if (Column >= T!Width) then
    begin "wrap"
      boolean
        AlreadyAtEol;

      clear(AlreadyAtEol);

      if ((Column = T!Width) and (B!GetC neq CR)) then
	idpb("|", Line!BP)
      else
      begin
	if ((B!GetP = SavePoint) and (WindowToFill = W!Num)) then
	be	  PointSeen         _ true;
	  SavedCursorRow    _ CurrentLine;
	  SavedCursorColumn _ Column;
	end;
	B!Move(FORWARDS);
	if (B!GetC = LF) then
	  B!Move(FORWARDS)
	else if (G!CRCtl) then
	  idpb("^", Line!BP)
	else
	  idpb("&", Line!BP);

        set(AlreadyAtEol);
      end;	

      if (not G!WrapP and not AlreadyAtEol) then
	while (true) do
	begin "move to EOL";
	  if ((B!GetP = SavePoint) and (WindowToFill = W!Num)) then
	  begin
	    PointSeen         _ true;
	    SavedCursorRow    _ CurrentLine;
	    SavedCursorColumn _ Column;
	  end;

	  if (B!EndP) then
	    done "fill a window";

	  C _ B!GetC;
	  B!Move(FORWARDS);

	  if (C = FF) then
	    done "fill a window";

	  if (C = CR) then
	  begin
	    if (B!GetC = LF) then
	      B!Move(FORWARDS);

	    done "move to EOL";
	  end;
	end "move to EOL";

      decr(LinesToFill);

      if (not LinesToFill) then
	done "fill a window";

      incr(CurrentLine);
      Line!BP _ point(7, W!Buf[CurrentLine, 1], -1);
      Column  _ 1;
    end "wrap" ;

    if (B!GetP = SavePoint and WindowToFill = W!Num) then
    begin
       PointSeen         _ true;
       SavedCursorRow    _ CurrentLine;
       SavedCursorColumn _ Column;
    end;

    if (B!EndP) then
      done "fill a window";

    C _ B!GetC;
    B!Move(FORWARDS);

    if (C = CR) then
    begin "CR"
      if (B!GetC = LF) then
      begin "CRLF"
	if (B!GetP = SavePoint) then
	begin
	   PointSeen         _ true;
	   SavedCursorRow    _ CurrentLine;
	   SavedCursorColumn _ Column;
	end;

	B!Move(FORWARDS);
      end "CRLF"
      else
      begin
	if (G!CRCtl) then
	begin
	  idpb("^", Line!BP);
	  idpb("M", Line!BP);
	end
	else
	begin "overstrike"
	  while (Column < T!Width) do
	  begin
	    ibp(Line!BP);
	    incr(Column);
	  end;

	  idpb("&", Line!BP);
	end "overstrike";
      end;

      decr(LinesToFill);

      if (not LinesToFill) then
	done "fill a window";

      incr(CurrentLine);
      Line!BP _ point(7, W!Buf[CurrentLine, 1], -1);
      Column  _ 1;
    end "CR"
    else if (C = FF) then
    begin "FF"
      idpb("^", Line!BP);  idpb("L", Line!BP);

      if (G!FFBreak) then
	done "fill a window";

      decr(LinesToFill);

      if (not LinesToFill) then
	done "fill a window";

      incr(CurrentLine);
      Line!BP _ point(7, W!Buf[CurrentLine, 1], -1);
      Column  _ 1;
    end "FF"
    else if ((C = TAB) and (C!TABWidth)) then
    begin "tab"
      while (Column mod C!TABWidth) do
      begin
	ibp(Line!BP);
	incr(Column);
      end;

      ibp(Line!BP);
      incr(Column);
    end "tab"
    else if ((C = ESC) and (G!ESCF)) then
    begin "ESC"
      idpb("$", Line!BP);
      incr(Column);
    end "ESC"
    else if ((C < " ") or (C = '177)) then
    begin "control character"
      if (Column = (T!Width - 1)) then
      begin
	idpb("|", Line!BP);
	incr(Column);
	B!Move(BACKWARDS);
      end
      else
      begin
	idpb("^", Line!BP);

	if (C = 0) then
	  idpb("@", Line!BP)
	else if (C = '177) then
	  idpb("#", Line!BP)
	else
	  idpb(C + '100, Line!BP);

	incr(Column, 2);
      end;
    end "control character"
    else
    begin "normal character"
      idpb(C, Line!BP);
      incr(Column);
    end "normal character";
  end "fill a window" ;

  B!SetP(SavePoint);
  return(PointSeen);
end "W!Fill";
!! title Rtn W!Frame ;
! check the framing for the current window ;

internal simple procedure W!Frame;
  begin
    if ( not W!Fill( W!Num ) ) then
      ReFrame;
  end;
!! title Rtn W!Set ;
! Set things up for the redisplay. ;

internal simple procedure W!Set;
  begin "set up the windows"
    own integer I, J;

    if (( W!Buf0 = W!BufNum ) and ( not W!Fill( 0 )) and ( W!Num = 0 )) then
      begin
	W!Frame;
	W!FixS;
	if ( not W!Fill( 0 ) ) then
	  usererr( 0,0, "W!Set lost big!" );
      end;

    if ( W!SplitP and
	( W!Buf1 = W!BufNum ) and ( not W!Fill( 1 )) and ( W!Num = 1 )) then
      begin
	W!Frame;
	W!FixS;
	if ( not W!Fill( 1 ) ) then
	  usererr( 0,0, "W!Set lost big!" );
      end;

    ! overlay the help box, if W!BoxP is true ;

    if ( W!BoxP ) then
      begin "box"
	for I := 1 upto 20 do
	  for J := 1 upto 12 do
	    W!Buf[ 1 + I, 2 + J ] := W!Box[ I, J ];
      end "box";

  end "set up the windows";
!! title Rtn W!2 ;
! Split the screen into two windows ;

internal procedure W!2;
  begin

    if ( W!SplitP ) then
      T!Bell
    else
      begin
	W!SplitP := true;
	W!Num := 1;		! just for kicks, put us in the bottom ;
	W!Buf1 := W!Buf0;
	W!Size1 := W!Size0 div 2;
	W!Size0 := W!Size0 - W!Size1 - 1;
        B!SetM(B!WS1, B!GetM(B!WS0));
	BuildBar;
	B!SetL( B!GetP );
	if ( C!Debug ) then
	  W!BAdd( "[split screen]" );
      end;
  end;
!! title Rtn W!1 ;
! Remove the "other" window of a split screen ;

internal procedure W!1;
begin
  integer
    T;

  if (not W!SplitP) then
    T!Bell
  else
  begin
    W!SplitP _ false;

    if (W!Num = 1) then 
      if (W!Buf0 = W!Buf1) then
        B!SetM(B!WS0, B!GetM(B!WS1))
      else
	W!Buf0 _ W!Buf1;

    B!SetM(B!WS1, -2);                  ! Window 1 is now undefined ;
    W!Buf1 _ 0;

    W!Num _ 0;
    W!Size0 _ W!Size0 + W!Size1 + 1;
    W!Size1 _ 0;
  end;
end;
!! title Rtn W!Grow ;
! Routine to grow a window ;

internal simple procedure W!Grow;
  begin
    if ( not W!SplitP ) then
      T!Bell
    else if ( W!Num ) then
      begin
	if ( W!Size0 > 2 ) then
	  begin
	    W!Size0 := W!Size0 - 1;
	    W!Size1 := W!Size1 + 1;
	    BuildBar;
	  end
	else
	  T!Bell;
      end
    else
      begin
	if ( W!Size1 > 2 ) then
	  begin
	    W!Size1 := W!Size1 - 1;
	    W!Size0 := W!Size0 + 1;
	    BuildBar;
	  end
	else
	  T!Bell;
      end;
  end;
!! title Rtn W!Switch ;
! Routine to switch between windows. ;

internal procedure W!Switch;
begin
  integer
    T;

  if (not W!SplitP) then
    W!Msg("Window is not split.")
  else
  begin
    if (W!Buf0 = W!Buf1) then
    begin
      T _ B!GetP;
      B!SetP(B!GetL);
      B!SetL(T);
      W!Num _ 1 - W!Num;
      W!NewS;
    end
    else
    begin
      W!Num _ 1 - W!Num;

      if (W!Num = 0) then
	B!SetB(W!Buf0)
      else
	B!SetB(W!Buf1);

      W!NewS;
    end;
  end;
end;
!! title Rtn W!Disp ;
! The editor interfaces to the redisplay through W!Disp.  It is
! responsible for building the correct image to present to the
! redisplay.
;

  internal procedure W!Disp (boolean PuntFlag);
  begin "W!Disp"
    ;
    if (not C!ChrP) then
    begin
      ! If there is not a character waiting for input from some source.
      ;

      W!Set;

      if (W!MsgF) then
        R!Disp(W!Buf, PuntFlag, T!Lines, W!MsgLen + 1)
      else
        R!Disp(W!Buf, PuntFlag, SavedCursorRow, SavedCursorColumn);
      
    end;
  end "W!Disp";
end "DED - window manager" ;


! **************************  End Window.Sai  ****************************;
 ^@fê