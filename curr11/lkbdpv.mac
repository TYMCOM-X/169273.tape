
	.TITLE	LKBDPV.MAC - Interrupt module for DPV-11
	.SBTTL	Introduction
	XLPAR			;Don't list parameter file
	CODE

;	Version 1.0

;	This module processes the interrupts from the DPV-11
; interface and passes the characters to LNKBPS the link type B
; protocol service.

;	Dennis Ernst  3-Septermber-1981


; DPV control bits

$BYTS8=!0		;DPV 8 bit bytes
$DTR  =!$BIT01		;Data terminal ready
$RQSND=!$BIT02		;Request to send
$RXENB=!$BIT06		;Receiver interrupt enable
$SEND =!$BIT04		;Transmitter send bit
$SHSYN=!$BIT04		;Receiver search synch
$STRIP=!$BIT13		;Receiver strip synch
$TXENB=!$BIT06		;Transmitter interrupt enable
$REST =!$BIT00		;Master reset the interface
$TXENA=!$BIT04		;DPV transmitter enable
$RCVEN=!$BIT04		;DPV receiver enable
$TSOM =!$BIT08		;DPV transmit start of message
$TEOM =!$BIT09		;DPV trasmit end of message
$RCRCZ=!$BIT12		;CRC zero bit
$RXERR=!$BIT11		;DPV Receiver underrun error
$CRC16=!$BIT09!$BIT08	;DPV CRC-16 mode
$BYTEM=!$BIT14		;DPV byte mode

$RXINI=!$RXENB!$RCVEN!$RQSND!$DTR
	.PAGE
	.SBTTL	LKBINI - Initialize service

;	C(R4) = Address of DDB to initialize
;	CALL	LKBINI

LKBINI::MOV	#DDLBCZ#,R1		;NUMBER OF WORDS TO CLEAR
	MOV	#DDLBFC#,R2		;Where to start
	ADD	R4,R2			;Make into address
LKBIN2:	CLR	(R2)+
	SOB	R1,LKBIN2

; Reset all CB pointers

	MOV	DDCIMB#(R4),DDCIMI#(R4)
	MOV	DDCIMB#(R4),DDCIMO#(R4)
	MOV	DDCMOB#(R4),DDCMOI#(R4)
	MOV	DDCMOB#(R4),DDCMOO#(R4)
	TST	@DDLBRS#(R4)		;Is there hardware present
	NXMJMP	NOLINK#			;If not do common exit
	BIS	#$ISTRT,DDFLAG#(R4)	;Set for initial start
	MOVB	#TIMVAL,DDTIMR#(R4)	;Start the rep timer
	MOVB	#THRSH,DDREPT#(R4)	;Start down timer
	MOV	DDCMOB#(R4),R2		;Get the start of the buffer
	MOV	#STRTYP,(R2)+		;Start message
	CLR	(R2)+
	MOV	R2,DDCMOI#(R4)		;Set up the in pointer
	MOV	#$D1DWN,DDSTS1#(R4)	;Link is down
	MOVB	#SNDCNT,DDSCNT#(R4)	;Our send ahead count
	CLRB	DDLBLS#(R4)		;Clear the load state
	MOVB	#1,DDLBSN#(R4)		;Clear our counter
	CALL	LKBRST			;Set up the hardware
LKBINS::RETURN
	.PAGE
	.SBTTL	LKBRST - Link reset routine

;	C(R4) = Address of the DDB
;	CALL	LKBRST#

LKBRST:: MOV	#$REST,@DDLBTS#(R4)	;Reset the interface
	MOVB	#LCOUNT,DDLBSS#(R4)	;Set for long synch
	MOV	#$BYTEM!$CRC16!$STRIP!SYN,@DDLBPR#(R4) ;Set up for DDCMP internal sync
	MOV	#$RXINI,@DDLBRS#(R4)	;Set up the receiver
	MOV	#$TXENB!$TXENA!$BYTS8,@DDLBTS#(R4)
	MOV	#$TSOM!SYN,@DDLBTB#(R4)	;SEND SYNCHS
	BIS	#$OIP,DDFLAG#(R4)	;When we reset the device it will
					; interrupt us
	RETURN
	.PAGE
	.SBTTL	LKBISV - Link B Input Service


;	CALL	LKBISV
;The link B input service
;	C(R4) - The DDB pointer for the DPV-11
;	The old R4 is on the stack

LKBISV::PUSH	R0			;Save the users R0
	MOV	@DDLBRB#(R4),R0		;Get the character from the interface
	BIT	#$RXERR,R0		;Test for receiver error
	BEQ	1$
	JMP	ERROR

1$:	PUSH	R1
	PUSH	R2
	PUSH	R3
;	MOV	#$BYTEM!$CRC16!SYN,@DDLBPR#(R4) ; Clear strip synch for input
	TBBN	$LOAD,DDFLAG#(R4),2$	;Just do protocol service stuff
	JMP	LKBPST

2$:	MOVB	DDLBLS#(R4),R2		;Get load state
	ASL	R2
	JMP	@LDISP(R2)		;Get the state

	PURE
LDISP:	.WORD	LKBTY1
	.WORD	LKBTY2
	.WORD	LKBCK1
	.WORD	LKBCK2
	CODE

LKBTY1:	MOVB	R0,DDLBT1#(R4)	;
	INCB	DDLBLS#(R4)
	BR	LEXIT

LKBTY2:	MOVB	R0,DDLBT1#+1(R4)
	INCB	DDLBLS#(R4)
	BR	LEXIT

LKBCK1:	MOVB	R0,DDLBC1#(R4)
	INCB	DDLBLS#(R4)
LEXIT:	POP	R3
	POP	R2
	POP	R1
	POP	R0
	POP	R4
	RTI


LKBCK2:	MOVB	R0,DDLBC1#+1(R4)
	CLRB	DDLBLS#(R4)		;CLEAR THE LOAD STATE
	CMP	DDLBT1#(R4),DDLBC1#(R4)	; Check the check sum
	BNE	ENQOT
	MOV	DDLBT1#(R4),R0		;GET THE RESPONSE
	BIC	#10400,R0		;MASK OFF THE SEQ AND THE ENQ
					; BIT
	CMP	#140307,R0		;A NAK?
	BEQ	LKBAGN			;RE TRANSMIT IT
	CMP	#100307,R0		;A ACK?
	BNE	ENQOT			;DO AN ENQUIRE
	TBBE	$SEQ,DDFLAG#(R4),LKBCK3

;HERE IF SET

;	CMP	#140307,R0		;A NAK?
;	BEQ	LKBAGN			;RE TRANSMIT IT
;	CMP	#100307,R0		;A ACK?
;	BNE	ENQOT			;DO AN ENQUIRE
	TBBE	400,DDLBT1#(R4),LKBAGN	;IF WRONG FLAVOR SEND AGAIN
	BIC	#$SEQ,DDFLAG#(R4)	;TOGGLE SEQ NO
LKBCMN:	MOV	DDLBPO#(R4),R1		;Get the packet acked
	CMP	R1,#255.		;In a reasonable range?
	BLO	SPCLCK			;DO SPECIAL CHECK
	MOV	PKDATA+2(R1),R0
	FREPKT				;Give it up
LKBCM0:	MOV	DDCIMI#(R4),R2		;Pick up the in pointer
	MOV	R2,R3			;Save a copy of the pointer
	ADD	#IMSIZ,R2		;Make a pointer to the end of the frame
	CMP	R2,DDCIME#(R4)		;Check for the end of the ring
	BNE	10$			;Not at the end
	MOV	DDCIMB#(R4),R2		;Point back to the top
10$:	CMP	DDCIMO#(R4),R2		;Is the ring full?
	BNE	20$			;No
	RSTOP	MRF			;[MRF - Main Ring Full]

20$:	MOV	#LODTYP,(R3)+		;Tell main we need more load data
	CLR	(R3)			;Next word in the ring not used
	MOV	R2,DDCIMI#(R4)		;We are done
	CMP	#-1,R0
	BNE	30$			;Not a reset
	BIC	#$SEQ,DDFLAG#(R4)	;Clear sequence bit on reset
30$:	CLR	DDLBPO#(R4)		;Make room for the next
	MOVB	#LODSYN,DDLBSS#(R4)	;Give us some margin
LKBCM1:	BIC	#$ELOAD+$WAKE2+$ENQWT+$ENQIR,DDFLAG#(R4)	;Don't bother us
	BR	LKBZAP

LKBCK3:;	CMP	#140307,R0		;A NAK?
;	BEQ	LKBAGN
;	CMP	#100307,R0		;AN ACK?
;	BNE	ENQOT			;GARBAGE?
	TBBN	400,DDLBT1#(R4),LKBAGN
	BIS	#$SEQ,DDFLAG#(R4)
	BR	LKBCMN

SPCLCK:	CMP	#1,R1			;A RESTART MESSAGE?
	BNE	SPCLC2
SPCLC1:	CLR	DDLBPO#(R4)
	BIC	#$SEQ+$WAKE2+$ELOAD+$ENQWT+$ENQIR,DDFLAG#(R4)
SPCLC2:	BR	LKBCM0			;Do common exit

;Here if we got a nak from the bootstrap

LKBAGN:	MOVB	#1,DDSNAK#(R4)		;Set nak so we will do output
	MOVB	#50,DDLBSS#(R4)		;Margin for retry
	BR	LKBCM1


ENQOT:	MOVB	#50,DDLBSS#(R4)		;Give us a chance to synch with him
	BIS	#$ENQIR+$ENQWT,DDFLAG#(R4)
	BIC	#$ELOAD,DDFLAG#(R4)
	MOVB	#1,DDSNAK#(R4)
	BR	LKBZAP

LKBPST:	BIS	DDICRC#(R4),R0		;Put CRC flag from two bytes ago
	TST	R0			;Test state of CRC bit
	BPL	1$			;Bit is not set
	BIS	#<$RCRCZ*4>,DDICRC#(R4)	;Set CRC is zero in right place
1$:	ROR	DDICRC#(R4)		;Move CRC flag right, shift in zero
	CLRB	DDICRC#(R4)		;Keep status bits from poluting data
	CALL	LNKBPS#			;Call link protocol

	TBBE	$RSYNC,DDFLAG#(R4),LKBIS1	;If no need for synch leave
LKBZAP:	BIC	#$SHSYN,@DDLBRS#(R4)		;Clear the synch
	BIS	#$RXINI,@DDLBRS#(R4)		;Search for synch
;	MOV	#$BYTEM!$CRC16!$STRIP!SYN,@DDLBPR#(R4) ;Set strip synch just in case
	BIC	#$RSYNC+$EOM,DDFLAG#(R4)	;CLEAR THE SYNCH FLAG
	CLRB	DDSTAT#(R4)			;Clear the protocol state
	BR	EXIT

LKBIS1:	TBBE	$EOM,DDFLAG#(R4),EXIT	;If end of message then clean up
	CLRB	DDSTAT#(R4)		;Clear input state
;	MOV	#$BYTEM!$CRC16!$STRIP!SYN,@DDLBPR#(R4) ;Set strip synch just in case
	BIC	#$EOM,DDFLAG#(R4)	;Clear our flag
EXIT:	POP	R3
	POP	R2
	POP	R1
EXIT1:	POP	R0
	TSTB	DDSNAK#(R4)
	BNE	ERROR1
	POP	R4
	RTI

ERROR:	TBBN	$ISTRT+$ASTRT,DDFLAG#(R4),EXIT1 ;If either is set skip error
	MOVB	#9.,DDSNAK#(R4)		;Set receiver overrun
	INC	DDERRO#(R4)		;Count the receive overrun
	POP	R0
	CLRB	DDSTAT#(R4)		;Clear the protocol state
	BIC	#$SHSYN,@DDLBRS#(R4)	;Clear search synch
	BIS	#$RXINI,@DDLBRS#(R4)	;And set it
;	MOV	#$BYTEM!$CRC16!$STRIP!SYN,@DDLBPR#(R4) ;Set strip synch just in case
ERROR1:	TBBN	$OIP,DDFLAG#(R4),ERREXT	;If going then leave
	BR	LKBOSV			;Branch into output

ERREXT:	POP	R4
	RTI
	.PAGE
	.SBTTL	LKBOSV - Link B output service

LKBOSV::PUSH	R0
	PUSH	R1
	PUSH	R2
;	PUSH	R3
	BIC	#$IRMA,DDFLAG#(R4)	;Clear lost interrupt flag
	TST	@DDLBTB#(R4)		;Check for under run
	BPL	10$			;Nope
	TBBE	$OIP,DDFLAG#(R4),10$	;If not active don't count under run
	INC	DDERTU#(R4)		;Count the under run
10$:	MOVB	DDOST#(R4),R0		;Get the output state
	ASL	R0			;Make into word index
	JMP	@DISPAT(R0)		;Jump off the dispatch table

	PURE

DISPAT:	STHDR				;Start header building
	HDCONT				;Rest of header output
	DATOUT				;Data output
	.PAGE
	.SBTTL	STHDR - Start of header output

	CODE

STHDR:	BIS	#$OIP,DDFLAG#(R4)	;Mark output active
	TSTB	DDLBSS#(R4)		;Check the synch count
	BEQ	STHOK			;If zero node to do
	MOV	#SYN,@DDLBTB#(R4)	;Send a SYN
	DECB	DDLBSS#(R4)		;One down
	JMP	DATEND			;Exit

STHOK:	TBBE	$LOAD,DDFLAG#(R4),STHOKN ;If not load mode go on
	TBBE	$ELOAD,DDFLAG#(R4),1$
	JMP	ACKQT

1$:	CLRB	DDSNAK#(R4)
	TBBE	$ENQIR,DDFLAG#(R4),NOPE	;Check for enq requested
	MOV	DDLBPO#(R4),R0		;Get the pointer to next packet
	CMP	#1,R0			;Check for special cases
	BEQ	2$			;Load start message
	CMP	#2,R0
	BEQ	3$			;Load end message
	CMP	#-1,2(R0)
	BNE	3$
2$:	BIC	#$ENQIR,DDFLAG#(R4)
	BR	NOPE

3$:	MOV	#6,DDCNTO#(R4)
	MOV	#ENQMS,DDPKTO#(R4)
	BR	STLDM1			;Set state and start output

NOPE:	MOV	DDLBPO#(R4),R0		;Get the packet to send
	BNE	2$
	JMP	ACKQT
2$:	CMP	#1,R0			;A start load mode message?
	BEQ	STLDM			;Yes! go do it
	CMP	#2,R0			;An end load mode message?
	BEQ	ENDLDM			;Yes, say bye bye
	MOV	R0,DDPKTO#(R4)		;Save the packet pointer
	MOVB	PKCNT(R0),DDCNTO#(R4)	;And the count
	ADD	#PKDATA,DDPKTO#(R4) 	;Plus the header
	MOVB	#6,DDLBSS#(R4)		;Give boot time to think
	BR	STLDM1			;Go send it

STLDM:	MOV	#6,DDCNTO#(R4)
	MOV	#RSTMS,DDPKTO#(R4)
	MOVB	#50,DDLBSS#(R4)		;Give the boot time to reset
STLDM1:	MOVB	#2,DDOST#(R4)
	MOV	#$TXENB!$TXENA!$BYTS8,@DDLBTS#(R4) ;Enable interrupts
	JMP	DATOUT

ENDLDM:	BIC	#$WAKE2+$ENQIR+$LOAD+$ELOAD+$SEQ,DDFLAG#(R4) ;Make
					; all happy
	CLR	DDLBPO#(R4)		;Clear message
	JMP	ACKQT			;Dismiss it all

STHOKN:	TSTB	DDSNAK#(R4)		;Is a NAK pending?
	BNE	NAKOT			;If so do a NAK output
	TSTB	DDSREP#(R4)		;Any reps pending?
	BEQ	STHOK1			;No
	JMP	REPOT


; Here to check for messages from the main program level
; The messages are two words.
; The first is the message type
; The second is the packet address of the packet to send
; If the message has no packets then the field is zero

STHOK1:	CMP	DDCMOI#(R4),DDCMOO#(R4) ;A message pending?
	BEQ	STHDR1			;Check for data
	MOV	DDCMOO#(R4),R2		;Get the pointer
	MOV	(R2)+,R0		;Get the type
;	MOV	(R2),R3			;And the packet address if one is there
	CLR	(R2)			;Just in case we attempt to use this
	MOV	DDCMOO#(R4),R2		;Reset the circular buffer pointers
	ADD	#MOSIZ,R2
	CMP	R2,DDCMOE#(R4)
	BNE	5$
	MOV	DDCMOB#(R4),R2
5$:	MOV	R2,DDCMOO#(R4)
	ASL	R0			;Make into a word index
	JMP	@DISPT2(R0)		;Do the routine service

	PURE
DISPT2:	.WORD	DIEB
	.WORD	ACKOT
	.WORD	DIEB
	.WORD	DIEB
	.WORD	DIEB
	.WORD	DIEB
	.WORD	STRTMS			;The start messsge routine
	.WORD	STCKMS			;The start ack routine

	CODE
STHDR1:	MOV	DDLBOH#(R4),R1		;Get the packet pointer
	BNE	10$			;
	JMP	ACKCK			;No data check for an ack

10$:	MOV	R1,DDPKTO#(R4)		;The packet to use
	MOV	(R1),DDLBOH#(R4)	;Point to the next
	ADD	#2,DDPKTO#(R4)		;Point past the link word
	MOVB	PKCNT(R1),DDCNTO#(R4)	;The character count
	TBBE	$ASTRT+$ISTRT,DDFLAG#(R4),1$ ; Go on if ok
	BR	ACKQT			;Bye

1$:	ADD	#<PKDATA-2>,DDCNTO#(R4)	;Plus the header size
	MOV	DDSTHO#(R4),R0		;The start of the header
	MOV	R0,DDCTHO#(R4)		;The current header pointer
	MOVB	#SOH,(R0)+		;DDCMP start of header(data)
	MOVB	DDCNTO#(R4),(R0)+	;And the character count
;	TST	DDLBOH#(R4)		;Check if more data
;	BEQ	STHDR2		;if not, set quick synch flag
;	CLRB	(R0)+			;More waiting clear Quick synch
;	BR	STHDR3			;
STHDR2:	MOVB	#$QSYNC,(R0)+		;Set the quick synch flag
	MOVB	#QCOUNT,DDLBSS#(R4)	;Set the synch count
STHDR3:	MOVB	DDRESP#(R4),(R0)+	;Tell him whats ACK'ed
	MOVB	PKSEQ(R1),(R0)+		;The message number
	MOVB	#1,(R0)+		;And the station number
	CLRB	DDSACK#(R4)		;Clear the ACK flag
;	INCB	DDOST#(R4)		;Next state
	MOVB	#TIMVAL,DDTIMR#(R4)	;Start rep timer
	BIS	#$OIP,DDFLAG#(R4)	;
	JMP	HDOUT			;Start the header out.
	.PAGE
	.SBTTL	DIEB - Invalid interrupt level state

DIEB:	FSTOP	IIS			;[Invalid Interrupt State]

	.SBTTL	NAKOT	- Nak out

NAKOT:	MOV	DDSTHO#(R4),R0		;Start of header
	MOV	R0,DDCTHO#(R4)		;Set up the current pointer
	MOVB	#ENQ,(R0)+		;Enquire message
	MOVB	#NAKTYP,(R0)+		;NAK
	MOVB	DDSNAK#(R4),(R0)	;The NAK reason
;	TST	DDLBOH#(R4)		;Check if more data
;	BEQ	NAKOT1			;If so set quick synch flag
;	INC	R0			;Point to the next
;	BR	NAKOT2
NAKOT1:	BISB	#$QSYNC,(R0)+		;Set the quick synch flag
	MOVB	#QCOUNT,DDLBSS#(R4)	;Set up the synch count
NAKOT2:	MOVB	DDRESP#(R4),(R0)+	;Send the ACK off
	CLRB	(R0)+			;Filler field
	MOVB	#1,(R0)			;Our station number
	CLRB	DDSACK#(R4)		;Clear the ack flag
	CLRB	DDSNAK#(R4)		;Clear the NAK flag
;	INCB	DDOST#(R4)		;Next state
	MOVB	#TIMVAL,DDTIMR#(R4)	;Start the rep timer
	JMP	HDOUT			;Go do it.
	.PAGE
	.SBTTL	ACKCK	- The ack check and output

ACKCK:	TSTB	DDSACK#(R4)		;Need to send an ACK?
	BNE	ACKOT
ACKQT:	TBBN	$IDL,DDFLAG#(R4),10$	;If set we are now going idle
	BIS	#$IDL,DDFLAG#(R4)	;We are going idle
	MOV	#SYN,@DDLBTB#(R4)	;Send synchs
	MOV	#$TXENA!$BYTS8,@DDLBTS#(R4) ;Disable transmitter interrupts
	BR	ACKOUT

10$:	BIC	#$IDL!$OIP,DDFLAG#(R4)	;Clear output in progress and idle
	BR	ACKOUT			;Exit

ACKOT:	MOV	DDSTHO#(R4),R0		;Get the start of the header
	MOV	R0,DDCTHO#(R4)		;The current header pointer
	MOVB	#ENQ,(R0)+		;Enquire
	MOVB	#ACKTYP,(R0)+		;An ACK
;	TST	DDLBOH#(R4)		;Check if more data
;	BEQ	ACKCK1			;If so then set quick synch
;	CLRB	(R0)+			;No flags
;	BR	ACKCK2
ACKCK1:	MOVB	#$QSYNC,(R0)+		;Set the quick synch flag
	MOVB	#QCOUNT,DDLBSS#(R4)	;Set up the synch count
ACKCK2:	MOVB	DDRESP#(R4),(R0)+	;That which we ACK
	CLRB	(R0)+			;Filler field
	MOVB	#1,(R0)			;Our station number
	CLRB	DDSACK#(R4)		;Clear the ack flag
;	INCB	DDOST#(R4)		;The next state
	BR	HDOUT

ACKOUT:	CLRB	DDOST#(R4)	;Set to start header
;	POP	R3
	POP	R2
	POP	R1
	POP	R0
	POP	R4
	RTI
	.PAGE
	.SBTTL	REPOT	- Repeat output service

REPOT:	MOV	DDSTHO#(R4),R0		;Get the start of the header
	MOV	R0,DDCTHO#(R4)		;Set up the current header pointer
	MOVB	#ENQ,(R0)+		;ENQUIRE
	MOVB	#REPTYP,(R0)+		;Rep
;	TST	DDLBOH#(R4)		;Check if more data
;	BEQ	REPO1
;	CLRB	(R0)+
;	BR	REPO2

REPO1:	MOVB	#$QSYNC,(R0)+		;Tell him no messages
	MOVB	#QCOUNT,DDLBSS#(R4)	;Send the standard synchs
REPO2:	CLRB	(R0)+			;Filler field
	MOVB	DDLBSN#(R4),(R0)	;Send the number of our last message
	DECB	(R0)+			;Adjust the rep number
	MOVB	#1,(R0)			;Station no.
;	CLRB	DDSACK#(R4)		;We have told him now
;	INCB	DDOST#(R4)		;Next state
	MOVB	#TIMVAL,DDTIMR#(R4)	;Start the rep timer
	CLRB	DDSREP#(R4)		;We have sent it
	BR	HDOUT
	.PAGE
	.SBTTL	STRTMS	- Start message

STRTMS:	MOV	DDSTHO#(R4),R0	;Get start of header
	MOV	R0,DDCTHO#(R4)		;Set up current pointer
	MOVB	#ENQ,(R0)+
	MOVB	#STRTYP,(R0)+
	MOVB	#$SELEC+$QSYNC,(R0)+
	CLRB	(R0)+
	CLRB	(R0)+
	MOVB	#1,(R0)
;	INCB	DDOST#(R4)
	MOVB	#QCOUNT,DDLBSS#(R4)	;The synch count
	BIS	#$ISTRT,DDFLAG#(R4)	;We are waiting for a stack
	BIC	#$ASTRT,DDFLAG#(R4)	;Clear the ASTART flag
	MOVB	#TIMVAL,DDTIMR#(R4)	;Start the rep timer
	MOV	DDCMOB#(R4),DDCMOI#(R4)	;Clear output ring
	MOV	DDCMOB#(R4),DDCMOO#(R4)	;
	BR	HDOUT
	.PAGE
	.SBTTL	STCKMS	- Start acknowledge message

STCKMS:	MOV	DDSTHO#(R4),R0		;Get start of header
	MOV	R0,DDCTHO#(R4)		;Set up current pointer
	MOVB	#ENQ,(R0)+
	MOVB	#STCKTY,(R0)+
	MOVB	#$SELEC+$QSYNC,(R0)+
	CLRB	(R0)+
	CLRB	(R0)+
	MOVB	#1,(R0)
;	INCB	DDOST#(R4)
	MOVB	#QCOUNT,DDLBSS#(R4)	;The synch count
	BIS	#$ASTRT,DDFLAG#(R4)	;We are waiting for an ack
	BIC	#$ISTRT,DDFLAG#(R4)	;Clear the ISTRT FLAG
	MOVB	#TIMVAL,DDTIMR#(R4)	;Start the rep timer
	MOV	DDCMOB#(R4),DDCMOI#(R4)	;Clear output ring
	MOV	DDCMOB#(R4),DDCMOO#(R4)	;
;	BR	HDOUT
	.PAGE
	.SBTTL	HDOUT	- Header out

HDOUT:	MOV	#$TSOM,@DDLBTB#(R4)	;Send an SYN
	MOV	#$TXENB!$TXENA!$BYTS8,@DDLBTS#(R4) ;Enable interrupts
	INCB	DDOST#(R4)		;Next state
	BR	DATEND			;Wait for interrupt (at HDCONT)

;	Handle rest of message
HDCONT:	CLR	R0
	BISB	@DDCTHO#(R4),R0		;Put char in low byte
	INC	DDCTHO#(R4)		;Point to the next character
	CMP	DDCTHO#(R4),DDEDHO#(R4)	;At the last char?
	BEQ	HDCON1			;Yes - set TEOM
	MOV	R0,@DDLBTB#(R4)		;Place char in device register
	BR	DATEND
HDCON1:	BIS	#$TEOM,R0		;Send the last char and CRC
	MOV	R0,@DDLBTB#(R4)		;Place char in device register
	BIC	#$IDL,DDFLAG#(R4)	;Make sure we clear the idle flag
	INCB	DDOST#(R4)		;Next state (data output)
	CMPB	#SOH,@DDSTHO#(R4)	;Are we sending data?
	BEQ	DATEND			;Continue to next state
HDEXT:	CLRB	DDOST#(R4)		;Clear output state
	BR	DATEND
	.PAGE
	.SBTTL	DATOUT	- Data output


DATOUT:	CLR	R0
DATOU1:	BISB	@DDPKTO#(R4),R0		;Get the character to output
	INC	DDPKTO#(R4)		;Point to the next character
	DEC	DDCNTO#(R4)		;Drop the character count
	BNE	20$			;If not zero go on
	CLRB	DDOST#(R4)		;Go to header state
	TBBE	$LOAD,DDFLAG#(R4),1$	;Load state?
	BIS	#$ELOAD,DDFLAG#(R4)	;Yes - set end of load
	BR	20$			;Don't send CRC for load mode
1$:	BIS	#$TEOM,R0		;Send the last char and CRC
20$:	MOV	R0,@DDLBTB#(R4)		;Place in device register

DATEND:;	POP	R3
	POP	R2
	POP	R1
	POP	R0
	POP	R4
	RTI
	.PAGE
	.SBTTL	Data area

	PURE

ENQMS:	.WORD	2307
	.WORD	-2
	.WORD	0

RSTMS:	.WORD	2307
	.WORD	-1
	.WORD	0

	.END
  