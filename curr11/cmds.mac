	.TITLE	CMDS   - COMMON COMMAND DECODER SUBROUTINES
	.SBTTL	CMDS   - NEXILIS/JRG

	XLPAR

;THIS FILE CONTAINS ROUTINES THAT ARE USED BY ALL TERMINAL COMMAND DECODERS

;DEFINE MACRO FOR INLINE MESSAGES

	.MACRO	INLMES  MES
	CALL	INLOUT
	.ASCIZ	MES
	.EVEN
	.ENDM

.IF NDF ..SAT
  ..SAT=!0
.ENDC

	CODE

;DEFINE COMMAND DECODER ENTRY INDEXES

.CXCMD==!0		;ENTER COMMAND MODE ENTRY
.CXCIN==!2		;COMMAND INTERRUPT ENTRY
.CXECL==!4		;END OF COMMAND LINE ENTRY
.CXRST==!6		;RESET COMMAND DECODER
.CXACK==!10		;CONNECT ACK PACKET ENTRY
.CXNAK==!12		;CONNECT NAK PACKET ENTRY
.CXDIS==!14		;DISCONNECT PACKET ENTRY
.CXDAK==!16		;DISCONNECT ACK PACKET ENTRY
	.PAGE
	.SBTTL	DISCONNECT, RETRY OR KILL PACKET RECEIVED
;HERE ON DISCONNECT, RETRY, OR KILL CONNECTION PACKET

CMDDIS::CALL	DELSRC#			;DELETE ANY INPUT
	  NOP
	BICB	#.OSESC,SDTROS#(SD)	;Clear Escape processing bit
	BIC	#$S1HLD,SDSTS1#(SD)	;Clear the output hold bit
.IF EQ ..SAT
	CALL	CLRIHB#			;Clear the input hold buffer
	TBBN	$S1BC2,SDSTS1#(SD),1$	;Were we connected to?
	TBBN	$T0CMD,SDTRS0#(SD),3$	;No-is command mode allowed?
1$:	BIC	#$T4HOE,SDTRS4#(SD)	;No-clear output hold enable!
.ENDC
3$:	TBBE	$T6PND,SDTRS6#(SD),4$	;IF CONNECTION PENDING, TREAT THIS
	JMP	CMDNAK			;  LIKE A CONNECT NAK

4$:	CALL	CHKSTA			;CHECK STATE
	  BR	CMDDAH			;IF FUNNY!

;	GETPKT SETS R1, SO THIS LINE NOT NEEDED ... DRE 11/04/80
;	MOV	SDPKDH#(SD),R1		;Do we still have a packet?
	CALL	GETPKT#			;Do we still have a packet?
	BEQ	14$			;No!
	MOVB	PKTYPE(R1),R0		;Yes-get packet type
	CMPB	#.PTDIS,R0		;Is it a Disconnect?
	BEQ	16$			;Yes-this is OK
	CMPB	#.PTKIL,R0		;No-is it a Kill?
	BEQ	16$			;Yes-this is OK too
14$:	FSTOP	KIL			;No-this makes us very unhappy!

16$:	BIC	#$T1SOP,SDTRS1#(SD)	;CLEAR SOME FLAGS
	BIC	#$T6PND!$T6CMW,SDTRS6#(SD)
	MOV	#.CXDIS,R2		;GET ENTRY INDEX
	BR	CMDENT			;CONTINUE
	.PAGE
	.SBTTL	DISCONNECT ACK RECEIVED
;HERE FOR DISCONNECT ACK PACKET

CMDDAK::BIC	$T6PND,SDTRS6#(SD)	;NOTHING PENDING NOW
	BICB	#.OSESC,SDTROS#(SD)	;Clear Escape processing bit
	CLRB	SDSTS1#(SD)		;CLEAR DEVICE STATE (DO NOT CALL SETFRE
					;  HERE BECAUSE WE WANT TO STAY "IN
					;  -USE"
	CMPB	#.CSIGN,SDTRCS#(SD)	;IS THE CARRIER STATE "IGNORE
					;  EVERYTHING"?
	BNE	2$			;NO-GO ON
	CALL	JNKCPK#			;YES-GIVE UP THE PACKET
	JMP	HANGU4#			;AND GO FINISH HANGUP CLEAN-UP

2$:	BIS	#$T1CMD!$T1ECM,SDTRS1#(SD);INDICATE COMMAND MODE
	MOVB	#100,SDTRBS#(SD)
	MOV	#.CXDAK,R2		;GET ENTRY INDEX
CMDENT::
.IF EQ ..SAT
	MOVB	SDTRCX#(SD),R3		;GET THIS SOCKET'S COMMAND DECODER INDEX
	MOV	CMDETB#(R3),R3		;GET ADDRESS OF DISPATCH TABLE
	ADD	R2,R3			;ADD IN THE INDEX
	JMP	@(R3)+			;DISPATCH TO COMMAND DECODER
.IFF
	JMP	@SATENT#(R2)
.ENDC

;Here if state of socket is strange - cannot get here unless we are
;  disconnected or disconnect pending

CMDDAH:	CALL	JNKCPX#			;JUNK THE PACKET
	BICB	#.OSESC,SDTROS#(SD)	;Clear Escape processing bit
	CJMP	HUNGUP#			;GET RID OF HIM!
	.PAGE
	.SBTTL	CONNECT ACK RECEIVED

;HERE FOR CONNECT ACK PACKET
;	C(R0) = FLAG BITS
;	  200 = REPORT TERMINAL TYPE PARAMETERS
;	  100 = ENABLE DEFERED ECHO MODE
;	  040 = LOGIN STRING NEEDED
;	  020 = DO NOT SET TERMINAL PROFILE
;	  010 = DO NOT CHANGE ECHO SETTING
;	  004 = DO NOT HOLD INPUT IF OUTPUT ACTIVE
;	  002 = NOT USED
;	  001 = NOT USED

CMDACK::
	MOV	UPTIME#,SDTR18#(SD)	;---KS601
	MOV	UPTIME#+2,SDTR18#+2(SD)	;---KS601
	INC	SDTR19#(SD)		;---KS601
	CALL	CHKSTA			;CHECK STATE
	  BR	FNYACK			;IF STATE IS FUNNY!
	BIC	#$T6BOF!$T6GBF!$T6RBF,SDTRS6#(SD);CLEAR THE BALL FLAGS
.IF EQ ..SAT
	CALL	GETCTL#			;GET SOCKET NUMBER
	MOV	R0,SDTRC4#(SD)		;STORE FOR LATER
	BCS	CMDAC4			;THATS ALL IF NO MORE THERE!
	CALL	GETCTL#			;GET FLAG BYTE
	BCS	CMDAC4			;IF NOT THERE
	PUSH	R0			;SAVE FLAG BYTE
	TBBN	020,R0,CMDAC3		;BRANCH IF NOT TO SET PROFILE BY REQUEST
	CALL	GETCTL#			;GET PROFILE BYTE
	BCS	CMDAC3			;IF NOT THERE
	TSTB	R0			;SHOULD WE SET THE PROFILE?
	BLE	CMDAC3			;NO
	DEC	R0			;YES-FIX UP THE VALUE
	CALL	SETPRF#			;SETUP THE TERMINAL PROFILE
	  NOP				;Ignore failure!
CMDAC3:	POP	R0			;GET FLAG BYTE
	TBBE	100,R0,4$		;WANT DEFFERED ECHO?
	BIC	#$T1DEX!$T1HIA,SDTRS1#(SD);YES
	TBBN	$T4SEL,SDTRS4#(SD),4$	;SELECTRIC TERMINAL?
	BIS	#$T1DEM,SDTRS1#(SD)	;NO-START OUT IN DEFERED MODE
4$:	TBBN	010,R0,10$		;SHOULD WE ENABLE ECHOING?
	BIS	#$T1ECH!$T1ECM,SDTRS1#(SD);YES
10$:	TBBE	004,R0,12$		;SHOULD WE HOLD INPUT WHEN OUTPUT IS
.ENDC					;  ACTIVE?
	BIC	#$T1HIA,SDTRS1#(SD)	;NO
.IF EQ ..SAT
12$:	TBBN	001,R0,14$		;SHOULD WE CLEAR LOGIN MODE?
	BIC	#$T3LGI,SDTRS3#(SD)	;YES
14$:	PUSH	R0			;SAVE FLAG BYTE
	TBBE	200,R0,16$		;WANT REPORT OF TERMINAL PARAMETERS?
	CALL	GETPKT#			;Yes-get address of packet
	CALL	RPTTYP#			;Build terminal parameter packet
	CALL	CPKRTN#			;Send the packet
	BR	20$			;CONTINUE
.ENDC

16$:	CALL	JNKCPX#			;GIVE UP THE ACK PACKET
.IF EQ ..SAT
20$:	POP	R0			;RESTORE FLAG BYTE TO R0
.ENDC
CMDAC4:	MOV	#.CXACK,R2		;GET ENTRY INDEX
	CALL	CMDENT			;FINISH PROCESSING THE ACK
	TBBE	$T4SEL,SDTRS4#(SD),RTN001;FINISHED IF NOT SELECTRIC
	CJMP	SNDGRX#			;SEND GREEN BALL HERE IF SELECTRIC!

;HERE IF STATE IS NOT RIGHT

FNYACK:	MOV	#.ERREQ,R0		;ERROR = REQUEST
	CALL	SETKIL#			;CHANGE ACK PACKET INTO KILL PACKET
	CJMP	PKTRTN#			;SEND IT AND RETURN
	.PAGE
	.SBTTL	CONNECT NAK RECEIVED

;HERE FOR CONNECT NAK PACKET

CMDNAK::CALL	DELSRC#			;DELETE ANY INPUT
	  NOP
	CALL	CHKSTA			;CHECK STATE
	  BR	CMDDAH			;IF STRANGE
	MOV	#.CXNAK,R2		;GET ENTRY INDEX
	BR	CMDENT			;CONTINUE
	.PAGE
	.SBTTL	CONNECT PACKET SENT TO TERMINAL

;Here on connect packet sent to the terminal

CMDCNP::
.IF EQ ..SAT
	TBBN	$T0HWR,SDTRS0#(SD),2$	;Hardwired line?
.ENDC
1$:	MOV	#.ERCRF,R0		;No-don't let anyone connect
	CJMP	REFUSE#

.IF EQ ..SAT
2$:	CALL	CON2TR#			;Initialize carrier state
	  BR	1$			;If can't connect!
	PUSHB	PKDATA+36(R1)		;Save terminal profile
	MOVB	PKDATA+35(R1),1(SP)	;And flag bits
	CLR	R2
	CALL	ACCEPT#			;Accept the connection
	BIC	#$T1CMD!$T1ECM!$T1SOP!$T1DEA!$T1ECH,SDTRS1#(SD)
	BIC	#$S1HLD,SDSTS1#(SD)
	BIC	#$T3DSC,SDTRS3#(SD)	;Make sure funny bits are clear
	MOVB	(SP),R0			;Get profile byte
	DEC	R0
	BLT	4$			;If should not change profile
	CALL	SETPRF#			;Setup the profile
	  NOP				;Ignore failure (we tried!)
	MOVB	SDTRBX#(SD),SDTRBS#(SD)	;Make the new break set the current
					;  break set
4$:	CLRB	SDTRCI#(SD)		;Make sure no command interupt
					;  character is defined
	TBBN	200*400,(SP),6$		;Does he want parameters reported?
	CMPB	#P.RSTS+1,(SP)		;Maybe not-RSTS always gets them!
	BNE	10$			;Nope
6$:	MAKPKT	.PLSTP#			;Get a packet
	BCS	30$			;Opps!
	INCB	SDPCNT#(SD)		;Keep the count right
	CALL	RPTTYP#			;Tell him about us!
	CALL	SNDPKT#
10$:	MAKPKT	.PLSTP#			;Get another packet
	BCS	30$			;Too bad
	MOV	#.PTDAT*400+7,PKCNT(R1) ;Set up data packet with 7 bytes overhead
	MOV	R1,R2			;Copy pointer
	ADD	#PKDATA,R2		;Point to data
	MOV	#LF*400+CR,(R2)+	;Go to next line
	MOV	#"P ,(R2)+		;Put in start of string
	MOVB	SDSOC#(SD),R0		;Get socket
	BIT	#70,R0			;Two digit port?
	BEQ	12$			;No
	ASHI	-3,R0			;position
	ADD	#'0,R0			;Convert to ascii
	MOVB	R0,(R2)+		;Store in string
	INCB	PKCNT(R1)		;Count this optional char
	MOVB	SDSOC#(SD),R0
12$:	BIC	#^C7,R0			;Clear off junk
	ADD	#'0,R0			;Convert to ASCII
	MOVB	R0,(R2)+		;Put in string
	MOVB	#CR,(R2)+		;END LINE
	MOVB	#LF,(R2)+
	MOV	#..TRMM#,R0		;Form pointer to user string
	TSTB	(R0)			;Is there a string?
	BEQ	20$			;No
14$:	INC	PKCNT(R1)		;Count the character
	MOVB	(R0)+,(R2)+		;Copy a byte of the string
	BNE	14$
	MOVB	#CR,-1(R2)		;Do end of line
	MOVB	#LF,(R2)+
	INCB	PKCNT(R1)
20$:	CALL	SNDPKT#			;Send it to the other end
30$:	TST	(SP)+			;Fix up the stack
	MOV	#OUTGET#,SDTROD#(SD)	;Set output done dispatch
.ENDC
RTN001:	RETURN				;Thats all
	.PAGE
	.SBTTL	SUBROUTINES

;SUBROUTINE TO CHECK SOCKET STATE WHEN RECEIVING PACKETS WHICH AFFECT THE
;  CONNECTION STATE
;	CALL	CHKSTA
;	  RETURN IF STATE IS NOT RIGHT
;	RETURN IF STATE OK - WAITS UNTIL OUTPUT IS IDLE

CHKSTA:	BIC	#$T6PND,SDTRS6#(SD)	;CONNECTION NOT PENDING NOW
	CMPB	#.CSNRM,SDTRCS#(SD)	;CARRIER STATE MUST BE NORMAL!
	BNE	RTN004			;OPPS!
	ADD	#2,(SP)			;OK-GIVE SKIP RETURN
					;FALL INTO WATIDL

;SUBROUTINE TO WAIT UNTIL OUTPUT IS IDLE
;	CALL	WATIDL
; **** WARNING ****
;    THIS SUBROUTINE MUST BE CALLED WITH NOTHING ON THE STACK RELATIVE TO
;    THE ENTRY TO THE COMMAND DECODER!!

WATIDL::TBBN	$T1ACT,SDTRS1#(SD),2$	;IS OUTPUT ACTIVE NOW?
	TBBE	$S1HLD,SDSTS1#(SD),RTN004;NO-HAS OUTPUT BEEN HELD OFF?
2$:	MOV	#WATID2,SDTROD#(SD)	;YES-MUST WAIT!
	POP	SDTRC3#(SD)		;SAVE RETURN ADDRESS UNTIL IDLE
RTN004:	RETURN				;RETURN ONE MORE LEVEL NOW

;Here when output is idle

WATID2:	MOV	#OUTGET#,SDTROD#(SD)	;Reset output done dispatch to be safe!
	BR	OUTDON			;And go return

;SUBROUTINE TO OUTPUT IN-LINE TEXT STRING
;	CALL	INLOUT
;	ASCIZ	/MESSAGE/

INLOUT::POP	SDTRC1#(SD)		;SAVE ADDRESS OF STRING
	MOV	#INLOU2,SDTROD#(SD)	;STORE OUTPUT DONE DISPATCH ADDR
	BIS	#$T1ACT!$T1ACX!$T1SYN,SDTRS1#(SD);INDICATE OUTPUT IS ACTIVE
INLOU2:	MOVB	@SDTRC1#(SD),R0		;GET CHARACTER FROM STRING
	BEQ	INLOU4			;IF FINISHED
INLOU3:	INC	SDTRC1#(SD)		;BUMP POINTER
	JMP	OUTCHR#			;AND GO OUTPUT CHARACTER

;HERE WHEN FINISHED

INLOU4:	BIC	#$T1ACT!$T1ACX,SDTRS1#(SD);CLEAR OUTPUT ACTIVE FLAG
	MOV	SDTRC1#(SD),R0		;GET ADDRESS OF LAST CHARACTER
	ROR	R0			;POINT TO NEXT WORD
	INC	R0
	ASL	R0
	JMP	(R0)			;RETURN FROM INLMES

;SUBROUTINE TO OUTPUT CARRIAGE-RETURN/LINE-FEED
;	CALL	ENDLIN

.IF EQ ..SAT
ENDLIN::MOV	#ENLMES,R1		;GET ADDRESS OF STRING
.ENDC					;FALL INTO OUTSTR

;SUBROUTINE TO OUTPUT ASCIZ STRING
;	C(R1) = ADDRESS OF STRING
;	CALL	OUTSTR

OUTSTR::POP	SDTRC3#(SD)		;SAVE RETURN ADDRESS
OUTST1::MOV	R1,SDTRC1#(SD)		;SAVE ADDRESS OF STRING
	BIS	#$T1ACT!$T1ACX!$T1SYN,SDTRS1#(SD);INDICATE OUTPUT IS ACTIVE
	MOV	#OUTST2,SDTROD#(SD)	;SET OUTPUT DONE DISPATCH ADDR
OUTST2:	MOVB	@SDTRC1#(SD),R0		;GET CHARACTER
	BNE	INLOU3			;GO BUMP POINTER AND OUTPUT CHARACTER
OUTDON:	BIC	#$T1ACT!$T1ACX,SDTRS1#(SD);FINISHED-CLEAR OUTPUT ACTIVE FLAGS
	JMP	@SDTRC3#(SD)		;AND RETURN
	.PAGE
;SUBROUTINE TO FETCH DECIMAL VALUE FROM THE COMMAND LINE
;	CALL	GETDEC
;	C(R0) = TERMINATING CHARACTER
;	C(R1) = VALUE
;	C:SET = NO VALUE FOUND

.IF EQ ..SAT

GETDEC::CALL	EATBLK			;EAT LEADING BLANKS
GETDE0:	CLR	R1			;CLEAR REGISTER FOR VALUE
GETDE2:	CMP	#'0,R0			;IS THIS A DIGIT?
	BGT	RTNCS6			;NO
	CMP	#'9,R0			;MAYBE
	BLT	RTNCS6			;NO-SET C AND RETURN
GETDE4:	BIC	#^C17,R0		;YES-GET VALUE
	MUL	#12,R1			;ADD IN VALUE
	ADD	R0,R1
	PUSH	R1			;SAVE R1 ON THE STACK
	CALL	GETCMC#			;GET NEXT CHARACTER
	  CLR	R0			;If not a digit
	POP	R1			;RESTORE R1
	CMP	#'0,R0			;IS IT A DIGIT?
	BGT	RTNCC6			;NO
	CMP	#'9,R0			;MAYBE
	BGE	GETDE4			;YES-CONTINUE
RTNCC6:	CLC				;MAKE SURE C IS CLEAR
RTN006:	RETURN				;AND RETURN

RTNCS6:	SEC
	RETURN

;SUBROUTINE TO EAT LEADING BLANKS BEFORE COMMAND ATOM
;	CALL	EATBLK
;	C(R0) = FIRST NON-BLANK CHARACTER

EATBLK::CALL	GETCMC#			;GET NEXT CHARACTER
	  BR	RTN006			;IF END OF LINE
	CMP	#SPA,R0			;SPACE?
	BEQ	EATBLK			;YES
	CMP	#HT,R0			;HORIZONTAL TAB?
	BEQ	EATBLK			;YES
	CMP	#VT,R0			;VERTICAL TAB?
	BEQ	EATBLK			;YES
	CMP	#FF,R0			;FORM FEED?
	BEQ	EATBLK			;YES
	RETURN				;NO-RETURN

.ENDC
	.PAGE
;SUBROUTINE TO OUTPUT DECIMAL VALUE
;	C(R1) = VALUE TO OUTPUT
;	CALL	OUTDEC

.IF EQ ..SAT

OUTDEC::POP	SDTRC3#(SD)		;PUT RETURN ADDRESS IN SAFE PLACE
	BIS	#$T1ACT!$T1ACX!$T1SYN,SDTRS1#(SD);INDICATE OUTPUT IS ACTIVE
	MOV	#17,R3			;SET END OF DIGITS VALUE
.IF EQ ..EIS
	CLR	R2
.ENDC
OUTDE2:	CLR	R0
	DIV	#12,R0			;GET NEXT DIGIT
.IF NE ..EIS
	ASHC	#4,R2			;SHIFT DIGITS
.IFF
	CLC
	ROL	R3
	ROL	R2
	ROL	R3
	ROL	R2
	ROL	R3
	ROL	R2
	ROL	R3
	ROL	R2
.ENDC
	BIS	R1,R3			;MERGE IN THIS DIGIT
	MOV	R0,R1			;GET QUO. IN RIGHT PLACE
	BNE	OUTDE2			;LOOP IF MORE DIGITS TO GET
	MOV	#OUTDE6,SDTROD#(SD)	;SET OUTPUT DONE DISPATCH ADDRESS
OUTDE4:	MOV	R3,R0			;GET NEXT DIGIT
	BIC	#^C17,R0
	CMP	#17,R0			;FINISHED?
	BNE	2$			;NO
	JMP	OUTDON			;YES

2$:	ADD	#'0,R0			;NO-MAKE INTO ASCII
.IF NE ..EIS
	ASHC	#-4,R2			;SHIFT OUT THIS DIGIT
.IFF
	ROR	R2
	ROR	R3
	ROR	R2
	ROR	R3
	ROR	R2
	ROR	R3
	ROR	R2
	ROR	R3
.ENDC
	MOV	R2,SDTRC1#(SD)		;STORE DIGITS
	MOV	R3,SDTRC2#(SD)
	JMP	OUTCHR#			;GO OUTPUT CHARACTER

;HERE WHEN OUTPUT FINISHED

OUTDE6:	MOV	SDTRC1#(SD),R2		;GET DIGITS
	MOV	SDTRC2#(SD),R3
	BR	OUTDE4			;CONTINUE

.ENDC
	.PAGE
;SUBROUTINE TO PROCESS AGRUMENT FOR CONNECT COMMAND - SYNTAX OF ARGUMENT IS:
;	[DV@]DSTNAM[.SOC]
;  WHERE:   ("[XXX]" INDICATES THAT "XXX" IS OPTIONAL)
;	DV     = 2 CHARACTER NETWORK DEVICE NAME
;	DSTNAM = 1 TO 6 CHARACTER NODE OR HOST NAME
;	SOC    = DECIMAL SOCKET NUMBER
;
;	ALTERNATE SYNTAX
;	GATEWAY/DSTNAM[.SOC]
;  WHERE:  GATEWAY IS A NETWORK (SUCH AS TYMNET OR BUBNET)
;
;  CALLING SEQUENCE:
;	C(R0) = FLAG BITS FOR PKDATA+35
;	CALL	CMDCON
;	C(R0) = CHARACTER WHICH STOPPED SCAN IF RETURN INDEX IS 6 OR 10
;	C(R1) = RETURN INDEX AS FOLLOWS:
;		 0 = SYNTAX ERROR
;		 2 = BAD DEVICE NAME
;		 4 = PACKET NOT AVAILABLE
;		 6 = NULL ARGUMENT, C(R0) = AUTO CONNECT INDEX
;		10 = NORMAL, C(R0) = STOPPER CHARACTER
;  IF RETURN INDEX = 10, CONNECT PACKET HAS BEEN SENT; IN ALL CASES, THE COMMAND
;    HAS NOT NECESSARILY BEEN SCANNED COMPLETELY

.IF EQ ..SAT

CMDCON::MOV	#S#,R2			;CLEAR THE SCRATCH AREA
	MOV	#40,R1
1$:	CLR	(R2)+
	SOB	R1,1$
	MOV	#.PTCON*400+37,PKCNT+S#	;STORE PACKET TYPE AND COUNT
	MOV	#13*400+200,S#+PKDATA+0	;STORE HEADER DATA
	MOVB	R0,S#+PKDATA+35		;STORE FLAG BYTE
	MOV	#S#+PKDATA+6,SDTRC2#(SD);STORE POINTER TO DST SPECIFICATION
	MOV	#6,SDTRC1#(SD)		;UP TO 6 CHARACTERS
	CALL	GETDST			;PROCESS NAME
	BCS	SYNERR			;IF ERROR
	BVS	2$			;GO ON IF NOT STOPPED BY END OF LINE
	CMP	#6,SDTRC1#(SD)		;END OF LINE-WAS A NAME GIVEN?
	BNE	CMDCN8			;YES-GO PROCESS THE NAME
	MOVB	SDTRS0#+1(SD),R0	;NO-DO WE HAVE A HOST CONNECT LIST?
	BIC	#^C<$T0HCL/400>,R0
	BEQ	SYNERR			;NO-SAY "SYNTAX ERROR"
	MOV	#6,R1			;RETURN INDEX = 6
	RETURN				;RETURN WITH C(R0) = AUTO CONNECT INDEX

;HERE IF NOT STOPPED BY END OF LINE

2$:	CMP	#'@,R0			;IS THIS A DEVICE NAME?
	BNE	DSTEXT			;NO-GO CHECK FOR DST EXTENSION
	CMP	#4,SDTRC1#(SD)		;YES-2 CHARACTERS?
	BNE	SYNERR			;NO-ERROR
	MOV	S#+PKDATA+6,R0		;YES-GET THE NAME
	MOV	#DEVMAX#,R1		;GET INDEX FOR NAME TABLE
6$:	CMP	DEVNAM#-2(R1),R0	;THIS ONE?
	BEQ	10$			;YES
	DEC	R1			;NO-CONTINUE
	SOB	R1,6$			;LOOP IF MORE TO CHECK
	MOV	#2,R1			;RETURN INDEX = 2
	RETURN

;HERE WITH MATCH ON DEVICE NAME

10$:	ASR	R1			;CALCULATE DEVICE I.D.
	DEC	R1
	MOVB	R1,S#+PKDATA+4		;STORE DEVICE I.D. IN PACKET
	CLR	S#+PKDATA+6		;CLEAR THE DEVICE NAME BYTES
	MOV	#S#+PKDATA+6,SDTRC2#(SD);STORE POINTER
	MOV	#6,SDTRC1#(SD)		;STORE COUNT
	CALL	GETDST			;PROCESS THE NAME
	BCS	SYNERR			;IF ERROR
	BVC	CMDCN8			;IF STOPPED BY END OF LINE
DSTEXT:	CMP	#'/,R0			;STOPPED BY "/"?
	BNE	DSTSOC			;NO-GO CHECK FOR SOCKET NUMBER
	MOV	#S#+PKDATA+37,SDTRC2#(SD);YES-STORE POINTER TO DST EXTENSION
	MOV	#12.,SDTRC1#(SD)	;ALLOW 12. CHARACTERS
	CALL	GETDST			;TRANSFER THE NAME
	BCS	SYNERR			;IF ERROR
	MOV	#12.,R2			;CALCULATE SIZE OF EXTENSION
	SUB	SDTRC1#(SD),R2
	BEQ	SYNERR			;SHOULD BE SOMETHING THERE!
	ADD	R2,S#+PKCNT		;UPDATE THE BYTE COUNT
	MOVB	R2,S#+PKDATA+2		;AND STORE DST EXTENSION SIZE
	TST	R0			;STOPPED BY END OF LINE?
	BEQ	CMDCN8			;YES-GO ON
DSTSOC:	CMP	#'.,R0			;SOCKET NUMBER NEXT?
	BNE	SYNERR			;NO-ERROR
	CALL	GETDEC			;YES-GET SOCKET NUMBER
	TST	R0			;END OF LINE NEXT?
	BNE	SYNERR			;NO-ERROR
	BIT	#177400,R1		;VALID SOCKET NUMBER?
	BEQ	CMDCN6			;YES-GO ON
SYNERR:	CLR	R1			;RETURN INDEX = 0
	RETURN

;HERE WITH VALID SOCKET NUMBER

CMDCN6:	MOVB	R1,S#+PKDATA+5		;STORE SOCKET NUMBER IN PACKET
CMDCN8:
.IF EQ ..SAT
	MOVB	SDTRPC#(SD),S#+PKDATA+24;STORE PRIV. LEVEL FOR TERMINAL
.IFF
	MOVB	#200,S#+PKDATA+24
.ENDC
					;PRIV. CLASS = 0
					;LINK PRIORITY = 0
;	MOVB	NIDPWN#(R0),S#+PKDATA+31;STORE WARNING VALUE
	MOVB	#4,S#+PKDATA+31		;STORE WARNING VALUE
;	MOVB	NIDPMX#(R0),S#+PKDATA+31;STORE MAXIMUM VALUE
	MOVB	#.IDTR#,S#+PKDATA+14	;SOURCE I.D.
	MOVB	SDSOC#(SD),S#+PKDATA+15	;SOURCE SOCKET NUMBER
	MOV	#6,R3			;SET COUNT
	MOV	#THSNOD#,R4		;AND POINTER
	MOV	#S#+PKDATA+16,R2
2$:	MOVB	(R4)+,(R2)+		;STORE CHARACTER
	SOB	R3,2$			;CONTINUE IF NEED MORE
	MOVB	SDTRCT#(SD),S#+PKDATA+33;STORE TERMINAL TYPE IN PACKET
	MOVB	SDTRS3#(SD),R0		;GET BAUD RATE (OUTPUT)
	BIC	#^C$T3ORT,R0
	MOVB	R0,S#+PKDATA+34		;STORE IN PACKET
	TBBE	$T4LCP,SDTRS4#(SD),3$	;LOCAL COPY TERMINAL?
	BISB	#002,S#+PKDATA+35	;YES-INDICATE THAT IN THE CONNECT PKT
3$:	MAKPKT	.PLSTP#			;GET A PACKET
	BCC	4$			;GO ON IF GOT ONE
	MOV	#4,R1			;RETURN INDEX = 4
	RETURN

;HERE WITH PACKET FOR CONNECT PACKET

4$:	MOV	R1,R2			;COPY SCRATCH AREA TO THE PACKET
	TST	(R2)+
	MOV	#S#+2,R3
	MOV	#37,R0
6$:	MOV	(R3)+,(R2)+
	SOB	R0,6$
	BIS	#$T6PND,SDTRS6#(SD)	;INDICATE NETWORK ACTIVITY PENDING
	MOV	#INPGET#,SDTROD#(SD)	;FIX UP THE OUTPUT DONE DISPATCH
	INCB	SDPCNT#(SD)		;COUNT THE PACKET
	PUSH	S#			;SAVE STOPPER CHARACTER
	CALL	SNDPKT#			;SEND THE PACKET
	MOV	#10,R1			;RETURN INDEX = 10
	POP	R0			;GET STOPPER CHARACTER
	RETURN				;AND RETURN

.ENDC			;.IF EQ ..SAT
	.PAGE
;SUBROUTINE TO GET DEVICE, NODE OR HOST NAME
;	C(SDTRC1(SD)) = MAXIMUM NUMBER OF CHARACTERS ALLOWED
;	C(SDTRC2(SD)) = POINTER
;	CALL	GETDST
;	C:SET = NAME TOO LONG
;	V:SET = STOPPED BY ILLEGAL CHARACTER
;	V:CLR = STOPPED BY END OF LINE

.IF EQ ..SAT

GETDST:	MOV	SDTRC4#(SD),R1		;DOING AUTO-CONNECT?
	BEQ	2$			;NO
	MOVB	(R1)+,R0		;YES-GET CHARACTER FROM STRING
	MOV	R1,SDTRC4#(SD)		;UPDATE POINTER
	TST	R0			;SET Z IF END OF STRING
	BEQ	12$			;If end of line
	BR	4$			;CONTINUE

;IF HERE IF INPUT SHOULD BE FROM THE TERINAL

2$:	CALL	GETCMC#			;GET A CHARACTER
	  BR	12$			;IF END OF LINE
4$:
;	REMOVE THESE TWO LINE TO LET CONTROLS THROUGH
;	CMP	#40,R0			;PRINTING CHARACTER?
;	BGE	2$			;NO-IGNORE IT!

	CMP	#140,R0			;IS IT LOWER CASE?
	BGT	10$			;NO
	BIC	#40,R0			;YES-MAKE UPPER CASE
10$:	CMP	#';,R0			;IS IT A ";"?
	BNE	14$			;NO-GO ON
	MOV	R0,S#			;YES-REMEMBER FUNNY STOPPER
12$:	CLR	R0			;RETURN WITHLEAR
	CLCV				;AND WITH V AND C CLEAR
	RETURN

;HERE IF NOT END OF LINE

14$:	CMP	#'-,R0			;IS IT A "-"?
	BEQ	GETDS4			;YES-THATS OK
	CMP	#':,R0			;IS IT A ":"?
	BEQ	GETDS4			;YES
	CMP	#'*,R0			;IS IT A "*"?
	BEQ	GETDS4			;YES
	CMP	#'Z,R0			;NO-IS IT A LETTER?
	BLT	GETDS2			;NO-BAD CHARACTER
	CMP	#'A,R0			;MAYBE
	BLE	GETDS4			;YES-GO ON
	CMP	#'0,R0			;NO-IS IT A DIGIT?
	BGT	GETDS2			;NO-BAD CHARACTER
	CMP	#'9,R0			;MAYBE
	BGE	GETDS4			;OK-GO ON
GETDS2:	SEV				;BAD CHARACTER-SET V
RTNCC8:	CLC				;AND CLEAR C
RTN008:	RETURN				;AND RETURN

;HERE WITH VALID CHARACTER FOR NAME (NUMBER OR LETTER)

GETDS4:	DEC	SDTRC1#(SD)		;GOOD CHARACTER-TOO MANY?
	BLT	RTNCS8			;YES-ERROR
	MOVB	R0,@SDTRC2#(SD)		;OK-STORE CHARACTER IN PACKET
	INC	SDTRC2#(SD)		;BUMP POINTER
	BR	GETDST			;CONTINUE

;HERE IF NAME IS TOO LONG

RTNCS8:	SEC				;SET C
	RETURN				;AND RETURN

.ENDC			;.IF EQ ..SAT
	.PAGE
;SUBROUTINE TO DISCONNECT
;	CALL	MAKDSC
;	C:SET = NO PACKET AVAILABLE

MAKDSC::BIS	#$T6PND,SDTRS6#(SD)	;INDICATE EXPECTING A PACKET BACK
	MOV	#INPGET#,SDTROD#(SD)	;RESET OUTPUT DONE DISPATCH
	MOV	#.PTKIL,R0		;MAKE A KILL PACKET
	CALL	CTLPKX#
	BCS	10$			;IF NO SPACE FOR PACKET
	MOV	#.ERREQ,R0		;REASON = "REQUEST"
	CALL	SETEPK#			;SETUP ERROR PACKET
	CALL	SNDPKT#			;SEND THE PACKET
	CLC				;CLEAR C
10$:	RETURN				;AND RETURN

;SUBROUTINE CALLED BY RECONNECT ROUTINE TO SETUP SRC FIELD IN CONNECT PACKET
;  MADE FROM A RECONNECT PACKET
;	C(R1) = ADDRESS OF PACKET
;	C(R4) = ADDRESS OF TERMINAL DDB
;	C(SD) = ADDRESS OF TERMINAL SDB
;	CALL	TRSSRC

.IF EQ ..SAT

TRSSRC::CALL	XXSSRC#			;CALL THE COMMON ROUTINE
	MOVB	SDTRPC#(SD),PKDATA+24(R1);STORE PRIV. LEVEL
	MOVB	SDTRCT#(SD),PKDATA+33(R1);STORE TERMINAL TYPE
	MOVB	SDTRS3#(SD),R0		;GET CURRENT BAUD RATE
	BIC	#^C$T3ORT,R0
	MOVB	R0,PKDATA+34(R1)	;STORE IN PACKET
	TBBE	$T4LCP,SDTRS4#(SD),6$	;LOCAL COPY TERMINAL?
	BISB	#002,PKDATA+35(R1)	;YES-INDICATE THAT
6$:	RETURN				;THATS ALL

.IFF
  TRSSRC==!0				;Don't need this routine if TYMSAT
.ENDC
	.PAGE
	.SBTTL	CMDS   - DATA

	PURE

;ASCII TEXT STRINGS

ENLMES:	.ASCIZ	<CR><LF>
	.EVEN

	.END

 ~|tô