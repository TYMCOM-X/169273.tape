BEGIN "gennod"
  REQUIRE "{}()" DELIMITERS;
  DEFINE !              =       "COMMENT";      ! Make "!" mean "COMMENT";
!
!
!************ D A T A    D E F N S . **********
!
!
!                                                               ;

  DEFINE prgmversion    =       """1.2""";      ! This program's current
                                                     version number;
                                                ! Any updates and revisions
                                                     to this program should
                                                     be recorded here;
  DEFINE crlf           =       "('15&'12)";    ! Normal line terminater;
  DEFINE maxdlv11jcount  =       "4";
  DEFINE notfinished    =       "0";            ! More host in list;
  DEFINE finished       =       "1";            ! No more host in list;
  DEFINE space          =       """ """;        ! Ascii space;
  DEFINE badhostlist    =       "2";            ! Bad host line syntax errcode;
  DEFINE maxhost        =       "4";            !  Maximum # of host on a node;
  DEFINE maxhostdigits  =       "4";            !  Maximum host # = 9999;
  DEFINE maxdl11count   =       "16";
  DEFINE nodename       =       "1";            ! This node's name (number);
  DEFINE nodetype       =       "2";            ! "BASE-11"" or "SAT-11";
  DEFINE clockspeed     =       "3";            ! Line frequency clock (50/60);
  DEFINE nodebusstype   =       "4";            ! Q-buss or Unibus ?;
  DEFINE nodeeisflag    =       "5";            ! Does this node have eis?;
  DEFINE hosttype       =       "6";            ! "X" or "11" currently;
  DEFINE kmcflag        =       "7";            ! If hosttype=X then specifies
                                                     if KMC hardware exist;
  DEFINE ostype         =       "8";            ! "RSTS","RSX", & "TYMSHR";
  DEFINE dl11count      =       "9";            ! Number of dl11 ports in node;
  DEFINE dlv11jcount    =       "10";           ! Number of dl11j ports;
  DEFINE dz11count      =       "11";           ! Number of dz ports in node;
  DEFINE wiringmode     =       "12";           ! "HW" or "DU" currently;
                                                ! ** Currently not used;
  DEFINE baudetectype   =       "13";           ! "TID" or "BCR" currently;
                                                !  ** Currently not used;
  DEFINE breaklogoff    =       "14";           ! Specifies if tty port should
                                                    disconnect on breakkey;
  DEFINE tymnetlink     =       "15";           ! Does a TYMNET gateway exist?;
  DEFINE neighborname   =       "16";           ! If a TYMNET gateway exist,
                                                     the neighbor node number;
  DEFINE tymnetbaud     =       "17";           ! "2400" or "4800" or "9600"
                                                     currently;
  DEFINE tymnetgroups   =       "18";           ! # of groups (16 channels) on
                                                     TYMNET sync line;
  DEFINE tymnetlinktype =       "19";           ! "SATLINK" or "NORMALLINK"
                                                     currently;
  DEFINE dutype         =       "20";           ! DUP or DU?;
  DEFINE hostname       =       "21";           ! Our host's name,must reserve
                                                    four entries;
                                !"21";          ! Reserved for 1st host name;
                                !"22";          ! Reserved for 2nd host name;
                                !"23";          ! Reserved for 3rd host name;
                                !"24";          ! Reserved for 4th host name;
  DEFINE hostcount      =       "25";           ! Number of host on this node;
  DEFINE answerlistsize =       "25";           ! Current number of answers;
  STRING ARRAY answerlist [ 1:answerlistsize ]; ! This is where the data is;
 BOOLEAN dummyflag;                             ! Used to set tty uppercase;
!
!
!************ End Of Data Defns. **************
!
!
!
!
!
!
!************P R O C E D U R E   D E F N S. *************
!
!
!                                                               ;

        DEFINE BEL     =       "('7)",         RUNFILE = """(upl)com""";
string  instr,
        wm,
        dbrk,
        cg,
        base,
        ru,
        mc,
        lk,
        gn,
        v;

define
  lh (i) = {((I) lsh -18)},
  hl (i) = {((I) lsh 18)},
  rh (i) = {((I) land '777777)},
  hr (i) = {((I) land '777777)},
  xwd (i,j) = {(((I) lsh 18) lor ((J) land '777777))};

boolean rew;
integer i,
        hostindex,
        j,
        k;
external
boolean !SKIP!;
!       Parse ("filespec",@Name,@Ext,@PPN)

        Scans the filespec and breaks it into Name, Ext and PPN.
        If a username is given, it is converted to a PPN.
;

procedure Parse (
        string  File;
        reference integer 
                Name,
                Ext,
                PPN );
begin "Parse"
    Name _ cvfil (File, Ext, PPN);
    if PPN = 0 then PPN _ calli(0,'24); ! GETPPN;
    if lh(PPN) = 0 then begin "UserName"
        integer array LKB[0:3];         ! block for extended lookup;
        integer Chan,Eof;
        LKB[0] _ arrinfo(LKB,2);        ! size of LKB;
        LKB[1] _ calli('16,'41);        ! MFD PPN;
        LKB[2] _ PPN;                   ! addr of SIXBIT user name;
        LKB[3] _ cvsix("UFD");          ! extension;
        open (Chan_getchan, "DSK", '17,0,0, 0,0,Eof);
        if Eof then return;             ! very, very ill;
        chnior (Chan, LKB[0], 4);       ! lookup UFD to get PPN;
        release (Chan);                 ! don't need the channel;
        PPN _ LKB[2];                   ! save real PPN for user;
    end "UserName";
end "Parse";


!       Run (program,[start])

        Run another program (overlay).  Program name may contain
        user name and program name, no device.
;

procedure Run (
        string  File; 
        integer Start(0) );
begin "run"
    integer array RNB[0:5];
    Parse (File, RNB[1], RNB[2], RNB[4]);
!     print("file= ",cv6str(rnb[1]),",ext=" ,cv6str(rnb[2]),",ppn= ",
      cvos(rnb[4]),crlf);
    RNB[0] _ cvsix("DSK");
    RNB[3] _ RNB[5] _ 0;
    calli ((Start lsh 18) + location(RNB[0]), '35);
    print (crlf&"Shouldn't get here!");
end "run";


BOOLEAN PROCEDURE yes(STRING prompt);           ! TRUE if yes, FALSE if no;
BEGIN "yes"
  STRING tempstring;                            ! TTY buffer;
  WHILE TRUE DO
    BEGIN
      PRINT(prompt);
      tempstring := INCHWL;
      IF (tempstring="Y") 
        THEN 
          BEGIN
            RETURN(TRUE);
            DONE
          END
        ELSE
          IF tempstring="N"
            THEN
              BEGIN
                RETURN(FALSE);
                DONE
              END
    END
END "yes";
STRING PROCEDURE spaces(INTEGER spacecount);
BEGIN "spaces"
  INTEGER index;
  STRING tmpstr;
  IF spacecount<=0
    THEN
      RETURN(NULL)
    ELSE
      BEGIN
        tmpstr := NULL;
        FOR index := 1 STEP 1 UNTIL spacecount DO
          tmpstr := tmpstr&'40;
        RETURN(tmpstr)
      END
END "spaces";
PROCEDURE printbanner;                          ! Print out prgm header;
BEGIN "printbanner"
  PRINT("++++++++  GENNOD --- PDP-11 ** NODE ** GEN PRGM --- Version ");
  PRINT(prgmversion&" ---  ++++++++"&crlf)
END "printbanner";
PROCEDURE getnodename(STRING ARRAY answerlist);
BEGIN "getnodename"
  WHILE TRUE DO
    BEGIN
      PRINT("Node name [ 1 to 7777 octal ] = ");
      answerlist[nodename] := INCHWL;
      IF ((CVO(answerlist[nodename])>'0)AND(CVO(answerlist[nodename])<'10000))
        THEN
          DONE
    END
END "getnodename";
PROCEDURE getclockspeed(STRING ARRAY answerlist);
BEGIN "getclockspeed"
  WHILE TRUE DO
    BEGIN
      PRINT("Clock frequency [ 50 or 60 ] = ");
      answerlist[clockspeed] := INCHWL;
      IF ((EQU(answerlist[clockspeed],"50"))OR EQU(answerlist[clockspeed],"60"))
        THEN
          DONE
    END
END "getclockspeed";
PROCEDURE getnodebusstype(STRING ARRAY answerlist);
BEGIN "getnodebusstype"
  IF yes("Is this PDP-11 node a Q-buss machine [ Y,N ] ? ")
    THEN
      answerlist[nodebusstype] := "QBUSS"
    ELSE
      answerlist[nodebusstype] := "UNIBUS"
END "getnodebusstype";
PROCEDURE getnodeeisflag(STRING ARRAY answerlist);
BEGIN "getnodeeisflag"
  IF yes("Does this PDP-11 node have hardware eis [ Y,N ] ?")
    THEN
      answerlist[nodeeisflag] := "YES"
    ELSE 
      answerlist[nodeeisflag] := "NO"
END "getnodeeisflag";
PROCEDURE gethostfromlist(REFERENCE STRING hostlist,hostvalue;REFERENCE INTEGER
                                statusreturn);
BEGIN "gethostfromlist"
  STRING tmpchr;
  INTEGER strlen,scanlimit,chrindex;
  hostvalue := NULL;
  statusreturn := notfinished;
  strlen := LENGTH(hostlist);
  IF strlen < (maxhostdigits + 1)
    THEN
      scanlimit := strlen
    ELSE 
      scanlimit := maxhostdigits + 1;
  FOR chrindex := 1 STEP 1 UNTIL scanlimit DO
    BEGIN
      tmpchr := LOP(hostlist);
      IF ((tmpchr = "0") OR (tmpchr = "1") OR (tmpchr = "2") OR (tmpchr = "3")
          OR (tmpchr = "4") OR (tmpchr = "5") OR (tmpchr = "6") OR (tmpchr = "7")
          OR (tmpchr = "8") OR (tmpchr = "9"))
        THEN
          hostvalue := hostvalue & tmpchr
        ELSE
          IF (tmpchr NEQ ",")
            THEN
              BEGIN
                statusreturn := badhostlist;
                DONE
              END
            ELSE
              DONE
    END;
  IF LENGTH(hostvalue) = 0
    THEN
      statusreturn := badhostlist;
  IF statusreturn NEQ badhostlist
    THEN
      BEGIN
        answerlist[hostcount] := cvs(cvd(answerlist[hostcount])) + 1;
        IF LENGTH(hostlist) = 0
          THEN
            statusreturn := finished
          ELSE 
            statusreturn := notfinished
      END
END "gethostfromlist";
PROCEDURE gethostnames(STRING ARRAY answerlist);
BEGIN "gethostnames"
  STRING hostlist,tmplist;
  INTEGER hostindex,statcode,tmplen,chrindex;
  WHILE TRUE DO
    BEGIN
      PRINT("Host #'s [from 1 to 4 host, seperated by "","" ] = ");
      hostlist := INCHWL;
      answerlist[hostcount] := "0";                     ! Init hostcount;
!
!
!                       Strip spaces from hostlist;
      tmplist := NULL;
      tmplen := LENGTH(hostlist);
      FOR chrindex := 1 STEP 1 UNTIL tmplen DO
        IF hostlist[ chrindex TO chrindex ] NEQ space
          THEN
            tmplist := tmplist & hostlist[chrindex TO chrindex];
      hostlist := tmplist;
!
!
!                       End of strip spaces from hostlist;
      FOR hostindex := 0 STEP 1 UNTIL maxhost-1 DO
        BEGIN
          gethostfromlist(hostlist,answerlist[hostname+hostindex],statcode);
          IF (cvd(answerlist[hostname+hostindex])<0)OR(cvd(answerlist[hostname+
              hostindex])>2047)
            THEN
              BEGIN
                PRINT("Host number out of range!"&crlf);
                statcode := badhostlist
              END;
          IF (statcode NEQ badhostlist)
            THEN
              BEGIN
                answerlist[hostcount] := cvs(cvd(answerlist[hostcount])+1);
                IF statcode = finished 
                  THEN
                    DONE
              END
            ELSE
              BEGIN
                PRINT("Bad host list.  Please try again! "&crlf);
                DONE
              END
         END;
       IF statcode NEQ badhostlist
        THEN
          BEGIN
            answerlist[hostcount] := cvs(hostindex);
            DONE
          END
    END
END "gethostnames";
PROCEDURE gethosttype(STRING ARRAY answerlist); ! DEC X or DEC PDP-11?;
BEGIN "gethosttype"
  WHILE TRUE DO
    BEGIN
      PRINT("Host type [ X,11 ] = ");
      answerlist[hosttype] := INCHWL;
      IF EQU(answerlist[hosttype],"X") OR
         EQU(answerlist[hosttype],"11")
        THEN
          DONE
    END
END "gethosttype";
PROCEDURE getkmcflag(STRING ARRAY answerlist);
BEGIN "getkmcflag"
  IF yes("Is there a KMC unit [ Y,N ] ? = ")
    THEN
      answerlist[kmcflag] := "YES"
    ELSE 
      answerlist[kmcflag] := "NO"
END "getkmcflag";
PROCEDURE getostype(STRING ARRAY answerlist);      ! "RSTS" or "RSX" ?;
BEGIN "getostype"
  WHILE TRUE DO
    BEGIN
      PRINT("PDP-11 OS [ RSTS,RSX ] = ");
      answerlist[ostype] := INCHWL;
      IF EQU(answerlist[ostype],"RSTS") OR EQU(answerlist[ostype],"RSX")
        THEN
          DONE
    END
END "getostype";
PROCEDURE gettymnetlink(STRING ARRAY answerlist);
BEGIN "gettymnetlink"
  IF yes("Is there a TYMNET link [ Y,N ] ? = ")
    THEN
      answerlist[tymnetlink] := "YES"
    ELSE
      answerlist[tymnetlink] := "NO"
END "gettymnetlink";
PROCEDURE getdutype(STRING ARRAY answerlist);
BEGIN "getdutype"
  IF yes("Does this Unibus node have a DUP (as opposed to a DU) [ Y,N } ? ")
    THEN
      answerlist[dutype] := "DUP"
    ELSE 
      answerlist[dutype] := "DU"
END "getdutype";
PROCEDURE getdl11count(STRING ARRAY answerlist);
BEGIN "getdl11count"
  INTEGER mindl11count;
  IF EQU(answerlist[nodebusstype],"UNIBUS")
    THEN
      mindl11count := 1
    ELSE
      mindl11count := 0;
  WHILE TRUE DO
    BEGIN
      PRINT("Number of single line DL-11 modules [ "&CVS(mindl11count)&" to "&
                                               CVS(maxdl11count)&" ] = ");
      answerlist[dl11count] := INCHWL;
      IF ((CVD(answerlist[dl11count])>=mindl11count)AND(CVD(answerlist[dl11count])
                                              <=maxdl11count))
        THEN
          DONE
    END
END "getdl11count";
PROCEDURE getdlv11jcount(STRING ARRAY answerlist);
BEGIN "getdlv11jcount"
  INTEGER dl11joverlap;
  WHILE TRUE DO
    BEGIN
      PRINT("Number of Dlv-11J modules in node [ 0 to "&CVS(maxdlv11jcount)&" ] = ");
      answerlist[dlv11jcount] := INCHWL;
      IF (CVD(answerlist[dlv11jcount])>=0) AND (CVD(answerlist[dlv11jcount])<5)
        THEN
          BEGIN
            answerlist[dlv11jcount] := CVS(CVD(answerlist[dlv11jcount]));
            IF (CVD(answerlist[dl11count])>0)AND(EQU(answerlist[nodebusstype],
                                                     "QBUSS"))
              THEN
                BEGIN
                  IF (CVD(answerlist[dl11count]) MOD 4)>0
                    THEN
                      dl11joverlap := 1
                    ELSE
                      dl11joverlap := 0;
                  dl11joverlap := dl11joverlap+(CVD(answerlist[dl11count]) DIV
                                                    4);
                  IF (dl11joverlap+CVD(answerlist[dlv11jcount]))>maxdlv11jcount
                    THEN
                      PRINT("With "&answerlist[dl11count]&" DL-11's, you "&
                               "can only have "&CVS(maxdlv11jcount-dl11joverlap)
                               &" DL-11J's!  Try again please."&crlf)
                    ELSE
                      DONE
                END
              ELSE
                DONE
          END
    END
END "getdlv11jcount";
PROCEDURE getdz11count(STRING ARRAY answerlist);
BEGIN "getdz11count"
  WHILE TRUE DO
    BEGIN
      PRINT("Dz-11 modules [ 0 to 4 ] = ");
      answerlist[dz11count] := INCHWL;
      IF (CVD(answerlist[dz11count])>=0) AND(CVD(answerlist[dz11count])<5)
        THEN
          BEGIN
            answerlist[dz11count] := CVS(CVD(answerlist[dz11count]));
            DONE
          END
    END
END "getdz11count";
PROCEDURE getneighborname(STRING ARRAY answerlist);
BEGIN "getneighborname"
  WHILE TRUE DO
    BEGIN
      PRINT("Neighbor number [ 1 to 7777 octal ] = ");
      answerlist[neighborname] := INCHWL;
      IF (CVO(answerlist[neighborname])>'0) AND (CVO(answerlist[neighborname])<'10000)
        THEN
          BEGIN
            answerlist[neighborname] := CVOS(CVO(answerlist[neighborname]));
            DONE
          END
    END
END "getneighborname";
PROCEDURE gettymnetbaud(STRING ARRAY answerlist);
BEGIN "gettymnetbaud"
  WHILE TRUE DO
    BEGIN
      PRINT("TYMNET sync speed [ 2400,4800,9600 ] = ");
      answerlist[tymnetbaud] := INCHWL;
      IF (EQU(answerlist[tymnetbaud],"2400"))OR(EQU(answerlist[tymnetbaud],"4800"))OR
          (EQU(answerlist[tymnetbaud],"9600"))
        THEN
          DONE
    END
END "gettymnetbaud";
PROCEDURE gettymnetlinktype(STRING ARRAY answerlist);
BEGIN "gettymnetlinktype"
  WHILE TRUE DO
    BEGIN
      PRINT("TYMNET packet format [ ""N"" ormal,""S"" atelite ] = ");
      answerlist[tymnetlinktype] := INCHWL;
      IF EQU(answerlist[tymnetlinktype],"N") OR
         EQU(answerlist[tymnetlinktype],"S")
        THEN
          DONE
    END
END "gettymnetlinktype";
PROCEDURE getbreaklogoff(STRING ARRAY answerlist);
BEGIN "getbreaklogoff"
  IF yes("Disconnect on break key [ Y,N ] ? ")
    THEN
        answerlist[breaklogoff] := "Y"
    ELSE
          answerlist[breaklogoff] := "N";
END "getbreaklogoff";
PROCEDURE gettymnetgroups(STRING ARRAY answerlist);
BEGIN "gettymnetgroups"
  WHILE TRUE DO
    BEGIN
      PRINT("Tymnet groups [ 1 to 16 decimal ] = ");
      answerlist[tymnetgroups] := INCHWL;
      IF (CVD(answerlist[tymnetgroups])>0)AND(CVD(answerlist[tymnetgroups])<17)
        THEN
          BEGIN
            answerlist[tymnetgroups] := CVS(CVD(answerlist[tymnetgroups]));
            DONE
          END
    END
END "gettymnetgroups";
PROCEDURE bldconfigfile(STRING ARRAY answerlist);
BEGIN "bldconfigfile"
  INTEGER i,j,l,m,lin,dzsiz,n,dr;
  STRING ARRAY dladdressvectbl[ 1:16 ];
  STRING ARRAY dzaddressvectbl[ 1:4 ];
  STRING ARRAY draddressvectbl[ 0:3 ];
  EXTERNAL BOOLEAN !SKIP!;
  draddressvectbl[0] := "<167770,330,5";
  draddressvectbl[1] := "<167760,40,5";
  draddressvectbl[2] := "<167750,50,5";
  draddressvectbl[3] := "<167740,70,5";
  OPEN(i := GETCHAN,"DSK",0,0,1,0,j,j);
  IF NOT !SKIP!
    THEN
      BEGIN
        PRIannot open ""DSK"" !");
        CALL(1,"EXIT")
      END;
  LOOKUP(i,"CG"&answerlist[nodename]&".MAC",j);
  IF NOT j
    THEN
      RENAME(i,NULL,0,j);
  ENTER(i,"CG"&answerlist[nodename]&".MAC",j);
  IF j
    THEN
      BEGIN
        PRINT("Cannot enter file name ""CG"&answerlist[nodename]&".MAC"" !");
        CALL(1,"EXIT")
      END;
  CPRINT(i,
".macro config                  ; Config file for node "&
                                        answerlist[nodename]&"  ."&crlf&
"       name    "&answerlist[nodename]&crlf&
"       define  jifsec,"&answerlist[clockspeed]&"."&crlf);
  FOR dr := 0 STEP 1 UNTIL cvd(answerlist[hostcount]) DO
    BEGIN
      CPRINT(i,
"       link    "&answerlist[hostname+dr]&spaces(6-LENGTH(answerlist[hostname+dr]))&",0,tf,31.,s,");
      IF EQU(answerlist[hosttype],"11")
        THEN
          CPRINT(i,"d")
        ELSE
          IF EQU(answerlist[hosttype],"X")
            THEN
              CPRINT(i,"f")
            ELSE
              BEGIN
                PRINT("Illegal host type """&answerlist[hosttype]&""" !");
                CALL(1,"EXIT")
              END;
      CPRINT(i,","&draddressvectbl[dr]);
      IF EQU(answerlist[hosttype],"X")
        THEN
          BEGIN
            CPRINT(i,",128.");
            IF EQU(answerlist[kmcflag],"YES")
              THEN
                CPRINT(i,",kmc");
                CPRINT(i,">"&crlf)
      END
        ELSE
      CPRINT(i,">"&crlf)
    END;
  IF EQU(answerlist[tymnetlink],"YES")
    THEN
      BEGIN
        CPRINT(i,
"       link    tymnet,0,tf,32.,s,h,<"&answerlist[nodename]&",32.");
        IF EQU(answerlist[hosttype],"11")
          THEN
            CPRINT(i,",tpc");
        CPRINT(i,">"&crlf);
        FOR dr := 0 STEP 1 UNTIL cvd(answerlist[hostcount]) DO
          CPRINT(i,
"               lnkh    "&answerlist[hostname+dr]&","&answerlist[hostname+dr]&crlf);
        CPRINT(i,
"               lnkl    "&CVS(16*CVD(answerlist[tymnetgroups]))&".,"&
                                answerlist[neighborname]&",");
        IF EQU(answerlist[tymnetbaud],"2400")
          THEN
            CPRINT(i,"1,")
          ELSE
            IF EQU(answerlist[tymnetbaud],"4800")
              THEN
                CPRINT(i,"2,")
              ELSE
                CPRINT(i,"4,");
        CPRINT(i,answerlist[tymnetlinktype]&",a,<160010,400,5>"&crlf);
        CPRINT(i,
"       dhost   tymnet"&crlf)
      END;
      CPRINT(i,
"dev st,200,<200,200>"&CRLF&"  soc 1."&CRLF&
"       hcl     A,<"&answerlist[hostname]&">"&crlf);
  dladdressvectbl[1] := "<177560,60>";
  dladdressvectbl[2] := "<176510,310>";
  dladdressvectbl[3] := "<176520,320>";
  dladdressvectbl[4] := "<176500,300>";
  dladdressvectbl[5] := "<176540,340>";
  dladdressvectbl[6] := "<176550,350>";
  dladdressvectbl[7] := "<176560,360>";
  dladdressvectbl[8] := "<176570,370>";
  dladdressvectbl[9] := "<176600,140>";
  dladdressvectbl[10] := "<176610,150>";
  dladdressvectbl[11] := "<176620,160>";
  dladdressvectbl[12] := "<176630,170>";
  dladdressvectbl[13] := "<176640,200>";
  dladdressvectbl[14] := "<176650,210>";
  dladdressvectbl[15] := "<176660,220>";
  dladdressvectbl[16] := "<176670,230>";


  dzaddressvectbl[1] := "<160100,410>";
  dzaddressvectbl[2] := "<160110,420>";
  dzaddressvectbl[3] := "<160120,430>";
  dzaddressvectbl[4] := "<160130,440>";


  FOR l := 1 STEP 1 UNTIL cvd(answerlist[dl11count]) DO
    CPRINT(i,
"Linx A,"&dladdressvectbl[l]&crlf&
"  Line "&cvs(l)&".,F,300,,TYM,CRT,ASC,"&answerlist[ostype]&",N,N,Y,N,"&
        answerlist[breaklogoff]&",HW,A,200"&crlf);
  lin := l - 1;
  IF cvd(answerlist[dlv11jcount]) > 0
    THEN
      BEGIN
        l := l + ( 4 - ( l MOD 4 ) );
        IF cvd(answerlist[dl11count]) = 0
          THEN
            l := 0;
        FOR m := 1 STEP 1 UNTIL cvd(answerlist[dlv11jcount]) DO
          FOR n := 1 STEP 1 UNTIL 4 DO
            BEGIN
              l := l + 1;
              lin := lin + 1;
              CPRINT(i,
"Linx A,"&dladdressvectbl[l]&crlf&
"  Line "&cvs(lin)&".,F,300,,TYM,CRT,ASC,"&answerlist[ostype]&",N,N,Y,N,"&
        answerlist[breaklogoff]&",HW,A,200"&crlf)
            END
     END;
  FOR l := 1 STEP 1 UNTIL cvd(answerlist[dz11count]) DO
    BEGIN
      CPRINT(i,
"Linx B,"&dzaddressvectbl[l]&crlf);
      IF equ(answerlist[nodebusstype],"QBUSS")
        THEN
          dzsiz := 4
        ELSE
          dzsiz := 8;
      FOR m := 1 STEP 1 UNTIL dzsiz DO
        BEGIN
          lin := lin + 1;
          CPRINT(i,
"  Line "&cvs(lin)&".,A,3,,TYM,CRT,ASC,"&answerlist[ostype]&",N,N,Y,N,"&
        answerlist[breaklogoff]&",HW,A,200"&crlf)
        END
    END;
  CPRINT(i,
".endm"&crlf);
  CLOSE(i);
  RELEASE(i);
  PRINT("Building CG file!"&crlf)
END "bldconfigfile";
PROCEDURE bldmacn11cmdfile(STRING ARRAY answerlist);
BEGIN "bldmacn11cmdfile"
  INTEGER i,j;
  EXTERNAL BOOLEAN !SKIP!;
  OPEN(i:=GETCHAN,"DSK",0,0,1,0,j,j);
  IF NOT !SKIP!
    THEN
      BEGIN
        PRINT("Cannot access ""DSK"" in MC   ! ");
        CALL(1,"EXIT")
      END;
  LOOKUP(i,"MC"&answerlist[nodename]&".CMD",j);
  IF NOT j
    THEN
      RENAME(i,NULL,0,j);
  ENTER(i,"MC"&answerlist[nodename]&".CMD",j);
  IF j
    THEN
      BEGIN
        PRINT("Cannot enter file ""MC"&answerlist[nodename]&".CMD"" !");
        CALL(1,"EXIT")
      END;
  CPRINT(i,
"CG"&answerlist[nodename]&",MP"&answerlist[nodename]&".MAP=XP,"&
        "CG"&answerlist[nodename]&",COMMON"&crlf&
"RU"&answerlist[nodename]&",MP"&answerlist[nodename]&".MAP=XP,"&
        "CG"&answerlist[nodename]&",RU"&answerlist[nodename]&",ROUTE"&crlf);
  CLOSE(i);
  RELEASE(i);
  PRINT("Building MC file!"&crlf)
END "bldmacn11cmdfile";
PROCEDURE bldroutefile(STRING ARRAY answerlist);
BEGIN "bldroutefile"
  INTEGER i,j;
  EXTERNAL BOOLEAN !SKIP!;
  OPEN(i:=GETCHAN,"DSK",0,0,1,0,j,j);
  IF NOT !SKIP!
    THEN
      BEGIN
        PRINT("Cannot access ""DSK"" in RU !");
        CALL(1,"EXIT")
      END;
  LOOKUP(i,"RU"&answerlist[nodename]&".MAC",j);
  IF NOT j
    THEN
      RENAME(i,NULL,0,j);
  ENTER(i,"RU"&answerlist[nodename]&".MAC",j);
  IF j
    THEN
      BEGIN
        PRINT("Cannot enter file ""RU"&answerlist[nodename]&".MAC"" !");
        CALL(1,"EXIT")
      END;
  CPRINT(i,
".MACRO ROUTE"&crlf&
"               node    "&answerlist[nodename]&crlf&
"               hpath   "&answerlist[hostname]&",*"&crlf);
  IF EQU(answerlist[tymnetlink],"YES")
    THEN
      CPRINT(i,
"               hpath   tymnet,*"&crlf);
  CPRINT(i,
".ENDM"&crlf);
  CLOSE(i);
  RELEASE(i);
  PRINT("Building RU file!"&crlf)
END "bldroutefile";
PROCEDURE bldlink11cmdfile(STRING ARRAY answerlist);
BEGIN "bldlink11cmdfile"
  INTEGER i,j;
  STRING ext;
  EXTERNAL BOOLEAN !SKIP!;
  OPEN(i := GETCHAN,"DSK",0,0,1,0,j,j);
  IF NOT !SKIP!
    THEN
      BEGIN
        PRINT("Cannot access ""DSK"" in LK!!!");
        CALL(1,"EXIT")
      END;
  LOOKUP(i,"LK"&answerlist[nodename]&".CMD",j);
  IF NOT j
    THEN
      RENAME(i,NULL,0,j);
  ENTER(i,"LK"&answerlist[nodename]&".CMD",j);
  IF j
    THEN
      BEGIN
        PRINT("Cannot enter file ""LK"&answerlist[nodename]&".CMD""  !");
        CALL(1,"EXIT")
      END;
  IF equ(answerlist[nodeeisflag],"YES")
    THEN
      ext := ".OBJ"
    ELSE
      ext := ".NBJ";
  CPRINT(i,
"SV"&answerlist[nodename]&"/IMG/B:1000,MP"&answerlist[nodename]&"=CG"&
answerlist[nodename]&",RU"&answerlist[nodename]&","&crlf&"EXEC"&ext&",ERROR"&
ext&",PKT1"&ext&",PKT2"&ext&",PKT3D"&ext&",CMDTYM"&ext&",CMDS"&ext&","&crlf&
"TRM"&ext&",TRMF"&ext&",TRMP"&ext&",TRMT"&ext&",AUTO13"&ext&",ZAPBUF"&ext&","&crlf&
"LNK1"&ext&",LNK2"&ext&",TYMA"&ext&",TYMB"&ext&",STATUS"&ext&","&crlf);
  IF equ(answerlist[ostype],"TYMSHR")
    THEN
      BEGIN
        CPRINT(i,
"LNKF"&ext&",LKHBS1"&ext&",LKHBS2"&ext&",LKHBS3"&ext&",");
        IF equ(answerlist[kmcflag],"YES")
          THEN
            CPRINT(i,
"LNKF2"&ext&","&crlf)
          ELSE
            CPRINT(i,
"LNKF1"&ext&","&crlf)
      END
    ELSE
      CPRINT(i,
"LNKD"&ext&",LKHPT1"&ext&",LKHTP2"&ext&",LKHTP3"&ext&",LNKRSP"&ext&","&crlf);
  IF equ(answerlist[tymnetlink],"YES")
    THEN
      IF equ(answerlist[dutype],"DUP")
        THEN
          CPRINT(i,"LNKHAP"&ext&",")
        ELSE
          CPRINT(i,"LNKHA"&ext&",");
  IF CVD(answerlist[dz11count]) > 0
    THEN
      CPRINT(i,"LINB"&ext&",");
  CPRINT(i,"ONCE"&ext&",END"&ext&crlf);
  CLOSE(i);
  RELEASE(i);
  PRINT("Building LK file!"&crlf)
END "bldlink11cmdfile";
PROCEDURE cleanup;
BEGIN "cleanup"
  PRINT("Cleaning up!");
  CALL(1,"EXIT")
END "cleanup";
!
!
!
!    MAIN PROGRAM  ****************************************************;
!
!
!                                                                       ;
  dummyflag := TTYUP(TRUE);                     ! Set to uppercase (TTY);
  printbanner;                                  ! Print the program header;
  WHILE TRUE DO
    BEGIN
      PRINT("Is this node to be a Tymbas-11 (""B"") or a Tymsat-11 (""S"") ? = ");
      answerlist[nodetype] := INCHWL;
      IF answerlist[nodetype] = "B"
        THEN
          BEGIN
            answerlist[nodetype] := "BASE-11";
            DONE
          END
        ELSE
          IF answerlist[nodetype] = "S"
            THEN
              BEGIN
                answerlist[nodetype] := "SAT-11";
                DONE
              END
     END;
  IF answerlist[nodetype] = "B"
    THEN
BEGIN
                                                ! ====== B A S E ======;
  getnodename(answerlist);                      ! Obtain the node's name;
  getclockspeed(answerlist);                    ! Obtain the clock frequency;
  getnodebusstype(answerlist);                  ! Q-buss or Unibus?;
  getnodeeisflag(answerlist);                   ! Extended instruction set?;
  gethostnames(answerlist);                      ! What host?;
  gethosttype(answerlist);                      ! What type host do we have?;
  IF EQU(answerlist[hosttype],"X")              
    THEN
      BEGIN
        getkmcflag(answerlist);
        answerlist[ostype] := "TYMSHR"
      END
    ELSE
      IF EQU(answerlist[hosttype],"11")
        THEN
          getostype(answerlist)
          ELSE
            BEGIN
              PRINT("Corrupted data!  Should not get here.");
              CALL(1,"EXIT")
            END;
  gettymnetlink(answerlist);
  IF answerlist[tymnetlink]="Y"
    THEN
      BEGIN
        IF EQU(answerlist[nodebusstype],"UNIBUS")
          THEN
            getdutype(answerlist)
          ELSE
            answerlist[dutype] := "DU";
        getneighborname(answerlist);
        gettymnetbaud(answerlist);
        gettymnetgroups(answerlist);
        gettymnetlinktype(answerlist)
      END
    ELSE
      answerlist[dutype] := "DU";
  getdl11count(answerlist);
  IF EQU(answerlist[nodebusstype],"QBUSS")
    THEN
      BEGIN
        getdlv11jcount(answerlist);
        IF (CVD(answerlist[dl11count])+CVD(answerlist[dlv11jcount]))=0
          THEN
            BEGIN
              PRINT("You must have at least 1 DL port  in a micro-node,"&
                                              " for the console!"&crlf);
              PRINT("Aborting --- no action taken.");
              CALL(1,"EXIT")
            END
      END
    ELSE
      answerlist[dlv11jcount] := "0";
  getdz11count(answerlist);
  getbreaklogoff(answerlist);
  OPEN(i := GETCHAN,"DSK",0,0,1,0,j,j);
  IF NOT !SKIP!
    THEN
      BEGIN
        PRINT("Cannot access ""DSK"" in GN!!!");
        CALL(1,"EXIT")
      END;
  LOOKUP(i,"GN"&answerlist[nodename]&".CMD",j);
  IF NOT j
    THEN
      RENAME(i,NULL,0,j);
  ENTER(i,"GN"&answerlist[nodename]&".CMD",j);
  IF j
    THEN
      BEGIN
        PRINT("Cannot enter ""GN"&answerlist[nodename]&".CMD"" !");
        CALL(1,"EXIT")
      END;
  CPRINT(i,
"daytime Command file to create files for base node "&answerlist[nodename]&"!"&
crlf&"delete ##"&answerlist[nodename]&".mac,##"&answerlist[nodename]&
".cmd -gn"&answerlist[nodename]&".cmd"&crlf&"run (11tools)genbas"&crlf&
answerlist[nodename]&crlf&answerlist[clockspeed]&crlf);
  IF equ(answerlist[nodebusstype],"QBUSS")
    THEN
      CPRINT(i,"YES"&crlf)
    ELSE
      CPRINT(i,"NO"&crlf);
  CPRINT(i,
answerlist[nodeeisflag]&crlf);
  FOR hostindex := 0 STEP 1 UNTIL cvd(answerlist[hostcount]) - 1 DO
    CPRINT(i,
      answerlist[hostname+hostindex]&",");
  CPRINT(i,answerlist[hostname+hostindex]&crlf);
  CPRINT(i,answerlist[hosttype]&crlf);
  IF equ(answerlist[hosttype],"X")
    THEN
      CPRINT(i,answerlist[kmcflag]&crlf)
    ELSE
      IF equ(answerlist[hosttype],"11")
        THEN
          CPRINT(i,answerlist[ostype]&crlf);
  CPRINT(i,answerlist[tymnetlink]&crlf);
  IF equ(answerlist[tymnetlink],"YES")
    THEN
      BEGIN
        IF equ(answerlist[nodebusstype],"UNIBUS")
          THEN
            IF equ(answerlist[dutype],"DUP")
              THEN
                CPRINT(i,"YES"&crlf)
              ELSE
                CPRINT(i,"NO"&crlf);
        CPRINT(i,answerlist[neighborname]&crlf&answerlist[tymnetbaud]&crlf);
        CPRINT(i,answerlist[tymnetgroups]&crlf&answerlist[tymnetlinktype]&crlf)
      END;
  CPRINT(i,answerlist[dl11count]&crlf);
  IF equ(answerlist[nodebusstype],"QBUSS")
    THEN
      CPRINT(i,answerlist[dlv11jcount]&crlf);
  CPRINT(i,answerlist[dz11count]&crlf&answerlist[breaklogoff]&crlf);
  CPRINT(i,"run (11tools)macn11"&crlf&"mc"&answerlist[nodename]&".cmd@"&crlf&
           "run (11tools)link11"&crlf&"lk"&answerlist[nodename]&".cmd@/e"&crlf&
           "run (11tools)nibtrn"&crlf&"SV"&answerlist[nodename]&".IMG"&crlf&
                                "SV"&answerlist[nodename]&".NIB"&crlf);
  IF yes("Leave only the NIB and CORE files? (Y/N) - ")
    THEN
      CPRINT(i,"del mc"&answerlist[nodename]&".*,cg"&answerlist[nodename]&
               ".*,ru"&answerlist[nodename]&".*,lk"&answerlist[nodename]&
               ".*"&crlf);
  CPRINT(i,"dir  ##"&answerlist[nodename]&".*"&crlf);
  CLOSE(i);
  RELEASE(i);
  PRINT("Respond to file request - ""gn"&answerlist[nodename]&".cmd""");
  run(runfile);
  PRINT(crlf&"Should not get here! Check to see if you have RF license set!"&crlf);
  CALL(1,"EXIT");
  cleanup
END                                             ! ======= OF B A S E =======;
  ELSE
BEGIN                                           ! ====== S A T ======;
rew := TTYUP(TRUE);
rew _ FALSE;
while true do
  begin outstr("Node (>= 1000) = ");
        if cvd(instr _ inchwl) geq 1000 then done
        else outstr(BEL&"[Wrong, Enter node number > 1000]"&CRLF);
  end;
open(i _ getchan, "DSK", 0, 0, 1, 0, j, j);
if not !SKIP! then
  begin outstr(CRLF&"[Can't open DSK]"&BEL);
        call(1,"EXIT");
  end;
gn _ "gn"&instr&".cmd";
lookup(i,gn,j);
if not j then
  begin outstr("Rewrite [Y/N]? ");
        if (base _ inchwl) neq "Y" and
           base neq "y" then call(1,"EXIT")
        else rew _ TRUE;
  end;
if rew then rename(i,NULL,0,j);
  begin enter(i,gn,j);
        if j then
          begin outstr(CRLF&"[Can't write "&gn&"]"&BEL);
                call(1,"EXIT");
          end;
        cprint(i,
        "daytime Command file to create files for node "&instr&CRLF&
        "delete ##"&instr&".mac,##"&instr&".cmd -gn"&instr&".cmd"&CRLF&
        "run (11tools)gensat"&crlf&
        instr&CRLF)
  end;
while true do
  begin outstr("Dzv-11 ports < '4','8','12','16' >=");
        if equ(mc _ inchwl,"4") or equ(mc,"8") or equ(mc,"12") or
           equ(mc,"16") then
          begin j _ cvd(mc);
                done;
          end
        else outstr(BEL&"[Wrong, Enter 4,8,12, or 16]"&crlf);
  end;
while true do
  begin print(" Dialups          [300/1200 baud--TID  selectable],"&crlf);
        print("     OR"&crlf);
        print(" Hardwires        [300 to 9600 baud--^brk/cr selectable]:"&crlf);
        print(" < 'DU' , 'HW' > ");
        wm := INCHWL;
        if equ(wm,"DU") or equ(wm,"HW") then done
  end;
while true do
  begin print("Disconnect on break key < 'Y' , 'N' > :");
        dbrk := INCHWL;
        if dbrk[1 TO 1]="Y" then begin dbrk:="YES";done end
                            else
                              if dbrk[1 TO 1]="N" then
                                begin dbrk:="NO";done end
  end;
while true do
  begin outstr("Neighbour node = ");
        if not equ(lk _ inchwl,NULL) then done
        else outstr(BEL&"[Wrong, Enter neighbour node number]"&CRLF);
  end;
while true do
  begin outstr("Sync line speed (2400,4800,9600) = ");
        if equ(ru _ inchwl,"2400") then done
        else if equ(ru,"4800") then     done
        else if equ(ru,"9600") then     done
        else 
          begin outstr(BEL&
               "[Wrong, Enter sync line speed(2400,4800,9600)]"&CRLF);
                continue;
          end;
  end;
while true do
  begin outstr("Line groups = ");
        if not equ(cg _ inchwl,NULL) then done
        else outstr(BEL&"[Wrong, Enter number of groups]"&CRLF);
  end;
cprint(i,
        mc&CRLF&wm&CRLF&dbrk&CRLF&
        lk&CRLF&
        ru&CRLF&
        cg&CRLF&
        "run (11tools)macn11"&CRLF&
        "mc"&instr&".cmd@"&CRLF&
        "run (11tools)link11"&CRLF&
        "lk"&instr&".cmd@/e"&CRLF&
        "run (11tools)nibtrn"&CRLF&
        "SV"&instr&".IMG"&CRLF&
        "SV"&instr&".NIB"&CRLF);
outstr("Leave only the NIB and CORE files? (Y/N) - ");
        if (equ(base_ inchwl,"Y" )) or (equ(base,"y" )) then
        cprint(i,
        "delete mc"&instr&".*,cg"&instr&".*,ru"&instr&".*,lk"&instr&".*"&CRLF&
        "dir ##"&instr&".*"&CRLF);

close(i);
release(i);
print( "Respond to file request - gn",instr,".cmd");
run (runfile);
print(crlf&"Shouldn't get here! Check to see if you have RF license set!");
call(1,"EXIT")
END
END "gennod";
8 ‘