Title	SAIARR	- Module to define external locations for SAIXXX
Subttl	SPOOL SYSTEM map pages

Define Fake( Nam, Dim )<;;		;; Fake array descriptors for SAIL

Entry	Nam'adr, Nam'rec		;; Internal integer, integer-array

Nam'adr:	Exp	Nam'rec + <<Nam'siz*DimSiz>+DimLen>
	Exp	-<1001+<<Nam'siz*DimSiz>+DimLen>>
	Exp	Nam'rec + <<Nam'siz*DimSiz>+DimLen> - Nam'Bas

$Dims$==0;;				;; Number of dimensions
$Base$==0;;				;; Base offset for calculations
$Mult$==1;;				;; Multiplier for calculations

Irp Dim,<;;				;; Once through for each half-dimension
    $Dims$==$Dims$+1			;; Count
    Ifn <$Dims$&1>,<$Low$==Dim>		;; First half? -- Only do assignment
    Ife <$Dims$&1>,<;;			;; Second half -- Expand macro
    $Top$==Dim
	Exp	$Low$,$Top$,$Mult$	;; Low, Top, Mult
    $Base$==$Base$+<$Mult$*$Low$>	;; Add to base calculation
    $Mult$==$Mult$*<1+$Top$-$Low$>	;; Add to size calculation
    >;;					;; Done argument expansion
>
    Nam'Bas==$Base$			;; Define base offset
    Nam'Siz==$Dims$/2			;; Define dimension count
	Xwd	Nam'siz,$Mult$		;; Xwd  Dims,Size

Nam'rec==<<Base+1>*1000>-<<Nam'siz*DimSiz>+DimLen>
Base==Base+2				;; Increment base address

	Purge	$Base$,$Base$,$Dims$,$Dims$,$Mult$,$Mult$
	Purge	$Low$,$Low$,$Top$,$Top$
>

Define	Setup( Arg )<
Entry	SAIadr, SAIknt, SAIkmx		; Page address list for descriptors

DimLen==4				; Base preface for any sail array
DimSiz==3				; Additional size for each dimension
SAIadr:	Exp	SAILST			; pointer to data list
	Exp	SAIadr-SAIend-1		; formula from SAIL manual
SAIhed:	Exp	SAILST-<1*1*1>		; baseoffset <string+1>*<low>*<mult>
	Exp	1,SAIknt,1		; triple lower,upper,multiplier
SAIkmx==.-2
	Xwd	1,SAIknt		; dims ,, size-of-this-array
SAIlst:	Irp Arg,< Arg'adr,,Arg'rec >	; Table of BLT words
SAIknt==.-SAIlst			; length of setup array
SAIend:	400000,,SAIadr			; pointer to array header
>

Base==740				; Base page
	Setup	<PAG>	; Order of pages - same as FAKEs
	Fake	PAG,< 0,777 >		; Miscellaneous page
Xpunge
End
