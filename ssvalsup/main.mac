SUBTTL MAIN - add and cancel commands
DEFINE ADD1 <
	X MAIL,1,CMDNUS,-1,-1	      ; add mail
 >

	SUBCOM	([ASCIZ/Add :/],ADD,ADD1)
CMDNUS: ; new user 
	TCR	(1)
        PUSHJ   GETCUD
         JRST   GETCMD
        PUSHJ   GETACC
         JRST   GETCMD
        MOVEI   AC,^D30
        MOVEM   AC,LUDNUM
        MOVEM   AC,HOST
        PUSHJ   P,GETOLD
         JRST   GETCMD
        MOVE    AC1,OLDASY
        PUSHJ   P,GETUNM
         JRST   GETCMD
        PUSHJ   P,GETPSW
         JRST   GETCMD
        PUSHJ   P,GETCOC
        PUSHJ   P,GETTMZ
; setup NMCPAR, NMCOLN, NMIRC, NMDIST and NMCLSS
        MOVEI   AC1,^D229
        MOVEM   AC1,NMDIST
        SETZM   AC1
        SETZM   NMCPAR
        TLO     AC1,BIL.CD            ; set no-billable
        LSH     AC1,-^D16
        MOVEM   AC1,NMCOLN
        SETZM   NMIRC
        MOVEI   AC1,2
        MOVEM   AC1,NMCLSS
        MOVEI   AC1,D.CLS             ; set class 2
        MOVEM   AC1,NMCLSS+1
        MOVEI   AC1,D.GRP             ; set group 1
        TRO     AC1,GRP.CD            ; set group indicator bit
        MOVEM   AC1,NMCLSS+2
        MOVE    AC1,HOST
        TRO     AC1,HOM.CD            ; set home host indicator bit
        MOVEM   AC1,NMCLSS+3
 
; add new user to cud
        PUSHJ   P,NUSMUD
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
         JRST   NUSRE1
        GETBYT  (CUDPRT)
         JRST   GETCMD
        GETBYT  (CUDPRT,\B.GAN)       ; get gan
         JRST   GETCMD
        GETBYT  (CUDPRT)
         JRST   GETCMD
        GETBYT  (CUDPRT,\B.UUN)       ; get uun for new user
         JRST   GETCMD
        MOVEM   AC1,NMUUN
        PUSHJ   P,GET40
        TCR     (1)
        TYPSTR  ([ASCIZ/********** The Uun for /],777,0,T.P0,N)
        TYPSTR  (UNMSPC,L.UNM,1,0,N)
        TYPSTR  ([ASCIZ/is /],777,1,0,N)
        TYPNUM  (NMUUN,O,5)
 
; new user for the accounting system type
        MOVEI   AC1,5
        MOVEM   AC1,UAPRIC            ; assign it to user
        MOVEI   AC1,1
        MOVEM   AC1,ACTSY2
        MOVEM   AC1,UALANC
        MOVEM   AC1,UACURC
        MOVEM   AC1,UARESC
        MOVEI   AC1,^D13516
        MOVEM   AC1,CUSNUM
        MOVEI   AC1,0
        MOVEM   AC1,UASLS
        PUSHJ   P,NUSACC
        PUSHJ   P,AGANS
         JRST   NUSRE2
 
; add user in lud 
  
        SETZ    AC,
        TLO     AC,TYM.LX
        TRO     AC,1B23               ; give Xexec mode
        MOVEM   AC,LUXPRV
        MOVEI   AC,^D200              ; 200/4 = 50 pages
        MOVEM   AC,LUXQIN
        MOVEM   AC,LUXQOT
        PUSHJ   P,ADD10
        PUSHJ   P,OLDA
         JRST   NUSRE3
        MOVE    AC1,OLDANS
	PUSHJ	P,LUDAOT
NEUSRD:	; done, successful completion
	TRO	FLG,U.OKAY
	POPJ	P,

 
NUSMUD:
        SNDBYT  (CUDPRT,\B.CCOM,NEWMUD,I)   ; ask for 17-bit uun
        PUSHJ   P,SNMCUD
        SNDBYT  (CUDPRT,\B.CYP,NMPSW) ; send password cipher
        SNDBYT  (AC,1,0,I)
        SNDBYT  (AC,\B.GAN,MY.GAN)
        SNDBYT  (AC,1,0,I)
        SETZM   NMUUN
        SNDBYT  (AC,\B.UUN,NMUUN)     
        SNDBYT  (AC,\B.CPR,NMCPAR)
        SNDBYT  (AC,\B.CLN,NMCOLN)
        SNDBYT  (AC,\B.CDST,NMDIST)   ; NMDIST came from old user
        SNDBYT  (AC,\B.IRC,NMIRC)
        SNDBYT  (AC,1,NMCLSS)
        SNDBYT  (AC,2,NMCLSS+1)
        SNDBYT  (AC,2,NMCLSS+2)
        SNDBYT  (AC,2,NMCLSS+3)
        POPJ    P,
 
NUSACC: SNDBYT  (ACTPRT,\B.ACOM,NUSACT,I)  ; new user in acctg.
        SNDBYT  (AC,\B.GAN,MY.GAN)
        SNDBYT  (AC,\B.UUN,NMUUN)
        SNDBYT  (AC,\B.ACOM,ACTSY2)
        SNDSTR  (AC,L.UNM,UNMSPC)
        SNDBYT  (AC,\B.CID,CUSNUM)
        SNDBYT  (AC,\B.DIS,NMDIST)
        SNDBYT  (AC,\B.LAN,UALANC)
        SNDBYT  (AC,\B.CUR,UACURC)
        SNDBYT  (AC,\B.SLS,UASLS)
        SNDBYT  (AC,\B.PRI,UAPRIC)
        SNDBYT  (AC,\B.TZ,UATZ)
        SNDBYT  (AC,\B.RES,UARESC)
        SNDBYT  (AC,\B.HST,HOST)
        SNDBYT  (AC,\B.CC,ACCCC)
        POPJ    P,
 
 
NUSRE1: ; error during add new user into the mud
        TYPSTR  ([ASCIZ/Error during Add New User in the Cud./])
        PUSHJ   P,ERRCUD
        JRST    GETCMD

NUSRE2: ; error during add new user in acctg.
        TYPSTR  ([ASCIZ/Error during Add User in Acctg./])
        MOVE    AC1,ACANS
        PUSHJ   P,LUDAOT
        SNDBYT  (CUDPRT,\B.CCOM,REVMUD,I)   ; remove user without telling pj
        PUSHJ   P,SNMCUD
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
         JRST   [TYPSTR  ([ASCIZ/Remove User not Working in the CUD!/])
                 JRST    ERRCUD]
        PUSHJ   P,GET40
        JRST    GETCMD
 
NUSRE3:	; error during add user in lud
	TYPSTR	([ASCIZ/Error during Add User in the Lud./])
	MOVE	AC1,OLDANS
	PUSHJ	P,LUDAOT
        SNDBYT  (CUDPRT,\B.CCOM,REVMUD,I)   ; remove user without telling pj
        PUSHJ   P,SNMCUD
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
         JRST   [TYPSTR  ([ASCIZ/Remove User not Working in the CUD!/])
                 JRST    ERRCUD]
        PUSHJ   P,GET40
        SNDBYT  (ACTPRT,\B.ACOM,REVACT,I)  ; reverse last acctg. entry
        PUSHJ   P,AGANS
         JRST   [TYPSTR  ([ASCIZ/Reverse Acctg. Entry not Working!/])
                 MOVE    AC1,ACANS
                 PUSHJ   P,LUDAOT
                 JRST    GETCMD]
        JRST    GETCMD
 
; end of code for add mailname
 
GETCOC:
        INNUM   ([ASCIZ/Cost Code :/],D)
         JRST   GETCO2
        CAIL    AC1,^D9999
         JRST   GETCO2
        CAIG    AC1,^D1000
         JRST   GETCO2
        MOVEM   AC1,ACCCC
        POPJ    P,
   
GETCO2:
        TYPSTR  ([ASCIZ/Cost Code out of range./])
        PUSHJ   P,INORNO
        JRST    GETCOC
 
DEFINE CNC1 <
        X MAIL,1,CMDCAU,-1,-1!U.DIS,-1    ; cancel mailname
>

	SUBCOM	([ASCIZ /Cancel :/],CNC,CNC1)
CMDCAU: ; cancel mailname
	TCR	(1)
        PUSHJ   P,GETCUD
         JRST   GETCMD
        PUSHJ   P,GETACC
         JRST   GETCMD
        MOVEI   AC1,^D30
        MOVEM   AC1,HOST
        MOVEM   AC1,LUDNUM
        PUSHJ   P,GETOLD
         JRST   GETCMD
        MOVE    AC1,OLDASY
        PUSHJ   GETUNM                ; get username
         JRST   GETCMD
        PUSHJ   GCREC1
         JRST   CMDCAU
 
 
; cancel user from accounting in both userdb and tuserdb (if any)
; when cancel user in userdb, always define ludnum = 1 (or any valid
; host number)
     
 
        SNDBYT  (ACTPRT,\B.ACOM,CAUACT,I)  ; remove user in userdb
        SNDBYT  (AC,\B.UUN,NMUUN)     ; send uun
        SNDBYT  (AC,\B.HST,LUDNUM)
        PUSHJ   P,AGANS
         JRST   CAUSE2
 
        ; remove user from pdp10 lud
 
	SNDBYT	(OLDPRT,\B.LCOM,REMLUD,I)	
        SNDSTR  (AC,L.UNM,UNMSPC)
        PUSHJ   P,OLDA
         JRST   CAUSE4
        GETBYT  (OLDPRT,\B.GAN)
         JRST   GETCMD
        GETBYT  (OLDPRT,\B.UUN)
         JRST   GETCMD
 
; remove user from mud
 
CAUMUD: SNDBYT  (CUDPRT,\B.CCOM,RUSMUD,I)
        PUSHJ   P,SNMCUD
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
         JRST   CAUSE5
        PUSHJ   P,GET40
 
CAUSTD:	; done, successful completion
	TRO	FLG,U.OKAY
	POPJ	P,
 
LUDBAD: TYPSTR  ([ASCIZ/error in writing LUDUPD.DEL file./])
        TRZ     FLG2,V.LUD
        JRST    GETCMD
 
CAUSE2:	; error during delete user host in accounting
	TYPSTR	([ASCIZ/Error during Cancel User in Accunting./])
	MOVE	AC1,ACANS
	PUSHJ	P,LUDAOT
        JRST    GETCMD
 
CAUSE4:	; error during cancel user from the lud
	TYPSTR	([ASCIZ/Error during Cancel User from the Lud./])
	MOVE	AC1,OLDANS
	PUSHJ	P,LUDAOT
        SNDBYT  (ACTPRT,\B.CCOM,REVACT,I)   ; reverse last acctg. entry
        PUSHJ   P,AGANS
         JRST   [TYPSTR  ([ASCIZ/Reverse Acctg. Entry not Working!/])
                 POPJ    P,]
        JRST    GETCMD

CAUSE5:	; error during cancel user in the mud
	TYPSTR	([ASCIZ/Error during Cancel User Host in the Cud./])
        PUSHJ   P,ERRCUD
        SNDBYT  (ACTPRT,\B.CCOM,REVACT,I)   ; reverse last acctg. entry
        PUSHJ   P,AGANS
         JRST   [TYPSTR  ([ASCIZ/Reverse Acctg. Entry not Working!/])
                 POPJ    P,]
        JRST    GETCMD
 
        ; end of cancel mailname
  
ADD10:  ; add user in pdp10 lud
 
	SNDBYT	(OLDPRT,\B.LCOM,ADDLUD,I)	
        SNDSTR  (AC,L.UNM,UNMSPC)
        SNDBYT  (AC,\B.GAN,MY.GAN)
        SNDBYT  (AC,\B.UUN,NMUUN)
        SNDBYT  (AC,\B.DIS,NMDIST)
        MOVE    AC1,LUXPRV
        LSH     AC1,-^D12
        MOVEM   AC1,LUXPRV
        SNDBYT  (OLDPRT,\B.PRV,LUXPRV)
        SNDBYT  (AC,\B.TZ,UATZ)
        SNDBYT  (AC,\B.QUOT,LUXQIN)
        SNDBYT  (AC,\B.QUOT,LUXQOT)
        POPJ    P,
 
 
GETTMZ: ; get UATZ for both accounting and lud
        INSTR   ([ASCIZ/Time Zone :/],TZSAV,B.MDI)  
        ; borrow B.MDI = 3 for only taking first 3 bytes from input
        MOVE    AC,TZSAV
        MOVE    AC1,[-TMZTLN,,0]
TMZLP:
        CAMN    AC,TMZTB(AC1)
         JRST   TMZFD                       ; match
        AOBJN   AC1,TMZLP                   ; add 1 on ac1 both halves
                                            ; and back to TMZLP if left half
                                            ; is not zero
        TYPSTR  ([ASCIZ/Options are : /])
        TYPSTR  (TMZTB)
        JRST    GETTMZ
 
TMZFD:
        MOVE    AC,TMZNU(AC1)
        MOVEM   AC,UATZ
        GETYR   ([ASCIZ/Subject to Daylight Savings ? /])
         JRST   [MOVEM   AC,ANS1            ; save no
                 POPJ    P,]
        MOVEM   AC,ANS1                     ; save yes
        MOVE    AC1,UATZ
        TRO     AC1,TZMASK                  ; set daylight savings bit
        MOVEM   AC1,UATZ
        POPJ    P,
 
TMZTB:  ASCII/HST  /
        ASCII/PST  /
        ASCII/MST  /
        ASCII/CST  /
        ASCII/EST  /
        ASCII/AST  /
        ASCII/GMT  /
        ASCII/WET  /
        ASCII/CET  /
        ASCII/JST  /
        ASCII/BST  /
TMZTLN==.-TMZTB
TMZNU:   6
         ^D8
         ^D9
         ^D10
         ^D11
         ^D12
         ^D16
         ^D16
         ^D17
         ^D25
         ^D26
  
