TITLE	LUDINF - Get LUD information

; CALL LUDINF(IFLAG,SIXUNM,IGAN,IUUN,IDIST,INITFN)
; IFLAG is -1 if record not found in LUD.SYS or 0 if successful.
; SIXUNM is an array [1..2] of integer
; IGAN, IUUN, and IDIST are integer variables
; INITFN is an array [1..3] of integer

	ENTRY	LUDINF

AC==	0
T1==	1			; FOR HASH ROUTINE
T2==	2			; FOR HASH ROUTINE
T3==	3			; FOR HASH ROUTINE
T4==	4			; FOR HASH ROUTINE
A==	5			; FOR HASH ROUTINE
B==	6			; FOR HASH ROUTINE
C==	7			; FOR HASH ROUTINE
D==	10			; FOR HASH ROUTINE
E==	11			; FOR HASH ROUTINE
AC12==	12			; USED FOR INDEXOR
AP==	16
P==	17

LUDINF: MOVE	AC,[2,,SAVREG]
	BLT	AC,SAVREG+17
	HRROI	T1,-22		; TO GET USER NAME
	HRROI	T2,-21		; TO GET USER NAME
	GETTAB	T1,		; GET USER NAME
	SETZ	T1,
	GETTAB	T2,		; GET USER NAME
	SETZ	T2,
	HRRZI	T3,@1(AP)	; Return Login username
	DMOVEM	T1,@T3

;	ENTER HASH ROUTINE TO GET THE DISTRICT OF THIS USER
;	AFTER HASHING, T1 WILL HAVE THE HASH USER NAME
;	AND T2 WILL HAVE THE BLOCK IN THE LUD
;	WHERE THE HASHED USER CAN BE FOUND
ALLDIS: SETZ	B,
	MOVE	C,[555555555555]
	MOVE	D,[361275431652]
	MOVE	E,[612754316523]
	MOVEI	AC12,4		; NO. TIME TO DO NEXT LOOP
HASH1:	ADD	D,T1
	ROTC	T1,-22
	MOVEI	A,5
RND1:	MOVE	T3,D(B)
	MUL	T3,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM	T4,E(B)
	AOJE	B,RND2
	MOVNI	B,1
	TRNE	D,1
	SKIPL	E
	MOVEI	B,0
	EXCH	C,E
RND2:	SOJG	A,RND1
	SOJG	AC12,HASH1
	XOR	E,D
	MOVE	T1,E
	TLZ	T1,400000
	IDIVI	T1,^D887
	ADDI	T2,1
	XOR	C,E
	MOVE	T1,C
;	NOW HAVE HASH USER NAME IN T1 AND BLK OF LUD IN T2
;	GO INTO LUD AND GET THE DISTRICT
	TEMP= 3
	INITFN= 4		; CONTAINS ADDRESS OF BUFFER
	INDEX= 5		; INDEXER INTO BUFFER
	SIZE= 6			; SIZE OF EACH ENTRY
	SETZB	TEMP,LUDFIL+3
	INIT	1,10		; SET UP CHANNEL
	SIXBIT /SYS/
	XWD	LUDRBH
	JRST	.-3
	MOVE	AC,[400000,,LUDRB1]
	MOVEM	AC,LUDRBH
	MOVE	AC,[201,,LUDRB1]
	MOVEM	AC,LUDRB1
	LOOKUP	1,LUDFIL	; LOOKUP THE LUD FILE
	JRST	LUDERR
GG1:	USETI	1,@T2		; POINT TO CORRECT BLOCK TO READ
	INPUT	1,		; READ THE BLOCK
	STATZ	1,340000	; CHECK THE STATUS
	JRST	LUDERR
	SETZ	INDEX
GG2:	MOVE	AC,LUDRBD(INDEX)	; GET PPN [GAN,,UUN]
	JUMPL	AC,[HRRZ  T2,AC		; Get next block to read
		    JRST  GG1]		; Go to read next block
	JUMPE	AC,LUDERR		; End of block, Username not found
	CAMN	T1,HUSERN(INDEX)	; See if Hash Username match
	JRST	FOUNDR			; Branch if match
	MOVE	AC,ENTRYS(INDEX)	; Get size of entry
	ANDI	AC,177			; Mask off size of entry
	ADD	INDEX,AC		; Increment INDEX by size of entry
	JRST	GG2			; Go to look at next entry
FOUNDR: SETZM	@0(AP)			; Zero flag
	MOVE	AC,LUDRBD(INDEX)	; Get PPN [GAN,,UUN]
	HLRZM	AC,@2(AP)		; Return GAN
	HRRZM	AC,@3(AP)		; Return UUN
	MOVE	AC,LUDDIS(INDEX)	; Get the district
	ANDI	AC,377			; Mask off the district no.
	MOVEM	AC,@4(AP)		; Return the district to caller
	MOVE	INITFN,ENTRYS(INDEX)
	TRNN	INITFN,1B27		; Init file?
	JRST	NOINIT			; No
        MOVEI   AC,LUDRBD+5             ; Get init filename and directory
	ADD	AC,INDEX
        TLNE    INITFN,(1B4)            ; Is there a TRU Budget word?
        AOJ     AC,                     ; Yes, increment to skip it
	HRLZS	AC
        HRRZI   TEMP,@5(AP)            ; Prepare to return init filename
        HRR     AC,TEMP
        ADDI    TEMP,2
        BLT     AC,(TEMP)
LEAVE:	RELEASE 1,		; RELEASE THE CHANNEL
	MOVE	AC,[SAVREG,,2]
	BLT	AC,17
	POPJ	P,		; RETURN
LUDERR: SETOM	@0(AP)
        SETZM	@1(AP)
        SETZM	@2(AP)
        SETZM	@3(AP)
        SETOM	@4(AP)
        SETZM	@5(AP)
	JRST	LEAVE
NOINIT: SETZM   @5(AP)
        JRST    LEAVE
SAVREG: BLOCK	17
LUDRBH: BLOCK	3
LUDRB:	0
LUDRB1: XWD	201,LUDRB1
LUDRB2: 0
LUDRBD: BLOCK	200
LUDDIS==LUDRBD+1
ENTRYS==LUDRBD+2
HUSERN==LUDRBD+4
LUDFIL: SIXBIT	/LUD/
	SIXBIT	/SYS/
	0
	1,,4
	END
