IFNDEF  DEBUG,<
TITLE	TYMVAL - Tymnet Validation Program.
>
IFDEF   DEBUG,<
TITLE	TYMDEB - (Debug Version)
>
SUBTTL DEFS - EQUATES
SALL

SEARCH UUOSYM(DSK:UUOSYM.UNV)
SEARCH SLVCOD(DSK:SLVCOD.UNV)

.REQUIRE LUDINF
.REQUIRE FNP
;
;  1.0  (05/30/86) - Release to alpha test.
;
;  1.01 (07/02/86) - Release to beta test & make the following changes:
;      1. No prompt for the mailname and  check district when change,
;         add, or remove user class from/to the MUD.
;      2. When a series of requests are submitted, the  program  only
;         asks to input name and phone number for the 1st request and
;         all the following requests will  have same phone number and
;         name.
;      3. Output to terminal the description of the invoice code when
;         the invoice code is input.
;      4. Correct TYMVAL generated messages.
;
;  1.02 (08/19/86) - Add CHANGE MAILNAME PASSWORD command so that users
;                    have ability to change their mailname passwords at 
;                    the TYMVAL prompt.
;
;  8.10 (11/17/86) - Release to TYMNET validations and customer.
;
;  8.11 (01/07/87) - Merge customer version and validation version.
;                  - Correct all bugs that version 8.10 had.
;
;  8.12 (03/03/87) - Fix "REQUES.DIR file busy" problem.
;                  - Check the new password entered by the user with the
;                    previous password and reject it if identical.
;                  - Prompt for Colon Option in NEW USERNAME command if
;                    the entered seed username has Colon Option.
;
;  8.13 (03/11/87) - Replace Monitor-Constructed Buffer Rings with
;                    User-Constructed Buffer Rings.
;
;  8.14 (03/25/87) - Check for invalid character in usernames.
;                  - Implement CHANGE GAN and LIST ALL PROFILE command.
;                  - Add/Remove multiple hosts to a mailname at one time.
;                  - Modify CHANGE CUSTOMER USERNAME command: change the
;                    user's district in the CUD if new customer's district
;                    and user's district are different.
;
;  9.01 (05/06/87) - Allow Tymnet's Validations to specify, by GAN, that
;                    all passwords must have at least one non-alpha char
;                    If this option is on for the GAN, users will not be
;                    able to set a password without a non-alpha char.
;
;                  - Allow Tymnet's Validations to specify, by GAN, the
;                    minimum password length (d= 3)
;
;       (06/15/87) - Timeout feature: Tymvalidate will automatically log
;                    the user off if it is not in use.
 
REL==^D9			; Release number
VER==1				; Version number
IF2	<
	LOC .JBVER
IFNDEF	DEBUG,<
	XWD	REL,VER>
IFDEF	DEBUG,<
	OCT	-1>
	RELOC
>
IF1	<
	IFDEF	DEBUG,<
	PRINTX ** ASSEMBLING DEBUG CODE ** >
	IFDEF TEST, <
	PRINTX ** ASSEMBLING TEST CODE ** >
	IFNDEF TEST, < IFNDEF DEBUG, <
	PRINTX ** ASSEMBLING PRODUCTION VERSION ** > >
>
IF2	<
	PRINTX PASS 2 >

	TWOSEG	400000
	EXTERNAL LUDINF,FNPAR		; EXTERNAL ROUTINES
 
; ACCUMULATOR DEFINITIONS

AC==	0			; TEMP OR SUB ARG HOLDER
AC1==	1			; TEMP
AC2==	2			; TEMP
AC3==	3			; TEMP
AC4==	4			; TEMP
FLG=	5			; PROCESS FLAGS WORD
FLG2==	6			; ANOTHER PROCESS FLAGS WORD
LPOSL== 7
ESCCHR==10			; ESCAPE CHARACTER
IF.P==	11			; INPUT FILE INDEX
SAC==	12			; PLACE TO SAVE AC (OR AC1, AC2, AC3, AC4)
LPOST== 13			; CHAR POSITION ON TTY LINE
LPOSF== 14			; CHAR POSITION ON TOUT FILE LINE
AP==	16			; ARGUMENT BLOCK POINTER
ACX==	16			; AP AND ACX ARE THE SAME REGISTER
P==	17			; STACK POINTER

; Bit definitions for FLG (left half)
; All bits checked by the command parsers must be in left half

U.PROG==(1B0)			; Programmer license
U.NET== (1B1)			; Tymnet validator license
U.TR6== (1B2)			; TYMRES6
U.CUST==(1B3)			; Tymnet customer license
U.CUS1==(1B4)			; Customer has TYMRES6 GAN
U.IMM== (1B5)			; Executing in immediate mode
U.TOUT==(1B6)			; We are doing output to a file
U.NOTY==(1B7)			; We are suppressing TTY output
U.CRLF==(1B8)			; Follow output with a CRLF
U.FTTY==(1B9)			; Force TTY output regardless of U.NOTY
U.IND== (1B10)			; Input from a command or inderect file
U.IEOF==(1B11)			; End of file found on terminal input device
U.EOL== (1B12)			; Last terminal input was end of line
U.IFL== (1B13)			; Tymvalidate is started by initial file
U.WC==	(1B14)			; Write Core license
U.TOF== (1B15)			; Timeout flag
U.PRV==U.PROG!U.NET!U.TR6
U.CST==U.CUST!U.CUS1
U.ALL==U.CUST!U.NET!U.TR6!U.CUS1

; BIT DEFINITIONS FOR FLG (RIGHT HALF)

U.PEK== 1B18			; WE HAVE PEEKED AHEAD ONE CHARACTER
U.NOES==1B19			; LOGICAL ESCAPE NOT ALLOWED
U.ACT== 1B20			; WE HAVE AN ACCOUNTING CIRCUIT
U.CUD== 1B21			; WE HAVE A CUD CIRCUIT
U.NNIB==1B23			; DO NOT GET A NEW BUFFER AFTER EOF ON INPUT
U.HOM== 1B24			; GROUP OR CLASS IS HOME
U.OKAY==1B25			; LAST COMMAND COMPLETED OK
U.UDF== 1B26			; PROCESSING A SELF DEFINING CLASS
U.OVF== 1B27			; STRING OR TOKEN INPUT WAS TRUNCATED
U.LFO== 1B28			; WE ARE DOING LIST TO FILE OUTPUT
U.C377==1B29			; STOP INPUT BYTES IF WE RECEIVE A 377 ON G#BYT
U.SIX==	1B30			; SIXBIT CONVERSION ON NUMERIC TO CHAR
U.SBL==	1B31			; SUPPRESS EXTRA SPACE AFTER TT FILE OUTPUT
U.NTT==	1B32			; OPENING A NEW TATTLE TALE FILE.
U.RFI==	1B33			; TERM INPUT IS COMING FROM A REQUEST FILE.
U.ASC== 1B34			; SEND ASCII CHARACTER

; BIT DEFINITIONS FOR FLG2 (LEFT HALF)

V.NPS==	(1B0)			; SPACE OR NUMERIC SEEN IN PASSWORD.
V.NPJ==	(1B1)			; NON ALPHA CHAR IN PSWD JUST SEEN.
V.NPF==	(1B2)			; THIS IS THE FIRST CHAR IN PASSWORD.
V.HDX==	(1B3)			; OUR TERMINAL IS HALF DUPLEX
;V.QUO==	(1B5)			; QUOTING NEXT CHAR
V.ESC==	(1B6)			; ESCAPE TYPED.
V.INP==	(1B7)			; DOING INPUT VIA STRIN ROUTINE
;V.INI==	(1B8)			; PROCESSING INIT FILE
V.PRQ==	(1B9)			; PROCESSING A REQUEST
V.EXQ==	(1B10)			; EXAMINING A REQUEST
V.LSQ==	(1B11)			; LISTING A REQUEST
V.NBU==	(1B12)			; NO REQUEST BACKUP FILE TO DELETE
V.EXC==	(1B13)			; CHANGING ITEMS DURING REQUEST EXAM
V.CHG==	(1B14)			; REQUEST CHANGED DURING EXAM
V.RER==	(1B15)			; ERROR SEEN DURING REQUEST INPUT
V.FUSR==(1B16)			; FIRST USER IN A NEW ACCOUNT
V.NWAC==(1B17)			; NEW ACCOUNT

; BIT DEFINITIONS FOR FLG2 (RIGHT HALF)

V.INS==	1B18			; INPUTTING A WHOLE LINE
V.1R==	1B19			; EXAMINING, APPROVING, OR PROCESSING 1 REQUEST
V.NIR==	1B20			; (A REQUEST) OR (NOT IMMEDIATE MODE)
V.URC== 1B21			; FLAG FOR OUTPUT USERNAME, REQ NO OR CUST. NO.
V.NTE== 1B22			; FLAG FOR NO TYPING ERROR MESSAGE
V.FRQ== 1B23			; FLAG FOR 1ST INPUT REQUEST
V.LCM== 1B24			; DOING THE LIST COMMAND
V.HIC== 1B25			; DOING THE HELP OR INSTRUCTIONS COMMAND
V.NNR== 1B26			; NON-NUMERICAL CHARACTER FLAG
V.PSW== 1B27			; PASSWORD INPUT FLAG
V.SCL== 1B28			; Set if password contains semicolon
V.CUP== 1B29			; Flag for Change Password Command.
V.NAO== 1B30			; Non-Alpha character require in password
V.HDR== 1B31			; Heading flag
V.LAG== 1B32			; List All GanPass

; BIT DEFINITIONS FOR WORDS IN REQUES.DIR

R.NEX== (1B0)			; THIS REQUEST NEEDS EXAM BY TYMNET VALIDATIONS
R.RDY== (1B1)			; THIS REQUEST READY TO PROCESS
R.HLD== (1B2)			; THIS REQUEST IS IN HOLD STATUS
R.COM== (1B3)			; THIS REQUEST IS COMPLETED.
R.CAN== (1B4)			; REQUEST IS CANCELLED BY TYMNET VALIDATIONS
R.UCN== (1B5)			; THIS REQUEST IS CANCELLED BY OWNER
R.ERR== (1B6)			; THIS REQUEST HAS AN ERROR CONDITION
R.LCK== (1B7)			; LOCKED REQUEST
R.USE== (1B8)			; This slot in REQUES.DIR being use
R.NAC== R.CAN!R.UCN!R.COM	; INACTIVE REQUEST (COMPLETED OR CANCELLED)
R.NAC2==R.NAC!R.HLD		; INACTIVE REQUEST OR IN HOLD

; THE RIGHT HALF OF THE WORD IS RESERVED FOR THE UUN OF THE ORIGINATOR

; DEFINITIONS NOT IN UUOSYM

LC.WF== 2B17			; WRITE FILES LICENSE
LC.CS== 1B15			; CS LICENSE FROM PASSWORD FILE
LC.AP== 1B14			; AP LICENSE FROM PASSWORD FILE
LC.VL== 1B10			; VALIDATOR LICENSE
IA.CLR==4			; CLEAR INTADR
IA.REE==2			; (RE-)ENABLE INTADR
IA.DIS==1			; DISABLE INTADR

; DEFINITIONS FOR HIBER

HI.SEC==1			; TIME IN SECONDS
HI.CHR==10			; RETURN WHEN CHAR ARRIVES
HI.AUX==100			; AUX PORT SPECIFIED
HI.WFI==HI.SEC!HI.CHR!HI.AUX	; TIME IN SEC, WAIT FOR CHAR, AUX PORT
HI.SHF==^D27			; BITS TO SHIFT PORT NUMBER FOR HIBER
HI.WAI==^D20			; WAIT TIME
SL.WAI==^D10			; SLEEP TIME

; ITEM LENGTH DEFINITIONS

B.CC==3				; COST CODE
B.CDST==2			; DISTRICT IN CUD
B.CLN==1			; NMCOLN
B.IRC== 1			; IRC NUM
B.CPR== 2			; CPARW
B.PDT== 3			; PASSWORD DATE STAMP
B.ASY== 1			; ACT SYS TYPE
B.FOCF==1			; FLAT FOCUS FEE
B.UUN== 3			; UUN IS 3 BYTES
B.GAN== 3			; GAN IS 3 BYTES
B.940W==3			; 940 WORD SIZE
B.SLS== 1			; SALESMAN
B.PRI== 2			; DEFAULT PRICECODE
B.VPRI==1			; VARIABLE PRICING FLAG
B.DET== 2			; DETAIL CODE
B.INV== 2			; INVOICE CODE
B.MIN== 1			; MINIMUM CODE
B.POMX==3			; POMAX
B.PODT==3			; PO START AND STOP DATES
B.RES== 1			; RESOURCE CODE
B.CUR== 1			; CURRENCY CODE
B.LAN== 1			; LANGUAGE CODE
B.TZ==	1			; TIME ZONE
B.HST== 2			; HOSTS
B.DIS== 1			; 1 BYTE FOR DISTRICT
B.CYP== 4			; LENGTH OF PASSWORD CIPHER
B.ACOM==1			; ACCOUNTING COMMAND IS ONE BYTE
B.CCOM==1			; CUD COMMAND IS ONE BYTE
B.CLS== 2			; SIZE OF CLASS
B.GRPH==2			; SIZE OF HOST OR GROUP
B.CID==3			; CID
L.UNM== ^D12			; SIZE OF USERNAME
L.CUSN==^D51			; SIZE OF CUSTOMER NAME
L.PO==	^D30			; PO NUMBER
L.ST==^D40			; NUMBER OF CHARS PER LINE OF MAILING ADDRS.
L.STRE==^D51			; CUS. ADDRESS STREET1 & STREET2
L.CITY==^D13			; CITY
L.STAT==2			; STATE
L.ZIP== 5			; ZIP
L.CTRY==5			; COUNTRY
L.ATTN==^D51			; ATTENTION LINE
L.CC==4				; OUTPUT SIZE FOR COSTCODE
L.CID== 5			; FIELD SIZE FOR RIGHT JUSTIFIED CID
L.GAN== 6			; FIELD SIZE FOR RIGHT JUSTIFIED GAN
PW.MIN==3			; MIN PASSWORD SIZE
PW.WMN==7			; MIN PASSWORD SIZE FOR WARNING
PW.MAX==^D30			; MAX PASSWORD SIZE

; COMMON TAB POSITIONS

T.P0==	0			; ALL T.P# POSITIONS ARE RELATIVE TO THIS.
T.P1==	T.P0+5			; NODE, HOST, AND CLASS TAB TO HERE IN L U C
T.P2==	T.P0+^D12		; MOST ITEMS START AT THIS POSITION
T.P3==	T.P2+5			; HOME FLAG FOR HOSTS AND GROUPS IN L U C
T.P4== ^D11

; OTHER DEFINITIONS

ACT.OK==1			; OK ANSWER FROM ACCTG
CUD.OK==1			; OK ANSWER FROM CUD
LUD.OK==1			; OK ANSER FROM LUD
MAXHSC==^D1000			; MAXIMUM NUMBER OF HOSTS
MAXHST==37777			; MAXIMUM ALLOWED HOST
ALHSTS==77777
HOMMSK==37777
.CR==	15			; CARRIAGE RETURN
.LF==	12			; LINE FEED
.CRCUD==155			; 940 STYLE CARIAGE RETURN
BLNK==	40			; SPACE
ATSIN== 100			; COMMERCIAL AT SIGN "@"
CTRLC== 3			; CONTROL C
CTRLD== 4			; CONTROL D
ESC==	33			; ESCAPE
QUOT==	47
CMA==	54
SEMIC== 73			; SEMI COLON
LCA==	141			; LOWER CASE A
LCZ==   172			; LOWER CASE Z
UPAR==	136
CUD.TR==40			; CUD TERMINATOR CHAR

; NOTE: CHANNELS 41 TO 45 USED BY COMMANDS FILES

TOUCHN==^D40			; TOUT FILE CHANNEL
ODFCHN==^D39			; LIST OUTPUT FILE CHANNEL
TATCHN==^D38			; TATTLE TALE FILE CHANNEL
RDFCHN==^D37			; REQUEST DIRECTORY CHANNEL
REQCHO==^D36			; REQUEST BODY CHANNEL FOR OUTPUT
DATCHO==^D35			; REQUEST DATA FILE CHANNEL FOR OUTPUT
CONCHO==^D34			; REQUEST CONTROL INFO CHANNEL FOR OUTPUT
REQCHI==^D33			; REQUEST BODY CHANNEL FOR INPUT
DATCHI==^D32			; REQUEST DATA FILE CHANNEL FOR INPUT
CONCHI==^D31			; CONTROL CONTROL FILE CHANNEL FOR INPUT
TMPCHN==^D30			; CHANNEL FOR QUICK TEMP CHORES
PSWCHN==^D29			; CHANNEL FOR PASSWORD FILE
REQMAX==17777			; MAX REQUEST NUMBER
RDFSIZ==<REQMAX+1>/1000	; SIZE OF REQUEST FILE IN PAGES
IF1	<
	IFN REQMAX - <RDFSIZ*1000-1>, 
	< PRINTX **** ERROR: REQMAX+1 MUST BE DIVISIBLE BY 1000 ****>
>
TIM.CH==1			; INTADR CHANNEL FOR TIMER INTERRUPT
T.SEC== 1			; CHOOSE SECONDS FOR TIMER UNITS
TL.HSK== 3*^D60			; Time limit for handshake (3 minutes)
TL.INP== ^D15*^D60		; Time limit for input device (15 minutes)
TL.WRN== ^D60			; Time limit for input device after warning
TL.AUX== 2*^D60			; Time limit for auxiliary port
RUNHOS==^D39			; HOST WE RUN FROM
JOBCHN==1			; CHANNEL FOR (PJ)JOBS.DAT
PJ.ACT==(1B5)			; ACT SYS BIT IN (PJ)JOBS.DAT
PJ.CUD==(1B0)			; CUD SYS BIT IN (PJ)JOBS.DAT
PJ.FSW==15			; FIRST SYSTEM RECORD IN (PJ)JOBS.DAT
NINE==	^D9

	; PPN'S

VALDEV==1,,364312		; DEBUGGING DIRECTORY
USEVAL==6,,144222		; PRODUCTION DIRECTORY
IFDEF	TEST, <
DB.DIR==VALDEV			; DATA FILE DIRECTORY
PJ==	VALDEV	>		; PJ
IFNDEF	TEST, <
DB.DIR==USEVAL			; DATA FILE DIRECTORY
PJ==	6,,214 >		; PJ
TR.GAN==10056
HQ1.GN==41320
TR6.GN==46243

; ERROR CODES FOR THIS PROGRAM

ERR.WF==^D1			; NO WF PROCESS LICENSE
ERR.AC==^D2			; NO AC PROCESS LICENSE
ERR.LF==^D3			; Error in LUD file.
ERR.LI==^D4			; NOT ENOUGH LICENSE
ERR.FI==^D5			; FILE OPERATION ERROR
ERR.NX==^D6			; CANNOT RUN FROM AUX CIRCUIT
ERR.HO==^D7			; THIS IS NOT THE CORRECT HOST
ERR.PJ==^D8			; ERROR WITH (PJ)JOBS.DAT
ERR.RN==^D9			; REQUES.DIR NOT FOUND
ERR.R1==^D10			; REQUES.DIR FULL!
ERR.R2==^D11			; FATAL ERROR IN REQUES.DIR
ERR.R3==^D12			; FATAL ERROR IN REQUEST FILE OUTPUT
ERR.R4==^D13			; FATAL ERROR IN REQUEST FILE
ERR.D1==^D14			; NO DISTRICT FILE
ERR.D2==^D15			; ERROR IN DISTRICT FILE
ERR.ML==^D16			; ERROR IN MAIL NAME
ERR.TV==^D17			; ERROR IN TYMVAL.UUN
ERR.IS==^D18			; Unable to initialize INTADR system
ERR.IC==^D19			; Unable to assign interrupt causes
ERR.IA==^D20			; INTADR not in effect
ERR.CL==^D21			; Unable to clear timer interrupt system

; FLAG FOR ACCOUNTING ERROR MESSAGES

X.M==	(1B0)			; WRITE ERROR CODE TO TERM/TOUT
X.S==	(1B1)			; WRITE MESSAGE 'CONTACT SOFTWARE SUPPORT'
X.A==	(1B2)			; ERROR ABORT
SUBTTL MACS - MACRO DEFINITIONS

DEFINE	BOMB(ERRCOD<^D9999>) <		; TYPE AN ERROR CODE AND EXIT
	JRST	[MOVEI	AC1,ERRCOD
		JRST	ERREX] >

DEFINE	TCR(CRNUM<1>) <			; TCR TYPES OUT CRLF SEQUENCES
	IFDIF	<CRNUM>, <1>, <
	MOVEI	AC4,CRNUM >		; SET TO DO CRNUM CRLF'S
	TLO	FLG,U.CRLF		; SET UP FOR CRLF
	PUSHJ	P,CRLF			; DO IT
	IFDIF	<CRNUM>, <1>, <
	SOJG	AC4,.-2 >		; DO THE REST
>
COMMENT '
TYPSTR outputs a string to the terminal and/or tout file.
ATRADR is address of string
MAXLEN is maximum length of string
SPAC is number of spaces to output before string
TAB is position to tab to before string
CRLF is Y to type a crlf at the end, this is default
FORTTY is Y to force tty output even if U.NOTY is on, default is N
'
DEFINE	TYPSTR(STRADR,MAXLEN<777>,SPAC<0>,TAB<0>,CRLF<Y>,FORTTY<N>) <
	IFDIF <SPAC>, <0>, <
	MOVEI	AC,SPAC			; SET TO OUTPUT SPACES
	PUSHJ	P,SPACES >		; DO IT
	IFDIF <TAB>, <0>, <
	MOVEI	AC,TAB			; SET TO TABULATE TO CHAR POS TAB
	PUSHJ	P,TABS >		; DO IT
	MOVEI	AC,STRADR		; PUT STRING ADDRESS IN AC
	MOVEI	AC1,MAXLEN		; put max length in ac1
	IFIDN <CRLF>, <Y>, <
	TLO	FLG,U.CRLF >		; set to type a carriage return
	IFIDN <FORTTY>, <Y>, <
	TLO	FLG,U.FTTY >		; force tty output
	PUSHJ	P,STROUT		; type the string
	IFIDN <FORTTY>, <Y>, <
	TLZ	FLG,U.FTTY >		; reset force tty output flag
>

; TNESTR outputs a none empty string to terminal and/or TOUT file.
DEFINE	TNESTR(PROMP<NONE>,STRADR,MAXLEN<777>,SPAC<0>,TAB<0>,CRLF<Y>,%NP) <
	MOVE	AC,STRADR
	CAMN	AC,[ASCIZ/	/]
	JRST	%NP
	IFDIF	<PROMP>,<NONE>,<
	TYPSTR	(PROMP,777,0,0,N) >
	TYPSTR	(STRADR,MAXLEN,SPAC,TAB,CRLF)
%NP:	; XALL
>

; TBSSTR outputs a string if a specified bit (BITSPC) is set.
DEFINE	TBSSTR(STRADR,ADRS,BITSPC,MAXLEN<777>,SPAC<0>,TAB<0>,CRLF<Y>,%NP) <
	MOVE	AC,ADRS
	TLNN	AC,BITSPC
	JRST	%NP
	TYPSTR	(STRADR,MAXLEN,SPAC,TAB,CRLF)
%NP:	; XALL
>
COMMENT '
TYPNUM outputs  a number in octal or decimal to terminal
and/or TOUT file. NUMADR may not be AC through AC4.
'
DEFINE	TYPNUM(NUMADR,RAD<O>,SPAC<0>,TAB<0>,CRLF<Y>,FORTTY<N>) <
	IFDIF <SPAC>, <0>, <
	MOVEI	AC,SPAC			; SET TO OUTPUT SPAC SPACES
	PUSHJ	P,SPACES>		; DO IT
	IFDIF <TAB>, <0>, <
	MOVEI	AC,TAB			; SET TO GO TO CHAR POSITION TAB
	PUSHJ	P,TABS	>		; DO IT
	MOVE	AC,NUMADR		; GET THE NUMBER
	IFIDN <CRLF>, <Y>, <
	TLO	FLG,U.CRLF >		; SET TO TYPE A CR.
	IFIDN <FORTTY>, <Y>, <
	TLO	FLG,U.FTTY >		; FORCE TTY OUTPUT
	IFIDN <RAD>, <O>, <
	PUSHJ	P,OCTOUT >		; OUTPUT IT
	IFIDN <RAD>, <D>, <
	PUSHJ	P,DECOUT >		; OUTPUT IT
>
COMMENT '
INSTR inputs an entire line or a single token from the current
command input device (may be a request body file).
PROMP is the address of a string to use as a prompt, or NONE.
STRADR is the address  of the block to put the input string or
token in.
LENGT is the maximum length of the string to be input.
TYPE is S to input a line, T to input a token.
DELIM is a location to store the delimiting character in.
FORTTY is Y to force prompt output to tty regardless of U.NOTY.
SKNEOF is N to try  the  next  device on EOF (for nesting)  and
always just return or Y if we want to skip return if not EOF or
just return if EOF is found (for list input).
PR is Y to save the address of the prompt as the last prompt.
the of last prompt is used if reprompting is necessary.
Note: If U.RFI is set, then prompt is not output.
      Indirect input will be from XXXX.REQ file.
'
DEFINE	INSTR(PROMP,STRADR,LENGT,TYP<S>,DELIM<AC>,FORTTY<N>,SKNEOF<N>,PR<Y>,%NP) <
	IFDIF <PROMP>, <NONE>, <
	IFIDN <PR>, <Y>, <
	MOVEI	AC,PROMP
	MOVEM	AC,LASPRM >		; SAVE ADDRESS OF PROMPT
	TRNE	FLG,U.RFI		; SKIP IF INPUT FROM REQUEST FILE
	JRST	%NP			; DO NOT PROMPT IF FROM REQUEST FILE
	TRZ	FLG2,V.URC		; RESET FLAG
	TLNN	FLG,U.EOL		; SKIP IF LAST INPUT ENDED BY EOL
	JRST	%NP			; SKIP PROMPT
	TRO	FLG2,V.URC		; SET FLAG TO INDICATE PROMPT PRINTED
	TYPSTR	(PROMP,777,0,0,N,FORTTY)
	MOVEI	AC,1
	PUSHJ	P,SPACES
;LALL
%NP:	;XALL >
	MOVEI	AC,STRADR		; ADDRESS OF STRING SPACE
	MOVEI	AC1,LENGT		; THE MAXIMUM LENGTH IS LENGT
	IFIDN <SKNEOF>, <Y>, <
	TRO	FLG,U.NNIB >		; SET NO NEW BUFFER ON EOF
	IFIDN <TYP>, <S>, <
	TRO	FLG2,V.INS >		; SET LINE FLAG
	IFIDN <TYP>, <T>, <
	TRZ	FLG2,V.INS >		; RESET LINE FLAG
	PUSHJ	P,STRIN			; GET IT
	IFDIF <DELIM>, <AC>, <
	MOVEM	AC,DELIM >		; STORE DELIMITER
	IFIDN <SKNEOF>, <Y>, <
	TRZ	FLG,U.NNIB		; RESET FLAG
	TLZE	FLG,U.IEOF >		; SKIP IF NOT EOF
>
COMMENT '
INNUM inputs a number from terminal, TOUT, or request body.
Skip return if input was numeric. Integer value in AC1.
'
DEFINE	INNUM(PROMP,RAD<O>) <
	INSTR	(PROMP,CNVHLD,^D9,T)
	MOVEI	AC,CNVHLD
IFIDN	<RAD>, <O>, <
	PUSHJ	P,CNVO0	>		; CALL OCTAL CONVERSION ROUTINEN	<RAD>, <D>, <
	PUSHJ	P,CNVD0	>		; CALL DECIMAL CONVERSION ROUTINE
>

DEFINE	INDAT(PROMP) <
	INSTR	(PROMP,DATSPC,^D14,T)
	PUSHJ	P,INPDT
>

; GETYR inputs a yes or no. Skip return if yes or just return if no.
DEFINE	GETYR (PROMP<NONE>,%NP) <
	IFDIF	<PROMP>, <NONE>, <
	MOVEI	AC,PROMP		; GET ADDRESS OF PROMPT
	MOVEM	AC,LASPRM		; SAVE IT
	TRNE	FLG,U.RFI		; SKIP IF NOT READING FROM REQUEST FILE
	JRST	%NP
	TYPSTR	(PROMP,777,0,0,N)
	MOVEI	AC,1
	PUSHJ	P,SPACES
>
%NP:	PUSHJ	P,YORN			; GET YES OR NO
>
COMMENT '
CMPSTR compares two strings, skips return if they are matched,
returns if they are unmatched.
ADR1 is the address of the 1st string
ADR2 is the address of the 2nd string
LENGT is the number of chars. to compare
'
DEFINE	CMPSTR(ADR1,ADR2,LENGT) <
	MOVEI	AC4,LENGT
	MOVEI	AC2,ADR1
	MOVEI	AC3,ADR2
	PUSHJ	P,COMSTR
>
COMMENT '
CONCAT concatenates 2 strings (STR1 and STR2) into 
       a single string (STR1).
STR1/  address of string 1
STR2/  address of string 2
LEN1/  maximum length of string 1, default length is 777
LEN2/  maximum length of string 2
'
DEFINE  CONCAT(STR1,STR2,LEN1<777>,LEN2<777>) <
        MOVEI   AC,STR1
        MOVEI   AC1,LEN1
        MOVEI   AC2,STR2
        MOVEI   AC3,LEN2
        PUSHJ   P,CONCTN
>
COMMENT '
SPLITN splits a username to two parts
ADR1/ address of username
ADR2/ address of part 1 (all characters before the dot)
ADR3/ address of part 2 (all characters after the dot)
'
DEFINE	SPLITN(ADR1,ADR2,ADR3)	<
	MOVEI	AC,ADR1
	MOVEI	AC1,^D12
	PUSHJ	P,CNTCHR
	MOVEI	AC2,ADR1
	MOVEI	AC3,ADR2
	MOVEI	AC4,ADR3
	PUSHJ	P,GTCPNM
>
COMMENT '

Auxiliary Circuit Macros.

SNDBYT send bytes over auxiliary circuit.
PORT contains the port number.
BTS is the number of bytes to send.  If BTS is symbolic,  the
number value must be interpreted by the macro exapander,  use
the backslash prefix, ex: \B.LCOM.
If TYP is Cthen ADR contains the value to send, if TYP is not
C then  ADR is the value to send,   although any value should 
work, I for immediate is the best.
'
DEFINE	SNDBYT(PORT<ACTPRT>,BTS<1>,ADR<AC2>,TYP<C>) <
	IFDIF	<PORT>, <AC>, <
	MOVE	AC,PORT	>		; GET THE LINE NUMBER FROM PORT
	IFDIF	<ADR>, <AC2>, <
	IFIDN	<TYP>, <C>, <
	MOVE	AC2,ADR	>		; GET CONTENT OF ADR
	IFDIF <TYP>, <C>, <
	MOVEI	AC2,ADR	>		; SEND IMMEDIATE ADR
	>
	IFIDN	<BTS>, <1>, <
	PUSHJ	P,S1BYT	>		; SEND 1 BYTE
	IFIDN	<BTS>, <2>, <
	PUSHJ	P,S2BYT >		; SEND 2 BYTES
	IFIDN	<BTS>, <3>, <
	PUSHJ	P,S3BYT >		; SEND 3 BYTES
	IFIDN	<BTS>, <4>, <
	PUSHJ	P,S4BYT >		; SEND 4BYTES
>
COMMENT '
GETBYT gets bytes from auxiliary circuit, skips return if success,
is non-skip return if circuit zapped.
PORT is the port number.
BTS is the number of bytes. If BST is symbolic,  the value must be
interpreted by the macro expander, use the backslash prefix.
IF CHTM is y the set U.C377 and if the first byte is 377 then stop
'
DEFINE	GETBYT(PORT<ACTPRT>,BTS<1>,CHTM<N>) <
	IFDIF	<PORT>, <AC>, <
	MOVE	AC,PORT	>		; GET LINE NUMBER FROM PORT
	IFIDN	<CHTM>, <Y>, <
	TRO	FLG,U.C377 >		; TERMINATE IF 1ST BYTE A 377
	IFIDN	<BTS>, <1>, <
	PUSHJ	P,G1BYT >		; GET 1 BYTE
	IFIDN	<BTS>, <2>, <
	PUSHJ	P,G2BYT >		; GET 2 BYTES
	IFIDN	<BTS>, <3>, <
	PUSHJ	P,G3BYT >		; GET 3 BYTES
	IFIDN	<BTS>, <4>, <
	PUSHJ	P,G4BYT >		; GET 4 BYTES
>
COMMENT '
GETSTR inputs a string from an auxiliary circuit. PORT contains the
port number. LENGT is the maximum length, may be symbolic, does not
need backslash. ADR is the address of the string result.
'
DEFINE	GETSTR(PORT<AC>,LENGT,ADR) <
	IFDIF	<PORT>, <AC>, <
	MOVE	AC,PORT >		; MOVE PORT TO AC
	MOVEI	AC1,LENGT		; MOVE MAX LENGTH OF STRING TO AC1
	MOVEI	AC2,ADR	; PUT STRING IN ADR
	PUSHJ	P,AUXSIN		; GET THE STRING
>
COMMENT '
SNDSTR sends a string over an auxiliary circuit. PORT contains the
port  number.   LENGT is the number of bytes to send.   ADR is the 
address of string to send.
'
DEFINE	SNDSTR(PORT<AC>,LENGT,ADR) <
	IFDIF	<PORT>, <AC>, <
	MOVE	AC,PORT >		; MOVE PORT TO AC
	MOVEI	AC1,LENGT		; MOVE LENGTH OF STRING TO AC1
	MOVEI	AC2,ADR		; GET THE STRING FROM ADR
	PUSHJ	P,AUXSOT		; SEND THE STRING
>
COMMENT "
SUBCOM generates a subcommand parser. PROMP is the address of an
ASCIZ format prompting string, may be a literal. ABRV is a three
character string.  MAC is a macro,  previously defined, with one
entry for each subcommand as follows:

	X COMD,MINCHR,ADRS,FLG1,FLG2

COMD is the address of an ASCIZ string (.ie USER, GAN, CUSTOMER,
etc.).  ADRS is the address of the routine that will process the
command.   FLG1 and FLG2 are bit masks that the left half of FLG
will be tested against, in sequence. For example, if FLG1= U.PRV
and FLG2= U.IMM then the command will require U.PRV and U.IMM to
be set.

Note:  Since SUBCOM gens its own address using ABRV as described
above, in the format CMD'ABRV then if this subcommand level have
other calls to subcommand under it,   then care must be taken to
make addresses in the same format.

Example:

	DEFINE SBX <
	X [ASCIZ/USER/],1,CMDLUS,-1,-1
	X [ASCIZ/CUSTOMER/],1,CMDLUC,U.PRV,U.IMM >
	SUBCOM([ASCIZ/LIST/],LIS,SBX)
"
DEFINE	SUBCOM (PROMP,ABRV,MAC) <
	DEFINE	X(A,B,C,D,E) <
	EXP	A >
ABRV'TAB: MAC
ABRV'LEN==.-ABRV'TAB
	DEFINE	X(A,B,C,D,E) <
	EXP	B >
ABRV'MIN: MAC
	DEFINE	X(A,B,C,D,E) <
	EXP	C >
ABRV'ADR: MAC
	DEFINE	X(A,B,C,D,E) <
	XWD	D,E >
ABRV'FLG: MAC
CMD'ABRV:
	TLNE	FLG,U.EOL		; SKIP IF NOT EOL
	JRST	ABRV'LE1
ABRV'LP:
	INSTR	(PROMP,STRSP,5,T)	; INPUT TOKEN
	MOVEI	AC,STRSP		; ADDRESS OF TOKEN
	MOVEI	AC1,5			; MAX LENGTH OF TOKEN
	PUSHJ	P,CNTCHR		; FIND ACTUAL LENGTH
	JUMPE	AC1,ABRV'LPE		; TRY AGAIN IF HE INPUT A NULL
	SETZ	AC2,			; PREPARE INDEX
ABRV'LP1:
	HLLZ	AC,ABRV'FLG(AC2)	; GE FIRST CHECK BITS
	TDNN	FLG,AC			; SKIP IF NOT ALL MASKED BITS EQ 0
	JRST	ABRV'LP2		; NO GO, HE DOESN'T HAVE IT
	HRLZ	AC,ABRV'FLG(AC2)	; GET 2ND BITS
	TDNN	FLG,AC			; SKIP IF HE'S GOT ONE
	JRST	ABRV'LP2		; NO, HE IS NOT
	MOVE	AC,ABRV'MIN(AC2)	; GET MINIMUM LENGTH
	CAMGE	AC1,AC			; SKIP IF ENOUGH INPUT
	JRST	ABRV'LP2		; DO NEXT CHECK
	MOVE	AC,@ABRV'TAB(AC2)	; GET THE STRING TO COMPARE
	AND	AC,MSK(AC1)		; DUMP EXTRA CHARS
	OR	AC,NMSK(AC1)		; PUT IN BLANKS
	CAMN	AC,STRSP		; SKIP IF NOT MATCH
	JRST	ABRV'LPD		; FOUND A MATCH
ABRV'LP2:
	AOJ	AC2,			; INCRMENT INDEX
	CAIG	AC2,ABRV'LEN	; SKIP IF OVERFLOW
	JRST	ABRV'LP1		; TRY AGAIN
	JRST	ABRV'LPE		; NO ENTRIES LEFT
ABRV'LPD:
	PUSHJ	P,@ABRV'ADR(AC2)	; DO THE COMMAND
	SKIPA
	AOS	(P)			; REFLECT THE SKIP RETURN
	POPJ	P,			; RETURN
ABRV'LPE:
	PUSHJ	P,WASLIN
ABRV'LE1:
	MOVNI	ACX,ABRV'LEN	; FOR AOBJN
	HRLZS	ACX
	TYPSTR	([ASCIZ/Options are:/])
ABRV'LE2:
	HLLZ	AC,ABRV'FLG(ACX)
	TDNN	FLG,AC
	JRST	ABRV'LE5		; SKIP DISPLAY OF THIS OPTION
	HRLZ	AC,ABRV'FLG(ACX)
	TDNN	FLG,AC
	JRST	ABRV'LE5
	TYPSTR	(@ABRV'TAB(ACX),777)
ABRV'LE5:
	AOBJN	ACX,ABRV'LE2
	TCR	(1)
	JRST	ABRV'LP
>
COMMENT '

Tatle Tale File MACROS.

GETT attempts to open a tattle tale file, skips return if ok.
EXT is  the  extention  (TTL is the default extention value).
'
DEFINE	GETT (EXT</TTL/>) <
	HRLZI	AC,(SIXBIT EXT)
	MOVEM	AC,TATTXE		; TT FILE EXTENTION IS EXT
	PUSHJ	P,GETTTF		; OPEN IT
>
COMMENT '
TTFSTR outputs a string to the tattle tale file. SUPSP should
be Y if an extra blank following the string is not wanted.
'
DEFINE	TTFSTR (STRADR,MAXLEN<777>,CRLF<Y>,SUPSP<N>) <
	MOVEI	AC,STRADR		; MOVE ADDRESS OF STRADR TO AC
	MOVEI	AC1,MAXLEN		; MOVE MAXLEN TO AC1
	IFIDN	<CRLF>, <Y>, <
	TLO	FLG,U.CRLF >		; SET FLAG INDICATING CRLF
	IFIDN	<SUPSP>, <Y>, <
	TRO	FLG,U.SBL >
	PUSHJ	P,TTFOUT		; OUTPUT IT
>
COMMENT '
TTFNUM outputs a number to the tattle tale file.
'
DEFINE	TTFNUM (NUMADR,RAD<O>,CRLF<Y>,SUPSP<N>) <
	MOVE	AC,NUMADR		; GET THE NUMBER
	IFIDN	<CRLF>, <Y>, <
	TLO	FLG,U.CRLF >
	IFIDN	<SUPSP>, <Y>, <
	TRO	FLG,U.SBL >
	IFIDN	<RAD>, <O>, <
	PUSHJ	P,OCTTTF >
	IFIDN	<RAD>, <D>, <
	PUSHJ	P,DECTTF >
>
COMMENT '

Request MACROS.

REQINP takes one argument, ON or OFF. If the argument is ON
and (V.PRQ or V.EXQ or V.LSQ is set), U.RFI is set.  If the
argument is OFF then U.RFI is reset.
'
DEFINE	REQINP (FLAG<ON>) <
	IFIDN	<FLAG>, <ON>, <
	PUSHJ	P,[TRZ	FLG,U.RFI	; RESET U.RFI
		   TLNE	FLG2,V.PRQ!V.EXQ!V.LSQ
		   TRO	FLG,U.RFI	; SET U.RFI
		   POPJ	P,]		; RETURN
	>
	IFIDN	<FLAG>,	<OFF>, <
	TRZ	FLG,U.RFI		; RESET U.RFI
	>
>
COMMENT '
REQOUT outputs an ascii string to a request file. FILE is CON or
REQ or DAT. STRADR is the address of string to output. MAXLEN is
maximum length of string.
'
DEFINE	REQOUT (FILE, STRADR, MAXLEN<120>) <
	IFIDN	<FILE>,	<REQ>, <
	SETZ	AC2, >			; INDEX INTO TABLES
	IFIDN	<FILE>,	<DAT>, <
	MOVEI	AC2,1 >
	IFIDN	<FILE>,	<CON>, <
	MOVEI	AC2,2 >
	MOVEI	AC,STRADR		; ADDRESS OF STRADR
	MOVEI	AC1,MAXLEN		; MAX LENGTH OF STRING
	PUSHJ	P,REQSTO
>
COMMENT '
REQNOT outputs a number to a request file. FILE is the extention
of request file.  NUMADR is address of number to output.  RAD is
radix indicator (O or D).
'
DEFINE	REQNOT (FILE, NUMADR, RAD<O>) <
	IFIDN	<RAD>,	<O>, <
	MOVEI	AC4,^D8 >		; SET OCTAL OUTPUT
	IFIDN	<RAD>,	<D>, <
	MOVEI	AC4,^D10 >		; SET DECIMAL OUTPUT
	MOVE	AC,NUMADR		; GET NUMBER
	PUSHJ	P,NUMOUT
	REQOUT	(FILE, NUMSP, ^D15)
>
COMMENT '
REQINS inputs a string from a request type file. FILE is REQ or
CON or DAT. STRADR is address of receiving buffer. LENGT is the
length of input in chars.
'
DEFINE	REQINS (FILE,STRADR,LENGT) <
	IFIDN	<FILE>,	<REQ>, <
	SETZ	AC2, >		; INDEX INTO TABLES
	IFIDN	<FILE>,	<DAT>,	<
	MOVEI	AC2,1 >
	IFIDN	<FILE>,	<CON>, <
	MOVEI	AC2,2 >
	MOVEI	AC,STRADR		; MOVE STRADR TO AC
	MOVEI	AC1,LENGT		; MAX CHARS IS LENGT
	PUSHJ	P,QSTRIN		; DO THE INPUT
>
COMMENT '
REQINN inputs a number from a request type file. 
'
DEFINE	REQINN (FILE,RAD<O>) <
	REQINS	(FILE, CNVHLD, ^D9)
	MOVEI	AC,CNVHLD
	IFIDN	<RAD>,	<O>, <
	PUSHJ	P,CNVO0 >		; CALL OCTAL CONVERSION ROUTINE
	IFIDN	<RAD>,	<D>, <
	PUSHJ	P,CNVD0 >		; DECIMAL CONVERSION
>
COMMENT '

EXMMAC sets up examine routines.

ABRV - abbreviation to use  for generating addresses, must
be three characters and unique in all calls to EXMMAC.

MAC - a macro,  previously defined,  with 1 entry for each
item that this command  must input and examine.  Described
below.

DISIND - dispatcher index  for request command dispatcher.
This is take care to match this entry with RCOM in request
command tables (STOR.MAC).

VALRT - a name  of a subroutine that will set up the value
of REQVAL for a new request.  REQVAL is written in request
directory file (REQUES.DIR). An example of a VALRT routine
is USRREQ in REQS.MAC.

Description of MAC, each entry in MAC is as follows:

	X (DISRT, ACCRT, REQRT, CPRADR, MAS, FLG1, FLG2) 

DISRT - a routine that will display the value of the item.
ACCRT - a routine that  will accept a value into the item.
REQRT - a routine that will output a item to the req file.
CPRADR - an address  whose contents will be tested under a
36-bit mask.  If  at  least one masked bit is one then the
corresponding routines  (DISRT, ACCRT, or REQRT)  will  be
called. So that 1 command may handle different situations,
especially different syste types. If the mask is all ones,
then  the routines  are  called regardless of the value of
CPRADR,  even if it is zero.  CPRADR defaults to <AC>.  An
example of a good way to use CPRADR is the situation where
data requirement for a command are different for different
sys. types. One of the first input routine would determine
the system type and create a mask. Subsequent entries will
be called only  if  the mask matches.  Remember  that  the
default mask of all ones matches always.  So if  a routine
needs to be called all the time just let it default.
MAS - the 36 bit mask for use with CPRADR. Default is <-1>
FLG1 and FLG2 - 18 bit masks to be tested against the left 
half of FLG.  Both masks must mask at least one "1" bit in 
order for the corresponding item to be output in  the exam
menu.  These masks do not prevent the calling of the input
routines (ACCRT, etc.), they just effect the menu display.
These masks are for use with default values.  For example,
the customer number  for a customer user is always assumed
and never  input  from  the terminal but the routines must
still be called to establish the default and to output the
default into the request files. Both flags default to <-1>

Notes:   Since  the  same  routine is called to accept the 
original  input and for changing during exam,  the routine
may test V.EXC in left half of FLG2.  If the bit is on, we
are changing. If off, then we are doing original input.
'
DEFINE	EXMMAC (ABRV, MAC, DISIND, VALRT) <
	JRST	ABRV'STP		; JUMP OVER TABLES
; EXMMAC TABLES
DEFINE	X (DISRT, ACCRT, REQRT, CPRADR<AC>, MAS<-1>, FLG1<-1>, FLG2<-1>) <
	EXP	DISRT >			; ROUTINE TO DISPLAY
ABRV'EXD: MAC
ABRV'LXM==.-ABRV'EXD
DEFINE	X (DISRT, ACCRT, REQRT, CPRADR<AC>, MAS<-1>, FLG1<-1>, FLG2<-1>) <
	EXP	ACCRT >			; ACCEPT ROUTINE
ABRV'EXA: MAC
DEFINE	X (DISRT, ACCRT, REQRT, CPRADR<AC>, MAS<-1>, FLG1<-1>, FLG2<-1>) <
	EXP	REQRT >			; REQUEST ROUTINE
ABRV'EXR: MAC
DEFINE	X (DISRT, ACCRT, REQRT, CPRADR<AC>, MAS<-1>, FLG1<-1>, FLG2<-1>)<
	EXP	CPRADR >		; ADDRESS FOR MASK COMPARE
ABRV'EXC: MAC
DEFINE	X (DISRT, ACCRT, REQRT, CPRADR<AC>, MAS<-1>, FLG1<-1>, FLG2<-1>)<
	EXP	MAS >			; MASK FOR CPRADR
ABRV'EXM: MAC
DEFINE	X (DISRT, ACCRT, REQRT, CPRADR<AC>, MAS<-1>, FLG1<-1>, FLG2<-1>) <
	XWD	FLG1,FLG2 >		; FLAGS FOR COMPARE AGAINS LEFT(FLG)
ABRV'EFL: MAC
; EXMMAC Code
ABRV'STP:
	TLZ	FLG2,V.RER!V.CHG	; RESET ERROR BIT AND CHANGED BIT
	MOVEI	AC,DISIND		; DISPATCHER INDEX
	MOVEM	AC,REQCOM		; SAVE
	MOVEI	AC,ABRV'LXM		; NUMBER OF ENTRIES
	MOVEM	AC,EXMCT		; SAVE
	SETZM	EXMIN
	TLZ	FLG2,V.EXC		; RESET CHANGING FLAG
ABRV'INP:
	MOVE	AC1,EXMIN		; GET INDEX
	MOVE	AC,ABRV'EXM(AC1)	; GET MASK
	CAMN	AC,[-1]			; SKIP IF NOT -1
	JRST	.+3			; DO
	TDNN	AC,@ABRV'EXC(AC1)	; TEST UNDER MASK
	JRST	.+3			; DON'T
	PUSHJ	P,@ABRV'EXA(AC1)
	JFCL
	SOSG	EXMCT
	JRST	ABRV'ST2
	AOS	EXMIN
	JRST	ABRV'INP
ABRV'ST2:
	MOVEI	AC,ABRV'LXM
	MOVEM	AC,EXMCT
	SETZM	EXMIN
	TCR	(1)
ABRV'EXS:
	MOVE	AC1,EXMIN
	MOVE	AC,ABRV'EXM(AC1)	; GET MASK
	CAMN	AC,[-1]			; SKIP IF NOT -1
	JRST	.+3			; DO
	TDNN	AC,@ABRV'EXC(AC1)	; TEST UNDER MASK
	JRST	ABRV'EX2		; DON'T
	HLLZ	AC,ABRV'EFL(AC1)	; GET LEFT HALF OF REQUIREMENT FLAGS
	TDNN	FLG,AC			; SKIP IF AT LEAST ONE MASKED BIT SET
	JRST	ABRV'EX2
	HRLZ	AC,ABRV'EFL(AC1)	; GET RIGHT HALF OF FLAGS
	TDNN	FLG,AC
	JRST	ABRV'EX2
	TLNN	FLG2,V.LSQ!V.PRQ	; SKIP IF LISTING OR PROCESSING
	PUSHJ	P,EXMNMO		; OUTPUT MENU NUMBER
	MOVE	AC1,EXMIN
	PUSHJ	P,@ABRV'EXD(AC1)
	JFCL
ABRV'EX2:
	SOSG	EXMCT
	JRST	.+3			; GO TO CHANGING SECTION
	AOS	EXMIN
	JRST	ABRV'EXS		; DISPLAY THE NEXT ENTRY
	TLNE	FLG2,V.LSQ!V.PRQ	; SKIP IF NOT PROCESS OR LIST REQUEST
	JRST	ABRV'EX4
	REQINP	OFF
ABRV'XSD:
	PUSHJ	P,CNFIRM
	JRST	ABRV'XDD
ABRV'XS1:
	TCR	(1)
ABRV'XS2:
	TLO	FLG2,V.EXC		; SET FLAG INDICATING CHANGING
	PUSHJ	P,GITEMN
	CAILE	AC1,ABRV'LXM		; SKIP IF NUMBER NOT TOO BIG
	JRST	ABRV'XS3
	MOVE	AC,CNVHLD
	CAMN	AC,[ASCII/     /]	; SKIP IF USER DID NOT INPUT NULL
	JRST	ABRV'ST2		; DONE WITH CHANGING
	JUMPE	AC1,ABRV'XS3
	SOJ	AC1,			; CORRECT
	MOVEM	AC1,EXMIN		; SAVE ITEM NUMBER TO CHANGE
	MOVE	AC,ABRV'EXM(AC1)	; GET MASK
	CAMN	AC,[-1]			; SKIP IF NOT -1
	JRST	.+3			; DO
	TDNN	AC,@ABRV'EXC(AC1)	; TEST UNDER MASK
	JRST	ABRV'XS1		; DO NOT CHANGE
	TLO	FLG2,V.CHG		; SET ITEM CHANGED FLAG
	PUSHJ	P,@ABRV'EXA(AC1)	; ACCEPT THE ITEM
	JFCL
	TLZ	FLG2,V.RER		; RESET ERROR FLAG (ASSUME HE FIXED)
	JRST	ABRV'XS1		; DO ANOTHER ITEM
ABRV'XS3:
	TLNE	FLG,U.IND
	JRST	GETCMD
	PUSHJ	P,ILLCHG
	JRST	ABRV'XS2
ABRV'XDD:
	TLZ	FLG2,V.EXC		; RESET FLAG
	REQINP	ON
ABRV'EX4: ; HERE WHEN EXAM IS COMPLETE, NOW DECIDE WHAT TO DO
	TLNE	FLG2,V.LSQ		; SKIP IF NOT LISTING
	JRST	ABRV'NRQ		; LISTING OR EXAMINING
	TLNN	FLG,U.IMM		; SKIP IF IMMEDIATE MODE
	JRST	ABRV'NIM		; NOT IMMEDIATE
	TLNN	FLG2,V.CHG		; SKIP IF AN ITEM WAS CHANGED
	JRST	ABRV'NRQ
	TLNN	FLG2,V.EXQ		; SKIP IF WE WERE EXAMINING
	JRST	ABRV'NRQ
	PUSHJ	P,ABRV'WRQ		; REWRITE REQUEST
	PUSHJ	P,RELRQI
	POPJ	P,			; RETURN TO COMMAND DISPATCHER
ABRV'NRQ: ; HERE WHEN EITHER THIS WAS NOT A REQUEST OR NOTHING WAS CHANGED
	TLNE	FLG2,V.PRQ		; SKIP IF NOT PROCESSING
	JRST	ABRV'XXX		; GO DO IT IF PROCESSING
	TLNN	FLG2,V.EXQ!V.LSQ ;SKIP IF EXAMINING OR LISTING
	JRST	ABRV'XXX		; GO DO IT IF NOT A REQUEST
	PUSHJ	P,RELRQI		; RELEASE INPUT CHANNELS IF OPEN
	POPJ	P,			; RETURN TO COMMAND DISPATCHER
ABRV'NIM: ; HERE WHEN NOT IN IMMEDIATE MODE, MUST BE USER ENTERING A REQUEST
	PUSHJ	P,VALRT			; SET UP REQVAL
	PUSHJ	P,INCON			; GET REQUESTOR PLACE OF CONTACT
	PUSHJ	P,GTODAY		; GET TODAY'S DATE
	MOVEM	AC,REQDAT		; SAVE IT
	PUSHJ	P,ABRV'WRQ		; WRITE NEW REQUEST
	PUSHJ	P,RQNOUT		; TYPE REQ # AND ASK TO DO ANOTHER REQ
	JRST	ABRV'STP		; YES, DO ANOTHER
	TRO	FLG,U.OKAY		; NO, SET EVERYTHING OKAY
	POPJ	P,			; RETURN TO COMMAND DISPATCHER
ABRV'WRQ: ; WRITE A REQUEST
	PUSHJ	P,CONHDR		; WRITE HEADER
	MOVEI	AC,ABRV'LXM		; NUMBER OF ENTRIES
	MOVEM	AC,EXMCT
	SETZM	EXMIN
ABRV'WR2:
	MOVE	AC1,EXMIN
	MOVE	AC,ABRV'EXM(AC1)	; GET MASK
	CAMN	AC,[-1]			; SKIP IF NOT -1
	JRST	.+3			; IF MASK ALL ONES, DON'T TEST
	TDNN	AC,@ABRV'EXC(AC1)	; TEST AGAINST CONTENTS OF COMP ADRS
	JRST	ABRV'WR3		; NO BITS MASKED, DO NOT OUTPUT
	PUSHJ	P,@ABRV'EXR(AC1)	; WRITE OUT THE ITEM
ABRV'WR3:
	SOSG	EXMCT
	JRST	.+3
	AOS	EXMIN
	JRST	ABRV'WR2
	PUSHJ	P,RELRQO		; CLOSE THE REQUEST
	POPJ	P,
ABRV'XXX:
	TLNE	FLG2,V.RER		; SKIP IF ERROR BIT NOT SET
	POPJ	P,			; RETURN
	SETZ	AC,
>

DEFINE	EXMCHG	(ABRV,MAC,MSGRTN) <
	JRST	STP'ABRV		; JUMP OVER TABLES
; EXMCHG TABLES
DEFINE	X (OUTRTN,INPRTN,CTLRTN<0>) <
	EXP	OUTRTN
>
EXD'ABRV: MAC
LXM'ABRV==.-EXD'ABRV
DEFINE	X (OUTRTN,INPRTN,CTLRTN<0>) <
	EXP	INPRTN
>
EXA'ABRV: MAC
DEFINE	X (OUTRTN,INPRTN,CTLRTN<0>) <
	EXP	CTLRTN
>
EXC'ABRV: MAC
; EXMCHG CODE
STP'ABRV:
	MOVEI	AC,LXM'ABRV		; NUMBER OF ENTRIES
	MOVEM	AC,EXMCT		; SAVE
	SETZM	EXMIN
INP'ABRV:
	MOVE	AC1,EXMIN		; GET INDEX
	MOVE	AC,EXC'ABRV(AC1)
	TRNE	AC,4
	JRST	IP2'ABRV
	PUSHJ	P,@EXA'ABRV(AC1)
	JFCL
IP2'ABRV:
	SOSG	EXMCT
	JRST	ST2'ABRV
	AOS	EXMIN
	JRST	INP'ABRV
ST2'ABRV:
	MOVEI	AC,LXM'ABRV
	MOVEM	AC,EXMCT
	SETZM	EXMIN
	TCR	(1)
	TYPSTR	(MSGRTN)
	TCR	(1)
EXS'ABRV:
	MOVE	AC1,EXMIN
	MOVE	AC,EXC'ABRV(AC1)
	TRNE	AC,2
	JRST	YXS'ABRV
	PUSHJ	P,EXMNMO
	MOVE	AC1,EXMIN
	PUSHJ	P,@EXD'ABRV(AC1)
YXS'ABRV:
	SOSG	EXMCT
	JRST	XSD'ABRV
	AOS	EXMIN
	JRST	EXS'ABRV
XSD'ABRV:
	TLNN	FLG2,V.LSQ
	PUSHJ	P,CNFIRM
	JRST	YYY'ABRV
XS1'ABRV:
	TCR	(1)
XS2'ABRV:
	PUSHJ	P,GITEMN
	CAILE	AC1,LXM'ABRV		; SKIP IF NUMBER NOT TOO BIG
	JRST	XS3'ABRV
	MOVE	AC,CNVHLD
	CAMN	AC,[ASCII/     /]	; SKIP IF USER DID NOT INPUT NULL
	JRST	ST2'ABRV
	JUMPLE	AC1,XS3'ABRV
	SOJ	AC1,			; CORRECT
	MOVE	AC,EXC'ABRV(AC1)
	TRNE	AC,1
	JRST	XS4'ABRV
	TLO	FLG2,V.EXC		; SET FLAG TO INDICATE CHANGING
	PUSHJ	P,@EXA'ABRV(AC1)
	TLZ	FLG2,V.EXC		; RESET FLAG TO INDICATE CHANGING
	JRST	XS1'ABRV
XS3'ABRV:
	PUSHJ	P,ILLCHG
	JRST	XS2'ABRV
XS4'ABRV:
	PUSHJ	P,NALCHG
	JRST	XS2'ABRV
YYY'ABRV:
	SETZ	AC,
>
  }lwaå