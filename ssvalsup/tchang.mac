SUBTTL  CHGCOM  - Change Commands

DEFINE  CHG1    <
        X [ASCIZ"CLASS"],2,CMDCUC,U.PRV,-1	; Change Class
        X [ASCIZ"COLON"],2,CMCCLN,U.PRV,-1	; Change Colon
        X [ASCIZ"CUSTOMER"],2,CMDCCT,U.PRV,-1	; Change Customer
        X [ASCIZ"GAN"],1,CMDCUG,U.PRV,-1	; Change Gan
        X [ASCIZ"HOME"],1,CMDCHO,-1,-1		; Change Home
        X [ASCIZ"IRC"],1,CMUIRC,U.PRV,-1	; Change IRC
        X [ASCIZ"MAILNAME"],1,CMDCMA,-1,-1	; Change Mailname
        X [ASCIZ"PASSWORD"],1,CMDCUP,-1,-1	; Change Password
        X [ASCIZ"TGW"],1,CMCTGW,U.PRV,-1        ; Change Transparent Gate Way
        X [ASCIZ"USERNAME"],1,CMDCUN,-1,-1	; Change Username
>
        SUBCOM  ([ASCIZ"CHANGE"],CHG,CHG1)

DEFINE  CCT1    <
	X [ASCIZ"ATTENTION"],2,CMDCAT,-1,-1	; Change Customer Attention
        X [ASCIZ"ADDRESS"],2,CMDCCA,-1,-1	; Change Customer Address
        X [ASCIZ"DETAIL"],1,CMDCDE,-1,-1	; Change Customer Detail
        X [ASCIZ"NAME"],2,CMDCCN,-1,-1		; Change Customer Name
        X [ASCIZ"NUMBER"],2,CMDCUM,-1,-1	; Change Customer Number
        X [ASCIZ"PO"],1,CMDCPO,-1,-1		; Change Customer PO
        X [ASCIZ"SALESMAN"],1,CMDCCS,-1,-1	; Change Customer Salesman
>
        SUBCOM  ([ASCIZ"CHANGE CUSTOMER"],CCT,CCT1)

DEFINE  CMA1    <
        X [ASCIZ"PASSWORD"],1,CMDPSW,-1,-1	; Change Mailname Password
        X [ASCIZ"USERNAMES"],1,CMDCMU,U.PRV,-1	; Change Mailname Username
>
        SUBCOM  ([ASCIZ"CHANGE MAILNAME"],CMA,CMA1)

CHGDON: TRO     FLG,U.OKAY
        POPJ    P,

CHCERR: ; CHANGE CUSTOMER ERROR
        PUSHJ   P,LUDAOT
        TRZ     FLG,U.OKAY
        POPJ    P,
; CHANGE CUSTOMER ADDRESS AND ATTENTION

CMDCCA: PUSHJ   P,BLDACT                ; BUILDING CIRCUIT TO ACTG.
        JRST    GETCMD

DEFINE  CCUA    <
        X (SYSOUT,INASYS,1)
        X (CNOOUT,GVCSNO,1)
        X (ATNOUT,GETATN)
        X (AD1OUT,GETAD1)
        X (AD2OUT,GETAD2)
        X (AD3OUT,GETAD3)
        X (CTYOUT,GETCTY)
        X (STAOUT,GETSTA)
        X (ZIPOUT,GETZIP)
        X (CTROUT,GETCTR)
>
        EXMCHG  (CCA,CCUA,[ASCIZ/Change Customer Address/])

        SNDBYT  (ACTPRT,\B.ACOM,CCAACT,I) ; CODE 45 = CHANGE CUSTOMER ADDRESS
        SNDBYT  (AC,\B.CID,CUSNUM)        ; SEND CUSTOMER NUMBER (3 BYTES)
        SNDBYT  (AC,\B.ASY,ACTSYS)        ; SEND SYSTEM TYPE (1 BYTE)
        PUSHJ   P,SNDADR                  ; SEND A NEW ADDRESS TO ACTG.
        PUSHJ   P,AGANS
        JRST    CHCERR
        PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CHGDON
        TTFSTR  ([ASCIZ/Change Customer Address/])
        PUSHJ   P,STDCHD
        PUSHJ   P,TTCADR                ; OUTPUTS CUSTOMER ADDRESS
        PUSHJ   P,RELTTF
        JRST    CHGDON
; CHANGE CUSTOMER ATTENTION

CMDCAT:	PUSHJ	P,BLDACT		; BUILDING CIRCUIT TO ACCOUNTING SYSTEM
	JRST	GETCMD

DEFINE	CCAT	<
	X (SYSOUT,INASYS,1)
	X (CNOOUT,GVCSNR,1)
	X (ATNOUT,GETATN)
>
	EXMCHG	(CAT,CCAT,[ASCIZ/Change Customer Attention/])

	SNDBYT	(ACTPRT,\B.ACOM,CCAACT,I)
	SNDBYT	(AC,\B.CID,CUSNUM)
	SNDBYT	(AC,\B.ASY,ACTSYS)
	PUSHJ	P,SNDADR
	PUSHJ	P,AGANS
	JRST	CHCERR
	PUSHJ	P,ENTDON
	GETT	(/CHG/)
	JRST	CHGDON
	TTFSTR	([ASCIZ/Change Customer Attention/])
	PUSHJ	P,STDCHD
	PUSHJ	P,TTATTN
	PUSHJ	P,RELTTF
	JRST	CHGDON
; CHANGE CUSTOMER DETAIL

CMDCDE: PUSHJ   P,BLDACT                ; BUILDING CIRCUIT TO ACTG.
        JRST    GETCMD

DEFINE  CCDE    <
        X (SYSOUT,INASYS,1)
        X (CNOOUT,GVCSNO,1)
        X (DETOUT,GETDET)
>
        EXMCHG  (CDE,CCDE,[ASCIZ/Change Customer Detail/])

        SNDBYT  (ACTPRT,\B.ACOM,CDTACT,I) ; SEND COMMAND CODE 34
        SNDBYT  (AC,\B.CID,CUSNUM)      ; SEND CUSTOMER NUMBER
        SNDBYT  (AC,\B.ASY,ACTSYS)      ; SEND SYSTEM TYPE
        SNDBYT  (AC,\B.DET,ACDETC)      ; SEND DETAIL CODE
        PUSHJ   P,AGANS
        JRST    CHCERR
        PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CHGDON
        TTFSTR  ([ASCIZ/Change Customer Detail/])
        PUSHJ   P,STDCHD
        PUSHJ   P,TTCDCD                ; OUTPUT DETAIL CODE
        PUSHJ   P,RELTTF
        JRST    CHGDON
; CHANGE CUSTOMER NAME

CMDCCN: PUSHJ   P,BLDACT                ; BUILDING CIRCUIT TO ACTG.
        JRST    GETCMD

DEFINE  CCUN    <
        X (SYSOUT,INASYS,1)
        X (CNOOUT,GVCSNO,1)
        X (CNAOUT,GETCNA)
>
        EXMCHG  (CDN,CCUN,[ASCIZ/Change Customer Name/])

        SNDBYT  (ACTPRT,\B.ACOM,CCNACT,I) ; SEND COMMAND CODE 44
        SNDBYT  (AC,\B.CID,CUSNUM)      ; SEND CUSTOMER NUMBER
        SNDBYT  (AC,\B.ASY,ACTSYS)      ; SEND SYSTEM TYPE
	SNDSTR	(AC,L.CUSN,NCNMSP)      ; SEND NEW CUSTOMER NAME
        PUSHJ   P,AGANS
        JRST    CHCERR
        PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CHGDON
        TTFSTR  ([ASCIZ/Change Customer Name/])
        PUSHJ   P,STDCHD
        PUSHJ   P,TTNCSN                ; OUTPUT NEW CUSTOMER NAME
        PUSHJ   P,RELTTF
        JRST    CHGDON
; CHANGE CUSTOMER PURCHASE ORDER

CMDCPO: PUSHJ   P,BLDACT
        JRST    GETCMD

DEFINE  CCUP    <
        X (SYSOUT,INASYS,1)
        X (CNOOUT,GVCSNO,1)
        X (POOUT,GETPO)
        X (POSOUT,GETPOS)
        X (POEOUT,GETPOE)
        X (POMOUT,GETPOM)
>
        EXMCHG  (CPO,CCUP,[ASCIZ/Change Customer PO/])

        SNDBYT  (ACTPRT,\B.ACOM,CPOACT,I) ; CHANGE CUST. PO (CODE 40)
	SNDBYT	(AC,\B.CID,CUSNUM)
        SNDBYT  (AC,\B.ASY,ACTSYS)
        SNDSTR  (AC,L.PO,ACPONM)
        PUSHJ   P,AGANS
        JRST    CHCERR
        SNDBYT  (ACTPRT,\B.ACOM,CPMACT,I) ; CHANGE CUST. PO MAX (CODE 41)
	SNDBYT	(AC,\B.CID,CUSNUM)
        SNDBYT  (AC,\B.ASY,ACTSYS)
        SNDBYT  (AC,\B.POMX,ACPOMX)
        PUSHJ   P,AGANS
        JRST    CHCERR
        SNDBYT  (ACTPRT,\B.ACOM,CPSACT,I) ; CHG CUST. PO START DATE (CODE 42)
	SNDBYT	(AC,\B.CID,CUSNUM)
        SNDBYT  (AC,\B.ASY,ACTSYS)
        SNDBYT  (AC,\B.PODT,ACPSDT)
        PUSHJ   P,AGANS
        JRST    CHCERR
        SNDBYT  (ACTPRT,\B.ACOM,CSTACT,I) ; CHG CUST. PO STOP DATE (CODE 43)
	SNDBYT	(AC,\B.CID,CUSNUM)
        SNDBYT  (AC,\B.ASY,ACTSYS)
        SNDBYT  (AC,\B.PODT,ACPEDT)
        PUSHJ   P,AGANS
        JRST    CHCERR
        PUSHJ   P,ENTDON
	GETT	(/CHG/)
        JRST    CHGDON
	TTFSTR	([ASCIZ/Change Customer PO/])
        PUSHJ   P,STDCHD                ; WRITE STANDARD HEADER
	PUSHJ	P,RELTTF
        JRST    CHGDON
; CHANGE CUSTOMER SALESMAN CODE

CMDCCS: PUSHJ   P,BLDACT                ; BUILDING CIRCUIT TO ACTG.
        JRST    GETCMD

DEFINE  CCUS    <
        X (SYSOUT,INASYS,1)
        X (CNOOUT,GVCSNO,1)
        X (SALOUT,GETSAL)
>
        EXMCHG  (CCS,CCUS,[ASCIZ/Change Customer Salesman/])

        MOVE    AC,ACTSYS
        MOVEI   AC2,CCSACT              ; CODE 36
        CAIE    AC,^D9
        MOVEI   AC2,CS2ACT              ; CODE 37
        SNDBYT  (ACTPRT,\B.ACOM,AC2)    ; SEND COMMAND CODE
        SNDBYT  (AC,\B.CID,CUSNUM)      ; SEND CUSTOMER NUMBER
        SNDBYT  (AC,\B.ASY,ACTSYS)      ; SEND SYSTEM TYPE
        SNDBYT  (AC,\B.SLS,ACSLS)       ; SEND SALESMAN NUMBER
        PUSHJ   P,AGANS
        JRST    CHCERR
        PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CHGDON
        TTFSTR  ([ASCIZ/Change Customer Salesman/])
        PUSHJ   P,STDCHD
        PUSHJ   P,TTSLMN                ; OUTPUT SALESMAN NUMBER
        PUSHJ   P,RELTTF
        JRST    CHGDON
; CHANGE USERNAME

CMDCUN: TLNE    FLG2,V.LSQ
        JRST    CMCUN1
        PUSHJ   P,ACTCUD
        JRST    GETCMD
CMCUN1: TRO     FLG2,V.NIR!V.FRQ

DEFINE CUNM <
        X (NAMOUP,GVUSRN,RWUSR)
        X (NNMOUT,GETNNM,RWNNM)
>
        EXMMAC  (CUN,CUNM,2,USRREQ)

; CODE FOR CHANGE USERNAME STARTS HERE, ONLY IMMEDIATE MODE AND
; PROCESS COMMAND CAN ENTER THE FOLLOWING CODES.
 
        TCR     (1)
        PUSHJ   P,CUNINM                ; CHANGE USERNAME IN CUD
        JRST    [PUSHJ  P,ERRCUD        ; ERROR IN CUD
                 PUSHJ  P,GET40
                 POPJ   P,]
        PUSHJ   P,MSEDON
        PUSHJ   P,CUNINA                ; CHANGE USERNAME IN ACCOUNTING
        JRST    CMCUNE                  ; ERROR IN ACCOUNTING
        PUSHJ   P,ASEDON
        GETT    (/CHG/)
        JRST    CUNMCD
	TTFSTR	([ASCIZ/Change Username/])
        PUSHJ   P,STDUHD
        PUSHJ   P,TTNWUS
        PUSHJ   P,RELTTF
CUNMCD: TLNE    FLG2,V.PRQ              ; SKIP IF NOT PROCESSING A REQUEST
        JRST    CHGDON
        TCR     (1)
        GETYR   ([ASCIZ "Do another? (Y/N):"])
        JRST    CHGDON
        JRST    CMCUN1
CMCUNE: ; ERROR DURING CHANGING A USERNAME IN ACCOUNTING
        PUSHJ   P,LUDAOT
        PUSHJ   P,RCUNCD                ; REVERSE CHANGE USERNAME IN MUD
        POPJ    P,

; CHANGE USER PASSWORD

CMDCUP: TLNE    FLG,U.PROG!U.NET
        JRST    CMCUP1
        PUSHJ   P,ACTCUD
        JRST    GETCMD
        JRST    CMCUP2
CMCUP1: PUSHJ   P,BLDCUD
        JRST    GETCMD
CMCUP2: PUSHJ   P,GETUNM                ; GET A USERNAME
        JRST    CHGDON
        PUSHJ   P,GCREC                ; GET CUD RECORD
	JRST	CMCPE3
        PUSHJ   P,CHKNET                ; CHECK DISTRICT
        JRST    CMCPE3
        TLNE    FLG,U.PROG!U.NET
        JRST    CMCUP3
        PUSHJ   P,GTUREC                ; GET ACTG. RECORD
	JRST	CMCPE3
        PUSHJ   P,CKMNAM
        JRST    CMCPE2
        PUSHJ   P,CKTRAC
        JRST    CMCPE2
CMCUP3: TCR     (1)
        MOVE    AC,NMCOLN               ; CLEAR NO PASSWORD BIT
        TLZ     AC,NPS.CD
        MOVEM   AC,NMCOLN
        MOVEI   AC,UNMSPC
        MOVEM   AC,NUM2
        PUSHJ   P,GCYPUN
        MOVEM   AC1,CYPUNM
	TRO	FLG2,V.CUP		; Set flag to indicate Change Password
        PUSHJ   P,GETPSW                ; INPUT PASSWORD FROM TERMINAL
        JRST    CMCPE1                  ; BAD PASSWORD
	TRZ	FLG2,V.CUP
        TLNE    FLG,U.PRV
        PUSHJ   P,ASKTGW                ; ASK TRANSPARENT GATE WAY OPTION
        PUSHJ   P,ASKCHG
        PUSHJ   P,CNPOPT                ; CHANGE NO PASSWORD BIT
	JRST	GETCMD
        TLNN    FLG,U.PRV               ; SKIP IF NOT CUSTOMER
	JRST	.+3
        PUSHJ   P,CLIWRD                ; CHANGE TRANS. GATEWAY
	JRST	GETCMD
        MOVE    AC,NMCOLN
        TLNN    AC,CLN.CD               ; SKIP IF "NO COLON OPTION" IS SET
        JRST    .+3
        PUSHJ   P,CCOOPT                ; CHANGE "NO COLON OPTION"
        JRST    GETCMD
	MOVE	AC,NMCOLN
	TLNE	AC,NPS.CD		; SKIP IF NO-PASSWORD-BIT NOT SET
	JRST	CMCUP4			; DO NOT SEND PASSWORD
        SNDBYT  (CUDPRT,\B.CCOM,CPSMUD,I)
        PUSHJ   P,SNMCUD
        SNDBYT  (CUDPRT,\B.CYP,NMPSW)   ; SEND PASSWORD CIPHER
        PUSHJ   P,SND40                 ; SEND TERMINATOR
        PUSHJ   P,NGANS                 ; GET ANSWER
	JRST	ERRCUD
        PUSHJ   P,GET40
CMCUP4: PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CHGDON
        TTFSTR  ([ASCIZ/Change Password/])
        PUSHJ   P,STDUHD
        PUSHJ   P,RELTTF                ; RELEASE TT FILE
        JRST    CHGDON
CMCPE1: TYPSTR  ([ASCIZ/Bad Password - not changed./])
        JRST    CMCUP3
CMCPE2: TYPSTR  (ILLACC)
	TYPSTR	(PLSREU)
CMCPE3: TCR     (1)
        JRST    CMCUP2
; CHANGE THE MAILNAME PASSWORD.

CMDPSW: PUSHJ   P,BLDCUD
        JRST    GETCMD
        TLNE    FLG,U.PROG!U.NET
        JRST    CMPSWV
        MOVE    AC,[MY.UNM,,UNMSPC]
        BLT     AC,UNMSPC+2
	JRST	CMPSWS
CMPWUE: TYPSTR	([0],1,0,0,Y,Y)
CMPSWV: INSTR   ([ASCIZ"Enter Mailname:"],UNMSPC,L.UNM,T)
        MOVEI   AC1,L.UNM
        MOVEI   AC,UNMSPC
        PUSHJ   P,CNTCHR
        SKIPG   AC1
        JRST    CHGDON
        PUSHJ   P,GCREC
        JRST    CMPWUE
        PUSHJ   P,CHKNET
        JRST    CMPWUE
CMPSWS: MOVEI   AC,UNMSPC
        MOVEM   AC,NUM2
        PUSHJ   P,GCYPUN
        MOVEM   AC1,CYPUNM
	TLNE	FLG,U.PROG!U.NET
	JRST	CMPSWA
CKOPSW: MOVEI   AC,IO.NEC
        SETMOD  AC,
        TYPSTR  ([ASCIZ/Enter Old Mailname Password:/],777,0,0,N,Y)
        MOVEI   AC,40
        PUSHJ   P,CHROUT
        PUSHJ   P,PWMSK
        PUSHJ   P,INPPSW
        MOVEM   AC1,OLDPSW
        MOVEM   AC1,NMPSW
	PUSHJ	P,CKOLDP
	JRST	CMNPSW
        MOVE    AC1,NMANS
        CAIE    AC1,22
	JRST	[PUSHJ	P,NANOUT
		 TYPSTR	([ASCIZ/Please try again./],777,0,0,Y,Y)
		 TYPSTR	([0],1,0,0,Y,Y)
		 JRST	CKOPSW]
	TYPSTR	([ASCIZ/Incorrect password - Please try again./],777,0,0,Y,Y)
	TYPSTR	([0],1,0,0,Y,Y)
	JRST	CKOPSW
CMPSWA: TYPSTR	([0],1,0,0,Y,Y)
CMNPSW: MOVEI   AC,IO.NEC
        SETMOD  AC,
        TYPSTR  ([ASCIZ/Enter New Mailname Password:/],777,0,0,N,Y)
        MOVEI   AC,40
        PUSHJ   P,CHROUT
        PUSHJ   P,INPPSW
        MOVEM   AC1,NMPSW
	MOVE	AC,NUM
        SKIPN   AC
        JRST    [TYPSTR ([ASCIZ/Response to prompt is required./],777,0,0,Y,Y)
                 JRST   CMPSWE]
        CAILE   AC,PW.MAX
        JRST    [TYPSTR (MPW2LG,777,0,0,Y,Y)
                 JRST   CMPSWE]
        CAIGE   AC,PW.WMN
        JRST    [TYPSTR (MPW2SS,777,0,0,Y,Y)
                 JRST   CMPSWE]
        TRZE    FLG2,V.SCL
        JRST    [TYPSTR (MPWCSC,777,0,0,Y,Y)
                 JRST   CMPSWE]
        SKIPN   NMPSW
        JRST    [TYPSTR (MPWZCF,777,0,0,Y,Y)
                 JRST   CMPSWE]
        CAMN    AC1,CYPUNM
        JRST    [TYPSTR (MPWEUN,777,0,0,Y,Y)
                 JRST   CMPSWE]
        TLNN    FLG2,V.NPS
        JRST    [TYPSTR (MNOEAC,777,0,0,Y,Y)
                 JRST   CMPSWE]
	CAMN	AC1,OLDPSW
	JRST	[TYPSTR (MNPSOP,777,0,0,Y,Y)
		 JRST	CMPSWE]
        TYPSTR  ([ASCIZ/Enter it again:/],777,0,0,N,Y)
        MOVEI   AC,40
        PUSHJ   P,CHROUT
        PUSHJ   P,PWMSK
        PUSHJ   P,INPPSW
        CAME    AC1,NMPSW
        JRST    [TYPSTR (MNOMAT,777,0,0,N,Y)
                 TYPSTR ([0],1,1,0,N,Y)
                 JRST   CMPSWE]
        SETZ    AC,
        SETMOD  AC,
        PUSHJ   P,ASKCHG
        SNDBYT  (CUDPRT,\B.CCOM,CPSMUD,I)
        PUSHJ   P,SNMCUD
        SNDBYT  (CUDPRT,\B.CYP,NMPSW)
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
        JRST    ERRCUD
        PUSHJ   P,GET40
        PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CMPSW2
        TTFSTR  ([ASCIZ/Change Mailname Password/])
        PUSHJ   P,STDUHD
        PUSHJ   P,RELTTF
CMPSW2: TLNN    FLG,U.PROG!U.NET
        JRST    CHGDON
        TCR     (1)
        JRST    CMPSWV
CMPSWE: TYPSTR  ([ASCIZ/Please try again./],777,0,0,Y,Y)
        JRST    CMPSWA
; CHANGE CLASS
; (Changes all class information for a given class position of a user.)

CMDCUC: PUSHJ   P,BLDCUD
        JRST    GETCMD
	PUSHJ	P,GETCLP		; Get class position
	PUSHJ	P,GTUCLS
CMCUC3: TCR     (1)
        PUSHJ   P,GETUNM
        JRST    CHGDON
        PUSHJ   P,GCREC
        JRST    CMCUC3
        MOVE    AC1,CLSWPO
        SOJ     AC1,
        IMULI   AC1,NMSZCS
        SKIPG   NMCLSS(AC1)
        JRST    CMCUCE
        SNDBYT  (CUDPRT,\B.CCOM,CCLMUD,I)
        PUSHJ   P,SNMCUD
        SNDBYT  (CUDPRT,1,CLSWPO)
        PUSHJ   P,SWKCLS                ; SEND CLASS INFORMATION
	JRST	CMCUC3
        GETT    (/CHG/)
        JRST    CMCUC3
        TTFSTR  ([ASCIZ/Change Class/])
        PUSHJ   P,STDUHD
        PUSHJ   P,RELTTF
        JRST    CMCUC3
CMCUCE: TYPSTR  ([ASCIZ/No such class position./])
        JRST    CMCUC3
; Change GAN for one username or more.

CMDCUG: PUSHJ	P,ACTCUD
	JRST	GETCMD
	PUSHJ	P,GETGAN
	MOVE	AC,NMGAN
	MOVEM	AC,GANSPC
CMCUGS: TCR	(1)
	PUSHJ	P,GETUNM
	JRST	CHGDON
	PUSHJ	P,GCREC
	JRST	CMCUGS
	PUSHJ	P,CHKNET
	JRST	CMCUGS
	MOVE	AC,NMGAN
	CAMN	AC,GANSPC
	JRST	NO.CUG
	SNDBYT	(ACTPRT,\B.ACOM,CTGACT,I)	; Send code 235
	SNDBYT	(ACTPRT,\B.UUN,NMUUN)	; Send UUN
	SNDBYT	(AC,\B.GAN,GANSPC)	; Send the new GAN
	PUSHJ	P,AGANS
	JRST	CUG.AE
	PUSHJ	P,LUDAOT
	SNDBYT	(CUDPRT,\B.CCOM,CGAMUD,I)
	PUSHJ	P,SNMCUD
	SNDBYT	(CUDPRT,4,GANSPC)
	PUSHJ	P,SND40
	PUSHJ	P,NGANS
	JRST	CUG.CE
	PUSHJ	P,GET40
	PUSHJ	P,MSEDON
	GETT	(/CHG/)
	JRST	CMCUGS
	TTFSTR	([ASCIZ"Change GAN"])
	PUSHJ	P,STDUHD
	TTFSTR	([ASCIZ"Old Global Account: "],777,N)
	TTFNUM	(NMGAN,O,Y,Y)
	TTFSTR	([ASCIZ"New Global Account: "],777,N)
	TTFNUM	(GANSPC,O,Y,Y)
	PUSHJ	P,RELTTF
	JRST	CMCUGS
CUG.AE: TYPSTR	([ASCIZ"Error during Changing Global Account."])
	PUSHJ	P,ACTERR
	JRST	CMCUGS
CUG.CE: PUSHJ	P,GET40
	TYPSTR	([ASCIZ"CUD error -"],777,0,0,N)
	MOVEI	AC,40
	PUSHJ	P,CHROUT
	PUSHJ	P,NANOUT
	JRST	CMCUGS
NO.CUG: TYPSTR	([ASCIZ"No change - New GAN is same as old GAN."])
	JRST	CMCUGS
; CHANGE HOME

CMDCHO: PUSHJ   P,ACTCUD                ; GET ACTG. AND CUD CIRCUITS.
        JRST    GETCMD                  ; CANNOT GET CIRCUITS.
        TLNN    FLG,U.PRV               ; SKIP IF CUSTOMER
        JRST    CMCHO2
        PUSHJ   P,GVMAIL                ; GET MAIL NAME AND VERIFY IN TYMHST
CMCHO1: TCR     (1)
CMCHO2: 

DEFINE  CUHO    <
        X (NAMOUP,GNMSHO,1)             ; NOT ALLOWED TO CHANGE
        X (HSYSOU,GETHOH)
>
        EXMCHG  (CHO,CUHO,[ASCIZ/Change Home/])

        MOVE    AC,HOMSYS
        TRO     AC,HOM.CD
        MOVEM   AC,@CLSIND
        SNDBYT  (CUDPRT,\B.CCOM,CCLMUD,I)
        PUSHJ   P,SNMCUD                ; SEND USERNAME
        SNDBYT  (CUDPRT,1,1,I)          ; POSITION NO. ALWAYS 1
        PUSHJ   P,SNDCLS                ; SEND CLASS
        JRST    ERRCUD
        PUSHJ   P,GET40
        PUSHJ   P,ENTDON
        GETT    (/CHG/)
        JRST    CMCHOD
        TTFSTR  ([ASCIZ/Change Home/])
        PUSHJ   P,STDUHD
        TTFSTR  ([ASCIZ/Old Home System: /],777,N)
        TTFNUM  (OLDHOH,D)
        TTFSTR  ([ASCIZ/New Home System: /],777,N)
        TTFNUM  (HOMSYS,D)
        PUSHJ   P,RELTTF
CMCHOD: TLNN    FLG,U.PRV
        JRST    CHGDON
        TCR     (1)
        GETYR   ([ASCIZ "Do another? (Y/N):"])
        JRST    CHGDON
        JRST    CMCHO1
; CHANGE IRC
; This command allows the user to change the IRC code
; for any user name.

CMUIRC: PUSHJ   P,BLDCUD
        JRST    GETCMD
	PUSHJ	P,GETIRC
	MOVEM	AC1,WRKIRC
CMUIR1:	TCR	(1)
	PUSHJ   P,GETUNM
        JRST    CHGDON
        SNDBYT  (CUDPRT,\B.CCOM,CICMUD,I)
        PUSHJ   P,SNMCUD
   SNDBYT  (CUDPRT,\B.IRC,WRKIRC)
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
	JRST	[PUSHJ	P,ERRCUD
		 JRST	CMUIR1]
        PUSHJ   P,ENTDON
        PUSHJ   P,GET40
        GETT    (/CHG/)
        JRST    CMUIR1
        TTFSTR  ([ASCIZ/Change IRC/])
        PUSHJ   P,STDUHD
        PUSHJ   P,RELTTF
        JRST    CMUIR1
; CHANGE USER'S CUSTOMER NUMBER

CMDCUM: PUSHJ   P,ACTCUD
        JRST    GETCMD
        MOVEI   AC,NINE
        MOVEM   AC,ACTSYS
        PUSHJ   P,GVCSNR
	JFCL
CMCUMS: TCR     (1)
        PUSHJ   P,GETUNM
        JRST    CHGDON
        PUSHJ   P,GCREC
        JRST    CMCUMS
        PUSHJ   P,CHKNET
        JRST    CMCUMS
	SNDBYT  (ACTPRT,\B.ACOM,CTCACT,I) ; SEND CODE 234
        SNDBYT  (ACTPRT,\B.UUN,NMUUN)     ; SEND UUN
        SNDBYT  (AC,\B.CID,CUSNUM)        ; SEND CUSTOMER NUMBER
        PUSHJ   P,AGANS
        JRST    CUCNE1
        PUSHJ   P,LUDAOT
        MOVE    AC,ACCDIS
        CAME    AC,NMDIST
	PUSHJ	P,CDISCD
        GETT    (/CHG/)
        JRST    CMCUMS
	TTFSTR	([ASCIZ/Change Customer Number/])
        PUSHJ   P,STDUHD
        PUSHJ   P,TTCNOU                ; OUTPUT CUSTOMER NUMBER
        PUSHJ   P,RELTTF
        JRST    CMCUMS
CUCNE1: TYPSTR  ([ASCIZ/Error during Change Customer Number./])
        PUSHJ   P,ACTERR
        JRST    CMCUMS
; CHANGE MAIL
; (Change Mail Name for a username or a group of usernames.)

CMDCMU: PUSHJ   P,ACTCUD
        JRST    GETCMD
        PUSHJ   P,GVMAIL                  ; INPUT A MAIL NAME
CMCMA1: TCR     (1)
        PUSHJ   P,GETUNM                  ; GET USERNAME
        JRST    CHGDON
        PUSHJ   P,GCREC                  ; GET CUD RECORD
        JRST    CMCMA1
        PUSHJ   P,CHKNET
        JRST    CMCMA1
        PUSHJ   P,FINHOM                  ; FIND HOME SYSTEM
        JRST    CMCMA3
        ANDI    AC,HOMMSK
        MOVEM   AC,HOMSYS                 ; SAVE HOME SYSTEM
        PUSHJ   P,CKMLHS                  ; CHECK MAIL NAME FOR HOMSYS
        JRST    CMCMA1
        SNDBYT  (ACTPRT,\B.ACOM,CMNACT,I) ; SEND COMMAND CODE 215
        SNDBYT  (AC,\B.UUN,NMUUN)         ; SEND UUN
        SNDBYT  (AC,\B.UUN,MAIUUN)        ; SEND MAIL UUN
        PUSHJ   P,AGANS
        JRST    CMCMA4
        PUSHJ   P,LUDAOT
        GETT    (/CHG/)
        JRST    CMCMA1
        TTFSTR  ([ASCIZ/Change Mail/])
        PUSHJ   P,STDUHD
        PUSHJ   P,TTMOUT                ; OUTPUTS MAILNAME TO TT FILE
        PUSHJ   P,RELTTF
        JRST    CMCMA1
CMCMA3: PUSHJ   P,HOMOU2                  ; NO HOME SYSTEM
        JRST    CMCMA1
CMCMA4: CAIE    AC1,3
        JRST    [TYPSTR ([ASCIZ/Error during Change Mail/])
                 PUSHJ  P,LUDAOT
                 JRST   CMCMA1]
        PUSHJ   P,GTURCE
        JRST    CMCMA1
; CHANGE COLON

CMCCLN: PUSHJ   P,BLDCUD                ; BUILDING CIRCUIT TO CUD & ACTG.
        JRST    GETCMD
        SETOM   WKFLAG
        GETYR   ([ASCIZ "Do you want Colon Option? (Y/N):"])
        SETZM   WKFLAG
CHCLNS: TCR     (1)
        PUSHJ   P,GETUNM                ; GET USERNAME
        JRST    CHGDON
        PUSHJ   P,GCREC
        JRST    GETCMD
        PUSHJ   P,CHKNET
        JRST    CHCLNS
        MOVE    AC,NMCOLN
        SKIPE   WKFLAG
        JRST    SETCLN
        TLNE    AC,CLN.CD
        JRST    CHCLND
        TLO     AC,CLN.CD
        JRST    CHCOLN
SETCLN: TLNN    AC,CLN.CD
        JRST    CHCLND
        TLZ     AC,CLN.CD
CHCOLN: MOVEM   AC,NMCOLN
        PUSHJ   P,CCOOPT
	JRST	CHCLNS
        GETT    (/CHG/)
        JRST    CHCLNS
        TTFSTR  ([ASCIZ/Change Colon/])
        PUSHJ   P,STDUHD
        PUSHJ   P,RELTTF
CHCLND: PUSHJ   P,ENTDON
        JRST    CHCLNS

; CHANGE TGW (TRANSPARENT GATE WAY) OPTION

CMCTGW: PUSHJ	P,BLDCUD		; BUILDING CIRCUIT TO THE CUD
	JRST	GETCMD			; CIRCUIT ERROR
	SETZM	NMCPAR			; RESET CONTROL PARAMETER WORD
	PUSHJ	P,ASKTGW		; ASK FOR TRANSPARENT GATE WAY
	MOVE	AC,NMCPAR
	MOVEM	AC,WKFLAG
CHTGWS: TCR	(1)
	PUSHJ	P,GETUNM		; GET A USERNAME
	JRST	CHGDON
	PUSHJ	P,GCREC		; GET MUD DATA
	JRST	GETCMD			; ERROR
        PUSHJ   P,CHKNET
        JRST    CHTGWS
        MOVE    AC,NMCPAR
        SKIPE   WKFLAG
        JRST    SETTGW
        TRNN    AC,GTW.CD
        JRST    CHTGWD
        TRZ     AC,GTW.CD
        JRST    CHGTGW
SETTGW: TRNE    AC,GTW.CD
        JRST    CHTGWD
        TRO     AC,GTW.CD
CHGTGW: MOVEM   AC,NMCPAR
        PUSHJ   P,CLIWRD
        JRST    CHTGWS
        GETT    (/CHG/)
        JRST    CHTGWS
        TTFSTR  ([ASCIZ"Change Transparent Gate Way"])
        PUSHJ   P,STDUHD
        PUSHJ   P,RELTTF
CHTGWD: PUSHJ   P,ENTDON
        JRST    CHTGWS

SYSOUT: ; OUTPUTS SYSTEM TYPE
        TYPSTR  ([ASCIZ/System Type:/],777,0,0,N)
        TYPNUM  (ACTSYS,D,1)
        POPJ    P,

CNOOUT: ; OUTPUTS CUSTOMER NUMBER AND CUSTOMER NAME
	TYPSTR	([ASCIZ/Customer Number:/],777,0,0,N)
	TYPNUM	(CUSNUM,D,1,0,N)
	TYPSTR	([ASCIZ/ -/],777,0,0,N)
	TYPSTR	(ACCNAM,L.CUSN,1)
	POPJ	P,

SALOUT: ; OUTPUTS SALESMAN NUMBER
        MOVE    AC,ACSLS
        PUSHJ   P,SLSOUT
        POPJ    P,

GVCSNO: ; GET AND VERIFY A CUSTOMER NUMBER
        PUSHJ   P,GETCUS
        JRST    GVCSN1
        PUSHJ   P,GCSNFA                ; GET CUSTOMER NAME
        PUSHJ   P,CUSASK                ; TYPE CUST. NAME & ASK OK?
        JRST    GVCSN1
        POPJ    P,
GVCSN1: TCR     (1)
        JRST    GVCSNO

GCSNFA: ; GET CUSTOMER NAME FROM ACCOUNTING
        SNDBYT  (ACTPRT,\B.ACOM,LCNACT,I) ; CODE 51 = GET CUSTOMER NAME
        SNDBYT  (AC,\B.CID,CUSNUM)      ; SEND CUSTOMER NUMBER
        SNDBYT  (AC,\B.ASY,ACTSYS)      ; SEND SYSTEM TYPE
        PUSHJ   P,AGANS
        JRST    CSNER1
	MOVEI	AC,ACCNAM
	PUSHJ	P,GTCNFA
        POPJ    P,
CSNER1: TYPSTR  ([ASCIZ/Error during Get Customer Name from Accounting./])
        PUSHJ   P,LUDAOT
        POPJ    P,

GETSAL: ; GET A SALESMAN NUMBER
        INNUM   ([ASCIZ/Salesman Number:/],D)
        JRST    GETSAL
	SKIPL	AC1
        CAILE   AC1,^D99
        JRST    GETSA2
        MOVEM   AC1,ACSLS
        POPJ    P,
GETSA2: MOVEI	AC,[ASCIZ"Salesman number out of bounds <0-99>"]
	MOVEM	AC,ERRMSG
	PUSHJ   P,OUTOFB
        JRST    GETSAL

GETATN: ; GET ATTENTION LINE
        TYPSTR  ([ASCIZ/Mailing Information:/])
        INSTR   ([ASCIZ/Mail to attention of:/],ACATTN,L.ATTN)
        POPJ    P,

AD1OUT: ; OUTPUTS STREET1
        TYPSTR  ([ASCIZ/Street (1st):/],777,0,0,N)
        TYPSTR  (ACSTR1,L.STRE,1)
        POPJ    P,

GETAD1: ; GET STREET1
        INSTR   ([ASCIZ/Street (1st):/],ACSTR1,L.STRE)
        POPJ    P,

AD2OUT: ; OUTPUTS STREET2
        TYPSTR  ([ASCIZ/Street (2nd):/],777,0,0,N)
        TYPSTR  (ACSTR2,L.STRE,1)
        POPJ    P,

GETAD2: INSTR   ([ASCIZ/Street (2nd):/],ACSTR2,L.STRE)
        POPJ    P,

AD3OUT: ; OUTPUT EXTRA LINE
        TYPSTR  ([ASCIZ/Street (3rd):/],777,0,0,N)
        TYPSTR  (ACEXLN,L.STRE,1)
        POPJ    P,

GETAD3: INSTR   ([ASCIZ/Street (3rd):/],ACEXLN,L.STRE)
        POPJ    P,

CTYOUT: 
        TYPSTR  ([ASCIZ/City:/],777,0,T.P0,N)
        TYPSTR  (ACCITY,L.CITY,1)
        POPJ    P,
 
GETCTY:  
        INSTR   ([ASCIZ/City:/],ACCITY,L.CITY)
        POPJ    P,
    
STAOUT: 
        TYPSTR  ([ASCIZ/State:/],777,0,T.P0,N)
        TYPSTR  (ACSTAT,L.STAT,1)
        POPJ    P,
 
GETSTA:  
        INSTR   ([ASCIZ/State:/],ACSTAT,L.STAT)
        POPJ    P,
    
ZIPOUT: 
        TYPSTR  ([ASCIZ/Zip Code:/],777,0,T.P0,N)
        TYPSTR  (ACZIP,L.ZIP,1)
        POPJ    P,
 
GETZIP:  
        INSTR   ([ASCIZ/Zip Code:/],ACZIP,L.ZIP)
        POPJ    P,
    
CTROUT: 
        TYPSTR  ([ASCIZ/Country:/],777,0,T.P0,N)
        TYPSTR  (ACCTRY,L.CTRY,1)
        POPJ    P,
 
GETCTR:  
        INSTR   ([ASCIZ/Country:/],ACCTRY,L.CTRY)
        POPJ    P,

SNDADR: ; SEND ADDRESS TO ACCOUNTING SYSTEM
        SNDSTR  (AC,L.STRE,ACSTR1)        ; SEND STREET1 (51 BYTES)
        SNDSTR  (AC,L.STRE,ACSTR2)        ; SEND STREET2 (51 BYTES)
        SNDSTR  (AC,L.STRE,ACEXLN)        ; SEND EXTRA LINE (51 BYTES)
        SNDSTR  (AC,L.CITY,ACCITY)        ; SEND CITY (13 BYTES)
        SNDSTR  (AC,L.STAT,ACSTAT)        ; SEND STATE (2 BYTES)
        SNDSTR  (AC,L.ZIP,ACZIP)          ; SEND ZIP CODE (5 BYTES)
        SNDSTR  (AC,L.CTRY,ACCTRY)        ; SEND COUNTRY (5 BYTES)
        SNDSTR  (AC,L.ATTN,ACATTN)        ; SEND ATTENTION LINE (51 BYTES)
        POPJ    P,

GETDET: ; GET DETAIL CODE
        MOVEI   AC,^D8
        MOVEM   AC,ACDETC
        GETYR   ([ASCIZ "Detail Listings? (Y/N):"])
	POPJ	P,
        MOVEI	AC,^D3
	MOVEM	AC,ACDETC
        POPJ    P,

GITEMN: INNUM   ([ASCIZ/Item to change:/],D)
	JRST	GITEMN
        POPJ    P,

NALCHG: MOVEI	AC,[ASCIZ"This item is not allowed to change"]
	MOVEM	AC,ERRMSG
	PUSHJ	P,OUTOFB
	POPJ	P,

ILLCHG: MOVEI	AC,[ASCIZ"Illegal item number"]
	MOVEM	AC,ERRMSG
	PUSHJ	P,OUTOFB
	POPJ	P,

CNAOUT: TYPSTR  ([ASCIZ/New Customer Name:/],777,0,0,N)
        TYPSTR  (NCNMSP,L.CUSN,1)
        POPJ    P,

GETCNA: INSTR   ([ASCIZ/New Customer Name:/],NCNMSP,L.CUSN)
        MOVEI   AC1,L.CUSN
        MOVEI   AC,NCNMSP
        PUSHJ   P,CNTCHR
        SKIPN   AC1                     ; SKIP IF NOT BLANK
        JRST    [PUSHJ  P,INORNO
                 JRST   GETCNA]
        POPJ    P,
 
POSOUT:
        TYPSTR  ([ASCIZ/P.O. Start Date:/],777,0,0,N)
        MOVE    AC,ACPSDT
        PUSHJ   P,TYPDT
        POPJ    P,
 
POEOUT:
        TYPSTR  ([ASCIZ/P.O. Stop  Date:/],777,0,0,N)
        MOVE    AC,ACPEDT
        SKIPN   AC
        JRST    POEOU2
        PUSHJ   P,TYPDT
        POPJ    P,
POEOU2: TYPSTR  ([ASCIZ"12/31/99"],777,1)
        POPJ    P,
 
GETPO:
        INSTR   ([ASCIZ/P.O. Number:/],ACPONM,L.PO)
        POPJ    P,
 
GETPOS: INSTR   ([ASCIZ/P.O. Start Date:/],DATSPC,16)
        PUSHJ   P,CNVDTS
        JRST    [TCR    (1)
                 JRST   GETPOS]
        SKIPN   DATSPC
        JRST    [PUSHJ   P,GTODAY       ; GET TODAY' DATE, GTODAY IN TAT.MAC
                 MOVEM   AC,ACPSDT
                 POPJ    P,]
        MOVE    AC1,DATSPC
        MOVEM   AC1,ACPSDT
        POPJ    P,

GETPOE: INSTR   ([ASCIZ/P.O. Stop Date:/],DATSPC,16)
        PUSHJ   P,CNVDTS
        JRST    [TCR    (1)
                 JRST   GETPOE]
        MOVE    AC1,DATSPC
        MOVEM   AC1,ACPEDT
        POPJ    P,
      
GETPOM: INNUM   ([ASCIZ/P.O. Limit (zero if none):/],D)
	JRST	GETPOM
        MOVEM   AC1,ACPOMX
        POPJ    P,

GETCSM: ; GET CUSTOMER NUMBER AND CHECK WITH ACCOUNTING DATA BASE
        PUSHJ   P,GETCUS
        JRST    .-1
        MOVEI   AC,NINE
        MOVEM   AC,ACTSYS
        PUSHJ   P,GCAREC
        JRST    [TYPSTR ([ASCIZ/Not a Tymnet Customer Number./])
                 JRST   GETCSM]
        MOVE    AC1,ACCDIS
        CAMN    AC1,NMDIST              ; SAME DISTRICT?
        POPJ    P,                      ; YES
        TYPSTR  ([ASCIZ/Customer's district and User's district must match./])
        JRST    GETCSM

CHKNET: MOVE    AC1,NMDIST
        PUSHJ   P,CHKDIS
        JRST    [PUSHJ  P,ERDISA
                 POPJ   P,]
        AOS     (P)
        POPJ    P,

NNMOUT: ; OUTPUTS NEW USERNAME
        TYPSTR  ([ASCIZ/New Username:/],777,0,0,N)
        MOVEI   AC,NNMSPC
        MOVEM   AC,NUM
        SKIPN   NNMSPC
        JRST    NNMOU1
        TYPSTR  (@NUM,L.UNM,1)
        POPJ    P,
NNMOU1: PUSHJ   P,INPERR
        POPJ    P,

INPERR: ; ERROR DURING INPUT DATA FROM REQUEST FILE OR TERM
        MOVEI   AC,MRQERR
        TLNN    FLG2,V.RER
        MOVEI   AC,MINVIT
        MOVEM   AC,NUM
        TYPSTR  (@NUM,777,1)
        POPJ    P,
 
GETNNM: ; GET NEW USERNAME
        INSTR   ([ASCIZ/Enter New Username:/],NNMSPC,L.UNM,T)
        MOVEI   AC1,L.UNM
        MOVEI   AC,NNMSPC
        PUSHJ   P,CNTCHR
        SKIPN   AC1                     ; SKIP IF NOT BLANK
        JRST    GETNN1
        PUSHJ   P,CKNNAM
        JRST    GETNN1
        TLNE    FLG2,V.EXC
        JRST    GETNN3
        TLNE    FLG2,V.EXQ!V.PRQ!V.LSQ
        POPJ    P,
GETNN3: PUSHJ   P,CKNNON                ; CHECK NEW NAME AGAINST OLD NAME
        JRST    GETNN1
        PUSHJ   P,CKNNOV
        JRST    GETNN2
        POPJ    P,
GETNN2: TYPSTR  (NNMSPC,L.UNM,0,0,N)
	TYPSTR  (UNEXIT)
GETNN1: TRNE    FLG,U.RFI
        JRST    [SETZM  NNMSPC
                 SETZM  NNMSPC+1
                 SETZM  NNMSPC+2
                 TLO    FLG2,V.RER
                 POPJ   P,]
        TCR     (1)
        JRST    GETNNM

CKNNON: ; CHECK OLDNAME AGAINST NEWNAME
	CMPSTR	(UNMSPC,NNMSPC,L.UNM)
	JRST	CKNONS
	TYPSTR	([ASCIZ"A new username cannot be same as old username."])
	POPJ	P,
CKNONS: TLNN    FLG,U.TR6!U.CUS1
        JRST    CKNOND
        TLNE    FLG2,V.EXQ
        JRST    CKNOND
        SPLITN  (UNMSPC,CPOUNM,PROUNM)
        JRST    CKONNE
        SPLITN  (NNMSPC,CPUNM,PRUNM)
        JRST    CKONNE
        CMPSTR  (CPOUNM,CPUNM,^D10)
        JRST    CKONNE
CKNOND: AOS     (P)
        POPJ    P,
CKONNE: TYPSTR  ([ASCIZ/Illegal format of new username./])
        POPJ    P,

RWNNM:  ; WRITE NEW USERNAME TO REQUEST FILE
        REQOUT  (REQ,NNMSPC,L.UNM)
        POPJ    P,

RWUSR:  ; WRITE USERNAME TO REQUEST FILE
	REQOUT	(REQ,UNMSPC,L.UNM)
	POPJ	P,

NAMOUP: ; OUTPUT USERNAME TO TERMINAL AND/OR TOUT
        SKIPN   UNMSPC
        JRST    NMOUP1
	MOVEI	AC,UNMSPC
	PUSHJ	P,NAMOUT
	TLNN	FLG2,V.PRQ
	POPJ	P,
	PUSHJ	P,GCREC
	TLO	FLG2,V.RER		; SET REQUEST ERROR FLAG.
	POPJ	P,
NMOUP1: TYPSTR  ([ASCIZ/Username:/],777,0,0,N)
        PUSHJ   P,INPERR
        POPJ    P,

CUNINM: ; CHANGE USERNAME IN MUD
        SNDBYT  (CUDPRT,\B.CCOM,CUNMUD,I)
        PUSHJ   P,NNMCUD                ; SEND NEW NAME
        PUSHJ   P,SNMCUD                ; SEND OLD NAME
        PUSHJ   P,SND40                 ; SEND CUD TERMINATOR
        PUSHJ   P,NGANS                 ; GET AN ANSWER FROM CUD
        POPJ    P,
        PUSHJ   P,GET40
        AOS     (P)
        POPJ    P,

RCUNCD: ; REVERSE CHANGE USERNAME COMMAND IN MUD
        SNDBYT  (CUDPRT,\B.CCOM,CUNMUD,I)
        PUSHJ   P,SNMCUD                ; CHANGE NEW NAME BACK TO OLD NAME
        PUSHJ   P,NNMCUD
        PUSHJ   P,SND40
        PUSHJ   P,NGANS
        JRST    [TYPSTR ([ASCIZ/Reverse MUD entry not working./])
                 PUSHJ  P,ERRCUD
                 POPJ   P,]
        PUSHJ   P,GET40
        POPJ    P,

CUNINA: ; CHANGE USERNAME IN ACCOUNTING
        SNDBYT  (ACTPRT,\B.ACOM,CTYMCT,I)
        SNDBYT  (AC,\B.UUN,NMUUN)       ; SEND UUN
        SNDSTR  (AC,L.UNM,NNMSPC)       ; SEND NEW NAME
        PUSHJ   P,AGANS
        POPJ    P,
        AOS     (P)
        POPJ    P,
 
ACTERR: ; TYPE ACOUNTING ERROR MESSAGES
        MOVE    AC,ACANS
        CAIE    AC,3
        JRST    ACTER1
        TYPSTR  (UNMSPC,L.UNM,0,0,N)
        TYPSTR  ([ASCIZ/ not in the Accounting database./])
        POPJ    P,
ACTER1: CAIE    AC,2
        JRST    LUDAOT
        TYPSTR  (UNMSPC,L.UNM,0,0,N)
        TYPSTR  ([ASCIZ" - New username already exists in Accounting System."])
        POPJ    P,

ASKTGW: ; ASK FOR TRANSPARENT GATE WAY OPTION
        GETYR   ([ASCIZ "Do you want Transparent Gate Way Option? (Y/N):"])
        JRST    NOTGW
        MOVE    AC,NMCPAR
        TRO     AC,GTW.CD               ; SET TRANSPARENT GATEWAY BIT
        MOVEM   AC,NMCPAR
        POPJ    P,
NOTGW:  MOVE    AC,NMCPAR
        TRZ     AC,GTW.CD               ; CLEAR TRANSPARENT GATEWAY BIT
        MOVEM   AC,NMCPAR
        POPJ    P,

CLIWRD: ; CHANGE LICENSE WORD
        SNDBYT  (CUDPRT,\B.CCOM,CLIMUD,I)
        PUSHJ   P,SNMCUD                ; SEND USERNAME TO CUD
        MOVE    AC2,NMCPAR
        LSH     AC2,-^D8                ; RIGHT SHIFT 8 BITS
        SNDBYT  (CUDPRT,\B.CPR,AC2)     ; SEND CPARW
CLIRTN: PUSHJ   P,SND40
        PUSHJ   P,NGANS
	JRST	[PUSHJ	P,ERRCUD
		 POPJ	P,]
        PUSHJ   P,GET40
	AOS	(P)
        POPJ    P,

CNPOPT: ; CHANGE NO PASSWORD OPTION BIT
        SNDBYT  (CUDPRT,\B.CCOM,CNPMUD,I)
        PUSHJ   P,SNMCUD                ; SEND USERNAME
        MOVE    AC2,NMCOLN
        LSH     AC2,-^D20               ; RIGHT SHIFT 20 BITS
        SNDBYT  (CUDPRT,\B.CLN,AC2)
        JRST    CLIRTN

CCOOPT: ; CHANGE NO COLON OPTION
        SNDBYT  (CUDPRT,\B.CCOM,CCOMUD,I)
        PUSHJ   P,SNMCUD
        MOVE    AC2,NMCOLN
        LSH     AC2,-^D16
        SNDBYT  (CUDPRT,\B.CLN,AC2)
        JRST    CLIRTN

GNMSHO: ; GET USERNAME AND FIND HOME SYSTEM
        PUSHJ   P,GVUNAM
        JRST    GNMHO2
        PUSHJ   P,FINHOM
        JRST    GNMHOE
        TRNE    AC,GRP.CD
        JRST    GNMHOE
        ANDI    AC,HOMMSK
GNMHO1: MOVEM   AC,OLDHOH
        PUSHJ   P,CKMLAC
        JRST    GNMHO2
        POPJ    P,
GNMHO2: TCR     (1)
        JRST    GNMSHO
GNMHOE: TYPSTR	([ASCIZ/Username does not have Home System./])
        TLNN    FLG,U.PRV
        JRST    [TYPSTR ([ASCIZ/Command aborted./])
                 TCR    (1)
                 JRST   GETCMD]
        GETYR   ([ASCIZ "Do you want to continue? (Y/N):"])
        JRST    [TCR    (1)
                 JRST    GETCMD]
        SETZM   OLDHOH
        POPJ    P,

CKMLAC: TLNN	FLG,U.PRV
	JRST	CKML1
	MOVEI	AC,MAINAM
	MOVEI	AC1,L.UNM
	PUSHJ	P,CNTCHR
	CMPSTR	(MAINAM,TUMAIL,@AC1)
	JRST	CKMLAE
CKML1:	AOS	(P)
	POPJ	P,
CKMLAE: TYPSTR	(UNMSPC,L.UNM,0,0,N)
	TYPSTR	([ASCIZ" - Username does not belong to your account."])
	TYPSTR	(PLSREU)
	POPJ	P,

CKCDUD: ; CHECK CUSTOMER DISTRICT AGAINST USER DISTRICT
        MOVE    AC1,ACCDIS
        CAMN    AC1,NMDIST
        JRST    [AOS    (P)
                 POPJ   P,]
        TYPSTR  ([ASCIZ/Customer's district and User's district must match./])
        POPJ    P,

CKOLDP: ; Check new password against old password
	SNDBYT	(CUDPRT,\B.CCOM,CKPMUD,I)
	PUSHJ	P,SNMCUD
	SNDBYT	(CUDPRT,\B.CYP,NMPSW)
	PUSHJ	P,SND40
	PUSHJ	P,NGANS
	JFCL
	PUSHJ	P,GET40
	MOVE	AC1,NMANS
	CAIE	AC1,CUD.OK
	AOS	0(P)
	POPJ	P,

CDISCD: ; Change user's district in the CUD
	MOVEM	AC,DISRCT
	SNDBYT	(CUDPRT,\B.CCOM,CDIMUD,I)
	PUSHJ	P,SNMCUD		; Send username
	SNDBYT	(CUDPRT,\B.CDST,DISRCT)	; Send 2 bytes!
	PUSHJ	P,SND40
	PUSHJ	P,NGANS			; Get a byte from CUD
	JRST	[TYPSTR	([ASCIZ"Error during changing district in the CUD."])
		 PUSHJ	P,ERRCUD
		 POPJ	P,]
	PUSHJ	P,GET40
	PUSHJ	P,MSEDON
	POPJ	P,
GETCLP: ; Get class position
	INNUM	([ASCIZ/Class Position:/],D)
	JRST	GETCLP
	SKIPL	AC1
        CAILE   AC1,NMNCLS
	JRST	[MOVEI	AC,[ASCIZ"Illegal class position"]
		 MOVEM	AC,ERRMSG
		 PUSHJ	P,OUTOFB
		 JRST	GETCLP]
	MOVEM	AC1,CLSWPO		; Save class position
	POPJ	P,

INPGAN: ; Input GAN from terminal
	INNUM	([ASCIZ/Global Account:/],O)
	JRST	INPGAN
	RNO2:	MOVE	AC,CNVHLD
	CAMN	AC,[ASCII/     /]
	POPJ	P,
	CAIL	AC1,1
	CAILE	AC1,777777
	JRST	[MOVEI	AC,[ASCIZ"Global Account out of bounds <1-777777>"]
		 MOVEM	AC,ERRMSG
		 PUSHJ	P,OUTOFB
		 JRST	INPGAN]
	MOVEM	AC1,NMGAN
	AOS	(P)
	POPJ	P,

GTCNFA: ; Input customer name from accounting system.
	MOVEM	AC,CSNADR
	GETSTR	(ACTPRT,L.CUSN,@CSNADR)
	MOVEI	AC,@CSNADR
	MOVEI	AC1,L.CUSN
	PUSHJ	P,CNTCHR
	CAIE	AC1,L.CUSN
	POPJ	P,
	GETBYT	(ACTPRT,1,Y)
	JRST	GETCMD
	POPJ	P,
 hV)