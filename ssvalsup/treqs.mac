SUBTTL REQS - REQUEST ROUTINES

; CMDNRF creates a new Request Directory File, REQUES.DIR, this is
; available only in the debug version of TYMVALIDATE.
IFDEF	DEBUG, <
CMDNRF: GETYR	([ASCIZ"Are you sure to create a new REQUES.DIR? (Y/N):"])
	POPJ	P,
	TYPSTR	([ASCIZ"If you say so."])
	PUSHJ	P,LKRDF			; Lookup REQUES.DIR
	JRST	NEWRDF			; New Request Directory File
	TYPSTR	([ASCIZ"Existing file."])
	GETYR	([ASCIZ"Are you quite positive about this? (Y/N):"])
	JRST	CMNRFD
	TYPSTR	([ASCIZ"OK - you're the boss."])
NEWRDF:	MOVE	AC,[.CHCLS,,RDFCHN]	; close any old file for superceed.
	CHANIO	AC,
	HRLZI	AC,(777B8)		; Protection code = 'NO NO NO'
	PUSHJ	P,BLDRDF		; Build a block for REQUES.DIR
	MOVE	AC,[.CHENT,,RDFCHN]	; Enter REQUES.DIR
	CHANIO	AC,NRFLK
	JRST	CMNRFE
	MOVEI	AC1,RDFSIZ		; Number of pages to create.
	MOVE	AC,[.CHCFP,,RDFCHN]
	SETO	AC2,
CREPGS:	CHANIO	AC,AC2			; Create a file page.
	JRST	CMNRFE
	AOJ	AC2,			; Bump the page number by 1.
	SOJG	AC1,CREPGS		; Do the rest.
	TYPSTR	([ASCIZ/Done./])
	JRST	CMNRFD
CMNRFE: TYPSTR	([ASCIZ"Error opening REQUES.DIR"])
CMNRFD: MOVE	AC,[.CHREL,,RDFCHN]
	CHANIO	AC,
	POPJ	P,
>
; End of CMDNRF code.

; NTMRQF creates temp filename for request files and store
; in lookup block also prepare rest of lookup/enter block.
NTMRQF:	PJOB	AC,		; get our absolute frame number
	ADDI	AC,^D1000	; add a thousand so names will be same size
	MOVEI	AC4,^D10	; decimal conversion
	TRO	FLG,U.SIX	; set sixbit flag
	PUSHJ	P,NUMOUT	; convert to sixbit chars
	TRZ	FLG,U.SIX	; reset sixbit flag
	MOVE	AC,NUMSP	; get sixbit filename
	IOR	AC,TQSUFF	; or in the suffix into the filename
	MOVEM	AC,TMRLK+.RBNAM	; store in lookup block
	MOVE	AC,[DB.DIR]
	MOVEM	TMRLK+.RBPPN
	SETZM	TMRLK+.RBEXT
	HRLZI	AC,(777B8)
	MOVEM	AC,TMRLK+.RBPRV
	POPJ	P,

NPMRQF:	; create request file name and store in lookup block
	MOVE	AC,[DB.DIR]
	MOVEM	AC,PMRLK+.RBPPN
	MOVE	AC,REQNUM	; convert reqnum to sixbit
	MOVEI	AC4,^D10
	TRO	FLG,U.SIX
	PUSHJ	P,NUMOUT
	TRZ	FLG,U.SIX
	MOVE	AC,NUMSP
	MOVEM	AC,PMRLK+.RBNAM	; move it into enter/lookup blok
	SETZM	PMRLK+.RBEXT
	HRLZI	AC,(777B8)
	MOVEM	AC,PMRLK+.RBPRV	; set prot = 'no no no'
	MOVEI	AC,PMRLKS	; set size of lookup block
	MOVEM	AC,PMRLK
	POPJ	P,

NPBRQF:	; create request file name in form Bxxxx. and store in lk blk
	PUSHJ	P,NPMRQF	; create std perm name
	MOVE	AC,PMRLK+.RBNAM	; get name
	LSH	AC,-6		; right shift one char
	TLO	AC,(SIXBIT/B/)	; put a b into 1st char position
	MOVEM	AC,PMRLK+.RBNAM
	POPJ	P,

AOSRQN: AOS	AC1,@RDFWD0
	JFCL	17,.+1
	ANDI	AC1,REQMAX
	SKIPG	AC1
	JRST	AOSRQN
	POPJ	P,

; GNXTRQ places the current available request number in REQNUM.
; It then finds the next available request number and updates word 0
; of reques.dir. Then it deletes any files associated with the current
; request number. As a byproduct, the next request number is placed
; in NEXREQ, but this can't be reliable because other copies of TYMVALIDATE
; may allocate requests at any time.

GNXTRQ: PUSHJ	P,ENTRDF		; Open REQUES.DIR in Multi-Update
	HRRZ	AC,.JBFF		; Get 1st free location for
	MOVEM	AC,SAVJBF		; buffer I/O to use and save it
	HRLI	AC,(3B7!1B17)		; Read-Write, Count
	HRRI	AC,1			; File page 1
	PUSHJ	P,MAP1PG		; Map file page 1 to virtual page
	HRRZ	AC,TTFPAG		; Get the virtual page
	MOVEM	AC,RDFPG1		; Save the virtual page
	LSH	AC,11			; Compute the 1st word address of
	MOVEM	AC,RDFWD0		; the virtual page
	ADDI	AC,1777			; Now compute last occupied address
	HRRM	AC,.JBFF		; Store new .JBFF value
	PUSHJ	P,AOSRQN		; Get next available request number
	MOVEM	AC1,BGREQN		; Save it
	SETZM	TTFPGM
GNXTR1: MOVEM	AC1,REQNUM		; Save current avail req # in REQNUM
	PUSHJ	P,CKREQN		; Check this request number
	JRST	GNXTRD			; OK to use this request no.
	PUSHJ	P,AOSRQN		; Cannot use this req #, find next one
	CAMN	AC1,BGREQN		; Is REQUES.DIR full?
	JRST	RDF.E1			; Yes
	JRST	GNXTR1			; No, continue
GNXTRD: PUSHJ	P,UNMAP1		; Unmap file page number 1
	PUSHJ	P,UNMAPD		; Unmap last data page
	PUSHJ	P,CLSRDF		; Release Request Directory Channel
	MOVE	AC,SAVJBF		; Restore 1st free loc for buffer
	MOVEM	AC,.JBFF		; I/O to use
GNXTD2: ; Now we can delete any existing files associated with this request
	MOVE	AC,[.CHOPN,,RDFCHN]
	CHANIO	AC,NRFOPN		; Open a channel
	JRST	GNXTD2
	MOVEI	AC,MAXINQ-1		; Number of files - 1 to delete
	MOVEM	AC,RQFCT		; Save in RQFCT
GNXTD3: PUSHJ	P,NPMRQF		; Create Lookup block
	MOVE	AC1,RQFCT		; Get index
	MOVE	AC,QEXT(AC1)		; Get extention
	MOVEM	AC,PMRLK+.RBEXT		; Move into Lookup block
	MOVE	AC,[.CHLK,,RDFCHN]
	CHANIO	AC,PMRLK		; Lookup a file
	JRST	GNXTD4			; No such file
	MOVE	AC,[.CHREN,,RDFCHN]
	SETZB	AC1,AC2
	CHANIO	AC,AC1			; Delete a file
	JFCL
GNXTD4:	SOSL	RQFCT			; Done?
	JRST	GNXTD3			; No, do next file
	MOVE	AC,[.CHREL,,RDFCHN]	; Yes
	CHANIO	AC,			; Release channel
	POPJ	P,

CKREQN: ; Check whether request number in REQNUM is available for use or not
	MOVE	AC,REQNUM
	IDIVI	AC,1000		; DIVIDE BY NUMBER OF REQUEST PER PAGE
	AOJ	AC,		; CORRECT
	CAMN	AC,TTFPGM
	JRST	CKRQN1
	MOVEM	AC,TTFPGM	; SAVE FILE PAGE
	PUSHJ	P,FPG2MP	; COMPUTE VIRTUAL PAGE
	HRLI	AC,(3B7!1B17)	; READ-WRITE, COUNT
	MOVEM	AC,TTFPAG
	MOVE	AC,[.CHMFP,,RDFCHN]
	CHANIO	AC,TTFPAG	; MAP FILE PAGE TO VIRTUAL PAGE
	JRST	[PUSHJ	P,UNMAP1
		 JRST	RDF.E2]
CKRQN1: MOVE	AC1,REQNUM	; GET REQUEST NUMBER TO CHECK
	ANDI	AC1,777		; WITHIN THIS PAGE
	HRRZ	AC2,TTFPAG	; GET VIRTUAL PAGE
	IMULI	AC2,1000	; CONVERT TO ADDRESS
	ADD	AC1,AC2
	MOVE	AC,(AC1)	; GET A WORD
	JUMPE	AC,CKRQND	; GOT IT
	TLNE	AC,R.NAC	; SKIP IF ACTIVE REQUEST
	JRST	CKRQND		; GOT IT
CKRQN2: AOS	(P)
	POPJ	P,
CKRQND: TLNE	AC,R.USE
	JRST	CKRQN2
	TLO	AC,R.USE
	MOVEM	AC,(AC1)
	POPJ	P,

UNMAP1: ; UNMAP FILE PAGE 1
	MOVE	AC,RDFPG1
	HRLI	AC,1
	VCLEAR	AC,
	JFCL
	POPJ	P,

UNMAPD: ; UMMAP LAST DATA PAGE
	HRRZ	AC,TTFPAG
	HRLI	AC,1
	VCLEAR	AC,
	JFCL
	POPJ	P,

CLSRDF: ; CLOSE REQUEST DIRECTORY FILE
	MOVE	AC,[.CHREL,,RDFCHN]
	CHANIO	AC,
	POPJ	P,

RDRDF:	; Input value of request word for request (REQNUM) into REQVAL
	SETOM	REQVAL			; Set REQVAL to -1
	MOVE	AC,REQNUM		; Get request number
	SKIPLE	AC			; Request number must be in the
	CAILE	AC,REQMAX		; range of 1 to REQMAX
	POPJ	P,			; Request number out of range
	PUSHJ	P,LKRDF			; Open REQUES.DIR in read mode
	JRST	RDF.NF			; REQUES.DIR not found
	MOVE	AC,REQNUM
	LSH	AC,-11			; Get the file page number
	AOJ	AC,			; Adjust the file page number
	HRLI	AC,(1B7!1B17)		; Read-Only, Count
	PUSHJ	P,MAP1PG		; Map file page into virtual page
	PUSHJ	P,CALRQN		; Find address(REQNUM) in virtual page
	MOVE	AC,0(AC1)		; Get the value
	MOVEM	AC,REQVAL		; return the value in REQVAL
	PUSHJ	P,UNMAPD		; Unmap the page
	PUSHJ	P,CLSRDF		; Release REQUES.DIR
	POPJ	P,

WRRDF:	; Write the value in REQVAL into REQUES.DIR file at REQNUM
	MOVE	AC,REQNUM		; Get request number
	SKIPLE	AC			; Request number must be in the
	CAILE	AC,REQMAX		; range of 1 to REQMAX
	POPJ	P,			; Request out of range
	PUSHJ	P,ENTRDF		; Open REQUES.DIR in Multi-Update
	MOVE	AC,REQNUM
	LSH	AC,-11			; Get the file page number
	AOJ	AC,			; Adjust the file page number
	HRLI	AC,(3B7!1B17)		; Read-Write, Count
	PUSHJ	P,MAP1PG		; Map file page into virtual page
	PUSHJ	P,CALRQN		; Find address(REQNUM) in virtual page
	MOVE	AC,REQVAL		; Get value to write
	TLZ	AC,R.USE
	MOVEM	AC,0(AC1)		; Write it
	PUSHJ	P,UNMAPD		; Unmap the page
	PUSHJ	P,CLSRDF		; Release REQUES.DIR
	AOS	0(P)			; Skip return
	POPJ	P,

CALRQN: ; Calculate the address of REQNUM in virtual page
	HRRZ	AC1,TTFPAG		; Get the virtual page number
	LSH	AC1,11			; 1st word address of virtual page
	MOVE	AC,REQNUM		; Get the request number
	ANDI	AC,777			; Relative address in file page
	ADD	AC1,AC			; Address(REQNUM) in virtual page
	POPJ	P,

MAP1PG: ; Map one file page into virtual page
	HRRZM	AC,TTFPGM		; TTFPGM= File page number
	HLLZM	AC,TTFPAG		; TTFPAG= Protection!Count,,0
	PUSHJ	P,FPG2MP		; Find next available virtual page
	HRRM	AC,TTFPAG		; TTFPAG= Prot!Count,,Virtual page
	MOVE	AC,[.CHMFP,,RDFCHN]	; .CHMFP= 35
	CHANIO	AC,TTFPAG		; Map file page into virtual page
	JRST	RDF.E2			; Fatal error in REQUES.DIR
	POPJ	P,			; Return

RDF.NF: ; Request Directory File is not found.
	PUSHJ	P,CLSRDF
	BOMB	(ERR.RN)

RDF.E1: ; REQUEST DIRECTORY IS FULL
	PUSHJ	P,UNMAP1	; UNMAP FILE PAGE 1
	PUSHJ	P,UNMAPD	; UNMAP LAST DATA PAGE
	PUSHJ	P,CLSRDF	; CLOSE REQUES.DIR
	BOMB	(ERR.R1)	; REQUES.DIR FULL

RDF.E2: ; FATAL ERROR IN REQUEST DIRECTORY FILE
	PUSHJ	P,CLSRDF	; CLOSE REQUEST DIRECTORY FILE
	BOMB	(ERR.R2)	; FATAL ERROR IN REQUES.DIR

GNMREQ: ; starting with REQNUM+1, retieve each word in reques.dir.
; do a pushj p, to the address provided in ac. If the routine
; skip returns continue, otherwise release reques.dir

	MOVEM	AC,REQPAD		; save
	AOS	REQNUM			; start with next request number
	MOVE	AC,REQNUM
	SKIPL	AC			; skip if < 0
	 CAILE	AC,REQMAX		; skip if <= max
		POPJ	P,			; return if out of range
GMNRQ0:
	PUSHJ	P,ENTRDF		; enter reques.dir
	MOVE	AC,REQNUM
	IDIVI	AC,1000			; divide by request per page
	AOJ	AC,			; correct page
	MOVEM	AC,TTFPGM		; save page
	PUSHJ	P,FPG2MP		; get virtual core page
	HRLI	AC,(3B7!1B17)		; prot, count
	MOVEM	AC,TTFPAG
	MOVE	AC,[.CHMFP,,RDFCHN]	; map file page to it
	CHANIO	AC,TTFPAG
	 BOMB	(ERR.R2)
	MOVE	AC1,REQNUM		; get request number
	ANDI	AC1,777			; isolate request within this page
	MOVEI	AC,1000			; calculate number of requests left
	SUB	AC,AC1			;	on this page.
	MOVNS	AC			; make negative.
	HRRZ	AC2,TTFPAG		; get core page address
	IMULI	AC2,1000		; convert to word address
	ADD	AC1,AC2			; add start point
	HRLM	AC,AC1			; ac1 now aobjn index
GMNRQ1:	MOVE	AC,0(AC1)		; move word from reques.dir to ac
	PUSHJ	P,@REQPAD		; perform the test
	JRST	GMNRQ2			; match found
	AOS	REQNUM			; increment request number
	AOBJN	AC1,GMNRQ1		; do next request this page
	HRRZ	AC,TTFPAG		; unmap this page
	HRLI	AC,1
	VCLEAR	AC,
	 JFCL
	MOVE	AC,REQNUM		; see if any more requests left
	CAILE	AC,REQMAX
	JRST	GMNRQ3			; no more requests
	JRST	GMNRQ0			; map next page
GMNRQ2:	; here when a match is found
	HRRZ	AC,TTFPAG		; unmap page
	HRLI	AC,1
	VCLEAR	AC,
	 JFCL
	MOVE	AC,[.CHREL,,RDFCHN]	; release channel
	CHANIO	AC,
	AOS	0(P)			; skip return
	POPJ	P,
GMNRQ3:	; match not found
	HRRZ	AC,TTFPAG		; unmap page
	HRLI	AC,1
	VCLEAR	AC,
	 JFCL
	MOVE	AC,[.CHREL,,RDFCHN]	; release channel
	CHANIO	AC,
	POPJ	P,			; non-skip return


REQTS1:	; non skip return if the value in ac skips on a tdnn with reqmsk.
	TDNN	AC,REQMSK		; test against mask
	 AOS	0(P)
	POPJ	P,

REQTS2:	; non skip return if the value in ac skips on a tdnn with reqmsk
;		and with reqms2.
	TDNN	AC,REQMSK
	JRST	SPOPJ
	TDNN	AC,REQMS2
	JRST	SPOPJ
	POPJ	P,
SPOPJ:	AOS	0(P)
	POPJ	P,

REQTS3:	; non skip return if the value in ac skips on a tdnn with reqmsk
;		and on a tdne with reqms2
	TDNN	AC,REQMSK
	JRST	SPOPJ
	TDNE	AC,REQMS2
	JRST	SPOPJ
	POPJ	P,

REQTS4:	; non skip return if the right half of ac matches my.uun
;	destroys left half of ac
	HRRZS	AC
	CAME	AC,MY.UUN	; skip if match
	JRST	SPOPJ
	POPJ	P,
 
CHKCOM:	; check if request is complete
	MOVE	AC,REQVAL
	TLNN	AC,R.COM		; skip if complete
	JRST	CHKCO1
	TYPSTR	([ASCIZ/This request has been processed./])
	POPJ	P,
CHKCO1:	AOS	0(P)
	POPJ	P,
CHKRE1:	AOS	0(P)
	POPJ	P,

CHKEXA:	; check if request need examined
	MOVE	AC,REQVAL
	TLNE	AC,R.NEX		; skip if not need examined
	JRST	CHKEX1
	TYPSTR	([ASCIZ"** This request should not be examined **"])
	POPJ	P,
CHKEX1:	AOS	0(P)
	POPJ	P,

CHLOCK:	; get the reqval value for reqnum, if locked just return
;	if not locked, lock it ans skip return
	MOVEI	AC,CHLOC2
	MOVEM	AC,REQPAD
	JRST	GMNRQ0		; go get the value
CHLOC2:
	JUMPE	AC,CHLOC3	; if zero, just return
	TLOE	AC,R.LCK	; skip if not locked
	JRST	CHLOC3
	MOVEM	AC,0(AC1)	; lock request
	MOVEM	AC,REQVAL	; move to reqval if updated
	POP	P,AC
	JRST	GMNRQ2		; release the file and skip return 
CHLOC3:	POP	P,AC
	TYPSTR	([ASCIZ/Request/],777,0,0,N)
	TYPNUM	(REQNUM,D,1,0,N)
	TYPSTR	([ASCIZ/is locked./],777,1)
	JRST	GMNRQ3		; release the file and just return

BLDRDF: ; Prepare the lookup block for REQUES.DIR
	MOVEM	AC,NRFLK+.RBPRV		; Protection code
	MOVE	AC,[DB.DIR]		; Database Directory
	MOVEM	AC,NRFLK+.RBPPN
	MOVE	AC,[SIXBIT/REQUES/]	; File name
	MOVEM	AC,NRFLK+.RBNAM
	HRLZI	AC,(SIXBIT/DIR/)	; File extention
	MOVEM	AC,NRFLK+.RBEXT
	MOVEI	AC,NRFLKS		; Block size
	MOVEM	AC,NRFLK
	POPJ	P,

LKRDF: ; Initial RDFCHN and Lookup (USEVAL)REQUES.DIR
	MOVE	AC,[.CHOPN,,RDFCHN]
	CHANIO	AC,NRFOPN		; Open a channel
	JRST	LKRDF
	SETZ	AC,			; Zero protection code
	PUSHJ	P,BLDRDF		; Build lookup block for REQUES.DIR
	MOVE	AC,[.CHLK,,RDFCHN]
	CHANIO	AC,NRFLK		; Lookup Request Directory File
	POPJ	P,			; Request Directory File not found
	AOS	(P)			; Skip return
	POPJ	P,

ENTRDF: ; Select REQUES.DIR for multi-update output.
	PUSHJ	P,LKRDF			; Initial RQFCHN & Lookup REQUES.DIR
	JRST	RDF.NF			; REQUES.DIR not found
	HRLZI	AC,(777B8)		; Protection code = 'NO NO NO'
	PUSHJ	P,BLDRDF		; Build Lookup Block
	MOVE	AC,[.CHMEN,,RDFCHN]	; Select file for
	CHANIO	AC,NRFLK		; multi-update output
	JRST	ENTRQE			; Error in ENTER
	SETZM	TTBCT
	POPJ	P,
ENTRQE: ; Enter error trying to enter REQUES.DIR
	HRRZ	AC,NRFLK+.RBEXT		; Get error code
	CAIN	AC,.ERFBM		; Skip if not file busy
	JRST	ENTQE1
	MOVEM	AC,NUM
	TYPSTR	([ASCIZ/Error opening REQDIR - error = /],777,0,0,N,Y)
	TYPNUM	(NUM,O,1,0,Y,Y)
	JRST	CMDQUI
ENTQE1: ; Here when REQUES.DIR is busy
	MOVE	AC,[.CHREL,,RDFCHN]	; release channel
	CHANIO	AC,
	MOVE	AC,TTBCT
	CAIE	AC,3
	JRST	ENTQE2
; type file busy error every 'mod (try-count, 1000) = 3'
	TYPSTR	([ASCIZ/REQDIR file busy./])
ENTQE2:	CAIN	AC,^D1000
	JRST	ENTQE3
	AOS	TTBCT
	JRST	ENTRDF	; try again
ENTQE3:	SETZM	TTBCT
	JRST	ENTRDF	; try again

REQSTO:	; output a string to a request file
; input:	ac/ address of string
;		ac1/ max length of string (string terminates on null)
;		ac2/ index into request file tables
; this routine copies string into REQSBU, so <ac1> must be <= size(REQSBU)
	HRLZS	AC	; move address to left half
	HRRI	AC,REQSBU
	BLT	AC,REQSBU+REQSSI-1	; copy string
	MOVEM	AC2,RQFCT		; save index
	MOVEM	AC1,RQFLN		; save max ct
	SKIPL	QACTO(AC2)		; skip if active
	 PUSHJ	P,REQOPN		; activate file
	MOVE	AC2,RQFCT		; get index
	MOVE	AC3,OTQTAB(AC2)		; address of ring header
	ADDI	AC3,2			; adjust to byte count
	MOVE	AC1,RQFLN
	MOVE	AC,[POINT 7,REQSBU]
	MOVEM	AC,REQSBP
REQST1:
	SOSGE	0(AC3)			; skip if buffer still has room
	 PUSHJ	P,REQONB		; output last buffer
	ILDB	AC,REQSBP		; get a byte
	JUMPE	AC,REQST2		; jump out of loop if null
	IDPB	AC,-1(AC3)		; deposit in buffer
	SOJG	AC1,REQST1		; next
	SOSGE	0(AC3)			; put in <crlf>
	 PUSHJ	P,REQONB
REQST2:
	MOVEI	AC,.CR
	IDPB	AC,-1(AC3)
	SOSGE	0(AC3)
	 PUSHJ	P,REQONB
	MOVEI	AC,.LF
	IDPB	AC,-1(AC3)
	POPJ	P,			; return

TTYBSZ==23
DSKBSZ==203
INITBR: ; Initialize a Buffer Ring
	PUSH	P,AC
	PUSH	P,AC1
	PUSH	P,AC2
	MOVEI	AC,DSKBSZ
	MOVE	AC1,CA.BRH
	CAIN	AC1,INR1
	MOVEI	AC,TTYBSZ
	MOVEM	AC,BUFSIZ
	SUBI	AC,2
	MOVEM	AC,BFSIZ1
        HRRZ    AC,CA.BRG
        AOJ     AC,
        TLO     AC,(1B0)
        MOVE    AC1,CA.BRH
        MOVEM   AC,@AC1
        AOJ     AC1,
        MOVE    AC2,[700,,0]
        MOVEM   AC2,@AC1
        AOJ     AC1,
        SETZM   @AC1
        MOVEI   AC2,3
INIBR1: HRRZ    AC1,AC
        ADD     AC,BUFSIZ
        HRL     AC,BFSIZ1
        MOVEM   AC,@AC1
        SOJG    AC2,INIBR1
        HRRZ    AC1,AC
        HRRZ    AC,CA.BRG
	AOJ	AC,
        HRL     AC,BFSIZ1
        MOVEM   AC,@AC1
	POP	P,AC2
	POP	P,AC1
	POP	P,AC
        POPJ    P,

REQOPN:	; init request file channel and enter a file
	MOVE	AC,OTQTAB(AC2)
	HRLZM	AC,REQFBO+2
	HRRZM	AC,CA.BRH
	HRLI	AC,.CHOPN		; open channel code
	HRR	AC,QCHNO(AC2)		; channel
	CHANIO	AC,REQFBO
	JRST	REQOPN
	PUSHJ	P,NTMRQF		; create filename
	MOVE	AC2,RQFCT		; get index
	MOVE	AC,QEXT(AC2)		; get extention
	MOVEM	AC,TMRLK+.RBEXT		; put into enter block
	MOVE	AC,ORBRTB(AC2)
	HRRZM	AC,CA.BRG
	HRLI	AC,.CHENT
	HRR	AC,QCHNO(AC2)		; channel
	CHANIO	AC,TMRLK		; enter
	 BOMB	(ERR.R3)		; suicide if enter does not work
        PUSHJ   P,INITBR
	HRLI	AC,.CHOUT
	CHANIO	AC,			; out
	 SKIPA
	 BOMB	(ERR.R3)
	MOVE	AC2,RQFCT		; set active flag
	SETOM	QACTO(AC2)
	POPJ	P,

REQONB:	; output a bufferful
	MOVE	AC2,RQFCT
	HRLI	AC,.CHOUT
	HRR	AC,QCHNO(AC2)
	CHANIO	AC,
	 SKIPA
	 BOMB	(ERR.R3)
	POPJ	P,

RELRQO:	; close output request files and rename them as permanent reqs
; If the contents of REQNUM is not zero, use this as the request number,
; deleting any existing files by the same name.	If REQNUM is zero, then
; this routine allocates a request number via GNXRQO and then renames
; the temp request output files to their permanent form.
; The contents of reqval are then copied to the correct word in reques.dir.
	SKIPG	REQNUM		; skip if contents of reqnum > 0
	 PUSHJ	P,GNXTRQ	; get a request number
	MOVEI	AC,MAXINQ-1	; number of files -1
	MOVEM	AC,RQFCT
RELRO1:
	MOVE	AC1,RQFCT
	SKIPN	QACTO(AC1)	; skip if active request file
	JRST	RELRO5
	PUSHJ	P,NPMRQF	; create lookup block
	MOVE	AC1,RQFCT
	MOVE	AC,QEXT(AC1)	; get extention
	MOVEM	AC,PMRLK+.RBEXT
	MOVE	AC,[.CHOPN,,TMPCHN]	; open temp channel
	CHANIO	AC,TMPOPN
	JRST	.-1
	TLO	FLG2,V.NBU	; set no-backup flag
	MOVE	AC,[.CHLK,,TMPCHN]
	CHANIO	AC,PMRLK	; lookup any old file by same name
	JRST	RELRO3
	TLZ	FLG2,V.NBU	; reset nobackup flag
	PUSHJ	P,NPBRQF	; get backup filename lookup block
	MOVE	AC,[.CHREN,,TMPCHN]	; rename old file
	CHANIO	AC,PMRLK
	 BOMB	(ERR.R3)
RELRO3:	PUSHJ	P,NPMRQF	; get permanent name
	MOVE	AC1,RQFCT
	MOVE	AC,QEXT(AC1)
	MOVEM	AC,PMRLK+.RBEXT
	HRLI	AC,.CHREN	; rename
	HRR	AC,QCHNO(AC1)
	CHANIO	AC,PMRLK
	 BOMB	(ERR.R3)
	HRLI	AC,.CHREL	; release channel
	CHANIO	AC,
	SETZM	QACTO(AC1)	; reset active switch
	TLNE	FLG2,V.NBU	; skip if backup file to delete
	JRST	RELRO4
	SETZB	AC1,AC2		; delete backup file
	MOVE	AC,[.CHREN,,TMPCHN]
	CHANIO	AC,AC1
	 JFCL
RELRO4:	MOVE	AC,[.,,TMPCHN]
	CHANIO	AC,
RELRO5:
	SOSL	RQFCT
	JRST	RELRO1		; do next file
	PUSHJ	P,WRRDF		; copy reqval to reques.dir
	 BOMB	(ERR.R2)
	POPJ	P,		; return

QSTRIN:	; input a line from a request type file
; input:	ac/ address of buffer to input to
;	 ac1/ max length to input
;	 ac2/ index into request file tables
;	 reqnum/ request number
 	MOVEM	AC2,RQFCT	; save index
	MOVEM	AC1,RQFLN	; save length
	MOVEM	AC,RQFBA	; save address of buffer

	IDIVI	AC1,5		; divide by number of chars per word
	SKIPE	AC1+1		; skip if division was even
	 AOJ	AC1,		; increment number of words by one
	MOVE	AC2,[ASCII /     /]
	MOVEM	AC2,@RQFBA	; move spaces to 1st word in buffer
	MOVE	AC2,RQFBA
	AOJ	AC2,
	HRL	AC2,RQFBA
	MOVE	AC3,RQFBA
	ADDI	AC3,-1(AC1)	; end address for blt
	BLT	AC2,0(AC3)	; copy spaces to rest of buffer
	MOVE	AC2,RQFCT
	SKIPL	QACTI(AC2)	; skip if file open
	 PUSHJ	P,REQOPI	; go open a file
	MOVE	AC2,RQFCT
	MOVE	AC3,INQTAB(AC2)	; address of ring header
	ADDI	AC3,2		; adjust
	MOVE	AC1,RQFLN	; count
	MOVE	AC,RQFBA	; buffer address
	HRLI	AC,(POINT 7)	; make it into byte pointer
	MOVEM	AC,REQSBP	; save
QSTR1:
	SOSGE	0(AC3)		; skip if buffer still has chars
	 PUSHJ	P,REQINB	; go input another buffer
	ILDB	AC,-1(AC3)	; get a char
	CAIN	AC,.LF		; skip if not a line feed
	JRST	QSTRD		; finished
	CAIGE	AC," "		; skip if not a control char
	JRST	QSTR1		; get another
	IDPB	AC,REQSBP	; deposit into buffer
	SOJG	AC1,QSTR1	; get next char
; waste chars until we hit a line feed
QSTR2:
	SOSGE	0(AC3)
	 PUSHJ	P,REQINB
	ILDB	AC,-1(AC3)
	JUMPE	AC,QSTRD
	CAIN	AC,.LF
	JRST	QSTRD
	JRST	QSTR2
QSTRD:
	POPJ	P,

REQOPI:	; init request file channel and lookup a file
;	just to keep things interesting, this routine will return
;	to the caller if it is successful, but will return to
;	the caller's caller if it is not.
	MOVE	AC,INQTAB(AC2)		;	buffer ring header address
	HRRZM	AC,REQFBO+2
	HRRZM	AC,CA.BRH
	HRLI	AC,.CHOPN
	HRR	AC,QCHNI(AC2)
	CHANIO	AC,REQFBO		; open a channel 
	JRST	REQOPI
	PUSHJ	P,NPMRQF		; create lookup block
	MOVE	AC2,RQFCT
	MOVE	AC,QEXT(AC2)		; move in the extention
	MOVEM	AC,PMRLK+.RBEXT
	HRRZ	AC,IRBRTB(AC2)
	MOVEM	AC,CA.BRG
	HRLI	AC,.CHLK		; lookup
	HRR	AC,QCHNI(AC2)
	CHANIO	AC,PMRLK
	JRST	REQOPE			; not found
	PUSHJ	P,INITBR
	HRLI	AC,.CHIN		; input 1st bufferful
	CHANIO	AC,
	 SKIPA
	JRST	REQOPE
	MOVE	AC2,RQFCT
	SETOM	QACTI(AC2)
	POPJ	P,
REQOPE:	; error, do not return to caller, return to caller's caller, no skip
	POP	P,AC		; waste one stack value
	SETZ	AC,
	POPJ	P,

REQINB:	; input a buffer from a request type file
	MOVE	AC2,RQFCT
	HRR	AC,QCHNI(AC2)
	HRLI	AC,.CHIN
	CHANIO	AC,
	 SKIPA
	JRST	REQOPE		; return to caller's caller
	POPJ	P,

RELRQI:	; release request input channels
	MOVEI	AC,MAXINQ-1
	MOVEM	AC,RQFCT
RELRI1:
	MOVE	AC1,RQFCT
	SKIPN	QACTI(AC1)		; skip if active
	JRST	RELRI2
	HRR	AC,QCHNI(AC1)		; release channel
	HRLI	AC,.CHREL
	CHANIO	AC,
	SETZM	QACTI(AC1)		; reset switch
RELRI2:
	SOSL	RQFCT
	JRST	RELRI1	; next file
	POPJ	P,

CLRREQ:	; clear up request channels
	PUSHJ	P,RELRQI	; release input channels
	PUSHJ	P,DELRQO	; release output channels
	POPJ	P,

DELRQO:	; close output channels without renaming to permanent names.
;	this routine is usually called to abort a request
	MOVEI	AC,MAXINQ-1
	MOVEM	AC,RQFCT
DELRO1:
	MOVE	AC1,RQFCT
	SKIPN	QACTO(AC1)
	JRST	DELRO2
	HRR	AC,QCHNO(AC1)
	HRLI	AC,.CHREL
	CHANIO	AC,
	SETZM	QACTO(AC1)
DELRO2:
	SOSL	RQFCT
	JRST	DELRO1
	POPJ	P,

CONHDR:	; WRITE A XXXX.CON FILE WITH STANDARD HEADER
	SKIPE	QACTO+2		; SKIP IF CON FILE NOT YET OPENED
	POPJ	P,			; JUST RETURN IF ALREADY DONE
	REQNOT	(CON,REQDAT,D)		; CREATION DATE
	REQNOT	(CON,[VER],D)		; VERSION NUMBER
	TLNE	FLG,U.IMM
	JRST	CONHR2
	REQOUT	(CON,MY.UNM,L.UNM)	; USERNAME LOGGED INTO
	JRST	CONHR3
CONHR2:	REQOUT	(CON,RQUORG,L.UNM)	; USERNAME OF ORIGINATOR
CONHR3:	REQOUT	(CON,CONSTR,L.CON)	; CONTACT
	TLNE	FLG,U.IMM
	JRST	CONHR4
	SETZM	VALEXM
	SETZM	VALEXM+1
	SETZM	VALEXM+2
	SETZM	VALPRO
	SETZM	VALPRO+1
	SETZM	VALPRO+2
CONHR4:	REQOUT	(CON,VALEXM,L.UNM)	; VALIDATIONS EXAMINER
	REQOUT	(CON,VALPRO,L.UNM)	; validations processor
	REQNOT	(CON,REQCOM,D)	; request header
	REQNOT	(CON,REQERC,D)		; error code
	REQOUT	(CON,[ASCIZ/END/])
	POPJ	P,

CONRD:	; read con file
	SETZM	REQCOM
	SETZM	REQERC
	PUSHJ	P,RDRDF		; get reqval
	SKIPN	REQVAL
	 POPJ	P,
	REQINN	(CON,D)
	 JFCL
	MOVEM	AC1,REQDAT
	REQINN	(CON,D)
	 JFCL
	MOVEM	AC1,RVER
	REQINS	(CON,RQUORG,L.UNM)
	REQINS	(CON,CONSTR,L.CON)
	REQINS	(CON,VALEXM,L.UNM)
	REQINS	(CON,VALPRO,L.UNM)
	REQINN	(CON,D)
	 JFCL
	MOVEM	AC1,REQCOM	; command number
	REQINN	(CON,D)
	 JFCL
	MOVEM	AC1,REQERC	; error code
	POPJ	P,
EXMNMO:	; output the number for display of exam items
	MOVE	AC,EXMIN
	AOJ	AC,
	MOVEM	AC,EXMNUM
	MOVE	AC1,REQCOM
	CAIE	AC1,^D52		; skip if new customer command
	JRST	EXMNO1
	CAIN	AC,^D22
	 POPJ	P,			; not printing 22nd item
	CAIN	AC,^D23
	 POPJ	P,			; not printing 23rd item
EXMNO1:	TYPNUM	(EXMNUM,D,0,0,N)
	TYPSTR	([ASCIZ/./],1,0,0,N)
	MOVEI	AC,5
	PUSHJ	P,TABS
	POPJ	P,
REQDIS:
; request command dispatcher
; expects c(REQCOM) is a valid command number

	MOVE	AC1,REQCOM		; get command number
	SKIPLE	AC1
	 CAILE	AC1,MAXRCM
		POPJ	P,			; out of range, return
	PUSHJ	P,@RCOMTB-1(AC1)	; branch to it
	 POPJ	P,
	AOS	0(P)			; reflect skip return
	POPJ	P,

REQERR:	; warn user and set error seen during request
	TLO	FLG2,V.RER		; set flag
	TYPSTR	([ASCIZ/Request cannot be processed until this item is changed/])
	POPJ	P,

; VALRT's (routines to set up reqval for new requests)
; These routines must set up the bits in left half of reqval and move
; MY.UUN to the right half.

USRREQ:	; SET UP REQVAL FOR A REQUEST
	SETZB	AC1,REQVAL
	MOVE	AC2,REQCOM
	SKIPE	RCOMEX-1(AC2)		; SKIP IF NO NEED EXAM
	TLO	AC1,R.NEX
	TLNN	AC1,R.NEX
	TLO	AC1,R.RDY
	HRR	AC1,MY.UUN		; MOVE IN UUN OF ORIGINATOR
	MOVEM	AC1,REQVAL
	POPJ	P, 
 
LRHDR:	; LIST CURRENT REQUEST INFORMATION
	TCR	(1)
	TYPSTR	([ASCIZ/- Request:/],777,0,0,N)
	TYPNUM	(REQNUM,D,1,0,N)
	TYPSTR	([ASCIZ/- Date requested:/],777,0,^D30,N)
	MOVE	AC,REQDAT
	PUSHJ	P,TYPDT
	TYPSTR	([ASCIZ/- Logged into:/],777,0,0,N)
	TYPSTR	(RQUORG,L.UNM,1,0,N)
	TYPSTR	([ASCIZ/- Contact:/],777,0,^D30,N)
	TYPSTR	(CONSTR,L.CON,1)
	MOVE	AC,VALEXM
	CAMN	AC,[ASCII/     /]
	JRST	LRHDR1
	TYPSTR	([ASCIZ/- Examined by:/],777,0,0,N)
	TYPSTR	(VALEXM,L.UNM,1,0,N)
	MOVE	AC,VALPRO
	CAMN	AC,[ASCII/     /]
	JRST	[TCR	(1)
		JRST	LRHDR1]
	TYPSTR	([ASCIZ/- Processed by:/],777,0,^D30,N)
	TYPSTR	(VALPRO,L.UNM,1)
LRHDR1:	TCR	(1)
	TYPSTR	([ASCIZ/Status -/],777,0,0,N)
	TBSSTR	([ASCIZ/Request needs to be examined./],REQVAL,R.NEX,777,0,12)
	TBSSTR	([ASCIZ/In hold./],REQVAL,R.HLD,777,0,12)
	TBSSTR	(ST.CAN,REQVAL,R.CAN,777,0,12)
	TBSSTR	([ASCIZ/Request already cancelled./],REQVAL,R.UCN,777,0,12)
	MOVE	AC,REQVAL
	TLNN	AC,R.ERR
	JRST	LRHDR2
	TYPSTR	([ASCIZ/Error:/],777,0,12,N)
	SKIPG	REQERC
	JRST	[TCR	(1)
		JRST	LRHDR2]
	MOVE	AC1,REQERC
	MOVE	AC,RQERS(AC1)
	MOVEM	AC,NUM
	TYPSTR	(@NUM,777,1)
LRHDR2:	TBSSTR	([ASCIZ/Ready to process./],REQVAL,R.RDY,777,0,12)
	TBSSTR	([ASCIZ/Request already completed./],REQVAL,R.COM,777,0,12)
	TBSSTR	([ASCIZ/Request locked./],REQVAL,R.LCK,777,0,12)
	TCR	(1)
	TYPSTR	([ASCIZ/Command:/],777,0,0,N)
	MOVE	AC1,REQCOM
	SKIPLE	AC1
	CAILE	AC1,MAXRCM
	JRST	LRHDR3
	MOVE	AC,RCOMDS-1(AC1)
	MOVEM	AC,REQTMP
	TYPSTR	(@REQTMP,777,1)
	POPJ	P,
LRHDR3:	TYPSTR	([ASCIZ/Unknown - Call Programmer/],777,1)
	POPJ	P,

INCON:	; input requestor's phone number or name
        TRZN    FLG2,V.FRQ              ; GET NAME AND PHONE ONLY FOR 1ST RQ.
        POPJ    P,
INCON1:	INSTR	([ASCIZ "Your name and telephone number:"],CONSTR,L.CON)
	MOVE	AC,CONSTR
	CAMN	AC,[ASCII/     /]
	JRST	INCON2
	JUMPE	AC,INCON2
	POPJ	P,
INCON2: PUSHJ   P,INORNO
        JRST    INCON1

CMDTYP:	; TYPE A REQUEST
	TCR	(1)
	PUSHJ	P,GRNO
	JRST	CMTYPD
	PUSHJ	P,CONRD		; read control and reqval
	SKIPN	REQVAL
	JRST	CMTYP1
	PUSHJ	P,LRHDR			; output control and reqval info
	TLO	FLG2,V.LSQ	; set listing flag
	REQINP	ON
	PUSHJ	P,REQDIS
	 JFCL
	REQINP	OFF
	TLZ	FLG2,V.LSQ
	PUSHJ	P,RELRQI
	JRST	CMDTYP
CMTYP1:	TYPSTR	([ASCIZ/Request not found./])
	JRST	CMDTYP
CMTYPD:	TRO	FLG,U.OKAY
	POPJ	P,

GRNO:	; input request number from terminal
	INNUM	([ASCIZ/Enter Request Number:/],D)
	JRST	GRNO
	MOVE	AC,CNVHLD
	CAMN	AC,[ASCII/     /]
	POPJ	P,
	CAIL	AC1,1
	CAILE	AC1,REQMAX
	JRST	[MOVEI	AC,[ASCIZ"Request number out of bounds <1-8191>"]
		 MOVEM	AC,ERRMSG
		 PUSHJ	P,OUTOFB
		 JRST	GRNO]
	MOVEM	AC1,REQNUM	; save request number
	AOS	0(P)
	POPJ	P,

GNRNO:	; get starting request number
	INNUM	([ASCIZ/Enter Starting Request Number:/],D)
	JRST	GNRNO
	MOVE	AC,CNVHLD
	CAMN	AC,[ASCII/     /]
	JRST	GNRNO0
	CAIL	AC1,1
	CAILE	AC1,REQMAX
	JRST	[MOVEI	AC,[ASCIZ"Request number out of bounds <1-8191>"]
		 MOVEM	AC,ERRMSG
		 PUSHJ	P,OUTOFB
		 JRST	GNRNO]
	SOJ	AC1,
GNRNO0:	MOVEM	AC1,REQNUM
	TRZ	FLG2,V.URC
	POPJ	P,

; EXAMINE REQUESTS

CMDEXM:	TCR	(1)
CMEXM0:	TRZ	FLG2,V.1R
	GETYR	([ASCIZ"One request only? (Y/N):"])
	JRST	CMEXMB
CMEXM1:	TRO	FLG2,V.1R
	PUSHJ	P,GRNO		; request number
	JRST	CMEXMD
	PUSHJ	P,CONRD		; get header and reqval
	SKIPN	REQVAL
	JRST	CMEXMD
	JRST	CMEXM3

CMEXMB:	PUSHJ	P,GNRNO		; starting request number
CMEXM2:	HRLZI	AC,R.NEX		; 1ST CONDITION
	MOVEM	AC,REQMSK	; save mask
	HRLZI	AC,R.NAC2!R.LCK	; not active flags
	MOVEM	AC,REQMS2	; save 2nd mask
	MOVEI	AC,REQTS3	; pick test 3
	PUSHJ	P,GNMREQ	; locate the request
	JRST	CMEXMD
	PUSHJ	P,CONRD
CMEXM3:	PUSHJ	P,CHKCOM	; check if complete
	JRST	CMEXM4
	PUSHJ	P,CHKEXA	; check if need examined
	JRST	CMEXM4
	PUSHJ	P,CHLOCK	; check if locked
	JRST	CMEXM4
	PUSHJ	P,LRHDR
	TLO	FLG2,V.EXQ	; set v.exq (examine)
	REQINP	ON
	PUSHJ	P,REQDIS
	 JFCL
	REQINP	OFF
	MOVE	AC,[MY.UNM,,VALEXM]	; SET UP VALIDATION EXAMINER
	BLT	AC,VALEXM+2
	GETYR	([ASCIZ"Ready to process? (Y/N):"])
	JRST	[MOVE	AC,REQVAL
		JRST	CEXM3B]
	SETZM	REQERC		; reset error code
	PUSHJ	P,CONHDR
	SETZM	VALEXM
	MOVE	AC,REQVAL
	TLZ	AC,R.NEX!R.ERR!R.COM!R.CAN!R.UCN!R.HLD
	TLO	AC,R.RDY	; turn on ready to process
CEXM3B:	TLZ	AC,R.LCK	; unlock
	MOVEM	AC,REQVAL
	PUSHJ	P,RELRQO
CMEXM4:
	PUSHJ	P,RELRQI
	TRNN	FLG2,V.1R	; skip if one request only
	JRST	CMEXM2		; do next request if any
CMEXMD:	; done
	TRO	FLG,U.OKAY
	POPJ	P,
 
; PROCESS REQUESTS

CMDPRO:	TRZ	FLG2,V.1R
	TCR	(1)
	GETYR	([ASCIZ"One request only? (Y/N):"])
	JRST	CMPROB
CMPRO1:
	TRO	FLG2,V.1R	; set single request flag
	PUSHJ	P,GRNO		; get request number
	JRST	CMPROD
	PUSHJ	P,CONRD		; get hdr and reqval
	SKIPN	REQVAL
	JRST	CMPROD		; zero request
	MOVE	AC,REQVAL
	TLNE	AC,R.RDY	; skip if not ready to process
	JRST	CMPRO3
	TYPSTR	([ASCIZ/** Request not ready to process **/])
	POPJ	P,

CMPROB:	PUSHJ	P,GNRNO		; get starting request number
CMPRO2:
	HRLZI	AC,R.RDY	; 1st condition
	MOVEM	AC,REQMSK
	HRLZI	AC,R.NAC2!R.LCK	; 2nd condition
	MOVEM	AC,REQMS2
	MOVEI	AC,REQTS3	; pick test 3
	PUSHJ	P,GNMREQ	; locate request
	JRST	CMPROD
	PUSHJ	P,CONRD		; got a request, read header
CMPRO3:
	PUSHJ	P,CHLOCK	; lock and skip if not locked
	JRST	CMPRO6
	PUSHJ	P,LRHDR		; output header
	TLO	FLG2,V.PRQ	; set processing flag
	TRZ	FLG,U.OKAY	; reset okay flag
	REQINP	ON
	PUSHJ	P,REQDIS
	 JFCL
	REQINP	OFF
	TRNN	FLG,U.OKAY	; skip if okay flag set
	JRST	CMPRO4
	MOVE	AC,REQVAL
	TLO	AC,R.COM	; set completed flag
	TLZ	AC,R.RDY!R.HLD!R.CAN!R.UCN!R.ERR!R.LCK ;reset flags
	MOVEM	AC,REQVAL
	MOVE	AC,[MY.UNM,,VALPRO]
	BLT	AC,VALPRO+2
	TCR	(1)
	TYPSTR	([ASCIZ/Request completed and answered./])
	JRST	CMPRO5
CMPRO4:	; error during processing
	MOVE	AC,REQVAL
	TLO	AC,R.ERR!R.NEX	; turn on error and need examine
	TLZ	AC,R.RDY!R.LCK		; TURN OFF READY AND UNLOCK
	MOVEM	AC,REQVAL
	SETZM	REQERC		; set error code to zero
	TCR	(1)
	TYPSTR	([ASCIZ/Request not completed - not answered./])
CMPRO5:
	PUSHJ	P,CONHDR
	PUSHJ	P,RELRQO
CMPRO6:	PUSHJ	P,RELRQI
	TRNN	FLG2,V.1R	; skip if one request only
	JRST	CMPRO2		; do next request

CMPROD:	; done
	TRO	FLG,U.OKAY
	POPJ	P,
CMDHOL:	; hold a request
	TCR	(1)
	PUSHJ	P,GRNO		; get request
	JRST	CMHOLD
	PUSHJ	P,CONRD
	SKIPN	REQVAL
	JRST	CMHOLD
	PUSHJ	P,CHLOCK	; lock and skip if not locked
	JRST	CMHOLD
	PUSHJ	P,LRHDR
	GETYR	([ASCIZ"Hold - OK? (Y/N):"])
	JRST	[MOVE	AC,REQVAL
		JRST	CMHOL1]
	MOVE	AC,REQVAL
	TLO	AC,R.HLD
	TLZ	AC,R.NEX		; turn off need examine while in hold
CMHOL1:	TLZ	AC,R.LCK
	MOVEM	AC,REQVAL
	PUSHJ	P,RELRQO
CMHOLD:	PUSHJ	P,RELRQI	
	TRO	FLG,U.OKAY
	POPJ	P,
CMDUNH:	; unhold a request
	TCR	(1)
	PUSHJ	P,GRNO		; get request
	JRST	CMUNHD
	PUSHJ	P,CONRD
	SKIPN	REQVAL
	JRST	CMUNHD
	PUSHJ	P,CHLOCK	; lock and skip if not already locked
	JRST	CMUNHD
	PUSHJ	P,LRHDR
	GETYR	([ASCIZ"Unhold - OK? (Y/N):"])
	JRST	[MOVE	AC,REQVAL
		JRST	CMUNH1]
	MOVE	AC,REQVAL
	TLZ	AC,R.HLD	; unhold
	TLO	AC,R.NEX	; turn on need examine flag
CMUNH1:	TLZ	AC,R.LCK	; unlock
	MOVEM	AC,REQVAL
	PUSHJ	P,RELRQO
CMUNHD:	PUSHJ	P,RELRQI	
	TRO	FLG,U.OKAY
	POPJ	P,
CMDUNL:	; unlock a request
	TCR	(1)
	PUSHJ	P,GRNO		; get number
	JRST	CMUNLD
	PUSHJ	P,CONRD
	SKIPN	REQVAL
	JRST	CMUNLD
	PUSHJ	P,LRHDR
	MOVE	AC,REQVAL
	TLNN	AC,R.LCK	; skip if locked
	JRST	CMUNLD
	GETYR	([ASCIZ"UnLock - are you sure? (Y/N):"])
	JRST	CMUNLD
	MOVE	AC,REQVAL
	TLZ	AC,R.LCK
	MOVEM	AC,REQVAL
	PUSHJ	P,RELRQO
CMUNLD:	PUSHJ	P,RELRQI
	TRO	FLG,U.OKAY
	POPJ	P,
CMDANS:	; ANSWER A REQUEST
	TCR	(1)
	PUSHJ	P,GRNO
	JRST	CMANSD
	PUSHJ	P,CONRD
	SKIPN	REQVAL
	JRST	CMANS4
	PUSHJ	P,CHLOCK		; lock and skip if not already locked
	JRST	CMANSD
	PUSHJ	P,LRHDR
	TCR	(1)
	PUSHJ	P,GREQEC		; get error code
	SKIPN	REQERC
	JRST	CMANS2
	MOVE	AC,REQVAL
	HRRZS	AC			; CLEARS THE LEFT HALF OF AC
	TLO	AC,R.ERR!R.CAN		; set error and cancelled
	MOVEM	AC,REQVAL
	JRST	CMANS3
CMANS2:	MOVE	AC,REQVAL
	HRRZS	AC			; CLEAR THE LEFT OF AC
	TLO	AC,R.COM
	MOVEM	AC,REQVAL
CMANS3:	MOVE	AC,[MY.UNM,,VALPRO]
	BLT	AC,VALPRO+2
	PUSHJ	P,CONHDR		; rewrite hdr
	SETZM	VALPRO
	PUSHJ	P,RELRQO
CMANSD:	PUSHJ	P,RELRQI
	TRO	FLG,U.OKAY
	POPJ	P,
CMANS4:	; REQUEST NOT FOUND
	TYPSTR	([ASCIZ/Request not found./])
	JRST	CMANSD

GREQEC:	; GET A REQUEST ERROR CODE
	INNUM	([ASCIZ/Error Code:/],D)
	JRST	GREQEC
	MOVE	AC,CNVHLD
	CAMN	AC,[ASCII/     /]
	JRST	[PUSHJ	P,INORNO
		 JRST	GREQEC]
	CAIL	AC1,0
	CAILE	AC1,NRQERS-1		; skip if le max - 1
	JRST	GRQEC2
	MOVEM	AC1,REQERC		; save it
	MOVE	AC,RQERS(AC1)
	MOVEM	AC,NUM
	CONCAT	(LINEBF,@NUM,0)
	CONCAT	(LINEBF,[ASCIZ" - OK? (Y/N):"])
	GETYR	(LINEBF)
	JRST	[TCR	(1)
		 JRST	GREQEC]
	POPJ	P,
GRQEC2: MOVEI	AC,[ASCIZ"Error Code out of bounds <0-4>"]
	MOVEM	AC,ERRMSG
	PUSHJ	P,OUTOFB
	JRST	GREQEC
CMDASK:	; ASK ABOUT A REQUEST
	TCR	(1)
	PUSHJ	P,GRNO
	JRST	CMASKD
	PUSHJ	P,CONRD
	SKIPN	REQVAL
	JRST	CMASK3
	TLNE	FLG,U.PRV		; SKIP IF NOT PRIVILEGED
	JRST	CMASK1			; DO IT IF HE'S PRIVILEGED
	HRRZ	AC,REQVAL		; GET UUN OF ORIGINATOR
	CAME	AC,MY.UUN		; SKIP IF ORIGINATED BY ME
	JRST	CMASK2
CMASK1:	PUSHJ	P,LRHDR
	TLO	FLG2,V.LSQ
	REQINP	ON
	PUSHJ	P,REQDIS
	JFCL
	REQINP	OFF
	PUSHJ	P,RELRQI
	TLZ	FLG2,V.LSQ
	JRST	CMASKD
CMASK2:	; NO ACCESS
	TYPSTR	([ASCIZ/You do not have access to that request./])
	PUSHJ	P,RELRQI
	JRST	CMASKD
CMASK3:	; REQUEST NOT FOUND
	TYPSTR	([ASCIZ/Request not found./])
CMASKD:	TRO	FLG,U.OKAY
	POPJ	P,
SUBTTL	NEEDCM - Need Commands
DEFINE NE1 <
	X [ASCIZ"EXAMINE"],1,CMDNEX,U.PRV,-1	; NEED EXAMINE
	X [ASCIZ"PROCESS"],1,CMDNPR,U.PRV,-1	; NEED PROCESS
>
	SUBCOM	([ASCIZ"NEED"],NEE,NE1)

; NEED EXAMINE

CMDNEX:	HRLZI	AC,R.NEX
	MOVEM	AC,REQMSK
CMNEX1:	HRLZI	AC,R.CAN!R.UCN		; CANCELLED
	MOVEM	AC,REQMS2		; AND SET IF CONDITIONS
	MOVEI	AC,REQTS3		; TEST THREE (1ST TRUE, 2ND FALSE)
	MOVEM	AC,REQPAD
	SETZM	REQNUM
	PUSHJ	P,GETODF		; OUTPUT DESTINATION
	TCR	(1)
	TYPSTR	([ASCIZ/Request numbers:/])
	TCR	(1)
CMNEX2:	MOVE	AC,REQPAD
	PUSHJ	P,GNMREQ
	JRST	CMNEXD
	TYPNUM	(REQNUM,D)		; OUTPUT REQUEST NUMBER
	JRST	CMNEX2
CMNEXD:	TRNE	FLG,U.LFO		; SKIP IF NOT LIST OUTPUT
	PUSHJ	P,CLSODF
	TRO	FLG,U.OKAY
	POPJ	P,

; NEED PROCESS

CMDNPR:	HRLZI	AC,R.RDY
	MOVEM	AC,REQMSK
	JRST	CMNEX1
    * 
}¼