SUBTTL TAT - TATTLE TALE FILE ROUTINES
COMMENT `
  The tattle tale files are written  to in order to be able to trace
activity. The naming convention is (USEVAL)yymmdd.ext. The extention
component  reflects  the  type of commands traced by this file,  for
example "RUN" for the file that is used each time the program is run
"CHG" for all Change commands.

  Since these files are shared by all users, a file is always opened
in single user update mode,  a complete entry is appended,  and  the
file is released.  There is  no  way to insure that nobody added any
entries since the last time you released the file.

  The date,  which is part of the filename,  and the time entries in
the files, are converted to pacific. An eastern user making an entry
at 1:00 am actually writes a 23:00 entry on the previous day's file.
`

GETTTF:	; GET TATTLE TALE FILE FOR THIS PROCESS.
	MOVE	AC,[.CHREL,,TATCHN]
	CHANIO	AC,			; RELEASE CHANNEL
	MOVEI	AC,.IODMP		; DATA MODE (.IODMP = 17)
	MOVEM	AC,TATOPN
	HRLZI	AC,(SIXBIT/DSK/)
	MOVEM	AC,TATOPN+1
	SETZM	AC,TATOPN+2
	MOVE	AC,[.CHOPN,,TATCHN]
	CHANIO	AC,TATOPN		; OPEN A CHANNEL
	JRST	GETTTF			; TRY AGAIN
	PUSHJ	P,MAKTTN		; MAKE TATTLE TALE FILE NAME
	MOVE	AC,TATTXE		; GET EXTENTION
	MOVEM	AC,TATEXT		; MOVE IN THE EXTENTION
	SETZM	TATEXT+1
	MOVE	AC,[DB.DIR]		; GET PPN
	MOVEM	AC,TATDIR		; MOVE IN PPN
	SETZM	TATDIR+1
	MOVE	AC,[.CHLK,,TATCHN]	; PREPARE FOR LOOKUP
	TRZ	FLG,U.NTT		; RESET FLAG TO CREATE A NEW FILE
	CHANIO	AC,TATLK		; LOOKUP THE TATTLE TALE FILE
	TRO	FLG,U.NTT		; FILE NOT FOUND, SO SET NEW FILE FLAG
	MOVE	AC,[DB.DIR]		; NOW DO AGAIN FOR ENTER
	MOVEM	AC,TATDIR
	MOVE	AC,TATTXE		; GET CURRENT EXTENTION
	MOVEM	AC,TATEXT
	SETZM	TATEXT+1
	SETZM	TATDIR+1
	MOVE	AC,[.CHENT,,TATCHN]
	CHANIO	AC,TATLK		; ENTER THE FILE (UPDATE)
	JRST	GETTER			; ERROR
	SETZM	TTBCT
	TRZE	FLG,U.NTT		; SKIP IF NOT A NEW FILE
	JRST	[PUSHJ	P,NTATB2	; GO TO CREATE A NEW FILE PAGE
		JRST	GETTD4]
	MOVE	AC,TATLK+3		; GET SIZE (OR PPN)
	CAMN	AC,[DB.DIR]		; SKIP IF NOT PPN
	JRST	GETTTD
	HLRES	AC			; ISOLATE SIZE
	JUMPGE	AC,[IDIVI AC,4		; SIZE IS BLOCKS
		JRST GETTT2]		; DIVIDE BY 4 FOR PAGES
	IDIVI	AC,1000			; DIVIDE BY PAGE SIZE
	SETCA	AC,			; MAKE POSITIVE
	AOJ	AC,
	SKIPE	AC1			; SKIP IF REMAINDER IS ZERO
	AOJ	AC,			; NEXT PAGE
GETTT2:	; MAP EXISTING FILE PAGES
	MOVEM	AC,TTFPGM		; FILE PAGE NUMBER TO MAP
	PUSHJ	P,FPG2MP		; FIND 1ST FREE ADDRESS PAGE TO MAP TO
	HRLI	AC,(3B7!1B17)		; SET PAGE COUNT & PROTECTION BITS
	MOVEM	AC,TTFPAG		; MOVE TO VIRTUAL PAGE WORD
	MOVE	AC,[.CHMFP,,TATCHN]	; PREPARE FOR THE MAPPING
	CHANIO	AC,TTFPAG		; MAP THE FILE PAGE
	PUSHJ	P,TATPGE		; ERROR RETURN
GETTTD:	PUSHJ	P,NTATB3		; MAKE BYTE PTR & COUNTER FOR FILE PAGE
GETTD2:	SOSG	TATCNT			; SKIP IF BYTES LEFT
	JRST	GETTD4
	MOVE	AC1,TATBP		; GET BYTE POINTER TO AC1
	ILDB	AC,AC1			; GET A BYTE
	JUMPE	AC,GETTD3		; JUMP IF A NULL
	MOVEM	AC1,TATBP		; REPLACE BYTE POINTER
	JRST	GETTD2			; GET ANOTHER BYTE
GETTD3:	AOS	TATCNT			; ADD ONE TO COUNT, LAST CHAR NOT USED
GETTD4:	; OUTPUTS STANDARD HEADER
	TTFSTR	([ASCIZ/**************/],777,N)
	MOVE	AC,DATUBL+1
	IDIVI	AC,^D3600		; CONVERT TO MINUTES
	IDIVI	AC,^D60			; CONVERT TO HOURS AND MINUTES
	MOVEM	AC1,NUM1
	MOVEM	AC,NUM2
	TTFNUM	(NUM2,D,N,Y)
	MOVEI	AC1,[ASCIZ":"]
	MOVE	AC,NUM1
	CAIG	AC,^D9
	MOVEI	AC1,[ASCIZ":0"]
	TTFSTR	(@AC1,777,N,Y)
	TTFNUM	(NUM1,D,Y,Y)
GETTDN:	AOS	(P)			; SKIP RETURN
	POPJ	P,
GETTER:	; ERROR OPENING TATTLE TALE FILE FOR OUTPUT. CHECK FOR FILE BUSY
	HRRZ	AC,TATEXT	; GET ERROR CODES
	CAIN	AC,.ERFBM	; SKIP IF NOT FILE BUSY
	JRST	GETTR1
	MOVEM	AC,NUM
	TYPSTR	([ASCIZ/Error opening TTF - Error =/],777,0,0,N,Y)
	TYPNUM	(NUM,O,1,0,Y,Y)
	JRST	CMDQUI
GETTR1:
	MOVE	AC,[.CHREL,,TATCHN]
	CHANIO	AC,
	MOVE	AC,TTBCT	; GET BUSY COUNT
	CAIE	AC,3
	JRST	GETTR2
	TYPSTR	([ASCIZ/Tattle tale file busy./])
GETTR2:	CAIN	AC,^D1500
	JRST	GETTR3
	AOS	TTBCT
	JRST	GETTTF
GETTR3:	SETZM	TTBCT
	JRST	GETTTF

GETPDT: ; GET CURRENT DATE IN PACIFIC TIME ZONE
	TIMER	AC,			; GET CURRENT TIME
	MOVEM	AC,DATUBL+1		; MOVE TO DATUUO BLOCK.
	DATE	AC,			; GET DATE.
	MOVEM	AC,DATUBL		; MOVE TO BLOCK.
	HRLZ	AC,MY.TZ		; GET CURRENT TIME ZONE.
	HRRI	AC,10!40		; CONVERT DATE AND TIME TO PDT.
	MOVEM	AC,DATUBL+2
	MOVEI	AC,DATUBL
	DATUUO	AC,			; DO CONVERSION.
	JFCL
	MOVE	AC,DATUBL
        POPJ    P,

MAKTTN: ; CREATE TATTLE TALE FILE NAME
        PUSHJ   P,GETPDT                ; GET CURRENT DATE IN PACIFIC TIME
	PUSHJ	P,CNVPDP		; CONVERT PDP-DATE FORMAT TO YYMMDD
	MOVEI	AC4,^D10		; FOR DECIMAL CONVERSION
	TRO	FLG,U.SIX		; SET SIXBIT FLAG
	PUSHJ	P,NUMOUT		; CONVERT TO ASCII
	TRZ	FLG,U.SIX		; RESET SIXBIT FLAG
	MOVE	AC,NUMSP		; GET THE FILENAME INTO AC
	MOVEM	AC,TATNM		; MOVE TO THE CORRECT LOCATION.
	POPJ	P,

GTODAY:	; CREATE TODAY'S DATE IN YYMMDD FORMAT
	DATE	AC,			; GET DATE.
	PUSHJ	P,CNVPDP		; CONVERT CURRENT DATE TO YYMMDD
	POPJ	P,

CNVPDP:	; CONVERT PDP DATE FORMAT TO yymmdd FORMAT.
	IDIVI	AC,^D31			; ISOLATE DAY-1
	AOJ	AC1,			; ISOLATE DAY
	MOVEM	AC1,AC2
	IDIVI	AC,^D12			; ISOLATE MONTH-1
	AOJ	AC1,
	IMULI	AC1,^D100		; LEFT SHIFT 2 DECIMAL PLACES
	ADDM	AC1,AC2
	ADDI	AC,^D64			; ADJUST YEAR
	IMULI	AC,^D10000		; LEFT SHIFT INTO PLACE
	ADDM	AC,AC2			; AND ADD IT TO THE REST
	MOVE	AC,AC2			; MOVE TO AC
	POPJ	P,

RELTTF:	; HERE TO RELEASE TATTLE TALE FILE
	TLO	FLG,U.CRLF
	PUSHJ	P,TTFCR			; OUTPUT A CRLF FOR LUCK.
	MOVE	AC,TATCNT
	CAIL	AC,TATBYT		; SKIP IF ANYTHING WRITTEN ON THIS PAGE
	JRST	RELTT2			; NO, JUST RELEASE THE CHANNEL.
	HRRZ	AC,TTFPAG		; PREPARE TO VCLEAR LAST PAGES.
	JUMPE	AC,RELTT2
	HRLI	AC,1
	VCLEAR	AC,			; CLEAR LAST PAGES.
	PUSHJ	P,TATPGE
RELTT2:	MOVE	AC,[.CHREL,,TATCHN]
	CHANIO	AC,			; RELEASE THE CHANNEL.
	POPJ	P,

TTFOUT:	; HERE TO OUTPUT A STRING TO TATTLE TALE FILE
	MOVEM	AC,SAC			; SAVE STRING ADDRESS
	PUSHJ	P,CNTCHR		; COUNT CHARS.
	MOVE	AC2,SAC			; RETRIEVE ADDRESS
	HRLI	AC2,(POINT 7)		; MAKE INTO A BYTE POINTER
TTFOU1:	ILDB	AC,AC2			; GET A CHARACTER
	PUSHJ	P,CHRTTF		; OUTPUT A CHAR
	SOJG	AC1,TTFOU1		; DO THE REST
	TRZE	FLG,U.SBL		; SKIP IF NO SUPPRESS SPACE
	JRST	TTFOU2
	MOVEI	AC,BLNK			; FOLLOW WITH A SPACE.
	PUSHJ	P,CHRTTF		; OUTPUT IT.
TTFOU2:	PUSHJ	P,TTFCR			; POSSIBLE CRLF.
	POPJ	P,

CHRTTF:	; OUTPUT A CHAR TO TATTLE TALE FILE
	SOSG	TATCNT
	PUSHJ	P,NTATBU		; GET A NEW PAGE
	IDPB	AC,TATBP
	POPJ	P,

NTATBU:	PUSH	P,AC			; SAVE AC AC1 AND AC2
	PUSH	P,AC1
	PUSH	P,AC2
	PUSHJ	P,NTATBF		; GO DO IT.
	POP	P,AC2			; RESTORE ACS.
	POP	P,AC1
	POP	P,AC
	POPJ	P,

NTATBF:	; GET NEW TT BUFFER
	HRRZ	AC,TTFPAG		; GET VIRTUAL PAGE NUMBER.
	JUMPE	AC,NTATB2		; JUMP TO NTATB2 IF NO OLD PAGE EXISTS.
	HRLI	AC,1			; COUNT.
	VCLEAR	AC,			; CLEAR OLD PAGE.
	PUSHJ	P,TATPGE		; ERROR.
	SETZM	TTFPAG			; CLEAR PAGE NUMBER WORD.
NTATB2:	MOVE	AC,[.CHCFP,,TATCHN]	; PREPARE TO CREATE A NEW PAGE
	SETOM	TTFPGM			; AT THE END OF THE FILE.
	CHANIO	AC,TTFPGM		; DO IT.
	PUSHJ	P,TATPGE		; ERROR.
	PUSHJ	P,FPG2MP		; FIND FIRST FREE EVEN PAGE.
	HRLI	AC,(3B7!1B17)		; PUT IN COUNT AND PROTECTION.
	MOVEM	AC,TTFPAG		; PREPARE FOR CHANIO.
	MOVE	AC,[.CHMFP,,TATCHN]
	CHANIO	AC,TTFPAG		; MAP NEW PAGES.
	JRST	TATERR
NTATB3:	MOVEI	AC3,TATBYT		; NUMBER OF BYTES IN A PAGE.
	MOVEM	AC3,TATCNT		; REFRESH TATCNT.
	HRLZI	AC3,(POINT 7)		; MAKE A BYTE POINTER.
	HRRZ	AC,TTFPAG
	IMULI	AC,1000
	IOR	AC3,AC
	MOVEM	AC3,TATBP		; STORE BYTE POINTER.
	POPJ	P,			; AND RETURN.

TATERR:	; TATTLE TALE FILE OUTPUT ERROR ROUTINE
	MOVE	AC3,[.CHGS,,TATCHN]
	CHANIO	AC3,SAC
	TYPSTR	([ASCIZ/Error during TT output - Status word:/],777,0,0,N)
	TYPNUM	(SAC,O,1)		; TYPE FILE STATUS WORD
	TYPSTR	([ASCIZ/Please report error to Software support./])
	POPJ	P,

TATPGE:	; PAGING ERROR
	MOVEM	AC,NUM
	TYPSTR	([ASCIZ/Paging error during TT output - Code:/],777,0,0,N)
	TYPNUM	(NUM,O,1)
	TYPSTR	([ASCIZ/Please report error to software support./])
	POPJ	P,

TTFCR:	TLZN	FLG,U.CRLF	; OUTPUT A CRLF TO TAT FILE
	JRST	TTFCR2
TTFCR1:	MOVEI	AC,.CR
	PUSHJ	P,CHRTTF
	MOVEI	AC,.LF
	PUSHJ	P,CHRTTF
TTFCR2:	POPJ	P,

DECTTF: ; OUTPUT DECIMAL NUMBER TO TATTLE TALE FILE
	MOVEI	AC4,^D10		; SET UP FOR DECIMAL CONV.
	PUSHJ	P,NUMOUT		; CONVERT TO ASCII.
	TTFSTR	(NUMSP,NUMSPL,N)	; OUTPUT, CRLF MAY BE PRESET.
	MOVE	AC,NUM			; RESTORE NUM.
	POPJ	P,

OCTTTF: ; OUTPUT OCTAL NUMBER TO TATTLE TALE FILE
	MOVEI	AC4,^D8			; PREPARE FOR OCTAL CONVERSION.
	PUSHJ	P,NUMOUT
	TTFSTR	(NUMSP,NUMSPL,N)
	MOVE	AC,NUM
	POPJ	P,			; RETURN.

FPG2MP:	HRRZ	AC,.JBFF		; GET HIGHEST ADDRESS LOWSEG OCCUPIED
	IDIVI	AC,2000			; DIVIDE BY PAGESIZE * 2.
	SKIPE	AC+1			; SKIP IF NO REMAINDER.
	AOJ	AC,			; ADD ONE TO QUOTIENT.
	IMULI	AC,2			; COMPUTE ADDRESS OF 1ST FREE EVEN PAGE
	POPJ	P,

TTSYST:	; OUTPUTS SYSTEM TYPE
	TTFSTR	([ASCIZ/System Type:/],777,N)
	TTFNUM	(ACTSYS,D,Y,Y)
	POPJ	P,

TTUOUT:	; OUTPUTS USERNAME (UNMSPC) TO TATTLE TALE FILE
	TTFSTR	([ASCIZ/Username:/],777,N)
	TTFSTR	(UNMSPC,L.UNM)
	POPJ	P,

TTMOUT:	; OUTPUTS THE MAILNAME TO TATTLE TALE FILE.
	TTFSTR	([ASCIZ/Mailname:/],777,N)
	TTFSTR	(MAINAM,L.UNM,N,Y)
	TTFSTR	([ASCIZ/ - /],777,N,N)
	TTFNUM	(MAIUUN,O,Y,Y)
	POPJ	P,

TTUDST:	; OUTPUTS USER DISTRICT
	TTFSTR	([ASCIZ/District:/],777,N)
	TTFNUM	(NMDIST,D,Y,Y)
	POPJ	P,

TTUUNO:	; OUTPUTS UUN
	TTFSTR	([ASCIZ/UUN:/],777,N)
	TTFNUM	(NMUUN,O,Y,Y)
	POPJ	P,

TTCDST:	; OUTPUTS CUSTOMER DISTRICT
	TTFSTR	([ASCIZ/District:/],777,N)
	TTFNUM	(DISRCT,D,Y,Y)
	POPJ	P,

TTCSCC:	; OUTPUTS CUSTOMER COST CODE
	TTFSTR	([ASCIZ/Cost Code:/],777,N)
	TTFNUM	(ACCCC,D,Y,Y)
	POPJ	P,

TTUSCC:	; OUTPUTS USER COST CODE
	TTFSTR	([ASCIZ/Cost Code:/],777,N)
	TTFNUM	(TUCOST,D,Y,Y)
	POPJ	P,

TTNOUT:	; OUTPUTS USERNAME IN NNMSPC TO TATTLE TALE FILE.
	TTFSTR	([ASCIZ/New Username:/],777,N)
TTNOU1: TTFSTR	(NNMSPC,L.UNM,Y,Y)
	POPJ	P,

TTNWUS: TTFSTR	([ASCIZ/New Username:/],777,N)
	JRST	TTNOU1

TTCNOU:	; OUTPUTS CUSTOMER NUMBER (CUSNUM) TO TT FILE
	TTFSTR	([ASCIZ/Customer Number:/],777,N)
	TTFNUM	(CUSNUM,D,Y,Y)
	POPJ	P,

TTGOUT:	; OUTPUTS GLOBAL ACCOUNT (NMGAN) TO TT FILE.
	TTFSTR	([ASCIZ/Global Account:/],777,N)
	TTFNUM	(NMGAN,O,Y,Y)
	POPJ	P,

TTCSNO:	; OUTPUT CUSTOMER NAME (ACCNAM) TO TT FILE
	TTFSTR	([ASCIZ/Customer Name:/],777,N)
	TTFSTR	(ACCNAM,L.CUSN)
	POPJ	P,

TTNCSN:	; OUTPUT CUSTOMER NAME (NCNMSP) TO TT FILE
	TTFSTR	([ASCIZ/Customer Name:/],777,N)
	TTFSTR	(NCNMSP,L.CUSN)
	POPJ	P,

TTCDCD:	; OUTPUTS DETAIL CODE
	TTFSTR	([ASCIZ/Detail Code:/],777,N)
	TTFNUM	(ACDETC,D,Y,Y)
	POPJ	P,

TTSLMN:	; OUTPUTS SALESMAN NUMBER
	TTFSTR	([ASCIZ/Salesman Number:/],777,N)
	TTFNUM	(ACSLS,D,Y,Y)
	POPJ	P,

TTATTN:	; OUTPUTS ATTENTION LINE
	TTFSTR	([ASCIZ/Attention:/],777,N)
	TTFSTR	(ACATTN,L.ATTN)
	POPJ	P,

TTINVC: ; Write Invoice Code to tatle tale file.
	TTFSTR	([ASCIZ"Invoice Code:"],777,N)
	TTFNUM	(ACINVC,D,Y,Y)
	POPJ	P,

TTMINC: ; Write Minimum Code to tatle tale file.
	TTFSTR	([ASCIZ"Minimum Code:"],777,N)
	TTFNUM	(ACMINC,D,Y,Y)
	POPJ	P,

TTPONM: ; Write P.O. Number to tatle tale file.
	TTFSTR	([ASCIZ"P.O. Number:"],777,N)
	TTFSTR	(ACPONM,L.PO)
	POPJ	P,

TTPOMX: ; Write P.O. Maximum to tatle tale file.
	TTFSTR	([ASCIZ"P.O. Maximum:"],777,N)
	TTFNUM	(ACPOMX,D,Y,Y)
	POPJ	P,

TTPSDT: ; Write P.O. start date to tatle tale file.
	TTFSTR	([ASCIZ"P.O. Start Date:"],777,N)
	TTFNUM	(ACPSDT,D,Y,Y)
	POPJ	P,

TTPEDT: ; Write P.O. stop date to tatle tale file.
	SKIPN	ACPEDT
	JRST	[TTFSTR	([ASCIZ"P.O. Stop Date: 991231"])
		 POPJ	P,]
	TTFSTR	([ASCIZ"P.O. Stop Date:"],777,N)
	TTFNUM	(ACPEDT,D,Y,Y)
	POPJ	P,

TTVPF:	; Write variable pricing flag to tatle tale file.
	TTFSTR	([ASCIZ"Variable Pricing Flag:"],777,N)
	TTFNUM	(ACVPF,D,Y,Y)
	POPJ	P,

TTDEFP: ; Write default pricing code to tatle tale file.
	TTFSTR	([ASCIZ"Default Pricing Code:"],777,N)
	TTFNUM	(ACDEFP,D,Y,Y)
	POPJ	P,

TTSTR1: ; Write street1 to tatle tale file.
	TTFSTR	([ASCIZ"Street 1:"],777,N)
	TTFSTR	(ACSTR1,L.STRE)
	POPJ	P,

TTSTR2: ; Write street2 to tatle tale file.
	TTFSTR	([ASCIZ"Street 2:"],777,N)
	TTFSTR	(ACSTR2,L.STRE)
	POPJ	P,

TTCITY: ; Write city to tatle tale file.
	TTFSTR	([ASCIZ"City:"],777,N)
	TTFSTR	(ACCITY,L.CITY)
	POPJ	P,

TTSTAT: ; Write state to tatle tale file.
	TTFSTR	([ASCIZ"State:"],777,N)
	TTFSTR	(ACSTAT,L.STAT)
	POPJ	P,

TTZIP:	; Write zip code to tatle tale file.
	TTFSTR	([ASCIZ"Zip Code:"],777,N)
	TTFSTR	(ACZIP,L.ZIP)
	POPJ	P,

TTEXLN: ; Write extra line to tatle tale file.
	TTFSTR	([ASCIZ"Extra Line:"],777,N)
	TTFSTR	(ACEXLN,L.STRE)
	POPJ	P,

TTCTRY: ; Write country to tatle tale file.
	TTFSTR	([ASCIZ"Country:"],777,N)
	TTFSTR	(ACCTRY,L.CTRY)
        POPJ    P,

TTCADR: ; Write customer address to tatle tale file.
	PUSHJ	P,TTSTR1		; Street 1
	PUSHJ	P,TTSTR2		; Street 2
	PUSHJ	P,TTCITY		; City
	PUSHJ	P,TTSTAT		; State
	PUSHJ	P,TTZIP			; Zip code
	PUSHJ	P,TTEXLN		; Extra line
	PUSHJ	P,TTCTRY		; Country
	POPJ	P,

STDHDR:	; STANDARD HEADER FOR TATTLE TALE FILE.
	TTFSTR	([ASCIZ/Logged into:/],777,N)
	TTFSTR	(MY.UNM,L.UNM)
	TLNN	FLG2,V.PRQ		; SKIP IF PROCESSING REQUEST
	POPJ	P,
STDHD1: TTFSTR	([ASCIZ/Originator:/],777,N)
	TTFSTR	(RQUORG,L.UNM)
	TTFSTR	([ASCIZ/Request:/],777,N)
	TTFNUM	(REQNUM,D,Y,Y)
	TTFSTR	([ASCIZ/Processed by:/],777,N)
	TTFSTR	(MY.UNM,L.UNM)
	POPJ	P,

STDUHD: PUSHJ	P,STDHDR		; STANDARD HEADER
	PUSHJ	P,TTUOUT		; OUTPUT USERNAME
	POPJ	P,

STDCHD: PUSHJ	P,STDHDR
	PUSHJ	P,TTSYST
	TTFSTR	([ASCIZ/Customer Number:/],777,N)
	TTFNUM	(CUSNUM,D,N,Y)
	TTFSTR	([ASCIZ/ - /],777,N)
	TTFSTR	(ACCNAM,L.CUSN,Y,Y)
	POPJ	P,

STDGHD: PUSHJ	P,STDHDR
	PUSHJ	P,TTGOUT
	POPJ	P,

NCSTTT:	; TATTLE TALE FOR NEW CUSTOMER COMMAND
	GETT	(/NEW/)
	POPJ	P,
	TTFSTR	([ASCIZ/New Customer/])
	PUSHJ	P,STDHDR		; Standard header
	PUSHJ	P,TTCNOU		; Customer number
	PUSHJ	P,TTSYST		; System type
	PUSHJ	P,TTNCSN		; Customer name
	PUSHJ	P,TTATTN		; Attention
	PUSHJ	P,TTCADR		; Customer address
	PUSHJ	P,TTCDST		; Customer district
	PUSHJ	P,TTCDCD		; Detail code
	PUSHJ	P,TTINVC		; Invoice code
	PUSHJ	P,TTSLMN		; Salesman number
	PUSHJ	P,TTMINC		; Minimum code
	PUSHJ	P,TTPONM		; PO number
	PUSHJ	P,TTPOMX		; PO maximum
	PUSHJ	P,TTPSDT		; PO start date
	PUSHJ	P,TTPEDT		; PO stop date
	PUSHJ	P,TTVPF			; Variable pricing flag
	PUSHJ	P,TTDEFP		; Default pricing code
	PUSHJ	P,TTCSCC		; Cost code
	PUSHJ	P,RELTTF
	POPJ	P,

NUSTTT:	; WRITE TATTLE TALE FILE FOR NEW USER COMMAND
	GETT	(/NEW/)
	POPJ	P,
	TTFSTR	([ASCIZ/New User/])
	PUSHJ	P,STDHDR
	PUSHJ	P,TTNOUT		; USERNAME
	PUSHJ	P,TTGOUT		; GAN
	PUSHJ	P,TTUUNO		; UUN
	PUSHJ	P,TTUDST		; DISTRICT
	PUSHJ	P,TTCNOU		; CUST. NO.
	PUSHJ	P,TTUSCC		; COST CODE
	PUSHJ	P,TTMOUT		; MAILNAME
	PUSHJ	P,RELTTF
	POPJ	P,

TTHSPC: ; OUTPUTS HOST (HSTSPC) TO TATTLE TALE FILE.
	TTFSTR	([ASCIZ/Host:/],777,N)
	MOVE	AC,HSTSPC
	CAIE	AC,ALHSTS
	JRST	[TTFNUM (HSTSPC,D,Y,Y)
		 POPJ	P,]
	TTFSTR	([ASCIZ/All Hosts./])
	POPJ	P,
   