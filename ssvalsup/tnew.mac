SUBTTL	NEWCOM	- New Type Commands.
DEFINE	NEW1	<
	X [ASCIZ"CUSTOMER"],1,CMDNCS,U.PRV,-1	; New Customer
	X [ASCIZ"USERNAME"],1,CMDNUS,-1,-1	; New User
>
	SUBCOM ([ASCIZ/NEW/],NEW,NEW1)

NEWDON:	TRO	FLG,U.OKAY
	POPJ	P,
; New Customer

CMDNCS:	PUSHJ	P,BLDACT
	JRST	GETCMD

DEFINE	NCUS	<
	X (SYSOUT,INASYS)
	X (DISTOU,GETDIS)
	X (OUTCNA,GETCNA)
	X (ATNOUT,GETATN)
	X (AD1OUT,GETAD1)
	X (AD2OUT,GETAD2)
	X (AD3OUT,GETAD3)
	X (CTYOUT,GETCTY)
	X (STAOUT,GETSTA)
	X (ZIPOUT,GETZIP)
	X (CTROUT,GETCTR)
	X (POOUT,GETPO)
	X (POSOUT,GETPOS)
	X (POEOUT,GETPOE)
	X (POMOUT,GETPOM)
	X (SALOUT,GETSAL)
	X (CCCOUT,GETCCC)
	X (DETOUT,GETDET)
	X (INVOUT,GETINV)
>
	EXMCHG	(NCS,NCUS,[ASCIZ/New Customer/])

	MOVEI	AC,1
	MOVEM	AC,ACMINC		; MINIMUM CODE ALWAYS 1
	MOVEM	AC,ACVPF		; VARIABLE PRICING CODE ALWAYS 1
	MOVEI	AC,^D30			; DEFAULT PRICING CODE FOR TYMNET
	MOVEM	AC,ACDEFP
	SNDBYT	(ACTPRT,\B.ACOM,NIDACT,I)	; GET CUSTOMER NUMBER FROM ACTG.
	PUSHJ	P,AGANS
	JRST	NCUSE0
	GETBYT	(ACTPRT,\B.CID)
	JRST	GETCMD
	MOVEM	AC1,CUSNUM
	TCR	(1)
	MOVE	AC,CUSNUM
	PUSHJ	P,CIDOUT
	SNDBYT	(ACTPRT,\B.ACOM,NCUACT,I)	; SEND COMMAND CODE 30
	SNDBYT	(AC,\B.CID,CUSNUM)
	SNDBYT	(AC,\B.ASY,ACTSYS)
	SNDSTR	(AC,L.CUSN,NCNMSP)
	SNDBYT	(AC,\B.DIS,DISRCT)
	SNDBYT	(AC,\B.DET,ACDETC)
	SNDBYT	(AC,\B.INV,ACINVC)
	SNDBYT	(AC,\B.MIN,ACMINC)
	SNDBYT	(AC,\B.SLS,ACSLS)
	SNDSTR	(AC,L.PO,ACPONM)
	SNDBYT	(AC,\B.POMX,ACPOMX)
	SNDBYT	(AC,\B.PODT,ACPSDT)
	SNDBYT	(AC,\B.PODT,ACPEDT)
	SNDBYT	(AC,\B.VPRI,ACVPF)
	SNDBYT	(AC,\B.PRI,ACDEFP)
	PUSHJ	P,SNDADR
	SNDBYT	(AC,\B.CC,ACCCC)
	PUSHJ	P,AGANS
	JRST	LUDAOT
	TCR	(1)
	PUSHJ	P,ASEDON
	PUSHJ	P,NCSTTT		; WRITE NEW CUSTOMER TATTLE TALE
	MOVE	AC,ACTSYS
	CAIE	AC,^D9
	JRST	NEWDON
	TCR	(1)
	GETYR	([ASCIZ "Do you want to add users for this customer? (Y/N):"])
	JRST	NEWDON
	TLO	FLG2,V.NWAC		; SET NEW ACCOUNT FLAG
	TLO	FLG2,V.FUSR		; SET FIRST USER FLAG
	MOVE	AC,[NCNMSP,,ACCNAM]
	BLT	AC,ACCNAM+^D10
	MOVE	AC,DISRCT
	MOVEM	AC,ACCDIS		; SAVE DISTRICT
	PUSHJ	P,BLDCUD
	JRST	GETCMD
	JRST	ADNUSR
NCUSE0:	TYPSTR	([ASCIZ/Error during get Customer Number from Accounting./])
	POPJ	P,
; New User

CMDNUS:	PUSHJ	P,ACTCUD
	JRST	GETCMD
	TLNN	FLG,U.PRV
	JRST	RQNUSR
	TLO	FLG2,V.FUSR
	TLZ	FLG2,V.NWAC		; RESET NEW ACCOUNT FLAG
	GETYR	([ASCIZ "Is the Global Account established? (Y/N):"])
	TLO	FLG2,V.NWAC		; SET NEW ACCOUNT FLAG
	MOVEI	AC,^D9			; NEW USER OF OLD GAN START HERE
	MOVEM	AC,ACTSYS		; TYMNET CUSTOMER
CMNUS1:	TCR	(1)
	PUSHJ	P,GVCSNR		; INPUT CUST. NO. & GET ACTG. RECORD
	JRST	CMNUS1
ADNUSR:	TCR	(1)
	PUSHJ	P,GVMAIL
	TLNE	FLG2,V.NWAC
	JRST	ADNUS0
CMNUS2:	TCR	(1)
	PUSHJ	P,GTSEED		; GET SEEDNAME
	PUSHJ	P,GANASK		; CONFIRM THE GAN
	JRST	CMNUS2			; NO RIGHT GET IT AGAIN
	MOVE	AC,TUCNO		; GET CUSTOMER OF THE SEEDNAME
	CAMN	AC,CUSNUM		; SKIP IF INPUT CUSTOMER NO. NOT
	JRST	CMNUS3
	TYPSTR	(MACCBD)		; MESSAGE IN STOR.MAC
	JRST	CMNUS2
CMNUS3:	MOVE	AC,ACCDIS		; GET CUSTOMER DISTRICT
	CAMN	AC,NMDIST		; SKIP IF SEEDNAME'S DISTRICT NOT
	JRST	ADNUS1			; EQUAL TO CUSTOMER'S DISTRICT.
	TYPSTR	(MDISER)		; MESSAGE IN THE STOR.MAC FILE
	JRST	CMNUS2
ADNUS0:	TLNE	FLG2,V.NWAC
	SETZM	SEDGAN			; SET ZERO FOR NEW ACCOUNT
	MOVE	AC,ACCDIS
	MOVEM	AC,SEDDST		; SET USER'S DIST. TO CUSTOMER'S DIST.
ADNUS1:	SETZM	NMUUN
	TCR	(1)

DEFINE	NUSR	<
	X (IRCCOU,GTIRCC)
	X (USROUT,GNUSRN)
	X (NGANOU,DUMMYR,5)
	X (NUUNOU,DUMMYR,5)
	X (OUTCSN,DUMMYR,1)
	X (PSWOUT,GTPSWD)
	X (TGWOUT,GETTGW)
	X (COLNOU,GTCLOP)
	X (MAIOUT,DUMMYR,5)
	X (DUMMYR,GCLSFF,3)
	X (HSYSOU,GCHOHF)
	X (UCCOUT,GETUCC)
>
	EXMCHG	(NUS,NUSR,[ASCIZ/New Username/])

	PUSHJ	P,RESEED
	PUSHJ	P,NUSMUD
	JRST	GETCMD
	PUSHJ	P,NUSNET
	JRST	GETCMD
	TLNE	FLG2,V.NWAC
	PUSHJ	P,WRPSWF
	PUSHJ	P,NUSTTT		; WRITE NEW USER TATTLE TALE FILE
	TCR	(1)
	GETYR	([ASCIZ "Do another? (Y/N):"])
	JRST	NEWDON
	TLZ	FLG2,V.FUSR!V.NWAC	; RESET FIRST USER AND NEW ACCOUNT FLG.
	JRST	ADNUS1
; New User (REQUEST)

RQNUSR:	TRO	FLG2,V.FRQ		; SET FLAG FOR 1ST REQUEST
	MOVEI	AC,3
	MOVEM	AC,REQCOM
	PUSHJ	P,GTSEED
RQNUS1:	TCR	(1)
RQNUS2:	PUSHJ	P,GNUSRN
	PUSHJ	P,GTPSWD
	PUSHJ	P,GTCOLN
	PUSHJ	P,GTCHOH

DEFINE	NUSQ	<
	X (USROUT,GNUSRN,4)		; DISPLAY AND CHANGE
	X (PSWOUT,GTPSWD,5)		; DISPLAY ONLY
	X (HSYSOU,GTCHOH,4)
	X (IRCCOU,GTIRCC,5)
>
	EXMCHG	(NUQ,NUSQ,[ASCIZ/New Username/])

	PUSHJ	P,USRREQ
	PUSHJ	P,INCON
	PUSHJ	P,GTODAY
	MOVEM	AC,REQDAT
	SETZM	NMUUN
	PUSHJ	P,NUSWRQ
	PUSHJ	P,RQNOUT
	JRST	RQNUS2
	TRO	FLG,U.OKAY
	POPJ	P,

RQNOUT:	TCR	(1)
	TYPSTR	([ASCIZ/Inquiries must reference request number:/],777,0,0,N)
	TYPNUM	(REQNUM,D,1)
	SETZM	REQNUM
	TCR	(1)
	GETYR	([ASCIZ "Do another? (Y/N):"])
	JRST	[AOS	(P)
		 POPJ	P,]
	TCR	(1)
	POPJ	P,

NUSWRQ:	PUSHJ	P,CONHDR
	PUSHJ	P,RWNUSR
	PUSHJ	P,RWGAN
	PUSHJ	P,RWUUN
	PUSHJ	P,RWCSNO
	PUSHJ	P,RWPSWD
	PUSHJ	P,RWCOLN
	PUSHJ	P,RWMAIL
	PUSHJ	P,RWDIST
	PUSHJ	P,RWHSYS
	PUSHJ	P,RWCOST
	PUSHJ	P,RWSEED
	PUSHJ	P,RELRQO
	POPJ	P,

OUTCNA:	; OUTPUTS CUSTOMER NAME
	MOVEI	AC,NCNMSP
	PUSHJ	P,CSNOUT
	TYPSTR	([ASCIZ/Customer Number: To be assigned/],777,0,5)
	POPJ	P,

DISTOU:	MOVE	AC,DISRCT
	PUSHJ	P,DISOUT
	POPJ	P,

GETCCC:	; INPUT CUSTOMER COST CODE
	PUSHJ	P,GETCC
	MOVEM	AC1,ACCCC
	POPJ	P,

CCCOUT:	; OUTPUT CUSTOMER COST CODE
	MOVE	AC,ACCCC
	PUSHJ	P,CCOUT
	POPJ	P,

CNVDTS:	; CONVERT INPUT DATE STRING INTO (yymmdd)
	MOVEI	AC,DATSPC		; COUNT THE CHARACTERS
	MOVEI	AC1,^D14		; MAX CHARS
	PUSHJ	P,CNTCHR
	JUMPE	AC1,INPDT3
	CAIL	AC1,6			; MIN SIZE
	CAILE	AC1,^D10		; MAX SIZE
	JRST	INPDTE
	SETZM	MONSPC			; SET INDIVIDUAL FIELDS TO ZEROS
	SETZM	YRSPC
	SETZM	DAYSPC
	MOVE	AC1,[POINT 7,DATSPC]	; POINTER TO STRING
	MOVEI	AC3,2			; MAX LENGTH OF MONTH FIELD
INPDT1:	PUSHJ	P,INPDN			; ISOLATE THE MONTH
	MOVEM	AC4,MONSPC
	MOVEI	AC3,2			; MAX LENGTH OF DAY FIELD
	PUSHJ	P,INPDN			; ISOLATE DAY FIELD
	MOVEM	AC4,DAYSPC
	MOVEI	AC3,4			; MAX LENGTH OF YEAR FIELD
	PUSHJ	P,INPDN			; ISOLATE YEAR
	MOVEM	AC4,YRSPC
	MOVE	AC,YRSPC
	CAIG	AC,^D99
	JRST	INPDT2
	IDIVI	AC,^D100		; ISOLATE 2 DIGIT YEAR IN AC+1
	MOVE	AC,AC1
INPDT2:	IMULI	AC,^D10000
	MOVE	AC1,MONSPC
	CAILE	AC1,^D12
	JRST	INPDTE			; ILLEGAL MONTH
	IMULI	AC1,^D100
	ADD	AC,AC1
	MOVEM	AC,DATSPC
	MOVE	AC,DAYSPC
	CAILE	AC,^D31
	JRST	AC,INPDTE		; ILLEGAL DAY
	ADDM	AC,DATSPC
	SKIPA
INPDT3:	SETZM	DATSPC
	AOS	(P)
	POPJ	P,

INPDN:	; ISOLATE A FIELD IN DATE STRING
	SETZ	AC4,
INPDN1:	ILDB	AC,AC1			; GET A CHAR
	CAIE	AC,BLNK			; SKIP IF NOT A SPACE
	JRST	INPDN3
	SOJGE	AC3,INPDN1		; CONTINUE UNTIL FOUND A NON-BLANK.
	JRST	INDTE1
INPDN2:	ILDB	AC,AC1
INPDN3:	CAIN	AC,"/"			; SKIP IF NOT A SLASH
	JRST	INPDND
	CAIN	AC,"-"			; SKIP IF NOT A DASH
	JRST	INPDND
	CAIN	AC,BLNK			; SKIP IF NOT A BLANK
	JRST	INPDND
	JUMPE	AC,INPDND		; JUMP TO DONE IF NULL
	CAIL	AC,"0"			; SKIP IF LESS THEN CHAR ZERO
	CAILE	AC,"9"			; SKIP IF LESS THEN OR EQ TO CHAR 9
	JRST	INDTE1
	IMULI	AC4,^D10		; LEFT SHIFT ONE DIGIT
	SUBI	AC,60
	ADD	AC4,AC
	SOJGE	AC3,INPDN2
	JRST	INDTE1			; ERROR
INPDND:	SKIPG	AC4
	JRST	INDTE1
	POPJ	P,
INPDTE:	; ERROR GETTING A DATE
	TYPSTR	([ASCIZ/Date must be in MM-DD-YY format./],777,0,0,Y,Y)
	POPJ	P,
INDTE1:	POP	P,AC			; WASTE ONE STACK VALUE
	PUSHJ	P,INPDTE
	POPJ	P,

GTSEED:	; INPUT A SEED NAME
	TLNE	FLG2,V.EXC
	JRST	NALCHG
	INSTR	(MGTSNM,UNMSPC,14,T)
	MOVEI	AC1,L.UNM
	MOVEI	AC,UNMSPC
	PUSHJ	P,CNTCHR
	JUMPE	AC1,GSEED1
	PUSHJ	P,CKUNMN
	JRST	GSEED1
	MOVE	AC,[UNMSPC,,SEEDNM]
	BLT	AC,SEEDNM+2
	TLNE	FLG2,V.LSQ
	POPJ	P,
	MOVE	AC,NMUUN
	TLNE	FLG2,V.EXQ!V.PRQ
	MOVEM	AC,SAVUUN
	PUSHJ	P,GCREC
	JRST	GSEED1
	TLNE	FLG2,V.EXQ!V.PRQ
	JRST	[MOVE	AC,SAVUUN
		 MOVEM	AC,NMUUN
		 POPJ	P,]
	PUSHJ	P,GTUREC
	JRST	GSEED1
	PUSHJ	P,CKMNAM
	JRST	[TYPSTR	(UNMSPC,L.UNM,0,0,N)
		 TYPSTR	([ASCIZ" - Seed Username does not belong to your account."])
		 TYPSTR	(PLSRES)
		 JRST	GSEED1]
	PUSHJ	P,CKTRAC
	JRST	GSEED1
	PUSHJ	P,STOSED
	PUSHJ	P,FINHOM
	SETZ	AC,
	TRNN	AC,GRP.CD
	ANDI	AC,HOMMSK
	MOVEM	AC,HOMSYS
	MOVE	AC,CLSIND
	MOVEM	AC,SCLIND
	POPJ	P,
GSEED1:	TRNE	FLG,U.RFI
	JRST	[SETZM	SEEDNM
		 SETZM	SEEDNM+1
		 SETZM	SEEDNM+2
		 TLO	FLG2,V.RER
		 POPJ	P,]
	TCR	(1)
	JRST	GTSEED

USROUT:	; OUTPUT USERNAME IN NNMSPC
	TYPSTR	([ASCIZ/Username:/],777,0,0,N)
	SKIPN	NNMSPC
	JRST	INPERR
	TYPSTR	(NNMSPC,L.UNM,1)
	POPJ	P,

PSWOUT:	; OUTPUT PASSWORD
	TYPSTR	([ASCIZ"Password:"],777,0,0,N)
	MOVE	AC,NMCOLN
	TLNE	AC,NPS.CD
	JRST	[TYPSTR	([ASCIZ" No Password Required."])
		 JRST	PSWOU3]
	MOVE	AC,NMPSW
	CAIN	AC,7400		; Null Password?
	JRST	[TYPSTR	([ASCIZ" Null Password."])
		 JRST	PSWOU3]
	SKIPN	NMPSW
	JRST	[TYPSTR	([ASCIZ" Bad Password."])
		 JRST	PSWOU3]
	TCR	(1)
PSWOU3:	TLNE	FLG,U.PRV
	POPJ	P,
	TLNE	FLG2,V.LSQ!V.PRQ!V.EXQ
	POPJ	P,
	MOVE	AC,NMCOLN
	TLNN	AC,CLN.CD
	JRST	PSWOU4
	TYPSTR	([ASCIZ/Ignore Colon Option./],777,0,5)
PSWOU4:	MOVE	AC,NMCPAR
	TLNN	AC,GTW.CD
	POPJ	P,
	TYPSTR	([ASCIZ/Transparent Gate Way Option./],777,0,5)
	POPJ	P,

GTCOLN:	MOVE	AC,NMCOLN
	TLNE	AC,CLN.CD
	POPJ	P,
	GETYR	([ASCIZ "Do you want the Colon Option? (Y/N):"])
	SKIPA
	POPJ	P,
	MOVE	AC,NMCOLN
	TLO	AC,CLN.CD
	MOVEM	NMCOLN
	POPJ	P,

HSYSOU:	TYPSTR	([ASCIZ/Home System:/],777,0,0,N)
	SKIPN	HOMSYS
	JRST	[TYPSTR ([ASCIZ/No Home System/],777,1)
		 POPJ	P,]
	SKIPG	HOMSYS
	JRST	INPERR
        MOVE    AC,HOMSYS
        ANDI    AC,HOMMSK
        MOVEM   AC,NUM
	TYPNUM	(NUM,D,1,0,N)
        MOVE    AC,HOMSYS
        TRNN    AC,GRP.CD
	JRST	HSYSO1
        TYPSTR  ([ASCIZ/ (Group)/],777,0,T.P3,N)
HSYSO1: TCR     (1)
        POPJ    P,

GTCHOH: MOVE    AC,HOMSYS
        TRNE    AC,GRP.CD
        POPJ    P,
	PUSHJ	P,GVMAIL
GCHOH1:	MOVE	AC,UAHST
	CAIN	AC,ALHSTS
	JRST	GCHOH2
	MOVE	AC,UAHSTP
	CAIG	AC,1
	JRST	GCHOH4
GCHOH2:	TLNE	FLG2,V.EXC
	JRST	GCHOH3
	TYPSTR	([ASCIZ/Home System is/],777,0,0,N)
	TYPNUM	(HOMSYS,D,1)
	GETYR	([ASCIZ "Do you wish to change the Home System? (Y/N):"])
	POPJ	P,
GCHOH3:	PUSHJ	P,GETHOH
	POPJ	P,
GCHOH4:	TLNN	FLG2,V.EXC
	POPJ	P,
	TYPSTR	([ASCIZ/Home system cannot be changed because of/])
	TYPSTR	([ASCIZ/the Mail name only valid on one system./])
	POPJ	P,

IRCCOU:	MOVE	AC,SEDIRC
	PUSHJ	P,IRCOUT
	POPJ	P,

GTNUSR:	; get new user name
	INSTR	([ASCIZ/Enter New Username:/],NNMSPC,L.UNM,T)
	MOVEI	AC1,L.UNM
	MOVEI	AC,NNMSPC
	PUSHJ	P,CNTCHR
	SKIPN	AC1			; skip if not a blank
	JRST	GTNNM4
        PUSHJ   P,CKNNAM
        JRST    [TCR    (1)
		 JRST	GTNUSR]
	TLNE	FLG2,V.EXC
	JRST	GTNNM1
	TLNE	FLG2,V.LSQ!V.EXQ!V.PRQ
	POPJ	P,
GTNNM1:	PUSHJ	P,CKNNMN
	JRST	[TCR	(1)
		 JRST	GTNUSR]
	PUSHJ	P,CKNNOV		; make sure this name not exist
	JRST	GTNNM2
	POPJ	P,
GTNNM2:	; type error msg
	TRNN	FLG,U.RFI		; skip if this is from a request
	JRST	GTNNM3
        SETZM   NNMSPC
        SETZM   NNMSPC+1
        SETZM   NNMSPC+2
	TLO	FLG2,V.RER
	POPJ	P,
GTNNM3:	TYPSTR	(NNMSPC,L.UNM,0,0,N)
	TYPSTR	(UNEXIT)
        TCR     (1)
        JRST    GTNUSR
GTNNM4:	PUSHJ	P,INORNO
	JRST	GTNUSR

GNUSRN:	; GET NEW NAME AND CHECK IT AGAINST THE MAILNAME.
	PUSHJ	P,GTNUSR
	PUSHJ	P,CKNNMN
	JRST	[PUSHJ	P,INORNO
		 JRST	GNUSRN]
	POPJ	P,

CKNNMN:	; CHECK NEW NAME AGAINST MAILNAME
        TLNN    FLG,U.TR6!U.CUS1
	JRST	CNNMNR
	SPLITN	(NNMSPC,CPUNM,PRUNM)
	JRST	NNMNER
	SPLITN	(MAINAM,CPMNM,PRMNM)
	JRST	NNMNER
	CMPSTR	(CPMNM,CPUNM,^D10)
	JRST	NNMNER
CNNMNR:	AOS	(P)
	POPJ	P,
NNMNER:	TYPSTR	([ASCIZ/Illegal format of new username./])
	TYPSTR	(PLSREN)
	POPJ	P,

CKUNMN:	; CHECK USER NAME (UNMSPC) AGAINST MAILNAME (MAINAM)
	TLNN	FLG,U.TR6!U.CUS1
	JRST	CUNMNR
	TLNE	FLG2,V.LSQ!V.PRQ!V.EXQ
	JRST	CUNMNR
	SPLITN	(UNMSPC,CPUNM,PRUNM)
	JRST	UNMNER
	SPLITN	(MAINAM,CPMNM,PRMNM)
	JRST	UNMNER
	CMPSTR	(CPMNM,CPUNM,^D10)
	JRST	UNMNER
CUNMNR:	AOS	(P)
	POPJ	P,
UNMNER:	TYPSTR	([ASCIZ"Illegal format of seed username."])
	TYPSTR	(PLSRES)
	POPJ	P,

TGWOUT:	; OUTPUTS TRANSPARENT GATE WAY.
	MOVE	AC,SEDCPW
	TRNE	AC,GTW.CD
	JRST	TGWOU1
	TYPSTR	([ASCIZ/No/],777,0,0,N)
	MOVEI	AC,BLNK
	PUSHJ	P,CHROUT
TGWOU1:	TYPSTR	([ASCIZ/Transparent Gate Way Option./])
	POPJ	P,

COLNOU:	; OUTPUT COLON OPTION
	MOVE	AC,SEDCLN
	TLNN	AC,CLN.CD
	JRST	OU1CLN
	TYPSTR	([ASCIZ/Ignore/],777,0,0,N)
	MOVEI	AC,BLNK
	PUSHJ	P,CHROUT
OU1CLN:	TYPSTR	([ASCIZ/Colon Option./])
	POPJ	P,

MAIOUT:	; OUTPUT MAIL NAME
	SKIPN	MAINAM
	JRST	[TYPSTR ([ASCIZ/Mailname:/],777,0,0,N)
		 JRST	INPERR]
	MOVEI	AC,MAINAM
	PUSHJ	P,LMAILN
	POPJ	P,

GMAIFF:	; INPUT MAILNAME IF FIRST USER OF NEW USER COMMAND
	TLNN	FLG2,V.FUSR		; SKIP IF 1ST USER OF NEW USER CMD.
	POPJ	P,
	PUSHJ	P,GVMAIL
	POPJ	P,

GCLSFF:	; ASK CLASS ONLY FOR NEW ACCOUNT AND 1ST USER.
	TLNN	FLG2,V.FUSR		; SKIP IF 1ST USER
	POPJ	P,
	TLNN	FLG2,V.NWAC		; SKIP IF NEW ACCOUNT
	POPJ	P,
	SETZM	SEDCLS
	MOVE	AC1,[SEDCLS,,SEDCLS+1]
	BLT	AC1,SEDEND-1
	GETYR	([ASCIZ "Is this user going to have the default class? (Y/N):"])
	JRST	GCLSF1
	PUSHJ	P,DFUCLS		; SET DEFAULT CLASS
	POPJ	P,
GCLSF1:	PUSHJ	P,GTUCLS
	MOVE	AC1,[CLSWRK,,SEDCLS]
	BLT	AC1,SEDCLS+7
	MOVE	AC1,[CLSWRK,,NMCLSS]
	BLT	AC1,NMCLSS+7
	PUSHJ	P,FINHOM
	SETZM	HOMSYS
	ANDI	AC,HOMMSK
	MOVEM	AC,HOMSYS
	POPJ	P,

DFUCLS:	; DEFAULT CLASS FOR TYMNET DOMESTIC
	MOVEI	AC,1
	MOVEM	AC,SEDCLS
	MOVEI	AC,4
	MOVEM	AC,SEDCLS+1
DFCLS1:	PUSHJ	P,GETHOH
	MOVE	AC,HOMSYS
	TRO	AC,HOM.CD		; SET HOME SYSTEM BIT
	MOVEM	AC,SEDCLS+2
	POPJ	P,

GCHOHF:	; GET HOME SYSTEM FOR 1ST USER OF NEW USER COMMAND.
	TLNN	FLG2,V.FUSR
	POPJ	P,
	TLNE	FLG2,V.NWAC
	POPJ	P,
        MOVE    AC,HOMSYS
        TRNE    AC,GRP.CD               ; SKIP IF NOT HOME GROUP
        POPJ    P,
	PUSHJ	P,GCHOH1
	MOVE	AC,SCLIND
	SUBI	AC,NMCLSS
	ADDI	AC,SEDCLS
	MOVEM	AC,SCLIND
	MOVE	AC,HOMSYS
	TRO	AC,HOM.CD
	MOVEM	AC,@SCLIND
	POPJ	P,

UCCOUT:	; OUTPUT USER COST CODE
	SKIPN	SEEDCC
	JRST	[TYPSTR ([ASCIZ/Cost Code:/],777,0,0,N)
		 JRST	INPERR]
	MOVE	AC,SEEDCC
	PUSHJ	P,CCOUT
	POPJ	P,

GETUCC:	; INPUT USER COST CODE
	PUSHJ	P,GETCC
	MOVEM	AC1,SEEDCC
	MOVEM	AC1,TUCOST
	POPJ	P,

DUMMYR:	; DUMMY SUBROUTINE
	TLNE	FLG2,V.EXC
	JRST	NALCHG
	POPJ	P,

NUUNOU:	; OUTPUT UUN
	SKIPLE	NMUUN
	JRST	[MOVE	AC,NMUUN
		 PUSHJ	P,UUNOUT
		 POPJ	P,]
	TYPSTR	([ASCIZ/UUN:/],777,0,0,N)
	SKIPE	NMUUN
	JRST	INPERR
	TYPSTR	([ASCIZ/ To be assigned./])
	POPJ	P,

NUUNIN:	; INPUT A UUN FROM A REQUEST FILE
	TLNE	FLG2,V.EXC
	JRST	NALCHG
	REQINN	(REQ,O)
	PUSHJ	P,[SETO AC1,
			TLO	V.RER
			POPJ P,]
	MOVEM	AC1,NMUUN
	POPJ	P,

NGANOU:	; OUTPUT GAN
	SKIPLE	SEDGAN
	JRST	[MOVE	AC,SEDGAN
		 PUSHJ	P,GANOUT
		 POPJ	P,]
	TYPSTR	([ASCIZ/Global Account:/],777,0,0,N)
	SKIPE	SEDGAN
	JRST	INPERR
	TYPSTR	([ASCIZ/ To be assigned./])
	POPJ	P,

NUSMUD:	; ADD NEW USER TO CUD
	SNDBYT	(CUDPRT,\B.CCOM,NEWMU2,I)
	PUSHJ	P,NNMCUD		; SEND USER NAME
	SNDBYT	(CUDPRT,\B.CYP,NMPSW)	; SEND PASSWORD CIPHER
	SNDBYT	(AC,1,0,I)
	SNDBYT	(AC,\B.GAN,NMGAN)
	SNDBYT	(AC,1,0,I)
	SETZM	NMUUN
	SNDBYT	(AC,\B.UUN,NMUUN)	
	MOVE	AC2,NMCPAR
	LSH	AC2,-^D8
	SNDBYT	(AC,\B.CPR,AC2)
	MOVE	AC2,NMCOLN
	TLZ	AC2,BIL.CD
	LSH	AC2,-^D16
	SNDBYT	(AC,\B.CLN,AC2)
	SNDBYT	(AC,\B.CDST,NMDIST)
	SNDBYT	(AC,\B.IRC,NMIRC)
	PUSHJ	P,SNDCLS
	JRST	[TYPSTR ([ASCIZ/Error during Adding New User to CUD./])
		 PUSHJ	P,ERRCUD
		 POPJ	P,]
	GETBYT	(CUDPRT)
	JRST	GETCMD
	GETBYT	(CUDPRT,\B.GAN)		; GET GAN
	JRST	GETCMD
	MOVEM	AC1,NMGAN
	MOVEM	AC1,SEDGAN
	GETBYT	(CUDPRT)
	JRST	GETCMD
	GETBYT	(CUDPRT,\B.UUN)		; GET UUN
	JRST	GETCMD
	MOVEM	AC1,NMUUN
	PUSHJ	P,GET40
	TCR	(1)
	PUSHJ	P,MSEDON		; TYPE MUD ENTRY COMPLETED.
	PUSHJ	P,ADDCLS
	POPJ	P,
	TCR	(1)
	TLNN	FLG2,V.NWAC		; SKIP IF NEW ACCOUNT
	JRST	MUDNUS
	MOVE	AC,NMGAN
	PUSHJ	P,GANOUT		; OUTPUT NEW ACCOUNT
MUDNUS:	MOVE	AC,NMUUN
	PUSHJ	P,UUNOUT		; OUTPUT NEW UUN
	AOS	(P)
	POPJ	P,
 
SNDCLS:	; SEND CLASS INFO.
	SNDBYT	(CUDPRT,1,NMCLSS)	; SEND NO. OF GROUPS OR HOSTS IN CLASS
	SNDBYT	(AC,\B.CLS,NMCLSS+1)	; SNND CLASS NO.
	MOVE	AC1,NMCLSS		; PREPARE INDEX FOR SENDING
	MOVEM	AC1,NUM			; GROUPS OR HOSTS
	MOVEI	AC1,NMCLSS+2
	MOVEM	AC1,NUM1
SNDCL1:	SNDBYT	(AC,\B.GRPH,@NUM1)	; SEND A GROUP OR HOST
	AOS	NUM1
	SOSLE	NUM
	JRST	SNDCL1
	PUSHJ	P,SND40
	PUSHJ	P,NGANS
	POPJ	P,
	AOS	(P)
	POPJ	P,

NUSNET: ; Add new user in Accounting.
	SNDBYT	(ACTPRT,\B.ACOM,ATYACT,I)
	SNDBYT	(AC,\B.GAN,NMGAN)
	SNDBYT	(AC,\B.UUN,NMUUN)
	SNDSTR	(AC,L.UNM,NNMSPC)
	SNDBYT	(AC,\B.CID,CUSNUM)
	SNDBYT	(AC,\B.DIS,NMDIST)
	SNDBYT	(AC,\B.UUN,MAIUUN)
	SNDBYT	(AC,\B.CC,TUCOST)
	PUSHJ	P,AGANS
	JRST	[TYPSTR ([ASCIZ/Error during Adding New User to Accounting./])
		 PUSHJ	P,LUDAOT
		 PUSHJ	P,RNUMUD
		 POPJ	P,]
	TCR	(1)
	PUSHJ	P,ASEDON		; TYPE ACCT. SYS. ENTRY COMPLETED.
	AOS	(P)
	POPJ	P,

ADDCLS:	; ADD CLASS INFO. AFTER ADD NEW USER
	MOVEI	AC,2
	MOVEM	AC,CLSCTR		; SET CLASS POSITION TO 2
	MOVEI	AC,10
	MOVEM	AC,CLSIND		; SET CLASS INDEX
ADCLS1:	MOVE	AC1,CLSIND		; GET CLASS INDEX
	SKIPN	NMCLSS(AC1)
	JRST	ADCLS3			; ALL DONE
	TCR	(1)
	TYPSTR	([ASCIZ/Adding Class Position:/],777,0,0,N)
	TYPNUM	(CLSCTR,D,1,0,N)
	MOVEI	AC1,NMCLSS
	ADD	AC1,CLSIND
	HRLZS	AC1
	HRRI	AC1,NMCLSS
	BLT	AC1,NMCLSS+7
	SNDBYT	(CUDPRT,\B.CCOM,ACLMUD,I)
	PUSHJ	P,NNMCUD		; SEND USER NAME
	PUSHJ	P,SNDCLS		; SEND CLASS INFO.
	JRST	ADCLSE			; ERROR IN ADDING CLASS
	TYPSTR	([ASCIZ" - MUD entry completed."])
	PUSHJ	P,GET40
	MOVE	AC,CLSIND
	CAIL	AC,110
	JRST	ADCLS3
	ADDI	AC,10
	MOVEM	AC,CLSIND
	AOS	CLSCTR
	JRST	ADCLS1
ADCLS3:	AOS	(P)
	POPJ	P,
	ADDI	AC,10
ADCLSE:	MOVEI	AC,BLNK
	PUSHJ	P,CHROUT
	MOVEI	AC,"-"
	PUSHJ	P,CHROUT
	MOVEI	AC,BLNK
	PUSHJ	P,CHROUT
	PUSHJ	P,ERRCUD
	PUSHJ	P,RNUMUD
	POPJ	P,

STOSED:	; STORE SEED'S DATA
	MOVE	AC1,[NMGAN,,SEDGAN]
	BLT	AC1,SEDDST
	MOVE	AC,TUCNO
	MOVEM	AC,SEDCSN
	MOVE	AC,TUCOST
	MOVEM	AC,SEEDCC
	MOVE	AC1,[NMCLSS,,SEDCLS]
	BLT	AC1,SEDEND
	POPJ	P,

RESEED:	; RESTORE SEED'S DATA 
	MOVE	AC1,[SEDGAN,,NMGAN]
	BLT	AC1,NMDIST
	MOVE	AC,SEDCSN
	MOVEM	AC,TUCNO
	MOVE	AC,SEEDCC
	MOVEM	AC,TUCOST
	MOVE	AC1,[SEDCLS,,NMCLSS]
	BLT	AC1,NMCLSS+127
	POPJ	P,

GTPSWD: MOVEI	AC,NNMSPC
        MOVEM	AC,NUM2
        PUSHJ	P,GCYPUN			; GET CIPHER FOR NEW USERNAME
	MOVEM	AC1,CYPUNM
	MOVE	AC,SEDCLN
	TLZ	AC,NPS.CD			; CLEAR NO PASSWORD OPTION BIT
	MOVEM	AC,NMCOLN
	PUSHJ	P,GETPSW
	JRST	[TYPSTR ([ASCIZ/Bad input password - Please re-enter./])
		 TCR	(1)
		 JRST	GTPSWD]
	POPJ	P,

GETTGW:	; GET TGW AND SN SEDCPW
	PUSHJ	P,ASKTGW
	MOVE	AC,NMCPAR
	MOVEM	AC,SEDCPW
	POPJ	P,

GTCLOP:	; GET COLON OPTION
	TRNE	FLG,U.RFI
	JRST	RRCOLN
	MOVE	AC,NMCOLN
	TLNE	AC,NPS.CD
	JRST	.+3
	PUSHJ	P,ASKCLN
	MOVE	AC,NMCOLN	
	MOVEM	AC,SEDCLN
	POPJ	P,
RRCOLN:	INSTR	([ASCIZ/Colon Word:/],CNVHLD,^D9,T)
	MOVEI	AC,CNVHLD
	PUSHJ	P,CNVO0
	HRLZI	AC1,CLN.CD
	MOVEM	AC1,SEDCLN
	MOVEM	AC1,NMCOLN
	POPJ	P,

ASKCLN:	; ASK FOR COLON OPTION
	GETYR	([ASCIZ "Do you want the Colon Option? (Y/N):"])
	JRST	ASKCL1
	MOVE	AC,NMCOLN
	TLZ	AC,CLN.CD
	JRST	ASKCL2
ASKCL1:	MOVE	AC,NMCOLN
	TLO	AC,CLN.CD
ASKCL2:	MOVEM	AC,NMCOLN
	POPJ	P,

GTIRCC:	; GET IRC CODE AND SAVE IN SEDIRC
	PUSHJ	P,GETIRC
	MOVE	AC,NMIRC
	MOVEM	AC,SEDIRC
	POPJ	P,

OUTCSN:	; OUTPUT CUSTOMER NUMBER
	SKIPN	CUSNUM
	JRST	[TYPSTR ([ASCIZ/Customer Number:/],777,0,0,N)
		 JRST	INPERR]
	MOVE	AC,CUSNUM
	PUSHJ	P,CIDOUT
	POPJ	P,

; LIST, EXAMINE OR PROCESS A NEW USER REQUEST

LEPNUQ:	TLNE	FLG2,V.LSQ
	JRST	NUQLEP
	PUSHJ	P,ACTCUD
	JRST	GETCMD
NUQLEP:	TRO	FLG2,V.NIR

DEFINE	LEPN	<
	X (USROUT,GTNUSR,RWNUSR)
	X (NGANOU,GTSGAN,RWGAN,AC,-1,U.PRV,U.IMM)
	X (NUUNOU,NUUNIN,RWUUN,AC,-1,U.PRV,U.IMM)
	X (OUTCSN,GETCUS,RWCSNO,AC,-1,U.PRV,U.IMM)
	X (PSWOUT,GETPSW,RWPSWD,AC,-1,U.PRV,U.IMM)
	X (COLNOU,GTCLOP,RWCOLN)
	X (MAIOUT,GTMUUN,RWMAIL,AC,-1,U.PRV,U.IMM)
	X (DSTOUT,GETDIS,RWDIST,AC,-1,U.PRV,U.IMM)
	X (HSYSOU,GETHOH,RWHSYS)
	X (UCCOUT,GETUCC,RWCOST,AC,-1,U.PRV,U.IMM)
	X (SEEDOU,GTSEED,RWSEED,AC,-1,U.PRV,U.IMM)
>
	EXMMAC	(NEP,LEPN,3,USRREQ)

	MOVE	AC,HOMSYS
        TRNE    AC,GRP.CD
        JRST    NOCHGH
	MOVEM	AC,OLDHOH
	PUSHJ	P,FINHOM
	JRST	NOCHGH
	MOVE	AC,OLDHOH
	MOVEM	AC,HOMSYS
	TRO	AC,HOM.CD
	MOVEM	AC,@CLSIND
NOCHGH: MOVE	AC,SEEDCC
	MOVEM	AC,TUCOST
	MOVE	AC,SEDCLN
	MOVEM	AC,NMCOLN
	PUSHJ	P,NUSMUD
	POPJ	P,
	PUSHJ	P,NUSNET
	POPJ	P,
	PUSHJ	P,NUSTTT
	PUSHJ	P,NUSWRQ
	JRST	NEWDON

RWNUSR:	REQOUT	(REQ,NNMSPC,L.UNM)
	POPJ	P,

RWPSWD:	REQNOT	(REQ,NMPSW,O)
	POPJ	P,

RWGAN:	REQNOT	(REQ,NMGAN,O)
	POPJ	P,

RWUUN:	REQNOT	(REQ,NMUUN,O)
	POPJ	P,

RWCSNO:	REQNOT	(REQ,TUCNO,D)
	POPJ	P,

RWMAIL:	REQOUT	(REQ,MAINAM,L.UNM)
	REQNOT	(REQ,MAIUUN,O)
	POPJ	P,

RWCOST:	REQNOT	(REQ,TUCOST,D)
	POPJ	P,

RWDIST:	REQNOT	(REQ,NMDIST,D)
	POPJ	P,

RWCOLN:	REQNOT	(REQ,NMCOLN,O)
	POPJ	P,

RWHSYS:	REQNOT	(REQ,HOMSYS,D)
	POPJ	P,

RWSEED:	REQOUT	(REQ,SEEDNM,L.UNM)
	POPJ	P,

GTMUUN:	TLNE	FLG2,V.EXC
	JRST	NALCHG
	REQINS	(REQ,MAINAM,L.UNM)
	MOVEI	AC1,L.UNM
	MOVEI	AC,MAINAM
	PUSHJ	P,CNTCHR
	SKIPG	AC1
	PUSHJ	P,[TLO	FLG2,V.RER
			SETZM	MAINAM
			SETZM	MAINAM+1
			SETZM	MAINAM+2
			POPJ	P,]
	REQINN	(REQ,O)
	SETZ	AC1,
	MOVEM	AC1,MAIUUN
	POPJ	P,

DSTOUT:	; OUTPUT DISTRICT
	SKIPGE	DISRCT
	JRST	[TYPSTR ([ASCIZ/District:/],777,0,0,N)
		 JRST	INPERR]
	MOVE	AC,DISRCT
	PUSHJ	P,DISOUT
	POPJ	P,

SEEDOU:	TYPSTR	([ASCIZ/Seed Username:/],777,0,0,N)
	SKIPN	SEEDNM
	JRST	INPERR
	TYPSTR	(SEEDNM,L.UNM,1)
	POPJ	P,

GTSGAN:	TLNE	FLG2,V.EXC		; NOT ALLOWED TO CHANGE DURING
	JRST	NALCHG			; EXAMMING A REQUEST
	REQINN	(REQ,O)
	PUSHJ	P,[SETO AC1,
		   TLO	FLG2,V.RER
		   POPJ	 P,]
	MOVEM	AC1,SEDGAN
	MOVEM	AC1,NMGAN
	POPJ	P,

GVCSNR:	; INPUT CUSTOMER NO. AND GET CUST. RECORD FROM ACTG.
	PUSHJ	P,GETCUS		; INPUT CUSTOMER NUMBER
	JRST	CAER01
	PUSHJ	P,GCAREC		; GET CUSTOMER RECORD FROM ACTG.
	JRST	CAER02
	PUSHJ	P,CUSASK
	JRST	CAER01
	AOS	(P)
	POPJ	P,
CAER01:	TCR	(1)
	JRST	GVCSNR
CAER02:	TYPSTR	([ASCIZ/Error during get a customer record from accounting./])
	PUSHJ	P,LUDAOT
	JRST	CAER01

CUSASK: CONCAT	(LINEBF,[ASCIZ/Customer Name: /],0,^D15)
	CONCAT	(LINEBF,ACCNAM,^D15,L.CUSN)
	CONCAT	(LINEBF,[ASCIZ" - OK? (Y/N):"])
	GETYR	(LINEBF)
	POPJ	P,
	AOS	(P)
	POPJ	P,

GANASK:	; ASK FOR A CORRECT GLOBAL ACCOUNT
	CONCAT	(LINEBF,[ASCIZ/Global Account: /],0,^D16)
        MOVE    AC,NMGAN
        MOVEI   AC4,^D8
        PUSHJ   P,NUMOUT
        CONCAT  (LINEBF,NUMSP,^D16,NUMSPL)
        CONCAT  (LINEBF,[ASCIZ" - OK? (Y/N):"])
	GETYR	(LINEBF)
	POPJ	P,
	AOS	(P)
	POPJ	P,

RNUMUD:	; REMOVE USER WITHOUT NOTIFYING PJ
	SNDBYT	(CUDPRT,\B.CCOM,REVMUD,I)
	PUSHJ	P,NNMCUD
	PUSHJ	P,SND40
	PUSHJ	P,NGANS
	JRST	[TYPSTR ([ASCIZ/Reverse new user not working./])
		 JRST	ERRCUD]
	PUSHJ	P,GET40
	TYPSTR	(NNMSPC,L.UNM,0,0,N)
	TYPSTR	([ASCIZ/is removed from the MUD./],777,1)
	POPJ	P,


CKCHAR: ; Check for illegal character.
        PUSH    P,AC1
        CAIL    AC,"0"
        CAILE   AC,"9"
        SKIPA
        JRST    NUM.OK
        CAIL    AC,"A"
        CAILE   AC,"Z"
        SKIPA
        JRST    CHR.OK
        MOVEI   AC1,LSCSET
CKCAGN: CAMN    AC,SCHSET-1(AC1)
        JRST    CHR.OK
        SOJG    AC1,CKCAGN
        POP     P,AC1
        POPJ    P,
CHR.OK: TRO     FLG2,V.NNR
NUM.OK: POP     P,AC1
        AOS     0(P)
        POPJ    P,

CKNNAM: TRZ	FLG2,V.NNR
	LDB	AC,[POINT 7,NNMSPC,6]
	CAIN	AC,"."
	JRST	[TYPSTR	([ASCIZ/Usernames cannot begin with a period "."/])
		 JRST	CKNNER]
	MOVE	AC2,[POINT 7,NNMSPC]
CNNAM2: ILDB	AC,AC2
	PUSHJ	P,CKCHAR
	JRST	CKNNME
	SOJG	AC1,CNNAM2
	TRZE	FLG2,V.NNR
	JRST	CNNAMR
	TLNE	FLG,U.PRV
	JRST	CNNAMR
	TYPSTR	([ASCIZ"Usernames may not contain all numeric characters."])
	JRST	CKNNER
CNNAMR: AOS	(P)
	POPJ	P,
CKNNME: TYPSTR  ([ASCIZ"Illegal Username -"],777,0,0,N)
	MOVEI	AC,40
	PUSHJ	P,CHROUT
CKNNER: TYPSTR  (PLSREN)
        POPJ    P,
 H@Pý