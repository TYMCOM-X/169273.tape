SUBTTL IOSUB - terminal input/output subroutines
; **** output a string in 7-bit
; input:
;  ac/   address of string
;  ac1/  max size of string
;  if u.crlf is set in flg, then terminate with a crlf
STROUT: 
        MOVEM   AC,SAC          ; save address
        PUSHJ   P,CNTCHR        ; count number of chars in string
        MOVE    AC2,SAC         ; retrieve address
        HRLI    AC2,(POINT 7)   ; make into a byte pointer
STROU1:
        ILDB    AC,AC2          ; get a char
        PUSHJ   P,CHROUT        ; output it
        SOJG    AC1,STROU1      ; do the rest
        PUSHJ   P,CRLF          ; output a crlf
        POPJ    P,              ; return

CRLF:   ;  here to output a carriage return and line feed.
        TLZN    FLG,U.CRLF
         JRST   CRLF2
        MOVEI   AC,.CR          ; set for a carriage return
        PUSHJ   P,CHROUT        ; output the carriage return
        MOVEI   AC,.LF          ; and do the same for a line feed
        PUSHJ   P,CHROUT
CRLF2:  POPJ    P,

CHROUT:   ; here to output a character in ac
        TLNE    FLG,U.NOTY      ; skip if we are not suppressing tty
        TLNE    FLG,U.FTTY      ; skip if we are not forcing tty
         SKIPA
         JRST   CHROU2          ; bypass tty output
        HRROI   AC3,.AXO8       ; set to auxcal the char to terminal
        AUXCAL  AC3,AC          ; do it
        AOJ     LPOST,
        CAIGE   AC,BLNK
         SETZ   LPOST,
CHROU2:    ; now check for file output
        TLNN    FLG,U.TOUT      ; skip if doing file output
         JRST   CHROU3
        SOSG    TOUCNT          ; modify tout file buffer pointer
         PUSHJ  P,NTOUBF        ; flush tout buffer
        IDPB    AC,TOUBP        ; deposit byte
        AOJ     LPOSF,
        CAIGE   AC,BLNK
         SETZ   LPOSF,
CHROU3:
        POPJ    P,              ; return
NTOUBF:  ; here to flush tout file buffer
        MOVE    AC3,[.CHOUT,,TOUCHN]    ; set for chanio
        CHANIO  AC3,            ; do it
        JRST    NTOUB2          ; ok return
         PUSHJ  P,TOUERR        ; tout error routine
NTOUB2: POPJ    P,              ; return

TOUERR: ; tout file output error routine
        TLZ     FLG,U.TOUT!U.NOTY ; reset tout and suppress tty bits
        MOVE    AC3,[.CHGS,,TOUCHN]
        CHANIO  AC3,SAC         ; get file status
        TYPSTR  ([ASCIZ/Error during TOUT output- status word :/],777,0,0,N)
        TYPNUM  (SAC,O,1)       ; type the file status word
        POPJ    P,

CHRIN:  ; input a character from the current terminal input device
	TRNE	FLG,U.RFI	; skip if not inputting from request file
	 POPJ	P,
	TRNN	FLG,U.NOES	; skip if logical escape not allowed
	 TLZN	FLG2,V.ESC	; skip if escape seen
	 SKIPA
	 JRST	ESCABT		; abort after escape seen
        TRZE    FLG,U.PEK       ; skip if no peek character
         JRST   [MOVE  AC,LASCHR ; restore peek character
                 JRST  CHRIN2]  ; and return
        MOVEI   AC3,@INRTAB(IF.P) ; address of ring buffer header
        ADDI    AC3,2           ; adjust to byte count
        SOSGE   0(AC3)          ; subtract one and skip if bytes left
         PUSHJ  P,NINBF         ; go get a new buffer
        TLNE    FLG,U.IEOF      ; skip if eof condition not set
         JRST   [MOVEI AC,.CR   ; set to return a delimiter (cr)
                 JRST  CHRIN2]  ; and return
        ILDB    AC,-1(AC3)      ; get a character
        JUMPE   AC,CHRIN        ; if null, get another
        CAIN    AC,.LF          ; skip if not a line feed
         JRST   CHRIN1          ; don't echo current line
        TLZN    FLG,U.EOL       ; skip if last char was eol
         JRST   CHRIN1          ; don't echo current line
        PUSHJ   P,ECHLN         ; echo the current line
CHRIN1:
	TLNE	FLG2,V.QUO	; skip if not quoting this char.
	 JRST	CHRINB
	   ; CAIN	AC,"_"		; skip if not quoting char.
	   ;  JRST	[TLO	FLG2,V.QUO
		   ;  JRST	CHRIN]
CHRINB: CAIL    AC,LCA          ; skip if less then a lowercase a
         SUBI   AC,BLNK         ; make uppercase
        CAIL    AC,BLNK         ; skip if it's less then blank
         JRST   CHRIN2
        CAIN    AC,.CR          ; skip if it's not a carriage return
         JRST   CHRIN2
        CAIN    AC,.LF          ; skip if it's not a line feed
         JRST   CHRIN2
        CAIN    AC,CTRLD        ; skip if it's not a ctrol d
         JRST   CHRIN2          ; echo to tout only
        JRST    CHRIN
CHRIN2: POPJ    P,

ECHLN:  ; echo an input line to TOUT and maybe TTY
        MOVE    AC4,-1(AC3)     ; get a byte pointer
        PUSHJ   P,ECHCHR        ; echo the first character
        MOVEM   AC,LASCHR       ; save it
ECHLN1: ILDB    AC,AC4          ; get the next char
        JUMPE   ECHLN2          ; jump to echln2 if we find a null
        PUSHJ   P,ECHCHR        ; echo the character
        CAIL    AC,BLNK         ; skip if char is less then a space
         JRST   ECHLN1          ; otherwise do the rest
        CAIN    AC,.LF          ; skip if it is not a line feed
         JRST   ECHLN2          ; terminate line echoing
        CAIN    AC,CTRLD        ; skip if it is not a control d
         JRST   ECHLN2          ; terminate
        JRST    ECHLN1          ; get another char
ECHLN2:
        MOVE    AC,LASCHR       ; restore the original character
        POPJ    P,              ; and return

ECHCHR: ; echo the character in ac
        JUMPE   AC,ECHCH2       ; stop if this is a null char
        CAIN    AC,.LF          ; skip if it's not a line feed
         JRST   ECHCH2
        CAIN    AC,.CR          ; skip if it's not a carriage return
         JRST   [PUSHJ  P,ECHLF ; echo crlf
                 JRST   ECHCH2] ; and return
        CAIN    AC,CTRLD        ; skip if it's not a control d
         JRST   [PUSHJ  P,ECHCD ; echo crlf
                 JRST   ECHCH2] ; and return
        CAMN    AC,ESCCHR       ; skip if this is not the escape char
         JRST   [PUSHJ  P,ECHESC
                 JRST   ECHCH2]
        CAIGE   AC,BLNK         ; skip if greater then or equal to space
         JRST   ECHCH2          ; return
        TLNN    FLG,U.IND       ; skip if input is from indirect file
         JRST   ECHCH1
        PUSHJ   P,CHROUT        ; echo to tout and possibly tty
        SKIPA                   ; and skip
ECHCH1: PUSHJ   P,CHROU2        ; echo to tout only
ECHCH2: POPJ    P,              ; and return

ECHLF:  ; echo cr as crlf
        TLNE    FLG,U.IND       ; skip if we are inputting from tty
         JRST   [TLO    FLG,U.CRLF
                 PUSHJ  P,CRLF
                 JRST   ECHLF2] ; output to tout and possibly tty
        MOVEI   AC,.CR          ; set for cr
        PUSHJ   P,CHROU2        ; output it to tout file
        MOVEI   AC,.LF          ; set for lf
        PUSHJ   P,CHROU2        ; output it to tout file
ECHLF2: MOVEI   AC,.CR          ; restore ac
        POPJ    P,

ECHCD:  ; echo control d as crlf to tout and tty
        MOVEI   AC,.CR          ; set to output cr
        PUSHJ   P,CHROUT        ; do it
        MOVEI   AC,.LF          ; same for line feed
        PUSHJ   P,CHROUT
        MOVEI   AC,CTRLD
        POPJ    P,

ECHESC: ; echo escape char as ^ crlf to tout and tty
        CAIE    AC,ESC          ; skip if escape char is escape
         JRST   ECHES2
        MOVEI   AC,UPAR         ; set to putput up arrow
        PUSHJ   P,CHROUT
        PUSHJ   P,ECHCD         ; do crlf
        MOVE    AC,ESCCHR
ECHES2: POPJ    P,

NINBF:  ; get a new input buffer from the terminal input device
        TLO     FLG,U.EOL
        HRLI    AC4,.CHIN       ; set for chanio input
        HRR     AC4,INCHN(IF.P) ; get the channel number
        CHANIO  AC4,            ; do the chanio
        SKIPA                   ; ok return
         JRST   ERRNIN          ; error return
        MOVEI   AC3,@INRTAB(IF.P) ; address of ring buffer header
        ADDI    AC3,2           ; adjust to byte count
        POPJ    P,

ERRNIN: ; here when terminal device input fails
        HRLI    AC4,.CHGS       ; set to get status bits
        HRR     AC4,INCHN(IF.P) ; get channel number
        CHANIO  AC4,AC4         ; get the status bits
        TRNN    AC4,IO.EOF      ; skip if end of file found
         JRST   ERRNI2          ; fatal error, give up
        HRLI    AC4,.CHREL      ; set to release the channel
        HRR     AC4,INCHN(IF.P) ;  ditto
        CHANIO  AC4,            ; do it
        JUMPG   IF.P,ERRNI1     ; jump to errni1 if this is not base device
        HRR     AC4,INCHN(IF.P) ; set to re-open tty channel
        HRLI    AC4,.CHOPN
        CHANIO  AC4,INDBL1      ; do it
         BOMB   (ERR.FI)
        SKIPA
ERRNI1: SOJG    IF.P,ERRNIA     ; adjust channel index and skip if not tty
	 TLZ	FLG2,V.INI	; reset init file flag
ERRNIA:
        TLZ     FLG,U.EOL       ; restore saved u.eol flag
        MOVE    AC4,SAVFLG(IF.P)
        TLNE    AC4,U.EOL
         TLO    FLG,U.EOL
        TRNN    AC4,U.PEK       ; see if saved flag had a peeked char
         JRST   ERRN1B          ; no, continue
        MOVE    AC,SAVLCH(IF.P) ; restore it
        MOVEM   AC,LASCHR
        TRO     FLG,U.PEK
        MOVEI   AC3,@INRTAB(IF.P)       ; echo rest of line (again)
        ADDI    AC3,2
        PUSHJ   P,ECHLN                 ; do it
        MOVEI   AC3,@INRTAB(IF.P)
        ADDI    AC3,2
ERRN1B:
        TRNE    FLG,U.NNIB      ; skip if we should try to get a buffer
         JRST   [TLO FLG,U.IEOF ; set eof on input bit
                 SKIPG IF.P     ; skip if input chan pointer gt 0
                  PUSHJ P,RESTTY        ; reset ind and noty flags
                 POPJ  P,]      ; and return
        JUMPG   IF.P,ERRNIB     ; continue
        PUSHJ   P,RESTTY        ; turn off ind file bit if this is base device
;ERRNIB: POPJ    P,              ; and try again
ERRNIB: POP     P,AC
        JRST    CHRIN

RESTTY:
        TLZ     FLG,U.IND       ; reset indirect file bit
        TLZN    FLG,U.NOTY      ; reset notty echo bit and skip if it was set
         JRST   RESTT2
        PUSH    P,AC1           ; save ac1 and ac2
        PUSH    P,AC2
        TCR     (1)
        TYPSTR  ([ASCIZ/Command file(s) exhausted - resuming input from tty/])
        TYPSTR  (@LASPRM,777,0,0,N)
        POP     P,AC2           ; restore ac1 and ac2
        POP     P,AC1
RESTT2: POPJ    P,

ERRNI2: ; input file error
        TLZ     FLG,U.IND!U.NOTY ; reset some flags
        MOVEM   AC4,NUM         ; save status word
        TYPSTR  ([ASCIZ/Error on input- status word :/],777,0,0,N)
        TYPNUM  (NUM,O,1)       ; output status word
        BOMB    (ERR.FI)        ; exit

STRIN:  ; here to input a line or a token
; ac/   address of place for the string or token
; ac1/  max length of string or token
	SETZ	AC2,		; in case doing input from request file
	TRNE	FLG,U.RFI	; skip if not inputting from request file
	 JRST	QSTRIN		; input continues from request in REQS.MAC
	TLO	FLG2,V.INP	; set doing input flag
        TRZ     FLG,U.OVF       ; reset overflow bit
        MOVEM   AC1,SAC         ; save the length
        IDIVI   AC1,5           ; get the number of words
        SKIPE   AC1+1           ; skip if no remainder
         AOJ    AC1,            ; round up
        MOVE    AC2,[ASCII /     /] ; set to fill it with blanks
        MOVN    AC3,AC1         ; make an aobjn index
        HRLZS   AC3             ;   ditto
        HRR     AC3,AC          ;   ditto
STRIN2: MOVEM   AC2,0(AC3)      ; move in the blanks
        AOBJN   AC3,STRIN2      ; do the rest
        MOVE    AC1,SAC         ; restore the length
        MOVE    AC2,AC          ; move address to ac2
        HRLI    AC2,(POINT 7)   ; make it a byte pointer
        MOVEM   AC,STIADR       ; save address
STRIN3: PUSHJ   P,CHRIN         ; get a character
        CAIN    AC,ATSIN        ; skip if not a @
         JRST   [PUSH   P,STIADR; save ac
                 PUSH   P,AC1   ; and ac1
                 PUSH   P,FLG   ; and flg
                 PUSHJ  P,CMDCOM ; get indirect file
                 JFCL
                 TRZ    FLG,U.OKAY      ; reset ok flag
                 TYPSTR (@LASPRM,777,0,0,N)
                 POP    P,AC    ; get old flg word
                 TRZ    FLG2,V.INS
                 TRNE   AC,V.INS
                  TRO   FLG2,V.INS
                 POP    P,AC1   ; restore ac1
                 POP    P,AC    ; and ac
                 JRST   STRIN ] ; and try again
	TLNE	FLG2,V.QUO	; skip if not quoting a char.
	 JRST	STRI3D
        CAIN    AC,BLNK         ; skip if not a blank
         JRST   STRIN3
        PUSHJ   P,CHKINC        ; skip if this is a delimiter
         JRST   STRI3D          ; go deposit it
        CAIE    AC,.CR          ; allow for carriage returns
         CAIN   AC,CTRLD        ; and allow for control d
         JRST   STRIN5          ; all done
        JRST    STRIN3          ; but ingore all other leading delimiters
STRI3B: PUSHJ   P,CHRIN         ; get a character
        PUSHJ   P,CHKINC        ; check for a delimiter
         SKIPA                  ; skip if not a delimiter
        JRST    STRIN5          ; jump to strin5 if it's a delimiter
        CAIE    AC,.CR          ; skip if it's a carriage return
STRI3D:  IDPB   AC,AC2          ;  otherwise, deposit it
	TLZ	FLG2,V.QUO
        SOJG    AC1,STRI3B      ; and get the rest
        TRO     FLG,U.OVF       ; set string overflow bit
STRIN4: PUSHJ   P,CHRIN         ; here if we're still getting chars after max
        PUSHJ   P,CHKINC        ; check for a delimiter
         JRST   STRIN4          ; not a delimiter
STRIN5:
        CAIGE   AC,BLNK         ; skip if delimiter was a space or comma
         JRST   [TLO   FLG,U.EOL
                 JRST  STRIN8]
        MOVEM   AC,SAC          ; save delimiter
;  now keep trying until we find an eol or a valid non delimiting character
STRIN6: PUSHJ   P,CHRIN         ; get a char
        CAIN    AC,BLNK         ; skip if not a space
         JRST   STRIN6          ; get another
        CAIN    AC,CMA          ; skip if not a comma
         JRST   STRIN6          ; get another
        CAIG    AC,BLNK         ; skip if greater then space
         JRST   STRIN7          ; done
        MOVEM   AC,LASCHR       ; save the character
        MOVE    AC,SAC          ; restore last delimiter
        TRO     FLG,U.PEK       ; set bit saying we have a peek char
        JRST    STRIN8          ; done
STRIN7: TLO     FLG,U.EOL       ; set end of line bit
STRIN8:
        TRNE    FLG,U.NOES      ; skip if logical escape allowed
         JRST   STRIN9
        MOVE    AC2,[ASCII/?    /]
        CAMN    AC2,@STIADR     ; skip if first word isn't a question mark
         JRST   GETCMD          ; jump to getcmd
STRIN9:
	TLZ	FLG2,V.QUO!V.INP
        POPJ    P,

CHKINC: ; here to see if the character input is a delimiter
	TLZE	FLG2,V.QUO	; skip if not quoting a char
	 JRST	CHKIN4
        TRNE    FLG2,V.INS       ; skip if we are inputting tokens
         JRST   CHKIN2          ; chkin2 if we're inputting a string
        CAIN    AC,BLNK         ; skip if this is not a blank
         JRST   CHKIN3
;       CAIN    AC,CMA          ; skip if this is not a comma
;        JRST   CHKIN3
CHKIN2: CAIN    AC,.LF          ; skip if this is not a line feed
         JRST   CHKIN3
        CAIN    AC,.CR          ; skip if this is not a carriage return
         JRST   CHKIN3
        CAIN    AC,CTRLD        ; skip if not a control d
         JRST   CHKIN3
        JRST    CHKIN4
CHKIN3: AOS     0(P)            ; skip return if char was a delimiter
CHKIN4: POPJ    P,

CNTCHR:   ; count number of chars in a string to print
        HRLI    AC,(POINT 7)    ; make it into a byte pointer
        SETZB   AC3,AC4         ; zero the counter
CNTCH1: ILDB    AC2,AC          ; get a char
        CAIN    AC2,BLNK        ; skip if is not a blank
         JRST   CNTCH2
        JUMPE   AC2,CNTCH3      ; if null then this is end
        MOVEI   AC4,1(AC3)      ; save last ok position
CNTCH2: AOJ     AC3,            ; increment counter
        SOJG    AC1,CNTCH1      ; do the rest
CNTCH3: MOVE    AC1,AC4
        POPJ    P,

OCTOUT: ; output the number in ac as an octal number
        MOVEI   AC4,^O10        ; set to output in octal
        PUSHJ   P,NUMOUT        ; output the number
; u.crlf may have allready been set
        PUSHJ   P,ADJNLN
        TYPSTR  (NUMSP,NUMSPL,0,0,N)
        MOVE    AC,NUM
        POPJ    P,

DECOUT: ; output the number in ac as a decimal number
        MOVEI   AC4,^D10        ; set for decimal output
        PUSHJ   P,NUMOUT        ; output the number
; u.crlf may have allready been set
        PUSHJ   P,ADJNLN
        TYPSTR  (NUMSP,NUMSPL,0,0,N)
        MOVE    AC,NUM          ; restore the number
        POPJ    P,

ADJNLN: ; adjst number by outputting spaces
        MOVE    AC,NUMLEN
        SUB     AC,NUMSIZ
        SKIPG   AC
         POPJ   P,              ; all done
        PUSHJ   P,SPACES        ; else output spaces
        POPJ    P,

OCTLIS: ; output the num in ac as a dec number to list file
        MOVEI   AC4,^O10        ; set for octal
        PUSHJ   P,NUMOUT
        MOVEI   AC,NUMSP
        MOVEI   AC1,NUMSPL
        PUSHJ   P,ODFOUT
        POPJ    P,

DECLIS:
        MOVEI   AC4,^D10
        PUSHJ   P,NUMOUT
        MOVEI   AC,NUMSP
        MOVEI   AC1,NUMSPL
        PUSHJ   P,ODFOUT
        POPJ    P,

NUMOUT: ; do numeric to string formatting
        SETZM   NUMSP
        SETZM   NUMSP+1
        SETZM   NUMSP+2
        SETZM   NUMSP+3
        SETZM   NUMSIZ
        MOVE    AC1,[POINT 7,NUMSP]
	TRNE	FLG,U.SIX	; skip if not converting to sixbit
	 MOVE	AC1,[POINT 6,NUMSP]
        MOVEM   AC1,NUMBP
        MOVE    AC1,AC          ; work with it in ac1 and ac2
NUMOUL: IDIV    AC1,AC4         ; isolate a digit
        HRLM    AC2,0(P)        ; put in stack
        AOS     NUMSIZ          ; increment field size
        JUMPE   AC1,NUMOU2      ; jump to numou2 if done
        PUSHJ   P,NUMOUL        ; nextt
NUMOU2: HLRZ    AC,0(P)         ; retrieve a digit
        ADDI    AC,"0"          ; change to ascii
	TRNE	FLG,U.SIX	; skip if not sixbit
	 SUBI	AC,40		; make sixbit
        IDPB    AC,NUMBP
        POPJ    P,              ; return

SPACES: ; space the number of chars in ac.
        MOVE    AC1,AC          ; move count to ac1
        MOVEI   AC,BLNK         ; set to output spaces
        PUSHJ   P,CHROUT        ; do it
        SOJG    AC1,.-1         ; do the rest
        POPJ    P,

TABS:   ; tabulate to the specified char position
        MOVE    AC1,LPOST       ; our current tty position
        TLNE    FLG,U.TOUT      ; skip if not goint to tout
         MOVE   AC1,LPOSF       ; if tout file, use tout position
        SUBI    AC,1(AC1)       ; compute number of spaces needed
        JUMPLE  AC,TABS2        ; no go if allready there or past
        PUSHJ   P,SPACES        ; output the necessary spaces
TABS2:  POPJ    P,
    wÚ