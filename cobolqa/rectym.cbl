**********************************************************************
*
*	R E C O M M E N D E D     S P A R E S     R O U T I N E S
*
**********************************************************************

IDENTIFICATION DIVISION.
PROGRAM-ID. RECTYM.
AUTHOR. JIM WESTLAKE.
REMARKS. TYMAINT-10 RECOMMENDED SPARES ROUTINES.

ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SPECIAL-NAMES.
	CHANNEL (1) IS TOP.

INPUT-OUTPUT SECTION.
FILE-CONTROL.
	SELECT MAST-FILE ASSIGN TO DSK
	ACCESS MODE IS INDEXED
	SYMBOLIC KEY IS MAST-KEY
	RECORD KEY IS M-REC-KEY
	RECORDING MODE IS SIXBIT.

	SELECT SITE-FILE ASSIGN TO DSK
	ACCESS MODE IS INDEXED
	SYMBOLIC KEY IS S-KEY
	RECORD KEY IS S-REC-KEY
	RECORDING MODE IS SIXBIT.

	SELECT REC-FILE ASSIGN TO DSK
	ACCESS MODE IS INDEXED
	SYMBOLIC KEY IS REC-KEY
	RECORD KEY IS REC-REC-KEY
	RECORDING MODE IS SIXBIT.

	SELECT LIST-FILE ASSIGN TO DSK
	ACCESS MODE IS SEQUENTIAL
	RECORDING MODE IS ASCII.

	SELECT NEW-FILE ASSIGN TO DSK
	ACCESS MODE IS SEQUENTIAL
	RECORDING MODE IS ASCII.

	SELECT GEN-SORT ASSIGN TO DSK DSK DSK DSK DSK.
**********************************************************************
*
*	F I L E     D E S C R I P T I O N S
*
**********************************************************************

DATA DIVISION.
FILE SECTION.

FD	MAST-FILE
	COPY FDMAST.
***********************************
*	FILE DESCRIPTIONS (CONT)
***********************************

FD	SITE-FILE
	COPY FDSITE.
***********************************
*	FILE DESCRIPTIONS (CONT)
***********************************

FD	REC-FILE
	COPY FDREC.



FD	NEW-FILE
	COPY FDNEW.



SD	GEN-SORT.
01	GEN-SORT-REC.
	02	GS-PARTNO	PIC X(11).
	02	GS-QUANS.
	  04	GS-QUAN-1	PIC 9999.
	  04	GS-QUAN-2	PIC 9999.
	  04	GS-QUAN-3	PIC 9999.
	  04	GS-QUAN-4	PIC 9999.
***********************************
*	FILE DESCRIPTIONS (CONT)
***********************************

FD	LIST-FILE
	BLOCK CONTAINS 5 RECORDS
	RECORD CONTAINS 132 CHARACTERS
	VALUE OF IDENTIFICATION IS LIST-FILE-NAME.

01	LIST-REC			PIC X(132).

01	CONFIG-LIST-REC.
	02	CL-PARTNO	PIC X(11).
	02	FILLER		PIC X.
	02	CL-DESC		PIC X(21).
	02	FILLER		PIC X.
	02	CL-VC		PIC XX.
	02	FILLER		PIC X.
	02	CL-VPN		PIC X(14).
	02	FILLER		PIC XX.
	02	CL-MAX		PIC ZZZ9.
	02	FILLER		PIC XX.
	02	CL-ON-HAND	PIC ZZZ9.
	02	FILLER		PIC XX.
	02	CL-PRICE	PIC ZZZ9.99.
	02	FILLER		PIC X.
	02	CL-QUAN-1	PIC ZZZ9.
	02	FILLER		PIC X.
	02	CL-PRICE-1	PIC ZZZZZ9.99.
	02	FILLER		PIC X.
	02	CL-QUAN-2	PIC ZZZ9.
	02	FILLER		PIC X.
	02	CL-PRICE-2	PIC ZZZZZ9.99.
	02	FILLER		PIC X.
	02	CL-QUAN-3	PIC ZZZ9.
	02	FILLER		PIC X.
	02	CL-PRICE-3	PIC ZZZZZ9.99.
	02	FILLER		PIC X.
	02	CL-QUAN-4	PIC ZZZ9.
	02	FILLER		PIC X.
	02	CL-PRICE-4	PIC ZZZZZ9.99.
**********************************************************************
*
*	W O R K I N G     S T O R A G E
*
**********************************************************************

WORKING-STORAGE SECTION.

******************************
*	ACCUMULATORS
******************************

77	I		PIC 999.
77	J		PIC 999.
77	L		PIC 9.
77	O		PIC 999.
77	X		PIC 999.
77	WS-LINE		PIC 999.
01	WS-PAGE		PIC 999.

******************************
*	ISAM FILE KEYS
******************************

01	MAST-KEY.
	02	M-K-PN-PL.
	  04	M-K-PARTNO	PIC X(11).
	  04	M-K-PL		PIC X.
01	WS-MAST-FILE-OPEN	PIC X.
01	MAST-REC-SAVE		PIC X(90).

01	S-KEY.
	02	S-K-SITE-NO	PIC 99.
	02	S-K-PARTNO	PIC X(11).
	02	S-K-REC-NO	PIC 9.
01	WS-SITE-FILE-OPEN	PIC X.

01	REC-KEY.
	02	REC-K-OPTION	PIC X(10).
	02	REC-K-PARTNO	PIC X(11).
01	WS-REC-FILE-OPEN	PIC X.
01	REC-REC-SAVE		PIC X(42).

******************************
*	CONTROL LOCATIONS
******************************

01	WS-ENA		PIC 9.
01	WS-MACRO-ARG.
	02	WS-MACRO-ARG-1.
	  04	FILLER		PIC X(5) USAGE DISPLAY-6.
	  04	WS-MACRO-ARG-N	PIC X    USAGE DISPLAY-6.
	02	WS-MACRO-ARG-2.
	  04	FILLER		PIC X(5) USAGE DISPLAY-6.
	  04	WS-MACRO-ARG-EN	PIC X    USAGE DISPLAY-6.
01	WS-ANS			PIC X.
01	RET-CODE		PIC 9.

01	WS-PL		PIC X.
01	WS-SITE-NAME	PIC X(20).
01	WS-SITE-NO	PIC 99.
01	WS-FIELD-NAME	PIC XXXX.
01	WS-PARTNO	PIC X(11).
01	WS-DESC		PIC X(21).
01	WS-CREF-DATA.
	02 FILLER	PIC X(4).
	02 WS-CREF-PARTNO	PIC X(11).
	02 FILLER		PIC X(6).
01	WS-VC		PIC XX.
01	WS-VPN		PIC X(14).
01	WS-MAX		PIC 999.
01	WS-QUAN		PIC 999.
01	WS-SYS-OPT.
	02	WS-S-OPT	PIC X(3).
	02	WS-S-OPT-NO	PIC 999.
	02	FILLER		PIC X(4).

01	WS-OPTION	PIC X(10).
01	WS-OPTION-TABLE.
	02	WS-OPT-TBL	PIC X(10) OCCURS 25 TIMES.
01	WS-DEV-ENTRY.
	02	WS-DEV-BLANK	PIC X VALUE SPACES.
	02	WS-DEV-NAME	PIC X(9).

01	LIST-FILE-NAME		PIC X(9).
01	WS-PARTIAL-FLAG		PIC X VALUE SPACES.
******************************
*	TOTAL ACCUMULATORS
*	FOR LIST TOTALS
******************************

01	WS-ITEMS-ALL.
	02	WS-ITEMS-1	PIC 9999.
	02	WS-ITEMS-2	PIC 9999.
	02	WS-ITEMS-3	PIC 9999.
	02	WS-ITEMS-4	PIC 9999.

01	WS-PRICE-ALL.
	02	WS-PRICE-1	PIC 99999V99.
	02	WS-PRICE-2	PIC 99999V99.
	02	WS-PRICE-3	PIC 99999V99.
	02	WS-PRICE-4	PIC 99999V99.
01	WS-PRICE-TOT-ALL.
	02	WS-PRICE-TOT-1	PIC 999999V99.
	02	WS-PRICE-TOT-2	PIC 999999V99.
	02	WS-PRICE-TOT-3	PIC 999999V99.
	02	WS-PRICE-TOT-4	PIC 999999V99.
01	WS-GRAND-TOT-ALL.
	02	WS-GRAND-TOT-1	PIC 999999V99.
	02	WS-GRAND-TOT-2	PIC 999999V99.
	02	WS-GRAND-TOT-3	PIC 999999V99.
	02	WS-GRAND-TOT-4	PIC 999999V99.

******************************
*	HEADERS
******************************

01	TYPE-OPT-HDR-1.
	02	FILLER	PIC X(22) VALUE SPACES.
	02	FILLER	PIC X(23) VALUE 'RECOMMENDED SPARES FOR '.
	02	TYPE-HDR-OPT	PIC X(10).

01	TYPE-OPT-HDR-2.
	02	FILLER	PIC X(20) VALUE 'PART NUMBER DESCRIPT'.
	02	FILLER	PIC X(20) VALUE 'ION         VPN OR G'.
	02	FILLER	PIC X(20) VALUE 'ENERIC   PRICE   Q-1'.
	02	FILLER	PIC X(12) VALUE ' Q-2 Q-3 Q-4'.

01	T-REC.
	02	T-PARTNO	PIC X(11).
	02	FILLER		PIC X.
	02	T-DESC		PIC X(18).
	02	FILLER		PIC XX.
	02	T-VPN		PIC X(14).
	02	FILLER		PIC X.
	02	T-PRICE		PIC ZZZ9.99.
	02	T-FLAG		PIC X.
	02	FILLER		PIC XX.
	02	T-QUAN-1	PIC ZZZ.
	02	FILLER		PIC X.
	02	T-QUAN-2	PIC ZZZ.
	02	FILLER		PIC X.
	02	T-QUAN-3	PIC ZZZ.
	02	FILLER		PIC X.
	02	T-QUAN-4	PIC ZZZ.

01	T-TOTAL-LINE.
	02	FILLER		PIC X(6) VALUE 'LEVEL '.
	02	T-TOTAL-LEVEL	PIC 9.
	02	FILLER		PIC X(5) VALUE SPACES.
	02	T-LINE-ITEMS	PIC ZZZ9.
	02	FILLER	PIC X(22) VALUE ' LINE ITEMS - PRICE = '.
	02	T-TOTAL-PRICE	PIC ZZZZZ9.99.
******************************
*	HEADERS (CONT)
******************************

01	WS-HDR-1.
	02	FILLER		PIC X(129).
	02	WS-HDR-PAGE	PIC ZZZ.
01	WS-HDR-2		PIC X(132).

01	LC-HDR-1.
	02	FILLER		PIC X(42) VALUE SPACES.
	02	LC1-SITE-NAME	PIC X(20).
	02	FILLER		PIC X(19) VALUE ' TEST REC-LIST FOR '.
	02	LC-OPTION	PIC X(10).
	02	FILLER		PIC X(33) VALUE SPACES.
	02	FILLER		PIC X(5)  VALUE 'PAGE '.
	02	FILLER		PIC X(3) VALUE SPACES.
01	LC-HDR-2.
	02	FILLER		PIC X(20) VALUE 'PART NUMBER DESCRIPT'.
	02	FILLER		PIC X(20) VALUE 'ION           VC VPN'.
	02	FILLER		PIC X(20) VALUE ' OR GENERIC   MAX   '.
	02	FILLER		PIC X(20) VALUE 'O/H   PRICE    Q-1  '.
	02	FILLER		PIC X(20) VALUE 'PRICE-1   Q-2  PRICE'.
	02	FILLER		PIC X(20) VALUE '-2   Q-3  PRICE-3   '.
	02	FILLER		PIC X(12) VALUE 'Q-4  PRICE-4'.

01	LC-HDR-3.
	02	FILLER		PIC X(42) VALUE SPACES.
	02	FILLER		PIC X(20) VALUE 'RECOMMENDED SPARES L'.
	02	FILLER		PIC X(8)  VALUE 'IST FOR '.
	02	LC3-SITE-NAME	PIC X(20).
	02	FILLER		PIC X(34).
	02	FILLER		PIC X(8)  VALUE 'PAGE    '.

01	LC-TOT-LINE	PIC X(22) VALUE 'TOTAL LINE ITEMS/PRICE'.


******************************
*	MESSAGES, ETC
******************************

01	MSG-FILE-ERROR.
	02	FILLER		PIC X(14) VALUE '** ERROR ON A '.
	02	MSG-ERR-TYPE	PIC X(7).
	02	FILLER		PIC X(4)  VALUE ' TO '.
	02	MSG-ERR-FILE	PIC X(4).
	02	FILLER		PIC X(8)  VALUE '-FILE **'.

01	MSG-FATAL		PIC X(15) VALUE '** F A T A L **'.
**********************************************************************
*
*	P R O C E D U R E     D I V I S I O N
*
**********************************************************************

PROCEDURE DIVISION.

******************************
DECLARATIVES.
******************************

MAST-BUSY SECTION.
	USE AFTER STANDARD ERROR PROCEDURE ON MAST-FILE OPEN.

MAST-BUSY-ROUTINE.
	DISPLAY '<MAST-FILE BUSY - WAITING...>'.
	ENTER MACRO WAIT.

REC-BUSY SECTION.
	USE AFTER STANDARD ERROR PROCEDURE ON REC-FILE OPEN.

REC-BUSY-ROUTINE.
	DISPLAY '<REC-FILE BUSY - WAITING...>'.
	ENTER MACRO WAIT.

******************************
END DECLARATIVES.
******************************
MAIN-PROGRAM SECTION.
START-PROGRAM-HERE.

******************************
*	START PROGRAM HERE
******************************

	MOVE SPACES TO WS-MACRO-ARG.
	ENTER MACRO INVINI USING WS-MACRO-ARG.
	IF WS-MACRO-ARG-1 NOT = 'RUN' OR WS-MACRO-ARG-EN NOT NUMERIC
		ENTER MACRO LOGOFF.
	MOVE WS-MACRO-ARG-EN TO WS-ENA.
	DISPLAY 'RECOMMENDED SPARES ROUTINES'.
	DISPLAY ' '.

******************************
*	PROCEDURE DISPATCH
******************************

PROC.
	DISPLAY 'PROCEDURE: ' WITH NO ADVANCING.
	ACCEPT WS-ANS.
	IF WS-ANS = 'A' GO TO ADD-REC;
	ELSE, IF WS-ANS = 'C' GO TO CHANGE-REC;
	ELSE, IF WS-ANS = 'D' GO TO DELETE-REC;
	ELSE, IF WS-ANS = 'E' OR WS-ANS = 'Q'
		IF WS-ENA = ZERO STOP RUN;
		ELSE, ENTER MACRO LOGOFF;
	ELSE, IF WS-ANS = 'G' GO TO GENERATE-SITE-FILE;
	ELSE, IF WS-ANS = 'L' GO TO LIST-CONFIGURATION;
	ELSE, IF WS-ANS = 'M'
		MOVE 'MASTYM' TO WS-MACRO-ARG
		MOVE WS-ENA TO WS-MACRO-ARG-EN
		ENTER MACRO RUNNIT USING WS-MACRO-ARG.
	IF WS-ANS = 'O' GO TO TYPE-OPTIONS;
	ELSE, IF WS-ANS = 'S'
		MOVE 'SITTYM' TO WS-MACRO-ARG
		MOVE WS-ENA TO WS-MACRO-ARG-EN
		ENTER MACRO RUNNIT USING WS-MACRO-ARG;
	ELSE, IF WS-ANS = 'T' GO TO TYPE-OPTION;
	ELSE, IF WS-ANS = 'Z'
		MOVE 'CONTYM' TO WS-MACRO-ARG
		MOVE WS-ENA TO WS-MACRO-ARG-EN
		ENTER MACRO RUNNIT USING WS-MACRO-ARG;
	ELSE, IF WS-ANS = '?' GO TO HELP;
	ELSE, DISPLAY 'INVALID - TYPE ? FOR HELP'
		GO TO PROC.
**********************************************************************
*
*	HELP ROUTINE - TYPES LIST OF VALID PROCEDURES
*
**********************************************************************

HELP.
	DISPLAY 'VALID COMMANDS ARE:'.
	DISPLAY 'A - ADD RECORDS TO REC-FILE'.
	DISPLAY 'C - CHANGE RECORDS IN REC-FILE'.
	DISPLAY 'D - DELETE RECORDS FROM REC-FILE'.
	DISPLAY 'E - EXIT'.
	DISPLAY 'G - GENERATE SITE RECOMMENDED SPARES FILE'.
	DISPLAY 'L - LIST REC-FILE'.
	DISPLAY 'M - ENTER MASTYM (MASTER FILE ROUTINES)'.
	DISPLAY 'O - TYPE OPTIONS IN REC-FILE'.
	DISPLAY 'Q - QUIT (EXIT)'.
	DISPLAY 'S - ENTER SITTYM (SITE INVENTORY SYSTEM)'.
	DISPLAY 'T - TYPE RECOMMENDED SPARES FOR AN OPTION'.
	DISPLAY '? - TYPE THIS TEXT'.
	DISPLAY ' '.
	GO TO PROC.
**********************************************************************
*
*	ADD-REC - ROUTINE TO ADD OPTIONS/RECORDS TO REC-FILE
*
**********************************************************************

ADD-REC.

ADD-REC-OPT-LOOP.
	DISPLAY 'ADD SPARES FOR OPTION - ' WITH NO ADVANCING.
	ACCEPT WS-DEV-NAME.
	IF WS-DEV-NAME = '.'
		GO TO PROC.
	MOVE WS-DEV-ENTRY TO REC-KEY.
	PERFORM READ-REC-FILE THRU READ-REC-FILE-END.
	IF RET-CODE NOT = 1
		GO TO ADD-REC-WRITE-PRIME.
	DISPLAY WS-DEV-NAME ' EXISTS IN REC-FILE - PROCEED ?'
		WITH NO ADVANCING.
	PERFORM YES-NO.
	IF WS-ANS = 'N'
		GO TO ADD-REC-OPT-LOOP;
		ELSE, MOVE WS-DEV-NAME TO REC-KEY
			MOVE REC-PL-ALL TO WS-PL
			GO TO ADD-REC-PARTNO-LOOP.
ADD-REC-WRITE-PRIME.
ADD-REC-PL-LOOP.
	DISPLAY 'P/L - ' WITH NO ADVANCING.
	ACCEPT WS-PL.
	IF WS-PL NOT = 'D' AND WS-PL NOT = 'I' AND WS-PL NOT = 'X'
		DISPLAY '** INVALID - '
		GO TO ADD-REC-PL-LOOP.
	PERFORM OPEN-O-REC-FILE THRU OPEN-O-REC-FILE-END.
	MOVE WS-DEV-ENTRY TO REC-KEY.
	MOVE WS-PL TO REC-PL-ALL.
	PERFORM WRITE-REC-REC THRU WRITE-REC-REC-END.
	MOVE WS-DEV-NAME TO REC-K-OPTION.
	PERFORM WRITE-REC-REC THRU WRITE-REC-REC-END.
	PERFORM CLOSE-REC-FILE THRU CLOSE-REC-FILE-END.
ADD-REC-PARTNO-LOOP.
	DISPLAY 'P/N: ' WITH NO ADVANCING.
	ACCEPT WS-PARTNO.
	IF WS-PARTNO = SPACES
		GO TO ADD-REC-PARTNO-LOOP;
	ELSE, IF WS-PARTNO = '.'
		GO TO ADD-REC-OPT-LOOP.
	MOVE WS-PARTNO TO REC-K-PARTNO.
	PERFORM READ-REC-FILE THRU READ-REC-FILE-END.
	IF RET-CODE NOT = 1
		GO TO ADD-REC-GET-DATA.
	DISPLAY 'OPT-P/N ' REC-KEY ' ALREADY EXISTS - ABORTING'.
	GO TO ADD-REC-PARTNO-LOOP.
******************************
*	ADD (CONT)
******************************

ADD-REC-GET-DATA.
	PERFORM READ-MASTER THRU READ-MASTER-END.
	IF RET-CODE = 1
		GO TO ADD-REC-GO.
	DISPLAY WS-PARTNO ' NOT IN MASTER FILE - ADD ? '
		WITH NO ADVANCING.
	PERFORM YES-NO.
	IF WS-ANS = 'N'
		DISPLAY '* ABORTING *'
		GO TO ADD-REC-PARTNO-LOOP.
	DISPLAY 'DESC: ' WITH NO ADVANCING.
	ACCEPT M-DESC.
	DISPLAY 'VPN: ' WITH NO ADVANCING.
	ACCEPT M-VPN.
	DISPLAY 'VC: ' WITH NO ADVANCING.
	ACCEPT M-VC.
	DISPLAY 'PRICE: ' WITH NO ADVANCING.
	ACCEPT M-PRICE.
ADD-GET-RR-CODE.
	DISPLAY 'R/R CODE - ' WITH NO ADVANCING.
	ACCEPT M-RR.
	IF M-RR NOT = 'E' AND M-RR NOT = 'R'
		DISPLAY '* INVALID - VALID R/R CODES ARE:'
		DISPLAY 'E - EXPENDABLE'
		DISPLAY 'R - REPAIRABLE'
		GO TO ADD-GET-RR-CODE.
	MOVE WS-OPTION TO M-OPT.
	PERFORM WRITE-MAST-REC THRU WRITE-MAST-REC-END.
******************************
*	ADD (CONT)
******************************

ADD-REC-GO.
	IF M-PRICE = ZERO
		DISPLAY 'PRICE: ' WITH NO ADVANCING
		ACCEPT M-PRICE.
	DISPLAY 'QUAN-1: ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-1.
	IF REC-QUAN-1 = 0
		MOVE 1 TO REC-QUAN-1
		MOVE ZERO TO REC-QUAN-2 REC-QUAN-3 REC-QUAN-4
		GO TO ADD-REC-QUAN-END.
	DISPLAY 'QUAN-2: ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-2.
	IF REC-QUAN-2 = 0
		MOVE ZERO TO REC-QUAN-3 REC-QUAN-4
		GO TO ADD-REC-QUAN-END.
	DISPLAY 'QUAN-3: ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-3.
	IF REC-QUAN-3 = 0
		MOVE ZERO TO REC-QUAN-4
		GO TO ADD-REC-QUAN-END.
	DISPLAY 'QUAN-4: ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-4.
ADD-REC-QUAN-END.
	DISPLAY 'OK? ' WITH NO ADVANCING.
	PERFORM YES-NO.
	IF WS-ANS = 'N'
		GO TO ADD-REC-PARTNO-LOOP.
	PERFORM WRITE-REC-REC THRU WRITE-REC-REC-END.
	IF M-OPT = SPACES OR M-OPT = 'SYSTEM'
		MOVE REC-OPTION TO M-OPT;
	ELSE, IF M-OPT = REC-OPTION
		NEXT SENTENCE;
		ELSE, MOVE M-OPT TO WS-SYS-OPT
			IF WS-S-OPT = 'SYS'
				ADD 1 TO WS-S-OPT-NO
				MOVE WS-SYS-OPT TO M-OPT;
			ELSE, MOVE 'SYS' TO WS-S-OPT
				MOVE 2 TO WS-S-OPT-NO
				MOVE WS-SYS-OPT TO M-OPT.
	IF REC-QUAN-1 > M-REC-QUAN
		MOVE REC-QUAN-1 TO M-REC-QUAN.
	PERFORM REWRITE-MAST-REC THRU REWRITE-MAST-REC-END.
	GO TO ADD-REC-PARTNO-LOOP.
**********************************************************************
*
*	CHANGE RECORDS IN RECOMMENDED SPARES FILE
*
**********************************************************************
CHANGE-REC.
	DISPLAY 'CHANGE RECORDS IN REC-FILE'.
	DISPLAY ' '.
CHANGE-REC-OPT-LOOP.
	DISPLAY 'OPTION: ' WITH NO ADVANCING.
	ACCEPT REC-K-OPTION.
	IF REC-K-OPTION = '.'
		GO TO PROC.
	MOVE SPACES TO REC-K-PARTNO.
	PERFORM READ-REC-FILE THRU READ-REC-FILE-END.
	IF RET-CODE NOT = 1
		DISPLAY 'OPTION ' REC-K-OPTION ' NOT IN REC-FILE'
		GO TO CHANGE-REC-OPT-LOOP.
CHANGE-REC-PN-LOOP.
	PERFORM CLOSE-OUT-REC-FILE THRU CLOSE-OUT-REC-FILE-END.
	DISPLAY REC-K-OPTION '/  P/N- ' WITH NO ADVANCING.
	ACCEPT REC-K-PARTNO.
	IF REC-K-PARTNO = '.'
		GO TO CHANGE-REC-OPT-LOOP.
	PERFORM READ-REC-FILE THRU READ-REC-FILE-END.
	IF RET-CODE NOT = 1
		DISPLAY REC-KEY '  NOT IN REC-FILE'
		GO TO CHANGE-REC-PN-LOOP.
CHANGE-REC-LOOP.
	DISPLAY 'QUAN-1 = ' REC-QUAN-1 '   QUAN-1 - ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-1.
	DISPLAY 'QUAN-2 = ' REC-QUAN-2 '   QUAN-2 - ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-2.
	DISPLAY 'QUAN-3 = ' REC-QUAN-3 '   QUAN-3 - ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-3.
	DISPLAY 'QUAN-4 = ' REC-QUAN-4 '   QUAN-4 - ' WITH NO ADVANCING.
	ACCEPT REC-QUAN-4.
	DISPLAY 'OK ?  ' WITH NO ADVANCING.
	PERFORM YES-NO.
	IF WS-ANS = 'N'
		DISPLAY '** ABORTING **'
		GO TO CHANGE-REC-PN-LOOP.
	PERFORM REWRITE-REC-REC THRU REWRITE-REC-REC-END.
	GO TO CHANGE-REC-PN-LOOP.
**********************************************************************
*
*	DELETE RECORDS FROM RECOMMENDED SPARES FILE
*
**********************************************************************

DELETE-REC.
	DISPLAY 'DELETE RECORDS FROM REC-FILE'.
DELETE-REC-OPT-LOOP.
	DISPLAY 'OPTION: ' WITH NO ADVANCING.
	ACCEPT REC-K-OPTION.
	IF REC-K-OPTION = '.'
		GO TO PROC.
DELETE-REC-PN-LOOP.
	PERFORM CLOSE-OUT-REC-FILE THRU CLOSE-OUT-REC-FILE-END.
	DISPLAY REC-K-OPTION ' / P/N: ' WITH NO ADVANCING.
	ACCEPT REC-K-PARTNO.
	IF REC-K-PARTNO = '.'
		GO TO DELETE-REC-OPT-LOOP.
	PERFORM OPEN-IO-REC-FILE THRU OPEN-IO-REC-FILE-END.
	DELETE REC-REC INVALID KEY
		DISPLAY REC-KEY ' NON-EXISTANT'.
	GO TO DELETE-REC-PN-LOOP.
**********************************************************************
*
*	GENERATE RECOMMENDED SPARES FILE FOR SITE CONFIGURATION
*
**********************************************************************

GENERATE-SITE-FILE.
	ENTER MACRO CONCHK USING WS-MACRO-ARG.
	IF WS-MACRO-ARG-N = '!'
		DISPLAY '** SYSTEM CONTROLLED **'
		IF WS-ENA NOT = ZERO
			DISPLAY '* GEN NOT ALLOWED DURING PRIME TIME *'
			GO TO PROC.
	PERFORM OPEN-I-REC-FILE THRU OPEN-I-REC-FILE-END.
	DISPLAY 'GENERATE SPARES  FILE FOR SITE'.
	DISPLAY 'SITE NAME - ' WITH NO ADVANCING.
	ACCEPT LC3-SITE-NAME.
	MOVE LC-HDR-3 TO WS-HDR-1.
	MOVE LC-HDR-2 TO WS-HDR-2.
	MOVE ZERO TO WS-PAGE WS-LINE.
GEN-SITE-LEVEL.
	DISPLAY 'GEN AT LEVEL (1 TO 4) - ' WITH NO ADVANCING.
	ACCEPT L.
	IF L NOT > 0 AND L NOT < 5
		GO TO GEN-SITE-LEVEL.
	DISPLAY 'STOCK FROM SITE # ' WITH NO ADVANCING.
	ACCEPT S-K-SITE-NO.
	MOVE ZERO TO S-K-REC-NO.
	PERFORM ACCEPT-CONFIGURATION THRU ACCEPT-CONFIG-END.
	IF RET-CODE NOT = 1
		GO TO PROC.
	PERFORM OPEN-I-SITE-FILE THRU OPEN-I-SITE-FILE-END.
	PERFORM OPEN-I-MAST-FILE THRU OPEN-I-MAST-FILE-END.
	MOVE 'SITE  LST' TO LIST-FILE-NAME.
	OPEN OUTPUT LIST-FILE NEW-FILE.
	PERFORM HEADER.
	SORT GEN-SORT ON ASCENDING KEY GS-PARTNO
	INPUT PROCEDURE IS GEN-SORT-INPUT THRU GSI-END
	OUTPUT PROCEDURE IS GEN-SORT-OUTPUT THRU GSO-END.
	CLOSE NEW-FILE LIST-FILE.
	DISPLAY 'SITE-FILE = SITE.SEQ / LIST-FISITE.LST'.
	GO TO PROC.

GEN-SORT-INPUT.
	PERFORM GET-NEXT-OPTION THRU GET-NEXT-OPTION-END.
	IF RET-CODE NOT = 1
		GO TO GSI-END.
GEN-SORT-INPUT-LOOP.
	PERFORM READ-REC-SEQ-OPT THRU READ-REC-SEQ-OPT-END.
	IF RET-CODE NOT = 1
		GO TO GEN-SORT-INPUT.
	MOVE REC-K-PARTNO TO GS-PARTNO.
	MOVE REC-QUANS TO GS-QUANS.
	RELEASE GEN-SORT-REC.
	GO TO GEN-SORT-INPUT-LOOP.
GSI-END. EXIT.

******************************
*	GENERATE SITE (CONT)
******************************

GEN-SORT-OUTPUT.
	MOVE SPACES TO NEW-REC.
	MOVE ZERO TO WS-MAX.
	RETURN GEN-SORT AT END
		DISPLAY 'NO RECORDS RET FM GEN-SORT **'
		GO TO GSO-END.
	MOVE ZERO TO WS-MAX.
	MOVE GS-PARTNO TO WS-PARTNO.
	GO TO GEN-SORT-OUTPUT-LOOP-START.
GEN-SORT-OUTPUT-LOOP.
	RETURN GEN-SORT AT END
		GO TO GSO-END.
GEN-SORT-OUTPUT-LOOP-START.
	IF L = 1
		MOVE GS-QUAN-1 TO WS-QUAN;
	ELSE, IF L = 2
		MOVE GS-QUAN-2 TO WS-QUAN;
	ELSE, IF L = 3
		MOVE GS-QUAN-3 TO WS-QUAN;
	ELSE, MOVE GS-QUAN-4 TO WS-QUAN.
	IF GS-PARTNO = WS-PARTNO
		IF WS-QUAN > WS-MAX
			MOVE WS-QUAN TO WS-MAX
			MOVE GS-QUANS TO REC-QUANS
			GO TO GEN-SORT-OUTPUT-LOOP;
		ELSE, GO TO GEN-SORT-OUTPUT-LOOP.
	MOVE WS-PARTNO TO NEW-PARTNO.
	MOVE WS-MAX TO NEW-MAX.
	WRITE NEW-REC.
	PERFORM LIST-CONFIG-LINE THRU LIST-CONFIG-LINE-END.
	MOVE GS-QUANS TO REC-QUANS.
	MOVE WS-QUAN TO WS-MAX.
	MOVE GS-PARTNO TO WS-PARTNO.
	GO TO GEN-SORT-OUTPUT-LOOP.
GSO-END.
	MOVE WS-PARTNO TO NEW-PARTNO.
	MOVE WS-MAX TO NEW-MAX.
	WRITE NEW-REC.
	PERFORM LIST-CONFIG-LINE THRU LIST-CONFIG-LINE-END.
	PERFORM LIST-CONFIG-TOTAL THRU LIST-CONFIG-TOTAL-END.
**********************************************************************
*
*	TYPE OPTIONS SUPPORTED IN REC-FILE
*
**********************************************************************

TYPE-OPTIONS.
	DISPLAY ' '.
	DISPLAY 'OPTIONS SUPPORTED:'
	DISPLAY ' '.
	MOVE ZERO TO WS-LINE.
	PERFORM CLOSE-REC-FILE THRU CLOSE-REC-FILE-END.
	PERFORM OPEN-I-REC-FILE THRU OPEN-I-REC-FILE-END.
	MOVE LOW-VALUE TO REC-KEY.
TYPE-OPTIONS-LOOP.
	READ REC-FILE INVALID KEY
		GO TO REC-FILE-ERROR.
	MOVE REC-OPTION TO WS-DEV-ENTRY.
	IF WS-DEV-BLANK NOT = SPACES
		MOVE SPACES TO WS-DEV-BLANK
		GO TO TYPE-OPTIONS-END.
	ADD 1 TO WS-LINE.
	DISPLAY REC-OPTION '	P/L=' REC-PL-ALL.
	GO TO TYPE-OPTIONS-LOOP.

TYPE-OPTIONS-END.
	DISPLAY ' '.
	DISPLAY WS-LINE ' TOTAL OPTIONS'.
	DISPLAY ' '.
	GO TO PROC.
**********************************************************************
*
*	LIST RECOMMENDED SPARES FOR SITE CONFIGURATION
*
**********************************************************************

LIST-CONFIGURATION.
	ENTER MACRO CONCHK USING WS-MACRO-ARG.
	IF WS-MACRO-ARG-N = '!'
		DISPLAY '** SYSTEM CONTROLLED **'
		IF WS-ENA NOT = ZERO
			DISPLAY '* LIST NOT ALLOWED DURING PRIME TIME *'
			GO TO PROC.
	MOVE 'CONFIGLST' TO LIST-FILE-NAME.
	PERFORM OPEN-I-REC-FILE THRU OPEN-I-REC-FILE-END.
	PERFORM OPEN-I-MAST-FILE THRU OPEN-I-MAST-FILE-END.
	PERFORM OPEN-I-SITE-FILE THRU OPEN-I-SITE-FILE-END.
	OPEN OUTPUT LIST-FILE.
	DISPLAY 'LIST RECOMMENDED SPARES FOR SITE CONFIGURATION.'
	DISPLAY 'SITE NAME - ' WITH NO ADVANCING.
	ACCEPT WS-SITE-NAME.
	MOVE WS-SITE-NAME TO LC1-SITE-NAME.
	MOVE LC-HDR-2 TO WS-HDR-2.
	DISPLAY 'STOCK FROM SITE # ' WITH NO ADVANCING.
	ACCEPT S-K-SITE-NO.
	MOVE ZERO TO S-K-REC-NO.
	MOVE ZERO TO WS-GRAND-TOT-ALL.
	PERFORM ACCEPT-CONFIGURATION THRU ACCEPT-CONFIG-END.
	IF RET-CODE NOT = 1
		CLOSE LIST-FILE
		GO TO PROC.
LIST-CONFIG-OPT-LOOP.
	MOVE ZEROS TO WS-PRICE-TOT-ALL WS-ITEMS-ALL.
	PERFORM GET-NEXT-OPTION THRU GET-NEXT-OPTION-END.
	IF RET-CODE NOT = 1
		GO TO LIST-CONFIG-END.
	MOVE WS-OPTION TO LC-OPTION.
	MOVE LC-HDR-1 TO WS-HDR-1.
	PERFORM HEADER.
LIST-CONFIG-ITEM-LOOP.
	PERFORM READ-REC-SEQ-OPT THRU READ-REC-SEQ-OPT-END.
	IF RET-CODE NOT = 1
		GO TO LIST-CONFIG-OPT-END.
	MOVE REC-PARTNO TO WS-PARTNO.
	PERFORM LIST-CONFIG-LINE THRU LIST-CONFIG-LINE-END.
	GO TO LIST-CONFIG-ITEM-LOOP.
LIST-CONFIG-OPT-END.
	PERFORM LIST-CONFIG-TOTAL THRU LIST-CONFIG-TOTAL-END.
	GO TO LIST-CONFIG-OPT-LOOP.
******************************
*	LIST CONFIG (CONT)
******************************

LIST-CONFIG-LINE.
	MOVE SPACES TO CONFIG-LIST-REC.
	PERFORM READ-MASTER THRU READ-MASTER-END.
	IF RET-CODE NOT = 1
		MOVE '* NOT IN MAST FILE *' TO CL-DESC
		GO TO LIST-CONFIG-NO-MASTER.
	MOVE M-PARTNO TO CL-PARTNO.
	MOVE M-DESC TO CL-DESC.
	MOVE M-VC TO CL-VC.
	MOVE M-VPN TO CL-VPN.
	MOVE M-PRICE TO CL-PRICE.
	PERFORM GET-SITE-REC THRU GET-SITE-REC-END.
	IF RET-CODE NOT = 1
		GO TO LIST-CONFIG-NO-MASTER.
	MOVE S-MAX TO CL-MAX.
	MOVE S-ON-HAND TO CL-ON-HAND.
LIST-CONFIG-NO-MASTER.
	PERFORM COMPUTE-REC-PRICES THRU COMPUTE-REC-PRICES-END.
	IF REC-QUAN-1 > 0
		MOVE REC-QUAN-1 TO CL-QUAN-1
		MOVE WS-PRICE-1 TO CL-PRICE-1.
	IF REC-QUAN-2 > 0
		MOVE REC-QUAN-2 TO CL-QUAN-2
		MOVE WS-PRICE-2 TO CL-PRICE-2.
	IF REC-QUAN-3 > 0
		MOVE REC-QUAN-3 TO CL-QUAN-3
		MOVE WS-PRICE-3 TO CL-PRICE-3.
	IF REC-QUAN-4 > 0
		MOVE REC-QUAN-4 TO CL-QUAN-4
		MOVE WS-PRICE-4 TO CL-PRICE-4.
	WRITE CONFIG-LIST-REC AFTER ADVANCING 1 LINE.
	ADD 1 TO WS-LINE.
	IF WS-LINE > 55
		PERFORM HEADER.
LIST-CONFIG-LINE-END. EXIT.
******************************
*	LIST CONFIG (CONT)
******************************
LIST-CONFIG-TOTAL.
	MOVE SPACES TO CONFIG-LIST-REC.
	WRITE CONFIG-LIST-REC AFTER ADVANCING 1 LINE.
	MOVE LC-TOT-LINE TO CONFIG-LIST-REC.
	MOVE WS-ITEMS-1 TO CL-QUAN-1.
	MOVE WS-PRICE-TOT-1 TO CL-PRICE-1.
	MOVE WS-ITEMS-2 TO CL-QUAN-2.
	MOVE WS-PRICE-TOT-2 TO CL-PRICE-2.
	MOVE WS-ITEMS-3 TO CL-QUAN-3.
	MOVE WS-PRICE-TOT-3 TO CL-PRICE-3.
	MOVE WS-ITEMS-4 TO CL-QUAN-4.
	MOVE WS-PRICE-TOT-4 TO CL-PRICE-4.
	WRITE CONFIG-LIST-REC AFTER ADVANCING 1 LINE.
LIST-CONFIG-TOTAL-END. EXIT.

LIST-CONFIG-END.
	MOVE WS-GRAND-TOT-ALL TO WS-PRICE-TOT-ALL.
	MOVE ZEROS TO WS-ITEMS-ALL.
	PERFORM LIST-CONFIG-TOTAL THRU LIST-CONFIG-TOTAL-END.
	CLOSE REC-FILE MAST-FILE LIST-FILE.
	DISPLAY LIST-FILE-NAME ' IS ON DISK'.
	GO TO PROC.
**********************************************************************
*
*	TYPE RECOMMENDED SPARES FOR AN OPTION
*
**********************************************************************

TYPE-OPTION.
	DISPLAY 'TYPE RECOMMENDED SPARES FOR AN OPTION'
	PERFORM OPEN-I-REC-FILE THRU OPEN-I-REC-FILE-END.
	PERFORM OPEN-I-SITE-FILE THRU OPEN-I-SITE-FILE-END.
	PERFORM OPEN-I-MAST-FILE THRU OPEN-I-MAST-FILE-END.
TYPE-OPTION-LOOP.
	DISPLAY 'FULL LISTING ? ' WITH NO ADVANCING.
	PERFORM YES-NO.
	IF WS-ANS = 'Y'
		MOVE SPACES TO WS-PARTIAL-FLAG;
	ELSE, MOVE 'P' TO WS-PARTIAL-FLAG.
	MOVE ZEROS TO WS-ITEMS-ALL WS-PRICE-ALL.
	DISPLAY 'OPTION - ' WITH NO ADVANCING.
	ACCEPT WS-OPTION.
	IF WS-OPTION = '.'
		GO TO PROC.
	MOVE WS-OPTION TO REC-K-OPTION.
	MOVE SPACES TO REC-K-PARTNO.
	READ REC-FILE INVALID KEY
		DISPLAY WS-OPTION ' NOT IN REC-FILE'
		GO TO TYPE-OPTION-LOOP.
	MOVE REC-PL-ALL TO WS-PL.
	IF WS-PL = 'D'
		MOVE ZERO TO S-K-SITE-NO;
	ELSE, IF WS-PL = 'X'
		MOVE 1 TO S-K-SITE-NO;
	ELSE, IF WS-PL = 'I'
		MOVE 2 TO S-K-SITE-NO.
	MOVE ZERO TO S-K-REC-NO.
	DISPLAY 'TYPE <CR> TO BEGIN -'.
	ACCEPT WS-ANS.
	MOVE ZERO TO WS-LINE.
	MOVE REC-PL-ALL TO WS-PL.
	MOVE WS-OPTION TO TYPE-HDR-OPT.
TYPE-OPTION-HEADER.
	DISPLAY TYPE-OPT-HDR-1.
	DISPLAY ' '.
	DISPLAY TYPE-OPT-HDR-2.
	DISPLAY ' '.
TYPE-OPTION-LINE.
	MOVE LOW-VALUE TO REC-KEY.
	READ REC-FILE INVALID KEY
		GO TO TYPE-OPTION-END.
	IF REC-OPTION NOT = WS-OPTION
		GO TO TYPE-OPTION-END.
	MOVE REC-PARTNO TO T-PARTNO WS-PARTNO.
	PERFORM READ-MASTER THRU READ-MASTER-END.
	IF RET-CODE = 0
		MOVE 'M' TO T-FLAG
		MOVE '* NOT IN MAST FILE *' TO T-DESC
		GO TO TYPE-OPTION-NO-MASTER.
	PERFORM GET-SITE-REC THRU GET-SITE-REC-END.
	IF RET-CODE = ZERO
		MOVE 'S' TO T-FLAG
		GO TO TYPE-OPTION-NO-SITE.
	IF S-LOC = SPACES OR S-MAX < REC-QUAN-1
		MOVE 'Q' TO T-FLAG;
	ELSE, IF S-ON-HAND < REC-QUAN-1
		MOVE '-' TO T-FLAG;
	ELSE,IF WS-PARTIAL-FLAG = 'P'
		GO TO TYPE-OPTION-LINE;
	ELSE, MOVE SPACES TO T-FLAG.
******************************
*	TYPE OPTION (CONT)
******************************

TYPE-OPTION-NO-SITE.
	MOVE M-DESC TO T-DESC.
	MOVE M-VPN TO T-VPN.
	MOVE M-PRICE TO T-PRICE.
TYPE-OPTION-NO-MASTER.
	MOVE REC-QUAN-1 TO T-QUAN-1.
	MOVE REC-QUAN-2 TO T-QUAN-2.
	MOVE REC-QUAN-3 TO T-QUAN-3.
	MOVE REC-QUAN-4 TO T-QUAN-4.
	IF REC-QUAN-1 NOT = 0
	    ADD 1 TO WS-ITEMS-1
	    COMPUTE WS-PRICE-1 = WS-PRICE-1 + (M-PRICE * REC-QUAN-1).
	IF REC-QUAN-2 NOT = 0
	    ADD 1 TO WS-ITEMS-2
	    COMPUTE WS-PRICE-2 = WS-PRICE-2 + (M-PRICE * REC-QUAN-2).
	IF REC-QUAN-3 NOT = 0
	    ADD 1 TO WS-ITEMS-3
	    COMPUTE WS-PRICE-3 = WS-PRICE-3 + (M-PRICE * REC-QUAN-3).
	IF REC-QUAN-4 NOT = 0
	    ADD 1 TO WS-ITEMS-4
	    COMPUTE WS-PRICE-4 = WS-PRICE-4 + (M-PRICE * REC-QUAN-4).
	ADD 1 TO WS-LINE.
	DISPLAY T-REC.
	GO TO TYPE-OPTION-LINE.

TYPE-OPTION-END.
	DISPLAY ' '.
	MOVE 1 TO T-TOTAL-LEVEL.
	MOVE WS-ITEMS-1 TO T-LINE-ITEMS.
	MOVE WS-PRICE-1 TO T-TOTAL-PRICE.
	DISPLAY T-TOTAL-LINE.
	MOVE 2 TO T-TOTAL-LEVEL.
	MOVE WS-ITEMS-2 TO T-LINE-ITEMS.
	MOVE WS-PRICE-2 TO T-TOTAL-PRICE.
	DISPLAY T-TOTAL-LINE.
	MOVE 3 TO T-TOTAL-LEVEL.
	MOVE WS-ITEMS-3 TO T-LINE-ITEMS.
	MOVE WS-PRICE-3 TO T-TOTAL-PRICE.
	DISPLAY T-TOTAL-LINE.
	MOVE 4 TO T-TOTAL-LEVEL.
	MOVE WS-ITEMS-4 TO T-LINE-ITEMS.
	MOVE WS-PRICE-4 TO T-TOTAL-PRICE.
	DISPLAY T-TOTAL-LINE.
	DISPLAY '-' WITH NO ADVANCING.
	ACCEPT WS-ANS.
	GO TO TYPE-OPTION-LOOP.

REC-FILE-ERROR.
	DISPLAY '**CONTENT ERROR IN REC-FILE - FATAL**'.
	CLOSE REC-FILE.
	STOP RUN.
**********************************************************************
*	COMMON SUBROUTINES
**********************************************************************

YES-NO.
	ACCEPT WS-ANS.
	IF WS-ANS NOT = 'Y' AND WS-ANS NOT = 'N'
		DISPLAY 'Y OR N ONLY'
		GO TO YES-NO.
YES-NO-END.


**********************************************************************
*	ROUTINE TO PRINT A HEADER FROM WS-HDR-1 AND WS-HDR-2
**********************************************************************

HEADER.
	ADD 1 TO WS-PAGE.
	MOVE WS-PAGE TO WS-HDR-PAGE.
	MOVE 0 TO WS-LINE.
	WRITE LIST-REC FROM WS-HDR-1 AFTER ADVANCING TOP.
	WRITE LIST-REC FROM WS-HDR-2 AFTER ADVANCING 2 LINES.
HEADER-END.
**********************************************************************
*	ROUTINE TO COMPUTE 3 LEVELS OF REC PRICING
**********************************************************************

COMPUTE-REC-PRICES.
	IF REC-QUAN-1 > 0
		ADD 1 TO WS-ITEMS-1
		MULTIPLY M-PRICE BY REC-QUAN-1 GIVING WS-PRICE-1
		ADD WS-PRICE-1 TO WS-PRICE-TOT-1
		ADD WS-PRICE-1 TO WS-GRAND-TOT-1;
	ELSE, MOVE ZERO TO WS-PRICE-1.
	IF REC-QUAN-2 > 0
		ADD 1 TO WS-ITEMS-2
		MULTIPLY M-PRICE BY REC-QUAN-2 GIVING WS-PRICE-2
		ADD WS-PRICE-2 TO WS-PRICE-TOT-2
		ADD WS-PRICE-2 TO WS-GRAND-TOT-2;
	ELSE, MOVE ZERO TO WS-PRICE-2.
	IF REC-QUAN-3 > 0
		ADD 1 TO WS-ITEMS-3
		MULTIPLY M-PRICE BY REC-QUAN-3 GIVING WS-PRICE-3
		ADD WS-PRICE-3 TO WS-PRICE-TOT-3
		ADD WS-PRICE-3 TO WS-GRAND-TOT-3;
	ELSE, MOVE ZERO TO WS-PRICE-3.
	IF REC-QUAN-4 > 0
		ADD 1 TO WS-ITEMS-4
		MULTIPLY M-PRICE BY REC-QUAN-4 GIVING WS-PRICE-4
		ADD WS-PRICE-4 TO WS-PRICE-TOT-4
		ADD WS-PRICE-4 TO WS-GRAND-TOT-4;
	ELSE, MOVE ZERO TO WS-PRICE-4.
COMPUTE-REC-PRICES-END.
**********************************************************************
*
*	ROUTINE TO ACCEPT A SYSTEM CONFIGURATION & LOAD INTO TABLE
*
*	RET-CODE = 1 SUCCESSFUL
*		   2 TABLE O'FLOWED
*
**********************************************************************

ACCEPT-CONFIGURATION.
	MOVE ZERO TO O.
	DISPLAY ' ENTER OPTION<CR>; AT END TYPE <END>'.
ACCEPT-CONFIG-LOOP.
	ACCEPT REC-KEY.
	IF REC-KEY = SPACES
		GO TO ACCEPT-CONFIG-LOOP;
	ELSE, IF REC-KEY = 'END'
		MOVE 1 TO RET-CODE
		GO TO ACCEPT-CONFIG-END;
	ELSE, IF REC-KEY = 'ALL' AND O = 0
		MOVE 999 TO O
		MOVE 1 TO RET-CODE
		GO TO ACCEPT-CONFIG-GET-FIRST.
	READ REC-FILE INVALID KEY
		DISPLAY REC-KEY ' NOT IN REC-FILE - IGNORED'
		GO TO ACCEPT-CONFIG-LOOP.
	MOVE 1 TO X.
ACCEPT-CONFIG-CHECK-DUP.
	IF X NOT > O
		IF WS-OPT-TBL(X) NOT = REC-KEY
			ADD 1 TO X
			GO TO ACCEPT-CONFIG-CHECK-DUP;
		ELSE, DISPLAY 'DUPLICATE ENTRY - IGNORED'
			GO TO ACCEPT-CONFIG-LOOP.
	ADD 1 TO O.
	IF O > 25
		DISPLAY 'DEVICE TABLE OVERFLOW - **FATAL**'
		MOVE 2 TO RET-CODE
		GO TO ACCEPT-CONFIG-END.
	MOVE REC-KEY TO WS-OPT-TBL(O).
	GO TO ACCEPT-CONFIG-LOOP.
ACCEPT-CONFIG-END. EXIT.

ACCEPT-CONFIG-GET-FIRST.
	MOVE LOW-VALUE TO REC-KEY.
	READ REC-FILE INVALID KEY
		DISPLAY 'INVALID KEY AT ACCEPT-CONFIG-GET-FIRST'
		STOP RUN.
	MOVE REC-OPTION TO WS-DEV-ENTRY.
	IF WS-DEV-BLANK = SPACES GO TO ACCEPT-CONFIG-GET-FIRST.
	MOVE REC-OPTION TO WS-OPTION.
	GO TO ACCEPT-CONFIG-END.
**********************************************************************
*	ROUTINE TO EXTRACT NEXT OPTION FROM TABLE
*	RET-CODE = 1 SUCCESSFUL
*		  2 NO MORE OPTIONS IN TABLE
**********************************************************************

GET-NEXT-OPTION.
	IF O NOT = 999
		GO TO GET-NEXT-OPTION-GO.
	IF REC-PARTNO = SPACES AND REC-OPTION NOT = SPACES
		MOVE  1 TO RET-CODE
		GO TO GET-NEXT-OPTION-END.
	MOVE LOW-VALUE TO REC-KEY.
	READ REC-FILE INVALID KEY
		MOVE 2 TO RET-CODE
		GO TO GET-NEXT-OPTION-END.
	IF REC-PARTNO NOT = SPACES
		DISPLAY ' ERROR IN REC-FILE TRYING TO FIND SEQ OPTION'
		DISPLAY 'REC-REC-KEY = ' REC-REC-KEY
		STOP RUN.
	MOVE 1 TO RET-CODE.
	GO TO GET-NEXT-OPTION-END.
GET-NEXT-OPTION-GO.
	IF O = 0
		MOVE 2 TO RET-CODE
		GO TO GET-NEXT-OPTION-END.
	MOVE WS-OPT-TBL(O) TO REC-KEY.
	SUBTRACT 1 FROM O.
	READ REC-FILE INVALID KEY
		DISPLAY 'ERROR UNLOADING OPTION TABLE'
		DISPLAY 'REC-KEY = ' REC-KEY '  O = ' O
		DISPLAY '**FATAL ERROR**'
		STOP RUN.
	MOVE 1 TO RET-CODE.
GET-NEXT-OPTION-END.
	MOVE REC-OPTION TO WS-OPTION.
	MOVE REC-PL-ALL TO WS-PL.
**********************************************************************
*	ROUTINE TO  SEQ READ REC-FILE FOR CURRENT OPT
*	RET-CODE = 1 SUCCESSFUL
*		   2 NO MORE RECORDS FOR CURRENT OPTION
**********************************************************************

READ-REC-SEQ-OPT.
	MOVE LOW-VALUE TO REC-KEY.
	READ REC-FILE INVALID KEY
		MOVE 2 TO RET-CODE
		GO TO READ-REC-SEQ-OPT-END.
	MOVE REC-REC-KEY TO REC-KEY.
	IF REC-K-OPTION NOT = WS-OPTION
		MOVE 2 TO RET-CODE
		GO TO READ-REC-SEQ-OPT-END.
	MOVE 1 TO RET-CODE.
READ-REC-SEQ-OPT-END. EXIT.
**********************************************************************
*
*	MASTER FILE HANDLERS
*
**********************************************************************

OPEN-I-MAST-FILE.
	IF WS-MAST-FILE-OPEN = 'I' OR WS-MAST-FILE-OPEN = 'B'
		GO TO OPEN-I-MAST-FILE-END;
	ELSE, IF WS-MAST-FILE-OPEN NOT = SPACES
		CLOSE MAST-FILE.
	OPEN INPUT MAST-FILE.
	MOVE 'I' TO WS-MAST-FILE-OPEN.
OPEN-I-MAST-FILE-END. EXIT.

OPEN-O-MAST-FILE.
	IF WS-MAST-FILE-OPEN = 'O' OR WS-MAST-FILE-OPEN = 'B'
		GO TO OPEN-O-MAST-FILE-END;
	ELSE, IF WS-MAST-FILE-OPEN NOT = SPACES
		CLOSE MAST-FILE.
	OPEN OUTPUT MAST-FILE.
	MOVE 'O' TO WS-MAST-FILE-OPEN.
OPEN-O-MAST-FILE-END. EXIT.

OPEN-IO-MAST-FILE.
	IF WS-MAST-FILE-OPEN = 'B'
		GO TO OPEN-IO-MAST-FILE-END;
	ELSE, IF WS-MAST-FILE-OPEN NOT = SPACES
		CLOSE MAST-FILE.
	OPEN I-O MAST-FILE.
	MOVE 'B' TO WS-MAST-FILE-OPEN.
OPEN-IO-MAST-FILE-END. EXIT.

READ-MASTER.
	PERFORM OPEN-I-MAST-FILE THRU OPEN-I-MAST-FILE-END.
	MOVE WS-PARTNO TO MAST-KEY.
READ-MASTER-LOOP.
	READ MAST-FILE INVALID KEY
		MOVE ZERO TO RET-CODE
		GO TO READ-MASTER-END.
	IF M-FLAG = 1 AND M-K-PL = SPACES
		MOVE WS-PL TO M-K-PL
		GO TO READ-MASTER-LOOP.
	IF M-CREF = 'X'
		MOVE M-CREF-PN-PL TO MAST-KEY
		GO TO READ-MASTER-LOOP.
	MOVE 1 TO RET-CODE.
READ-MASTER-END. EXIT.
******************************
*	MASTER FILE (CONT)
******************************

WRITE-MAST-REC.
	IF WS-MAST-FILE-OPEN NOT = 'O' AND WS-MAST-FILE-OPEN NOT = 'B'
		MOVE MAST-REC TO MAST-REC-SAVE
		PERFORM OPEN-O-MAST-FILE THRU OPEN-O-MAST-FILE-END
		MOVE MAST-REC-SAVE TO MAST-REC.
	WRITE MAST-REC INVALID KEY
		GO TO MAST-WRITE-ERROR.
WRITE-MAST-REC-END. EXIT.

REWRITE-MAST-REC.
	IF WS-MAST-FILE-OPEN NOT = 'B'
		MOVE MAST-REC TO MAST-REC-SAVE
		PERFORM OPEN-IO-MAST-FILE THRU OPEN-IO-MAST-FILE-END
		MOVE MAST-REC-SAVE TO MAST-REC.
	REWRITE MAST-REC INVALID KEY
		GO TO MAST-REWRITE-ERROR.
REWRITE-MAST-REC-END. EXIT.

MAST-WRITE-ERROR.
	MOVE 'WRITE' TO MSG-ERR-TYPE.
	GO TO MAST-ERROR-ALL.
MAST-REWRITE-ERROR.
	MOVE 'REWRITE' TO MSG-ERR-TYPE.
MAST-ERROR-ALL.
	MOVE 'MAST' TO MSG-ERR-FILE.
	DISPLAY MSG-FILE-ERROR.
	DISPLAY 'MAST-KEY: ' MAST-KEY '    WS-PARTNO: ' WS-PARTNO.
	DISPLAY 'MAST-REC: ' MAST-REC.
	GO TO FATAL-FILE-ERROR.
**********************************************************************
*
*	RECOMMENDED SPARES FILE HANDLERS
*
**********************************************************************

OPEN-I-REC-FILE.
	IF WS-REC-FILE-OPEN = 'I' OR WS-REC-FILE-OPEN = 'B'
		GO TO OPEN-I-REC-FILE-END;
	ELSE, IF WS-REC-FILE-OPEN NOT = SPACES
		CLOSE REC-FILE.
	OPEN INPUT REC-FILE.
	MOVE 'I' TO WS-REC-FILE-OPEN.
OPEN-I-REC-FILE-END. EXIT.

OPEN-O-REC-FILE.
	IF WS-REC-FILE-OPEN = 'O' OR WS-REC-FILE-OPEN = 'B'
		GO TO OPEN-O-REC-FILE-END;
	ELSE, IF WS-REC-FILE-OPEN NOT = SPACES
		CLOSE REC-FILE.
	OPEN OUTPUT REC-FILE.
	MOVE 'O' TO WS-REC-FILE-OPEN.
OPEN-O-REC-FILE-END. EXIT.

OPEN-IO-REC-FILE.
	IF WS-REC-FILE-OPEN = 'B'
		GO TO OPEN-IO-REC-FILE-END;
	ELSE, IF WS-REC-FILE-OPEN NOT = SPACES
		CLOSE REC-FILE.
	OPEN I-O REC-FILE.
	MOVE 'B' TO WS-REC-FILE-OPEN.
OPEN-IO-REC-FILE-END. EXIT.

READ-REC-FILE.
	PERFORM OPEN-I-REC-FILE THRU OPEN-I-REC-FILE-END.
	READ REC-FILE INVALID KEY
		MOVE ZERO TO RET-CODE
		GO TO READ-REC-FILE-END.
	MOVE 1 TO RET-CODE.
READ-REC-FILE-END. EXIT.

WRITE-REC-REC.
	IF WS-REC-FILE-OPEN NOT = 'O' AND WS-REC-FILE-OPEN NOT = 'B'
		MOVE REC-REC TO REC-REC-SAVE
		PERFORM OPEN-O-REC-FILE THRU OPEN-O-REC-FILE-END
		MOVE REC-REC-SAVE TO REC-REC.
	WRITE REC-REC INVALID KEY
		GO TO REC-WRITE-ERROR.
WRITE-REC-REC-END. EXIT.
******************************
*	REC FILE (CONT)
******************************

REWRITE-REC-REC.
	IF WS-REC-FILE-OPEN NOT = 'B'
		MOVE REC-REC TO REC-REC-SAVE
		PERFORM OPEN-IO-REC-FILE THRU OPEN-IO-REC-FILE-END
		MOVE REC-REC-SAVE TO REC-REC.
	REWRITE REC-REC INVALID KEY
		GO TO REC-REWRITE-ERROR.
REWRITE-REC-REC-END. EXIT.

CLOSE-OUT-REC-FILE.
	IF WS-REC-FILE-OPEN = 'B' OR WS-REC-FILE-OPEN = 'O'
		CLOSE REC-FILE
		MOVE SPACES TO WS-REC-FILE-OPEN.
CLOSE-OUT-REC-FILE-END. EXIT.

CLOSE-REC-FILE.
	IF WS-REC-FILE-OPEN NOT = SPACES
		CLOSE REC-FILE
		MOVE SPACES TO WS-REC-FILE-OPEN.
CLOSE-REC-FILE-END. EXIT.

REC-WRITE-ERROR.
	MOVE 'WRITE' TO MSG-ERR-TYPE.
	GO TO REC-ERROR-ALL.
REC-REWRITE-ERROR.
	MOVE 'REWRITE' TO MSG-ERR-TYPE.
REC-ERROR-ALL.
	MOVE 'REC' TO MSG-ERR-FILE.
	DISPLAY MSG-FILE-ERROR.
	DISPLAY 'REC-KEY: ' REC-KEY '     WS-PARTNO: ' WS-PARTNO.
	DISPLAY 'REC-REC: ' REC-REC.
FATAL-FILE-ERROR.
	DISPLAY MSG-FATAL.
	ENTER MACRO LOGOFF.
**********************************************************************
*
*	SITE FILE HANDLERS
*
**********************************************************************

OPEN-I-SITE-FILE.
	IF WS-SITE-FILE-OPEN = 'I' OR WS-SITE-FILE-OPEN = 'B'
		GO TO OPEN-I-SITE-FILE-END;
	ELSE, IF WS-SITE-FILE-OPEN NOT = SPACES
		CLOSE SITE-FILE.
	OPEN INPUT SITE-FILE.
	MOVE 'I' TO WS-SITE-FILE-OPEN.
OPEN-I-SITE-FILE-END. EXIT.

CLOSE-SITE-FILE.
	IF WS-SITE-FILE-OPEN NOT = SPACES
		CLOSE SITE-FILE
		MOVE SPACES TO WS-SITE-FILE-OPEN.
CLOSE-SITE-FILE-END. EXIT.

GET-SITE-REC.
	MOVE M-PARTNO TO S-K-PARTNO.
	READ SITE-FILE INVALID KEY
		MOVE ZERO TO RET-CODE
		GO TO GET-SITE-REC-END.
	MOVE 1 TO RET-CODE.
GET-SITE-REC-END. EXIT.
 Qh«