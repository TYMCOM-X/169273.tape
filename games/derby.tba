1 DIM ZZ12$(0:100)
2 DEF STRING EXTERNAL GET.LO
3 DEF STRING EXTERNAL GET.RU
4 DEF STRING EXTERNAL GET.PR
5 PPN$=SUBSTR(GET.LO,2,INDEX(GET.LO,"]")-2)
6 OPEN"(HOLD)TATTLE",INPUT,1,ERR 12
7 ON ENDFILE (1) GOTO 11
8 INPUT FROM 1 IN FORM"R":ZZ12$(XX1)
9 XX1=XX1+1
10 GOTO 8
11 CLOSE 1
12 ZZ12$(XX1+1)=PPN$+"  "+GET.PR
13 OPEN"(HOLD)TATTLE-A",OUTPUT,1
14 FOR YY1=0 TO XX1+1
15 PRINT ON 1:ZZ12$(YY1)
16 NEXT YY1
17 CLOSE 1
100 LET X=(TIME MOD 45)
110 FOR I=0 TO X
120 LET NUM=RND
130 NEXT I
140 DIM A(0:30),B(30),D(30),D1(30)
150 DIM L(50),L3(50)
160 DIM R(156,4),C4(10),C5(10),D7(10)
170 DIM Q(156,4)
180 FOR N8=1 TO 10
190 LET D7(N8)=5000
200 NEXT N8
210 PRINT
220 DIM H5$(52)
230 DIM H1$(52),H2$(52),H3$(52),H4$(0:52),C2$(10)
240 PRINT "                  **WELCOME TO DERBY**"
250 PRINT
260 PRINT "         A HORSE RACE SIMULATOR FOR THE TYMCOM-X'S"
270 PRINT
280 GOTO 3940
290 FOR N8=1 TO 27 BY 26
300 FOR N1=(1+N8-1) TO (26+N8-1)
310 LET H5$(N1)="1"
320 READ H1$(N1)
330 NEXT N1
340 FOR N2=(1+N8-1) TO (26+N8-1)
350 READ H2$(N2)
360 NEXT N2
370 FOR N3=(1+N8-1) TO (26+N8-1)
380 READ H3$(N3)
390 NEXT N3
400 NEXT N8
410 DATA ATARI,BAND AID,CITATION,DAMION,ENTERPRISE,FORTE,GALAXIAN
420 DATA HARRY O,INTREPID,JOKER,KING KONG,LANCELOT,MAGICIAN,NUMBER GAME
430 DATA ORION,PRANCER,QUEEN B,ROUNDTABLE,SECRETARIAT,THORNBIRD
440 DATA ULTRAMAX,VICTORIOUS,WEE WILLY,XTRA EFFORT,YODELER,ZEBRA KING
450 DATA ASTRO BOY,BANK IT,CLEAN UP,DRAG RACER,EZ STREET,FRODO
460 DATA GOLLUM,HELL HOOVES,IGGY,JET SET,KAMAKAZI,LOW BALL,MEAT WAGON
470 DATA NEON LIGHTS,OVERCAT,PICK POCKET,QUACKERS,RINGER,SEA BISCUIT
480 DATA TIE FIGHTER,U-BOAT,VIPER PILOT,WING IT,XL-ENT,YELLOW BEARD,ZOOT SUIT
490 DATA APPLE ANNIE,BREAK DANCER,CREEP ALONG,DEADBEAT,EXLAX,FINGER FOOD
500 DATA GLUE FACTORY,HORSITOSIS,INTER COOLED,JEEZ,KILLER B,LONG GONE
510 DATA MAD MAX,NECROMANCER,OUTTA HERE,PUBERTY QUEEN,QUICKSAND
520 DATA ROAD WARRIOR,SKUNK BREATH,TURKEY TROT,UNDERDOG,VINDICATOR
530 DATA WHITE HEAD,X-PONENT,YOKEL,ZERO HOUR
540 DATA ALPHA CENTAURI,BLADE RUNNER,COUNTER TOP,DEVIL DOG,ELF KING
550 DATA FORNICATOR,GANDALF,HAIRY DOG,IVANHOE,JACKPOT
560 DATA KNIGHT RIDER,LOVE MAKER,MAGNUM PI,NEPTUNE,OATS AND GRITS
570 DATA PETER PAN,QUEUED,ROYAL FLUSH,SEATTLE SLEW,THE JUICE
580 DATA UNICORN,VAGABOND,WHIRLAWAY,X RATED,YANKEE CLIPPER,ZEPLIN EXPRESS
590 DATA AMBER WAVES,BANZAI,CHUG A LUG,DOUBLE CLUTCH,EAGER BEAVER
600 DATA FORMULA ONE,GERONIMO,HEAVEN SENT,INJUN JOE,JUMPING BEAN,KIWI FRUIT
610 DATA LICORISH,MORK FROM ORK,NIGHT DIVER,OBTUSE OLLIE,PUKA,QUENCH IT
620 DATA RATTLER,SUGAR DADDY,TOP SPIN,UKALELE,VITALIS,WET LIPS
630 DATA XAVIER,YAQUI WARRIOR,ZEN BUDDIST
640 DATA AUNT LILLY,BROWN BAG,CHUCK IT,DOOBIE,EMPTY TANK,FLAMEOUT
650 DATA GREY HAVENS,HEAVY TRAFFIC,ICE CUBE,JESTER,KICKOUT,LOCO,MUMBLES
660 DATA NOT TONIGHT,ORNOTHOPTER,POTTY TRAINED,Q-CUMBER,REVVING WILD
670 DATA SLOW SIMMER,T-BIRD,UNICORN,VEGITATE,WIPEOUT
680 DATA XCUSE ME,YELLOW STREAK,ZIG ZAG
690 PRINT
700 PRINT
710 PRINT
720 PRINT "WOULD YOU LIKE INSTRUCTIONS (Y OR N)":
730 INPUT W$
740 if LEFT(W$,1)="n" then 1010
750 IF LEFT(W$,1)="N" THEN 1010
760 PRINT
770 PRINT "     THE DISTANCE OF EACH RACE VARIES FROM .5 TO 1.75 MILES."
780 PRINT "THE HORSES WILL RACE ACROSS YOUR DISPLAY FROM LEFT TO RIGHT."
790 PRINT "EACH RACE CONSISTS OF A VARIABLE NUMBER OF HORSES WITH ODDS"
800 PRINT "RANGING FORM 1:1 TO 50:1 ASSIGNED. HORSES WITH LOWER ODDS"
810 PRINT "CAN BE EXPECTED TO WIN MORE OFTEN THAN HORSES WITH HIGHER"
820 PRINT "ODDS, BUT THE HIGHER THE ODDS, THE MORE MONEY YOU CAN MAKE"
830 PRINT "IF YOUR HORSE SHOULD WIN.  ADDITIONALLY, A RATING IS COMPUTED"
840 PRINT "FOR EACH HORSE BASED ON PAST PERFORMANCE.  A RATING OVER"
850 PRINT "100 IS EXCEPTIONAL.  ONLY BETS BETS ON A HORSE TO WIN (NO SHOW"
860 PRINT "OR PLACE BETS) ARE ALLOWED.  MORE THAN ONE PLAYER MAY"
870 PRINT "BET ON THE SAME HORSE.  THE RESULTS OF THE RACE ALONG WITH"
880 PRINT "YOUR CUMULATIVE WINNINGS OR LOSSES ARE DISPLAYED AFTER EACH"
890 PRINT "RACE.  YOUR CUMULATIVE TOTAL STARTS AT $1000 AND YOU CAN'T"
900 PRINT "CONTINUE PLAYING WITH A NEGATIVE TOTAL.  THE VIGORISH AT THIS"
910 PRINT "TRACK IS 20% PER RACE (REAL LIFE), SO YOU CAN CONSIDER YOURSELF"
920 PRINT "FORTUNATE SHOULD YOU WIN FOR THE DAY."
930 PRINT
940 PRINT "WOULD YOU LIKE TO SEE THE HORSES (Y OR N)":
950 INPUT W1$
960 IF LEFT(W1$,1)='Y' THEN GOSUB 4060
970 IF LEFT(W1$,1)='y' THEN GOSUB 4060
980 PRINT
990 PRINT "GOOD LUCK."
1000 PRINT
1010 PRINT
1020 PRINT
1030 REM THE FOL INITIALIZES THE PLAYER ROUTINE.
1040 PRINT
1050 PRINT "WOULD YOU LIKE TO RUN IN SILENT MODE":
1060 INPUT W1$
1070 IF LEFT(W1$,1)='Y' THEN LET CHARTABLE(7)="!"
1080 PRINT
1090 PRINT "HOW MANY PLAYERS (10 MAX)":
1100 INPUT C1
1110 PRINT
1120 IF C1>10 THEN 1850
1130 FOR K1=1 TO C1
1140 PRINT "ENTER THE NAME OF PLAYER NUMBER":K1:
1150 INPUT C9$
1160 LET C2$(K1)=C9$
1170 NEXT K1
1180 REM THE FOL COUNTS AND DETERMINES THE LENGTHS OF RACES.
1190 PRINT
1200 A3=A3+1
1210 GOSUB 5010
1220 PRINT "RACE NUMBER":A3
1230 PRINT
1240 LET A5=(TIME MOD 6)+1
1250 ON A5 GOTO 1260,1280,1300,1320,1340,1360,1240
1260 LET A4=.5
1270 GOTO 1370
1280 LET A4=.75
1290 GOTO 1370
1300 LET A4=1
1310 GOTO 1370
1320 LET A4=1.25
1330 GOTO 1370
1340 LET A4=1.5
1350 GOTO 1370
1360 LET A4=1.75
1370 PRINT "LENGTH OF THIS RACE IS":A4:" MILES"
1380 PRINT
1390 IF A4<=1 THEN GOTO 1460
1400 PRINT "IS THIS RACE TOO LONG (Y OR N)":
1410 INPUT Q$
1420 LET A1=0
1430 LET A2=0
1440 IF LEFT(Q$,1)="Y" THEN 1810
1450 if LEFT(Q$,1)="y" then 1810
1460 PRINT "HORSE#":TAB(13):"NAME":TAB(25):"ODDS:1":TAB(35):"RACES":TAB(45):"WINS":TAB(55):"RATING"
1470 REM THE FOL COMPUTES ODDS
1480 FOR I=1 TO 50
1490 LET A(I)=(INT((((ABS((94*RND-30)/10))**2)+1)*10))/10
1500 LET B(I)=1/(A(I)+1)
1510 D1(I)=B(I)/1.2
1520 LET A2=A1
1530 LET A1=A1+B(I)
1540 IF A1>1.2 THEN 1620
1550 GOSUB 1890
1560 IF R(I,1)=0 THEN 1590
1570 PRINT I:TAB(7):H4$(I):TAB(26):A(I):TAB(36):R(I,1):TAB(46):R(I,2):TAB(56):R(I,3)
1580 GOTO 3550
1590 PRINT I:TAB(7):H4$(I):TAB(26):A(I):TAB(36):R(I,1):TAB(46):R(I,2):TAB(57):"?"
1600 GOTO 3550
1610 NEXT I
1620 LET L0=1
1630 LET B(I)=1.2-A2
1640 IF B(I)<.02 THEN 1870
1650 LET A(I)=(INT((1/B(I)-1)*10))/10
1660 LET D1(I)=B(I)/1.2
1670 GOSUB 1890
1680 IF R(I,1)=0 THEN 1710
1690 PRINT I:TAB(7):H4$(I):TAB(26):A(I):TAB(36):R(I,1):TAB(46):R(I,2):TAB(56):R(I,3)
1700 GOTO 3550
1710 PRINT I:TAB(7):H4$(I):TAB(26):A(I):TAB(36):R(I,1):TAB(46):R(I,2):TAB(57):"?"
1720 GOTO 3550
1730 LET A2=0
1740 LET A1=0
1750 LET L0=0
1760 FOR T0=1 TO 26
1770 LET H5$(T0)="1"
1780 NEXT T0
1790 PRINT
1800 GOTO 2250
1810 LET A4=A4-.75
1820 PRINT
1830 LET A5=A5-3
1840 GOTO 1370
1850 PRINT "THE MAXIMUM NUMBER OF PLAYERS IS 10"
1860 GOTO 1090
1870 LET B(I)=.02
1880 GOTO 1650
1890 LET N4=(TIME MOD 26)+1
1900 WAIT 1
1910 IF H5$(N4)="0" THEN 1890
1920 LET T2=26*INT(2*RND)
1930 IF A(I)>=10 THEN 2140
1940 IF A(I)>=2 THEN 2050
1950 LET H4$(I)=H1$(N4+T2)
1960 LET R(I,1)=Q(N4+T2,1)
1970 LET R(I,1)=Q(N4+T2,1)
1980 LET R(I,4)=Q(N4+T2,4)
1990 LET R(I,2)=Q(N4+T2,2)
2000 IF R(I,4)=0 THEN 2020
2010 LET R(I,3)=(INT(2000*Q(N4+T2,3)/R(I,4)))/10
2020 LET Q(N4+T2,1)=Q(N4+T2,1)+1
2030 LET Q(N4+T2,4)=Q(N4+T2,4)+(A5+1)*10
2040 GOTO 2220
2050 LET H4$(I)=H2$(N4+T2)
2060 LET R(I,1)=Q(N4+T2+52,1)
2070 LET R(I,4)=Q(N4+T2+52,4)
2080 LET R(I,2)=Q(N4+T2+52,2)
2090 IF R(I,4)=0 THEN GOTO 2110
2100 LET R(I,3)=(INT(2000*Q(N4+T2+52,3)/R(I,4)))/10
2110 LET Q(N4+T2+52,1)=Q(N4+T2+52,1)+1
2120 LET Q(N4+T2+52,4)=Q(N4+T2+52,4)+(A5+1)*10
2130 GOTO 2220
2140 LET H4$(I)=H3$(N4+T2)
2150 LET R(I,1)=Q(N4+T2+104,1)
2160 LET R(I,4)=Q(N4+T2+104,4)
2170 LET R(I,2)=Q(N4+T2+104,2)
2180 IF R(I,4)=0 THEN 2200
2190 LET R(I,3)=(INT(2000*Q(N4+T2+104,3)/R(I,4)))/10
2200 LET Q(N4+T2+104,1)=Q(N4+T2+104,1)+1
2210 LET Q(N4+T2+104,4)=Q(N4+T2+104,4)+(A5+1)*10
2220 LET H5$(N4)="0"
2230 RETURN
2240 REM THE FOL ALLOWS PLAYERS TO BET.
2250 FOR K2=1 TO C1
2260 PRINT C2$(K2):" -- ":
2270 IF D7(K2)<=0 THEN
2280  PRINT "YOU HAVE NO MONEY LEFT."
2290  LET D7(K2)=0
2300  LET C4(K2)=0
2310  LET C5(K2)=0
2320  GOTO 2570
2330 ENDIF
2340 PRINT "WOULD YOU LIKE TO PLACE A BET (Y OR N)":
2350 INPUT C3$
2360 IF LEFT(C3$,1)="N" THEN 2540
2370 if LEFT(C3$,1)="n" then 2540
2380 PRINT "WHICH HORSE (NO. 1 THRU":I:")":
2390 INPUT C4(K2)
2400 IF C4(K2)>I THEN 2670
2410 PRINT "HOW MUCH ($":
2420 IF D7(K2)<1000 THEN PRINT D7(K2): ELSE PRINT "1000":
2430 PRINT " LIMIT)":
2440 INPUT T3
2450 LET C5(K2)=ABS(T3)
2460 PRINT
2470 IF C5(K2)>D7(K2) THEN
2480  PRINT "YOU ONLY HAVE $":D7(K2):" TO BET, TRY AGAIN."
2490  PRINT
2500  GOTO 2410
2510 ENDIF
2520 IF C5(K2)>1000 THEN GOTO 2690
2530 GOTO 2570
2540 LET C4(K2)=0
2550 LET C5(K2)=0
2560 PRINT
2570 NEXT K2
2580 PRINT
2590 PRINT "BETTING SUMMARY"
2600 PRINT
2610 PRINT "PLAYER","$ BET","HORSE #","NAME","ODDS:1"
2620 FOR K3=1 TO C1
2630 LET C6=C4(K3)
2640 PRINT C2$(K3),C5(K3),C4(K3),H4$(C6),A(C6)
2650 NEXT K3
2660 GOTO 2720
2670 PRINT "MAXIMUM NO. FOR THIS RACE IS":I
2680 GOTO 2380
2690 PRINT "$1000 IS THE MAXIMUM ALLOWABLE BET"
2700 GOTO 2410
2710 REM THE FOLLOWING SIMULATES THE RACE.
2720 PRINT
2730 PRINT "THE HORSES ARE IN THE STARTING GATE."
2740 PRINT ".........."
2750 FOR NUM=1 TO I
2760 PRINT H4$(NUM)
2770 NEXT NUM
2780 PRINT ".........."
2790 PRINT
2800 PRINT ""
2810 PRINT "AND THEY'RE OFF!!!!!"
2820 FOR I2=1 TO 20
2830 PRINT "       ":
2840 PRINT CHAR(7):
2850 NEXT I2
2860 LET D2=2.65-(A5*.15)
2870 ON A5 GOTO 2880,2900,2920,2940,2960,2980,2980
2880 PRINT ".........0.25.......!"
2890 GOTO 2990
2900 PRINT ".........0.25......0.5........!"
2910 GOTO 2990
2920 PRINT ".........0.25......0.5.......0.75.......!"
2930 GOTO 2990
2940 PRINT ".........0.25......0.5........0.75......1.0........!"
2950 GOTO 2990
2960 PRINT ".........0.25......0.5........0.75......1.0.......1.25.......!"
2970 GOTO 2990
2980 PRINT ".........0.25......0.5........0.75......1.0.......1.25......1.5........!"
2990 FOR J=1 TO I
3000 LET D(J)=D(J)+(RND*((D1(J)*D2)+(4-(.4*D2))))
3010 IF D(J)>(A5+1)*5 THEN 3200
3020 IF D8=1 THEN 3140
3030 PRINT TAB(D(J)*2):LEFT(H4$(J),1)
3040 IF D(J)>7 THEN 3120
3050 LET D9=0
3060 NEXT J
3070 IF D4>1 THEN 3240
3080 IF D4>0 THEN 3280
3090 IF D9=I THEN 3160
3100 LET D9=0
3110 GOTO 2870
3120 LET D9=D9+1
3130 GOTO 3060
3140 PRINT H4$(J):TAB(D(J)*2):LEFT(H4$(J),1)
3150 GOTO 3060
3160 LET D8=1
3170 GOTO 3110
3180 PRINT H4$(J):TAB((A5+1)*10+1):"":H4$(J):CHAR(7)
3190 GOTO 3060
3200 LET D4=D4+1
3210 IF D8=1 THEN 3180
3220 PRINT TAB((A5+1)*10+1):"":H4$(J):CHAR(7)
3230 GOTO 3060
3240 PRINT
3250 PRINT TAB(10):"PHOTO FINISH!!!":TAB(70):" "
3260 PRINT TAB(70):" "
3270 PRINT TAB(70):" "
3280 FOR J1=1 TO I
3290 GOTO 3780
3300 IF D(J1)>D5 THEN 3490
3310 NEXT J1
3320 PRINT "--------------------------"
3330 PRINT "AND THE WINNER IS                      ":H4$(D6):CHAR(7)
3340 GOTO 3620
3350 PRINT "HORSE NO.":D6
3360 PRINT "AND PAYS":A(D6):" : 1"
3370 FOR L2=2 TO 40
3380 IF A(D6)>=L2 THEN 3410
3390 LET L3(L2)=L3(L2)+1
3400 GOTO 3420
3410 NEXT L2
3420 PRINT
3430 FOR J2=1 TO C1
3440 IF C4(J2)=D6 THEN 3520
3450 LET D7(J2)=D7(J2)-C5(J2)
3460 PRINT C2$(J2):" LOST $":C5(J2):" AND HAS $":INT(D7(J2)):" LEFT"
3470 NEXT J2
3480 GOTO 4410
3490 LET D5=D(J1)
3500 LET D6=J1
3510 GOTO 3310
3520 LET D7(J2)=D7(J2)+(C5(J2)*A(D6))
3530 PRINT C2$(J2):" WON $":C5(J2)*A(D6):" AND HAS $":INT(D7(J2)):" LEFT"
3540 GOTO 3470
3550 FOR L1=2 TO 50
3560 IF A(I)>=L1 THEN 3590
3570 LET L(L1)=L(L1)+1
3580 GOTO 3600
3590 NEXT L1
3600 IF L0=0 THEN 1610
3610 GOTO 1730
3620 FOR Q1=1 TO 52
3630 IF H1$(Q1)=H4$(D6) THEN 3720
3640 NEXT Q1
3650 FOR Q2=1 TO 52
3660 IF H2$(Q2)=H4$(D6) THEN 3740
3670 NEXT Q2
3680 FOR Q3=1 TO 52
3690 IF H3$(Q3)=H4$(D6) THEN 3760
3700 NEXT Q3
3710 PRINT "ERROR DOZO"
3720 LET Q(Q1,2)=Q(Q1,2)+1
3730 GOTO 3350
3740 LET Q(Q2+52,2)=Q(Q2+52,2)+1
3750 GOTO 3350
3760 LET Q(Q3+104,2)=Q(Q3+104,2)+1
3770 GOTO 3350
3780 FOR Q4=1 TO 52
3790 IF H1$(Q4)=H4$(J1) THEN 3880
3800 NEXT Q4
3810 FOR Q5=1 TO 52
3820 IF H2$(Q5)=H4$(J1) THEN 3900
3830 NEXT Q5
3840 FOR Q6=1 TO 52
3850 IF H3$(Q6)=H4$(J1) THEN 3920
3860 NEXT Q6
3870 PRINT "ERROR DOZO"
3880 LET Q(Q4,3)=Q(Q4,3)+D(J1)
3890 GOTO 3300
3900 LET Q(Q5+52,3)=Q(Q5+52,3)+D(J1)
3910 GOTO 3300
3920 LET Q(Q6+104,3)=Q(Q6+104,3)+D(J1)
3930 GOTO 3300
3940 OPEN "(GAMES)DERBY.DAT-A", INPUT, 1, ERR 290
3950 FOR P6=1 TO 156
3960 INPUT FROM 1: Q(P6,1),Q(P6,2),Q(P6,3),Q(P6,4)
3970 NEXT P6
3980 CLOSE 1
3990 GOTO 290
4000 OPEN "(GAMES)DERBY.DAT-A", OUTPUT, 1
4010 FOR P8=1 TO 156
4020 PRINT ON 1: Q(P8,1):",":Q(P8,2):",":Q(P8,3):",":Q(P8,4)
4030 NEXT P8
4040 CLOSE 1
4050 GOTO 4950
4060 PRINT
4070 PRINT TAB(20):"DAILY RACING FORM"
4080 PRINT
4090 PRINT
4100 PRINT "HORSE","ENTRIES","WINS","% WON","RATING"
4110 FOR P3=1 TO 26
4120 FOR P2=1 TO 27 BY 26
4130 LET P0=P2-1+P3
4140 IF Q(P0,1)=0 THEN 4350
4150 LET S1=(INT(1000*Q(P0,2)/Q(P0,1)))/10
4160 LET S2=(INT(2000*Q(P0,3)/Q(P0,4)))/10
4170 PRINT H1$(P0),Q(P0,1),Q(P0,2),S1,S2
4180 NEXT P2
4190 FOR P4=1 TO 27 BY 26
4200 LET P0=P4-1+P3
4210 IF Q(P0+52,1)=0 THEN 4370
4220 LET S1=(INT(1000*Q(P0+52,2)/Q(P0+52,1)))/10
4230 LET S2=(INT(2000*Q(P0+52,3)/Q(P0+52,4)))/10
4240 PRINT H2$(P0),Q(P0+52,1),Q(P0+52,2),S1,S2
4250 NEXT P4
4260 FOR P5=1 TO 27 BY 26
4270 LET P0=P5-1+P3
4280 IF Q(P0+104,1)=0 THEN 4390
4290 LET S1=(INT(1000*Q(P0+104,2)/Q(P0+104,1)))/10
4300 LET S2=(INT(2000*Q(P0+104,3)/Q(P0+104,4)))/10
4310 PRINT H3$(P0),Q(P0+104,1),Q(P0+104,2),S1,S2
4320 NEXT P5
4330 NEXT P3
4340 RETURN
4350 PRINT H1$(P0),Q(P0,1),Q(P0,2),0.0,"?"
4360 GOTO 4180
4370 PRINT H2$(P0),Q(P0+52,1),Q(P0+52,2),0.0,"?"
4380 GOTO 4250
4390 PRINT H3$(P0),Q(P0+104,1),Q(P0+104,2),0.0,"?"
4400 GOTO 4320
4410 FOR J3=1 TO I
4420 LET D(J3)=0
4430 NEXT J3
4440 LET D4=0
4450 LET D5=0
4460 LET D6=0
4470 LET D8=0
4480 LET D9=0
4490 PRINT
4500 IF C1=0 THEN 4550
4510 FOR K2=1 TO C1
4520 IF D7(K2)>0 THEN 4550
4530 NEXT K2
4540 GOTO 4960
4550 PRINT "WOULD YOU LIKE ANOTHER RACE (Y OR N)":
4560 INPUT Z1$
4570 IF LEFT(Z1$,1)='F' THEN
4580  GOSUB 4060
4590  GOTO 4490
4600 ENDIF
4610 PRINT
4620 if LEFT(Z1$,1)="N" then 4660
4630 if LEFT(Z1$,1)="n" then  4660
4640 PRINT
4650 GOTO 1200
4660 PRINT
4670 PRINT "WOULD YOU LIKE TO SEE THE DAY'S RACING STATISTICS (Y OR N)":
4680 INPUT Z5$
4690 IF LEFT(Z5$,1)="N" THEN 4940
4700 if LEFT(Z5$,1)="n" then 4940
4710 PRINT
4720 PRINT "     ODDS",,"ENTRIES","WINS"
4730 LET L7=1
4740 FOR L4=2 TO 5
4750 PRINT L7:":1 TO":(INT((L4-.01)*10))/10:":1",,L(L4),L3(L4)
4760 LET L7=L4
4770 NEXT L4
4780 FOR L5=6 TO 10
4790 LET L6=L6+L(L5)
4800 LET L8=L8+L3(L5)
4810 NEXT L5
4820 PRINT L7:":1 TO":(INT((L5-.01)*10))/10:":1",,L6,L8
4830 LET L7=L5
4840 FOR M1=1 TO 3
4850 FOR M2=(M1*10+1) TO (M1*10+10)
4860 LET M3=M3+L(M2)
4870 LET M4=M4+L3(M2)
4880 NEXT M2
4890 PRINT L7:":1 TO":(INT((((M1+1)*10)-.01)*10))/10:":1",M3,M4
4900 LET L7=(M1+1)*10
4910 LET M3=0
4920 LET M4=0
4930 NEXT M1
4940 GOTO 4000
4950 END
4960 PRINT
4970 PRINT "NO ONE HAS ANY MONEY LEFT, I GUESS THE SHOW IS OVER!"
4980 PRINT
4990 LET Z1$="N"
5000 GOTO 4620
5010 FOR NUM1=1 TO C1
5020 IF D7(NUM1)=0 THEN
5030  FOR NUM2=1 TO C1
5040  IF C2$(NUM2)=C2$(NUM1) THEN 5200
5050  IF D7(NUM2)=0 THEN 5200
5060  PRINT
5070  PRINT C2$(NUM2):", WOULD YOU LIKE TO LOAN ":C2$(NUM1):" SOME MONEY":
5080  INPUT Z1$
5090  IF LEFT(Z1$,1)="Y" THEN
5100   PRINT "HOW MUCH":
5110   INPUT NUM3
5120   IF NUM3>D7(NUM2) THEN
5130    PRINT "YOU ONLY HAVE $":D7(NUM2)
5140    GOTO 5100
5150    ENDIF
5160   LET D7(NUM1)=D7(NUM1)+NUM3
5170   LET D7(NUM2)=D7(NUM2)-NUM3
5180   GOTO 5220
5190   ENDIF
5200  NEXT NUM2
5210  ENDIF
5220 NEXT NUM1
5230 RETURN
     