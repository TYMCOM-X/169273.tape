        TITLE   EXP             Dec 13, 1980

LASTDT=:^D5477			;through Dec 31 1986, after which there
				;may be date problems
EXP::   PUSHJ   P,EXA
        JRST    CDISP##

EXA:    SETOM 	T3#
        SKIPN 	LIST
EXER:   ERROR   [ASCIZ "NO REQUEST HAS BEEN ENTERED"]
        SKIPLE  SDATE
	PUSHJ   P,EX1
	MOVEI 	R1,LIST
	MOVEM 	R1,LP#
EX2: 	MOVE 	R1,@LP
	JUMPE 	R1,EXD3A
	LDB 	R3,[POINT 3,R1,14]
	CAIE 	R3,1
	JRST 	EXD
        TDNE    R1,[17777]
        JRST 	EXFD		;FOUND A DATE
EX3:    TDNE    R1,[2B17]
	JRST 	EXFS
        TDNE    R1,[1B17]
	JRST 	EXFL
        TYPE    [ASCIZ 'THE EARLIEST OF: ']
EXN: 	AOS 	LP
	JRST 	EX2
EX1:    TYPE [ASCIZ '$STARTING ']
        MOVEI 	R1,SDATE
        MOVEM   R1,LP
        PUSHJ   P,PDATE
        POPJ    P,
EXFL: 	TYPE	[ASCIZ 'THE LATEST OF:']
	JRST 	EXN
EXFS:   TDNE    R1,[1B17]
        JRST 	EXFA
	TYPE 	[ASCIZ "DO IN ORDER: (ONCE ONLY)"]
	JRST 	EXN
EXFA: 	TYPE 	[ASCIZ 'EXCEPT THE FOLLOWING ']
	JRST 	EXN
EXFD:   TDNE    R1,[4B17]
        JRST 	EXFD2
	MOVEM 	R1,T3
	TYPE 	[ASCIZ 'USING THE REFERENCE DATE OF ']
EXFD1:  PUSHJ   P,PDATE
	JRST 	EXN
EXFD2:  TDNE    R1,[1B17]
	JRST 	EXFST
EXFU: 	TYPE 	[ASCIZ "UNTIL "]
        PUSHJ   P,UPDATE
	MOVE 	R1,@LP
	JRST 	EX3
UPDATE: MOVE 	R1,@LP
	AOS 	R1
        JRST    PDATE+1
EXFST: 	TYPE 	[ASCIZ 'STARTING ']
	JRST 	EXFD1
EXD: 	CAIE 	R3,3
	JRST 	EXDN
EXD00:	MOVE 	R1,@LP
	TDNE 	R1,[7]
	JRST 	EXDW
	TDNE 	R1,[7770]
	JRST 	EXDY
EXD0: 	LSH 	R1,-14
	ANDI 	R1,17
	JUMPLE 	R1,EXDER
	MOVE 	R1,@LP
        AND 	R1,[3B17]
	CAMG 	R1,[2B17]
	SKIPG 	R1
	JRST 	EXD01
	TYPE 	[ASCIZ 'THE ']
	MOVE 	R1,@LP
	LSH 	R1,-14
	ANDI 	R1,17
	CAIE 	R1,1
	JRST 	EXD02
EXD03: 	TYPE 	[ASCIZ 'MONTH']
	JRST 	EXD04
EXD02: 	JRST 	EXNM
	JRST 	EXD03
EXD01: 	TYPE 	[ASCIZ 'EVERY ']
	MOVE 	R1,@LP
	LSH 	R1,-14
	ANDI 	R1,17
	CAIG 	R1,1
	JRST 	EXD03
        PUSHJ   P,PDNO
	TYPE 	[ASCIZ ' MONTHS']
EXD04: 	MOVE 	R1,@LP
	LDB 	R3,[POINT 2,R1,17]
	CAIE 	R3,3
	JRST 	EXD1A
	JRST 	EXD3B
EXD1: 	MOVE 	R1,@LP
EXD1A: 	TDNE 	R1,[2B20]
	JRST 	EXD2
EXD3: 	TDNE 	R1,[3B17]
	JRST 	EXD4
EXD3B: 	AOS 	LP
	MOVE 	R1,@LP
        JUMPN   R1,EXD5
EXD3A: 	TCO 	[CRCHR]
EXEX: 	POPJ 	P,
EXD5: 	TYPE 	[ASCIZ '$OR ']
	JRST 	EX2
EXD4: 	TDNE 	R1,[2B17]
	JRST 	EXD7
EXD6: 	TYPE 	[ASCIZ '$AFTER ']
	JRST 	EXN
EXD7:   TYPE    [ASCIZ '$BEFORE ']
	JRST 	EXN
EXD2: 	TYPE 	[ASCIZ '(ONCE ONLY)']
	JRST 	EXD3B
EXDER: 	ERROR 	[ASCIZ 'NO DAY OR MONTH INDICATED$']
EXDW: 	TDNE 	R1,[7770]
	JRST 	EXDW1
	ERROR 	[ASCIZ 'DAY OF WEEK WITH NO COUNT']
EXDW1:  PUSHJ   P,EXNMS
	MOVE 	R1,@LP
	ANDI 	R1,7
	MOVE 	R1,DWKM(R1)
        PUSHJ   P,PMSG
EXDW2: 	MOVE 	R1,@LP
	TDNE 	R1,[17B23]
	JRST 	EXDW2B
	JRST 	EXD1
EXDW2B:	TDNE 	R1,[4B17]
	JRST 	EXDW3
        TYPE    [ASCIZ 'OF ']
EXDW2A: MOVE 	R1,@LP
	JRST 	EXD0
EXDW3:  TYPE 	[ASCIZ 'BEFORE THE END OF ']
	JRST 	EXDW2A
EXDY: 	PUSHJ 	P,EXNMS
	TYPE 	[ASCIZ 'DAY ']
	JRST 	EXDW2
EXDT2: 	PUSHJ 	P,PDATE
EXDT2A: AOS 	LP
	MOVE 	R1,@LP
	JUMPE 	R1,EXD3A
	JRST 	EX2
EXDN: 	CAIN 	R3,2
	JRST 	EXDT2
        TYPE    [ASCIZ ' INVALID DATE ']
	JRST 	EXDT2A
EXNM: 	CAIE 	R1,1
	JRST 	EXNM1
	TYPE 	[ASCIZ 'FIRST ']
EXNM9: 	POPJ 	P,
EXNM1: 	CAIE 	R1,2
	JRST 	EXNM2
	TYPE 	[ASCIZ '2ND ']
	POPJ 	P,
EXNM2: 	CAIE 	R1,3
	JRST 	EXNM3
	TYPE 	[ASCIZ '3RD ']
	POPJ 	P,
EXNM3: 	PUSHJ 	P,PDNO
	TYPE 	[ASCIZ 'TH ']
	POPJ 	P,

EXNMS: 	LDB 	R3,[POINT 2,R1,17]
	CAIN 	2,3
	JRST 	EXNMS0
	TDNE 	R1,[2B20]
	POPJ 	P,
	TDNE 	R1,[17B23]
	JRST 	EXNMS1
EXNMS0: TYPE 	[ASCIZ 'EVERY ']
	MOVE 	R1,@LP
	LSH 	R1,-3
	ANDI 	R1,777
	CAIG 	R1,1
	POPJ 	P,
EXNMS2: PUSHJ 	P,EXNM
	POPJ 	P,
EXNMS1: SKIPL 	T3
	JRST 	EXNMS4
	TDNE 	R1,[4B20]
	JRST 	EXNMS4
EXNMS3: TYPE 	[ASCIZ 'THE ']
	MOVE 	R1,@LP
	LSH 	R1,-3
	ANDI 	R1,777
	JRST 	EXNMS2
EXNMS4: TDNE 	R1,[17B23]
	JRST 	EXNMS3
	JRST 	EXNMS0

PDATE: 	MOVE 	R1,@LP
	MOVEM 	R1,T1#
	LSH 	R1,-5
	ANDI 	R1,17
	MOVE 	R1,MONM(R1)
	PUSHJ 	P,PMSG
	MOVE 	R1,T1
	ANDI 	R1,37
	PUSHJ 	P,PDNO
	TCO 	[COMCHR]
        TCO 	[SPACE]
	TYPE 	[ASCIZ '19']
	MOVE 	R1,T1
	LSH 	R1,-9
	ANDI 	R1,17
	CAIE 	R1,17
	SUBI 	R1,^D99-17-^D72
        ADDI    R1,^D99-17
	PUSHJ 	P,PDNO
	MOVE 	R3,LP
	SKIPE 	1(R3)
	TCO 	[COMCHR]
	TCO 	[CRCHR]
	POPJ 	P,

DWKM: 	Z
        DWM
	DWM1
	DWM2
	DWM3
	DWM4
	DWM5
	DWM6
DWM:    ASCIZ   'SUNDAY    '
DWM1:   ASCIZ   'MONDAY    '
DWM2:   ASCIZ   'TUESDAY   '
DWM3:   ASCIZ   'WEDNESDAY '
DWM4:   ASCIZ   'THURSDAY  '
DWM5:   ASCIZ   'FRIDAY    '
DWM6:   ASCIZ   'SATURDAY  '
MONM:   MM1
        MM2
        MM3
        MM4
        MM5
        MM6
        MM7
        MM8
        MM9
        MM10
        MM11
        MM12
MM1:    ASCIZ   'JAN '
MM2:    ASCIZ   'FEB '
MM3:    ASCIZ   'MAR '
MM4:    ASCIZ   'APR '
MM5:    ASCIZ   'MAY '
MM6:    ASCIZ   'JUN '
MM7:    ASCIZ   'JUL '
MM8:    ASCIZ   'AUG '
MM9:    ASCIZ   'SEP '
MM10:   ASCIZ   'OCT '
MM11:   ASCIZ   'NOV '
MM12:   ASCIZ   'DEC '
DIS::   JSR     DS
        JRST    CDISP
TEST::  PUSHJ   P,EXA
        JRST    DIS

;DISPLAY ALL DATES
DS:     Z
        SKIPN   LIST
        JRST    EXER            ;N LIST EXISTS
        JSR     DSX
        TCO     [CRCHR]
        JRST    DSST

DSX::   Z
        PUSHJ   P,GCRDT
        MOVE    R1,SDATE
        TDNE    R1,[4B13]
        JRST    .+3
        PUSHJ   P,DATCV
        SKIPG   R1
        MOVE    R1,T3
        MOVEM   R1,CURDT#
        MOVE    R3,[-100]
        MOVE    R1,LIST+100(R3)
        MOVEM   R1,TLIST+100(R3)
        JUMPE   R1,.+2
        AOJL    R3,.-3
        JRST    @DSX

DSST:   JSR     DS0
DSST1:  CAMG    R1,[LASTDT]
        SKIPG   R1
        JRST    DSER
        MOVEM   R1,CURDT
        PUSHJ   P,PBNDT         ;PRINT DATE IN R1
        TCO     [CRCHR]
        JRST    DSST

DS0::   Z
        MOVEI   R1,TLIST
        MOVEM   R1,LP
	setz	r1,
	movem	r1,tlp#
	movem	r1,xlp#
DS1:    SETO    R1,
        MOVEM   R1,REFDT#
        MOVEM   R1,UNTDT#
        MOVEM   R1,XDATE#
        MOVEM   R1,LATSW#
        MOVEM   R1,ORDSW#
        MOVEM   R1,GRPDT#
        MOVEM   R1,TCRDT#
DS2:    MOVE    R1,@LP
        LDB     R3,[POINT 3,R1,14]
        CAIE    R3,1
        JRST    DSD
        JSR     DSF
        JRST    DS2

;FLAG WORD
DSF:    Z
        TDNE    R1,[17777]
        JRST    DSFD            ;HAVE A DATE
DS3:    TDNE    R1,[2B17]
        JRST    DSFS
        TDNE    R1,[1B17]
        JRST    DSFL
        SETO    R1,
        JRST    DSFL
DSN:    AOS     LP
        JRST    @DSF
DSFL:   MOVEM   R1,LATSW
        SETO    R1,
        MOVEM   R1,ORDSW
        JRST    DSN
DSFS:   TDNE    R1,[1B17]
        JRST    DSFA
        MOVEM   R1,ORDSW
        JRST    DSN
DSFA:   ERROR   [ASCIZ 'EXCEPT USED WITHOUT A LIST OF DATES$']

;PROCESS A FLAG WORD WITH DATE
DSFD:   TDNE    R1,[4B17]
        JRST    DSFD2           ;UNLESS
        PUSHJ   P,DATCV
        MOVEM   R1,REFDT
        MOVEM   R1,TSTDT#
DSFD1:  MOVE    R1,@LP
        JRST    DS3
DSFD2:  TDNE    R1,[1B17]
        JRST    DSFST
DSFU:   CAME    R1,[14017000]
        JRST    .+3
        SETO    R1,
        JRST    .+2
        PUSHJ   P,DATCV
        MOVEM   R1,UNTDT
        MOVEM   R1,TUNDT#
        JRST    DSN
DSFST:  PUSHJ   P,DATCV
        CAMG    R1,CURDT
        JRST    DSN
        MOVEM   R1,TCRDT
        JRST    DSN

;*PROCESS DATE LIST
DSD:    MOVE    R1,LP
        MOVEM   R1,BGLIS#
DSD0A:  SETO    R1,
        MOVEM   R1,XDATE
        MOVEM   R1,EXCSW#
DSDS:   JSR     DSGD
        JRST    DSDEN           ;END OF LIST
        SKIPL   ORDSW
        JRST    DSDS2
        JSR     DSDT
        JRST    DSDS
        JRST    DSDS
DSDS2:  MOVE    R3,LP
        SETO    R2,
        MOVEM   R2,-1(R3)
        JSR     DSDT
        JRST    DSDS
        JRST    DSDEN

;*ERROR RETURN IF FOUND NO VALID DATE
DSDT:   Z
        SKIPL   TUNDT
        CAMG 	R1,TUNDT
        JRST    .+2
        JRST    @DSDT
        CAMG    R1,TCRDT
        JRST    @DSDT
        CAMG    R1,CURDT
        JRST    @DSDT
        SKIPL   XDATE
        JRST    DSDT2
DSDT1:  MOVEM   R1,XDATE
        MOVE    R1,TLP
        MOVEM   R1,XLP
        AOS     DSDT
        JRST    @DSDT
DSDT2:  SKIPL   LATSW
        JRST    .+4
        CAMG    R1,XDATE
        JRST    DSDT1
        JRST    @DSDT
        CAMG    R1,XDATE
        JRST    @DSDT
        JRST    DSDT1

;END OF LIST
DSDEN:  MOVE    R1,XDATE
        JUMPLE  R1,DSDEN1
        MOVEM   R1,CURDT
        SKIPL   EXCSW
        JRST    DSDEN5
        MOVE    R1,@XLP
        TDNN    R1,[2B20]
        JRST    .+3
        SETO    R1,
        MOVEM   R1,@XLP
        MOVE    R1,XDATE
        JRST    @DS0

;* PROCESS 'EXCEPT'
DSDEN5: SOSL    CURDT
DSDEN2: JSR     DSGD
        JRST    DSEXN
        SUBI    R1,1
        CAME    R1,CURDT
        JRST    DSDEN2
        AOS     CURDT
        MOVE    R1,BGLIS
        MOVEM   R1,LP
        JRST    DSD0A
DSEXN:  AOS     CURDT
        MOVE    R1,CURDT
        JRST    @DS0
DSDEN1: SKIPL   TCRDT
        JRST    DSDEN4
DSDEN3: SETZ    R1,
        SKIPL   EXCSW
        JRST    @DS0
        SKIPE   @LP
        MOVE    R1,@LP
        SKIPE   @LP
        JRST    DS1
        JRST    @DS0
DSDEN4: MOVE    R1,TCRDT
        CAMG    R1,CURDT
        JRST    DSDEN3
        MOVEM   R1,CURDT
        JRST    DS0+1

;GETS A DATE FROM A GROUP
DSGD:   Z
DSGD2:  MOVE    R1,LP
        MOVEM   R1,BGGRP#
        MOVE    R1,REFDT
        SKIPG   REFDT
        MOVE    R1,CURDT
        MOVEM   R1,TSTDT
        MOVE    R1,UNTDT
        MOVEM   R1,TUNDT
        SETO    R1,
        MOVEM   R1,GRPDT
        MOVEM   R1,AFTSW
        MOVEM   R1,AFTDT#
        MOVEM   R1,BEFDT#
        MOVEM   R1,ASW#
        MOVEM   R1,BSW#
        MOVE    R1,@LP
DSGD2A: CAME    R1,[-1]
        JRST    .+3
DSGDN:  AOS     LP
        JRST    DSGD2
        JUMPE   R1,@DSGD
        LDB     R3,[POINT 3,R1,14]
        CAIE    R3,1
        JRST    DSGD3
        LDB     R3,[POINT 3,R1,17]
        CAIE    R3,3
        JRST    DSGF
        AOS     LP
        AOS     EXCSW
        JRST    @DSGD
DSGF:   JSR     DSF
        JRST    DSGD2
DSGD3:  SKIPL   TUNDT
        JRST    D14
        MOVE    R1,CURDT
        CAMLE   R1,[LASTDT]
        JRST    DSGDN
        MOVE    R1,@LP
DSGD3A: CAIE    R3,2
        JRST    DSGD4
;TYPE DATE WORD
        PUSHJ   P,DATCV
        CAMG    R1,CURDT
        JRST    DSGDN
DEX:    AOS     LP
        AOS     DSGD
        JRST    @DSGD
DSGD4:  CAIE    R3,3
        JRST    ER1
        AND     R1,[3B17]
        CAMG    R1,[2B17]
        SKIPG   R1
        JRST    D2
        AOS     AFTSW#
D1:     AOS     LP
        MOVE    R1,@LP
        SKIPG   R1
        JRST    DSGD2A
        LDB     R3,[POINT 3,R1,14]
        CAIN    R3,1
        JRST    ER2
        CAIE    R3,2
        JRST    D3
;PROCESS DATE WORD
        SKIPN   AFTSW
        JRST    D7              ;FOR SOME AFTER DATE
D11:    TDNE    R1,[4B17]
        JRST    D8              ;FOR UNTIL
        PUSHJ   P,DATCV         ;CONVERT THE DATE
        MOVEM   R1,TSTDT
D12:    MOVE    R1,@LP
        TDNE    R1,[4B20]
        JRST    D9              ;DATE CONTINUES
        MOVE    R1,LP
        MOVEM   R1,ENGRP#
;SEARCH BACK FOR LAST FOUND DAY WORD
D10:    SOS     LP
        MOVE    R1,@LP
        LDB     R3,[POINT 3,R1,14]
        CAIE    R3,3
        JRST    D10
        JRST    .+2
        SOS     LP
        JSR     GTD
        MOVE    R1,LP
        CAME    R1,BGGRP
        JRST    .-4
        MOVE    R1,ENGRP
        MOVEM   R1,LP
        JRST    DJ4

;FOR DAT AFTER 'AFTER'

D7:     TDNE    R1,[4B17]
        JRST    ER3             ;CANT HAVE UNTIL
        PUSHJ   P,DATCV
        MOVEM   R1,GRPDT
        JRST    D12

;POSSIBLE CONTINUATION AFTER DATE

D9:     AOS     LP
        MOVE    R1,@LP
        LDB     R3,[POINT 3,R1,14]
        CAIE    R3,2
        JRST    ER3             ;TROUBLE
        JRST    D11
D3:     CAIE    R3,3
        JRST    ER1             ;NOT A VALID WORD

;DAY WORD AFTER CONTINUATION

        TDNE    R1,[4B20]
        JRST    D4              ;CONTINUES WITH AN AND
        AND     R1,[3B17]
        CAMG    R1,[2B17]
        SKIPG   R1
        JRST    D5              ;NO MORE CONTINUE

;AFTER-BEFORE CONTINUE

        AOS     AFTSW
        JRST    D1
D5:     SETO    R1,
        MOVEM   AFTSW
        MOVE    R1,LP
        MOVEM   R1,ENGRP
        JSR     GTD
D6:     SOS     LP
D6A:    JSR     GTD
        MOVE    R1,LP
        CAME    R1,BGGRP
        JRST    D6
        MOVE    R1,ENGRP
        MOVEM   R1,LP
        JSR     DJ
        JRST    D6A

D8:     PUSHJ   P,DATCV
        SKIPL   TUNDT
        CAMG    TUNDT
        MOVEM   R1,TUNDT
        JRST    D12
D4:     SETO    R1,
        MOVEM   R1,AFTSW
        JRST    D1

D2:     JSR     GTD
        JSR     DJ
        JRST    D2

D14:    MOVE    R1,CURDT
        CAMLE   R1,TUNDT
        JRST    DSGDN           ;CUR PAST UNTIL
        MOVE    R1,@LP

       JRST    DSGD3A

;
;DATE ADJUST
;
DJ:     Z
        MOVE    R1,GRPDT
        CAMG    R1,CURDT
        JRST    DJ1
        SKIPL   BEFDT
        JRST    DJ4
        MOVEM   R1,AFTDT
        AOS     BSW             ;FORCE BACKUP
DJ2:    SETO    R1,
        MOVEM   R1,GRPDT
        JRST    @DJ
DJ1:    SKIPL   AFTDT
        JRST    DJ3
        MOVEM   R1,BEFDT
        AOS     ASW
        JRST    DJ2
DJ3:    MOVE    R1,AFTDT
        MOVEM   R1,GRPDT
DJ4:    SETO    R2,
        MOVE    R1,@LP
        TDNE    R1,[2B20]
        JRST    DJ5
        MOVE    R1,GRPDT
        JRST    DEX
DJ5:    MOVE    R1,LP
        MOVEM   R1,TLP
        MOVE    R1,GRPDT
        JRST    DEX

DSER:   TYPE    [ASCIZ '$NO FURTHER DATES']
        JRST    @DS
ER1:    ERROR   [ASCIZ '$INVALID WORD OR GARBAGE FOUND IN LIST']
ER2:    ERROR   [ASCIZ '$CONTINUATION WITH AND-AFTER-BEFORE DOES NOT END']
ER3:    ERROR   [ASCIZ '$CONTINUATION FROM DATE WORD WITH NO DATE WORD AFTER']

;GETS DATE FOR DAY WORD SETS GRPDT AND RETURNS IN R1
GTD:    Z
        MOVE    R1,@LP
        LDB     R3,[POINT 3,R1,14]
        CAIE    R3,3
        JSR     HLTER           ;MUST BE A DAY WORD
        MOVEM   R1,ABSW#
        TDNE    R1,[3B17]
        JRST    G1
G01:    SKIPL   GRPDT
        JSR     HLTER           ;CANT ALREADY HAVE DATE W.O BEF-AFT
        OR      R1,[1B17]
        MOVEM   R1,ABSW         ;FORCE AFTER
        TDNE    R1,[170000]
        JRST    G12             ;HAVE MONTH
;NON BEFORE AFTER LOGIC
G03:    MOVE    R1,ABSW
        TDNE    R1,[7]
        JRST    G4              ;HAVE DAY OF THE WEEK
        TDNN    R1,[7770]
        JSR     HLTER           ;ERROR
;PROCESS NUMBER OF DAY CYCLE
        LSH     R1,-3
        AND     R1,[777]
        MOVEM   R1,CYCLE#
        MOVE    R1,TSTDT
        CAME    R1,CURDT
        JRST    .+3
        MOVE    R1,CYCLE
        JRST    G04A
        IDIV    R1,CYCLE
        MOVEM   R2,RMNDR#
G04:    MOVE    R1,CURDT
        IDIV    R1,CYCLE
        MOVN    R1,R2
        ADD     R1,RMNDR
        MOVE    R2,ABSW
        TDNE    R2,[2B17]
        MOVNS   R1
        CAIG    R1,0
        ADD     R1,CYCLE
        TDNE    R2,[2B17]
        MOVNS   R1
G04A:   ADD     R1,CURDT
        SKIPL   ASW
        JRST    G06
        SKIPL   BSW
        JRST    G07
G05:    MOVEM   R1,GRPDT
        JRST    @GTD
G06:    MOVE    R2,ASW
        MOVEM   R2,T2#
        ADD     R1,CYCLE
        SOSL    T2
        JRST    .-2
        JRST    G05
G07:    MOVE    R2,BSW
        MOVEM   R2,T2
        SUB     R1,CYCLE
        SOSL    T2
        JRST    .-2
        JRST    G05

;POSSIBLE AFTER/BEFORE FLAGS
G1:     LDB     R3,[POINT 2,R1,17]
        CAIN    R3,3
        JSR     HLTER
;       START BEFORE AFTER PROCESSING
;
G1B:    TDNE    R1,[170000]
        JRST    G2              ;HAVE MONTH
G1A:    TDNE    R1,[7]
        JRST    G3              ;HAVE DAY OF THE WEEK
        TDNN    R1,[7770]
        JSR     HLTER
        LSH     R1,-3
        AND     R1,[777]
        MOVEM   R1,CYCLE
        MOVE    R2,ABSW
        MOVE    R1,GRPDT
        TDNE    R2,[1B17]
        ADD     R1,CYCLE
        TDNE    R2,[2B17]
        SUB     R1,CYCLE
        TDNE    R1,[4B14]
        SETZ    R1,
        MOVEM   R1,GRPDT
        JRST    @GTD
;PROCESS MONTH FOR AFTER/BEFORE
G2:     LSH     R1,-14
        AND     R1,[17]
        MOVEM   R1,T1
        MOVE    R1,GRPDT
        JSR     FDMN            ;GETS YEAR/MONTH
        MOVE    R1,ABSW
        TDNE    R1,[1B17]
        JRST    G6              ;AFTER
;BEFORE
        MOVE    R1,MN##
        SUB     R1,T1
        CAMG    R1,[-1]
        JRST    G7
G8:     MOVEM   R1,MN
G8A:    MOVE    R1,ABSW
        TDNE    R1,[7777]
        JRST    G9              ;HAVE SOMETHING ELSE
        MOVEI   R1,1
        MOVEM   R1,DY##
        JSR     GBNDT
        MOVEM   R1,GRPDT
        JRST    @GTD
G7:     ADDI    R1,14
        SOS     YR##
        JRST    G8
G9:     TDNE    R1,[4B17]
        JRST    G10
        SETZ    R1,
        MOVEM   R1,DY
        JSR     GBNDT   
        MOVEM   R1,GRPDT        ;FOR REFERENCE
        MOVE    R1,[1B17]       ;AFTER
G9A:    MOVEM   R1,ABSW
        MOVE    R1,@LP
        JRST    G1A
;MONTH AFTER
G6:     MOVE    R1,MN
        ADD     R1,T1
        CAIG    R1,13		;fixes bug in ver 7 14
        JRST    G8
        SUBI    R1,14
        AOS     YR
        JRST    G8
;PREPARE FOR BEFORE END OF MONTH BY SETTING 1ST NEXT MONTH
G10:    MOVEI   R1,1
        MOVEM   R1,DY
        AOS     MN
        JSR     GBNDT
        MOVEM   R1,GRPDT
        MOVE    R1,[2B17]
        JRST    G9A
;PROCESS DAY OF WEEK FOR AFTER/BEFORE
G3:     JSR     GDWK
        MOVE    R1,GRPDT
        IDIVI   R1,7
        MOVE    R1,R2
        SUB     R1,RMNDR
        MOVE    R2,ABSW
        TDNE    R2,[2B17]
        JRST    G3A
        CAMG    R1,[-1]
        ADDI    R1,7
        MOVNS   R1
        ADD     R1,CYCLE
G3B:    ADD     R1,GRPDT
        MOVEM   R1,GRPDT
   JRST    @GTD
G3A:    CAILE   R1,0
        SUBI    R1,7
        ADD     R1,CYCLE
        MOVNS   R1
        JRST    G3B

;PROCESS DAY OF WEEK, NOT AFTER/BEFORE
G4:     JSR     GDWK
        MOVE    R1,TSTDT
        IDIV    R1,CYCLE
        MOVEM   R2,T1
        MOVE    R1,RMNDR
G4A:    CAME    R1,T1
        JRST    G4C
G4B:    MOVEM   R1,RMNDR
        JRST    G04
G4C:    CAMLE   R1,T1
        JRST    G4B
        ADDI    R1,7
        JRST    G4A

GDWK:   Z
        LSH     R1,-3
        ANDI    R1,777
        CAIG    R1,0
        MOVEI   R1,1
        IMULI   R1,7
        MOVEM   R1,CYCLE
        MOVE    R1,@LP
        ANDI    R1,7
        SUBI    R1,1
        MOVEM   R1,RMNDR
        JRST    @GDWK

;PROCESS MONTH
G12:    LSH     R1,-14
        ANDI    R1,17
        MOVEM   R1,T1
        MOVE    R1,CURDT
        JSR     FDMN
        MOVE    R1,YR
        MOVEM   R1,CRYR#
        MOVE    R2,MN
        MOVEM   R2,CRMN#
        MOVE    R1,TSTDT
        JSR     FDMN
        JRST    G13B
G13:    MOVE    R1,MN
G13A:   ADD     R1,T1
        CAIG    R1,13
        JRST    .+3
        SUBI    R1,14
        AOS     YR
        MOVEM   R1,MN
G13B:   MOVE    R1,YR
        CAMG    R1,CRYR
        JRST    G14
G15:    SKIPL   ASW
        JRST    G17
        SKIPGE  BSW
        JRST    G8A
        MOVE    R1,BSW
        MOVEM   R1,T2
        MOVE    R1,MN
        SUB     R1,T1
        SOSL    T2
        JRST    .-2
        CAMLE   R1,[-1]
        JRST    G16
        ADDI    R1,14
        SOS     YR
G16:    MOVEM   R1,MN
        JRST    G8A
G17:    MOVE    R1,ASW
        MOVEM   R1,T2
        MOVE    R1,MN
        ADD     R1,T1
        SOSL    T2
        JRST    .-2
        CAIG    R1,13
        JRST    G16
        SUBI    R1,14
        AOS     YR
        JRST    G16
G14:    CAME    R1,CRYR
        JRST    G13
        MOVE    R1,MN
        CAMLE   CRMN
        JRST    G15
        CAME    R1,CRMN
        JRST    G13A
        JRST    G15

;TAKES BINARY DATE AND CONVERTS TO MONTH DAY YEAR
FDMN::  Z
        SETZ    R3,
        CAMG    R1,YTB+1(R3)
        JRST    .+3
        AOS     R3
        JRST    .-3
        MOVEM   R3,YR
        SUB     R1,YTB(R3)
	trnn	r3,3		;test to see if year divisible by 4
	jrst	FdmnLy		;yes, so use month table for leap years
        SETZ    R3,
        CAMG    R1,MTB+1(R3)
        JRST    .+3
        AOS     R3
        JRST    .-3
        MOVEM   R3,MN
        SUB     R1,MTB(R3)
        JRST    fdmnrt
fdmnly: setz	r3,
	camg	r1,MtbLy+1(r3)
        jrst    .+3
	aos	r3	
	jrst	.-3	
        movem   r3,mn
        sub     r1,MtbLy(r3)
fdmnrt: MOVEM   R1,DY
        JRST    @FDMN

;TAKES YR MN DY AND CONVERTS BACK TO BINARY
GBNDT:  Z
        SKIPL   YR
        JRST    .+3
        SETZ    R1,
        JRST   @GBNDT
	move	r3,mn
	move	r1,yr
	trne	r1,3
	jrst	.+3		; not a leap year so use regular table
	move	r1,MtbLy(r3)
	skipa
	move	r1,mtb(r3)
	move	r3,yr
	add	r1,ytb(r3)	; add in no. of days up to begin of year
        ADD     R1,DY		; add in no. of days since 1st of month
        JRST    @GBNDT

;CONVERTS DATE WORD FROM TO BINARY FORM

DATCV:  MOVEM   R1,T1
        LSHC    R1,-11
        ANDI    R1,17
        MOVE    R3,R1
        MOVE    R1,YTB(R3)
        MOVEM   R1,T2
	move	r1,r3
	trne	r1,3
	jrst	datcv1		;not a leap year so use regular table
        SETZ    R1,
        LSHC    R1,4
        MOVE    R3,R1
        MOVE    R1,MTBly(R3)
	jrst	datcv2
datcv1: setz	r1,
	lshc	r1,4
	move	r3,r1
	move	r1,mtb(r3)
datcv2: ADDM    R1,T2
        SETZ    R1,
        LSHC    R1,5
        ADD     R1,T2
        POPJ    P,

GCRDT:  DATE    R1,     ;READ DATE TO R1
        PUSHJ   P,GCRDTS
        SOS     R1
        SOS     T3
        POPJ    P,
GCRDTS: IDIVI   R1,^D31
        AOS     R2
        MOVEM   R2,DY
        IDIVI   R1,^D12
        MOVEM   R2,MN
        SUBI    R1,10
        MOVE    R3,YTB(R1)
	trne	r1,3		;skip if this IS a leap year
	jrst	.+3
	add	r3,MtbLy(r2)
	skipa
        ADD     R3,MTB(R2)
        ADD     R3,DY
        MOVE    R1,R3
        MOVEM   R1,T3
        POPJ    P,

PMSG::  MOVEM   R1,TPMSG#
        TYPE    @TPMSG
        POPJ    P,
PDNO::  MOVE    R0,R1
        PUSHJ   P,PRDNUM##
        POPJ    P,
;TAKES BINARY DAY AND PRINTS
PBNDT:: MOVEM   R1,T1
        JSR     FDMN
        MOVE    R3,MN
        MOVE    R1,MONM(R3)
        PUSHJ   P,PMSG
        TCO     [SPACE]
        MOVE    R1,DY
        CAIG    R1,9
        TCO     [SPACE]
        PUSHJ   P,PDNO
        TCO     [COMCHR]
        TCO     [SPACE]
        MOVE    R1,YR
        ADDI    R1,^D1972
        PUSHJ   P,PDNO
        TCO     [SPACE]
        MOVE    R1,T1
        IDIVI   R1,7
        MOVE    R1,DWKM+1(R2)
        PUSHJ   P,PMSG
        POPJ    P,

YTB:    DEC     -1,364,729,1094,1459,1825,2190,2555,2920,3286,3651,4016
        DEC     4381,4747,5112,5477
MTB:    DEC     0,31,59,90,120,151,181,212,243,273,304,334,365
MtbLy: DEC     0,31,60,91,121,152,182,213,244,274,305,335,366
LIST::  BLOCK   100
TLIST:  BLOCK   100
HLTER:  EXP 0,0
SDATE:: -1
        END
 :`htÌ