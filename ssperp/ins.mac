        TITLE   INSERT          3 Nov 1982

COMMA=54
INS::   SKIPN   LIST##          ;BE SURE THER IS SOMETHING IN THE LIST
        ERROR   [ASCIZ '$MUST USE DATE COMMAND FIRST$']
RT:     SETZM   TMWD
        TYPE    [ASCIZ '$WHAT HOUR (0 TO 23) ']
RT00:   PUSHJ   P,GWD##
        JFCL
        MOVEI   5,TT
        PUSHJ   P,LKUP##
        JRST    .-4
        PUSHJ   P,@R3
        SKIPL   NUMBER##
        JRST    RTN
RT0:    JRST    RQ
RTN:    MOVE    R1,NUMBER
        CAIG    R1,^D23
        CAIGE   R1,0
        JRST    RTNER
        CAIE    CT,DA
        JRST    RTN2
        MOVE    R1,NUMBER
        LSH     R1,5
        MOVEM   R1,T1#
        MOVE    R1,TMWD#
        TDNE    R1,[1740]
        JRST    RTNER1
        OR      R1,T1
        OR      R1,[14B17]
RTN1:   MOVEM   R1,TMWD
        MOVE    R1,NUMBER
        PUSHJ   P,GWD
        JFCL
        MOVEI   R1,TT##
        PUSHJ   P,LKUP
        JRST    RTER
        SKIPGE  NUMBER
        JRST    RTNER2
        CAIE    CT,DA
        JRST    RTN2
        TYPE    [ASCIZ '$DASH CAN ONLY BE USED ONCE$']
        JRST    RT
RTN2:   MOVE    R1,NUMBER
        CAIG    R1,^D23
        CAIGE   R1,0
        JRST    RTNER
        MOVE    R1,TMWD
        TDNE    R1,[37]
        JRST    RTNER1
        OR      R1,NUMBER
        MOVEM   R1,TMWD
        JRST    RT0
NOT::   MOVE    R1,TMWD
        XOR     R1,[2B14]
        MOVEM   R1,TMWD
        JRST    RT00
TBEFR:: MOVE    R1,TMWD
        OR      R1,[1B14]
        JRST    RTN1
ANY::   JRST    RT0
TAFTER:: MOVE    R1,TMWD
        OR      R1,[4B17]
        JRST    RTN1
DFRD::  MOVE    R1,[14001244]
        MOVEM   R1,TMWD
        INTERN  DFRSW
        SETOM   DFRSW#          ;SET TO 1'S IF WE DOING DEFER
        JRST    RT0
RTER::  TYPE    RNERM
        JRST    RT
RNERM:  ASCIZ   'VALID EXPRESSIONS ARE:
        ANY     TO RUN ANY TIME
        DEFER   TO RUN AFTER 2200
        A NUMBER 0 TO 23
        A RANGE AS 4-16
        THE WORDS "AFTER" OR "BEFORE" FOLLOWED BY 0 TO 23
        OR THE WORD NOT BEFORE THE RANGE, "AFTER", OR "BEFORE"$'
RTNER:  TYPE    [ASCIZ '$TIME MUST BE AN HOUR BETWEEN 0 AND 23$']
        JRST    RT
RTNER1: TYPE    [ASCIZ '$CONFUSING TIME EXPRESSION$']
        JRST    RT
RTNER2: TYPE    [ASCIZ '$REQUIRE NUMBER AFTER DASH$']
        JRST    RT
USFLG:  0
RQ:     PUSHJ   P,RQS
INS1:   PUSHJ   P,GNXD
        MOVE    R1,.INS
        PUSHJ   P,OPCH
        MOVE    R1,TMWD
        PUSHJ   P,SWD
        MOVE    R1,NXD
        PUSHJ   P,SWD
        PUSHJ   P,XRQS          ;SENDS RQS DATA
        PUSHJ   P,SLIST
XINS1:  PUSHJ   P,GWDA
        CAME    R1,.EJB
        ERROR   [ASCIZ 'SYSTEM ERROR']
        JRST    CDISP##

RQS::   MOVEI   R0,^D88         ; FOR LOCKHEED DO NOT REQUEST COMPTR
        IFNDEF  LOCHSW,<
        TYPE    [ASCIZ '$COMPUTER ']
        PUSHJ   P,RDDNUM##
        CAIE    CH,CRCHR
        JRST    RQE
	SKIPL	R0		; Must be positive!
        CAIL	R0,^D256	; Maximum system # is 255.
         JRST	RQRE>		; Do range-error message.
        MOVEM   R0,CPN
        PUSHJ   P,GUSNM
        TYPE    [ASCIZ '$PROJECT CODE ']
        TCI     CT,CH
        CAIN    CT,TE
        JRST    RQS1            ;NO PROJECT CODE
        MOVEI   R0,33
        PUSHJ   P,WCHI
        MOVE    R0,CH
        ADDI    R0,140
        ANDI    R0,177
        PUSHJ   P,WCHI
        MOVNI   R3,11
        TCI     CT,CH
        CAIN    CT,TE
        JRST    RQS1
        MOVE    R0,CH
        ADDI    R0,140
        ANDI    R0,177
        PUSHJ   P,WCHI
        AOJL    R3,.-7
        TYPE    [ASCIZ '$PROJECT CODE TOO LONG$']
        CLRIN
        JRST    RQS
RQS1:   MOVEI   R0,32           ;COLON
        PUSHJ   P,WCHI
CO0:    TYPE    [ASCIZ '$COMMAND FILE NAME ']
CO1:    TCI     CT,CH
        CAIN    CT,SP
        JRST    CO1
        CAIN    CT,TE
        JRST    CO0
        JRST    CO2
COM1:   TCI     CT,CH
CO2:    CAIN    CT,TE
        JRST    RQS2
        CAIE    CT,SP
        JRST    COM2
        CLRBFI
        JRST    RQS2
COM2:   MOVE    R0,CH
        ADDI    R0,140
        ANDI    R0,177
        PUSHJ   P,WCHI
        JRST    COM1
RQS2:   MOVEI   R0,155
        PUSHJ   P,WCHI
        MOVNI   R3,6
        SETZ    R0,
        PUSHJ   P,WCHI
        AOJL    R3,.-1
        POPJ    P,
GUSNM:: MOVE    R1,[XWD -1,-22]
        GETTAB  R1,
        JFCL
        MOVEM   R1,UN1#
        MOVE    R1,[XWD -1,-21]
        GETTAB  R1,
        JFCL
        MOVEM   R1,UN2#
        MOVE    I,NMP
        MOVE    R1,UN1
        MOVNI   R3,6
        SETZM   NCOUNT
        SETZ    R0,
        LSHC    R0,6
        SKIPG   R0
        POPJ    P,
        PUSHJ   P,WCHI
        AOS     NCOUNT#
        AOJL    R3,.-6
        MOVNI   R3,6
        MOVE    R1,UN2
        SETZ    R0
        LSHC    R0,6
        SKIPG   R0
        POPJ    P,
        PUSHJ   P,WCHI
        AOS     NCOUNT
        AOJL    R3,.-6
        POPJ    P,
RQE:    TYPE    [ASCIZ '$INVALID TERMINATOR$']
        CLRIN
        JRST    RQS
RQRE:	TYPE	[ASCIZ '$COMPUTER NUMBER OUT OF RANGE,
COMPUTER NUMBER MUST BE BETWEEN 1 AND 255.$']
	CLRIN
	JRST	RQS

WCHI:   DPB     R0,I
        IBP     I
        HLRZ    R2,I
        CAIN    R2,341000
        HRLI    I,201000
        POPJ    P,
NMT::   BLOCK   20
NMP:    POINT   8,NMT,19

GNXD:   PUSHJ   P,GTUZ
        SKIPN   DFRSW           ;DEFER? 8/6/76 CLC
        JRST    REG             ;NO
        MOVEI   R1,25*74+15     ;DEFER START @21:15 
        JRST    GNX             ;CONTINUE END 8/6/76
REG:    MOVE    R1,TMWD
        TDNE    R1,[34000077]
        JRST    GNX1
GNX0:   SETZ    R1,
GNX:    MOVEM   R1,NXD
        JSR     DSX##
        JSR     DS0##
        SKIPG   R1
        ERROR   [ASCIZ 'ERROR IN DATE ROUTINE']
	camle	R1,[lastdt##]	; skip if our run date is in bounds
	error	[asciz 'NO MORE DATES']
        JSR     FDMN##
        MOVE    R1,DY
        SOS     R1
        IMULI   R1,^D1440
        ADDM    R1,NXD
        MOVE    R1,MN
        LSH     R1,20
        ADDM    R1,NXD
        MOVE    R1,YR
        LSH     R1,24
        ADD     R1,NXD
        PUSHJ   P,CUBS
        MOVEM   R1,NXD
        POPJ    P,
GNX1:   TDNE    R1,[2B14]
        JRST    GNX2
        TDNE    R1,[4B17]
        JRST    GNX4
        TDNE    R1,[1B14]
        JRST    GNX0
GNX3:   ANDI    R1,37
        IMULI   R1,74
        JRST    GNX
GNX2:   TDNE    R1,[1B14]
        JRST    GNX3
        JRST    GNX0
GNX4:   LSH     R1,-5
        JRST    GNX3

GET::   PUSHJ   P,RQS
        MOVE    R1,.GET
        PUSHJ   P,OPCH
        PUSHJ   P,XRQS
        PUSHJ   P,GWDA
        CAMN    R1,.NJB
        JRST    RERR
        MOVEI   R2,LIST
        MOVEM   R2,T2#
XGET1:  MOVEM   R1,@T2
        SKIPN   R1
        JRST    CDISP
        AOS     T2
        PUSHJ   P,GWDA
        JRST    XGET1

DOLIST:: PUSHJ  P,GUSNM
        MOVE    R1,.LIS
        PUSHJ   P,OPCH
        PUSHJ   P,SUSNM
        MOVEI   R1,DOBUFF
        MOVEM   R1,T2
        PUSHJ   P,GWDA
        MOVEM   R1,@T2
        AOS     T2
        SKIPE   R1
        JRST    .-4
        MOVE    R1,T2
        MOVEM   R1,T1
        PUSHJ   P,GWDA                  ;CLEAR THE EJB
        CAME    R1,.EJB
        ERROR   SYSER
XDOLR:  MOVEI   R1,DOBUFF
        CAME    R1,T1
        JRST    LEN1
        TYPE    [ASCIZ '$JOB LIST EMPTY$']
        JRST    CDISP
LEN1:   MOVEM   R1,XPOS#
        MOVE    R1,@XPOS
        SKIPE   R1
        JRST    LEN2
        TYPE    [ASCIZ '$END OF JOB $']
        JRST    CDISP
LEN2:   PUSHJ   P,PDMDT
        TYPE    [ASCIZ ', CPN: ']
        AOS     XPOS
        MOVE    R0,@XPOS
        AOS     XPOS
        ANDI    R0,7777		; extract the system number
        PUSHJ   P,PRDNUM
        TCO     [COMMA]
        TCO     [SPACE]
        MOVE    R1,@XPOS
        
        AOS     XPOS
        PUSHJ   P,PWD
        JRST    .-3
        JRST    LEN1+1

PWD:    SETZ    R0
        LSHC    R0,^D20
        PUSHJ   P,PCH
        JRST    SKIPRT
        LSHC    R0,8
        PUSHJ   P,PCH
        JRST    SKIPRT
        LSHC    R0,8
        PUSHJ   P,PCH
SKIPRT: AOS     (P)
        POPJ    P,

PCH:    ADDI    R0,40
        ANDI    R0,177
        TCO     R0
        CAIE    R0,CRCHR
        AOS     (P)
        POPJ    P,

SUSNM:  MOVE    R1,NCOUNT
        MOVEI   R2,3
        HRL     R2,AUX
        AUXCAL  R2,R1
        MOVE    R1,UN1
        SETZ    R0,
        LSHC    R0,6
        SKIPN   R0
        JRST    SUSNM1
        AUXCAL  R2,R0
        SOS     NCOUNT
        JRST    .-6
SUSNM1: SKIPG   NCOUNT
        POPJ    P,
        MOVE    R1,UN2
        SETZ    R0,
        LSHC    R0,6
        SKIPN   R0
        POPJ    P,
        AUXCAL  R2,R0
        SOS     NCOUNT
        JRST    .-6

STATUS:: PUSHJ  P,RQS
        MOVE    R1,.STA
        PUSHJ   P,OPCH
        PUSHJ   P,XRQS
        PUSHJ   P,GWDA
        CAMN    R1,.STAR
        ERROR   SYSER
        CAMN    R1,.NJB
RERR:   ERROR   [ASCIZ 'JOB NOT IN LIST$']
        PUSHJ   P,PDMDT
        TCO     [CRCHR]
        JRST    CDISP
PDMDT:  MUL     1,[^D100000000]
        DIV     1,[^D178813930]
        IDIVI   1,^D1440                ;LEAVE IN 1 NUMBER OF DAYS 
        IDIVI   2,^D60                  ;SINCE FEB 29, 72 AND IN 2
                                        ;THE NUMBER OF HOURS, 3=MIN
        MOVEM   2,HRS#
        MOVEM   3,MIN#
        ADDI    1,^D58                  ;CONVERT IT TO START OF 1972
        MOVEM   1,ETAD#
        PUSHJ   P,GTUZ
        MOVE    R1,UZ
        MOVE    R2,ETAD
	trzn	r1,40			;turn off DST bit if on
	jrst	sc2			;wasn't on--no DST fix needed
        setz 	r3,
SC3:    CAMG    2,DSTD(R3)
        JRST    SC1             	;DST in effect
        AOS     R3
        CAMG    R2,DSTD(R3)
        JRST    SC2			;DST not in effect
        AOJA    3,SC3           	;CONTINUE ON
sc1:    aos	r1 			;fix up for daylight savings
sc2:    subi	r1,20			;get offset to GMT
        addm	r1,hrs			;now it's corrected to GMT
	move	r2,hrs			;put it in an AC for checking
        CAIL    R2,^D24			;see if hr is geq 24
        JRST    SLOW			;yep--go fix up hrs and days
        skipge  r2
        JRST    SHI			;oops--hr is < 0, so fix up
SC:     MOVEM   R2,HRS
        MOVE    R1,ETAD
        PUSHJ   P,PBNDT##       ;PRINT THE DATE
        TCO     [SPACE]
        TCO     [SPACE]
        MOVE    R0,HRS
        PUSHJ   P,PRDNUM##
        TCO     [72]            ;COLON
        MOVE    R0,MIN
        PUSHJ   P,PRD2N##
        JFCL
        POPJ    P,
SHI:    ADDI    R2,^D24
        SOS     ETAD
        JRST    SC
SLOW:   SUBI    R2,^D24
        AOS     ETAD
        JRST    SC
                        ;THE next table gives STARTING DATES AND ENDING
                        ;DATES FOR DAYLIGHT SAVINGS TIME IN DAYS SINCE
                        ;JAN 1, 1972. CALCULATED AS THE LAST SUNDAY
                        ;OF April AND OCTOBER.
DSTD:   DEC     0
	DEC	1149
	DEC	1393
	DEC	1519
	DEC	1764
	DEC	1938
	DEC	2128
	DEC	2302
	DEC	2492
	DEC	2666
	DEC	2856
	DEC	3037
	DEC	3220
	DEC	3401
	DEC	3584
	DEC	3765
	DEC	3955
	DEC	4130
        -1				;indicates end of table

DELETE:: PUSHJ  P,RQS
        MOVE    R1,.DEL
        PUSHJ   P,OPCH
        PUSHJ   P,XRQS
        PUSHJ   P,GWDA
        CAMN    R1,.NJB
        JRST    RERR            ;ASSUME THAT NJB MEANS CANNOT FIND JOB
        CAME    R1,.EJB
        ERROR   SYSER
        TYPE    [ASCIZ '$DELETED$']
        JRST    CDISP

YR::    Z
MN::    Z
DY::    Z
NXD::   Z
.INS:   12227063
.EJB::  11225042
.STA::  14632041
.GET::  11622464
.DEL::  11022454
.LIS::  13024463
.NJB::  13425042
.STAR:: 2405012
CPN::   Z
OMPS:	Z
OPCH::  MOVEM   R1,CMD
        PUSHJ   P,GETSNO        	; GET MASTER PERP SYSTEM # IN R1
SNOERR:	  ERROR	[ASCIZ '$ERROR READING MASTER SYSTEM LIST: (SYS)PERP.SYS
CALL QUALITY ASSURANCE IMMEDIATELY$']
        SKIPLE	AUX			; Circuit already built?
	 CAME	R1,OMPS			; Same as Old Master Perp System?
        Caia				; No circuit - or - No match
	  Jrst	SGCMD			; Same-Same -- Send command
	Seto	R1,			; Set to -1
	Skiple	AUX			; Skip if no circuit.
	 ZapCir	R1,			; Zap the old circuit (Wrong master)
	Setzm	AUX			; Make sure flagged as OFF
	TYPE    [ASCIZ '$BUILDING CIRCUIT TO MASTER PERP SYSTEM$']
        PUSHJ   P,SETIM
	PUSHJ	P,GETSNO        	; GET MASTER PERP SYSTEM # IN R1
	 Jrst	SNOERR			; Can't find good data in file
	MOVEM	R1,OMPS			; Save Master Perp System #
	PUSHJ	P,CNVSNO		; R1_ addr of [ASCIZ 'DUPJOB:##-;']
        CREAUX  R1,
        JRST    OPCHER
        MOVEM   R1,AUX#
        MOVEI   R1,4
        HRL     R1,AUX
        AUXCAL  R1,15
        AUXCAL  R1,15
        JRST    INIT
OPCHER: MOVS    R2,R1
        JRST    ER(R2)
ER:     JRST    SUPERR          ;SUPERVISOR ERROR
        ERROR   SYSER
        ERROR   SYSER
        ERROR   [ASCIZ '$UNABLE TO BUILD CIRCUIT, TRY LATER$']
        ERROR   [ASCIZ '$MASTER PERP SYSTEM UNAVAILABLE- PLEASE REENTER LATER$']
        ERROR   SYSER
        ERROR   SYSER
        ERROR   SYSER
SUPERR: ANDI    R1,17
        CAIN    R1,4
        ERROR   [ASCIZ '$MASTER PERP SYSTEM UNAVAILABLE- PLEASE REENTER LATER$']
        ERROR   SYSER
SYSER:: ASCIZ '$SYSTEM ERROR$'
SYSERR: ERROR   SYSER
SNMERR:	ERROR	[ASCIZ '$ERROR READING CURRENT SYSTEM NUMBER,
PLEASE RE-ENTER REQUEST WITH CORRECT COMPUTER NUMBER
OR CALL QUALITY ASSURANCE TO EXPLAIN YOUR PROBLEM$']

;
;       THIS ROUTINE SETS UP R1 FOR CORRECT COMPUTER NUMBER
;	{Historically:}
;
;	   DUPJOB:10 IF USER IS TO RUN ON  SYSTEMS 35 OR 36
;	   DUPJOB:8  IF USER IS TO RUN ON  SYSTEMS 31,32,33,34,37,38,88
;	   DUPJOB:14 IF USER IS IN DISTRICT 30-39, 70-79, 180-199
;	   DUPJOB:2  IF USER IS TYMSHARE
;	   DUPJOB:3  IF USER IS TO RUN ON  AN OVERFLOW SYSTEM
;        ALL IX USERS GO TO SYSTEM 10 except MDSI who go to 20
;
;	Currently, the system number obtained by indexing into the file
;	(SYS)PERP.SYS via the execution system number.  Location 0
;	contains SIXBIT /#PERP#/ for file validation.  If this file is
;	missing, an error message will be generated for both PERP and
;	DEFER.  The entry format for each word is:
;		Byte (9)  <0>, <french>, <tymshare>, <default>
;	   e.g.
;		System 34/  Byte (9)  0, ^D14,  ^D2,  ^D8
;		System 35/  Byte (9)  0, ^D14,  ^D2, ^D10
;		System 70/  Byte (9)  0,  ^D3,  ^D3,  ^D3
;

GETSNO:	Skipe	SYSBLK			; Anything in SYSBLK?
	  Jrst	GOTSNO			; Yes, Don't need to read file
	INIT	SCHAN,17		; Mode 17
	Sixbit	/SYS/			; Device 'SYS'
	Exp	0			; No buffer headers
	 POPJ	P,			;  Error Return - SYS not there?
	Lookup	SCHAN,SBLK		; Look for (SYS)PERP.SYS.
	 POPJ	P,			; Error Return - File Not Found?
	Input	SCHAN,SYSLST		; Read the file (Ignore errors).
	Close	SCHAN,			; Once read in, we are done
	Releas	SCHAN,			;  so finish up channel stuff.
GOTSNO:	Move	R1,[SIXBIT/#PERP#/]	; Make sure we got good data
	Came	R1,SYSBLK		;  by checking first word.
	 POPJ	P,			;  Error Return - Bad file?
	Skipg	R2,CPN			; Get the execution CPU.
	  Pushj	P,GETTHS		; Get my system # since CPN leq 0
	Jumpe	R2,SNMERR		; System number error if 0
	Movem	R2,CPN			; Store for later (might want it)
	Skipn	SYSBLK(R2)		; Check for non-zero data.
	 POPJ	P,			;  Error Return - Incorrect data?
	Ldb	R1,[Point 9,SYSBLK(R2),35] ; Default master system #.
	Skipge	USFLG			; French district?
	  Ldb	R1,[Point 9,SYSBLK(R2),17] ; Yes, use this one instead.
	Push	P,R0			; Save a register
	Hrroi	R0,6			; !GTPRV - privilage word
	Gettab	R0,			; Get priv word.
	 Setz	R0,			;  Clear bits if error
	Trne	R0,40000		; Check Tymshare bit
	  Ldb	R1,[Point 9,SYSBLK(R2),26] ; Yes, use this one instead.
	Pop	P,R0			; Restore register R0
	Aos	(P)			; Set Skip Return
	Popj	P,			; And return

CNVSNO:	Push	P,R0			; Save a register
	Movei   R0,[Asciz 'DUPJOB:##-;']; Get literal address in R0.
	Pushj	P,SETSNO		; Setup ##...
	Movei	R2,"-"			; Get dash...
	Idpb	R2,R1			; Store it.
	Movei	R2,";"			; Get semi...
	Idpb	R2,R1			; Store it.
	Movei	R2,0			; Null!
	Idpb	R2,R1			; Store it.
	Move	R1,R0			; Copy pointer (nice an' easy)
	Pop	P,R0			; Restore register R0
	Popj	P,			; Return with ASCIZ 'DUPJOB:nn-;'

SETSNO:	Idivi	R1,^D10			; Simple numeric parse
	Hrlm	R2,(P)			;  depends on "PUSHJ P,SETSNO"
	Skipe	R1			; Done?
	  Pushj	P,SETSNO		;  Not yet...loop
	Skipe	R1			; First time?
	  Jrst	SETSN0			; No... skip a few
	Move	R1,[Point 7,0,13]	; Yes, setup pointer.
	Hrr	R1,R0			; Get String address
	Aoj	R1,			; Increment to right place
SetSN0:	Hlrz	R2,(P)			; Get number
	Movei	R2,"0"(R2)		; Make ASCII
	Idpb	R2,R1			; Deposit it in string
	Popj	P,			; Return (Loop)

GETTHS:	Move	R2,[33,,11]		; .CNSYS,,.GTCNF  = %CNSYS
	Gettab	R2,			; Get system Number
	  Jrst	[PUSH   P,R1		; Above method fails on P032/Q
		 MOVEI  R1,11		; ...So use old method and assume
		 GETTAB R1,		;    we will never have a 3-digit
		  JFCL			; (Lose really big, some negative #)!
		 LDB    R2,[POINT 7,R1,13]
		 SUBI   R2,"0"		; system running such an OLD
		 IMULI  R2,^D10		; monitor.  /CARL
		 LDB    R1,[POINT 7,R1,20]
		 ADDI   R2,-"0"(R1)
		 POP    P,R1
		 POPJ   P,]		; Done -- doing it the "old" way.
	Popj	P,			; Return with Result in R1


INIT:   MOVE    R1,[POINT 7,GOMES]
        MOVEI   R2,5
        HRL     R2,AUX
        AUXCAL  R2,R1
        PUSHJ   P,GCHA
        CAIE    R1,377
        JRST    .-2
ICM:    SETZ    R1,
        MOVEM   R1,T3
        MOVNI   R3,5
        AOS     T3
        MOVEI   R1,3
        HRL     R1,AUX
        AUXCAL  R1,T3
        AOJL    R3,.-4
ICM1:   MOVE    R1,T2#
        MOVEM   R1,T1#
        PUSHJ   P,GCHA
        MOVE    CH,R1
        MOVEM   CH,T2
        SUB     CH,T1
        CAIE    CH,1
        JRST    ICM1
        MOVE    R1,T2
ICM2:   MOVEM   R1,T1
ICM3:   PUSHJ   P,GCHA
        MOVE    CH,R1
        MOVEM   CH,T2
        SUB     CH,T1
        CAIE    CH,1
        JRST    ICM1
        MOVE    R1,T2
        CAME    R1,T3#
        JRST    ICM2
;
;       SENC COMMAND WORD
;
SGCMD:	MOVE    R1,CMD#irst copy command to send
SCMD:   PUSHJ   P,SWD
        PUSHJ   P,GWDA
        CAME    R1,CMD
        ERROR   SYSER
        PUSHJ   P,CLTIM
        POPJ    P,

SWD::   MOVEI   R2,3
        HRL     R2,AUX
        SETZ    R0,
        LSHC    R0,24
        AUXCAL  R2,R0

        SETZ    R0,
        LSHC    R0,10
        AUXCAL  R2,R0

        SETZ    R0,
        LSHC    R0,10
        AUXCAL  R2,R0

        POPJ    P,

GWDA::   HRLZ    R2,AUX
        SETZ    R0,
        PUSHJ   P,GCHA
        OR      R0,R1
        LSH     R0,10
        PUSHJ   P,GCHA
        OR      R0,R1
        LSH     R0,10
        PUSHJ   P,GCHA
        OR      R1,R0
        POPJ    P,

GCHA:   MOVEI   R2,10
        HRL     R2,AUX
        HRRI    R2,2
        AUXCAL  R2,R1
        ERROR   SYSER
        POPJ    P,
GCHA1:  MOVEI   R2,1000
        SLEEP   R2,
        JRST    GCHA
MVSUB:  MOVEM   R1,T1
        MOVE    R1,@T1
        SKIPE   R1
        JRST    MVSUB1
        ERROR   [ASCIZ 'INCOMPLETE INPUT DATA']
MVSUB1: PUSHJ   P,SWD
        AOS     T1
        MOVE    R1,@T1
        SKIPE   R1
        JRST    MVSUB1
        PUSHJ   P,SWD
        POPJ    P,

SLIST:: MOVEI   R1,LIST
        PUSHJ   P,MVSUB
        POPJ    P,

XRQS::  MOVE    R1,UZ
        LSH     R1,14
        OR      R1,CPN
        PUSHJ   P,SWD
        MOVEI   R1,NMT
        PUSHJ   P,MVSUB
        POPJ    P,
SETIM:  MOVEI   R1,0
        SETTIM  R1,
        JFCL
        MOVEI   R1,6
        HRLI    R1,4
        INTASS  R1,
        JFCL
        MOVEI   R1,^D180
        HRLI    R1,1
        SETTIM  R1,
        JFCL
        POPJ    P,

CLTIM:  MOVEI   R1,0
        SETTIM  R1,
        JFCL
        POPJ    P,
TIMOUT:: ERROR  [ASCIZ '$SYSTEM TIMEOUT- TRY AGAIN LATER$']
GOMES:  ASCIZ /
1GPERP
/

;this routine converts a DMIN date (see the 940 BRS manual) to CUBS
;assumes the date is in R1 and returns the CUBS date (in GMT!) in R1

CUBS:   MOVEM   R1,TEMP#	; save the DMIN date for later
        PUSHJ   P,GTUZ		; look up user's time zone
        MOVE    r3,UZ		; put time zone in R3
        MOVE    R2,TEMP		; get the date we are converting
	trzn	r3,40		; test for DST bit and turn off if on
	jrst	cubs1		; user not subject to DST
        setz    r1,		; clear r1 to use as an index
	caml	r2,dstab(r1)	; find next day that DST stops
	aoja	r1,.-1
	camle	r2,dstab1(r1)	; see if DST has already started
	aos	r3		; yes, so adjust the time zone by 1
cubs1:	movn	r3,r3		; get negative of tz
	addi	r3,20		; and make it the offset to GMT
	imuli	r3,^d60		; convert it to minutes
	movem	r3,zmin#	; and save the correction for later
	setz	r1,		; clear r1 before shifting
	lshc	r1,^d16		; right-justify year field in r1
	lshc	r2,-^d32	; right-justify month field in r2
	lsh	r3,-^d20	; right-justify minutes of month in r3
	addm	r3,zmin		; add the offset to GMT
	move	r3,zak(r2)	; fetch no. of days since begin of year
	add	r3,yrday(r1)	; add in no. of days UP TO begin of year
	trne	r1,3		; test for leap year
	jrst	cubs2		; no leap year fix needed
	trne	r2,16		; skip if mon < 2 (i.e., Jan or Feb)
	aos	r3		; add a day for leap year
cubs2:	subi	r3,^d59		; make it days since Feb 29, 1972
	imuli	r3,^d1440	; convert days to minutes
	move	r1,r3		; use r1 so as not to zap r4 with ASHC
	add	r1,zmin		; add minutes since 1st of month
	mul	r1,[^d234375]	; 1st part of conversion to CUBS
	ashc	r1,^d18		; finish conversion
	popj	p,

; Zak is the number of days from beginning of year to begin of month

zak:	DEC	  0		; Jan
	DEC	 31		; Feb
	DEC	 59		; Mar
	DEC	 90		; April
	DEC	120		; May
	DEC	151		; June
	DEC	181		; July
	DEC	212		; Aug
	DEC	243		; Sep
	DEC	273		; Oct
	DEC	304		; Nov
	DEC	334		; Dec

; DsTab gives the DMIN date for END of daylight savings time figured
; 	as 1 AM of last Sunday in October of the indicated year.

DSTAB:	EXP	   2316674	; 29 OCT 72
	EXP	   6314034	; 28 OCT 73
	EXP	  12311174	; 27 OCT 74
	EXP	  16306334	; 26 OCT 75
	EXP	  22324374	; 31 OCT 76
	EXP	  26321534	; 30 OCT 77
	EXP	  32316674	; 29 OCT 78
	EXP	  36314034	; 28 OCT 79
	EXP	  42306334	; 26 OCT 80
	EXP	  46303474	; 25 OCT 81
	EXP	  52324374	; 31 OCT 82
	EXP	  56321534	; 30 OCT 83
	EXP	  62314034	; 28 OCT 84
	EXP	  66311174	; 27 OCT 85
	EXP	  72306334	; 26 OCT 86
	EXP	  76303474	; 25 OCT 87
	EXP	3777777777	; dummy entry to indicate end of table

; DsTab1 gives the DMIN date for START of daylight savings time figured
;	 as 2 AM of last Sunday in April of the indicated year.

DSTAB1: EXP	  721630	; 30 APR 72
	EXP	 4716770	; 29 APR 73
 	EXP	10016230	;  6 JAN 74 (special date this year)
 	EXP	14276070	; 23 FEB 75 (again a special date)
 	EXP	20703570	; 25 APR 76 (no special law this year)
 	EXP	24700730	; 24 APR 77
  	EXP	30721630	; 30 APR 78
	EXP	34716770	; 29 APR 79
	EXP	40711270	; 27 APR 80
	EXP	44706430	; 26 APR 81
	EXP	50703570	; 25 APR 82
	EXP	54700730	; 24 APR 83
	EXP	60716770	; 29 APR 84
	EXP	64714130	; 28 APR 85
	EXP	70711270	; 27 APR 86
	EXP	74706430	; 26 APR 87
	EXP	3777777777	; dummy entry to indicate end of table

; the following table gives no. of days up to BEGINNING of the year

YrDay:	DEC	   0		;1972
	DEC	 366		;1973
	DEC	 731		;1974
	DEC	1096		;1975
	DEC	1461		;1976
	DEC	1827		;1977
	DEC	2192		;1978
	DEC	2557		;1979
	DEC	2922		;1980
	DEC	3288		;1981
	DEC	3653		;1982
	DEC	4018		;1983
	DEC	4383		;1984
	DEC	4749		;1985
	DEC	5114		;1986
	DEC	5479		;1987
	DEC	   0		; Obvious error if 1972!

GTUZ::  MOVE    R1,[XWD -1,-23] ;12/26/76 CLC
        GETTAB  R1,
        JFCL
        MOVEM   R1,AUN#
        MOVE    R1,[XWD -1,-22]
        GETTAB  R1,
        JFCL
        MOVEM   R1,UN1#
        MOVE    R2,[XWD -1,-21]
        GETTAB  R2,
        JFCL
        MOVEM   R2,UN2#
        PUSHJ   P,HASH          ;GET THE HASH FUNCTION
        MOVEM   R1,HNAME#
        MOVEM   R2,BLK#
        INIT    LCHAN,17
        SIXBIT/SYS/
        0
        JRST    ERR
        LOOKUP  LCHAN,LBLK
        JRST    ERR
RDLD:   USETI   LCHAN,@BLK          ;POSITION
        INPUT   LCHAN,LUDLST
        SETZ    R1,
        MOVE    R2,AUN
LOP:     CAMN    R2,LUDBLK(R1)
        JRST    FOUND
        SKIPN   LUDBLK(R1)
        JRST    ERR
        SKIPG   LUDBLK(R1)
        JRST    OVFL
        MOVE    R3,LUDBLK+2(R1)
        ANDI    R3,177
        ADD     R1,R3
        JRST    LOP
OVFL:   HRRZ    R2,LUDBLK(R1)
        MOVEM   R2,BLK
        JRST    RDLD
FOUND:  MOVE    R2,R1
        MOVS    R1,LUDBLK+3(R1)
        LSH     R1,-12
        ANDI    R1,77
        SKIPE   USFLG
        JRST    FOU1
        SKIPE   DFRSW   ;DEFER?
        JRST    PSTSET
FOU1:   SKIPN   R1
PSTSET:: MOVEI   R1,50
        MOVEM   R1,UZ
        CLOSE   LCHAN,
        RELEASE LCHAN,
        MOVE    R2,LUDBLK+1(R2)
        MOVEM   R2,DISTNO#
        CAIG    R2,^D29         ; CHECK FOR BELGIUM
        JRST    US
        CAIG    R2,^D39         ; CHECK FOR BELGIUM
        JRST    P14             ; AREA 3 IS FOREIGN
        CAIG    R2,^D69
        JRST    US
        CAIG    R2,^D79
        JRST    P14
        CAIGE   R2,^D180
        JRST    US
        CAIL    R2,^D200
        JRST    US
P14:    SETOM   USFLG
        POPJ    P,
US:     SETZM   USFLG
        POPJ    P,
        POPJ    P,
ERR:    ERROR   [ASCIZ 'TROUBLE IN READING TIME ZONE
        CALL QUALITY ASSURANCE']
LBLK:   SIXBIT/LUD/
        SIXBIT/SYS/
        0
        0
LUDBLK: BLOCK   200
LUDLST: IOWD    200,LUDBLK
        0

SBLK:	SIXBIT	/PERP/
	SIXBIT	/SYS/
	0
	0
SYSBLK:	BLOCK	^D256
SYSLST:	IOWD	^D256,SYSBLK
	0

HASH:   MOVEI   6,0
        MOVE    3,[555555555555]
        MOVE    4,[361275431652]
        MOVE    5,[612754316523]
        PUSHJ   P,RND
        PUSHJ   P,RND
        PUSHJ   P,RND
        PUSHJ   P,RND
        XOR     5,4
        MOVE    R1,5
        TLZ     R1,400000
        IDIVI   R1,^D887
        ADDI    R2,1
        XOR     3,5
        MOVE    R1,3
        POPJ    P,

RND:    ADD     4,R1
        ROTC    R1,-22
        MOVEI   7,5
RND1:   MOVE    10,4(6)
        MUL     10,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
        ADDM    11,5(6)
        AOJE    6,RND2
        MOVNI   6,1
        TRNE 4,1
        SKIPL   5
        MOVEI   6,0
        EXCH    3,5
RND2:   SOJG    7,RND1
        POPJ    P,
UZ::    Z
DOBUFF: BLOCK   500
        END
    O+™