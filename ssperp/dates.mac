        TITLE   DAT             Dec 13, 1980
;THE FOLLOWING ARE ROUTINES THAT ARE USED IN THE DATE COMMAND
;TO GENERATE THE PROPER BIT CONFIGURATIONS
;
;       DAY OF THE WEEK
DWK:    PUSHJ   P,DWKS
        MOVEM   R1,T1#
        MOVE    R1,CCM#
        LSH     R1,-17
        OR      R1,T1
        OR      R1,[3B14]
        MOVEM   R1,@LP
        SKIPL   NUMSW#
        POPJ    P,
        MOVE    R1,ESW#
        CAME    R1,[-2]
        POPJ    P,
        MOVE    R1,@LP
        OR      R1,[2B20]
        MOVEM   R1,@LP          ;STORE WITH ONCE ONLY BIT ON
        POPJ    P,
;
DWKS:   MOVE    R1,@LP
        SKIPE   R1
        PUSHJ   P,DWKS1
        EXCH    R1,NUMBER
        SKIPG   R1
        MOVEI   R1,1
        LSH     R1,3
        POPJ    P,
DWKE:   ERROR   [ASCIZ 'CONFLICTING DAYS IN YOUR EXPRESSION']
DWKS1:  AOS     LP
        SETZ    R1,
        POPJ    P,


;MONTH FOR A DATE
SMON:   MOVE    R1,@LP
        SKIPE   R1
SME:    ERROR   [ASCIZ 'DATE USED WITHOUT A VALID SEPARATOR']
        PUSHJ   P,GD
        OR      R1,[2B14]
        MOVEM   R1,@LP
        MOVE    R1,TC#
        CAIE    R1,LFCHR
        JRST    .+3
        PUSHJ   P,OR
        POPJ    P,
        CAIE    R1,COMCHR
        POPJ    P,
        PUSHJ   P,OR
        POPJ    P,

;GET DATE
GD:     MOVE    R1,CCM
        AND     R1,[77B20]
        LSHC    R1,-12
        CAIE    R1,740
        JRST    .+3
        MOVE    R1,[17001]
        POPJ    P,
        MOVEM   R1,T1
        PUSHJ   P,GDS
        CAIG    R1,^D31
        SKIPG   R1
GDE1:   ERROR   [ASCIZ 'DATE MUST BE IN FORM MONTH DAY,YEAR']
        ADDM    R1,T1
        PUSHJ   P,GDS
        CAILE   R1,^D100
        SUBI    R1,^D1900
        SUBI    R1,^D72
        CAILE   R1,16
        ERROR   [ASCIZ 'DATE MUST BE BEFORE 1987']
        SKIPGE  R1
        ERROR   [ASCIZ 'DATE MUST BE AFTER 1972']
        SETZ    R2,
        LSHC    R1,11
        OR      R1,T1
        POPJ    P,
GDS:    PUSHJ   P,GWD
        JRST    GDE1

        PUSHJ   P,SLKUP
        CAIE    GDE1
        CAIE    R1,NUM
        JRST    GDE1
        SETZ    R1,
        EXCH    R1,NUMBER
        POPJ    P,
;
;GET A NUMBER
SNUM:   AOS     NUMSW
        MOVE    R1,NUMBER
        SKIPE   R1
        ERROR   [ASCIZ 'TWO NUMBER EXPRESSIONS TOGETHER']
        MOVE    R1,CCM
        LSH     R1,-17
        MOVEM   R1,NUMBER
        POPJ    P,

DAYS:   PUSHJ   P,DWKS
        OR      R1,[3B14]
        MOVEM   R1,@LP
        POPJ    P,

MONTHS: SETZ    R1,
        EXCH    R1,NUMBER
        SKIPG   R1
        MOVEI   R1,1
        LSH     R1,14
        MOVEM   R1,T1
        MOVE    R1,@LP
        SKIPE   R1
        JRST    MON1
        MOVE    R1,T1
MON2:   OR      R1,[3B14]
        MOVEM   R1,@LP
        MOVE    R1,[-2]
        MOVEM   R1,ESW
        POPJ    P,
MON1:   TDNE    R1,[170000]
        ERROR   [ASCIZ 'NO VALID SEPERATOR BETWEEN MONTH EXPRESSIONS']
        OR      R1,T1
        SKIPL   ESW
        ERROR   [ASCIZ '$UNIPLIMENTED SYNTAX (EVERY...EVERY)']
        JRST    MON2

;REFERENCE
SREF:   MOVE    R1,CCM
        MOVEM   R1,T4#
        AND     R1,[7B17]
SREF1:  MOVEM   R1,T3#
SREF02: MOVE    R1,@LP
        SKIPE   R1
        JRST    SREF2
SREF0:  PUSHJ   P,GDATW
        MOVE    R2,T4
        TDNE    R2,[1B20]
        SUBI    R1,1
        OR      R1,T3
        OR      R1,[1B14]
        MOVEM   R1,@LP
        AOS     LP
        SETZM   @LP
        POPJ    P,
SREF2:  AOS     LP
        JRST    SREF02

GDATW:  PUSHJ   P,GWD
        JRST    GDE1
        PUSHJ   P,SLKUP
        JRST    .-3
        CAIE    R3,SMON
        JRST    GDE1
        PUSHJ   P,GD
        POPJ    P,

;AFTER BEFORE
RLDAY:  MOVE    R1,@LP
        SKIPE   R1
        JRST    RLD
        ERROR   [ASCIZ 'AFTER-BEFORE MUST BE PRECEEDED BY A DAY EXPRESSION']
RLD:    AND     R1,[73B17]
        CAME    R1,[3B14]
        JRST    .+2
        JRST    .+3
        CAME    R1,[2B14]
RLDE:   ERROR   [ASCIZ 'AFTER-BEFORE INCONSISTENCY']
        MOVE    R1,CCM
        AND     R1,[3B17]
        ORM     R1,@LP
        AOS     LP
        SETZB   R1,@LP
        POPJ    P,

;SETS INITIAL FLAG WORD
SFLAG:  MOVE    R1,@LP
        SKIPE   R1
        JRST    SFLGE
        MOVE    R1,CCM
        AND     R1,[7B17]
        OR      R1,[1B14]
        MOVEM   R1,@LP
        AOS     LP
        SETZB   R1,@LP
        POPJ    P,
SFLGE:  AOS     LP
        JRST    SFLAG

;STARTING
STARTI: SKIPL   SDATE
        JRST    SST             ;ALREADY HAVE ONE DATE
        MOVE    R1,LP
        CAIE    R1,LIST##
        JRST    SST
        PUSHJ   P,GDATW
        MOVEM   R1,SDATE
        POPJ    P,
SST:    MOVE    R1,[5B17]
        MOVEM   R1,T4
        JRST    SREF1

;OR     
OR:     MOVE    R1,@LP
        SKIPN   R1
        ERROR   [ASCIZ 'CONNECTING WORD WITHOUT DAY OR DATE BEFORE']
        PUSHJ   P,ENGRP
        POPJ    P,

ENGRP:  SETOM   NUMSW
        MOVE    R1,[-2]
        MOVEM   R1,ESW
        MOVE    R1,NUMBER
        SKIPE   R1
        JRST    ENGE1
        SKIPL   ANDSW#
        JRST    ENGE
        AOS     LP
        SETZB   R1,@LP
        POPJ    P,
ENGE:   SETOM   ANDSW
        ERROR   [ASCIZ 'CANNOT USE "AND" BETWEEN DAY EXPRESSIONS WITHOUT TERMINATING THE GROUP EXPRESSION WITH A REFERENCE DATE']
ENGE1:  SETZM   NUMBER
        ERROR   [ASCIZ 'NUMBER OF WHAT$ (INAPPROPRIATE NUMBER USED)']

EVERY:  AOS     ESW
        POPJ    P,

END:    MOVE    R1,@LP
        SKIPE   R1
        ERROR   [ASCIZ 'CANNOT USE MONTHS IN RELATION TO END OF MONTH']
        MOVE    R3,LP
        MOVE    R1,-1(R3)
        TDNE    R1,[170000]
        JRST    END1
        SOS     LP
        OR      R1,[4B17]
        TDNE    R1,[2B17]
        XOR     R1,[2B17]       ;REMOVE IT IF IT WERE THERE
        MOVEM   R1,@LP
END1:   MOVE    R1,[34000010]    ;FOR END OF MONTH
        MOVE    R1,@LP
        POPJ    P,


WEK:    SETZ    R2,
        MOVE    R1,WEKT(R2)
        MOVEM   R1,@LP
        AOS     LP
        AOS     R2
        CAIG    R2,4
        JRST    .-5
        POPJ    P,

WEKT:   30000012
        30000013
        30000014
        30000015
        30000016
        00000000
;LAST
LAST:   PUSHJ   P,GWD
LASTE:  ERROR   [ASCIZ 'LAST MUST BE FOLLOWED BY DAY OF WEEK OR DAY']
        PUSHJ   P,SLKUP
        JRST    LASTE
        CAIN    R3,DWK
        JRST    LAST1
        CAIE    R1,DAYS
        JRST    LASTE
LAST1:  AOS     NUMSW
        ANDI    R3,37777
        PUSHJ   P,@R3
        OR      R1,[4B17]
        MOVEM   R1,@LP
        POPJ    P,
;
TODAY:  MOVE    R1,@LP
        SKIPE   R1
        JRST    SME
        PUSHJ   P,BRS176
        OR      R1,[2B14]
        MOVEM   R1,@LP
        POPJ    P,
TOMRW:  MOVE    R1,@LP
        SKIPE   R1
        JRST    SME
        PUSHJ   P,BRS176
        AOJ     R1,
        PUSHJ   P,REDATE
        OR      R1,[2B14]
        MOVEM   R1,@LP
        POPJ    P,
GWD::   SKIPL   SWLWD
        JRST    GWD4
        AOS     (P)
GWD0:   SETZM   CURW
        MOVE    15,[POINT 7,CURW,6]
        TCI     CT,CH
        CAIN    CT,SP           ;IS IT A SPACE
        JRST    GWD0            ;IF SO KEEP GETTING CHARS
        CAIN    CH,CRCHR
        JRST    GWD0
        CAIE    CT,EX
        JRST    .+3
        SOS     (P)             ;GET RID OF SKIP RETURN
        JRST    GWD4
        MOVEM   CH,FCH# ;STORE THE FIRST CHARACTER
GWD2:   DPB     CH,15
        IBP     15
        TCI     CT,CH
        CAIN    CT,SP           ;IS IT A SPACE
        JRST    GWD4
        CAIN    CT,TE           ;CARRIAGE RETURN OR LINE FEED
        JRST    GWD4
        CAIN    CT,CO           ;COMMA
        JRST    GWD4
        CAIN    CT,DA           ;DASH
        JRST    GWD4
        CAIE    CT,EX           ;PERIOD
        JRST    GWD2
GWD3:   MOVEM   CH,SWLWD
GWD4:   MOVEM   CH,TC
        MOVE    CH,[EOLIT]
        DPB     CH,15
        POPJ    P,
CURW:   BLOCK   5

SLKUP:  MOVEI   5,STAB#
LKUP::  MOVE    6,[POINT 7,CURW,6]
        MOVE    R1,FCH
        CAIG    R1,71           ;IS IT GREATER THAN 9
        CAIGE   R1,60           ;OR 0
        JRST    LK              ;NOT A NUMBER
        ;                       PROCESS A NUMBER
        SUBI    R1,60
        MOVEM   R1,NUMBER
LK7:    ILDB    1,6                     ;GET THE NEXT CHAR
        CAIG    R1,71
        CAIGE   R1,60
        JRST    LK6
        SUBI    R1,60
        EXCH    R1,NUMBER
        IMULI   R1,12
        ADDM    R1,NUMBER
        JRST    LK7
LK6:    MOVEI   R3,NUM
        MOVE    R1,R3
        JRST    SKIPRT
LK:     MOVE    R1,6
        MOVE    R2,@5
        SSKE    R1,@2
        JRST    LKN
        MOVE    R3,1(5)
        MOVE    R1,R3
        MOVEM   R1,CCM
        ANDI    R3,37777
        JRST    SKIPRT
LKN:    ADDI    5,2
        SKIPN   @5
        POPJ    P,
        JRST    LK
SKIPRT: AOS     (P)
        POPJ    P,


BRS176:  DATE    R1,             ;GET THE DATE
        ADDI    R1,1            ;TO GET THE RIGHT NUMBER
        IDIVI   R1,^D31
        MOVEM   R2,DANO
        IDIVI   R1,^D12
        MOVEM   R2,MTH
        SUBI    R1,10
        LSH     R1,4
        OR      R1,MTH
        LSH     R1,5
        OR      R1,DANO
        POPJ    P,
REDATE: MOVE    R2,R1
        ANDI    R2,777000
        LSH     R2,-11
        MOVEM   R2,YR
        MOVE    R2,R1
        ANDI    R2,740
        LSH     R2,-5
        MOVEM   R2,MTH
        MOVE    R0,R1
        ANDI    R0,37
        MOVEM   R0,DANO
DC:     CAMG    R0,MLTAB(R2)
        JRST    MT
        SUB     R0,MLTAB(R2)
        AOS     MTH
        AOJ     R2
        CAIGE   R2,^D12
        JRST    DC
MC:     SUBI    R2,^D12
        MOVEM   R2,MTH
        AOS     YR
        JRST    DC
MT:     CAIL    R2,^D12
        JRST    MC
        SETZ    R1
        MOVE    R1,YR
        LSH     R1,4
        OR      R1,MTH
        LSH     R1,5
        OR      R1,DANO
        POPJ    P,
MLTAB:  DEC 31,28,31,30,31,30,31,31,30,31,30,31
MTH:    Z
DANO:    Z
YR:     Z

NUM:    POPJ    P,
LALL
DEFINE DEF(W,I,T) <[ASCIZ "W'"]
        <I>B20+T>
STAB:   BLOCK 0
DEF SUNDAYS,1,DWK
DEF MONDAYS,2,DWK
DEF TUESDAYS,3,DWK
DEF WEDNESDAYS,4,DWK
DEF THURSDAYS,5,DWK
DEF FRIDAYS,6,DWK
DEF SATURDAYS,7,DWK
DEF JANUARY,,SMON
DEF FEBRUARY,1,SMON
DEF MARCH,2,SMON
DEF APRIL,3,SMON
DEF MAY,4,SMON
DEF JUNE,5,SMON
DEF JULY,6,SMON
DEF AUGUST,7,SMON
DEF SEPTEMBER,10,SMON
DEF OCTOBER,11,SMON
DEF NOVEMBER,12,SMON
DEF DECEMBER,13,SMON
DEF DAYS,,DAYS
DEF DAILY,,DAYS
DEF MONTHS,,MONTHS
DEF MONTHLY,,MONTHS
DEF FIRST,1,SNUM
DEF OTHER,2,SNUM
DEF SECOND,2,SNUM
DEF THIRD,3,SNUM
DEF FOURTH,4,SNUM
DEF FIFTH,5,SNUM
DEF SIXTH,6,SNUM
DEF SEVENTH,7,SNUM
DEF EIGHTH,8,SNUM
DEF NINTH,9,SNUM
DEF TENTH,10,SNUM
DEF EARLIEST,,SFLAG
DEF LATEST,10,SFLAG
DEF UNTIL,41,SREF
DEF THROUGH,40,SREF
DEF EXCEPT,30,SFLAG
DEF AFTER,10,RLDAY
DEF BEFORE,20,RLDAY
DEF END,,END
DEF ENDING,40,SREF
DEF REFERENCE,,SREF
DEF BASED,,SREF
DEF STARTING,,STARTI
DEF AND,,OR
DEF OR,,OR
DEF ORDER,20,SFLAG
DEF EVERY,,EVERY
DEF EACH,,EVERY
DEF FOREVER,17,SMON
DEF LAST,,LAST
DEF TODAY,,TODAY
DEF TOMORROW,,TOMRW
DEF WEEKDAYS,,WEK
0
TT::    BLOCK 0
        DEF ANY,,ANY##
        DEF NOT,,NOT##
        DEF AFTER,,TAFTER##
        DEF     BEFORE,,TBEFR##
        DEF     HELP,,RTER##
        DEF     DEFERRED,,DFRD##
0

PAGE


DATES:: MOVE    R1,[-2]
        MOVEM   R1,ESW
        SETOM   SWLWD
        SETOM   ANDSW
        SETOM   NUMSW
        SETOM   SDATE##
        MOVE    R3,[-100]
        SETZM   LIST+100(R3)
        AOJL    R3,.-1
        SETZM   NUMBER
        MOVEI   R1,LIST
        MOVEM   R1,LP
        TYPE    [ASCIZ '$ENTER YOUR DATE PERIOD$']
R:      PUSHJ   P,GWD;
        JRST    REN
        PUSHJ   P,SLKUP
        JRST    R
        ANDI    R3,37777
        PUSHJ   P,@3
        JRST    R
REN:    PUSHJ   P,ENGRP
        JRST    CDISP##
SWLWD:: Z

NUMBER::        Z

        END
 