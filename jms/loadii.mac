;       THIS PROGRAM IS THE SOLE PROPERTY AND CONFIDENTIAL
;       INFORMATION OF TYMSHARE, INC., AND MAY NOT BE
;       COPIED IN WHOLE OR IN PART OR DISCLOSED TO ANY
;       THIRD PARTY WITHOUT THE PRIOR WRITTEN CONSENT OF
;       TYMSHARE.
;
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;               ALOAD.MAC          WRITTEN BY BILL SCHEIBLE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;       MACRO SUBROUTINES FOR USE WITH NETGEN.FTF
;       THE NETWORK DOWNSTREAM LOADING PROGRAMS
;
;       SUPPLIIES VARIOUS PACKING AND UNPACKING OPERATIONS,
;       8-LEVEL CHARACTER READING, AND A FEW OTHER THINGS.
;       ALSO HAS ALL THE AUX CIRCUIT CONTROL AND I/O DRIVERS
;
TITLE ALOAD
; WD32 SPLITS A 36BIT WD INTO TWO 16BIT WDS. IT USES
;  THE RIGHT 2 BITS AS CONTROL BITS. IF ON,WRD IS VAILD TO ASSEMBLE
;       CALL WD32(WRD,HWRD1,HWRD2)
        ENTRY WD32,EXPAND,MDATE
WD32:   0
        MOVE 3,@0(16)
        LSHC 3,-^D20
        TRNN 4,400000
        TLO 3,400000
        MOVEM 3,@1(16)
        SETZ 3,0
        LSHC 3,^D16
        TLNN 4,40000
        TLO 3,400000
        MOVEM 3,@2(16)
        JRA 16,3(16)
;
; WD24 TAKES 3 36BIT WORDS AND PACKS THE RT 24BITS OF EACH INTO
;       2 36BIT WORDS.
;       CALL WD24(WD24A,WD24B,WD24C,WD36A,WD36B)
        ENTRY WD24
WD24:   0
        MOVE 3,@0(16)
        AND 3,[OCT 77777777]
        LSH 3,^D12
        MOVE 4,@1(16)
        AND 4,[OCT 77777777]
        SETZ 5,
        LSHC 4,-^D12
        IOR 3,4
        MOVEM 3,@3(16)
        MOVE 3,@2(16)
        AND 3,[OCT 77777777]
        IOR 3,5
        MOVEM 3,@4(16)
        JRA 16,5(16)
;
; WD16 SPLITS A 16BIT WRD INTO TWO 8BIT WORDS AND DOES THE ADDITIVE
;       CHECKSUM..ALSO DOES A BARBER POLE CHECKSUM
;       CALL WD16(WRD,HWRD1,HWRD2,CKSUM,BPSUM)
        ENTRY WD16
WD16:   0
        MOVE 1,@0(16)
        XORM 1,@3(16)
        SETZ  3,0
        SETZ 4,0
        MOVE 3,@4(16)   ;GET THE CURRENT BARBERPOLE
        LSHC 3,-^D15
        LSHC 4,-^D20   ;GET EVERYTHING RIGHT JUSTIFIED....HEE HEE
        IOR  3,4        ;THIS SHOULD PUT THINGS BACCK TOGETHER AGAIN
        XORM 1,3        ;AND OR THE WHOLE THING TOGETHER
        AND   3,[177777]        ;SET UP FOR 16 BITS ONLY
        MOVEM 3,@4(16)
        LSHC 1,-^D8
        MOVEM 1,@1(16)
        SETZ 1,0
        LSHC 1,^D8
        MOVEM 1,@2(16)
        JRA 16,5(16)
;
;
;               EXPAND PICKS UP THE PDP-10 LENGTH OF THE REL FILE
;
EXPAND: 0
        HLROS   ,@0(16)
        SETCMM  ,@0(16)
        AOS     ,@0(16)
        JRA     16,1(16)

;
;               MDATE RETURNS THE DATE
;
MDATE:  0
        DATE    0,
        JRA     16,1(16)
;
;
;       ROUTINE TO BUILD A CIRCUIT...PASSES BACK THE PORT
;       FOR THE REASON FOR FAILURE
        ENTRY BCIR
BCIR:   0
        MOVE    5,[ POINT  7,0]
        HRR     5,0(16)
        MOVE    6,[POINT   7,LOGIN]
        HRRZI   7,25
        HRRZI   4,204           ;PUT IN A CNTRL D
        IDPB    4,6
;
BCOPY:  ILDB    4,5
        CAIE    4,40
        IDPB    4,6
        JUMPE   4,BC
        SOJG    7,BCOPY
BC:     HRRZI   5,LOGIN
        CREAUX 5,       ;BUILD THE CIRUCIT
        JRST    BCIR1   ;ERROR
        HRRZM   5,@1(16)        ;SEND BACK THE PORT NUMBER
        JRA     16,3(16)        ;NORMAL RETURN
BCIR1:  MOVEM  5,@2(16)        ;SEND BACK ERROR CODE
        JRA     16,3(16)        ;AND RETURN
;
LOGIN:  BLOCK   5
;
;       ROUTINE TO ZAP A CIRCUIT
        ENTRY ZAP
ZAP:    0
        MOVE    5,@0(16)
        ZAPCIR  5,
        JRA     16,0(16)
;
;
;
;
;       LITTLE ROUTINE TO SEE WHO IS RUNNING WHAT
;       
        ENTRY WHOYOU
WHOYOU: 0
        MOVE    5,[36,,-1]
        FRMOP   5,LO1
        JUMPA   YOUNOT          ;ERROR
;
        MOVE    5,LO2
        TDON    5,[3700,,0]     ;CHECK ON TERMINAL ID
        JUMPA   YOUNOT          ;NO GOOD
        SETZM   5,              ;CLEAR OUR ACC 5
        MOVE    5,[-25]         ;LOOKING FOR A PPN...
        GETTAB  5,              ;GO GET IT
        JFCL    0               ;INCASE WE GET A SKIP RETURN
        CAME    5,[11334,,201464]  ;ARE WE LOCATED IN NETCON??
        JRST    YOU2            ;NOPE...HOW ABOUT CES
        JRA     16,1(16)
YOU2:   CAME    5,[11334,,267137];ARE WE ISIS
        JRST    YOU3
        JRA     16,1(16)
YOUNOT: MOVE    5,[-1]
        MOVEM   5,@0(16)        ;RETURN THE -1
        JRA     16,1(16)

YOU3:   CAME    5,[1,,4]
        JRST    YOUNOT
        JRA     16,1(16)
LO1:    1,,LO2
LO2:    0
;
;
;
;
;
;       OUTDAT..SENDS OUT 2 CONSECUTIVE 8 BITS CHARS
;       CALL AS (PORT,ID1,ID2,RES)
;       IF RES IS NOT 0..THEN SOME KIND OF ERROR OCCURRED..
;       I'M NOT SURE OF THE ERROR (SKIP TYPE)..OR IF IT WILL
;WORK...WORRIED ABOUT ZAPPED CIRCUITS
;
;
        ENTRY OUTDAT
OUTDAT: 0
        HRLZ    6,@0(16)        ;PUT THE PORT NUMBER IN 6
        HRR     6,[0,,3]        ;TYPE 3 REQUEST..IMAGE OUT
        MOVE    5,@1(16)        ;LOAD UP FIRST BYTE
        AUXCAL  6,5             ;SEND IT OUT
        MOVE    5,@2(16)        ;GET THE 2ND BYTE
        AUXCAL  6,5             ;AND SEND IT OUT
        JRA     16,3(16)        ;AND RETURN
;
;
;       ROUTINE TO CHECK AND SEE IF AUX CIRCUIT STILL THERE
;
;       CALL AS CHECK(RES,ADDRESS OF TABLE)
;
        ENTRY CHECK
CHECK:  0
        MOVE    6,[-1,,TABS]
        AUXRED  6,
        JRST    CK1             ;WE GOT AN ERROR
        CAME    6,[0]           ;0 MEANS NO PORT
        JRA     16,1(16)        ;JUST RETURN IF EVERYTHING IS COOL
        MOVE    5,[-1]
        MOVEM   5,@0(16)        ;SEND BACK A -1
        JRA     16,1(16)        ;BYE BYE
CK1:    MOVE    5,[-1]
        MOVEM   5,@0(16)
        JRA     16,1(16)
TABS: BLOCK 3
;
;       END OF CHECK ROUTINE
;
;
;       
;
;
;
;       OK..ROUTINE TO GET ONLY ONE CHAR IN..RATHER SLOPPY I THINK
;       CALL AS GETCH (PORT,CHAR)
;
;
        ENTRY GETCHR
GETCHR: 0
        SETZ    5,0            ;MAKE SURE ACC 5 IS CLEAN
        HRLZ    6,@0(16)        ;GET THE PORT NUMBER
        HRR     6,[0,,10]       ;CHECK TO SEE IF SOMETHING THERE
        AUXCAL  6,5             ;RESULT IN ACC 5
        JRST    INO1            ;NO DATA
        HRR     6,[0,,1]
        AUXCAL  6,5
        JFCL    0
        HRRZM   5,2             ;SLAP IT AROUND
        AND     2,[377]         ;8 BITS ONLY!!
        HRRZM   2,@1(16)        ;AND SEND IT BACK
        JRA     16,2(16)        ;AND SPLIT
INO1:   MOVE    5,[-1]          ;SET UP FRO ALL 1'S
        HRROM   5,@1(16)        ;SET IT BACK FOR AN ERROR
        JRA     16,2(16)        ;BYE!!!!
;
;
;
;       OK...TAKES WHAT WE ARE ABOUT TO RECEIVE...
;       AND..DOES THE CHECKSUMMING...
;       AMEN
;       AMEN
;
;
;       CALL AS GETCKS(WORD,CHECKSUM,BARBERPOLE)
;
        ENTRY GETCKS
GETCKS: 0
        SETZ    2,0
        SETZ    3,0
        SETZ    4,0     ;CLEAR UP EVERYTHING
        MOVE    2,@0(16)        ;GET THE DATA
        MOVE    3,@1(16)        ;GET THE CURRENT VERT CHECKSUM
        XOR     3,2     
        AND     3,[177777]      ;MAKE SURE WE ONLY HAV 16 BITS
       MOVEM   3,@1(16)        ;PUT IT BACK
        MOVE    3,@2(16)        ;GET THE CURRENT BARBER POLE
        LSHC    3,-^D15
        LSH     4,-^D20
        IOR     3,4             ;PUT THEM BACK TOGETHER AGAIN..
        XOR     3,2             ;AND MERGE...
        AND     3,[177777]
        MOVEM   3,@2(16)
        JRA     16,3(16)        ;AND RETRUN
;
;
;       OUTPUT A SINGLE CHAR...
;       CALL AS OUTONE(PORT,DATA)
        ENTRY OUTONE

OUTONE: 0
        HRLZ    6,@0(16)        ;THE PORT NUMBER
        HRR     6,[0,,3]        ;PACK IN THE REQUEST TYPE
        MOVE    5,@1(16)        ;GET THE CHAR
        AUXCAL  6,5             ;SEND IT OUT
        JRA     16,0(16)        ;DON'T SEND ANYTHING BACK
;
;       ROTINE TO GO SLEEP FOR 1 SEC..AND WAKE IF CHAR
;       SHOWS UP
;       CALL AS     CALL DELAY (PORT)
;
;
        ENTRY DELAY
DELAY:  0
        HRLZ    5,@0(16)        ;GET THE PORT NUMBER
        LSH     5,^D9           ;PUT IN THE HIGH ORDER
        IOR     5,[000110,,^D1000]   ;SLEEP FOR 1 SECOND AND INTERR ON CHAR INCOMING
        HIBER   5,              ;GO TO IT
        JRA     16,1(16)        ;ERROR RETURN..BAD NEWS
        JRA     16,1(16)        ;NORMAL RETURN
;
;       ROUTINE TO PROVIDE THE SHORTEST POSSIBLE DELAY
;
        ENTRY   SDELAY
SDELAY: 0
        HRLZ    5,@0(16)
        LSH     5,^D9
        IOR     5,[130,,1]
        HIBER   5,
        JRA     16,1(16)
        JRA     16,1(16)
;
;
;

;
;
;
;
;       ROUTINE TO READ THE FILE STATUS WORD FOR
;       A SPECIFIED PORT.
;
;       FORTRAN CALL:
;
;       CALL GETCHN(PORT,STATUS)
;
;       RETURNS THE CHANNEL STATUS WORD IN STATUS
;       IF STATUS = -1 A GETTABS ERROR HAS OCCURED
;
        ENTRY   GETCHN
GETCHN: 0
        HRLZ    3,@0(16)        ;PUT PORT IN LEFT
        HRRI    3,-31           ;GETTABS TABLE 31
        GETTAB  3,
        SETO    3,              ;RETURN -1 IF ERROR
        MOVEM   3,@1(16)        ;RETURN RESULT
        JRA     16,2(16)        ;RETURN TO CALLER
        END
  ;
;       MACRO SUPPORT ROUTINES FOR DOWNSTREAM LOADING
;       THIS PROGRAM IS THE SOLE PROPERTY AND CONFIDENTIAL
;       INFORMATION OF TYMSHARE, INC., AND MAY NOT BE
;       COPIED IN WHOLE OR IN PART OR DISCLOSED TO ANY
;       THIRD PARTY WITHOUT THE PRIOR WRITTEN CONSENT OF
;       TYMSHARE.
;
;
;****************************************
;       S E T U P                       *
;****************************************
;       SETUP
;               FORTRAN CALL:   CALL SETUP(ARRY,S,M)
;               PUTS A BYTE POINTER FOR ARRY IN "M" WITH
;               BYTE SIZE S
;
        ENTRY   SETUP
SETUP:  0
        HRLZ    5,@1(16)        ;GET BYTE SIZE
        LSH     5,6             ;POSITION SHIFT
        IOR     5,[440000,,0]   ;BUILD BYTE POINTER
        HRR     5,0(16)         ;PUT IN ADDRESS
        MOVEM   5,@2(16)         ;GIVE RESULT
        JRA     16,3(16)        ;GO BACK
;****************************************
;       F I L L I T                     *
;****************************************
;
;       FILLIT
;               FORTRAN CALL:   CALL FILLIT(CHAR,M)
;               PUT THE BYTE IN CHAR INTO ARRY POSITION POINTED BY M
;
        ENTRY   FILLIT
FILLIT: 0
        MOVE    5,@0(16)        ;GET CHAR
        IDPB    5,@1(16)        ;DEPOSIT CHAR
        JRA     16,2(16)        ;RETURN
;****************************************
;       X M I T                         *
;****************************************
;
;       XMIT
;               FORTRAN CALL:   CALL XMIT(ARRY,PORT)
;               TRANSMITS 66 8 BIT BYTES FROM ARRAY
;
        ENTRY   XMIT
XMIT:   0
        HRRZ    4,0(16)        ;GET ARRAY ADDRESS
        HRLI    4,441000        ;MAKE BYTE POINTER FOR 8 BITS
        MOVEM   4,PNT           ;SAVE AWAY POINTER
;
        HRRZ    4,[0,,102]      ;SET UP REG 4 AS A COUNTER
        HRLZ    6,@1(16)        ;GET THE PORT FOR THE AUXCAL
        HRR     6,[0,,3]        ;PACK IN THE REQUEST TYPE
;
SHIPIT: ILDB    5,PNT           ;GET THE BYTE
        AUXCAL  6,5             ;SHIP IT
        SOJG    4,SHIPIT        ;LOOP ON COUNT
;
;       DATA SENT.....LEAVE
;
        JRA     16,2(16)
PNT:    BLOCK   1
;****************************************
;       D U M P I T                     *
;****************************************
;
;       DUMPIT
;               FORTRAN CALL:   CALL DUMPIT(CHAR,M)
;               FETCHES THE NEXT BYTE FROM THE LOCATION
;               SET BY M (A BYTE POINTER) INTO CHAR.
;
        ENTRY   DUMPIT
DUMPIT: 0
        ILDB    5,@1(16)
        MOVEM   5,@0(16)
        JRA     16,2(16)
;****************************************
;       N X T R E C                     *
;****************************************
;
;       NXTREC
;
;       FORTRAN CALL:   CAL NXTREC(PORT,DATA,ERR)
;
;       ERR < 0         NO DATA READY
;       ERR = 0         RECORD PRESENT
;       ERR = 1         CIRCUIT LOST
;
        ENTRY NXTREC
NXTREC: 0
        HRRZI   4,1             ;SET ERROR TYPE
        MOVE    6,[-1,,PORTS]   ;CHECK THAT THERE IS...
        AUXRED  6,              ;A PORT
        JUMPA   0,NXTRET         ;ERR RETURN
;
        JUMPE   6,NXTRET        ;ERROR RETURN NO PORTS
;
;       DO WE ALREADY HAVE DATA
;
NXTDCK: SETZ    4,              ;ASSUME MORE COMMING
        SKIPE   0,DATCNT        ;SKIP IF NO DATA
        JUMPA   0,NXTDAT        ;GET NEXT RECORD
;
;       MUST BRING IN MORE DATA
;
        HRRZI   5,^D400         ;SET COUNTER
        MOVEM   5,DATA
        HRLZ    5,@0(16)        ;GET A PORT
        IORI    5,36            ;MAKE SKIP IF BLOCK PRESENT
        AUXCAL  5,DATA
        SETO    4,              ;NO DATA SET ERR RETURN
        JUMPL   4,NXTRET        ;GO DO A RETURN IF NO DATA
;
;       WE HAVE NEW DATA SET UP BYTE POINTER
;       AND PROCESS
;
        MOVE    5,DATPT         ;GET STANDARD POINTER
        MOVEM   5,DATPNT        ;PUT IN WORK POINTER
        MOVE    6,DATA          ;SET UP COUNTER
        MOVEM   6,DATCNT        ;
;
;       GOTO PROCESS FOR CURRENT STATE
;
NXTDAT: MOVE    5,STATE         ;GET STATE
        JUMPA   0,@STATBL(5)    ;GO TO RIGHT PROCESS
;
;       STARTING A NEW RECORD
;
NXTNEW: HRRZ    5,1(16)         ;MAKE A POINTER TO OUTPUT
        HRLI    5,441000
        MOVEM   5,OUTPNT
;
        MOVE    6,DATCNT        ;GET REMAINING COUNT
;
NXTSYN: ILDB    5,DATPNT        ;GET A BYTE
        CAIN    5,62            ;CHECK IF A SYNC
        JUMPA   0,INSYN         ;GOT A SYNC
;
        SOJG    6,NXTSYN        ;LOOP FOR MORE
        SETZM   0,DATCNT        ;CLEAR COUNT
        JUMPA   0,NXTDCK        ;GO FIND MORE DATA
;
INSYN:  SOJ     6,              ;BUMP PAST FIRST SYNC
        MOVEM   6,DATCNT        ;SET UP DATA COUNT
        IDPB    5,OUTPNT        ;SEND ON SYNC
        HRRZI   7,^D65          ;SET OUTPUT COUNT
        MOVEM   7,OUTCNT        ;
        AOS     0,STATE         ;BUMP STATE
        JUMPA   NXTDCK          ;GET NEW RECORD IF REQUIRED
;
;       TRANSFERRING DATA
;
NXTMOV: MOVE    6,DATCNT        ;GET CURRENT COUNT
NXTCHR: ILDB    5,DATPNT        ;GET NEXT CHAR
        IDPB    5,OUTPNT        ;PUT IT AWAY
        SOSG    0,OUTCNT        ;DO OUTPUT COUNT
        JUMPA   0,RECEND        ;GO CLOSE REC
        SOJG    6,NXTCHR        ;GO DO NEXT CHAR
;
        SETZM   6,DATCNT        ;CLEAR THE COUNT
        JUMPA   0,NXTDCK        ;GET MORE DATA
;
RECEND: MOVEM   6,DATCNT        ;UPDATE DATA COUNTER
        SOS     0,DATCNT        ;UPDATE FOR LAST DATA REMOVED
        SETZM   0,@2(16)        ;SET FLAG
        SETZM   0,STATE         ;SET BACK TO SYNC STATE
        JRA     16,3(16)        ;RETURN
;
NXTRET: MOVEM   4,@2(16)        ;SET ERROR RETURN
        JRA     16,3(16)
;
;       DATA AREA
;
DATCNT: 0
DATA:   BLOCK   1
DTA:    BLOCK   ^D100
DATPT:  POINT   8,DTA
DATPNT: DATA+1
STATE:  0
STATBL: NXTNEW
        NXTMOV
OUTPNT: 0
OUTCNT: 0
PORTS:  BLOCK   3
;****************************************
;       B I O S T R                     *
;****************************************
;
;       BIOSTR
;               FORTRAN CALL:   CALL BIOSTR(PORT,ERR)
;
;               PLACES PORT IN BLOCK MODE,
;       IF SUCCESSFUL IT RETURNS ERR=0 AND
;       IF NOT ERR=-1
;
        ENTRY   BIOSTR
BIOSTR: 0
        SETZ    4,              ;PRESET ERROR RETURN
        HRLZ    5,@0(16)        ;GET PORT
        IORI    5,34            ;MAKE A START  BLOCK I/O
        AUXCAL  5,              ;
        SETO    4,              ;RETURN -1 IF AN ERROR
        MOVEM   4,@1(16)        ;SET ERROR RETURN
        JRA     16,2(16)        ;RETURN
;****************************************
;       S U M S                         *
;****************************************
;
;       SUMS
;               FORTRAN CALL:   CALL SUMS(REC,ERR)
;
;               REC CONTAINS A RECORD IMAGE AS RECEIVED FROM THE
;               NET.  IF THE CHECKSUM IS GOOD ERR=0, IF BAD ERR=1
;
        ENTRY   SUMS
SUMS:   0
        HRRZ    4,0(16)         ;GET REC ADDR
        HRLI    4,242000        ;MAKE A POINTER
;
        SETZ    ^D8,            ;CLEAR SUMS
        SETZ    6,
;
        HRRZI   3,^D31          ;SUM FOR 30 BYTES
;
SUM01:  ILDB    5,4             ;GET A WORD
        XOR     ^D8,5             ;SUM VIRTICAL
        SETZ    7,              ;CLEAR OUT 7 
        LSHC    6,-^D15         ;SEPARATE HI BIT AND THE REST
        LSH     7,-^D20         ;RT JUST RT 15 BITS IN 16 BIT FRAME
        IOR     6,7             ;PUT BACK TOGETHER AGAIN
        XOR     6,5             ;MAKE SUM
;
        SOJG    3,SUM01         ;LOOP ON RECORD
;
        JUMPN   ^D8,BADS          ;CHECK VIRTICAL SUM
;
        ILDB    5,4             ;VIRTICAL OK...
        XOR     5,6             ;CHECK DIAGONAL
        JUMPN   5,BADS
;
        SETZM   0,@1(16)        ;GOOD SUM
        JRA     16,2(16)        ;RETURN
;
BADS:   HRRZI   5,1
        HRRZM   5,@1(16)        ;SET BAD SUM
        JRA     16,2(16)
;****************************************
;       I N S E R T                     *
;****************************************
;
;       INSERT
;               FORTRAN CALL:   CALL INSERT(REC,BLKPTR)
;       REC IS A FORTRAN ARRAY CONTAINING 16 BIT DATA
;       TO BE SUTFFED AT BLKPTR.  30 WORDS ARE TRANSFERRED.
;
        ENTRY INSERT
INSERT: 0
        MOVE    3,@1(16)
        HRRZ    4,@2(16)
        HRRZ    5,0(16)                
;
INS01:  HRRZ    6,0(5)          ;GET THE DATA
        IDPB    6,3             ;PUT IT AWAY
        AOJ     5,              ;BUMP ADDRESS
        SOJG    4,INS01         ;LOOP ON BLOCK
;
        MOVEM   3,@1(16)
        JRA     16,3(16)        ;RETURN...DONE
;*****************************************
;       S H I P                         *
;****************************************
;
;       SHIP
;               FORTRAN CALL:   CALL SHIP(REC,PORT,ERR)
;
;  TRANSMITS THE 30 16 BIT WORDS STARTING AT REC.
;       PRECEEDS WITH SYNC AND TRAILS WITH CHECKSUMS.
;
        ENTRY SHIP
;
;       REG 4 INPUT COUNTER
;
SHIP:   0
        SETZM   0,@2(16)        ;SET ERROR TO 0
        HRRZI   4,^D30          ;XFER 30 16 BIT BYTES
        HRRZ    3,0(16)        ;GET ADDRESS
        HRLI    3,442000        ;MAKE A BYTE PNTR
        MOVE    2,DATFIL        ;FETCH THE OTHER BYTE POINTER
;
        SETZ    ^D8,            ;CLEAR VIRT SUM
        SETZ    6,              ;AND DIAGONAL
;
        HRRZI   5,31062         ;PUT IN A SYNC
        IDPB    5,2
        AOS     0,ODAT          ;BUMP CNT
;
SHMOV:  ILDB    5,3             ;GET A BYTE
        IDPB    5,2             ;PUT IT AWAY
        XOR     ^D8,5           ;VIRTICAL SUM
;
        SETZ    7,              ;DIAGONAL SUM
        LSHC    6,-^D15         ;SEPARATE HI BIT
        LSH     7,-^D20         ;RT JUST
        IOR     6,7     ;PUT 'EM TOGETHER
        XOR     6,5             ;SUM IT UP
;
        AOS     0,ODAT          ;BUMP OUT CNTR
        SOJG    4,SHMOV         ;LOOP ON MOVE
;
        IDPB    ^D8,2           ;PUT IN VIRT SUM
        AOS     0,ODAT          ;COUNT IT
;
        SETZ    7,              ;INCLUDE VIRT IN DIAG
        LSHC    6,-^D15
        LSH     7,-^D20
        IOR     6,7
        XOR     6,^D8
        IDPB    6,2             ;PUT AWAY DIAG
        AOS     0,ODAT
        MOVEM   2,DATFIL        ;SAVE FILL POINTER
;
        HRRZI   5,^D99          ;BUFFER FULL?
        CAME    5,ODAT
        JRA     16,3(16)        ;NO RETURN
;
        HRRZI   5,^D198         ;SET UP COUNT
        HRRZM   5,ODAT          ;TUCK AWAY THE COUNT
        HRLZ    5,@1(16)        ;GET PORT
        HRRI    5,37            ;SET FOR OUTPUT BYTE
        AUXCAL  5,ODAT
        JUMPA   CIRLOS          ;REPORT CIRCUIT LOST
        SETZM   0,ODAT
;
        MOVE    5,DATOTP        ;RESET BYTE PNTR
        MOVEM   5,DATFIL
        JRA     16,3(16)        ;DONE
CIRLOS: HRRZI   4,1             ;REPORT CIRCUIT LOST
        HRRZM   5,@2(16)
        JRA     16,3(16)        ;RETURN
;
ODAT:   0
SHPDAT: BLOCK   ^D100
SHIN:   0
DATFIL: POINT   ^D16,SHPDAT
DATOTP: POINT   ^D16,SHPDAT
;****************************************
;       X P U R G                       *
;****************************************
;
;       XPURG
;               FORTRAN CALL:   CALL XPURG(PORT)
;       EMPTIES THE OUTPUT BUFFER
;
        ENTRY   XPURG
XPURG:  0
        SKIPN   0,ODAT          ;BUFFER EMPTY
        JRA     16,1(16)        ;YES RETURN
;
        MOVE    5,ODAT          ;DOUBLE THE COUNT
        LSH     5,1
        MOVEM   5,ODAT
;
        HRLZ    5,@0(16)        ;GET PORT
        HRRI    5,37
        AUXCAL  5,ODAT
        EXIT
        SETZM   0,ODAT
;
        MOVE    5,DATOTP
        MOVEM   5,DATFIL
        JRA     16,1(16)                ;RETURN
;******************************************
;       R W A I T                       *
;****************************************
;
;       RWAIT
;               FORTRAN CALL:   CALL RWAIT(PORT,SEC)
;
;       WAITS A MAXIMUM OF SEC SECONDS FOR DATA.  IF DATA
;       ARRIVES RETURN IMMEDIATLY
;
        ENTRY   RWAIT
RWAIT:  0
        HRLZ    5,@0(16)
        LSH     5,11
        HRR     5,@1(16)
        IOR     5,[111,,0]
        HIBER   5,
        JRA     16,2(16)
        JRA     16,2(16)
;**********************************************
;************************************************
;       S N D R                                 *
;************************************************
;
;       SNDR
;               FORTRAN CALL:   CALL SNDR(RPOS,BLNT,PORT,ERR)
;               GET DATA FROM DISK AND SENDIT ON PORT
;
        ENTRY   SNDR
        EXTERNAL GETRC
SNDR:   0
        HRLZ    5,@2(16)        ;GET PORT
        HRRI    5,34            ;SET BLOCKI/O
        AUXCAL  5,
        JUMPA   BIOERR          ;PROCESS BIO ERROR
;
        HRRI    5,40            ;TERMINATE BLOCK INPUT
        AUXCAL  5,
        HRRI    5,37            ;SET UP FOR BLOCK OUTPUT
;
        MOVE    0,0(16)         ;GET ADDR OF RPOS
        MOVEM   0,SND010        ;BUILD DATA CALL
;
        HRRZ    13,@1(16)       ;R-13 IS TOTAL BYTE CNT
        MOVE    12,SND900       ;R12 IS POINTER TO FILE INPUT
        MOVE    11,SND930       ;R11 IS POINTER TO OUTPUT BLOCK
        SETZ    10,             ;OUTPUT BYTE COUNT
        SETZ    6,              ;CLEAR ZERO COMPRESSION
        SETZ    7,              ;CLEAR 377 COMPRESSION
;
SND000: MOVE    14,16           ;SAVE RETURN LINK
        JSA     16,GETRC        ;GET SOME DATA
        JUMP    SNDD            ;DATA ADDRESS
SND010: BLOCK   1               ;RECORD POSITION
;
        MOVEM   14,16           ;PUT BACK RETURN LINK
;
SND020: ILDB    0,12            ;GET A BYTE
        CAIL    0,400   ;SKIP IF NO COMPRESSION
        JUMPA   SND050          ;BR COMPRESSION
;
;       SHIP THE BYTE
;
        JSA     14,SBYT
        SOSN    13              ;DROP BYTE COUNT
        JUMPA   SND070          ;DONE, CLEAN UP
;
SND030: CAME    12,SND910       ;AT END OF FILE INPUT?
        JUMPA   0,SND020        ;NO
;
        MOVE    12,SND920       ;RESET POINTER
        JUMPA   0,SND000
;
SND050: ANDI    0,377           ;STRIP COMPRESSION BIT
        MOVE    2,0             ;REG 2 HOLDS FIRST BYTE
        ILDB    3,12            ;REG 3 HOLDS SECOND BYTE
        ILDB    1,12            ;REG 1 HOLDS COUNT
        LSH     1,9             ;MOVE OVER FOR SECOND HALF
        ILDB    0,12
        IOR     1,0             ;PUT TOGETHER THE HALVES
;
SND060: MOVE    0,2             ;COPY FIRST BYTE TO 0
        JSA     14,SBYT         ;SENDIT
        MOVE    0,3             ;COPY SECOND BYTE TO 0
        JSA     14,SBYT         ;SEND IT
;
        SOS     13
        SOSG    13              ;SKIP IF NOT DONE
        JUMPA   SND070          ;
;
        SOJGE   1,SND060        ;LOOP TILL COMPRESSION DONE
        JUMPA   SND030
;
SND070: CAIE    6,0             ;ZERO COMPRESSION PENDING?
        JSA     14,CLOSZ        ;CLOSE ZERO COMPR
;
        CAIE    7,0             ;377 COMPRESSION PENDING?
        JSA     14,CLOSF        ;CLOSE 377 COMPRESSION
        CAIN    10,0            ;LAST BLOCK FULL?
        JUMPA   SND080          ;YES
;
        MOVEM   10,ODAT         ;PUT OUT COUNT
        AUXCAL  5,ODAT          ;SEND REMAINDER
        JUMPA   OUTERR          ;BR ON ERROR
;
SND080: HRRI    5,42            ;TERMINATE BLOCK INPUT
        AUXCAL  5,
;
        SETZ    0,
        MOVEM   0,@3(16)        ;REPORT NO ERRORS
        JRA     16,4(16)        ;RETURN
;
BIOERR: HRRZI   0,1             ;REPORT ERROR
        MOVEM   0,@3(16)        ;RETURN
        JRA     16,4(16)
;
OUTERR: HRRZI   0,2             ;REPORT ERROR
        MOVEM   0,@3(16)
        JRA     16,4(16)
;
;
SNDD:   BLOCK   200
SND900: POINT   9,SNDD+2
SND910: POINT   9,SNDD+177,35
SND920: POINT   9,SNDD
SND930: POINT   8,SHPDAT
;
;       SBYT   CHECK THE NEED FOR AN EXCAPE SEQUENCE AND
;       SEND THE DATA
SBYT:   0
        SKIPE   0,@4(16)        ;SKIP IF COMPRESSION PERMITTED
        JUMPA   SBY010          ;DO NOT COMPRESS
        CAIN    0,0             ;IS IT ZERO?
        JUMPA   SBY060          ;DO ZERO COMPR
;
        CAIN    0,377           ;IS IT 377?
        JUMPA   SBY080          ;DO 377 COMPR
;
        CAIE    6,0             ;ZERO COMP PEND?
        JSA     14,CLOSZ        ;CLOSE ZERO COMPR
;
        CAIE    7,0             ;377 COMP PENDING?
        JSA     14,CLOSF        ;CLOSE ZERO COMP
SBY010: JSA     14,SBTEX        ;SEND WITH EXCAPES
        JRA     14,0(14)        ;RETURN
;
;
;GOT A ZERO, COMPRESS IT
;
SBY060: CAIE    7,0             ;377 COMP PENDING?
        JSA     14,CLOSF
;
        AOS     6               ;INCREMENT COUNT
        CAIN    6,377           ;COUNT AT MAX?
        JSA     14,CLOSZ
        JRA     14,0(14)        ;RETURN
;
SBTEX:  0
;
SBY020: CAIGE   0,200           ;NEED ESCAPE?
        JUMPA   SBY030          ;NO
        CAIG    0,217           ;MAYBE
        JUMPA   SBY050          ;SEND ESCAPE
;
SBY030: CAIN    0,3             ;CHECK FOR 3
        JUMPA   SBY050          ;YES
;
        CAIN    0,17            ;CHECK FOR 17
        JUMPA   SBY050          ;YES
;
SBY040: JSA     14,PBYT         ;PUT IT AWAY
        JRA     14,0(14)
;
SBY050: XORI    0,100           ;ALTER DATA
        MOVEM   0,SHIN          ;SAVE 0
        HRRZI   0,200           ;SEND AN ESCAPE
        JSA     14,PBYT
        MOVE    0,SHIN          ;RESTORE REG 0
        JSA     14,PBYT         ;PASS IT ON ALSO
        JRA     14,0(14)        ;RETURN
SBY080: CAIE    6,0             ;ZERO COMP PENDING?
        JSA     14,CLOSZ
;
        AOS     7               ;INCREMENT COUNT
        CAIN    7,377           ;COUNT AT MAX?
        JSA     14,CLOSF
        JRA     14,0(14)
;
CLOSZ:  0
        MOVEM   0,4             ;SAVE CURRENT REG 0
        CAIN    6,1             ;IS IT ONLY ONE?
        JUMPA   CLZ020
;
        HRRZI   0,204           ;ZERO FILL PREFIX
        JSA     14,PBYT         ;SEND IT
        MOVEM   6,0             ;GET COUNT
CLZ010: JSA     14,SBTEX        ;SEND WITH EXCAPES IF REQUIRED
        MOVEM   4,0             ;RESTORE 0
        SETZ    6,              ;CLEAR 6
        JRA     14,0(14)
;
CLZ020: SETZ    0,              ;JUST SEND THE ZERO
        JUMPA   CLZ010
;
;
CLOSF:  0
        MOVEM   0,4             ;SAVE CURRENT REG 0
        CAIN    7,1             ;IS IT ONLY 1
        JUMPA   CLF020
;
        HRRZI   0,205           ;377 FILL PREFIX
        JSA     14,PBYT         ;SEND IT
        MOVEM   7,0             ;GET COUNT
CLF010: JSA     14,SBTEX        ;SEND IT
        MOVEM   4,0             ;RESTORE 0
        SETZ    7,              ;SET COUNT TO 0
        JRA     14,0(14)
;
CLF020: SETO    0,              ;JUST SEND THE ZERO
        JUMPA   CLF010
;
;
PBYT:   0
        IDPB    0,11            ;PUT BYTE IN BUFFER
        AOS     10              ;INC COUNT
        CAIGE   10,^D400        ;CHECK FOR FULL
        JRA     14,0(14)        ;RETURN
;
        MOVEM   10,ODAT         ;SET COUNT
        AUXCAL  5,ODAT          ;SEND IT
        JUMPA   OUTERR
;
        SETZ    10,             ;CLEAR COUNT
        MOVE    11,SND930       ;RESET POINTER
        JRA     14,0(14)
;************************************************
;       ISEN                                    *
;************************************************
;
;       ISEN
;               FORTRAN CALL    CALL ISEN(CHAR,PORT,ERR)
;               OUTPUTS CHAR ON PORT.  SENDS ESCAPES IF
;               REQUIRED.
;
        ENTRY   ISEN
ISEN:   0
        HRRZ    0,@0(16)
        HRLZ    3,@1(16)        ;SET UP PORT FOR AUXCAL
        HRRI    3,3
;
        CAIGE   0,200           ;NEED ESCAPE?
        JUMPA   ISN030
        CAIG    0,217
        JUMPA   ISN050          ;YES
;
ISN030: CAIN    0,3             ;3?
        JUMPA   ISN050          ;YES
;
        CAIN    0,17            ;17?
        JUMPA   ISN050          ;YES
;
ISN040: AUXCAL  3,0
        SETZM   @2(16)          ;NO ERROR
        JRA     16,3(16)        ;RETURN
;
ISN050: HRRZI   10,200          ;SEND ESCAPE
        AUXCAL  3,10
        XORI    0,100
        JUMPA   ISN040
;                                       *
;***********************************
;       SETWS
;***********************************
;
;  SETS THE WORKING SET SIZE TO A LIMIT OF 15 PAGES
; THEN LETS THE WORKING SET SIZE EXPAND AS REQUIRED
;
        ENTRY   SETWS
SETWS:  0
        MOVE    5,[5,,WSMAX]
        CALLI   5,-74
        JRA     16,0(16)
        MOVE    5,[2,,[10]]
        CALLI   5,-74
        JRA     16,0(16)
        MOVE    5,[2,,WSMAX]
        CALLI   5,-74
        JRA     16,0(16)
        JRA     16,0(16)
WSMAX:  0
;****************************************************************
;       ERRLOG
;****************************************************************
;
;       ERRLOG
;         FORTRAN CALL  CALL ERRLOG(ERR)
;       PRINTS TEXT FOR LOGIN ERROR CODES
;
        ENTRY   ERRLOG
ERRLOG: 0
        HLRZ    4,@0(16)        ;GET LEFT HALF ARGUMENT
        CAIE    4,0     ;SKIP IF SUP ERROR
        JUMPA   ERRL20          ;GO PROCESS PDP ERR
;
ERRL10: HRRZ    4,@0(16)        ;GET SUP CODE
        CAIL    4,12            ;SKIP IF RECOGNIZED
        SETZ    4,              ;UNRECOGNIZED ZERO
;
        OUTSTR  @SUPMSG(4)      ;GET SUP MESSAGES
        OUTSTR  ERCRLF          ;CRLF
        JRA     16,1(16)        ;RETURN
;
ERRL20: CAIL    4,7
        JUMPA   ERRL10          ;UNDEFINED OR 7
;
        OUTSTR  @PDPMSG(4)      ;PUT OUT MESSAGE
        OUTSTR  ERCRLF          ;CR LF
        JRA     16,1(16)
;
SUPMSG: BRES
        FMT
        BUSR
        MUDE
        SYSU
        SYSU
        TIM
        NACC
        NPOR
        BUSY
;
PDPMSG: BRES
        TCIR
        NAC
        NMON
        NSUP
        NSUP
        NSUP
;
FMT:    ASCIZ   /FORMAT ERROR/
BUSR:   ASCIZ   /BAD USERNAME/
MUDE:   ASCIZ   /MUD ERR/
SYSU:   ASCIZ   /SYSTEM UNAVAILABLE/
TIM:    ASCIZ   /TIMOUT/
NACC:   ASCIZ   /ACCESS NOT PERMITTED/
NPOR:   ASCIZ   /OUT OF PORTS/
BUSY:   ASCIZ   /SUP BUSY/
TCIR:   ASCIZ   /TO MANY CIRCUITS/
NAC:    ASCIZ   /NO AC LICENCE/
NMON:   ASCIZ   /OUT OF MONITOR SPACE/
NSUP:   ASCIZ   /NO SUP RESPONSE/
BRES:   ASCIZ   /UNINTERPRETED CODE/
ERCRLF: 64240000000
;****************************************************************
;       HERRS
;****************************************************************
;
;       HERRS
;               FORTRAN CALL   CALL HERRS(J)
;               OUTPUT TEXT FOR ISIS DDT ERROR CODES
;
        ENTRY   HERRS
HERRS:  0
        HRRZ    4,@0(16)        ;GET ARGUMENT
        CAILE   4,41            ;OVER MAX?
        SETZ    4,              ;SET TO DUMMY
;
        OUTSTR  @DDTERR(4)      ;PUT OUT THE ERROR
        OUTSTR  ERCRLF          ;CR-LF
        JRA     16,1(16)
;
DDTHSH: ASCIZ   /HAND SHAKE/
DDTILG: ASCIZ   /ILLEGAL COMMAND/
DDTINS: ASCIZ   /ILLEGAL INSTRUCTION/
DDTBRK: ASCIZ   /BREAKPOINT ENCOUNTERED/
DDTADR: ASCIZ   /ADDRESS OUT OF RANGE/
DDTBPR: ASCIZ   /BREAKPOINT # OUT OF RANGE/
DDTNBP: ASCIZ   /NO BREAKPOINT TO PROCEED FROM/
DDTMEM: ASCIZ   /MEMORY OUT OF BOUNDS/
DDTREG: ASCIZ   /ILLEGAL REGISTER/
DDTDSC: ASCIZ   /DISK ERROR/
DDTNUM: ASCIZ   /INVALID NUMBER/
DDTBND: ASCIZ   /INCORRECT BOUNDS/
DDTBSL: ASCIZ   /BAD SEARCH LENGTH/
DDTBSB: ASCIZ   /BAD SEARCH BOUNDRY/
DDTSVC: ASCIZ   /ILLEGAL SVC EXECUTED/
DDTILM: ASCIZ   /ILLEGAL MEMORY REFERENCE OUT-SVC/
DDTPAR: ASCIZ   /PARITY ERROR/
DDTIMP: ASCIZ   /ILLEGAL MEM REF BY PROGRAM/
DDTLOG: ASCIZ   /LOGGING OFF?/
DDTJMP: ASCIZ   /JUMP OUT OF BOUNDS/
DDTCON: ASCIZ   /SLOT ALREADY CONNECTED/
DDTBKE: ASCIZ   /BREAKPOINT ALREADY EXIXTS/
DDTDPC: ASCIZ   /DISPATCHER CRASH/
DDTM:   ASCIZ   /BAD 'M' ARGUMENTS/
DDTSPY: ASCIZ   /SPY IN USE/
DDTABT: ASCIZ   /ABORTING/
DDTLIC: ASCIZ   /INSUFFICIENT LICENSE/
DDTFRZ: ASCIZ   /FROZEN/
DDTMSU: ASCIZ   /MAC SET INCORRECTLY/
DDTOS:  ASCIZ   /OUT OF SPACE/
DDTDSP: ASCIZ   /DISC SAVE AREA PROTECTED/
;
DDTERR: BRES    
        BRES
        DDTHSH
        DDTILG
        DDTINS
        DDTBRK
        DDTADR
        DDTBPR
        DDTNBP
        DDTMEM
        DDTREG
        DDTDSC
        DDTNUM
        DDTBND
        DDTBSL
        DDTBSB
        DDTSVC
        DDTILM
        DDTPAR
        DDTIMP
        DDTLOG
        DDTJMP
        DDTCON
        DDTINS
        DDTBKE
        DDTDSP
        DDTM
        DDTSPY
        DDTABT
        DDTLIC
        DDTFRZ
        DDTMSU
        DDTOS
        DDTDSP
;************************************************************
;       RSCAN
;************************************************************
;
;       FORTRAN CALL    CALL RSCAN
;
;               SETS INPUT BACK TO THE COMMAND LINE
;
        ENTRY   RSCAN
RSCAN:  0
        RESCAN
        JRA     16,0(16)
;**************************************************************
;
;       FORTRAN CALL    CALL YBLCHK(PORT,ERR)
;               SETS ERR TO 1 IF YELLOW BALL SEEN...0 IF NOT
;
        ENTRY   YBLCHK
YBLCHK: 0
        MOVE    5,[002000,,YBLST]
        POLPRT  5,
        JUMP    YBLCH1
YBLCH1: JUMPE   5,YBLCH2
        HRRZ    6,YBLST(5)      ;GET PORT NUMBER
        CAME    6,@(16)         ;COMPARE TO REQUESTED PORT NUMBER
        SOJ     5,YBLCH1+1
;
YBLCH2: MOVEM   5,@1(16)
        JRA     16,2(16)
;
YBLST:  2
        0
        0
;****************************************************************
;       B I O N X T
;****************************************************************
;
;       BIONXT
;               FORTRAN CALL BIONXT(DATA,PORT,ERR)
;
;       READ THE NEXT BYTE FROM THE CIRCUIT USING BIO
;
        ENTRY   BIONXT
;
BIONXT: 0
        SETZ    4,
        SKIPE   0,DATCNT        ;SKIP IF NO DATA
        JUMPA   BIGOTD
;
BIOAGN: HRRZI   5,^D400         ;INPUT CNT
        MOVEM   5,DATA
        HRLZ    5,@1(16)        ;GET DATA
        HRRI    5,35
        AUXCAL  5,DATA
        SETO    4,              ;ERROR RESPONSE
        JUMPL   4,BIEXT         ;ESIT IF ERROR
;
;       WE HAVE DATA
;
        MOVE    5,DATPT         ;SET UP DATA POINTER
        MOVEM   5,DATPNT
        MOVE    6,DATA          ;SET UP COUNTER
        MOVEM   6,DATCNT
;
        JUMPE   BIOAGN          ;READ AGAIN IF NO DATA
        OUTSTR  [ASCIZ /*/]
BIGOTD: ILDB    5,DATPNT        ;MOVE OUT THE DATA
        MOVEM   5,@0(16)
;
        SOS     DATCNT
BIEXT:  MOVEM   4,@2(16)
        JRA     16,3(16)
        END
  ,^^Û