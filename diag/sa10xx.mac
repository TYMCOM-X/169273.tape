

TITLE SA10 VERSION 1.4 - DIAGNOSTIC FOR SYSTEMS CONCEPTS SA-10 IBM-TO-DEC SUBSYSTEM ADAPTER
		;AS OF 2-NOV-76

;TYMSHARE PARAMETERS

BASE=300
TUNIT=200	;MAGTAPE ADDRESS
;IDEV=300	;DISK ADDRESS

A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
I=11
J=12
K=13
L=14
BF=16		; BUSY FLAGS, FOR LIGHT WATCHERS. B0-7 MEAN DRIVE
		;  0-7 WAITING FOR DEVICE END. RH IS MOST RECENT INT DV
P=17		; PUSHDOWN POINTER

	TT=13
	FORT=14
	FT=15
	ST=16
	ELEV=11
	TEN=10
IFNDEF CV,<
CV==1>		; 0 FOR B, 1 FOR C (HARDWARE POINTER LAYOUTS)
IFLE CV,<
CNTSFT==2>	;SHIFT IN LH FOR COUNT
IFG CV,<
CNTSFT==6>	; ..
IFNDEF EADR,<
EADR==1>	;22 BIT ADDRESS FEATURE
IFNDEF BASE,<
BASE==300>	;CHNL 0 GO ADR
SA0==274	;DEVICE NUMBER OF SA-10
;IDEV==300	;DEVICE TO USE FOR UCODE SIM
;IFNDEF DCHAN,<DCHAN==1>		;DISK SUBCHANNEL
;IFNDEF TCHAN,<TCHAN==0>		;TAPE SUBCHANNEL
IFNDEF PCHAN,<PCHAN==1>		; PRINTER SUBCHANNEL
APRCH==2	;PI CH FOR TIME, PAR ERR, AND NXM
IFNDEF RECL,<
RECL==200>	;LENGTH OF RECORD ON DISK
NRPT==^D18	;NUMBER OF RECORDS PER TRACK
IFNDEF NPACKS,<
NPACKS==^D16>	;NUMBER OF DISK PACKS TO RUN IN DSKT
IFNDEF NCHN,<
NCHN==2>	;# OF CHANNELS SIMULATED BY THIS
		;  PARTICULAR SA-10 (2 OR 4)
SA0CH==1	;PI CHANNEL IN DISK ROUTINE
NBF==20		; # OF BYTES IN RING BUFFER (FIFO) IN EACH CHANNEL
NRETRY==^D8	; TIMES TO REREAD A CORRECTABLE DATA CHECK
NBADSP==100	; NUMBER OF BADSPOTS TO REMEMBER

;MACROS FOR WORD POINTERS DEPENDING ON WHICH KIND OF SA10 (B OR C)

IFE CV,<
	DEFINE IOW(CT,ADR)
	<-CT*4,,ADR>
	DEFINE IOWC(CT,ADR)
	<400000-CT*4,,ADR>>
IFN CV,<
	DEFINE IOW(CT,ADR)
	<-CT*100,,ADR>
	DEFINE IOWC(CT,ADR)
	<400000-CT*100,,ADR>>

;BITS FOR CONO
RSTSA==400000	;RESET WHOLE SCHMEER, START CLOCK
CLMERR==200000	;CLEAR MEMORY ERROR FLAGS
SASTEP==1000	;STOP CLOCK IF RUNNING; STEP IT IF STOPPED
SACGO==1040	;START CLOCK
RSTCH==300	;RESET CHANNEL SPECIFIED BY 10 AND 20 BITS
CLBSY==400	;CLEAR BUSY FLAG OF CHANNEL . . .
STBSY==440	;SET BUSY FLAG OF CHANNEL . . .
CLSRQ==500	;CLEAR STATUS REQUEST FLAG OF CHANNEL . . .
STSRQ==540	;SET STATUS REQUEST FLAG OF CHANNEL . . .
CLSTF==600	;CLEAR STATUS FLAG OF CHANNEL . . .
STSTF==640	;SET STATUS FLAG OF CHANNEL . . .
CLPIE==700	;CLEAR PI ENABLE OF CHANNEL . . .
STPIE==740	;SET PI ENABLE OF CHANNEL . . .

;CERTAIN OF ABOVE BITS ALSO SELECT WHAT DIAGNOSTIC WORD WILL
;BE READ BY THE NEXT DATAI TO BE EXECUTED, AS FOLLOWS:

RDCCI==0	;READ CHANNEL SELECTED (BITS 16, 17); CON-
		;TENTS OF CBUS (BITS 18-25); MICRO-PROGRAM
		;COUNTER (BITS 26-35)
RDMA==10	;READ MEMORY ADDRESS REGISTER (BITS 16-35)
RDMBL==20	;READ CHANNEL REFERENCING MEMORY (BITS 16,
		;17); LEFT HALF OF MEMORY BUFFER (BITS 18-
		;35).  THIS M.B. IS ONLY FOR DATA GOING
		;TOWARDS THE MEMORY.
RDMBR==30	;READ PARITY BIT OF MEMORY BUFFER (BIT 17);
		;RIGHT HALF OF MEMORY BUFFER (BITS 18-35)
RDCONI==40	;READ THE "CLOCK RUNNING" BIT (BIT 16); THE
		;"MEMORY SUBROUTINE BUSY" BIT (BIT 17); AND
		;THE SAME DATA SENSED BY CONI (BITS 18-35)
;CONTINUED ON NEXT PAGE


RDBITS==50	;READ SOME MISCELLANEOUS BITS:
		;BITS 16 AND 17 SPECIFY WHICH CHANNEL IS
		;USING THE MEMORY
	WRRQ==400000	;CURRENT MEM CYCLE IS A WRITE
	MUXACK==200000	;ACKNOWLEDGE FROM MEMORY PORT
	MEMACK==100000	;ACKNOWLEDGE FROM MEMORY
	MBUSDN==40000	;MEMORY BUS FINISHED
	MDNSYN==20000	;MEMORY DONE SYNC
	RDDIAG==10000
	IOFWR==4000
	CHRST==2000
	RESET==1000
	BRANCH==400	;THE MICRO-INSTRUCTION WHICH IS
			;ABOUT TO BE EXECUTED IS GOING TO BRANCH
;LOW 3 BITS ARE PI CHANNEL ASSIGNMENT

;BITS FOR CONI, CONSZ, CONSO
PIRQ==400000	;THIS SA-10 IS REQUESTING AN INTERRUPT
PARERR==200000	;PARITY ERROR IN SOME WORD (COMMAND OR
		;DATA) READ FROM MEMORY
NXM==100000	;SA-10 TRIED TO REFERENCE NON-EX MEMORY
PIE0==40000	;PI ENABLE FLAG FOR CHANNEL 0
PIE1==20000	; . . . CHANNEL 1
PIE2==10000	; . . . CHANNEL 2
PIE3==4000	; . . . CHANNEL 3
BSY0==2000	;BUSY FLAG FOR CHANNEL 0
BSY1==1000	; . . . CHANNEL 1
BSY2==400	; . . . CHANNEL 2
BSY3==200	; . . . CHANNEL 3
STF0==100	;STATUS FLAG FOR CHANNEL 0
STF1==40	; . . . CHANNEL 1
STF2==20	; . . . CHANNEL 2
STF3==10	; . . . CHANNEL 3
;LOW 3 BITS ARE PI CHANNEL ASSIGNMENT

HANGPC==64	;MAGIC PLACE TO STICK MICRO-PROGRAM COUNTER
		; THIS IS A MICRO-CODE PC. NOT A PDP-10 ADDRESS

;DEFS FOR TAPE
TRECL==2000	; RECORD LENGTH ON TAPE
;TUNIT==340

DSW==0
DEFINE LOOP(N,LOC)
<	DATAI DSW,0
;	HLRZS A
	CAIN 0,N
	JRST LOC
>

DEFINE SLOOP
<	DATAI DSW,0
	TLNE 0,2
	JRST GOGO
	TLNE 0,1
	JRST DDT
>

	DEFINE ALT	;ALTMODE TRAP
<	CONSO TTY,40
	JRST .+15
	DATAI	TTY,FT	;GET TTY INTO AC FT
	ANDI	FT,177	;STRIP OFF VALID DATA
	CAIN	FT,176	;LOOK FOR ALTMODE
	JRST	.+7		;IF DDT
	CAIN FT,175	;LOOK FOR ANOTHER FLAVOR ALT MODE
	JRST .+5
	CAIN FT,33	;STILL LOOKING FOR ALT MODE
	JRST .+3
	CAIE FT,124	;LOOK FOR LETTER T
	JRST .+3
	DATAO PAG,[400000,,400000]
	JRST DDT
>
	DEFINE NALT	;ALT MODE HANG
<	CONSO TTY,40
	JRST .-1
	DATAI TTY,FT
	ANDI FT,177
	CAIN FT,176
	JRST .+7
	CAIN FT,175
	JRST .+5
	CAIN FT,33
	JRST .+3
	CAIE FT,120	;LOOK FOR LETTER P
	JRST .-13
>
PAGE
LOC 4000

GOGO:	MOVEI P,PDL-1
	DATAO PAG,[400000,,400000]
	CONO 635550
	SETZM TABCNT
	PUSHJ P,SETPTR
	JRST DDTE
DDTG:	MOVE A,116
	MOVEM A,36
	MOVE A,117
	MOVEM A,32
	MOVE	A,	121	;.JBFF
	MOVEM	A,ADRLO
	JRST	MSG
DDTE:	SKIPN SUPER0
	JRST DDT
	MOVE A,SUPER0
	MOVEM A,36
	MOVE A,SUPER1
	MOVEM A,32
	SETZM SUPER0
	JRST DDT

MSG:	MOVEI P,PDL-1	;SET UP PUSH STACK
	PUSHJ P,SETPTR
	HALT .+1
	MOVEI C,[ASCIZ/


     PDP-10	SA10 DIAGNOSTIC
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/


*****TYPE HELP ALT G FOR LIST OF COMMANDS*****
/]
	PUSHJ P,STYO
	JRST DDT
PAGE

EXT:	SETOM ,EXETR	;SET UP FOR ONE PASS
	SKIPA
BEG:	SETZM ,EXETR
	CONO 635550	;CLEAR OUT THE WORLD
	DATAO	PAG,[0]	;CLEAR PAGING FOR BEG
	SETZM	ER28
	SETZM	BEGERR#		;REALLY A COUNT O F#PASSES
	CLEARM PIE	;NO INTERRUPTS ARE EXPECTED
	CONO PI,11577
	MOVE A,[JSR UUOH]
	MOVEM A,41	;INITIALIZE MAGIC MEMORY LOCATIONS
	MOVEI B,42	;ALL INTERRUPT ROUTINES ARE SAME LENGTH
	MOVE A,[JSR INT1]
PISETL:	MOVEM A,(B)
	ADDI A,INT2-INT1
	ADDI B,2
	CAIE B,60
	JRST PISETL
	CONO SA0,RSTSA!CLMERR
BTE:	CONO 200000	;I O RESET
	MOVEI A,^D300
	SOJG A,.	;DELAY A BIT
	CONI SA0,A
	TLNE A,1	;CONI SHOULD GIVE 1 IN L.H.
	JRST BTE2	;IF IT DID, OK
	CONO SA0,7	;OTHERWISE LOOK DEEPER
	CONI SA0,A
	TDNE A,[1,,7]	;ANY PIA BITS GET SET?
	JRST ERR50	;IF YES
	MOVEI A,5
	MOVEM A,PIE	;WE'LL TRY AN INTERRUPT
	CONO PI,2377	;TURN ON ALL CHANNELS
	CONO SA0,STSTF+6	;SHOULD NOT INTERRUPT YET
	MOVEI A,10
	CONO SA0,STPIE+5	;THIS SHOULD CAUSE INTERRUPT
	SOJG A,.	;GIVE IT A CHANCE
	CONO SA0,400000	;RESET SA-10
	CONO PI,11577	;CHANNELS OFF AGAIN
	SKIPN PIE
	JRST ERR51	;INTERRUPT OCCURRED
	CLEARM PIE	;INTERRUPT DIDN'T OCCUR
	JRST ERR52

BTE2:	CAME A,[1,,0]
	JRST ERR53	;SOME BAD BITS ON
	MOVSI C,40000	;WILL SWEEP THRU DEVICE CODE BITS
BTE2A:	MOVE B,[CONI SA0,A]
	XOR B,C	;CREATE CODE OF SIMILAR DEVICE
	XCT B	;CONI IT
	CAMN A,[1,,0]	;NO OTHER DEVICE CAN MAKE THIS STATEMENT
	JRST ERR54	;IF ONE TRIED
	LSH C,-1
	CAME C,[200,,0]
	JRST BTE2A	;IF MORE DEV ADDR BITS TO CHECK
	MOVEI A,10
	CONO PI,2377	;ENABLE ALL PI CHANNELS
	SOJG A,.	;WAIT NERVOUSLY
	CONO SA0,7	;SET PIA BITS
	CONI SA0,A
	CAME A,[1,,7]
	JRST ERR55	;IF PIA DOESN'T READ IN
	CONO 200000	;I O RESET SHOULD CLEAR PIA
	CONI SA0,A
	CONO PI,11577	;RESET PI
	CAME A,[1,,0]
	JRST ERR56	;PIA DIDN'T CLEAR
BTE4:	CONO SA0,RDCONI+7	;CAN WE DO A DATAI?
	DATAI SA0,A
	CAME A,[2,,7]	;CLEN, PIA
	JRST ERR57	;IF WRONG
	CONO SA0,SASTEP	;STOP THE CLOCK
	CONO	SA0,RDCONI	;NOW CHECK FOR CLOCK STOPPED
	DATAI SA0,A
	JUMPN	A,ERR58
BEG1AA:	MOVEI B,2*NCHN
BEG1A:	DATAO SA0,[0]
	CONO SA0,SASTEP!RDCCI
	DATAI SA0,C
	MOVE A,C
	LSH A,-22	;FLUSH CBUS AND MICRO-P.C.
	TDNE A,[-1_2]
	JRST ERR1	;DATAI READ IN EXTRANEOUS BITS
	JUMPN A,BEG1	;WAIT UNTIL WE GET TO CHANNEL 0
BEG1B:	LOOP 1,BEG1A
BEG1C:	LOOP 2,BEG1AA
	MOVSI A,-NCHN	;CLEAR OUT COUNT TABLE
BEG20:	CLEARM CCTBL(A)
	AOBJN A,BEG20
BEG20A:	MOVEI B,100*NCHN
BEG2:	DATAO SA0,[0]	;GET THE THING A NO-OP TO EXECUTE
	CONO SA0,SASTEP!RDCCI
	DATAI SA0,A
	LSH A,-22
	ANDI A,-1_<-44+2>
	AOS CCTBL(A)	;NOTE THAT THIS CHANNEL HAS BEEN COUNTED
	SOJG B,BEG2
	MOVSI A,-NCHN	;MAKE SURE ALL CHANNELS GOT SERVICE
BEG21:	SKIPN CCTBL(A)
	JRST ERR3	;THIS GUY DIDN'T GET COUNTED
BEG21A:	LOOP 3,BEG20A
	AOBJN A,BEG21	;CHECK THE OTHERS
;FALL THRU

;MAKE SURE ALL BITS OF MICRO-P.C. CAN BE SET
	PUSHJ P,STEPC0
	MOVEI A,^D10
	JSR PTN	;TRY VALUES FOR 10 BITS
IARL:	MOVE B,A
	DATAO SA0,B
	PUSHJ P,STEP0
	ANDI A,1777
	CAME A,B
	JRST ERR4	;MICRO-P.C. NOT BEING SET PROPERLY
IARL1:	LOOP 4,IARL
	DATAO SA0,[040000]	;CBUS_IARB
	DATAI SA0,A
	LDB A,[121000,,A]
	MOVE C,B
	ANDI C,377
	CAME A,C
	JRST ERR5	;CBUS_IARB LOSING
IARL2:	LOOP 5,IARL
	JSR PTN0	;ON TO NEXT DATA VALUE
;SEE IF SOURCE OF ZEROES WORKS
SZER:	MOVEI A,176320	;REG. 3_0
	PUSHJ P,C0XCT
	MOVEI A,072260	;START DUMMY WRITE FROM REG. 3
	PUSHJ P,C0XCT
	CONO SA0,RDMBL
	DATAI SA0,A	;SHOULD GIVE US 0 IN THE R.H. OF A
	TRNE A,-1
	JRST ERR40	;FAILS HERE IF NO ROMS INSTALLED YET
SZER1:	LOOP ^D40,SZER
MAMB:	MOVEI A,^D36
	JSR PTN	;TRY ALL 36 BITS OF WORD
MAMB1:	MOVE C,[200000,,TEST]
	PUSHJ P,LC0CW
	MOVE C,A
	HLRZM C,MAMBTL
	HRRZM C,MAMBTR
	PUSHJ P,LC0R2
	MOVEI A,062260	;START DUMMY WRITE FROM REG. 2
	PUSHJ P,C0XCT
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
	CONO SA0,RDMA
	DATAI SA0,C
	MOVE A,C
	AND A,[3,,-1]
	CAIE A,TEST
	JRST ERR11	;MEM ADDR REG HAS WRONG THING IN IT
MAMB1A:	LOOP ^D11,MAMB1
	CONO SA0,RDMBL
	DATAI SA0,A
	HRRZ B,A
	CAME B,MAMBTL
	JRST ERR6	;MB LEFT LOSING
MAMB2:	LOOP 6,MAMB1
	TLNE A,-1
	JRST ERR7	;WRONG CHANNEL HAS MEMORY SUBROUTINE
MAMB3:	LOOP 7,MAMB1
	CONO SA0,RDMBR
	DATAI SA0,A
	CAME A,MAMBTR
	JRST ERR12	;MB RIGHT LOSING
MAMB4:	LOOP ^D12,MAMB1
	JSR PTN0	;ON TO NEXT DATA VALUE
CWTST0:	MOVEI G,NCHN-1	;TRY OUT WC, CA, PC PATHS
CWTST:	PUSHJ P,STEPCX	;STEP CLOCK TO APPROPRIATE CHANNEL
CWTST1:	MOVEI A,^D36	;CHECK 36 BIT POSITIONS
	JSR PTN
	MOVEM	A,CWTA	;GET AND SAVE DATA
	SETCM	B,A	;COMPLEMENT OF A TO B
	LSH	B,1	;SLIGHTLY DIFFERENT #
	MOVEM	B,CWTB	;CWTA AND B JUST FOR NOTHING
	LDB	C,[POINT 11,A,11]	;GET WC FIELD
	DPB	C,[POINT 11,WCX,11]	;STORE IN MASKED LOC
	LDB	C,[POINT 20,A,35]	;GET ADDRESS FIELD
	DPB	C,[POINT 20,CAX,19]	;PUT IT IN BITS 0-X,IE.,16-32 OF ADDR
	LDB	C,[POINT 3,A,15]	;GET BITS 14 SND 15
	DPB	C,[POINT 3,CAX,23]	;TUCK IT AWAY
	LDB	C,[POINT 20,B,35]	;GET ADDRESS FLD. OF WORD 2
	DPB	C,[POINT 20,PCX,19]	;STORE THIS AWAY
	LDB	C,[POINT 3,B,15]	;DON'T FORGET THESE TWO
	DPB	C,[POINT 3,PCX,23]	;PRBABLY WRON FOR THIS POINT
	JRST CWTST2
	WCXBP:	POINT  8,WCX	;8 BIT BYTE  FROM WCX
	POINT 	8,CAX	;8 BIT BYTE FROM CAX
	POINT	8,PCX	;8 BITS FROM PCX
	
WCX:	400077400000
CAX:	0
PCX:	0
CWTST2:	MOVE C,CWTA
	PUSHJ P,LCXCW	;PUT INTO WC, CA REGISTERS
	MOVE C,CWTB
	PUSHJ P,LCXR2
	MOVEI A,065300	;PC_ADR FILED OF REG. 2
	PUSHJ P,CXXCT
	MOVEI C,040400	;CBUS_BYTE 1 OF WC
	MOVEI	H,0	;INDEX REG FOR 1 FO 3 BYTE POINTERS
	MOVEI F,3	;DO STUFF FOR EACH OF 3 REGISTERS
CWT1:	MOVEI B,3	;DO THIS STUFF FOR EACH OF 3 BYTES
	MOVE	J,WCXBP(H)	;GET THE BYTE POINTER
CWT2:	DATAO SA0,C	;GET DATA ONTO CBUS
	CONO SA0,RDCCI
	DATAI SA0,A
	LDB A,[121000,,A]	;GET CONTENTS OF CBUS
	ILDB D,J	;GET EXPECTED ANSWER
	CAME D,A	;ARE THEY THE SAME?
	JRST ERR28	;NOPE
CWT2A:	ADDI C,400	;ADVANCE TO NEXT BYTE OF REGISTER
	SOJG B,CWT2	;THAT'S ALL FOR THAT REGISTER
	ADDI C,400	;ADVANCE TO NEXT REGISTER
	AOS	H	;INC XR FOR BYTE POINTERS
	SOJG F,CWT1	;WE'VE TESTED ALL THREE REGISTERS
	JSR PTN0	;TRY NEXT DATA VALUE
	SOJGE G,CWTST	;TRY ALL CHANNELS
	LOOP ^D28,CWTST0
INCT:	MOVEI J,0	;0 OR -1
INCT0:	HRLOI F,17	;SHIFTS
INCT1:	MOVE C,F	;DATA WORD
	XOR C,J	;COMPLEMENTED SECOND TIME THROUGH
	MOVEM C,INCTW	;REAL DATA WORD
	ADDM J,INCTW
	JUMPL J,INCT1A
	AOS INCTW	;SIMULATE THE INCREMENTER
INCT1A:	PUSHJ P,LC0CW	;LOAD CA, WC
	MOVEI A,42260	;MICRO INST, XANC INCREMENT CA,DUMMY,DIAG
	JUMPGE J,INCT2
	MOVEI A,42660	;MICRO INST,XANC, DECREMENT CA,DUMMY DIAG
INCT2:	PUSHJ P,C0XCT	;DO THE INCREMENT/DECREMENT
	MOVEI A,42260
	PUSHJ P,C0XCT
	CONO SA0,RDMA	;TO READ THE MA BACK
	DATAI SA0,A
	CONO SA0,RDBITS	;TO READ THE EXTRA ADDR BITS
	DATAI SA0,B
	DPB B,[240200,,A]	;PUT EXTRA BITS WHERE THEY BELONG
	XOR A,INCTW	;COMPARE
	TDNE A,[17,,777777]	;WHERE WORDS SHOULD MATCH
	JRST ERR39	;IF ERROR
INCT4:	LOOP ^D39,INCT1	;USE SAME DATA IF LOOPING
	JUMPE F,INCT5	;IF ALL BIT POSITIONS TRIED
	ASH F,-1
	JRST INCT1	;OTHERWISE TRY NEXT ONE

INCT5:	SETCA J,0	;COMPLEMENT J
	JUMPL J,INCT0	;AND DO OTHER PASS IF NEEDED
MRD0:	MOVE C,[200000,,TEST]
	PUSHJ P,LC0CW
	MOVEI E,146720	;REG. 0_MEM RD BUFFER
MRD1:	MOVEI A,042200	;START READ AT CA
	PUSHJ P,C0XCT
	MOVEI B,3
MRD:	MOVE A,E
	PUSHJ P,C0XCT
	SOJLE B,ERR8	;MEM CYCLE TOOK TOO MANY TICKS
	CONO SA0,RDCONI
	DATAI SA0,A
	TLNE A,1
	JRST MRD
MRD1A:	LOOP ^D8,MRD1
	MOVE A,E
	PUSHJ P,C0XCT
	ADDI E,10000	;FILL NEXT REGISTER
	TRNE E,40000	;ALL FILLED?
	JRST MRD1	;NO
	SETOM TEST1	;CLOBBER PLACE WHERE DATA WILL BE READ BACK
	CLEARM TEST1+1
	MOVE A,TEST+2
	SETCAM A,TEST1+2
	MOVEM A,TEST1+3
	MOVEI E,042240	;START WRITE CYCLE
MWR1:	MOVE A,E
	PUSHJ P,C0XCT
	MOVEI B,3
MWR:	SOJLE B,ERR9	;MEM CYCLE TOOK TOO MANY TICKS
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
	MOVEI A,0
	PUSHJ P,C0XCT
	CONO SA0,RDCONI
	DATAI SA0,A
	TLNE A,1
	JRST MWR
MWR1A:	LOOP ^D9,MWR1
	ADDI E,10000
	TRNE E,40000
	JRST MWR1
	MOVSI A,-4
MCMP:	MOVE B,TEST1(A)
	MOVE C,TEST(A)
	CAME B,C
	JRST ERR10	;DATA GOTTEN BACK WRONG
MCMP1:	LOOP ^D10,MRD0
	AOBJN A,MCMP


;FALLEN INTO FROM PREVIOUS PAGE
STCL:	MOVEI G,NCHN-1	;HANG ALL CHANNELS
STCLH:	PUSHJ P,STEPCX	;GET TO RIGHT CHANNEL
	MOVEI A,HANGPC	;FEED IT MAGIC PC WHICH WILL HANG IT
	PUSHJ P,CXXCT
	SOJGE G,STCLH	;DO SAME FOR ALL CHANNELS
	CONO SA0,SACGO	;START THE CLOCK SO CONO'S WILL WORK
	MOVEI E,10*<NCHN-1>
	MOVEI D,PIE0_<1-NCHN>
	MOVEI C,NCHN-1
STCL1:	CONO SA0,STPIE(E)
	CONSO SA0,(D)
	JRST ERR13
STCL1A:	CONO SA0,CLPIE(E)
	CONSZ SA0,(D)
	JRST ERR14
STCL1B:	LSH D,-4
	CONO SA0,STBSY(E)
	CONSO SA0,(D)
	JRST ERR15
STCL1C:	CONO SA0,CLBSY(E)
	CONSZ SA0,(D)
	JRST ERR16
STCL1D:	LSH D,-4
	CONO SA0,STSTF(E)
	CONSO SA0,(D)
	JRST ERR17
STCL1E:	CONO SA0,CLSTF(E)
	CONSZ SA0,(D)
	JRST ERR18
STCL1F:	LSH D,10+1
	LOOP ^D13,STCL1
	SUBI C,1
	SUBI E,10
	JUMPGE E,STCL1
	MOVSI B,-3
	CONO SA0,SASTEP
	PUSHJ P,STEPC0
STCLM2:	MOVE A,STCLM(B)
	PUSHJ P,C0XCT
	CONSO SA0,@STCLM1(B)
	JRST ERR19
STCLM3:	MOVE A,STCLM(B)
	SUBI A,4000
	PUSHJ P,C0XCT
	CONSZ SA0,@STCLM1(B)
	JRST ERR20
STCLM4:	LOOP ^D19,STCLM2
	AOBJN B,STCLM2
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
QFIFO:	MOVEI G,NCHN-1	;FILL UP THE FIRST-IN, FIRST-OUT
		;BUFFERS OF EACH OF THE CHANNELS
FIFO1:	PUSHJ P,STEPCX	;STEP TO THE APPROPRIATE CHANNEL
	MOVEI A,002000	;CLEAR TAG BYTE
	PUSHJ P,CXXCT
	MOVEI A,027000	;SET WRITE BIT IN TAG BYTE
	PUSHJ P,CXXCT
	MOVEI A,027400	;SET BUFFER ENABLE BIT IN TAG BYTE
	PUSHJ P,CXXCT
	MOVEI A,145000	;TEST BUFFER EMPTY BIT
	PUSHJ P,CXTST
	TRNN A,40_12	;CHECK RESULTS OF TEST
	JRST ERR21	;BUFFER WASN'T EMPTY
FIFO1A:	LOOP ^D21,FIFO1
	MOVEI C,0
	PUSHJ P,LCXCW	;CAUSE AN APPARENT W.C. OVERFLOW
	MOVEI D,NBF	;SET UP COUNT OF # BYTES TO SEND
FIFO2:	MOVE A,G
	LSH A,5
	ADD A,D		;SYNTHESIZE A UNIQUE BYTE OF DATA
	PUSHJ P,CXXCT	;PUT IT INTO MICRO-P.C.
	MOVEI A,040140	;PUT "FIFO_MICRO-P.C." IN MICROIN-
	PUSHJ P,CXTST	;STRUCTION REGISTER TO SEE IF IT
			;WILL BRANCH
	PUSHJ P,STEPX	;NOW EXECUTE THE MICROINSTRUCTION
	MOVEI A,145000	;TEST BUFFER EMPTY BIT
	PUSHJ P,CXTST
	TRNE A,40_12
	JRST ERR22	;BUFFER WAS EMPTY
FIFO2A:	LOOP ^D22,FIFO2
	SOJG D,FIFO2	;KEEP DOING IT UNTIL FIFO SHOULD BE FULL
FIFO2C:	MOVEI A,040140	;TRY TO PUT ANOTHER WORD INTO FIFO
	PUSHJ P,CXXCT
	TRNN A,1	;OUGHT TO COMPLAIN THIS TIME
	JRST ERR24	;DIDN'T
FIFO2B:	LOOP ^D24,FIFO2C
	SOJGE G,FIFO1	;FILL UP FIFOS OF OTHER CHANNELS
;O.K., FOLKS, NOW WE SEE IF WE CAN GET THE DATA BACK
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
	MOVEI G,NCHN-1	;START UNLOADING HIGHEST CHANNEL
FIFO3:	PUSHJ P,STEPCX	;STEP COUNTER TO APPROPRIATE CHANNEL
	MOVEI A,023400	;CLEAR BUFFER ENABLE
	PUSHJ P,CXXCT
	MOVEI A,027400	;SET BUFFER ENABLE
	PUSHJ P,CXXCT
	MOVEI A,145000	;TEST BUFFER EMPTY (SHOULD BE)
	PUSHJ P,CXTST
	TRNN A,40_12
	JRST ERR25	;WASN'T
FIFO3A:	LOOP ^D25,FIFO3
	MOVEI A,023000	;CLEAR WRITE
	PUSHJ P,CXXCT
FIFO3Z:	MOVEI D,NBF
FIFO4:	MOVEI A,145000	;TEST BUFFER EMPTY
	PUSHJ P,CXTST
	TRNE A,40_12
	JRST ERR26	;OOPS, IT WAS
FIFO4A:	LOOP ^D26,FIFO4
	MOVEI A,147000	;R0B0_FIFO
	PUSHJ P,CXTST
	PUSHJ P,STEPX
	MOVE H,G
	LSH H,5
	ADD H,D
	MOVEI A,140000	;CBUS_R0B0
	PUSHJ P,CXTST
	LDB A,[121000,,A]
	CAME A,H
	JRST ERR27	;WRONG DATA READ BACK FROM FIFO
FIFO4B:	SOJG D,FIFO4	;READ BACK THE REST
	LOOP ^D27,FIFO3Z
	MOVEI A,145000	;BUFFER SHOULD NOW BE EMPTY
	PUSHJ P,CXTST
	TRNN A,40_12
	JRST ERR23	;OOPS, IT ISN'T
FIFO4C:	LOOP ^D23,FIFO3
	SOJGE G,FIFO3	;READ BACK FIFOS OF OTHER CHANNELS
;FALL THRU

;TRY OUT P.I. REQUEST LOGIC
PITST0:	CONO SA0,SACGO
	MOVEI G,<NCHN-1>*10
PITST:	CONO SA0,CLSRQ(G)	;CLEAR STATUS REQUEST FLAG
	CONO SA0,CLSTF(G)	;AND STATUS FLAG
	CONO SA0,STPIE(G)	;AND SET P.I. ENABLE FLAG
	SUBI G,10
	JUMPGE G,PITST	;FOR ALL CHANNELS
	CONSZ SA0,PIRQ	;SHOULD NOT BE REQUESTING AN INTERRUPT
	JRST ERR29	;OOPS, WAS
PITST1:	LOOP ^D29,PITST0
	CLEARM PIE	;NOT EXPECTING ANY INTERRUPTS
	CONO PI,12377	;ENABLE ALL P.I. CHANNELS
	MOVEI B,7
PIT1:	CONO SA0,(B)
	SOJG B,.-1	;SHOULD NEVER INTERRUPT
	MOVEI G,<NCHN-1>*10
PIT2:	MOVEI B,7
PIT3:	MOVEM B,PIE
	ADD B,G
	CONO SA0,STSTF(B)	;SHOULD CAUSE AN INTERRUPT
	ROT 377	;DELAY FOR KI-10
	JFCL
	SKIPE PIE	;DID IT?
	JRST ERR38	;NO, COMPLAIN
PIT3A:	LOOP ^D38,PIT3
	SUB B,G
	SOJG B,PIT3	;TRY ALL P.I. CHANNELS
	SUBI G,10
	JUMPGE G,PIT2	;TRY ALL SA-10 CHANNELS
UCT:	MOVEI B,0	;ADDRESS
	PUSHJ P,STEPC0	;DO GOTO
UCT0:	HRRZ C,MCBEG0(B)	;WHAT UINST SHOULD BE
			;ABOVE INSTRUCTION CHANGED TO CLEAR LEFT HALF OF AC
	TRNE C,140000
	JRST UCT8	;IF NOT GOTO OR OFF/ON
	MOVE A,B	;CHANGED 26-APR-76-***LEM
	PUSHJ P,C0XCT	;IT'S HARMLESS, DO IT
	PUSHJEP0
	LDB D,[100200,,B]
	TRZE C,36000
	DPB D,[100200,,C]
	ANDI A,1777
	CAME A,C
	JRST	ER41N
UCT7:	LOOP ^D41,UCT0
UCT8:	CAIGE B,1400-1
	AOJA B,UCT0
	AOS	TEN,BEGERR#
	ALT
	DATAO	PI,TEN
	SKIPN EXETR	;MOD BY GBH
	JRST BTE-1	;O.K., IT ALL WORKS.  TRY IT OVER AGAIN
	SETZM ,EXETR	;MOD BY GBH
	POPJ P,		;MOD BY GBH
ER41N:	CAIE	3,20
	JRST	ERR41
	AOS	ER28
	JRST	UCT7
;HERE


BEG1:	SOJG B,BEG1A
	JRST ERR2		;DIDN'T GET TO CHANNEL 0
ER28:	0


PAGE

STEPC0:	MOVEI G,0
STEPCX:	CONO SA0,RDCCI
STPC0A:	DATAI SA0,A
	LDB H,[220200,,A]	;CHANNEL SELECTION.
	CAMN H,G		;DESIRED CHANNEL?
	POPJ P,			;YES.
	DATAO SA0,[HANGPC]	;NO. STICK OTHER CHANNELS AT STICKY PC
STEPX:
STEPX1:	CONO SA0,SASTEP!RDCCI
	JRST STPC0A

LC0CW:	MOVEI G,0
LCXCW:	PUSHJ P,LCXR2
	MOVEI A,060700	;WC_WC FIELD OF REG. 2
	PUSHJ P,CXXCT
	MOVEI A,063300	;CA_CA FIELD OF REG. 2
CXXCT:	DATAO SA0,A
	JRST STEPX

C0XCT:	DATAO SA0,A
STEP0:	MOVEI G,0
	JRST STEPX1

CXTST:	DATAO SA0,A
	CONO SA0,RDCCI
	DATAI SA0,A
	POPJ P,

LC0R2:	MOVEI G,0
LCXR2:	MOVEI D,060000-20	;IARB TO BYTE OF REG. 2
LCXR2A:	ADDI D,20	;ADVANCE TO NEXT BYTE OF REGISTER
	MOVEI B,0
	LSHC B,10
	DATAO SA0,B	;PUT A BYTE OF DATA INTO MICRO-P.C.
	PUSHJ P,STEPX
	DATAO SA0,D	;TRANSFER IT TO BYTE OF REG. 2
	PUSHJ P,STEPX
	TRNN D,100	;WAS THAT THE LAST BYTE?
	JRST LCXR2A	;NO
	POPJ P,	;I CAN'T BELIEVE WE FILLED THE WHOLE WORD


PAGE

;TRACE ONE COMMAND ON ANY CHANNEL IN SELECTOR CHANNEL MODE.
;ENTRY REQUIREMENTS
;AC	A=CHANNEL NUMBER
;	B=TIC TO COMMAND
;	C=DEV ADDRESS
;		IF C=0 THEN ADDRESS SUPPLIED IN CMD
;		IF C NOT =0 THEN STUFF ADDRESS IN CMD
;	D=TRACE CONTROL
;		1=PRINT DIFFERENCE IF IN COMPARE MODE
;		0=PRINT IAR
;		-1=PRINT IAR AND CBUS
;	E=STORE ADDRESS FOR ADRTST
;	F=FETCH ADDRESS FOR ADRTST
;	G=CLOCK CONTROL
;		0=FREE RUNNING CLOCK
;		NOT 0=SINGLE STEP CHANNEL CLOCK
;INTERNAL CMDS AVAILABLE TO TRACE
;PSENSE
;PNOP
;ADRTST
;EXAMPLE OF INTERNAL CMD ENTRY
;AC A=1
;AC B=TIC PSENSE
;AC C=300

TRACES:	CONO SA0,RSTSA!CLMERR ;RTST SA CLR MEM ERRS AND STRT CLK
	MOVE J,PARMA-1
	BLT J,J
	ANDI A,1	;MAKE 2 CHAN SA ONLY
	SETOM PASS
	SETZM SRCH
	MOVEI L,5	;SET UP SEARCH COUNT
	MOVEM A,CHAN	;SAVE CHAN ADDRESS
	PUSHJ P,CSET	;SET UP CHAN DEPENDENT ADDRESSES
	MOVEM B,@CBASE	;LOAD BASE WITH TIC
	HRRZ B,PARMB
	CAIN B,ADRTST	;IS THIS ADRTST
	PUSHJ P,LDADR	;YES SET UP FOR RUN
	JUMPE C,.+2
	PUSHJ P,STUFF
	MOVE B,[CONO SA0,SASTEP]	;SETUP FOR STOP CLOCK
	MOVE A,[CONO SA0,0]	;SETUP FOR  SET GO FLAG
	HRR A,STBSYC	;FINISH CONO
	MOVE C,[JRST STP1+1] ;GET BACK TO MEMORY
	MOVEI 0,7777	;SET UP WAIT LOOP
	SOJGE 0,.	;WAIT FOR SA10 TO FINISH RESET
	JRST A		;GOTO AC TO RUN
STPONE:	JUMPE G,STP1	;DONT HANG IF CONTINUOUS MODE
	NALT		;HANG HERE FOR SINGLE STEP
STP1:	CONO SA0,SASTEP	;STEP SA CLOCK ONE TIME
	DATAI SA0,E	;GET DATAI 0
	SETZB A,F
	LDB A,[POINT 1,E,17] ;GET CHAN NUM.
	LDB F,[POINT 8,E,25] ;SAVE CBUS
	ANDI E,1777	;MASK OUT CBUS BITS
	CAME A,CHAN	;IS CHAN SAME
	JRST STP1	;NO STEP CLOCK
	JUMPN D,TRCTL
	MOVEI C,[ASCIZ/IAR=%E
/]
	PUSHJ P,STYO	;PRINT IAR
TRCONT:	CAIN E,52	;CHECK FOR IDLE3
	JRST .+3
	CAIE E,276	;IS IT IN HANG LOOP
	JRST STPONE	;DO IT AGAIN
	AOS K,PASS	;YES SET UP FOR ONE MORE LOOP
	JUMPG K,FINI
	JRST STPONE
STUFF:	CAIN B,ADRTST	;CHECK FOR ADRTST
	POPJ P,		;BUGOUT IF ADRTST
	HRRM B,ADDPTR	;STUFF CCW ADDRESS IN POINTER
	DPB C,ADDPTR ;STUFF ADDRESS IN CMD
	POPJ P,		;FINISH TRACE

LDADR:	HRRM F,ADRTST	;STUFF FETCH ADDRESS
	CAMGE E,ADRLO	;CHECK STORE ADDRESS
	JRST .+3
	HRRM E,ADRTST+1	;STUFF STORE ADDRESS
	POPJ P,
	CAIGE E,PATCH	;SEE IF STORE AREA WITHIN PATCH
	JRST TRCER1
	CAIL E,PATCH+777
	JRST TRCER1
	HRRM E,ADRTST+1	;STUFF STORE ADDRESS
	POPJ P,

TRCER1:	MOVEI C,[ASCIZ/
STORE ADDRESS IS WITHIN PROGRAM AREA 
SELECT ANOTHER ADDRESS
/]
	PUSHJ P,STYO
	JRST DDT

TRCTL:	JUMPG D,.+4
	MOVEI C,[ASCIZ/IAR=%E  CBUS=%F
/]
	PUSHJ P,STYO
	JRST TRCONT
	HRRZ B,PARMB
	CAIN B,PSENSE
	JRST CMP
	CAIN B,PNOP
	JRST CMP1
	CAIN B,ADRTST
	JRST CMP2
	MOVEI C,[ASCIZ/
NO COMPARE DATA FOR SELECTED COMMAND
/]
	PUSHJ P,STYO
	JRST DDT

CMP:	SKIPN SRCH
	JRST SRCH+1
	ILDB F,H
	JRST CMPM
CMP1:	SKIPN SRCH
	JRST SRCH +1
	ILDB F,I
	JRST CMPM
CMP2:	SKIPN SRCH
	JRST SRCH+1
	ILDB F,J
CMPM:	CAMN E,F
	JRST TRCONT
	MOVEI C,[ASCIZ/   

COMPARE ERROR
EXPECTED IAR=%E RECEIVED IAR=%F
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/
FOR A COPY OF THE COMPARE DATA TYPE TRDATA ALT G
/]
	PUSHJ P,STYO
	JRST DDT
SRCH:	0
	CAIE E,343
	JRST .+2
	SETOM SRCH
	SOJGE L,TRCONT
	MOVEI C,[ASCIZ/
SYNC ERROR
PROGRAM FAILED TO FIND SA10 IAR AT 343 WITHIN 6 CLOCK TIMES
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/
CHANGE PARMD TO -1 AND RERUN PROGRAM
/]
	PUSHJ P,STYO
	JRST DDT
FINI:	MOVEI C,[ASCIZ/
TRACE COMPLETED
/]
	PUSHJ P,STYO
	JRST DDT

;DEFINITIONS FOR TRACE
LAC=40000
DAC=440000
TIC=200000

;MEMORY LOCATIONS FOR TRACE
ADRTST:	LAC,,0
	DAC,,0
	0
ADDPTR:	141000,,0	;POINTER
CSNS:	BYTE(10)344,135,121,140,141,142,143,145,146
	BYTE(10)166,167,174,170,173,200,202,203,204
	BYTE(10)205,201,206,207,210,211,220,223,224
	BYTE(10)225,227,230,231,232,221,760,761,762
	BYTE(10)763,764,776,777,500,501,503,504,514
	BYTE(10)515,516,520,522,526,553,540,542,545
	BYTE(10)545,546,543,557,550,552,552,556,556
	BYTE(10)547,553,540,540,542,542,545,545,546
	BYTE(10)543,557,550,552,552,556,556,547,553
	BYTE(10)540,540,542,542,545,545,546,543,557
	BYTE(10)550,552,552,556,556,547,553,541,700
	BYTE(10)702,704,740,741,742,751,400,411,401
	BYTE(10)402,404,420,422,424,452,412,413,414
	BYTE(10)416,360,362,364,140,141,142,143,144
	BYTE(10)150,152,237,162,163,164,300,301,302
	BYTE(10)240,241,242,243,244,245,246,247,250
	BYTE(10)251,252,252,253,253,254,157,261,262
	BYTE(10)265,270,272,276,276
CNOP:	BYTE(10)344,135,121,140,141,142,143,145,146
	BYTE(10)166,167,174,170,173,200,202,203,204
	BYTE(10)205,201,207,210,211,220,223,224,225
	BYTE(10)226,234,235,212,213,214,400,411,401
	BYTE(10)402,404,420,422,424,452,412,413,414
	BYTE(10)416,360,362,364,140,141,142,143,144
	BYTE(10)150,152,237,162,163,164,300,301,302
	BYTE(10)240,241,242,243,244,245,246,247,250
	BYTE(10)251,252,252,253,253,254,157,261,262
	BYTE(10)265,270,272,276,276,276
CADR:	BYTE(10)344,135,121,140,141,142,143,144,150
	BYTE(10)153,147,154,156,156,140,141,142,143
	BYTE(10)144,150,153,147,155,140,140,141,142
	BYTE(10)143,144,150,152,237,162,163,164,300
	BYTE(10)301,302,240,241,242,243,244,245,246
	BYTE(10)247,250,251,252,252,253,253,254,157
	BYTE(10)261,262,265,270,272,276,276,276,276

TRDATA:	SETZB E,D
	MOVEI C,[ASCIZ/     
/]
	PUSHJ P,STYO
	MOVEI E,343
	MOVEI C,[ASCIZ/IAR=%E
/]
	PUSHJ P,STYO
	MOVEI 0,7777
	SOJG 0,.
	JUMPN D,TRD1
	HRRZ E,PARMB
	CAIN E,PSENSE
	MOVE D,PCSNS
	CAIN E,PNOP
	MOVE D,PCNOP
	CAIN E,ADRTST
	MOVE D,PCADR
	JUMPE D,TRD2
TRD1:	ILDB E,D
	CAIE E,276
	JRST TRDATA+4
	MOVEI E,276
	MOVEI C,[ASCIZ/IAR=%E
/]
	PUSHJ P,STYO
	JRST DDT
TRD2:	MOVEI C,[ASCIZ/
NO COMPARE DATA FOR SELECTED COMMAND
/]
	PUSHJ P,STYO
	JRST DDT
	PARMA,,A
PARMA:	0
PARMB:	0
PARMC:	0
PARMD:	0
PARME:	0
PARMF:	0
PARMG:	0
PCSNS:	POINT 10,CSNS,
PCNOP:	POINT 10,CNOP,
PCADR:	POINT 10,CADR,
PAGE
HELP:	MOVEI C,[ASCIZ/CMD	OPTION	COMMENTS
BEG		BASIC TEST OF SA10 CHANNEL
CMT		ADDRESS TEST TO MEMORY
	ADRLO	STARTING ADDRESS(DEFAULT=FIRST FREE)
	ADRHI	ENDING ADDRESS(DEFAULT=777000)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/DSKT		RANDOM DISK TEST
	DCHAN	CHANNEL ADDRESS(DEFAULT=1)
	ENDCYL	LAST CYLINDER TO TEST ON PACK(DEFAULT=807)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	IDEV	CONTROL UNIT ADDRESS(DEFAULT=300)
	STRCYL	FIRST CYLINDER TO TEST ON PACK(DEFAULT=0)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		FORMAT FOR ENDCYL AND STRCYL IS
		ENDCYL+UNIT ADDRESS SLASH TO OPEN MEMORY
		ENTER DATA AS FOLLOWS CYL.*19.+HEAD.
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		EXAMPLE  CYL=339,HD=9,DEV=314
		ENDCYL+14 SLASH 339.*19.+9. CR
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/DSKTV		SURFACE VERIFIER TEST
		WRITES AND READS ALL TRACKS ON A PACK
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	CEPTRN	SELECT DATA PATTERN FOR TEST(DEFAULT=-1)
	0	USE ALL ZEROS FOR DATA
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	1	USE ALL ONES FOR DATA
	2	USE WORST CASE DATA 
	-1	USE ALL THE DATA PATTERNS SEQUENTIALLY
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		EXAMPLE DRIVE 313 USE WORST CASE DATA
		CEPTRN+13 SLASH 2 CR
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		ALL OTHER OPTIONS AVAILABLE FOR DSKT
		ARE IN EFFECT FOR DSKTV
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/DSKTVT		SAME AS DSKTV BUT IGNORE
		9C AND 9E TEST CYLINDERS
		USE SAME OPTIONS AS DSKTV
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/FMT		FORMAT PACK
	ALLTRK	NUMBER OF CYLINERS TO FORMAT FOR OPTIONS 1,-1,-2
	DDEV	DEVICE ADDRESS OF DRIVE TO FORMAT(DEFAULT=300)
	FMTM	TYPE OF FORMAT
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		0 FORMAT DATA RECORDS ONLY
		1 FORMAT RECORD 0 AND ALL DATA RECORDS
		-1 FORMAT HOME ADDRESS WITH CE BIT OFF AND ALL RECORDS
		-2 FORMAT HOME ADDRESS WITH CE BIT ON AND ALL RECORDS
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		   MANUAL INTERVENTION REQUIRED
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	FMTX	NUMBER OF TIMES TO FORMAT PACK
	NTRK	NUMBER OF CYLINDERS TO FORMAT FOR OPTION 0
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	STRTRK	STARTING CYLINDER (DEFAULT=0)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/HELP		LIST ALL COMMANDS,TEST
		AND RUN OPTIONS
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/QTT		TEST SELECTED TAPE DRIVE
	MODE7	SELECT BIT DENSITY FOR 7 TRACK TAPE DRIVE
		(DEFAULT=223 800BPI ODD PARITY NO TRANSLATOR)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	MODE9	SELECT BIT DENSITY FOR 9 TRACK TAPE DRV
		(DEFAULT=303 1600BPI)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	TCHAN	CHANNEL ADDRESS(DEFAULT=0)
	TDEV	TAPE UNIT ADDRESS OF DRIVE TO BE TESTED
		(DEFAULT=200)
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/TRACES		TRACE COMMAND THRU SA10
		MICRO-INSTRUCTIONS IN SELECTOR CHANNEL MODE
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	PARMA	CHANNEL NUMBER
	PARMB	TIC TO COMMAND
		EXAMPLE TIC,,PNOP WILL TRACE A NOP CCW
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	PARMC	DEVICE NUMBER
	PARMD	TRACE CONTROL
		0=PRINT IAR
		-1=PRINT IAR AND CBUS
		1=COMPARE INTERNAL COMMANDS AND PRINT ANY DIFFERENCE
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/		PNOP,PSENSE,AND ADRTST ARE AVAILABLE
		TO TRACE COMPARE
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	PARME	STORE ADDRESS FOR ADRTST
	PARMF	FETCH ADDRESS FOR ADRTST
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	PARMG	SINGLE STEP CONTROL
		0=DEFAULT DO NOT SINGLE STEP CLOCK
		1=SINGLE STEP CLOCK
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/TRDATA		DUMP COMPARE DATA FOR SELECTED
		COMMAND
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/CPU DATA SWITCH CONTROL
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/	BIT3	SUPPRESS ALL PRINT OUT
	BIT4	FORCE ALL PRINT OUT TO LINE PRINTER
/]
	PUSHJ P,STYO
	JRST DDT
PAGE
CMT:	PUSHJ P,EXT	;MOD BY GBH MAKE A PASS THRU BEG FIRST
	SETZM PASS	;MOD BY GBH
	CONO 635550		;MEMORY TEST
	SETZM	MAPSAV	;ZERO PAGE COUNTER
	PUSHJ	P,SETMAP	;GO INITIALIZE PAGE TABLE
	DATAO	PAG,[420001,,420001]	;TRAP ENABLE AT LOC 1000
;	MOVEI P,PDL-1	
	MOVEI A,0
	MOVEM A,CHAN
	PUSHJ P,CSET
CMT0:	MOVE E,ADRLO
	MOVE	D,E	;NEED PAGED AND REAL AC ADDRESS
CMT1:	MOVE A,[MOVE CMTP0]
	PUSHJ P,RND
	TLNE	E,1	;>256K
	PUSHJ	P,NEWMAP	;GET NEXT 128K
CMT1L:	DPB	D,[POINT 22,CMTP0,35]	;LOAD SA READ ADDRESS 0
	MOVEM B,(E)
	PUSHJ P,CMTG
;	CAME B,CMTPT
	MOVE A,B	;MOD BY GBH
	XOR B,CMTPT	;MOD BY GBH
	JUMPN B,NCMP	;MOD BY GBH
;	JRST DDT	;DELETED BY GBH
	SETCAM A,CMTPT	;MOD BY GBH
;	SETCAM B,CMTPT	;DELETED BY GBH
	MOVE A,[MOVE CMTP1]
	DPB	D,[POINT 22,CMTP1A,35]	;LOAD SA READ ADDRESS 1
	PUSHJ P,CMTG
	EQV B,(E)
	JUMPE B,NCMP	;MOD BY GBH
;	JUMPN B,DDT		;DELTED BY GBH
	ADDI E,1
	ADDI	D,1
	CAMGE D,ADRHI
	JRST CMT1
	MOVE A,PASS	;MOD BY GBH
	ADDI A,1
	MOVEM A,PASS
	MOVEI C,[ASCIZ/PASS $A COMPLETED
/]
	PUSHJ P,STYO
	MOVEI 0,-1
	SOJG 0,.
	JRST CMT+2

CMTG:	MOVEM A,@CBASE
	ALT
	CONO SA0,@STBSYC
	CONSO SA0,600170
	JRST .-1
	CONSZ SA0,600000
	JRST CMTGE
	CONO SA0,@CLSTFC
	POPJ P,
CMTGE:	PUSHJ P,DSPE1
	DATAO PAG,[400000,,400000]	;MOD BY GBH
	JRST DDT
;RELOAD PAGE TABLE FOR NEXT 128 K.
;ENTRY IS PUSHJ P,NEWMAP
	EXPMP=1000	;UBR REAL LOC
NEWMAP:MOVE	FT,[XWD -200,200]	;POINTER TO EXPGMP
	MOVE	ST,MAPSAV	;GET LAST KNOWN LOCATION
	ADDI	ST,400	;IN PAGE MAP LANGUAGE, +128K
	MOVEM	ST,MAPSAV	;SAVE FOR FUTURE
ENTMAP:		DPB	ST,[POINT	13,EXPMP(FT),17];PUT IT IN LEFT HALF
	ADDI	ST,1	;BUMP PHYSICAL LOC
		DPB	ST,[POINT 13,EXPMP(FT),35]; HOPEFULLY RIGHT HALF
	ADDI	ST,1	;BUMP AGAIN
	AOBJN	FT,ENTMAP	;GO THROUGH PAGES 400-777
	MOVEI	E,400000	;MAKE IT LOOK LIKE NEXT
	DATAO	PAG,[420001,,420001]
	POPJ	P,0	;RETURN
MAPSAV:	0
SETMAP:	MOVE	FORT,[760000,,760000]	;ACCESS,PUBLI,WRITE
	MOVE	TT,[XWD	-220,200]	;CHECK EXPMP FOR LOC 200
	MOVEM	FORT,EXPMP(TT)	;STORE PAGE DATA AWAY
	AOBJN	TT,.-1	;DO FOR 200-417
	PUSHJ	P,NEWMAP	;INIT
	MOVEI	ST,340	;DONT FORGET EXEC 340-377
	MOVE	FT,[XWD -20,400]	;CEHCK UPMP LOC 400
	MOVE	ELEV,[JSR	TRAPX]
	MOVSI	TT,-3	;XR FOR LOADING PAGE FAULT ,OVERFLOW
	HRRI	TT,1420	;PAGE MAPE ENTRY FOR ABOVE
	MOVEM	ELEV,(TT)
	AOBJN	TT,.-1
	PUSHJ	P,ENTMAP	;SPECIAL ENTRY
	POPJ	P,0	;SEE YOU LATER
CMTP0:	40000,,0	;LAC X (X FILLED IN ABOVE)
	440000,,CMTPT	;DAC CMTPT
	0

CMTP1:	40000,,CMTPT	;LAC CMTPT
CMTP1A:	440000,,0	;DAC XX (XX FILLED IN ABOVE)
	0

PASS:	0	;MOD BY GBH
CMTPT:	0
TRAPX:	0
	JFCL	.+1
	JEN	@TRAPX
PAGE
;DISK TEST INTERRUPT CODE FOR DSKT BELOW
;JSR HERE ON CH 1 INT, SET UP AT START OF DSKT
DSKI:	0
	MOVEM G,ACS+G
	MOVEI G,ACS
	BLT G,ACS+G-1
	CONI SA0,0
	TRNN 0,PIRQ
	JRST DSKIE
	TRNE 0,PARERR+NXM
	JRST DSPE
	TRNE 0,170
	JRST DSI0
DSKIE:	MOVEI C,[ASCIZ /UNKNOWN INT. CONI=%@
/]				;INT ON CH 1, NOT FROM SA10
	PUSHJ P,STYO
	JRST DSKXX

DSKX1:	CONO SA0,@CLSTFC
DSKEX:	MOVSI G,ACS
	BLT G,G
	JRST 12,@DSKI

DSPE1:	CONO SA0,10
	DATAI SA0,D		;ADDR
	CONO SA0,20
	DATAI SA0,A
	HLRZM A,E		;CH
	CONO SA0,30
	DATAI SA0,F
	HRLM A,F		;WORD
	MOVE G,(D)		;WORD IN MEM
	MOVEI C,[ASCIZ /NON EX MEM ON CH %E
ADDR=%D MEM HAS %G CHNL GOT %F
/]
	CONSZ SA0,PARERR	;SELECT PARITY ERR OR NXM MSG
	MOVEI C,[ASCIZ /MEM PAR ERR ON CH %E
ADDR=%D MEM HAS %G CHNL GOT %F
/]
	JRST STYO	

NCMP:	MOVE A,(E)	;MOD BY GBH
	MOVE F,CMTPT
	DATAO PAG,[400000,,400000]
	MOVEI C,[ASCIZ/FAILING BIT %B
FAILING ADDRESS %D
EXPECTED DATA %A
RECEIVED DATA %F
/]
	PUSHJ P,STYO
	JRST DDT

DSPE:	PUSHJ P,DSPE1
	LSH E,3			; CHANNEL NUMBER CAUSING INTERRUPT

	CONO SA0,RSTCH+40(E)
	CONO SA0,CLMERR+SA0CH
	JRST DSKEX

DSKXX:	SKIPE SERR
	JSR GOPR
	JRST DSKT0

;HERE ON SA10 INT FOR DSK CHANNEL

DSI0:	MOVE A,@CBASE+1
	ALT
	TLC A,600000
	TLNN A,600000
	JRST DSKXR	;DUMMY STAT
	LDB A,[DEVFLD,,@CBASE+1]
	MOVN G,IDEV	;MOD BY GBH
	ADD G,A		;MOD BY GBH
	HRLI G,0	;MOD BY GBH
;	MOVEI G,-IDEV(A)
	CAILE G,NPACKS-1
	JRST DSE1		;BAD DEV #
	MOVE A,TIME		; OK. UPDATE TIME OF RECENT INTERRUPT.
	EXCH A,HUNGT(G)		; FOR THIS DRIVE
	SKIPN OFFLIN(G)		; IF IT HAD BEEN OFF LINE, OR
	AOJN A,DSI0A		; IF IT HAD BEEN HUNG,
	MOVSI B,10		; SET FLAG TO COMMENT ON ITS REVIVAL
	IORM B,CHCW(G)		; AT MAIN PROGRAM LEVEL
	SETZM OFFLIN(G)		; AND CLEAR THE OFF-LINE FLAG.
DSI0A:	MOVE A,@CBASE+1
	MOVE B,@CBASE+2
	TLNN A,40
	JRST DSI0C
	MOVE C,-1(B)
	CAIN C,1
	JRST DSKSKS	;SEEK OR SET SECTOR STTD
DSI0C:	TLC A,450
	TLCN A,450
	JRST DSKCRT	;CMD RETRY
	TLNN A,20
	JRST DSI0B
	TLNN A,1754
	JRST DSKDEA	;DEV END ALONE
DSI0B:	SETOM DEVL(G)
	TLNE A,163014		;SELECT ERR,BIPERR,CONTROL ERR,PROG INT,
				; ATTN, UNIT CK, UNIT EXCEPTION
	JRST DSE1		;BAD BITS IN STATUS
	TLNE A,100
	JRST DSIBSY		;POSS CU BUSY
	TLNE A,200
	JRST DSKXR		;CU END, RESTART CHAN
	TLNN A,20		;DEV END
	JRST DSI5		; NO.
	TLNE A,4000			; LENGTH ERROR HERE IS BAD
	JRST DSE1		; ..
	MOVSI B,1		; YES. CLEAR BIT IN STATUS WORD FOR DRV
	TDZ BF,BITS(G)		; AND IN LIGHT-WATCHER WORD.
	ANDCAM B,CHCW(G)
DSI5:	MOVE A,CHCW(G)
	TLNE A,7		; MAIN PROG WAIT OR DEV END WAIT?
	JRST DSKX1		; YES, DISMISS
	HRRZ B,IDEV	; MOD BY GBH
	ADD B,G		;MOD BY GBH
;	MOVEI B,IDEV(G)		; NO, PROCESS THIS INT.
	HRR BF,B		; FOR LIGHT WATCHERS
	DATAO PI,BF
	HRRZM B,DEVN
	JRST (A)		;DISMISS VIA FINITE STATE MACHINE

DSIBSY:	TLNN A,400		; STATUS MODIFIER?
	JRST DSIB1		;NO STATUS MOD, DEVICE BUSY
	MOVE B,CPGP(G)		; CONTROLLER BUSY
	MOVEM B,DEVL(G)		;BACK UP CHNL PC
	JRST DSKX1		;BUT WAIT FOR CU END BEFORE RESTARTING

DSIB1:	TLNN A,200		; DEVICE BUSY. CU END?
	JRST DSE1		; NO.
	MOVE B,CPGP(G)		; YES. PREVIOUS CU BUSY IS NOW OVER
	MOVEM B,DEVL(G)
	JRST DSKXR		;TRY CMD AGAIN IF CU END IN INIT STAT

DSKXR:	MOVSI G,-NPACKS
DSKXR1:	MOVE B,DEVL(G)
	TLNN B,100000
	JRST DSKXR3
	AOBJN G,DSKXR1
	JRST DSKX1

DSKXR3:	SKIPN (B)
	JRST DSI5
DSKXR2:	HRLI B,200000
	MOVEM B,@CBASE
	CONO SA0,@STBSYC
	JRST DSKX1

DSKCRT:	CONSO TTY,20
	DATAO TTY,[0]
	SUBI B,2
DSKSKS:	TLNE A,20
	JRST DSKXR2
	HRLI B,700000
	MOVEM B,DEVL(G)
	JRST DSKXR

DSKDEA:	MOVE B,DEVL(G)
	TLNE B,40000
	JRST DSE1
	JRST DSKXR2

;CALLED AT THESE TAGS FROM INT ROUTINE WITH CHCW(DRIVE) IN A.
; BITS IN LH MEAN:
;B1: RANDOM TRACK AND HEAD
;B2: DO WRITES
;B3: DO READS
;B4: RE-FILL DATA, CHECK DATA
;B5: RANDOM CORE ADDR ON RD AND WRITE
;B6: TEST THIS DRIVE AT ALL.
;B7-8 - COUNT THRU SURFACE VERIFIER PATTERNS: 00=ZEROES, 01=ONES,
;	1X=WORST CASE PATTERN.
;B9 - USE VERIFIER, NOT RANDOM TRK AND DATA
;B17: WAITING FOR DEVICE END
;B16: REQUESTS MAIN PROG TO FILL WITH NEW DATA
;B15: REQUESTS MAIN PROG TO CHECK DATA
;B14: DRIVE CAME BACK UP AFTER HANG.
;B13: RETRYING CORRECTABLE DATA CHECK

DIP0:	TLNN A,(1B9)		; NOT RANDOM IF SURFACE VERIFYING
	TLNN A,200000		; OK, RANDOM IF REQUESTED BY B1
	JRST DIP1
	PUSHJ P,RNDTK
	MOVEM C,TRK(G)
DIP1:	TLNN A,(1B9)		; DON'T RANDOMIZE IN SURFACE VERIFIER
	TLNN A,10000		; RANDOM CORE ADDR REQUESTED? (WRITE)
	JRST DIP1A
	PUSHJ P,RNDAD
	MOVEM C,ADDR(G)
DIP1A:	TLNN A,20000		; RE-FILL DATA AREA?
	JRST DIP1B
	MOVSI B,2
	JSP F,DIMP		; REQUEST MAIN PROG TO FILL DATA
DIP1B:	TLNN A,100000		; WRITE THE DATA?
	JRST DIP2
	MOVEI C,5		; WRITE DATA OPCODE
	PUSHJ P,DIRW		; DISK INT, READ/WRITE RTN
	MOVEI A,NRPT*RECL
	ADDM A,NWRITE(G)	; COUNT WORDS WRITTEN
	JSP F,DIGO
DIP2:	TLNN A,(1B9)		; DON'T RANDOMIZE ADR IN SURF VERIFY
	TLNN A,10000		; RANDOM CORE ADDR? (READ)
	JRST DIP2A
	PUSHJ P,RNDAD
	MOVEM C,ADDR(G)
DIP2A:	TLNN A,40000		; DO READS?
	JRST DIP3
	MOVEI C,6		; READ DATA OPCODE
	PUSHJ P,DIRW
	MOVEI A,NRPT*RECL
	ADDM A,NREAD(G)		; COUNT WORDS READ
	JSP F,DIGO
DIP3:	TLNE A,40000		; READING?
	TLNN A,20000		; YES. WANT DATA CHECKED?
	JRST DIP0		; NO TO ONE OF THOSE. DON'T CHECK
	MOVSI B,4		; REQUEST MAIN PROG TO CHECK DATA
	JSP F,DIMP
	JRST DIP0

DIGO:	HLL F,CHCW(G)
	TLO F,1
	MOVEM F,CHCW(G)
	IOR BF,BITS(G)		; FLAG BUSY FOR LIGHTS WATCHERS
	JRST DSKXR

DIMP:	HRRM F,CHCW(G)
	IORM B,CHCW(G)
	JRST DSKXR

RNDTK:	PUSHJ P,RND
	TLNE B,7
	JRST RNDT1		;MAKE CYL RANDOM ONLY 1/8 OF THE TIME
	SKIPE UWP(G)
	JRST RNDT3
	IDIVI B,ENDCYL(G)	;MOD BY GBH
	JRST RNDT2		; MAKE SURE NOT A BADSPOT

RNDT1:	IDIVI B,^D19		;RANDOM HEAD ONLY
	MOVE D,TRK(G)
	IDIVI D,^D19		;GET OLD HEAD
	SUB C,E	;NEW-OLD
	ADD C,TRK(G)
	SKIPN UWP(G)
	JRST RNDT2
	CAIGE C,^D798*^D19
	JRST RNDT3
RNDT2:	MOVE D,C		; TRACK*19+HEAD
	HRL D,G			; DRIVE
	MOVSI E,-NBADSP		; COUNT THRU BAD SPOTS TABLE
RNDT2L:	SKIPGE SPOTTB(E)	;A SPOT IN TABLE?
	JRST CECYL	;MOD BY GBH
;	POPJ P,0		;NO, SO SELECTED ONE IS OK
	CAMN D,SPOTTB(E)	;YES. SAME AS SELECTED ONE?
	JRST RNDTK		;YES. DON'T USE IT. PICK ANOTHER
	AOBJN E,RNDT2L		;NO, SEE IF ANY MORE
	POPJ P,

;CE SCRATCH PACK MODS BY GBH

CECYL:	MOVEI 15,3
	CAML C,LOWCYL(15)
	CAMLE C,HICYL(15)
	JRST .+2
	JRST RNDTK
	SOJGE 15,CECYL+1
	POPJ P,


LOWCYL:	XWD 0,^D18*^D19		;BEGIN 9C OUTER CYLINDER
	XWD 0,^D22*^D19		;BEGIN 9E OUTER CYLINDER
	XWD 0,^D780*^D19	;BEGIN 9E INNER CYLINDER
	XWD 0,^D790*^D19	;BEGIN 9C INNER CYLINDER
HICYL:	XWD 0,^D19*^D19		;BEGIN 9C OUTER CYLINDER
	XWD 0,^D26*^D19		;END 9E OUTER CYLINDER
	XWD 0,^D784*^D19	;END 9E INNER CYLINDER
	XWD 0,^D791*^D19	;END 9C INNER CYLINDER


IDEV:	300	;MOD BY GBH  DEFAULT DISK ADDRESS
CETST:	0
EXETR:	0

STRCYL:	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0
	XWD 0,0

ENDCYL:	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
	XWD 0,^D807*^D19
;END OF CE SCRATH PACK MODS



RNDT3:	IDIVI B,^D7*^D19
	ADDI C,^D798*^D19
	JRST RNDT2
RND:	MOVEI B,105
	FMPB B,RNDM
	POPJ P,
RNDM:	123456,,654321

RNDAD:	MOVE D,ADRHI
	SUB D,ADRLO		;SIZE OF BUFFER AREA
	IDIVI D,NPACKS		; SIZE FOR EACH DEVICE
	MOVEI E,(D)		; SIZE OF ONE DRIVE'S SPACE
	IMULI E,(G)		; BASE OF CURRENT GUY'S SPACE
	ADD E,ADRLO		; PLUS BASE OF ALL DATA AREAS.
	SUBI D,RECL*NRPT	;MAXIMUM POSSIBLE BUFFER ORIGIN OFFSET
	PUSHJ P,RND
	IDIVI B,1(D)		;OFFSET TO C
	ADD C,E			;BUFFER ORIGIN TO C
	POPJ P,

PBITS:	JUMPE E,CPOPJ
	TRNN E,1
	JRST PBIT1
	TLON D,400000
	JRST PBIT2
	MOVEI B,"+"
	PUSHJ P,TYO
PBIT2:	MOVE C,(D)
	PUSHJ P,STYO
PBIT1:	LSH E,-1
	AOJA D,PBITS

SNSMSG:	[ASCIZ /STATISTICS/]
	[ASCIZ /2-2/]
	[ASCIZ /CORRECTABLE/]
	[ASCIZ /2-0/]
	[ASCIZ /INCOMPLETE/]
	[ASCIZ /WRITE INHIBIT/]
	[ASCIZ /FILE PROTECT/]
	[ASCIZ /NO RECORD FOUND/]
	[ASCIZ /1-3/]
	[ASCIZ /CYLINDER END/]
	[ASCIZ /TRACK OVERRUN/]
	[ASCIZ /PERMANENT ERROR/]
	[ASCIZ /0-7/]
	[ASCIZ /0-6/]
	[ASCIZ /OVERRUN/]
	[ASCIZ /DATA CHECK/]
	[ASCIZ /EQUIPMENT CHECK/]
	[ASCIZ /BUS OUT PARITY CHECK/]
	[ASCIZ /INTERVENTION REQUIRED/]
	[ASCIZ /COMMAND REJECT/]
STAMSG:	[ASCIZ /UNIT EXCEPTION/]
	[ASCIZ /UNIT CHECK/]
	[ASCIZ /DEVICE END/]
	[ASCIZ /CHANNEL END/]
	[ASCIZ /BUSY/]
	[ASCIZ /CONTROL UNIT END/]
	[ASCIZ /STATUS MODIFIER/]
	[ASCIZ /ATTENTION/]
	[ASCIZ /PROG INT FLAG/]
	[ASCIZ /LENGTH ERROR/]
	[ASCIZ /BIT 5?/]
	[ASCIZ /SA10 CONTROL SEQ CHECK/]
	[ASCIZ /BUS IN PARITY CHECK/]
	[ASCIZ /SELECT ERROR/]

;DISK INTERRUPT, READ OR WRITE TO BE DONE. OPCODE IN C.

DIRW:	MOVSI A,2000
	MOVE B,CPGP(G)
	MOVEM B,DEVL(G)
	HRLI B,200000		;TCH
	LSH C,^D20		; TO OPCODE FIELD (RD OR WRT)
	MOVE D,DEVN		; DEVICE WHICH CAUSED THIS INTERRUPT
	LSH D,^D12
	TLC D,124034		;SEEK, UNCHAINED
	MOVE E,TRK(G)
	IDIVI E,^D19
	LSH E,^D20
	LSH F,4
	IOR E,F
	ADDI B,7
	MOVEM D,-7(B)		;STORE SEEK
	MOVE F,[<<-2>_CNTSFT>-400000,,[0]]
	MOVEM F,-6(B)
	MOVSI F,<-6>_CNTSFT
	HRRI F,200(B)
	MOVEM F,-5(B)		;PTR TO SEEK ARG
	MOVEI F,1
	MOVEM F,-4(B)
	MOVEM F,-1(B)
	TLC D,4220		;MAKE SET SECTOR
	MOVEM D,-3(B)
	MOVE F,[<<-1>_CNTSFT>,,[0]]	; ONE BYTE OF ZERO FOR SECTOR NUMBER
	MOVEM F,-2(B)		;PTR TO SECTOR
	TLC D,40110		;MAKE SRCH ID =
DIRW1:	MOVEM D,(B)		;STORE SRCH
	MOVSI F,<<-5>_CNTSFT>
	ADDI F,200(B)
	MOVEM F,1(B)		;STORE DCW FOR SEARCH
	MOVEM B,2(B)		;STORE TCH *-1 WHILE SRCH FAILS
	TLZ D,21774		; THEN DO THE RD OR WRT
	XOR D,C			; PUT RD OR WRT OPCODE INTO CMD
	MOVEM D,3(B)		;STORE CMD
	MOVSI F,<<-RECL>_CNTSFT>		; WORDS IN A RECORD (ONE PAGE)
	ADDI F,(A)
	ADD F,ADDR(G)		; BASE ADDR OF DATA AREA FOR THIS PACK
	MOVEM F,4(B)		;STORE DCW
	TLZ D,21774
	TLC D,20304		; CONVERT BACK TO "SEARCH ID EQUAL"
	MOVEM E,200(B)		;STORE CCHH
	MOVEM A,201(B)		;STORE REC #
	ADDI B,5		; COMMAND WORDS PER RECORD
	ADD A,[2000,,RECL]
	CAMGE A,[<NRPT+1>*2000,,0]	; RECORDS PER TRK. FILLED TRACK?
	JRST DIRW1			; NO. GO DO ANOTHER RECORD
	SETZM (B)		; YES. DONE. STORE HALT
	MOVSI F,40000
	ANDCAM F,-2(B)		;UNCHAIN LAST CMD
	POPJ P,


;HERE IF BUSY, NOT STATUS MOD, NOT CU END.
;HERE IF WRONG DEVICE INTERRUPTS.
;HERE IF "BAD BITS" IN CONI OR STATUS WORD (CHANNEL ERRORS)
DSE1:	SETOM SENSED		; IN CASE NO SENSE DONE
	HRLZ A,CBASE		; COPY THE 4 WORDS FROM LOW CORE
	HRRI A,DSTAT		; FORM BLT POINTER
	BLT A,DSTAT+3		; BEFORE DOING SENSE
	LDB D,[141000,,DSTAT+1]	; DEV CAUSING INT
	MOVE A,DSTAT+1		; SEE IF UNIT CHECK
	TLNE A,10		; ..
	PUSHJ P,DSENSE		; YES. DO A SENSE.
	LDB E,[202400,,SENSED]	; SUPPRESS ERROR TYPEOUT ON SOME OF
	CAIE E,100004		; THE CORRECTABLES, IN REPEATS.
	JRST DSE4		; NOT A CORRECTABLE
	MOVE E,RETRYC(G)	; CORRECTABLE. FIRST OR LAST?
	CAIE E,NRETRY
	CAIN E,0		; ..
	SKIPA			; YES.
	JRST DSKCDC		; NO. DON'T TYPE THE SENSE STUFF
DSE4:	PUSHJ P,PTIME		; TIME-STAMP THE ERROR
	LDB A,[420200,,DSTAT+1] ; GET 0=END,1=INIT,2=ASYNC,3=DUMMY
	MOVE C,STTT(A)		; WHAT KIND OF STATUS STORED
	PUSHJ P,STYO
	LDB D,[141000,,DSTAT+1]	; DEVICE NUMBER
	MOVEI C,[ASCIZ / STATUS, DEVICE %D =
/]
	PUSHJ P,STYO
	LDB E,[241600,,DSTAT+1] ;FOURTEEN STATUS BITS FRM 1ST WD
	MOVEI D,STAMSG
	PUSHJ P,PBITS
	MOVE D,DSTAT+2
	MOVE E,TRK(G)		; WHERE SHOULD IT HAVE BEEN?
	IDIVI E,^D19		; CYL TO E, HD TO F
	MOVEI C,[ASCIZ /
SW2=%D , CYLINDER $E, HEAD $F
/]
	PUSHJ P,STYO
	MOVE A,DSTAT+1
	TLNN A,10		; UNIT CHECK?
	JRST DSKXX		; NO, RESTART
DSE2:	MOVEI C,[ASCIZ /SENSE: /]
	PUSHJ P,STYO
	MOVE E,SENSED
	AOJE E,[HRROI C,[ASCIZ \MISSING
\]
		PUSHJ P,STYO
		JRST DSKXX]
	LDB E,[202400,,SENSED]
	LDB A,[POINT 6,SENSED+1,7]	;BYTE 4 TELLS WHAT DRIVE
	PUSHJ P,DPHADR		; PRINT PHYS ADR LETTER OR "?"
	PUSHJ P,CR
	MOVEI D,SNSMSG
	PUSHJ P,PBITS
	PUSHJ P,PSB
	MOVE E,SENSED		; SEE IF OFF LINE
	TLNE E,(1B1)		; ..
	SETOM OFFLIN(G)		; YES, FLAG NOT TO RUN IT.
	LDB E,[202400,,SENSED]	; FIRST TWENTY BITS OF SENSE DATA
	CAIN E,1		;STATISTICS?
	JRST DSKT0		;YES. NOT A FATAL ERROR, DON'T GET OPR
	CAIN E,100004		;SKIP UNLESS CORRECTABLE DATA CHECK
	JRST DSKCDC		;PRINT WHERE THE ERROR WAS
	AOS A,ERRCT(G)		; COUNT "OTHER" ERRORS
	CAMGE A,MAXBER		; MORE THAN MAX BAD ERRORS?
	JRST DSE5		; NO
	SETOM OFFLIN(G)		; YES. SHUT IT DOWN.
	MOVEI D,(G)		; TELL THE WORLD
	MOVEI C,[ASCIZ /
*** DRIVE %D EXCESSIVE ERRORS. MARKED OFF LINE, /]
	PUSHJ P,STYO
	PUSHJ P,PTIME
DSE5:	JRST DSKXX		; GET OPR OR RESTART.

MAXBER:	EXP ^D50

; CALL WITH DRIVE CODE IN A

DPHADR:	PUSH P,A
	MOVEI C,[ASCIZ /PHYSICAL DRIVE /]
	PUSHJ P,STYO
	MOVSI C,-10
	POP P,A
	CAMN A,PHADRT(C)	; LOOK THRU FUNNY NAMES
	JRST DPHAD2		; FOUND IT
	AOBJN C,.-2
	MOVEI B,"?"		; NOT FOUND
	JRST DPHAD3
DPHAD2:	MOVEI B,"A"(C)
DPHAD3:	PUSHJ P,TYO
	POPJ P,0

DSENSE:	DPB D,[141000,,PSENSE]	; SUBR TO GET SENSE FROM DEV IN D
	MOVEI A,10
	MOVEM A,SENSCT
DSE2A:	MOVE A,[MOVE PSENSE]
	MOVEM A,@CBASE
	SETOM SENSED
	SETZM SENSED+1
	SETZM SENSED+2
	SETZM SENSED+3
	SETZM SENSED+4
	SETZM SENSED+5
	SETZM @CBASE+1
	CONO SA0,@STBSYC
	CONO SA0,@CLSTFC
	MOVEI A,-1
	CONSO SA0,170
	SOJG A,.-1
	JUMPLE A,DSE2B
	MOVE A,@CBASE+1
	TLNN A,(1B11)		; BUSY?
	JRST DSE2B
	SOSLE SENSCT
	JRST DSE2A
DSE2B:	POPJ P,0

DSENS1:	DPB D,[141000,,PNOP]	; SAME SUBR BUT DO NOP FIRST
	MOVE A,[MOVE PNOP]	; BECAUSE SENSE NOT UP TO DATE AT
	MOVEM A,@CBASE		; AT STARTUP WITHOUT IT
	CONO SA0,@STBSYC
	CONO SA0,@CLSTFC
	MOVEI A,-1
	CONSO SA0,170
	SOJG A,.-1
	JRST DSENSE
PNOP:	BYTE (8) 240,3,0,0
	0

STTT:	[ASCIZ /ENDING/]
	[ASCIZ /INITIAL/]
	[ASCIZ /ASYNC/]
	[ASCIZ /DUMMY/]


DSKCDC:	AOS NCORDC(G)		; COUNT CORRECTABLE DATA CHECKS
	MOVSI A,(1B13)		; FLAG CORRECTABLE CHECK
	IORB A,CHCW(G)		; IN DRIVE STATUS WORD
	SOS D,RETRYC(G)		; COUNT DOWN RETRIES
	CAIN D,NRETRY-1		; IF THIS IS FIRST ERROR,
	JRST DSKWHR		; GO REPORT ITS LOCATION
	JUMPGE D,DSKWHX		; IF NOT COUNTED OUT, QUIT HERE.
	MOVEI D,NRETRY		; CALL IT PERMANENT
	MOVEI C,[ASCIZ /*** DATA CHECK AFTER $D RE-READS.
/]
	PUSHJ P,STYO		; COMPLAIN, FALL INTO FULL REPORTER
DSKWHR:	LDB D,[POINT 3,SENSED+1,27]	; SENSE FORMAT DESCRIPTOR
	CAIE D,4		;IF 4 OR 5, GIVES POSITIONS.
	CAIN D,5		; ..
	JRST DSKWH1		; GOOD
	MOVEI C,[ASCIZ /SENSE BYTE FORMAT IS %D, SHOULD BE 4 OR 5.
/]
	PUSHJ P,STYO
	JRST DSKWHX		;GIVE UP
DSKWH1:	LDB D,[POINT 16,SENSED+2,15]	; CYL
	LDB E,[POINT 16,SENSED+2,31]	; HEAD
	MOVEI C,[ASCIZ /ERROR REPORT: DRIVE %G, CYLINDER %D, HEAD %E/]
	PUSHJ P,STYO
	LDB D,[POINT 8,SENSED+3,7]	; RECORD
	LDB E,[POINT 8,SENSED+3,15]	; SECTOR
	LDB F,[POINT 8,SENSED+3,23]	; OFFSET
	MOVEI C,[ASCIZ /
   RECORD %D, SECTOR %E, OFFSET %F
/]
	PUSHJ P,STYO
DSKWHX:	JRST DSKT0

PHADRT:	EXP 70,61,52,43,34,25,16,07	;WIERD NAMES OF DRIVES

; ROUTINE TO PRINT ERROR STUFF FOR TAPE TEST

TSPE1:	SETOM TAPF		; FLAG TAPE, NOT DISK
	LDB A,[420200,,@CBASE+1] ; GET 0=END,1=INIT,2=ASYNC,3=DUMMY
	MOVE C,STTT(A)		; WHAT KIND OF STATUS STORED
	PUSHJ P,STYO
	LDB D,[141000,,@CBASE+1] ; DEVICE ADDR FROM STORED STATUS
	DPB D,[141000,,PSENSE]	; TO DO SENSE ON IT.
	MOVEI C,[ASCIZ / STATUS, DEVICE %D =
/]
	PUSHJ P,STYO
	LDB E,[241600,,@CBASE+1] ;FOURTEEN STATUS BITS FRM 1ST WD
	MOVEI D,STAMSG
	PUSHJ P,PBITS
	MOVE D,@CBASE+2
	MOVEI C,[ASCIZ /
SW2=%D
/]
	PUSHJ P,STYO
	MOVE A,@CBASE+1
TSE2:	MOVEI A,10
	MOVEM A,SENSCT
TSE2A:	MOVE A,[MOVE PSENSE]
	MOVEM A,@CBASE
	SETOM SENSED
	SETZM SENSED+1
	MOVE A,[SENSED+1,,SENSED+2]
	BLT A,SENSED+5
	SETZM @CBASE+1
	CONO SA0,@STBSYC
	CONO SA0,@CLSTFC
	MOVEI A,-1		; COUNTER
	CONSO SA0,170
	SOJG A,.-1
	JUMPLE A,TSE2B
	MOVE A,@CBASE+1
	TLNN A,(1B11)		; BUSY?
	JRST TSE2B		; NO.
	SOSLE SENSCT
	JRST TSE2A
TSE2B:	MOVEI C,[ASCIZ /SENSE: /]
	PUSHJ P,STYO
	MOVE E,SENSED
	AOJE E,[HRROI C,[ASCIZ \MISSING
\]
		PUSHJ P,STYO
		JRST QTT0]
	LDB E,[202400,,SENSED]
	MOVEI D,SNSMSG
	LDB E,[242000,,SENSED]	;SIXTEEN BITS OF SENSE DATA
	MOVEI D,TAPSNS
	PUSHJ P,PBITS
	PUSHJ P,PSB
	POPJ P,0
PAGE
;MAINTANCE PACK MODS BY GBH 18 APRIL 77


DSKT:	PUSHJ P,EXT	;MAKE A PASS THRU BEG
	SETZ A,
	MOVEM A,DSVFLG
	JRST DSGO
DSKTV:	PUSHJ P,EXT
	SETZM CETST
	SETO A,
	MOVEM A,DSVFLG
	JRST DSGO
DSKTVT:	PUSHJ P,EXT	;ENTRY FOR VERIFIER ON CE SCRATCH PACK
	SETOM CETST
	SETO A,
	MOVEM A,DSVFLG
DSGO:	MOVEI G,NPACKS-1
	MOVE A,ENDCYL(G)
	CAILE A,^D807*^D19
	PUSHJ P,ALTCYL
	SOJGE G,DSGO+1
CKCYL:	MOVEI G,NPACKS-1
	MOVE A,STRCYL(G)
	MOVE B,ENDCYL(G)
	CAMLE A,ENDCYL(G)
	JRST SAMCYL
	MOVEI C,3
	CAML A,LOWCYL(G)
	CAMLE B,HICYL(G)
	JRST .+2
	JRST CYLNOP
	SOJGE C,.-4
	SOJGE G,CKCYL+1
	MOVEI C,[ASCIZ/**********WARNING**********
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/TEST WILL DESTROY DATA ON ALL ONLINE DRIVES
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/OR ANY DRIVE(S) THAT COME ONLINE DURING TEST
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/INSURE ALL SYSTEM PACKS ARE WRITE PROTECTED AND STOPPED
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/PUSH ALT MDE OR P ON TTY WHEN READY TO START TEST
/]
	PUSHJ P,STYO
	NALT

;END OF MAINTANCE PACK MODS

;THE FOLLOWING HAS BEEN DELETED BY GBH

;DSKT:	TDZA A,A		;ENTRY FOR RANDOM TRANSFERS
;DSKTV:	SETO A,			;ENTRY FOR VERIFYING SURFACES
;	MOVEM A,DSVFLG		;DISK SURFACE VERIFY FLAG


;END OF DELETIONS

;	MOVEI A,DCHAN	;DELETED BY GBH
	MOVEI C,[ASCIZ/TEST STARTED
/]
	PUSHJ P,STYO
	MOVE A,DCHAN	;MOD BY GBH
	MOVEM A,CHAN		;CHANNEL OF SA10 (0-3)
	PUSHJ P,CSET		;SET UP TO USE IT
;MOD BY GBH
	PUSHJ P,SDLST		;SET DEVICE LIST
	SETZM TIME
	SETZM TIMEC		; CLEAR RUNNING TIME COUNTERS.
	CONSZ TTY,20		; RESTART HERE ON FATAL ERRORS
	JRST .-1
	CONO 635550
	CONO 20000	;DIS AUTO RESTART ON KI-10
	MOVEI A,10000
	SOJG A,.
	CONO 4,10000		; CLEAR PI
	JFCL 17,.+1		; AND APR FLAGS SO CONSZ BELO WORKS
				; ON BOTH KA AND KI10
	MOVSI B,-1000		; SCAN FOR MEMORY SIZE
DSKTS2:	MOVE A,(B)		; SEE IF MEMORY EXISTS
	CONSZ 0,1B23!1B29	; KA OR KI NXM?
	JRST DSKTS1		; YES
	ADDI B,777		; NO. ANOTHER PAGE
	AOBJN B,DSKTS2		; LOOP THRU ALL MEMORY
DSKTS1:	MOVEI B,-1(B)		; LAST EXISTENT LOCATION
	CAMGE B,ADRHI		; ADRHI TOO HIGH?
	MOVEM B,ADRHI		; YES. BACK OFF.
	MOVEI D,(B)
	MOVEI C,[ASCIZ /
LAST WORD OF MEMORY IS %D
/]
	PUSHJ P,STYO
	MOVSI G,-NPACKS
DSKTL1:	SETZM OFFLIN(G)		; ASSUME NOT OFF LINE
	HRRZ D,IDEV		;MOD BY GBH
	ADD D,G			;MOD BY GBH
	HRLI D,0	;MOD BY GBH
;	MOVEI D,IDEV(G)		; SEE IF IT IS ON LINE
	PUSHJ P,DSENS1		; DISK SENSE ROUTINE
	MOVE A,SENSED		; GET OFF-LINE BIT
	TLNN A,(1B1)		; INTERVENTION REQUIRED?
	JRST DSKONL		;MOD BY GBH DO ONLINE ROUTINE
;	JRST DSKTS3		; NO.
	SETOM OFFLIN(G)		; YES.
	SKIPE OFFPRT		;MOD BY GBH
	JRST DSKTS3		;MOD BY GBH
	LDB A,[POINT 6,SENSED+1,7] ; MAGIC CODE FOR THIS DRIVE
	PUSHJ P,DPHADR		; TYPE PHYSICAL DRIVE NAME
	LDB D,[POINT 1,SENSED+1,0]	;MOD BY GBH
	MOVEI C,[ASCIZ/, STRING %D/] ;MOD BY GBH
	PUSHJ P,STYO
	HRRZ D,IDEV	;MOD BY GBH
	ADD D,G		;MOD BY GBH
	HRLI D,0	;MOD BY GBH
;	MOVEI D,(G)		; AND LOGICAL NAME
	MOVEI C,[ASCIZ /, LOGICAL DRIVE %D OFF LINE.
/]
	PUSHJ P,STYO
DSKTS3:	PUSHJ P,RNDAD		; SET UP ADDRESS TABLE
	MOVEM C,ADDR(G)		; IN CASE NOT RANDOM DURING TEST
	SETZM RETRYC(G)		; CLEAR RETRY COUNTER
	SETZM NREAD(G)		; CLEAR SUMMARY COUNTERS OF READS,
	SETZM NWRITE(G)		; WRITES,
	SETZM NCORDC(G)		; AND CORRECTABLE DATA CHECKS
	SETZM ERRCT(G)		; AND OTHER ERRORS
;DELETED BY GBH
;	SETZM TRK(G)		; START AT TRACK 0 IN CASE VERIFIER
	MOVE A,STRCYL(G)	;MOD BY GBH
	MOVEM A,TRK(G)		;MOD BY GBH
	MOVSI A,(37B17!1B9!3B8)	; CLEAR WAITING STATES, VERIFIER FLAG,
	ANDCAB A,CHCW(G)	; AND VERIFIER STATE FIELD
	MOVSI A,(1B9)		; SET VERIFIER BIT IF
	SKIPE DSVFLG		; STARTED AT DSKVT
	IORM A,CHCW(G)		; ..
	AOBJN G,DSKTL1
	JRST .+1
	JRST .+1
	JRST DSKT0		;MOD BY GBH

;MOD BY GBH

DSKONL:	SKIPN OFFPRT		;SKIP IF ONLINE MESSAGE
	JRST DSKTS3		;PRINT OFFLINE MESSAGE
	LDB A,[POINT 6,SENSED+1,7]
	PUSHJ P,DPHADR
	LDB D,[POINT 1,SENSED+1,0]
	MOVEI C,[ASCIZ/, STRING %D/]
	PUSHJ P,STYO
	HRRZ D,IDEV	;MOD BY GBH
	ADD D,G		;MOD BY GBH
	HRLI D,0	;MOD BY GBH
;	MOVEI D,(G)
	MOVEI C,[ASCIZ/, DRIVE %D ONLINE
/]
	PUSHJ P,STYO
	JRST DSKTS3

ALTCYL:	MOVEI D,(G)	;MOD BY GBH
	MOVEI C,[ASCIZ/WARNING ON DRIVE %D
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/ROUTINE WILL WRITE ON ALTERNATE TRACK CYLINDERS
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/ERROR CONDITIONS MAY RESULT
/]
	PUSHJ P,STYO
	POPJ P,

SAMCYL:	MOVEI D,(G)
	MOVEI C,[ASCIZ/DRIVE %D HAS START CYL GREATER THAN END CYL
/]
	PUSHJ P,STYO
	JRST DDT

CYLNOP:	MOVEI D,(G)
	MOVEI C,[ASCIZ/DRIVE %D HAS START AND END CYL WITHIN
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/A CE CYLINDER
/]
	PUSHJ P,STYO
	JRST DDT
;END OF MOD


;FALL THRU, ALSO BACK HERE AFTER ERRORS.
DSKT0:	CONSZ TTY,20
	JRST .-1
	CONO 0,635550
	CONO 4,252377
;	MOVEI P,PDL-1
	MOVE A,[JSR DSKI]
	MOVEM A,40+SA0CH+SA0CH
	MOVE A,[JSR APRI]
	MOVEM A,40+APRCH*2
	MOVEI C,[BYTE (7)15,12,0]
	PUSHJ P,STYO
	CONO 2000+APRCH		;ENABLE CLOCK

	MOVEI BF,0		; LIGHT-WATCHER CELL
	MOVSI G,-NPACKS
DSKT1:	MOVSI A,17		; CLEAR WAITING STATES
	ANDCAM A,CHCW(G)	; IN CONTROL WORD
	SETOM DEVL(G)
	SETZM HUNGT(G)		; NEVER STARTED DRIVE YET
DSKT1A:	MOVEI B,DIP0		; INITIAL FSM FOR DRIVE
	MOVE A,CHCW(G)
	TLNN A,(1B13)		; RETRYING?
	JRST DSKT1B		; NO.
	MOVEI B,DIP2A		; YES. START AT THE READ
	SKIPL RETRYC(G)		; UNLESS ALL COUNTED OUT
	JRST DSKT1B		; STILL TRIES LEFT. GO READ.
	MOVSI A,(1B13)		; TOO MANY. CLEAR RETRY FLAG
	ANDCAB A,CHCW(G)	; SET TO RUN CHECKER
	MOVE C,TRK(G)		; PUT IT IN THE BADSPOT TABLE
	HRL C,G			; PUT DRIVE IN LH
	MOVSI B,-NBADSP		; AND PUT RESULT IN TABLE
	SKIPL SPOTTB(B)		; FREE SLOT? (NEGATIVE)
	AOBJN B,.-1		; NO, FIND ONE
	SKIPGE B		; ONE FOUND?
	MOVEM C,SPOTTB(B)	; YES. STORE THE BAD SPOT
	MOVEI B,DIP3		; CHECKER WILL MOVE PAST THE ERROR
DSKT1B:	HRRM B,CHCW(G)
	MOVE B,CPGP(G)
	HRRI B,[0]
	TLNE A,4000	;TEST THIS DRIVE AT ALL?
	SKIPE OFFLIN(G)
	SKIPA			; DON'T START DRIVE
	MOVEM B,DEVL(G)		; GIVE IT EMPTY CMD LIST TO GET INT
	AOBJN G,DSKT1

	MOVE A,[600000,,DEVL]
	MOVEM A,@CBASE
	CONO SA0,@STPIEC
	CONO SA0,@STSRQC
DSKT2:	MOVSI	G,-NPACKS
DSKT3:	MOVE A,CHCW(G)
	TLNE A,2
	JRST FILLIT
	TLNE A,4
	JRST CHKIT
	SKIPG B,HUNGT(G)	; SEE IF DRIVE IS HUNG
	JRST DSKT4		; NEGATIVE MEANS IDLE
	ADDI B,^D60*^D30	; TIME TILL DRIVE IS HUNG (TICKS)
	CAMG B,TIME		; THAT LONG SINCE INT?
	JRST HUNG		; YUP
DSKT4:	MOVSI B,10		; DRIVE JUST COME ON LINE?
	TDNN B,CHCW(G)		; ..
	JRST DSKT5		; NOPE.
	ANDCAM B,CHCW(G)	; YES. CLEAR THE FLAG AND COMMENT.
	MOVEI D,(G)
	MOVEI C,[ASCIZ /*** DRIVE %D RUNNING, /]
	PUSHJ P,STYO
	PUSHJ P,PTIME
DSKT5:	AOBJN G,DSKT3
	MOVE A,TIMEC
	CAIGE A,^D60*^D60*^D15
	JRST DSKT2
	SETZM TIMEC
	MOVEI C,[ASCIZ /
TIME CHECK:  /]
	PUSHJ P,STYO
	PUSHJ P,PTIME		; REPORT TOTAL TIME
	MOVEI C,[ASCIZ /SUMMARY:

DRIVE, BYTES READ, BYTES WRITTEN, CORR'BLE DATA CKS, OTHER ERRS

/]
	PUSHJ P,STYO
	MOVEI G,0
DSKSML:	SKIPE OFFLINE(G)	;MOD BY GBH
	JRST DSKSM1	;MOD BY GBH
	MOVE D,NREAD(G)	;MOD BY GBH
;DSKSML:	MOVE D,NREAD(G)
	ASH D,-1
	IMULI D,11		; CONVERT TO BYTES (8 BITS)
	MOVE E,NWRITE(G)
	ASH E,-1
	IMULI E,11		; CONVERT TO BYTES
	MOVE F,NCORDC(G)
	MOVE H,ERRCT(G)
	HRRZ A,IDEV	;MOD BY GBH
	ADD A,G	;MOD BY GBH
	HRLI A,0	;MOD BY GBH
	MOVEI C,[ASCIZ /  %A,    $D,    $E,    $F,   $H
/]
	PUSHJ P,STYO
DSKSM1:	CAIGE G,NPACKS-1
	AOJA G,DSKSML
	PUSHJ P,CR
	JRST DSKT2

FILLIT:	MOVEI A,NRETRY		; SET UP MAX COUNT IN CASE OF ERRORS
	MOVEM A,RETRYC(G)	; ..
	HRRZ A,ADDR(G)
	HRLI A,-RECL*NRPT
	MOVE C,CHCW(G)		; WANT RANDOM, OR SURFACE VERIFIER
	MOVE B,TRK(G)
	TLNE C,(1B9)		; ..
	JRST FILLV		; VERIFIER
FILI1:	MOVEM B,(A)
	SETCAM B,1(A)
	AOBJP A,FILX
	ROT B,1
	AOBJN A,FILI1
FILX:	MOVSI A,6
	ANDCAM A,CHCW(G)
	MOVE A,CPGP(G)
	HRRI A,[0]
	MOVEM A,DEVL(G)
	CONO SA0,@STSRQC
	JRST DSKT4

FILLV:	TLNE C,(2B8)		; WORST CASE?
	JRST FILLVW		; YES
	MOVEI B,0		; NO. ZEROES OR ONES
	TLNE C,(1B8)		; ..
	SETO B,			; ONES.
	MOVEM B,(A)
	AOBJN A,.-1		; FILL WITH THIS WORD
	JRST FILX

FILLVW:	MOVSI C,-NWCPDT		; GENERATE BUFFER FULL OF WORSTCASE PAT
FILVW1:	MOVE B,WCPDAT(C)
	MOVEM B,(A)		; A WORD OF THE PATTERN
	AOBJN C,.+2		; COUNT THE SOURCE
	MOVSI C,-NWCPDT		; WRAPAROUND
	AOBJN A,FILVW1
	JRST FILX

WCPDAT:	; 28-BIT REPEATING PATTERN FROM CALCOMP CONSULTANTS

IFG CV,<	; HERE IT IS EXCEPT FOR SA10 ROTATING/SPLITTING A BYTE
	BYTE (4) 17,13,6,15,13,6,0,17,13
	BYTE (4) 6,15,13,6,0,17,13,6,15
	BYTE (4) 13,6,0,17,13,6,15,13,6
	BYTE (4) 0,17,13,6,15,13,6,0,17
	BYTE (4) 13,6,15,13,6,0,17,13,6
	BYTE (4) 15,13,6,0,17,13,6,15,13
	BYTE (4) 6,0,17,13,6,15,13,6,0
	BYTE (4) 17,13,6,15,13,6,0,17,13
	BYTE (4) 6,15,13,6,0,17,13,6,15
	BYTE (4) 13,6,0,17,13,6,15,13,6
	BYTE (4) 0,17,13,6,15,13,6,0,17
	BYTE (4) 13,6,15,13,6,0,17,13,6
	BYTE (4) 15,13,6,0,17,13,6,15,13
	BYTE (4) 6,0,17,13,6,15,13,6,0
>
IFLE CV,<	;AND THIS IS SAME WITH ODD WORDS SHUFFLED AS NEEDED
	BYTE (4) 17,13,6,15,13,6,0,17,13
	BYTE (4) 15,13,6,0,17,13,6,15,6
	BYTE (4) 13,6,0,17,13,6,15,13,6
	BYTE (4) 17,13,6,15,13,6,0,17,0
	BYTE (4) 13,6,15,13,6,0,17,13,6
	BYTE (4) 13,6,0,17,13,6,15,13,15
	BYTE (4) 6,0,17,13,6,15,13,6,0
	BYTE (4) 13,6,15,13,6,0,17,13,17
	BYTE (4) 6,15,13,6,0,17,13,6,15
	BYTE (4) 6,0,17,13,6,15,13,6,13
	BYTE (4) 0,17,13,6,15,13,6,0,17
	BYTE (4) 6,15,13,6,0,17,13,6,13
	BYTE (4) 15,13,6,0,17,13,6,15,13
	BYTE (4) 0,17,13,6,15,13,6,0,6
>
NWCPDT==.-WCPDAT		;LENGTH OF REPEATED BLOCK

CHKIT:	MOVE C,CHCW(G)		; SEE IF IN RETRY SEQUENCE
	TLNN C,(1B13)		; ..
	JRST CHK0		; NO.
	MOVNI D,NRETRY+1	; YES. BUT READ OK NOW, SAY SO.
	ADD D,RETRYC(G)		; COMPUT READS NEEDED
	MOVMS D			; ..
	HRRZ E,IDEV	;MOD BY GBH
	ADD E,G	;MOD BY GBH
	HRLI E,0	;MOD BY GBH
;	MOVEI E,(G)		; RH DRIVE NUMBER
	MOVEI C,[ASCIZ /DRIVE %E READ OK ON PASS $D.

/]
	PUSHJ P,STYO
	MOVSI A,(1B13)		; CLEEAR RETRYING FLAG
	ANDCAM A,CHCW(G)
CHK0:	HRRZ A,ADDR(G)
	HRLI A,-RECL*NRPT
	MOVE C,CHCW(G)		; RANDOM OR FIXED PATTERN?
	TLNE C,(1B9)		; ..
	JRST CHKV		; FIXED. VERIFY SURFACE
	SKIPGE RETRYC(G)	; PERMANENT ERROR?
	JRST CHKX		; YES. DON'T TEST DATA
	MOVE B,TRK(G)
CHK1:	CAME B,(A)
	PUSHJ P,UNDET		; UNDETECTED ERROR!!
	SETCM C,1(A)
	CAME B,C
	JRST [	ADDI A,1	; UNDETECTED ERROR. MAKE AC'S LIKE 
		SETCA B,	; IN OTHER CALLS TO ERR ROUTINE
		PUSHJ P,UNDET
		SETCA B,
		SOJA A,.+1]
	AOBJP A,FILX
	ROT B,1
	AOBJN A,CHK1
CHKX:	SETZM RETRYC(G)		; START FRESH
	JRST FILX

CHKV:	SKIPGE RETRYC(G)	; END OF PERMANENT ERROR?
	JRST CHKVX		; YES.
	TLNE C,(2B8)		; WORST CASE PATTERN?
	JRST CHKVW		; YES
	MOVEI B,0		; NO. ZEROES, OR ONES
	TLNE C,(1B8)		; ..
	SETO B,			; ONES.
	CAME B,(A)		; IS WORD CORRECT?
	PUSHJ P,UNDET		; NO. UNDETECTED ERROR.
	AOBJN A,.-2		; CHECK WHOLE BUFFER
	JRST CHKVX		; DONE

CHKVW:	MOVSI C,-NWCPDT		; COUNT THRU CORRECT DATA
CHKVW1:	MOVE B,WCPDAT(C)	; GET CORRECT DATUM
	CAME B,(A)		; IS THIS WHAT WE READ?
	PUSHJ P,UNDET		; NO. UNDETECTED ERROR
	AOBJN C,.+2		; OK, COUNT THRU GOOD DATA
	MOVSI C,-NWCPDT		; WRAP AROUND
	AOBJN A,CHKVW1
CHKVX:	AOS A,TRK(G)		; ON TO NEXT HEAD
	CAML A,ENDCYL(G)	;MOD BY GBH
	JRST .+4		;MOD BY GBH
	SKIPN CETST		;MOD BY GBH
	JRST CHKVXX		;MOD BY GBH
	JRST CHKVA		;MOD BY GBH
	MOVE A,STRCYL(G)	;MOD BY GBH
	MOVEM A,TRK(G)		;MOD BY GBH
	SKIPL A,CEPTRN(G)	;MOD BY GBH
	JRST FXPTRN		;MOD BY GBH

;THE FOLLOWING HAS BEEN DELTED BY GBH

;	CAIGE A,^D411*^D19	; ALL DONE?
;	JRST CHKVXX		; NO
;	SETZM TRK(G)		; YES. ON TO NEXT PATTERN, BACK TO CYL 0

;END OF DELETIONS


	LDB A,[POINT 2,CHCW(G),8]
	ADDI A,1
	CAIL A,3
	MOVEI A,0		; RECYCLE AFTER ALL DONE
	DPB A,[POINT 2,CHCW(G),8]
	JUMPN A,CHKVXX
CHKVB:	MOVEI D,(G)
	MOVEI C,[ASCIZ /FINISHED VERIFY PASS ON DRIVE %D.
/]
	PUSHJ P,STYO
CHKVXX:	JRST CHKX

;CE SCRATCH PACK MOD BY GBH

CHKVA:	MOVEI 15,3
	CAML A,LOWCYL(15)
	CAMLE A,HICYL(15)
	JRST .+2
	JRST CHKVX
	SOJGE 15,CHKVA+1
	JRST CHKVXX

CEPTRN:	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1
	-1

FXPTRN:	SKIPG A,CEPTRN(G)
	JRST ZRO
	CAIG A,1
	JRST ONEDAT
WRST:	MOVEI A,2
	DPB A,[POINT 2,CHCW(G),8]
	HRRZ D,IDEV
	ADD D,G
	HRLI D,0
	MOVEI C,[ASCIZ/WORST CASE DATA DRIVE %D
/]
	PUSHJ P,STYO
	JRST CHKVB

ONEDAT:	MOVEI A,1
	DPB A,[POINT 2,CHCW(G),8]
	HRRZ D,IDEV
	ADD D,G
	HRLI D,0
	MOVEI C,[ASCIZ/ALL ONES DATA DRIVE %D
/]
	PUSHJ P,STYO
	JRST CHKVB
ZRO:	MOVEI A,0
	DPB A,[POINT 2,CHCW(G),8]
	HRRZ D,IDEV
	ADD D,G
	HRLI D,0
	MOVEI C,[ASCIZ/ALL ZEROES DATA DRIVE %D
/]
	PUSHJ P,STYO
	JRST CHKVB

;END OF MODS
PTIME:	MOVE A,TIME		; REPORT TOTAL TIME
	IDIVI A,^D60*^D60*^D60	; HOURS TO A
	MOVE D,A
	IDIVI B,^D60*^D60	;MINUTES TO B
	MOVE E,B
	MOVEI C,[ASCIZ /TIME $D HOURS, $E MINUTES
/]
	PUSHJ P,STYO
	POPJ P,0

HUNG:	SETOM HUNGT(G)		;SO DOESN'T REPEAT
	MOVEI D,(G)
	MOVEI C,[ASCIZ /
***   DRIVE %D HUNG, /]
	PUSHJ P,STYO
	PUSHJ P,PTIME
	JRST DSKT4

APRI:	0
	CONSZ APR,10000
	JSR BAD	;NXM
	CONSZ PI,200000
	JSR BAD	;PAR ERR
	CONSO APR,1000
	JRST 12,@APRI
	AOS TIME
	AOS TIMEC
	CONO 1000+APRCH
	JRST 12,@APRI

UNDET:	PUSH P,C		; HERE ON UNDETECTED READ ERROR
	PUSH P,A		; OR CATASTROPHE CAUSING CHECK ROUTINE
	PUSH P,B		; TO FAIL. SAVE AC'S
	HRRZ D,IDEV	;MOD BY GBH
	ADD D,G	;MOD BY GBH
	HRLI D,0	;MOD BY GBH
;	MOVEI D,(G)		; DRIVE NUMBER
	MOVE E,TRK(G)		; CYL AND HEAD
	IDIVI E,^D19		; CYL TO E, HD TO F
	MOVEI C,[ASCIZ /
*** UNDETECTED ERROR, DRIVE %D, CYLINDER %E, HEAD %F
/]
	PUSHJ P,STYO
	MOVE D,0(P)
	MOVE A,-1(P)
	MOVE E,0(A)
	MOVEI C,[ASCIZ /    WORD SHOULD BE %D, BUT WAS %E
/]
	PUSHJ P,STYO
	PUSHJ P,PTIME
	PUSHJ P,CR2
	POP P,B
	POP P,A
	POP P,C
	SKIPE SERR		; STOP ON ERROR?
	JSR GOPR		; YES. RING FOR HUMAN OPERATOR
	POPJ P,0

BAD:	0
	CONO PI,400
	PUSH P,A
	PUSH P,B
	PUSH P,C
	PUSH P,D
	HRRZ D,BAD
	MOVEI C,[ASCIZ /BAD ERROR FROM %D
/]
	PUSHJ P,STYO
	POP P,D
	POP P,C
	POP P,B
	POP P,A
	SKIPN SERR
	JRST DSKT0
	JSR GOPR

GOPR:	0
	DATAI TTY,B
GOPR1:	MOVEI B,7
	PUSHJ P,TYO
	CONSO TTY,40
	JRST GOPR1
	JRST DDT



CPGP:
IFLE CV,<
	BYTE (8)320,0(20)CPG0
	BYTE (8)320,0+1(20)CPG1
	BYTE (8)320,0+2(20)CPG2
	BYTE (8)320,0+3(20)CPG3
	BYTE (8)320,0+4(20)CPG4
	BYTE (8)320,0+5(20)CPG5
	BYTE (8)320,0+6(20)CPG6
	BYTE (8)320,0+7(20)CPG7
	BYTE (8)320,0+10(20)CPG10
	BYTE (8)320,0+11(20)CPG11
	BYTE (8)320,0+12(20)CPG12
	BYTE (8)320,0+13(20)CPG13
	BYTE (8)320,0+14(20)CPG14
	BYTE (8)320,0+15(20)CPG15
	BYTE (8)320,0+16(20)CPG16
	BYTE (8)320,0+17(20)CPG17
>
IFG CV,<
	BYTE (8)0,320(20)CPG0
	BYTE (8)0+1,320(20)CPG1
	BYTE (8)0+2,320(20)CPG2
	BYTE (8)0+3,320(20)CPG3
	BYTE (8)0+4,320(20)CPG4
	BYTE (8)0+5,320(20)CPG5
	BYTE (8)0+6,320(20)CPG6
	BYTE (8)0+7,320(20)CPG7
	BYTE (8)0+10,320(20)CPG10
	BYTE (8)0+11,320(20)CPG11
	BYTE (8)0+12,320(20)CPG12
	BYTE (8)0+13,320(20)CPG13
	BYTE (8)0+14,320(20)CPG14
	BYTE (8)0+15,320(20)CPG15
	BYTE (8)0+16,320(20)CPG16
	BYTE (8)0+17,320(20)CPG17
>
;MOD BY GBH
SDLST:
IFLE CV,<
	MOVE B,IDEV
	MOVEI A,777760
	AND B,A
	MOVEI G,0
	MOVE A,[POINT 8,CPGP(G),15]
	DPB B,A
	ADDI B,1
	ADDI G,1
	CAILE G,17
	POPJ P,
	JRST .-5
>
IFG CV,<
	MOVE B,IDEV
	MOVEI A,777760
	AND B,A
	MOVEI G,0
	MOVE A,[POINT 8,CPGP(G),7]
	DPB B,A
	ADDI B,1
	ADDI G,1
	CAILE G,17
	POPJ P,
	JRST .-5
>

; CONTROL WORDS FOR DRIVE. DISPATCH IN RH, BITS IN LH.
; SEE COMMENTS ON BIT USAGE AT DIP0
CHCW:	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0
	374000,,0

ADRLO:	40000		;FIRST AREA FREE FOR BUFFERS
			; SPACE TO LOAD PROG AT 1000, DDT, SYMS.
ADRHI:	777000		;FIRST NON-EX ADDR

ADDR:	BLOCK 20		; DATA AREAS PER DRIVE
OFFLIN:	BLOCK 20		; NON-ZERO IF DRIVE OFF LINE
NREAD:	BLOCK 20
NWRITE:	BLOCK 20
NCORDC:	BLOCK 20
ERRCT:	BLOCK 20		; NUMBER OF ERRORS OTHER THAN CORR-
				; ECTABLE DATA CHECKS AND STATISTICS
TRK:	BLOCK 20		; CYL*19 + HEAD OF CURRENT TRANSFER
UWP:	BLOCK 20		;USE WHOLE PACK IF NON ZERO
RETRYC:	BLOCK 20		; COUNTER FOR CORRECTABLE DATA CK'S

HUNGT:	BLOCK 20		;VALUE OF TIME WHEN INT OCCURRED.
				; MINUS ONE IF HUNG OR NOT STARTED.

DCHAN:	000000,,000001
DEVN:	0
ACS:	BLOCK G+1
OFFPRT:	-1	;MOD BY GBH
SERR:	0
DSVFLG:	0			; DSK SURFACE VERIFIER FLAG

SPOTTB:	REPEAT NBADSP,<-1>	; SPACE FOR BAD SPOTS. -1 IS FREE
	-1			; AND ONE FOR END TEST

DEVL:	-1
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0
	0

TIME:	0
TIMEC:	0
SENSCT:	0
DSTAT:	BLOCK 4			; COPY OF 4 WORDS FROM LOW CORE
				; ON CHANNEL COMPLETION

TYIM:	PUSHJ P,STYO
TYI:	CONSO TTY,40
	JRST .-1
	DATAI TTY,B
	ANDI B,177
	JRST TYO

NUMGM:	PUSHJ P,STYO
NUMG:	MOVEI A,0
NUMG1:	PUSHJ P,TYI
	CAIG B,"9"
	CAIGE B,"0"
	POPJ P,
	LSH A,3
	ADDI A,-"0"(B)
	JRST NUMG1

HTYO:	CAIL B,12
	ADDI B,"A"-"9"-1
	JRST OPT3
PAGE
SMPT:	EXP 007,007,007,007,007,007,007,007
	EXP 007,007,007,007,007,007,007,007
	EXP 207,107,047,027,017,003,005,005
	EXP 007,007,007,007,006,377,017,007
SMPT1:	EXP 000,000,000,000,364,364,364,364
	EXP 364,363,070,010,364,310,070,010
	EXP 000,000,000,000,000,000,000,000
	EXP 000,000,000,000,000,000,000,000
SMPT2:	EXP 020,010,004,002,001,000,000,000
	EXP 001,020,010,004,002,000,000,000
	EXP 000,000,000,000,000,037,010,000
	EXP 000,000,000,000,000,000,000,000

RCROM1:
	EXP 000,105,012,101,240,123,111,125
	EXP 015,104,122,112,116,106,103,113
	EXP 124,132,114,127,110,131,120,121
	EXP 117,102,107,200,115,130,126,377

RCROM2:
	EXP 000,063,012,055,240,007,070,067
	EXP 015,044,064,047,054,041,072,050
	EXP 065,042,051,062,043,066,060,061
	EXP 071,077,045,200,056,057,073,377

RCROM3:
	EXP 000,140,210,250,220,100,050,320
	EXP 010,040,170,260,110,340,240,360
	EXP 150,330,120,020,300,060,030,160
	EXP 230,270,310,104,024,124,264,214

RCROM4:
	EXP 200,026,112,000,000,000,130,000
	EXP 266,262,000,000,316,206,126,224
	EXP 374,174,074,034,014,004,204,304
	EXP 344,364,000,252,000,000,000,062
PAGE
;FORMAT A PACK

FMT:	PUSHJ P,EXT	;MOD BY GBH
	MOVE A,DCHAN	;MOD BY GBH
;	MOVEI A,DCHAN
	MOVEM A,CHAN
	PUSHJ P,CSET
	MOVE D,DDEV		; DEVICE TO FORMAT
	MOVEI C,[ASCIZ/**********WARNING**********
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/ROUNTINE WILL DESTROY DATA ON DRIVE %D

/]
	PUSHJ P,STYO
	MOVEI F,0		; FORMAT PATTERN NUMBER. (JUST 0 NOW)
;	MOVEI P,PDL-1
	CONO 635550
FMT0:	LSH D,^D12
	MOVE I,FMTM	;0=HA, R0 ARE GOOD; 1=WRT R0; -1=WRT HA AND R0
	;-2=WRT HA AND R0 WITH CE BIT ON
	;PUT IN DEV FLD
	PUSHJ P,CEBIT	;MOD BY GBH
	MOVEI C,[ASCIZ/PUSH ALT MODE ON TTY WHEN READY
/]
	PUSHJ P,STYO
	NALT
	MOVEI C,[ASCIZ/FORMATTER STARTED
/]
	PUSHJ P,STYO
	MOVE J,RECSIZ(F)
	IMULI J,^D9
	ADDI J,1
	ASH J,-1	;J HAS SIZE IN BYTES
	LSH J,4
	HRLZ E,STRTRK(F)	;MOD BY GBH
	LSH E,2			;MOD BY GBH
;	MOVEI E,0
FMT1:	TRZ E,7760		; CLEAR HEAD FIELD
	MOVE H,ADRLO	;PLACE TO PUT C C H H # KL DL DL
	MOVSI C,200000	;A TIC CMD
	ADDI C,<NRPT+1>*^D20*2(H)	;LEAVE ROOM FOR DATA
	MOVEM C,FWP0
	JUMPE I,FMT1A	;IF NO SET FM NEEDED
	MOVE G,[BYTE (8)70,37]	;SET FM
	PUSHJ P,FMTS0
	MOVE G,[-1,,[BYTE (8)300]]	;ALLOW ALL
	PUSHJ P,FMTS1
FMT1A:	TRNE E,7760	;ALWAYS SEEK IF HEAD 0
	JUMPGE I,FMT1B	;OR IF WRITING HA'S
	MOVE G,[BYTE (8)70,7]	;SEEK
	PUSHJ P,FMTS0
	MOVE G,[400000-2,,[0]]	;2 BYTES OF BB
	PUSHJ P,FMTS1
	MOVSI G,-4
	PUSHJ P,FMTS1A
FMT1B:	JUMPL I,FMT1C
	JUMPG I,FMT1D
	MOVE G,[BYTE (8)70,61]	;SRCH ID EQ
	TRNE E,7760
	TLO G,1000	;SET M-T IF NOT 1ST REC
	PUSHJ P,FMTS0
	MOVSI G,-5
FMT1BR:	PUSHJ P,FMTS1A
	PUSHJ P,FMTS2
;FALLS THRU
;FALLS IN
FMT1E:	TLZ J,776000	;SET RN TO 0
FMT2:	MOVEM E,(H)
	MOVEM J,1(H)
	MOVEI A,200
	TLNN J,776000
	HRRM A,1(H)	;R0 HAS DL OF 8
	ADDI H,2
	CAML J,NRECS2(F)
	JRST FMT2A	;NO MORE RECS ON TRK
	ADD J,[2000,,0]
	MOVE G,[BYTE (8)72,35]	;SLI, WRT CKD
	PUSHJ P,FMTS0
	MOVSI G,-^D8
	PUSHJ P,FMTS1A
	JRST FMT2
FMT2A:	ADDI E,20
	LDB A,[41000,,E]
	CAMGE A,NHEDS(F)
	JRST FMT1A	;MORE TRKS ON CYL
	MOVE G,[BYTE (8)240,3]	;NOP
	PUSHJ P,FMTS0
	SETZM (C)
	MOVE A,FWP0
	PUSHJ P,SCHN	;FORMATS ONE CYL
	ADD E,[4,,0]		; COUNT A CYLINDER
	ALT
	LDB A,[242000,,E]	; WHAT CYLINDER ARE WE UP TO?
	JUMPN I,CEFMT		;MOD BY GBH
	CAMGE A,NTRKS(F)
	JRST FMT1		; NOT DONE YET.
	SOSLE ,FMTX		;MOD BY GBH
	JRST FMT0+3		;MOD BY GBH
FMTEND:	MOVEI C,[ASCIZ /
********************
FORMATTER DONE.
********************
/]
	PUSHJ P,STYO
	SETZM FMTX		;MOD BY GBH
	JRST DDT

FMT1D:	MOVE G,[BYTE (8)70,71]	;SRCH HA EQ
	TRNE E,7760
	TLO G,1000	;SET M-T IF NOT 1ST TRK
	PUSHJ P,FMTS0
	MOVSI G,-4
	PUSHJ P,FMTS1A
	PUSHJ P,FMTS2
	JRST FMT1CR

FMT1C:	MOVE G,[BYTE (8)70,31]	;WRT HA
	PUSHJ P,FMTS0
	MOVE G,FLGBYT	;MOD BY GBH
	CAME I,FMTCE	;MOD BY GBH
	MOVE G,[400000-1,,[0]]
	PUSHJ P,FMTS1	;PUT OUT A ZERO FLAG BYTE
	MOVSI G,-4
	PUSHJ P,FMTS1A
FMT1CR:	MOVE G,[BYTE (8)72,25]	;WRITE R0
	PUSHJ P,FMTS0
	MOVSI G,-^D8
	PUSHJ P,FMTS1A
	JRST FMT1E

CEFMT:	CAMGE A,ALLTRKS(F)	;MOD BY GBH
	JRST FMT1
	SOSLE ,FMTX
	JRST FMT0+3
	CAME I,FMTCE
	JRST FMTEND
	MOVEI C,[ASCIZ/RE-IMPL CONTROLLER; PUSH ALT MDE OR P ON TTY
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/WHEN READY
/]
	PUSHJ P,STYO
	NALT
	JRST FMTEND


FMTS0:	ADD G,D
FMTS3:	MOVEM G,(C)
	ADDI C,1
	POPJ P,

FMTS2:	MOVE G,C
	SUBI G,2
	JRST FMTS3

FMTS1A:	HRR G,H
FMTS1:	HLLZ A,G
	LSH A,CNTSFT
	SKIPL G
	TLZ A,400000
	HLL G,A
	JRST FMTS3

CEBIT:	CAME I,FMTCE	;MOD BY GBH
	POPJ P,
	MOVEI C,[ASCIZ/*****
MODIFY MICROCODE AT DC830 ADDRESS 2510
/]
	PUSHJ P,STYO
	MOVEI C,[ASCIZ/
TO 0776D1CE
/]
	PUSHJ P,STYO
	POPJ P,

MXRECT=40			; MAX RECORDS PER TRACK
DEVFLD=141000
; FOLLOWING 4 ARE TABLES, OF LENGTH 1 NOW, DEFINING A FORMAT OF
; A PACK. INDEXED BY F IN FMT ABOVE
RECSIZ:	RECL
NRECS2:	NRPT*2000,,0
NHEDS:	^D19
NTRKS:	^D807	;MOD BY GBH
ALLTRK:	^D815	;MOD BY GBH
STRTRK:	0	;MOD BY GBH
FMTCE:	777777,,777776	;MOD BY GBH
FLGBYT:	400000-1,,CEFLG	;MOD BY GBH
CEFLG:	010000,,0	;MOD BY GBH

FWP0:	0	;PTR TO CHNL PRGM
FMTM:	0	;FORMATTING MODE
FMTX:	0	;MOD BY GBH
DDEV:	300	;MOD BY GBH

SCHN:	HRLI A,200000
	MOVEM A,@CBASE
	CONO SA0,@STBSYC
	CONSO SA0,170
	JRST .-1
	CONO SA0,@CLSTFC
	MOVE A,@CBASE+1
	TLNN A,1314
	POPJ P,
	MOVEM A,STBUF
	MOVE A,@CBASE+2
	MOVEM A,STBUF2
	MOVE A,@CBASE
	LDB A,[DEVFLD,,(A)]
	DPB A,[DEVFLD,,SCHS]
	MOVE A,[MOVE SCHS]
	MOVEM A,@CBASE
	CONO SA0,@STBSYC
	CONSO SA0,170
	JRST .-1
	CONO SA0,@CLSTFC
SCHNX:	MOVEI C,[ASCIZ / !!! ERROR !!!
/]
	PUSHJ P,STYO
	JRST DDT
SCHS:	BYTE (8)50,4
	IOW (30,SENSED)
	0
PSB:	PUSHJ P,CR
	MOVE	F,SENSED
	AOJE	F,[HRROI C,[ASCIZ \MISSING
\]
	 	JRST	STYO]
	PUSHJ	P,CR
	MOVE F,[440400,,SENSED]
	MOVEI D,6
PS0:	MOVEI E,^D8
PS1:	ILDB B,F
	CAILE B,^D9
	ADDI B,"A"-"0"-^D10
	ADDI B,"0"
	PUSHJ P,TYO
	MOVEI B,40
	TLNE F,40000
	PUSHJ P,TYO
	SOJG E,PS1
	PUSHJ P,TYO
	IBP F
	TRNE D,1
	PUSHJ P,CR
	SOJG D,PS0
	POPJ P,

PS:	PUSHJ P,PSB
	JRST DDT
PAGE
PDEV==16	;PRINTER DEVICE CODE
PBUFLB==^D132	;NUMBER CHARS PER LINE
PBUFL==PBUFLB/4	;WORDS OF PRINT BUFFER
UCBLB==^D240	;NUMBER OF LOGICAL CHARS ON CHAIN
UCBL==UCBLB/4	;WORDS FOR UCB BUFFER

DEFINE UCS2 (A,B,C,D)
<BYTE (8)"A","B","C","D">

DEFINE UCS1 (AA,BB,CC)
<UCS2 1,2,3,4
UCS2 5,6,7,8
UCS2 9,0,X,Y
UCS2 /,S,T,U
BYTE (8)"V","W",AA,BB
BYTE (8)CC,"*",",","="
UCS2 J,K,L,M
UCS2 N,O,P,Q
UCS2 R,-,Z,(
UCS2 A,B,C,D
UCS2 E,F,G,H
UCS2 I,+,.,)
>

	SALL
UCB:	UCS1 137,42,"$"
	UCS1 74,73,"#"
	UCS1 "?",76,40
	UCS1 "]","'","&"
	UCS1 "[",":","%"
UCBE:	UCS1 137,42,"$"


PAT0:	UCS2 A,B,C,D
	UCS2 E,F,G,H
	UCS2 I,J,K,L
	UCS2 M,N,O,P
	UCS2 Q,R,S,T
	UCS2 U,V,W,X
	UCS2 Y,Z,0,1
	UCS2 2,3,4,5
	UCS2 6,7,8,9
	BYTE (8)"/","*",",","="
	BYTE (8)"-","(","+","."
	BYTE (8)")",137,42,"$"
	BYTE (8)74,73,"#","?"
	BYTE (8)76,40,"]","'"
	BYTE (8)"&","[",":","%"

	XALL

PBUF:	BLOCK PBUFL
PBUFE:

RIP0:	AOS C,PPOS
	MOVE B,[441000,,PBUF]
	DATAI 0,A
	ANDI A,177
	CAIL A,3
	JRST RIP2
	SOJG A,RIP3
	MOVEI D,^D45
	SKIPL A
	MOVEI D,^D60
RIP1:	AOS E,C
	IDIV E,D
	ROT F,-2
	MOVE E,PAT0(F)
	LSH F,-^D31
	LSH E,-^D28(F)
	IDPB E,B
	CAME B,[041000,,PBUFE-1]
	JRST RIP1
	POPJ P,

RIP2:	IDPB A,B
	CAME B,[041000,,PBUFE-1]
	JRST RIP2
	POPJ P,

RIP3:	IMULI C,43
RIP3A:	MOVEI G,3
RIP4:	AOS E,C
	IDIVI E,^D288
	ROT F,-2
	MOVE E,UCB(F)
	LSH F,-^D31
	LSH E,-^D28(F)
	IDPB E,B
	SOJG G,RIP4
	CAME B,[041000,,PBUFE-1]
	SOJA C,RIP3A
	POPJ P,

PTST:	MOVEI P,PDL-1
	CONO 635550
	MOVEI A,PCHAN
	MOVEM A,CHAN
	PUSHJ P,CSET
	MOVEI A,PRTI
	PUSHJ P,PTSTG
PTST1:	PUSHJ P,RIP0
	MOVEI A,PRGO
	PUSHJ P,PTSTG
	JRST PTST1

PTSTG:	HRLI A,200000
	MOVEM A,@CBASE
	CONO SA0,@STBSYC
	CONSO SA0,170
	JRST .-1
	MOVE A,@CBASE+1
	TLNE A,163310
	JRST DDT
	CONO SA0,@CLSTFC
	POPJ P,

PRTI:	BYTE (8)260,353,PDEV
	BYTE (8)260,163,PDEV
	BYTE (8)70,373,PDEV
	IOW (360,UCB)
	BYTE (8)240,3,PDEV
PRTIE:	0
PRGO:	BYTE (8)70,11,PDEV
	IOW (PBUFLB,PBUF)
	BYTE (8)240,3,PDEV
PRGOE:	0
PPOS:	0
PAGE

QTT:	PUSHJ P,EXT	;MOD BY GBH
	SETOM TAPF		;FOR ERROR ROUTINE
	SETZM RECDNO		;CLEAR COUNTERS
	SETZM REWNO		; OF RECORDS, REWINDS, WRITE ERRS
	SETZM WERRNO		; ..
QTT0:	CONO SA0,RSTSA!CLMERR	;RESET, START CLOCK
	MOVEI 0,7777	;SET UP DELAY 
	SOJGE 0,.		;WAIT HERE
;	MOVEI P,PDL-1	;STACK
	MOVE A,TCHAN	;MOD BY GBH
;	MOVEI A,TCHAN
	MOVEM A,CHAN
	PUSHJ P,CSET
	SETZB I,K	;START AT REC 0
	MOVE A,TDEV	;SET DEVICE NUMBER OF TAPE ON IBM BUS
	DPB A,[DEVFLD,,TREW]
	DPB A,[DEVFLD,,TERG]
	DPB A,[DEVFLD,,TCP0]	;SET CHANNEL PGM FOR PROPER DEV
	DPB A,[DEVFLD,,TCP0W]
	DPB A,[DEVFLD,,TCP1]
	DPB A,[DEVFLD,,TCP2]
	MOVN A,KTRECL		;MINUS LENGTH OF RECORD
	ADDI A,7
IFLE CV,<
	ASH A,2>		;SIXTEEN BIT WD CT
IFG CV,<
	ASH A,6>
	HRLM A,TCP0B		;SET CHANNEL PROGRAM LENGTHS
	MOVN A,KTRECL
	ADDI A,13
IFLE CV,<
	ASH A,2>
IFG CV,<
	ASH A,6>
	HRLM A,TCP2B		; ..
	SETZM F7TK	;ASSUME NINE TRACK DRIVE
	PUSHJ P,TSENSE	;GET THE DRIVE SENSE DATA
	CONO SA0,@CLSTFC	;CLEAR THE INTERRUPT FLAG
	MOVE A,SENSED+0	;7 TRACK BIT
	TLNE A,100	; ..
	SETOM F7TK	;IT IS A 7 TRACK DRIVE
	MOVE A,MODE9	;MOD BY GBH
;	MOVEI A,303	;MODE SET FOR 1600 PE 9 TK
	SKIPGE F7TK	;UNLESS 7 TRACK,
	MOVE A,MODE7	;MOD BY GBH
;	MOVEI A,223	;MAKE IT 800 BPI, ODD, NO XLATORS
	DPB A,[241000,,TCP0]
	PUSHJ P,TGB	;FILL FIRST BUFFER (REC 0)
	PUSHJ P,TGO	;START FIRST OPER
QTT1:	MOVEI I,1(K)	;GENERATE DATA FOR REC N+1
	ALT
	PUSHJ P,TGB
	PUSHJ P,TWT	;WAIT FOR COMPLETION ON REC N
	ADDI K,1	;NOW ON NEXT REC
	PUSHJ P,TGO	;START IT
	MOVEI I,-1(K)	;SET TO CHECK DATA ON N-1
	PUSHJ P,TCB	;CHECK IT
	JRST QTT1	;KEEP GOING

TGB:	TDZA J,J	;J=0 FOR GENERATE
TCB:	MOVEI J,1	;J=1 FOR CHECK
	MOVN G,KTRECL		;LENGTH OF RECORD
	MOVSI G,(G)		;AOBJN COUNTER
	HRR G,ADRLO	;SETUP FOR BUF 0
	TRNN I,1	;SKIP IF ODD REC
	JRST .+3	;EVEN. DON'T OFFSET
	ADD G,KTRECL	;STEP UP TWO BUFFERS
	ADD G,KTRECL	; ..
	MOVE D,I	;USE REC # FOR SEED WORD
	MOVE C,KTRECL	;POINTER TO SECOND BUFFER OF PAIR (READ)
	HRLI C,G	;INDEX BY AOBJN COUNT
	IMUL D,[175*175*175*175]
TGB1:	XCT TGB3(J)	;STORE OR CHECK DATA (MOVEM OR CAMN)
	JRST TGB2	;OK
	MOVE E,@C	;ERROR, GET BAD WORD
	HLRO F,G	;- REMAINING COUNT
	ADD F,KTRECL	;GET INDEX TO READ WORD
	MOVEI C,[ASCIZ /DATA ERR, REC=%I, WD=%F, WROTE=%D, GOT=%E
/]
	PUSHJ P,STYO
	JRST QTT0	;START TEST OVER
;I.E., TOTAL ABORT ON ANY DATA FAILURE

TGB2:	TLNN D,200000
	TLC D,400000	;FEEDBACK SHIFT REGISTER TYPE RANDOMIZER
	ROT D,1
	AOBJN G,TGB1	;LOOP FOR EACH WORD OF REC
	POPJ P,

TGB3:	MOVEM D,(G)		;XCT'ED FOR DATA GENERATE
	CAMN D,@C		;XCT'ED FOR DATA CHECK

TGO:	AOS RECDNO		;STARTING ANOTHER RECORD
	MOVE A,[MOVE TCP0]	;I.E., TCH TCP0. SKIP REWIND
	MOVEM A,@CBASE	;SET UP PTR TO CHAN PGM, BASE FOR THIS CHAN
	LDB A,[200,,K]		;DECIDE PACKING MODE FROM RECD NO.
	MOVE A,TGOT(A)		;TOP BYTE FOR READ COMMAND
	DPB A,[341000,,TCP2]	;PUT IN READ COMMAND
	ADDI A,22		;ADD CHAIN AND SLI BITS
	DPB A,[341000,,TCP0W]	;PUT IN WRITE COMMAND, MATCHING FORMAT.
	MOVE A,ADRLO
	TRNN K,1		;WHICH BUFFER? (ODD/EVEN REC)
	JRST .+3		;EVEN. FIRST PAIR
	ADD A,KTRECL		;ODD. SECOND PAIR. SKIP 2
	ADD A,KTRECL		; ..
	HRRM A,TCP0A	;PUT IN CHAN PGM
	ADDI A,7
	HRRM A,TCP0B	; SECOND OF TWO WRITE  POINTERS
	SUBI A,7
	ADD A,KTRECL		;AND SECOND OF PAIR IS FOR READING
	HRRM A,TCP2A	;SETUP READ ADDR
	ADDI A,13
	HRRM A,TCP2B	; SECOND READ POINTER
	CONO SA0,@STBSYC	; SPIN!
	POPJ P,

TGOT:	EXP 40,140,150,140	;WORD, NAT, TCOMPAT, NAT.

; NON-PI WAIT LOOP

TWT:	CONSZ SA0,PARERR+NXM
	JRST TWME	;MEM ERR, BAD NEWS
	CONSO SA0,170
	JRST TWT	;WAIT FOR SOME ACTION
	LDB A,[DEVFLD,,@CBASE+1]	; GET DEVICE CAUSING INTERRUPT
	CAME A,TDEV		;IS IT THE TAPE?
	JRST TWE	;UNEXPECTED DEVICE CAUSED TROUBLE
	MOVE A,@CBASE+1	;GET STATUS
TWT1:	TLNE A,163010
	JRST TWE	;BAD BITS ON
	TLNE A,4
	JRST TWUX	;UNIT EXCEPTION, PROBABLY EOT
	TLNE A,100
	JRST TWBSY	;CONTROL BUSY OR STATUS PENDING
	TLNE A,200
	JRST TWRST	;CU END, RESTART
	TLNN A,20
	JRST TWE	;NO DEVICE END IN STATUS
	TLNN A,40
	JRST TWRST	;DEVICE END ALONE
	HRRZ A,@CBASE+2	;LOOKS OK. CHECK THAT WHOLE PGM WAS DONE
	CAIE A,TCPE+1
	JRST TWE	;WASN'T
;HERE IF ALL IS WELL
TWTX:	CONO SA0,@CLSTFC	; CLEAR STATUS FLAG
	POPJ P,

MODE9:	XWD 0,303	;MOD BY GBH  SET 9TRK FOR 1600 BPI
MODE7:	XWD 0,223	; MOD BY GBH  SET 7TRK FOR 800 BPI  NO XLATOR
TCHAN:	0,,0
TDEV:	TUNIT		;THIS GOES INTO CHAN CMDS BELOW
KTRECL:	TRECL		;LENGTH OF RECORDS IN TAPE TEST
TREW:	BYTE (8)260	;NO MEM XFER, GO, CHAIN, REWIND OR BACKSPACE
TERG:	BYTE (8)260	;NO MEM XFER, GO, CHAIN, NOP OR ERG
TCP0:	BYTE (8)260	;NO MEM XFER, GO, CHAIN, SET MODE
TCP0W:	BYTE (8)162,1	;GO, CHAIN, SLI, WRITE
TCP0A:	IOWC (7,0)	;BFR ADDR PLUGGED IN RH THIS WD
TCP0B:	IOW (TRECL-7,0)
TCP1:	BYTE (8)260,47	;NO MEM XFER, GO, CHAIN, BACKSPACE BLOCK
TCP2:	BYTE (8)140,2	;GO, READ
TCP2A:	IOWC (13,0)
TCP2B:	IOW (TRECL-13,0)
TCPE:	0		;HALT

TWRST:	CONO SA0,@CLSTFC
	CONO SA0,@STBSYC	; TRY OPERATION AGAIN
	JRST TWT	;AND WAIT FOR RESULTS

TWBSY:	TLNE A,260
	JRST TWRST	;JUST PENDING STATUS, TRY AGAIN
	CONO SA0,@CLSTFC
	JRST TWT	;REALLY BUSY, WAIT FOR COMPLETION

TWUX:	HRRZ A,@CBASE+2
	CAIE A,TCP1		; PC POINTING AFTER THE WRITE?
	JRST TWE	;BOMBED IN WRONG PLACE, ERROR
	AOS D,REWNO	;COUNT REWINDS
	MOVE E,RECDNO	;TYPE SUMMARY FOR PASS
	MOVEI C,[ASCIZ /END OF TAPE, PASS $D, $E RECORDS.
/]
	PUSHJ P,STYO
	MOVEI A,7	;REWIND CMD
	MOVEI B,3	;NOP CMD
TWUX1:	DPB A,[241000,,TREW]	;BACK HERE ON WRITE ERR TOO
	DPB B,[241000,,TERG]
	MOVEI A,TREW
	HRRM A,@CBASE	;SET TO REWIND OR ERASE TAPE, IF
			; WRITE ERROR,  AND TRY AGAIN
	JRST TWRST

TWME:	PUSHJ P,TSPE1	;TYPE MESSAGE
	JRST QTT0

TWE:	MOVE A,@CBASE+1
	TLNN A,10
	JRST TWEX	;OTHER THAN UNIT CHECK, GIVE UP
	HRRZ A,@CBASE+2	;WHAT WAS CHANNEL PC AT FAILURE?
	CAIE A,TCP1	;WRITE ERROR?
	JRST TWEX	;FAILURE NOT ON WRITE CMD, GIVE UP
	PUSH P,@CBASE+1	;YES. TRY TO RECOVER
	PUSH P,@CBASE+2
	PUSHJ P,TSENSE	;GET SENSE BYTES FROM TAPE UNIT
	LDB A,[242000,,@CBASE+1]
	CAIE A,14
	JRST TWEX1	;NOT CHEND+DEVEND, GIVE UP
	LDB A,[341000,,SENSED]	;FIRST BYTE OF SENSE DATA
	CAIE A,10
	JRST TWEX1	;NOT JUST A DATA CHK
	POP P,@CBASE+2	;DATA ERROR ON WRITE. TRY TO
			; ERASE GAP, RE-WRITE.
	POP P,@CBASE+1	;MOSTLY JUST TO RESTORE PDL
	MOVE E,RECDNO	;REPORT THE WRITE ERROR.
	AOS D,WERRNO
	MOVEI C,[ASCIZ /WRITE ERROR # $D, RECORD $E.
/]
	PUSHJ P,STYO
	MOVEI A,47	;BACKSPACE
	MOVEI B,27	;ERASE GAP
	JRST TWUX1	;GO TRY TO RECOVER

TSENSE:	MOVEI A,PSENSE	;GET SENSE BITS TO EXAMINE ERROR
			; (NEW CHANNEL PROGRAM)
	HRRM A,@CBASE	;SETUP TO DO SENSE
	MOVE A,TDEV
	DPB A,[DEVFLD,,PSENSE]	;ON RIGHT DEV
	CONO SA0,@STBSYC
	CONO SA0,@CLSTFC	;DO IT
	CONSO SA0,170
	JRST .-1	;WAIT FOR SENSE DATA
	POPJ P,0

TWEX1:	POP P,@CBASE+2	;RESET STATUS FOR MESSAGE
	POP P,@CBASE+1
TWEX:	SETOM TAPF	;TELL ERR ROUTINE ITS TAPE
	PUSHJ P,TSPE1	;TYPE MESSAGE
	JRST QTT0	;RESTART WHOLE TAPE ROUTINE AT RECORD 0

TAPF:	0
TAPSNS:	[ASCIZ /NOT CAP/]
	[ASCIZ /FILE PROT/]
	[ASCIZ /WRT STAT/]
	[ASCIZ /LD POINT/]
	[ASCIZ /7 TRK/]
	[ASCIZ /TU ST B/]
	[ASCIZ /TU ST A/]
	[ASCIZ /NOISE/]
	[ASCIZ /DATA CONV CHK/]
	[ASCIZ /WC ZERO/]
	[ASCIZ /OVERRUN/]
	[ASCIZ /DATA CHK/]
	[ASCIZ /EQUIP CHK/]
	[ASCIZ /BUS OUT CHK/]
	[ASCIZ /INTERVENTION REQ/]
	[ASCIZ /COMMAND REJECT/]

RECDNO:	BLOCK 1
REWNO:	BLOCK 1
WERRNO:	BLOCK 1
F7TK:	BLOCK 1

QQ=PUSHJ P,MCINTR

MCINTR:	MOVEI B,NITX+1
	CONO SA0,RDCONI
	DATAI SA0,A	;IS CLOCK RUNNING
	TLNE A,2
	CONO SA0,SASTEP	;STOP IT IF SO
	MOVEI G,0
	PUSHJ P,STEPCX	;STEP TO APPROPRIATE CHANNEL
	MOVE A,IAR	;SEE WHERE WE WERE
MCIL:	PUSHJ P,CXXCT	;1ST TIME SETUP IAR, OTHER TIMES XCT INST
	ANDI A,1777	;GET THE RESULTING IAR
	CAMN A,BPLOC	;BREAKPOINT HERE
	JRST DDT	;GO TO DDT
	SOJLE B,MCIL1	;GO BACK TO MAIN PROGRAM
	MOVE A,MCBEG0(A)	;GET MICRO INST. TO BE EXECUTED
	JRST MCIL

MCIL1:	MOVEM A,IAR	;REMEMBER WHERE WE WERE
	DATAO SA0,[0]	;IN CASE CLOCK GETS STARTED
	POPJ P,

; NOTE: FOLLOWING ROUTINE ASSUMES DISK IS ON CHAN 0

MPROG:	MOVE A,[MOVE PSEEK]
	MOVEM A,BASE
MP:	CLEARM IAR
	CONO SA0,RSTSA
	MOVEI P,PDL-1
	CONO SA0,SACGO
	CONO SA0,STBSY
MP1:	QQ
	CONSZ SA0,BSY0
	JRST MP1
	CONO SA0,SACGO
	CONO SA0,STBSY
	MOVSI D,-LSTBUF
MP2B:	MOVEI E,NTICKS
MP2:	QQ
	CONSO SA0,STF0
	JRST MP2A
	MOVE A,BASE+1
	MOVEM A,STBUF(D)
	MOVE A,BASE+2
	MOVEM A,STBUF2(D)
	CONO SA0,SACGO
	CONO SA0,CLSTF
	AOBJN D,MP2B
	JRST DDT	;TOO MUCH STATUS!!

MP2A:	SOJLE E,DDT	;IT'S BEEN A WHILE SINCE STATUS REC'D
	JRST MP2	;WAIT A LITTLE LONGER


NITX==1
NTICKS==^D5000
LSTBUF==20
PSEEK:	120034000000+<0_14>	;SEEK, BYTE MODE
	IOW 6,SEEKD
	0
SEEKD:	0
	0
PSENSE:	120020000000+<0_14>	;SENSE
	IOW 30,SENSED		;24 BYTES OF SENSE DATA
	0
SENSED:	BLOCK 6
STBUF:	BLOCK LSTBUF
STBUF2:	BLOCK LSTBUF
IAR:	0
BPLOC:	-1

	EXTERN .JBREL
IOLIST:	EXP 0,0

SUPER:	OPEN 1,[EXP 17,SIXBIT /SUPER/,0]
	HALT .
	MTAPE 1,1
	MTAPE 1,0
	MOVN A,.JBREL
	MOVSI A,GOGO(A)
	HRRI A,GOGO-1
	MOVEM A,IOLIST
	OUT 1,IOLIST
	SKIPA
	HALT .
	RELEAS 1,
	EXIT
SUPER0:	0
SUPER1:	0


PPP=PUSHJ P,PWORLD
PWORLD:	MOVEI	C,[ASCIZ/
/]
	PUSHJ	P,STYO
	
	PUSHJ P,STEPX1
	MOVSI D,-5
PW0:	CONO SA0,@PW0T1(D)
	MOVE C,PW0T2(D)
	PUSHJ P,STYO
	DATAI SA0,A
	TRNN D,-1
	ANDI A,1777
	PUSHJ P,OPT
	AOBJN D,PW0
	CONO SA0,RDCCI
	MOVSI D,-4
PW1:	MOVE C,BTIMT(D)
	PUSHJ P,STYO1
	DATAO SA0,BTIMD(D)
	DATAI SA0,A
	LDB A,[121000,,A]
	PUSHJ P,OPT
	AOBJN D,PW1
	MOVSI D,-4
	MOVEI E,040000
PW2:	MOVE C,PCWT(D)
	PUSHJ P,STYO1
	MOVE C,[301000,,A]
	ADDI E,400
PW2A:	DATAO SA0,E
	DATAI SA0,B
	LSH B,-12
	IDPB B,C
	ADDI E,400
	TRNE E,1400
	JRST PW2A
	LSH A,-4
	PUSHJ P,OPT
	AOBJN D,PW2
	MOVSI D,-4
	MOVEI E,140002
PW3:	MOVE C,REGT(D)
	PUSHJ P,STYO1
	MOVE C,[441000,,A]
PW3A:	DATAO SA0,E
	DATAI SA0,F
	LSH F,-12
	IDPB F,C
	TRNE E,2000
	JRST PW3B
	ADDI E,400
	JRST PW3A

PW3B:	LSH A,-4
	LSHC A,4
	PUSHJ P,OPT
	ADDI E,6000
	AOBJN D,PW3
	MOVEI C,[ASCIZ \
TDS0=\]
	PUSHJ P,STYO
	DATAI SA0,A
	LDB A,[100400,,A]
	PUSHJ P,OPT
CR:	MOVEI C,[ASCIZ \
\]
;	PUSHJ	P,STYO
;	JRST	DDT
STYO:	DATAI DSW,0	;MOD BY GBH
	TLNE 0,040000	;MOD BY GBH
	POPJ P,		;MOD BY GBH
	HRLI C,440700
STYO1:	ILDB B,C
	CAIN B,"%"
	JRST STYO2
	CAIN B,"$"
	JRST STYO3		;DECIMAL TYPEOUT
	CAIN B,"\"
	JRST STYO4		;HEX TYPEOUT
	CAIN B,15	;IS THIS A CR
	JRST TERM	;YES FINISH PRINTING
TTYO:	JUMPE B,CPOPJ	;ALL FINISHED
	PUSHJ P,TYO	;NO STUFF ANOTHER CHARACTER
	JRST STYO1

STYO2:	ILDB B,C
	MOVE A,-100(B)
	PUSHJ P,OPT
	JRST STYO1
STYO3:	ILDB B,C
	MOVE A,-100(B)
	PUSHJ P,DPT
	JRST STYO1
STYO4:	ILDB B,C
	MOVE A,-100(B)
	PUSHJ P,HPT
	JRST STYO1

CR4:	PUSHJ P,CR2
CR2:	PUSHJ P,CR
	JRST CR

OPT:	TLNN A,-1
	JRST OPT1
	PUSH P,A
	HLRZS A
	PUSHJ P,OPT2
	POP P,A
	MOVEI B,54
	PUSHJ P,TYO
	PUSHJ P,TYO
OPT1:	HRRZS A
OPT2:	IDIVI A,10
	JUMPE A,OPT3
	HRLM B,(P)
	PUSHJ P,OPT2
	HLRZ B,(P)
OPT3:	ADDI B,60
	CAILE B,"9"
	ADDI B,"A"-"9"-1
TYO:	CONSZ TTY,20
	JRST .-1
	ALT
	DATAI DSW,0	;MOD BY GBH
	TLNE 0,020000	;MOD BY GBH
	JRST LPTOUT	;MOD BY GBH
	CAIN B,11
	JRST TABRTN
	DATAI DSW,0
	TLNE 0,040000
	POPJ P,
FTYO:	DATAO TTY,B
	AOS ,TABCNT
	POPJ P,

TABRTN:	PUSH P,BF
	MOVE BF,TABCNT
	TRNN BF,7
	JRST TABE
TABL:	MOVEI B,40
	PUSHJ P,TYO
	JRST TABRTN+1
TABE:	CAIN B,11
	JRST TABL
	POP P,BF
	POPJ P,


JPOPJ:	PUSHJ P,SETPTR
	SETZM TABCNT
	DATAO LPT,PTRBUF	;JAM NULLS TO SEE IF LPT BUSY
	CONSO LPT,100
	JRST .-1
	CONO LPT,2000
	CONSO LPT,100
	JRST .-1
CPOPJ:	POPJ P,
;LINE PRINTER MODS

LPTOUT:	PUSH P,L
	MOVE L,LPTPTR	;MOD BY GBH
	TLNN L,760000
	PUSHJ P,SETPTR
	IDPB B,LPTPTR
	MOVE L,LPTPTR
	TLNE L,760000
	JRST .+4
	CONSZ LPT,200
	JRST .-1
	DATAO LPT,PTRBUF
	CONSO LPT,100
	JRST .-1
	POP P,L
	POPJ P,

SETPTR:	PUSH P,L
	MOVE L,[POINT 7,PTRBUF,]
	MOVEM L,LPTPTR
	SETZM PTRBUF
	POP P,L
	POPJ P,


TERM:	SETZM TABCNT
	DATAI DSW,0
	TLNN 0,020000
	JRST TTYO
	MOVEI B,12
	PUSHJ P,LPTOUT
	PUSH P,L
	MOVE L,LPTPTR
	TLNE L,760000
	PUSHJ P,FOUT
	CONSZ LPT,200
	JRST .-1
	ILDB B,C
	CAIN B,12
	ILDB B,C
	POP P,L
	JUMPE B,JPOPJ
	JRST STYO1+1



FOUT:	CONSZ LPT,200
	JRST .-1
	DATAO LPT,PTRBUF
	CONSO LPT,200
	JRST .-1
	PUSHJ P,SETPTR
	POPJ P,

PTCHIT:	BLOCK 40
TABCNT:	0
FPRT:	0
FPRTE:	0
LPTPTR:	0
PTRBUF:	0

;END OF MODS

DPT:	MOVEI B,"-"
	SKIPGE A
	PUSHJ P,TYO
	MOVMS A
DPT1:	IDIVI A,12
	JUMPE A,OPT3
	HRLM B,(P)
	PUSHJ P,DPT1
	HLRZ B,(P)
	JRST OPT3

HPT:	MOVEI B,0
	ROTC A,-4
	ROT B,4
	JUMPE A,OPT3
	HRLM B,(P)
	PUSHJ P,HPT
	HLRZ B,(P)
	JRST OPT3

BTIMT:	440700,,[ASCIZ \
BUS= \]
	440700,,[ASCIZ \
TAG= \]
	440700,,[ASCIZ \
INS= \]
	440700,,[ASCIZ \
MISC=\]

BTIMD:	144000
	145000
	145400
	142400

PCWT:	440700,,[ASCIZ \
WC=  \]
	440700,,[ASCIZ \
CA=  \]
	440700,,[ASCIZ \
PC=  \]
	440700,,[ASCIZ \
A3=  \]

REGT:	440700,,[ASCIZ \
R0=  \]
	440700,,[ASCIZ \
R1=  \]
	440700,,[ASCIZ \
R2=  \]
	440700,,[ASCIZ \
R3=  \]

PW0T1:	RDCCI
	RDMA
	RDMBL
	RDMBR
	RDCONI

PW0T2:	[ASCIZ /
IARB=/]
	[ASCIZ /
MA=  /]
	[ASCIZ /
MBLT=/]
	[ASCIZ /
MBRT=/]
	[ASCIZ /
CONI=/]


CSET:	MOVE A,CHAN
	LSH A,2
	ADDI A,BASE
	MOVEM A,CBASE
	ADDI A,1
	MOVEM A,CBASE+1
	ADDI A,1
	MOVEM A,CBASE+2
	MOVE A,CHAN
	DPB A,[030200,,STBSYC]
	DPB A,[030200,,CLSTFC]
	DPB A,[030200,,STPIEC]
	DPB A,[030200,,STSRQC]
	POPJ P,0

CHAN:	0
CBASE:	BLOCK 3
STBSYC:	STBSY+SA0CH
CLSTFC:	CLSTF+SA0CH
STPIEC:	STPIE+SA0CH
STSRQC:	STSRQ+SA0CH

INT1:	0
	MOVEI I,1
	CAME I,PIE	;ARE WE EXPECTING AN INTERRUPT ON THIS CHANNEL?
	JRST ERR31	;NO
	CONO SA0,CLSTF(B)	;REMOVE THE CAUSE FOR THIS INTERRUPT
			;(MAIN PROGRAM HAS SET B UP)
	CLEARM PIE	;NOTE THAT NO FURTHER INTERRUPTS ARE EX-
			;PECTED, ALSO TELL MAIN PROGRAM THAT ONE
			;INTERRUPT DID OCCUR
	JRST 12,@INT1

INT2:	0
	MOVEI I,2
	CAME I,PIE
	JRST ERR32
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT2

INT3:	0
	MOVEI I,3
	CAME I,PIE
	JRST ERR33
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT3

INT4:	0
	MOVEI I,4
	CAME I,PIE
	JRST ERR34
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT4

INT5:	0
	MOVEI I,5
	CAME I,PIE
	JRST ERR35
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT5


INT6:	0
	MOVEI I,6
	CAME I,PIE
	JRST ERR36
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT6

INT7:	0
	MOVEI I,7
	CAME I,PIE
	JRST ERR37
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT7

PIE:	0	;CHANNEL ON WHICH INTERRUPT IS EXPECTED
UUOH:	JSR RRR
PAGE
;WRAPAROUND TESTS;  PUT SPECIAL JUMPER CABLE BETWEEN BUS-IN AND BUS-OUT OF CHANNEL N
;  THEN GO TO WRAPN

WRAP0:	MOVEI G,0
	JRST WRP

WRAP1:	MOVEI G,1
	JRST WRP

WRAP2:	MOVEI G,2
	JRST WRP
WRAP3:	MOVEI G,3

WRP:	MOVEI A,^D8	;8 BIT POSITIONS TO CHECK
	PUSHJ P,PTN
	LDB D,[1000,,A]	;RIGHT 8 BITS
WRP2:	MOVEI A,2400	;PULSE 01
	PUSHJ P,CXXCT	;RESET OP STATUS BITS
	MOVE A,D	;DATA BYTE
	PUSHJ P,PARITY	;COMPUTE ODD PARITY
	MOVE A,D
	DPB B,[100100,,A]	;PUT PAR BIT LEFT OF DATA BITS
	LSH A,-1	;AND SHIFT RIGHT 1 BIT
	MOVEM A,WRAPV	;EXPECTED DATA BYTE
	MOVE A,D
	PUSHJ P,CXXCT	;DO GOTO WITH INITIAL BYTE
	MOVEI A,40120
	PUSHJ P,CXXCT	;DO IARB TO BUS OUT
	MOVEI A,144000
	DATAO SA0,A	;ROUTE BUS IN TO C BUS
	CONO SA0,RDCCI
	DATAI SA0,B	;READ IN CBUS, ETC.
	LDB B,[121000,,B]	;EXTRACT C BUS
	CAME B,WRAPV	;THE MOMENT OF TRUTH
	JRST ERR42
WRP4:	LOOP ^D42,WRP2	;IF LOOPING TEST, USE SAME DATA
	PUSHJ P,STEPX	;DO THE BUSIN TO CBUS
	MOVEI A,145400
	DATAO SA0,A	;ROUTE OP STATUS BITS TO CBUS
	CONO SA0,RDCCI
	DATAI SA0,C	;READ IN CBUS, ETC.
	TRNE C,40000	;BUS IN PAR ERR?
	JRST ERR43	;YES
WRP6:	LOOP ^D43,WRP2	;TRY AGAIN IF LOOPING
	JSR PTN0	;DO NEXT DATA VALUE
	JRST DDT	;WHEN ALL DONE
;GENERATE ODD PARITY OF WORD IN A
;  LEAVES 0 IN A, 0 OR 1 IN B
PARITY:	MOVEI B,0
PARIT1:	TRNE A,1
	TRC B,1
	LSH A,-1
	JUMPN A,PARIT1
	POPJ P,
EXTERN DDT

RRR:	0
	MOVEM A,SAVA
	MOVEM B,SAVB
	MOVEM C,SAVC
	MOVEM D,SAVD
	HRRZ D,@RRR
	MOVEI C,[ASCIZ /
ERR # $D /]
	PUSHJ P,STYO
RRR2:	AOS C,RRR
	SKIPN C,(C)
	JRST RRR8
	PUSHJ P,RRRT
	JRST RRR3	;IF NO PRINTOUT
	PUSHJ P,STYO
RRR3:	AOS C,RRR
	SKIPN C,(C)
	JRST RRR8
	PUSHJ P,RRRT
	JRST RRR2	;IF NO PRINTOUT
	MOVE A,(C)
	PUSHJ P,OPT
	JRST RRR2

RRR8:	AOS C,RRR
	MOVE C,(C)
	HRRM C,RRR
	MOVEI C,[ASCIZ /
/]
	TRNN A,3
	PUSHJ P,STYO
RRR9:	MOVE D,SAVD
	MOVE C,SAVC
	MOVE B,SAVB
	DATAI DSW,0
	TLNE 0,4
	JRST RRR95
	MOVE A,SAVA
	JRST @RRR

RRR95:	MOVE A,SAVA
RRRB:	JFCL
	JRST @RRR

RRRT:	DATAI DSW,0	;READ DATA SWITCHES
	MOVEI B,7	;BELL
	TLNE 0,10000
	CONSZ TTY,20
	JRST RRRT3	;IF TTY BUSY
	DATAO TTY,B
RRRT3:	TLNN 0,20
	AOS (P)
	POPJ P,
SAVA:	0
SAVB:	0
SAVC:	0
SAVD:	0

PAGE
ERR8:	CONSZ SA0,200000
	JRST ERR8P	;PAR ERR
	JSR RRR
	^D8
	[ASCIZ /MEM READ UNFINISHED/]
	0
	MRD1A

ERR8P:	CONO SA0,RDMBL+CLMERR
	DATAI SA0,ERR8P0
	CONO SA0,RDMBR
	DATAI SA0,ERR8P1
	JSR RRR
	^D8
	[ASCIZ /MEM READ PAR ERR: MB /]
	ERR8P0
	[ASCIZ /, MBR /]
	ERR8P1
	0
	MRD1A

ERR8P0:	0
ERR8P1:	0
ERR1:	JSR RRR
	1
	[ASCIZ /DATAI /]
	SAVC
	0
	BEG1B

ERR2:	JSR RRR
	2
	[ASCIZ /CHAN 0 NOT SERVED/]
	0
	BEG1C

ERR3:	HRRZ B,A
	JSR RRR
	3
	[ASCIZ /CHAN /]
	SAVB
	[ASCIZ / NOT SERVED/]
	0
	BEG21A

ERR4:	JSR RRR
	4
	[ASCIZ /IAR WROTE /]
	SAVB
	[ASCIZ /, READ /]
	SAVA
	0
	IARL1

ERR5:	JSR RRR
	5
	[ASCIZ /WROTE IAR /]
	SAVB
	[ASCIZ /, READ CBUS /]
	SAVA
	0
	IARL2

ERR40:	JSR RRR
	^D40
	[ASCIZ /WROTE 0 IN R3 TO MBL, READ /]
	SAVA
	0
	SZER1
ERR11:	JSR RRR
	^D11
	[ASCIZ /WROTE /]
	[0,,TEST]
	[ASCIZ / IN R2 TO MA, READ /]
	SAVC
	0
	MAMB1A

ERR6:	JSR RRR
	6
	[ASCIZ /WROTE /]
	MAMBTL
	[ASCIZ / IN R2 TO MBL, READ /]
	SAVB
	0
	MAMB2

MAMBTL:	0
MAMBTR:	0
INCTW:	0
WRAPV:	0
ERR28V:	0
CWTA:	0
CWTB:	0
PLISTE:	BLOCK	40
ERR7:	HLRZ B,A
	JSR RRR
	7
	[ASCIZ /CHAN /]
	SAVB
	[ASCIZ / HAS MEM/]
	0
	MAMB3

ERR12:	JSR RRR
	^D12
	[ASCIZ /WROTE /]
	MAMBTR
	[ASCIZ / IN R2 TO MBR, READ /]
	SAVA
	0
	MAMB4

ERR9:	JSR RRR
	^D9
	[ASCIZ /MEM WRITE UNFINISHED/]
	0
	MWR1A

ERR10:	HRRZ D,A
	JSR RRR
	^D10
	[ASCIZ /WORD /]
	SAVD
	[ASCIZ / WROTE /]
	SAVC
	[ASCIZ /, READ /]
	SAVB
	0
	MCMP1
ERR28:	MOVEI E,3
	SUB E,F
	MOVEM E,ERR28V
	MOVEI E,3
	SUB E,B
	JSR RRR
	^D28
	[ASCIZ /CHAN /]
	G
	[ASCIZ /, WORD /]
	ERR28V
	[ASCIZ /, BYTE /]
	E
	[ASCIZ / WROTE /]
	SAVD
	[ASCIZ /, READ /]
	SAVA
	0
	CWT2A

ERR13:	JSR RRR
	^D13
	[ASCIZ /CHAN /]
	SAVC
	[ASCIZ / CAN'T SET PIE/]
	0
	STCL1A

ERR14:	JSR RRR
	^D13
	[ASCIZ /CHAN /]
	SAVC
	[ASCIZ / CAN'T CLEAR PIE/]
	0
	STCL1B

ERR15:	JSR RRR
	^D13
	[ASCIZ /CHAN /]
	SAVC
	[ASCIZ / CAN'T SET BUSY/]
	0
	STCL1C

ERR16:	JSR RRR
	^D13
	[ASCIZ /CHAN /]
	SAVC
	[ASCIZ / CAN'T CLEAR BUSY/]
	0
	STCL1D
ERR17:	JSR RRR
	^D13
	[ASCIZ /CHAN /]
	SAVC
	[ASCIZ / CAN'T SET STATUS FLAG/]
	0
	STCL1E

ERR18:	JSR RRR
	^D13
	[ASCIZ /CHAN /]
	SAVC
	[ASCIZ / CAN'T CLEAR STATUS FLAG/]
	0
	STCL1F

ERR19:	MOVE C,STCLM(B)
	JSR RRR
	^D19
	[ASCIZ /CAN'T DO /]
	SAVC
	0
	STCLM3

ERR20:	MOVE C,STCLM(B)
	SUBI C,4000
	JSR RRR
	^D19
	[ASCIZ /CAN'T DO /]
	SAVC
	0
	STCLM4

ERR21:	JSR RRR
	^D21
	[ASCIZ /CHAN /]
	G
	[ASCIZ / FIFO NOT EMPTY/]
	0
	FIFO1A

ERR22:	JSR RRR
	^D22
	[ASCIZ /CHAN /]
	G
	[ASCIZ / FIFO EMPTY/]
	0
	FIFO2A

ERR24:	JSR RRR
	^D24
	[ASCIZ /CHAN /]
	G
	[ASCIZ / FIFO NOT FULL/]
	0
	FIFO2B

ERR25:	JSR RRR
	^D25
	[ASCIZ /CHAN /]
	G
	[ASCIZ / FIFO NOT EMPTY/]
	0
	FIFO3A

ERR26:	JSR RRR
	^D26
	[ASCIZ /CHAN /]
	G
	[ASCIZ / FIFO EMPTY/]
	0
	FIFO4A
ERR27:	MOVEI C,NBF
	SUB C,D
	JSR RRR
	^D27
	[ASCIZ /CHAN /]
	G
	[ASCIZ /, WORD /]
	SAVC
	[ASCIZ / WROTE /]
	H
	[ASCIZ /, READ /]
	SAVA
	0
	FIFO4B

ERR23:	JSR RRR
	^D23
	[ASCIZ /CHAN /]
	G
	[ASCIZ / FIFO NOT EMPTY/]
	0
	FIFO4C

ERR29:	JSR RRR
	^D29
	[ASCIZ /INTERRUPT REQ/]
	0
	PITST1

ERR38:	MOVE C,B
	JSR RRR
	^D38
	[ASCIZ /PIA /]
	SAVC
	[ASCIZ / DOESN'T INTERRUPT/]
	0
	PIT3A

ERR31:
ERR32:
ERR33:
ERR34:
ERR35:
ERR36:
ERR37:	JSR RRR
	^D31
	[ASCIZ /PIA /]
	I
	[ASCIZ / INTERRUPTED, /]
	PIE
	[ASCIZ / EXPECTED/]
	0
	DDT
ERR39:	XOR A,INCTW
	JSR RRR
	^D39
	[ASCIZ ?INC/DEC EXPECTED ?]
	INCTW
	[ASCIZ /, READ /]
	SAVA
	0
	INCT4

ERR41:	JSR RRR
	^D41
	[ASCIZ /UCODE LOC /]
	SAVB
	[ASCIZ / ADDR /]
	SAVC
	[ASCIZ / JUMPED TO /]
	SAVA
	0
	UCT7

ERR42:	JSR RRR
	^D42
	[ASCIZ /EXPECTED /]
	WRAPV
	[ASCIZ /, READ/]
	SAVB
	0
	WRP4

ERR43:	JSR RRR
	^D43
	[ASCIZ /BUS IN PAR ERR/]
	0
	WRP6

ERR50:	JSR RRR
	^D50
	[ASCIZ /CONI SHOULD BE /]
	[1,,7]
	[ASCIZ /, IS /]
	SAVA
	0
	BTECI

BTECI:	SLOOP
BTECI1:	CONI SA0,A
	JRST BTECI
ERR51:	JSR RRR
	^D51
	[ASCIZ /CONI FAILING/]
	0
	BTECI

ERR52:	JSR RRR
	^D52
	[ASCIZ /SA-10 INACCESSABLE/]
	0
	BTECI

ERR53:	JSR RRR
	^D53
	[ASCIZ /CONI SHOULD BE /]
	[1,,0]
	[ASCIZ /, IS /]
	SAVA
	0
	BTECR

BTECR:	SLOOP
	CONO 200000
	CONI SA0,A
	JRST BTECR

ERR54:	LDB D,[301100,,B]
	TRZ D,3
	JSR RRR
	^D54
	[ASCIZ /DEVICE /]
	SAVD
	[ASCIZ / CONI GIVES /]
	[1,,0]
	0
	BTECW

BTECW:	SLOOP
	XCT SAVB
	JRST BTECW

ERR55:	JSR RRR
	^D55
	[ASCIZ /CONO 7 GIVES CONI /]
	SAVA
	0
	BTECC

BTECC:	SLOOP
	CONO SA0,7
	CONI SA0,A
	JRST BTECC
ERR56:	JSR RRR
	^D56
	[ASCIZ /RESET GIVES CONI /]
	SAVA
	0
	BTERC

BTERC:	SLOOP
	CONO SA0,7
	CONO 200000
	JRST BTERC

ERR57:	JSR RRR
	^D57
	[ASCIZ /CONO RDCONI+7 GIVES DATAI /]
	SAVA
	[ASCIZ /, SHOULD BE /]
	[2,,7]
	0
	BTEDI

BTEDI:	SLOOP
	DATAI SA0,A
	JRST BTEDI

ERR58:	JSR RRR
	^D58
	[ASCIZ /CONO RDCONI+SASTEP GIVES DATAI /]
	SAVA
	0
	BTESD

BTESD:	SLOOP
	CONO SA0,RDCONI+SASTEP
	DATAI SA0,A
	JRST BTESD
PTN0:	0	;CALLED BY JSR
	JRST PTN1	;ON TO NEXT VALUE

PTN:	0
	ADDI A,PTNTX	;BEG OF SINGLE BITS IN TABLE
	MOVEM A,PTNA	;WHERE IN TABLE TO STOP
	MOVEI A,PTNTX-1
	MOVEM A,PTNP	;INITIALIZE POINTER
PTN1:	AOS A,PTNP
	CAML A,PTNA	;AT END OF DATA?
	JRST PTN5	;YES, MAKE 1 RANDOM #
	MOVE A,(A)	;NO, TAKE ENTRY FROM TABLE
	JRST @PTN	;EXIT TO TOP OF LOOP

PTN5:	CAMN A,PTNA	;GIVEN RANDOM VALUE YET?
	JRST @PTN0	;YES, EXIT TO BOTTOM OF LOOP
	MOVEI A,105	;NO, MAKE IT NOW
	FMPB A,RNDM
	JRST @PTN	;AND EXIT TO TOP OF LOOP
PTNTX:	REPEAT ^D36,<	EXP 1B<PTNTX-.+^D35>>

PTNT:	0
	-1
	252525,,252525
	525252,,525252

PTNA:	0
PTNP:	0


TEST:	0
	-1
	252525,,252525
	525252,,525252	;PSEUDO-RANDOM TEST DATA
TEST1:	BLOCK 4	;PLACE TO READ STUFF BACK INTO

CWTD:
IFE CV,<325125,,340125>
IFN CV,<325137,,340125>
	125120,,252252
	240000,,

STCLM:	014000	;SET BUSY FLAG
	015000	;SET STATUS FLAG
	015400	;SET P.I. ENABLE FLAG

STCLM1:	BSY0
	STF0
	PIE0

BITS:	REPEAT ^D36,<	EXP 1B<.-BITS>>

CCTBL:	BLOCK NCHN	;LITTLE TABLE USED AT BEGINNING OF PROG.

	BLOCK 10
CPG0:	BLOCK 400
CPG1:	BLOCK 400
CPG2:	BLOCK 400
CPG3:	BLOCK 400
CPG4:	BLOCK 400
CPG5:	BLOCK 400
CPG6:	BLOCK 400
CPG7:	BLOCK 400
CPG10:	BLOCK 400
CPG11:	BLOCK 400
CPG12:	BLOCK 400
CPG13:	BLOCK 400
CPG14:	BLOCK	400
CPG15:	BLOCK	400
CPG16:	BLOCK 400
CPG17:	BLOCK 400

PDL:	BLOCK 200	;PUSHDOWN LIST

R0==0
R1==10000
R2==20000
R3==30000
B0==0
B1==1
B2==2
B3==3
B4==4
SW1==5
BUSOUT==5
FOFI==6
FOFIS==7
BUSIN==10
STRD==10
STRDC==11
TAGB==12
STWR==12
INTST==13
STWRD==13
ZERO==14
LOAD==14
MRB==15
WHOLE==15
FIFO==16
B14==16
FIFOS==17

WC==0
CA==2000
PC==4000
A3==6000
IARB==0
INCR==0
WCFLD==1
ADRFLD==2
MAGIC==3
DECR==1
CLRINS==400
CLRTAG==0
STGO==10000
STRQ==10400
STFLG==11000
STPIE==11400
ADROUT==20000
SELOUT==20400
SRVOUT==21000
NOPOUT==21400
SUPOUT==22000
CMDOUT==22400
WRBIT==23000
BUFENB==23400
INS0==30000
INS1==30400
SELERR==31000
CTBYT==31400
CTLERR==32000
SKPFLG==32400
LENERR==33000
PCIFLG==33400
BIT00==0
BIT01==1
BIT10==20
BIT11==21
BIT20==40
BIT21==41
BIT30==60
BIT31==61
BIT40==100
BIT41==101
BIT50==120
BIT51==121
BIT60==140
BIT61==141
BIT70==160
BIT71==161
Z==200
NZ==201
ERR==221
NERR==220
DEVM==240
RBAK==260
NBTM==300
IFN EADR,<SKPC==320>
IFE EADR,<SKPC==Z>
TAGBS==TAGB_10
ADRI0==TAGBS+BIT00
ADRI1==TAGBS+BIT01
SELI0==TAGBS+BIT10
SELI1==TAGBS+BIT11
BFMT0==TAGBS+BIT20
BFMT1==TAGBS+BIT21
OPLI0==TAGBS+BIT30
OPLI1==TAGBS+BIT31
RQI0==TAGBS+BIT40
RQI1==TAGBS+BIT41
STAI0==TAGBS+BIT50
STAI1==TAGBS+BIT51
BCRQ0==TAGBS+BIT60
BCRQ1==TAGBS+BIT61
BFHLT0==TAGBS+BIT71
BFHLT1==TAGBS+BIT70
INSTS==INTST_10
INS00==INSTS+BIT00
INS01==INSTS+BIT01
INS10==INSTS+BIT10
INS11==INSTS+BIT11
PCIF0==INSTS+BIT70
PCIF1==INSTS+BIT71
SW1S==SW1_10
BSY0==SW1S+BIT00
BSY1==SW1S+BIT01
STRQ0==SW1S+BIT10
STRQ1==SW1S+BIT11
STFL0==SW1S+BIT20
STFL1==SW1S+BIT21
WCOK0==SW1S+BIT40
WCOK1==SW1S+BIT41
TIMER0==SW1S+BIT50
TIMER1==SW1S+BIT51

DEFINE MPAGE
<IFN .&17,<.PRINT <BADMPG>>>

DEFINE ADRSET (NAME)
<IFIDN <NAME>,<>,<ADR=.+1>
IFDIF <NAME>,<>,<ADR=NAME>
IF2,<IFN <.&1400>-<ADR&1400>,<.PRINT <BNDER1>>>
ADR=ADR&377
>
DEFINE PULSE (BIT,NAME)
<ADRSET NAME
.,,BIT+2000+ADR
>

DEFINE ON (BIT,NAME)
<ADRSET NAME
.,,BIT+4000+ADR
>
DEFINE OFF (BIT,NAME)
<ADRSET NAME
.,,BIT+ADR
>
DEFINE ADRST1 (NAME)
<IFIDN <NAME>,<>,<ADR=.+1>
IFDIF <NAME>,<>,<ADR=NAME>
IF2,<IFN <.&1760>-<ADR&1760>,<.PRINT <BNDERR>>>
ADR=ADR&17
>
DEFINE XFER (REG,SRC,DEST,NAME)
<ADRST1 NAME
.,,140000+REG+<SRC_10>+<DEST_4>+ADR
>
DEFINE XANC (SRC1,SRC2,REG,DEST,NAME)
<ADRST1 NAME
.,,040000+REG+SRC1+<SRC2_10>+<DEST_4>+ADR
>
DEFINE GOTO (NAME)
<.,,NAME
>
DEFINE TEST (REG,SRC,FN,NAME)
<ADRST1 NAME
.,,100000+REG+<SRC_10>+<<FN!<ADR+1>>-<FN&<ADR+1>>>
>
DEFINE HANG (REG,SRC,FN)
<.,,100000+REG+<SRC_10>+FN+.&17
>
;THIS IS WHERE THE MICROCODE IS LOADED

	SALL
MCBEG0:

PHASE 0

MCBEG:!
PAGE

	PULSE CLRINS,RST	;SYS
	ON ADROUT,HIO	;HALT I/O
	PULSE CLRINS,RST	;PROG SYS
	ON INS0,RST	;PROG SEL
	REPEAT 4,<ON INS0,RST>	;PANIC
	REPEAT 10,<0>
	MPAGE

RST:!	OFF STGO
	OFF STFLG
	PULSE CLRTAG
	TEST 0,0,INS01,RS1
RS1:!	ON SUPOUT
	ON NOPOUT
	HANG 0,0,TIMER0
	XFER R0,ZERO,WHOLE
	HANG 0,0,TIMER1
	OFF STRQ
	HANG 0,0,TIMER0
	TEST 0,0,INS00,RS2
RS2:!	OFF NOPOUT,IDLE
HIO1:!	XFER R0,ZERO,WHOLE
	HANG 0,0,OPLI1
	OFF NOPOUT,RS3
	MPAGE

HIO:!	OFF SELOUT,HIO1
ASC1:!	TEST R1,B1,BIT10,ASC1A
ASC1A:!	XANC PC,INCR,0,LOAD	;SKIP A CMD IF DEVEND+STAMOD ON DCC
	GOTO ASC1B
IDLE:!	PULSE CLRINS
	XFER R1,ZERO,WHOLE
IDL1:!	TEST 0,0,BSY1,IDL2
	ON SELOUT,ASY1	;DO A POLL CYCLE
IDL2:!	TEST 0,0,STRQ1,IDL3
	OFF STGO,GO0	;START COMMAND
IDL3:!	TEST 0,0,RQI1,IDL1
	GOTO STR	;DUMMY STATUS REQUEST
RS3:!	PULSE CLRTAG
	PULSE CLRINS
	ON CTLERR
	ON INS0,STR	;TERMINATE A SEL RST WITH A CTRL CHECK INT
	MPAGE


ASY1:!	TEST 0,0,OPLI1,ASY2
	OFF SELOUT,ASYU	;GOT SELECT IN ON POLL - REQ IN MUST HAVE DROPPED
ASY2:!	TEST 0,0,SELI1,ASY1
	ON INS1,ASY3	;POLL SUCCESSFUL
ASY3:!	HANG 0,0,ADRI0
	XFER R1,BUSIN,B2,ASY4	;GET ADDR
ASY5:!	HANG 0,0,ADRI1
	OFF CMDOUT
	HANG 0,0,STAI0
	XFER R1,BUSIN,B1	;GET THE STATUS
	OFF SELOUT,AST	;AND GO TEST FOR DCC
ASY4:!	ON CMDOUT,ASY5
ASYU:!	HANG 0,0,SELI1
	GOTO IDLE
ASP1:!	HANG 0,0,OPLI1
	GOTO STR
	MPAGE

AST:!	TEST R1,B1,NERR,AST1
AST3B:!	TEST R2,B0,BIT01,AST4
AST1:!	ON SRVOUT,ASP	;ERR IN STATUS, GIVE TO PROG
	TEST R1,B1,BIT51,AST2
AST2:!	ON SRVOUT,ASP	;NO DEVEND IN STATUS,GIVE TO PROG
	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,STRD
	GOTO AST3
AST3A:!	TEST R2,B1,BIT01,AST4
	TEST R2,B1,BIT30,AST6	;MATCHING DEV CODE FOUND, CHK WAIT
AST4:!	ON SRVOUT,ASP	;SEL MODE OR END OF LIST,GIVE TO PROG
	XANC A3,INCR,0,STRD
	XFER R2,MRB,WHOLE	;DEV TABLE ENTRY TO R2
AST5:!	TEST R1,0,DEVM,AST3A
AST6:!	ON SRVOUT,ASP	;START OR HALTED, GIVE TO PROG
	ON SUPOUT,ASC	;ALL TESTS PASSED, INDICATE CHAINING
	MPAGE

ASC:!	ON SRVOUT,ASC1
ASC3:!	PULSE CLRINS,FET
ASC1B:!	HANG 0,0,STAI1
	OFF SRVOUT,ASC1C
CMD12:!	GOTO XD4
	0
ASC2:!	XANC A3,DECR,0,STWRD
	XANC A3,B1,R0,B2
	XANC A3,B2,R0,B3
	XANC A3,B3,R0,B14
	XANC CA,INCR,0,LOAD
	XANC CA,INCR,0,LOAD	;BASE+3
	XANC CA,INCR,R0,STWR	;STORE PTR TO DEVLST
ASC2A:!	XANC PC,ADRFLD,R2,LOAD,ASC3
ASP:!	HANG 0,0,STAI1
	OFF SRVOUT,ASP1
	MPAGE

FET:!	XANC PC,INCR,0,STRD
FET0:!	ON INS0
	XFER R0,MRB,WHOLE	;GET CMD
	TEST R0,B0,BIT21,FET1
FET1:!	TEST R0,B0,BIT11,FET2	;SP CMD
	XFER R0,B2,BUSOUT	;DEV CMD
	ON ADROUT,INI2
FET4:!	TEST R0,B0,BIT01,FET5
FET2:!	TEST R0,B0,BIT31,FET3
	XANC PC,ADRFLD,R0,LOAD,FET	;TCH
FET3:!	ON INS1,INERR	;HALT
	XANC CA,ADRFLD,R0,LOAD,FET4	;LOAD OR STORE
FET5:!	XANC CA,INCR,0,STRD,FET6	;LOAD
	XANC CA,INCR,R2,STWR,FET	;STORE
FET6:!	XFER R2,MRB,WHOLE,FET
STR1:!	OFF STRQ,STX
MPAGE

INI6:!	HANG 0,0,SELI1
	OFF ADROUT,INI6A
MERR:!	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,STRD
	ON PCIFLG,MERR1
INI5A:!	OFF SELOUT,INI7
INI2:!	ON SELOUT
INI2A:!	XFER R1,MRB,B2,INI5	;SAVE DEVADR IN STAT FOR CU BSY ETC
INI3:!	TEST 0,0,OPLI1,INI4
	OFF SELOUT,INI6	;NOT OPER
INI4:!	TEST 0,0,STAI1,INI5
	OFF ADROUT,CMD	;NORMAL SELECTION
INI5:!	TEST 0,0,SELI1,INI3
	XFER R1,BUSIN,B1,INI5A	;CU BUSY SEQ, GET STATUS
INI7:!	HANG 0,0,STAI1
	OFF ADROUT,INERR
	MPAGE

CMD:!	OFF SUPOUT,CMD1
CMD2:!	TEST R0,B0,NBTM,CMD3
CMD1:!	HANG 0,0,ADRI0
	XFER R1,BUSIN,B2	;GET ECHOED ADDR
	XFER R0,B1,BUSOUT	;PUT OUT CMD BYTE
	ON CMDOUT,CMD2
CMD3:!	ON CTBYT
	XANC PC,B1,R2,B2
	HANG 0,0,ADRI1	;WAIT FOR CMD ACCEPTED
	OFF CMDOUT,CMD4
CMD10:!	HANG 0,0,STAI0
	TEST 0,BUSIN,Z,CMD11
CMD11:!	GOTO ED
	ON SRVOUT	;ACCEPT INITIAL STATUS OF 0 (NMT)
	HANG 0,0,STAI1
	OFF SRVOUT,CMD12
	MPAGE

CMD4:!	TEST R0,B1,NZ,CMD5
CMD8:!	GOTO DAT
CMD5:!	GOTO ESW	;CMD WAS TEST I/O
	XANC PC,B2,R2,B3
	XANC PC,B3,R2,B14
	TEST R0,B0,BIT00,CMD6
CMD6:!	XANC A3,ADRFLD,R2,LOAD,CMD9	;NMT, SETUP FOR RETRY
	XANC PC,INCR,0,STRD	;START FIRST DCW FET
	XANC A3,ADRFLD,R2,LOAD	;SETUP SAVED PC FOR RETRY
	TEST R0,B1,BIT71,CMD7
CMD7:!	OFF WRBIT,CMD8
	ON WRBIT,CMD8
CMD9:!	ON WRBIT
	ON INS1,CMD10	;MAKE BUFFER NO AVAIL TO DEVICE
INI6A:!	ON SELERR,INERR
INERR:!	OFF SUPOUT,MERR
	MPAGE

STR:!	XFER R1,INTST,B0	;STORE STATUS ROUTINE
	XANC WC,B1,R2,B0
	XANC WC,B2,R2,B1
	XANC PC,B1,R2,B2
	XANC PC,B2,R2,B3
	XANC PC,B3,R2,B14
	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,LOAD
	HANG 0,0,STFL1
	XANC CA,INCR,R1,STWR
	XANC CA,INCR,R2,STWR
	XFER R2,MRB,WHOLE	;WAIT FOR LAST STORE DONE
	ON STFLG,STR1
STX2:!	TEST R0,B0,BIT21,STX3
STX3:!	PULSE CLRINS,FET	;NORMAL PCI
	GOTO SIT	;PCI WITH CHNL END AND DCC
	MPAGE

STX8:!	TEST R2,B0,BIT10,STX9
STX:!	TEST 0,0,PCIF1,STX1
STX1:!	TEST 0,0,INS00,STX4
	OFF PCIFLG,STX2
STX4:!	TEST 0,0,INS10,STX5	;ASYNC OR DUMMY
	TEST R0,B0,BIT21,STX6	;INI OR END
STX5:!	XANC CA,MAGIC,0,LOAD,STX7	;DUMMY
	GOTO STX9	;ASYNC
STX6:!	TEST R0,B0,BIT01,STX6A	;HLT
	GOTO STX9	;NO A HLT
STX6A:!	GOTO STX9	;SLOW HALT
	GOTO SIT	;FAST HLT
STX7:!	XANC CA,INCR,0,STRD
	XFER R2,MRB,WHOLE,STX8
STX9:!	HANG 0,0,STFL1
	GOTO IDLE
	MPAGE

MERR1:!	XFER R2,MRB,WHOLE
	TEST R2,B0,BIT01,MERR2
MERR2:!	OFF PCIFLG,STR	;SEL MODE, JUST STORE
MRK:!	XANC CA,INCR,0,LOAD	;BM
	XANC CA,INCR,0,LOAD
	XANC CA,INCR,0,STRD	;GET SAVED PTR TO DEVLST
	XFER R3,MRB,WHOLE
	XANC A3,ADRFLD,R3,LOAD
	XANC A3,INCR,0,STRD	;GET DEVLST ENTRY
	XANC A3,ADRFLD,R3,LOAD	;SET A3 BACK FOR REWRITE
	XFER R2,MRB,WHOLE
	XANC PC,B1,R2,B2
	XANC PC,B2,R2,B3
	XANC CA,WCFLD,0,LOAD
	GOTO MRK1
	0
	MPAGE

MRK1:!	TEST 0,0,PCIF0,MRK2
CMD7A:!	GOTO DAT
MRK2:!	XANC CA,INCR,0,LOAD	;SET CA FOR TERM
	XANC CA,B3,R2,B1,MRK2A
MRK2B:!	XANC A3,INCR,R2,STWR	;STORE UPDATED PTR
	TEST 0,0,PCIF0,MRK3
MRK3:!	OFF PCIFLG,STR	;MARK TERM DONE
	TEST R0,B0,BIT50,MRK4	;MARK HUNG DONE
MRK4:!	ON PCIFLG,STR	;PIC W/DCC CHNL END
SIT:!	TEST R3,B1,BIT01,SIT1	;TEST FOR ASYNC OR GOFLG STRT
SIT1:!	GOTO IDLE	;ASYNC START
	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,LOAD	;SET CA FOR ASC
	GOTO GO2
MRK2A:!	XANC PC,B3,R2,B14,MRK2B
	0
	MPAGE

GO0:!	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,STRD
	XFER R2,MRB,WHOLE
	TEST R2,B0,BIT01,GO1
GO1:!	GOTO ASC2A	;SEL MODE
	XANC A3,ADRFLD,R2,LOAD,GO2	;BM
GO3:!	GOTO IDLE	;END OF LIST
	TEST R2,B1,BIT21,GO4
GO4:!	XFER R0,MRB,WHOLE,GO4A	;START FOUND,MAKE R0 NEG
GO2:!	XANC A3,INCR,0,STRD	;KEEP LOOKING
	XFER R2,MRB,WHOLE
	TEST R2,B1,BIT01,GO3
GO5:!	GOTO ASC2	;CA:!BASE+1, A3:!DEVLSTPT+1, R2:!CMD ADR
GO4A:!	XFER R1,MRB,B0	;PUT DEVADR INTO STATUS WORD
	XFER R1,B0,B2	;GET IT IN THE CORRECT BYTE
	XFER R1,ZERO,B1,GO5	;AND CLEAR BYTE 1
	MPAGE

DUN:!	TEST 0,0,PCIF1,DUN1
DUN3:!	ON PCIFLG,STR	;DO PCI
DUN1:!	TEST R0,B0,BIT51,DUN2	;NOT DCC
	OFF PCIFLG,DUN4	;DCC
DUN2:!	PULSE CLRINS,FET	;NOT PCI
	XFER R0,ZERO,WHOLE,DUN3	;PCI, CAUSE RET TO FETCH
DUN4:!	OFF SUPOUT,MRK	;MARKS HUNG
AST3:!	XFER R2,MRB,WHOLE	;BLK MUX FLAG TO R2
	XANC A3,ADRFLD,R2,LOAD
	GOTO AST3B
	0
	0
ASC1C:!	HANG 0,0,OPLI1
	XFER R0,ZERO,WHOLE
	GOTO GO4A
	0
	MPAGE

ED:!	TEST 0,0,OPLI1,ED1
IMM:!	TEST 0,INTST,BIT31,ES1
ES1:!	TEST 0,INTST,BIT61,ES1A	;NO PARERR
	GOTO ES7	;BUS IN PAR ERR
ES1A:!	GOTO ES2	;NO LEN ERR
	TEST R0,B0,BIT61,ES1B	;TEST SLI
ES1B:!	GOTO ES3B	;HALT ON LEN ERR
	GOTO ES2	;IGNORE LEN ERR
ED1:!	ON CTLERR,ED1A	;END STATUS MISSING
	XFER R1,BUSIN,B1,IMM
ESWI:!	HANG 0,0,STAI1
	OFF SRVOUT
ESW:!	TEST 0,0,OPLI1,ESW1
ES0:!	XFER R1,BUSIN,B1,IMM	;MORE STATUS
ESW1:!	GOTO DUN	;NORMAL DISCONNECT
	TEST 0,0,STAI1,ESW
	MPAGE

ES2:!	TEST 0,BUSIN,ERR,ES3
ES8A:!	XANC PC,ADRFLD,R2,LOAD,ES8B	;CHAIN BACK TO PREV CMD
ES3:!	TEST R0,B0,BIT31,ES4	;NO BAD BITS
ES3B:!	TEST 0,BUSIN,BIT61,ES7	;BAD BITS
ES4:!	OFF SELOUT,ES5	;NO CHAINING
ES4A:!	ON SUPOUT,ES6	;CHAINING
ES7:!	OFF SUPOUT,CMDE	;NO UNIT CHECK
	TEST 0,BUSIN,BIT11,ES8	;UN CHECK
ES8:!	OFF SUPOUT,CMDE	;NO STA MOD
	XANC A3,B1,R2,B2	;RETRY STATUS
	XANC A3,B2,R2,B3
	XANC A3,B3,R2,B14,ES8A
ES8B:!	OFF LENERR
	XANC PC,DECR,0,STWRD,ES4A
CMDE:!	OFF SELOUT
	ON SRVOUT,CMDE1
	MPAGE

ES6:!	TEST 0,BUSIN,BIT51,ES6A
ES9A:!	TEST R2,B0,BIT01,ES5
ES6A:!	XANC CA,MAGIC,0,LOAD,ES9	;NO DEV END
	TEST 0,BUSIN,BIT11,ES6B	;DEV END
ES6B:!	OFF SELOUT,ESF	;DEVEND W/O STAMOD
	TEST 0,BUSIN,BIT61,ES6C	;STA MOD
ES6C:!	XANC PC,INCR,0,LOAD	;DEVEND, STAMOD, NOT UNCHK
	OFF SELOUT,ESF	;UNCHK OVERRIDES SKIP
ES9:!	XANC CA,INCR,0,STRD
	XFER R2,MRB,WHOLE,ES9A
ES5:!	ON SRVOUT,ESWI
	ON PCIFLG,ES4
XD2D:!	HANG 0,0,BFHLT0
	ON LENERR,XD2C
	0
	0
	MPAGE

DSRO:!	TEST R3,B2,SKPC,DSRO1
XD3:!	TEST R0,B1,BIT71,XD3A
DSRO1:!	TEST R0,B1,RBAK,DSRO3	;NOT SKIP
	TEST R3,B3,Z,DSRO2	;POSS SKIP
DSRO2:!	TEST R0,B1,RBAK,DSRO3	;NOT SKIP
	GOTO SK6	;SKIP
DSRO3:!	GOTO RD6	;RD FWD
	TEST R0,B0,BIT41,DSRO4	;RD BAK
DSRO4:!	GOTO RB6	;RD BAK WD
	GOTO RBB	;RD BAK BYTE
XD3A:!	GOTO XD2	;DON'T DO ANYTHING SPECIAL IF READ
	XANC WC,WCFLD,0,LOAD,XD3B	;FORCE WCOK
XD4:!	XFER R3,ZERO,WHOLE,XD3D	;SET TO CLE@R WC
XD3B:!	XFER R3,ZERO,WHOLE,XD3C	;CLEAR TSD0
XD3D:!	XANC WC,ADRFLD,R3,LOAD,XD3A	;FORCE WC ZERO
XD3C:!	XFER R2,B4,FOFI,XD3D	;WRITE LAST BYTE (HALF FULL)
	MPAGE

DAT2:!	HANG 0,0,STAI0
	TEST 0,BUSIN,Z,DAT3	;TEST INITIAL STATUS
DAT3:!	OFF INS1,XD1D	;NOT ZERO, NO XFER
	TEST R0,B1,BIT71,DAT4	;TEST WRITE
DAT4:!	ON SRVOUT,DAT5	;ACCEPT ZERO STATUS ON READ
	XANC CA,INCR,0,STRD	;FETCH FIRST WORD TO WRITE
	TEST R0,B0,BIT11,DAT6	;TEST TAPE COMPAT MODE
	0
DAT6:!	GOTO DSW0	;NOT TAPE COMPAT
	XFER R2,ZERO,WHOLE	;FOR ZERO BITS IN 9 TRACK MODE
	OFF CTBYT	;MAY HAVE BEEN SET IF 9 TRACK
	GOTO DXW
DAT5:!	HANG 0,0,STAI1
	TEST R0,B0,BIT11,DAT5A
DAT5A:!	OFF SRVOUT,DSR
	GOTO DXR	;TAPE COMPAT READ
	MPAGE

DSR:!	TEST R3,B2,SKPC,DSR1
DSR6A:!	GOTO SK6
DSR1:!	TEST R0,B1,RBAK,DSR3	;NOT SKIP
	TEST R3,B3,Z,DSR2	;POSS SKIP
DSR2:!	TEST R0,B1,RBAK,DSR3	;NOT SKIP
	TEST R0,B1,RBAK,DSR4	;SKIP
DSR3:!	GOTO RD0	;RD FWD
	TEST R0,B0,BIT71,DSR5	;RD BAK
DSR4:!	GOTO SK0	;SKP FWD
	TEST R0,B0,BIT71,DSR6	;SKP BAK
DSR5:!	TEST R0,B0,BIT41,DSR7	;RD BAK EVEN
	GOTO RB11	;RD BAK ODD
DSR6:!	GOTO SK0	;SKP BAK EVEN
	XFER R2,FIFO,B4,DSR6A	;SKP BAK ODD
DSR7:!	GOTO RB0	;RD BAK WD
	GOTO RBB	;RD BAK BYTE
	MPAGE

RD1:!	XFER R2,FIFO,B1,RD2
RDX:!	GOTO DEE
RD2:!	XFER R2,FIFO,B2,RD3
RD5:!	XANC CA,INCR,R2,STWR,RD6
	0
RD3:!	XFER R2,FIFO,B3,RD4
RD4:!	XFER R2,FIFO,B4,RD5
RD10:!	XANC CA,INCR,R3,STWR,RD0
RD7:!	XFER R3,FIFO,B1,RD8
RDX2:!	GOTO DEO
RD8:!	XFER R3,FIFO,B2,RD9
RD0:!	XFER R2,FIFO,B0,RD1
	0
	0
RD9:!	XFER R3,FIFO,B3,RD10
RD6:!	XFER R3,FIFO,B0,RD7
	MPAGE

RB1:!	XFER R3,FIFO,B2,RB2
RBX:!	GOTO DEE
RB2:!	XFER R3,FIFO,B1,RB3
RB5:!	XANC CA,DECR,R3,STWR,RB6
	0
RB3:!	XFER R3,FIFO,B0,RB4
RB4:!	XFER R2,FIFO,B4,RB5
RB10:!	XANC CA,DECR,R2,STWR,RB0
RB7:!	XFER R2,FIFO,B2,RB8
RBX2:!	GOTO DEO
RB8:!	XFER R2,FIFO,B1,RB9
RB0:!	XFER R3,FIFO,B3,RB1
ED1A:!	GOTO MERR
RB9:!	XFER R2,FIFO,B0,RB10
RB6:!	XFER R2,FIFO,B3,RB7
RB11:!	XFER R2,FIFO,B4,RB6
MPAGE

SK1:!	XFER R2,FIFO,B1,SK2
SKX:!	GOTO DEE
SK2:!	XFER R2,FIFO,B2,SK3
SK6:!	XFER R3,FIFO,B0,SK7
	0
SK3:!	XFER R2,FIFO,B3,SK4
SK4:!	XFER R2,FIFO,B4,SK6
SK0:!	XFER R2,FIFO,B0,SK1
SK7:!	XFER R3,FIFO,B1,SK8
SKX2:!	GOTO DEO
SK8:!	XFER R3,FIFO,B2,SK9
	0
	0
SK9:!	XFER R3,FIFO,B3,SK0
DSW4:!	HANG 0,0,STAI1
	OFF SRVOUT,WR2
	MPAGE

ESF:!	ON SRVOUT
	TEST R0,B0,BIT51,ESF1
ESF1:!	XANC PC,INCR,0,STRD,ESF2	;NO PCI, START NEXT FETCH
	GOTO ESWI	;PCI, CAN'T BE FAST
ESF2:!	PULSE CLRINS,ESF3
RBB:!	XFER R2,ZERO,WHOLE,RBBA
RBBT:!	TEST R3,B1,BIT71,RBB0	;3 OR 4 BYTES IN 1ST WD
	TEST R3,B1,BIT71,RBB2	;1 OR 2 BYTES IN 1ST WD
RBB1:!	XFER R2,FIFO,B2,RBB2
	GOTO DEE
RBB2:!	XFER R2,FIFO,B1,RBB3	;2 BYTE ENTRY
RBB3:!	XFER R2,FIFO,B0,RBB4	;1 BYTE ENTRY
RBB0:!	XFER R2,FIFO,B3,RBB1	;4 BYTE ENTRY
	XFER R2,FIFO,B2,RBB2	;3 BYTE ENTRY
RBB4:!	XANC CA,DECR,R2,STWR,RBB0
RBBA:!	TEST R3,B1,BIT61,RBBT
	MPAGE

WR1:!	XFER R2,B1,FOFI,WR2
WRX:!	GOTO DEE
WR2:!	XFER R2,B2,FOFI,WR3
WR3A:!	XFER R3,MRB,WHOLE,WR5
WR4:!	XFER R2,B4,FOFI,WR6
WR3:!	XFER R2,B3,FOFI,WR3A
WR6:!	XFER R3,B0,FOFI,WR7
WR10:!	XFER R2,MRB,WHOLE,WR10A
WR7:!	XFER R3,B1,FOFI,WR8
WRX2:!	GOTO DEO
WR8:!	XFER R3,B2,FOFI,WR9
WR0:!	XFER R2,B0,FOFI,WR1
WR10A:!	XANC CA,INCR,0,STRDC,WR0
WR9:!	XFER R3,B3,FOFI,WR10
WR5:!	XANC CA,INCR,0,STRDC,WR4
WRT:!	XANC CA,INCR,0,STRD,WR10
	MPAGE

DSRE:!	TEST R3,B2,SKPC,DSRE1
	0
DSRE1:!	TEST R0,B1,RBAK,DSRE3	;NOT SKIP
	TEST R3,B3,Z,DSRE2	;POSS SKIP
DSRE2:!	TEST R0,B1,RBAK,DSRE3	;NOT SKIP
	GOTO SK0	;SKIP
DSRE3:!	GOTO RD0	;RD FWD
	TEST R0,B0,BIT41,DSRE4	;RD BAK
DSRE4:!	GOTO RB0	;RD BAK WD
	GOTO RBB	;RD BAK BYTE
DSW2:!	ON SRVOUT,DSW3	;ACCEPT INITIAL STATUS FOR NORMAL WRITE
DSW0:!	TEST R0,B0,BIT71,DSW1
DSW1:!	XFER R2,MRB,WHOLE,DSW2	;WAIT FOR FIRST WORD TO BE WRITTEN
	XFER R2,MRB,WHOLE	;SKIP 2 BYTES
	XANC CA,INCR,0,STRD
	ON SRVOUT,DSW4	;ACCEPT INITIAL STATUS - SKIP 2 BYTES MODE
	MPAGE

DEE:!	TEST 0,0,WCOK1,DEE1
DEE2A:!	XANC CA,ADRFLD,R3,LOAD,DEE2B
DEE1:!	TEST 0,0,INS11,DEE2	;WC OVF TEST DC
DEE1A:!	TEST 0,0,INS11,DEE1B	;DEV TRUNC, SEE IF SKIP DCW'S NEEDED
DEE2:!	GOTO XD2	;NO DC (CHNL TRUNC OR OK)
	XANC PC,INCR,0,STRD	;GET NEXT DCW
	XFER R3,MRB,WHOLE,DEE2A	;DONT STEP ON TDS0
DEE2B:!	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD	;SET READ-OK
	TEST R3,B0,BIT00,DEE3
DEE3:!	ON INS1	;FLAG NO MORE DCWS
	TEST R0,B1,BIT71,DEE4
DEE4:!	GOTO DSRE
	GOTO WRT
DEE1B:!	ON LENERR,XD2C	;NO SKIPS NEEDED
	ON INS1,XD1E	;FLAG AS ENDING STATUS, DO SKIPS IF REQUIRED
	MPAGE

DEO:!	TEST 0,0,WCOK1,DEO1
DEO2A:!	XANC CA,ADRFLD,R3,LOAD,DEO2B
DEO1:!	TEST 0,0,INS11,DEO2	;WC OVF TEST DC
	GOTO DEE1A	;DEV TRUNC
DEO2:!	GOTO XD3	;NO DC (CHNL TRUNC OR OK)
	XANC PC,INCR,0,STRD	;GET NEXT DCW
	XFER R3,MRB,WHOLE,DEO2A	;DONT STEP ON TDS0
DEO2B:!	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD	;SET READ-OK
	TEST R3,B0,BIT00,DEO3
DEO3:!	ON INS1	;FLAG NO MORE DCWS
	TEST R0,B1,BIT71,DEO4
DEO4:!	GOTO DSRO	;READ
	XANC CA,INCR,0,STRD	;WRITE
	GOTO WR3A
	0
	MPAGE

XD2:!	HANG 0,0,BFHLT0
	TEST 0,0,BCRQ1,XD2A
XD2A:!	TEST 0,0,BFMT1,XD2B
	ON CMDOUT	;CHNL TRUNC
	HANG 0,0,BCRQ1
	OFF CMDOUT,XD2D
XD1C:!	XFER R3,MRB,WHOLE
XD1D:!	TEST R3,B0,BIT00,XD1A
XD2B:!	ON LENERR
XD2C:!	OFF BUFENB,ED
DSW3:!	HANG 0,0,STAI1
	OFF SRVOUT,WR10A
XD1A:!	ON LENERR,XD2C	;NO MORE
XD1E:!	TEST R0,B0,BIT61,XD1B	;UNUSED DCW
XD1B:!	ON LENERR,XD2C	;NO SLI OR NO MORE
	XANC PC,INCR,0,STRD,XD1C	;SKIP A DCW
	MPAGE

DAT:!	XFER R3,MRB,WHOLE
	XANC CA,ADRFLD,R3,LOAD
	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD	;SET READ-OK
	TEST R3,B0,BIT00,DAT1
	0
ESF3:!	HANG 0,0,STAI1
	OFF SRVOUT
	HANG 0,0,OPLI1
	GOTO FET0
CMDE1:!	HANG 0,0,STAI1
	OFF SRVOUT
	HANG 0,0,OPLI1
	GOTO MERR
DAT1:!	ON INS1	;FLAG NO MORE DCW'S
	ON BUFENB,DAT2
	MPAGE

W71:!	XFER R3,B1,FOFI,W72
W7X:!	GOTO DXE
W72:!	XFER R3,B2,FOFI,W73
W74:!	XFER R2,MRB,WHOLE,W75
W75:!	XANC CA,INCR,0,STRDC,W76
W73:!	XFER R3,B3,FOFI,W74
W77:!	XFER R2,B1,FOFIS,W78
W76:!	XFER R2,B0,FOFIS,W77
W78:!	XFER R2,B2,FOFIS,W79
W7X1:!	GOTO DXO
W79:!	XFER R2,B3,FOFIS,W7A
W7B:!	XFER R3,MRB,WHOLE,W7C
W70:!	XFER R3,B0,FOFI,W71
W7A:!	XFER R2,B4,FOFIS,W7B
W7C:!	XANC CA,INCR,0,STRDC,W70
W7D:!	XANC CA,INCR,0,STRD,W7B
	MPAGE

R71:!	XFER R3,FIFO,B1,R72
R7X:!	GOTO DXE
R72:!	XFER R3,FIFO,B2,R73
R75:!	XANC CA,INCR,R3,STWR,R76
	0
R73:!	XFER R3,FIFO,B3,R74
R74:!	XFER R2,FIFOS,B0,R75
R76:!	XFER R2,FIFOS,B1,R77
R77:!	XFER R2,FIFOS,B2,R78
R7X1:!	GOTO DXO
R78:!	XFER R2,FIFOS,B3,R79
R7A:!	XANC CA,INCR,R2,STWR,R70
R70:!	XFER R3,FIFO,B0,R71
R79:!	XFER R2,FIFOS,B4,R7A
	0
	0
	MPAGE

B71:!	XFER R2,FIFOS,B3,B72
B7X:!	GOTO DXE
B72:!	XFER R2,FIFOS,B2,B73
B75:!	XANC CA,DECR,R2,STWR,B76
	0
B73:!	XFER R2,FIFOS,B1,B74
B74:!	XFER R2,FIFOS,B0,B75
B7B:!	XFER R2,FIFOS,B0,B76
B77:!	XFER R3,FIFO,B2,B78
B7X1:!	GOTO DXO
B78:!	XFER R3,FIFO,B1,B79
B7A:!	XANC CA,DECR,R3,STWR,B70
	0
B79:!	XFER R3,FIFO,B0,B7A
B76:!	XFER R3,FIFO,B3,B77
B70:!	XFER R2,FIFOS,B4,B71
	MPAGE

S71:!	XFER R3,FIFO,B1,S72
S7X:!	GOTO DXE
S72:!	XFER R3,FIFO,B2,S73
S75:!	XFER R2,FIFOS,B1,S76
S79:!	XFER R2,FIFOS,B0,S7A
S73:!	XFER R3,FIFO,B3,S74
S74:!	XFER R2,FIFOS,B0,S75
	0
S76:!	XFER R2,FIFOS,B2,S77
S7X1:!	GOTO DXO
S77:!	XFER R2,FIFOS,B3,S78
S70:!	XFER R3,FIFO,B0,S71
	0
S78:!	XFER R2,FIFOS,B4,S70
S7A:!	XFER R2,FIFOS,B1,S76
	0
	MPAGE

W91:!	XFER R3,B1,FOFI,W92
W9X:!	GOTO DXE
W92:!	XFER R3,B2,FOFI,W93
W95:!	XFER R3,MRB,WHOLE,W96
W90:!	XFER R3,B0,FOFI,W91
W93:!	XFER R3,B3,FOFI,W94
W94:!	XFER R2,B4,FOFI,W95
W96:!	XANC CA,INCR,0,STRDC,W90
R91:!	XFER R3,FIFO,B1,R92
R9X:!	GOTO DXE
R92:!	XFER R3,FIFO,B2,R93
R95:!	XANC CA,INCR,R3,STWR,R90
W97:!	XANC CA,INCR,0,STRD,W95
R93:!	XFER R3,FIFO,B3,R94
R94:!	XFER R3,FIFO,B4,R95
R90:!	XFER R3,FIFO,B0,R91
	MPAGE

B91:!	XFER R3,FIFO,B3,B92
B9X:!	GOTO DXE
B92:!	XFER R3,FIFO,B2,B93
B95:!	XANC CA,DECR,R3,STWR,B90
B90:!	XFER R3,FIFO,B4,B91
B93:!	XFER R3,FIFO,B1,B94
B94:!	XFER R3,FIFO,B0,B95
	0
S91:!	XFER R3,FIFO,B1,S92
S9X:!	GOTO DXE
S92:!	XFER R3,FIFO,B2,S93
S90:!	XFER R3,FIFO,B0,S91
DXW:!	XFER R3,MRB,WHOLE,DXWA
S93:!	XFER R3,FIFO,B3,S94
S94:!	XFER R3,FIFO,B4,S90
DXWA:!	ON SRVOUT,DXW0
	MPAGE

DXE:!	TEST 0,0,WCOK1,DXE1
DXE2A:!	XANC CA,ADRFLD,R3,LOAD,DXE2B
DXE1:!	TEST 0,0,INS11,DXE2
	GOTO DEE1A	;DEV TRUNC
DXE2:!	GOTO XD2	;NO DC
	XANC PC,INCR,0,STRD
	XFER R3,MRB,WHOLE,DXE2A
DXE2B:!	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD
	TEST R3,B0,BIT00,DXE3
DXE3:!	ON INS1
	TEST R0,B1,BIT71,DXE4
DXE4:!	GOTO DXRE
	TEST R0,B0,BIT41,DXE5
DXE5:!	GOTO W7D
	GOTO W97
	MPAGE

DXO:!	TEST 0,0,WCOK1,DXO1
DXO2A:!	XANC CA,ADRFLD,R3,LOAD,DXO2B
DXO1:!	TEST 0,0,INS11,DXO2
	GOTO DEE1A	;DEV TRUNC
DXO2:!	GOTO XD3	;NO DC
	XANC PC,INCR,0,STRD
	XFER R3,MRB,WHOLE,DXO2A
DXO2B:!	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD
	TEST R3,B0,BIT00,DXO3
DXO3:!	ON INS1
	TEST R0,B1,BIT71,DXO4
DXO4:!	GOTO DXRO
	XANC CA,INCR,0,STRD
	GOTO W74
	0
	MPAGE

DXRE:!	TEST R3,B2,SKPC,DXRE1
	OFF CTBYT,DXRS
DXRE1:!	TEST R0,B1,RBAK,DXRE3	;NOT SKIP
	TEST R3,B3,Z,DXRE2	;POSS
DXRE2:!	TEST R0,B1,RBAK,DXRE3	;NOT SKIP
	TEST R0,B0,BIT41,DXRE4	;SKIP
DXRE3:!	TEST R0,B0,BIT41,DXRE5	;RD FWD
	TEST R0,B0,BIT41,DXRE6	;RD BAK
DXRE4:!	GOTO S70	;SKIP 7
	OFF CTBYT,S90	;SKIP 9
DXRE5:!	GOTO R70	;RD 7
	OFF CTBYT,R90	;RD 9
DXRE6:!	GOTO B70	;RB 7
	OFF CTBYT,B90	;RB 9
DXR:!	OFF SRVOUT
	TEST R0,B0,BIT71,DXRE
	MPAGE

DXRO:!	TEST R3,B2,SKPC,DXRO1
DXRS:!	TEST R3,B2,SKPC,DXRS1
DXRO1:!	TEST R0,B1,RBAK,DXRO3	;NOT SKIP
	TEST R3,B3,Z,DXRO2	;POSS
DXRO2:!	TEST R0,B1,RBAK,DXRO3	;NOT SKIP
	GOTO S75	;SKIP
DXRO3:!	GOTO R76	;RD FWD
	GOTO B76	;RD BAK
DXRS1:!	GOTO B7B	;NOT SKIP
	TEST R3,B3,Z,DXRS2	;POSS
DXRS2:!	GOTO B7B	;NOT SKIP
	GOTO S79	;SKIP
DXW0:!	HANG 0,0,STAI1
	TEST R0,B0,BIT41,DXW1
DXW1:!	OFF SRVOUT,W7C
	OFF SRVOUT,W96
	MPAGE

	BLOCK 6*20
	XALL
DEPHASE

PATCH:
PAT:!	BLOCK 1000
	LIT
	VAR

	END DDTG
   2;Ha%