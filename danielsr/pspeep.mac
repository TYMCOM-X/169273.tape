	TITLE PSPEEP
;LIBRARY ROUTINES

P==17
BELL==7
LF==12
OPDEF CALL [PUSHJ	P,]
OPDEF RET [POPJ	P,]
CALL==PUSHJ P,
RET==POPJ P,


CR==15


	SUBTTL	DATE AND TIME ROUTINES
ENTRY OUTDAT
;OUTPUTS DATE AND TIME TO TTY IN FORMAT  DD MMM YY
;CALL OUTDAT
;CLOBBERS AC'S 1,2
;IF AC1 IS NEG, USE CURRENT DATE
;ELSE AC1 CONTAINS DATE
;IF AC2 IS NEG, DON'T GET TIME

OUTDAT:	PUSH	P,2
	CAIN	1,0
	DATE	1,
	IDIVI	1,^D31
	EXCH	1,2
	AOJ	1,		;DAY IN 1
	CAIGE	1,^D10
	OUTCHI	"0"
	CALL	DECOUT		;PRINT IT
	MOVEI	1," "
	OUTCHR	1
	SETZ	1,
	DIVI	1,^D12
	EXCH	1,2		;MONTH IN 1
	MOVE	1,MONTH(1)
	OUTSTR	1		;PRINT IT
	MOVEI	1," "
	OUTCHR	1
	MOVE	1,2
	ADDI	1,^D64
	CALL	DECOUT		;AND YEAR
	POP	P,2
	JUMPL	2,CPOPJ
	OUTSTR	[ASCIZ /    /]
	TIMER	1,
	CALL	OUTTIM		;OUTPUT TIME
CPOPJ:	RET


MONTH:	ASCIZ /JAN/
	ASCIZ /FEB/
	ASCIZ /MAR/
	ASCIZ /APR/
	ASCIZ /MAY/
	ASCIZ /JUN/
	ASCIZ /JUL/
	ASCIZ /AUG/
	ASCIZ /SEP/
	ASCIZ /OCT/
	ASCIZ /NOV/
	ASCIZ /DEC/


ENTRY OUTTIM
;OUTPUT TIME IN FORMAT HH:MM:SS
;TIME IN JIFFIES IN AC1
;CALL OUTTIM
;CLOBBERS AC1

OUTTIM:	PUSH	P,2
	IDIVI	1,^D60
	IDIVI	1,^D60
	PUSH	P,2
	IDIVI	1,^D60
	PUSH	P,2
	CAIGE	1,^D10
	OUTCHI	"0"
	CALL	DECOUT
	MOVEI	1,":"
	OUTCHR	1
	POP	P,1
	CAIGE	1,^D10
	OUTCHI	"0"
	CALL	DECOUT
	MOVEI	1,":"
	OUTCHR	1
	POP	P,1
	CAIGE	1,^D10
	OUTCHI	"0"
	CALL	DECOUT
	POP	P,2
	RET


SUBTTL	NUMERICAL OUTPUT ROUTINES
ENTRY DECOUT,OCTOUT
;MOVE 1,NUMBER
;CALL DECOUT
;CLOBBERS AC 1

DECOUT:	PUSH	P,2
	CALL	DIGOUT	;SAVE 2 AND PRINT NUMBER IN 1
	POP	P,2		;RESTORE 2
	RET

DIGOUT:	IDIVI	1,^D10
	HRLM	2,(P)	;KEEP DIVIDING AND SAVING REMAINDER
	CAIE	1,		;UNTIL WE GET 0 AS QUOTIENT
	CALL	DIGOUT
	HLRZ	1,(P)
	OUTCHI	"0"(1)
	RET

;MOVE	1,NUMBER
;IF WANT TO SUPPRESS LEADING ZEROS, TURN ON SIGN BIT IN 2

OCTOUT:	PUSH	P,0
	PUSH	P,4
	MOVNI	4,^D12	;UP TO 12 DIGITS BUT SUPPRESS LEADING 0'S
OCT1:	SETZ	0,
	LSHC	0,3
	TRNN	0,7
	JUMPL	2,OCT2
	ADDI	0,"0"
	OUTCHR
	AOJE	4,OCT3
	SETZ	0,
	LSHC	0,3
	ADDI	0,"0"
	OUTCHR
	AOJL	4,.-4
	CAIA
OCT2:	AOJL	4,OCT1
OCT3:	POP	P,4
	POP	P,0
	RET


ENTRY GETBTS
COMMENT !
GET FIRST ON BIT IN AC1 AND PUT ITS NUMBER IN AC2 (BITS ARE
NUMBERED 1 TO 36, NOT 0 TO 35.) TURN OFF THE BIT IN AC1.
RETURN 0 IN AC2 IF NO BITS ARE ON IN AC1
!

GETBTS:	JFFO	1,GETBT1
	SETZ	2,
	RET
GETBT1:	PUSH	P,3
	MOVSI	3,400000
	MOVN	2,2
	ROT	3,(2)
	XOR	1,3
	MOVN	2,2
	AOJ	2,
	POP	P,3
	RET


	END
