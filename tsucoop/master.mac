TITLE MASTER -- TELECOPY CONTROLLER

;STORAGE REFERENCED ELSEWHERE
INTERN VAROPT,FIXOPT,OPT2,OPTSZ1
;ROUTINES REFERENCED ELSEWHERE
 ENTRY ADIAG,BDIAG
 ENTRY CKTIM,COPYST,NEWSEN,NEWREC
;EXTERNAL STORAGE REFERENCED
EXTERN VERBOSE,UPDNO,JFYHR,JFYMIN,JFYSEC,CNVDAT,CNVTIM,CNVFMT
EXTERN TT,T1,T2
EXTERN COPT,LOWSW,TRU,BLNKSW,VRECL,FRECL,SUP,LCOPT
EXTERN FCOPT,FRPARAM,FTPARAM
EXTERN UCOPT,DCOPT,OPTLEN,UTMODE,URMODE
EXTERN TPARAM,RPARAM,RMODE,TMODE,DONOR,MASTER
EXTERN BPARAM,BYTESIZ,RLCCNT
EXTERN RBCNT,RBLIC,FDLIC,OFDLIC,UPDSW
EXTERN SYSTYP,TIMFLG
EXTERN STIME,ETIME,TRTOT,TMP,CCNT,TYPTIM
EXTERN RBLK,DBLK,UNCT,FNCT,SW10,SW370
EXTERN CIN,ASK
;EXTERNAL ROUTINES REFERENCED
EXTERN ABORT,RESET,SAVREG,INVU,FPF,FLKR,YESN2
EXTERN DECOUT,EROFN,EROUN,CPOPJ1,RF,TF
EXTERN GC,GCOD,SCOD,SBYTE,SNDYEL,BAD

;       OPTION PROCESSING

;       SEND SELECTED OPTIONS.  DO NOT SEND IF UNCHANGED
SNDOPT: SETOM BLNKSW    ;SUPPRESS TRAILING BLANKS
        SETOM LOWSW     ;PERMIT LOWER CASE OUTPUT
        TRO F,LCO       ;PERMIT LOWER CASE OUTPUT
        MOVE LCOPT
        CAIE 123
        JRST .+3
        AOS LOWSW       ;CONVERT LOWER TO UPPER CASE
        TRZ F,LCO               ;DITTO
        MOVE SUP
        CAIN 126
        AOS BLNKSW      ;TRANSMIT TRAILING BLANKS
        MOVE R1,[-OPTSZ2,,0]
SNO1:   MOVE @OPT1(R1)
        SKIPL OPT1(R1)  ;IS THIS OPTION DESTINED FOR 370
        SKIPE SW370     ;YES, ARE WE TALKING TO ONE
        CAMN OPT2(R1)
        JRST .+2        ;DO NOT SEND
        XCT  OPT3(R1)
        AOBJN R1,SNO1
        POPJ P,

; RECEIVE RESPONSES
RCVOPT: MOVE R1,[-OPTSZ2,,0]
SNO2:   MOVE @OPT1(R1)
        SKIPL OPT1(R1)  ;IS THIS OPTION DESTINED FOR 370
        SKIPE SW370     ;YES, ARE WE TALKING TO ONE
        CAMN OPT2(R1)
        JRST .+2        ;DO NOT RECEIVE
        PUSHJ P,GACK
        AOBJN R1,SNO2
        POPJ P,

;       OPTION TABLES
;ADDR OF OPTION CELL
OPT1: XWD 400000,COPT
 XWD 400000,LOWSW
 EXP TRU,BLNKSW,VRECL,FRECL
;OLD VALUES
OPT2: EXP -^D69,-^D69,-^D69,-^D69
OPTSZ1==.-OPT2
VAROPT: ^D65536
FIXOPT: ^D65536
OPTSZ2==.-OPT2
;SEND ROUTINES
OPT3: REPEAT 4,<PUSHJ P,STDOPT>
 REPEAT 2,<PUSHJ P,FX>
;CODES TO SEND
OPT4: EXP CPACOD,ONLCOD,TRUCOD,TRICOD,VARCOD,FIXCOD
;ERROR MESSAGES
OPT5: EXP ERR42,ERR43,ERR44,ERR45,ERR46,ERR47

ERR40: ASCIZ"
TRANSFER ABORTED
"
ERR41: ASCIZ" OPTION NOT IMPLEMENTED"
ERR42: ASCIZ"
COMPRESSION"
ERR43: ASCIZ"
UPPER-LOWER CASE"
ERR44: ASCIZ"
TRUNCATION"
ERR45: ASCIZ"
SUPPRESS TRAILING BLANKS"
ERR46: ASCIZ"
VARIABLE RECORD LENGTH"
ERR47: ASCIZ"
FIXED RECORD LENGTH"

;       SEND STANDARD OPTIONS
STDOPT: MOVEM T1
 PUSH P,R1      ;SAVE R1 ON THE STACK
 MOVE C,OPT4(R1)
 PUSHJ P,SCOD
 MOVE C,T1
 PUSHJ P,SBYTE
 JRST FXEXIT

;       SEND FIXCOD OR VARCOD
FX: MOVEM T1
 PUSH P,R1      ;SAVE R1 ON THE STACK
 MOVE C,OPT4(R1)
 PUSHJ P,SCOD
 MOVE R2,[POINT 8,T1,3]
 REPEAT 4,<ILDB C,R2
 PUSHJ P,SBYTE>
FXEXIT: PUSHJ P,SNDYEL
 POP P,R1       ;RETRIEVE R1
 POPJ P,

GACK: DMOVEM T1 ;SAVE VALUE IN T1, INDEX IN T2
 PUSHJ P,GCOD
 DI <<NICOD,GACK1>,<SUCCOD,GACK2>>
GACK1: MOVE R1,T2
 OUTSTR @OPT5(R1)
 OUTSTR ERR41   ;NOT IMPLEMENTED
 MOVEI ABORT
 PUSHJ P,SETTT
 POPJ P,
GACK2: DMOVE T1
 MOVEM OPT2(R1)
 POPJ P,

SETTT: SKIPGE TT
 MOVEM TT
 POPJ P,

;MOVE USER-SPECIFIED OPTIONS TO ACTIVE SET.  USE DEFAULT OPTIONS IF
;       NO USER-SPECIFIED OPTIONS ARE SUPPLIED.
ACT: SETZ R0,
 SKIPN SW10     ;ARE WE TALKING TO A 10?  IF SO, DEFAULT MODE IS BINARY
 JRST ACT2      ;NO
; CHOOSE XMODE OR SMODE DEPENDING ON FILE EXTENSION
 HLLZ R1,DBLK+FEXT
 MOVE R2,[-EXTLEN,,EXTTAB]
ACT1: CAME R1,0(R2)
 JRST ACT4
 MOVEI SMODE    ;SERIAL MODE
ACT2: MOVEM FTPARAM
 MOVEM FRPARAM
 HRLZI R1,-OPTLEN
 HRREI R2,-1
ACT3: MOVE DCOPT(R1)
 TDNE R2,FCOPT(R1)
 MOVE FCOPT(R1)
 TDNE R2,UCOPT(R1)      ;USER OVERRIDE ALWAYS WINS THIS RACE
 MOVE UCOPT(R1)
 MOVEM COPT(R1)
 AOBJN R1,ACT3
 MOVE TMODE
 CAMN RMODE
 POPJ P,
 OUTSTR [ASCIZ"
TRANSMISSION ABORTED DUE TO MIXED TRANSMISSION MODES
"]
 JRST ABORT
ACT4: AOBJN R2,ACT1
 JRST ACT2

EXTTAB: SIXBIT /BAC/
 SIXBIT /BIN/
 SIXBIT /BUG/
 SIXBIT /CAL/
 SIXBIT /CHN/
 SIXBIT /DAE/
 SIXBIT /DCR/
 SIXBIT /DMP/
 SIXBIT /HGH/
 SIXBIT /LOW/
 SIXBIT /MSB/
 SIXBIT /OVR/
 SIXBIT /QUE/
 SIXBIT /QUF/
 SIXBIT /REL/
 SIXBIT /RIM/
 SIXBIT /RMT/
 SIXBIT /RTB/
 SIXBIT /SAV/
 SIXBIT /SFD/
 SIXBIT /SHR/
 SIXBIT /SVE/
 SIXBIT /SYS/
 SIXBIT /UFD/
 SIXBIT /XPN/
EXTLEN==.-EXTTAB

; SEND FILE NAME
NEWSFN: MOVEI C,FLKCOD
        PUSHJ P,SCOD
        MOVEI R1,RBLK
        SKIPN DONOR
        MOVEI R1,DBLK
        SKIPE SW370     ;ARE WE TALKING TO 370
        JRST [ MOVE C,FNCT      ;YES, SEND FILENAME ONLY
                PUSHJ P,SBYTE   ;PRESERVES R1
                JRST NEWSF1]
        MOVE C,FNCT
        ADD C,UNCT
        ADDI C,2
        PUSHJ P,SBYTE   ;PRESERVES R1
        MOVEI C,LPAREN
        PUSHJ P,SBYTE
        MOVE R2,[POINT 6,UNAME(R1)]
        MOVE R3,UNCT
        PUSHJ P,SNDSTR  ;SEND USERNAME STRING
        MOVEI C,RPAREN
        PUSHJ P,SBYTE
NEWSF1: MOVE R2,[POINT 6,FNAME(R1)]
        MOVE R3,FNCT
        PUSHJ P,SNDSTR  ;SEND FILENAME STRING
        PUSHJ P,SNDYEL
        POPJ P,

SNDSTR: ILDB C,R2
        ADDI C,40
        PUSHJ P,SBYTE   ;SBYTE DESTROYS R0
        SOJG R3,SNDSTR
        POPJ P,

;       SEND PATH NAME (USERNAME, FILENAME, + OPTIONS) AS MULTIPLE
;       MESSAGES.  THEN READ THE REPLIES.
;
SPATH: PUSHJ P,ACT
 SETOM TT       ;ERROR SWITCH
 TRO F,FILFND    ;ASSUME FILE WILL BE FOUND
 PUSHJ P,NEWSFN
 PUSHJ P,SNDOPT
 PUSHJ P,GCOD
 SKIPE SW370
 JRST LK370     ;ONE LESS RESPONSE REQUIRED FROM 370
 DI <<ULKFCD,ULKF>,<ULKSCD,ULKS>>
ULKF: MOVEI INVU        ;INVALID USER ON REMOTE SYSTEM
 PUSHJ P,SETTT          ;SET ERROR SWITCH POS.
ULKS: PUSHJ P,GCOD      ;READ RESPONSE FOR FILENAME
LK370: DI <<FLKFCD,FLKF>,<FLKSCD,FLKS>,<FPFCOD,FPFSET>,<FLKRCD,FLKRSET>>
FPFSET: MOVEI FPF
 PUSHJ P,SETTT
 JRST OPTRD
FLKRSET: MOVEI FLKR
 PUSHJ P,SETTT
 JRST OPTRD
FLKF: TRZ F,FILFND       ;MUST BE NEW FILE
 JRST OPTRD
FLKS: SKIPN SW10        ;ARE WE TALKING TO 10?
 JRST OPTRD             ;NO
; READ FILE ATTRIBUTES OF REMOTE FILE
 MOVE R2,[POINT 6,OFDLIC] ;ATTRIBUTES OF RECEIVE FILE
 SKIPN DONOR            ;ARE WE DONOR
 MOVE R2,[POINT 6,FDLIC] ;NO, GET ATTRIBUTES OF DONOR FILE
 MOVEI R1,6
 PUSHJ P,GC
 IDPB C,R2
 SOJG R1,.-2
OPTRD: PUSHJ P,RCVOPT   ;WILL SET TT FOR FAILURES
 SKIPL TT               ;DID WE HAVE ANY ERRORS
 JRST @TT               ;YES, JUMP TO APPROPRIATE CODE
 POPJ P,

ADIAG: MOVEI R2,[ASCIZ"
BEGIN RANK"]
 JRST .+2
BDIAG: MOVEI R2,[ASCIZ"
BEGIN DUPLICATE"]
 SKIPN VERBOSE
 POPJ P,
 OUTSTR (R2)
 OUTSTR [ASCIZ" COMPRESSED DATA RECORD, "]
 MOVE R1,TMODE
 CAIG R1,4
 CAIG R1,2
 JRST BDIAG1
 MOVE TPARAM
 CAME RPARAM
 JRST BDIAG3    ;PARAMETERS UNEQUAL, PRINT "BINARY" OR "BINARY UPDATE"
 CAIE 361
 CAIN SMODE
 ADDI R1,4
 CAIN XMODE
 ADDI R1,6
 CAIG R1,4
 JRST BDIAG3
BDIAG1: OUTSTR @DIAGT(R1)
 OUTSTR [ASCIZ" MODE"]
BDIAG2: OUTSTR [ASCIZ"
"]
 POPJ P,
BDIAG3: OUTSTR @DIAGT(R1)
 OUTSTR [ASCIZ" MODE
  BLOCKING FILL BYTESIZE
TRANS.   "]
 MOVEI R2,TPARAM
 PUSHJ P,BDIAG4
 OUTSTR [ASCIZ"REC.     "]
 MOVEI R2,RPARAM
 PUSHJ P,BDIAG4
 JRST BDIAG2
BDIAG4: LDB R0,[POINT 4,0(R2),34] ;BLOCKING
 ADDI 1
 PUSHJ P,DECOUT
 MOVE (R2)
 MOVEI R1,[ASCIZ"    R        "]
 TRNE 1
 MOVEI R1,[ASCIZ"    L        "]
 OUTSTR (R1)
 LDB R0,[POINT 4,0(R2),30] ;BYTESIZE
 ADDI 1
 PUSHJ P,DECOUT
 OUTSTR [ASCIZ"
"]
 POPJ P,

DIAGT=.-1
 EXP DIAG1,DIAG2,DIAG3,DIAG4,DIAG5,DIAG6,DIAG7,DIAG8,DIAG9,DIAG10

DIAG1: ASCIZ"ASCII"
DIAG2: ASCIZ"EBCDIC"
DIAG3: ASCIZ"BINARY"
DIAG4: ASCIZ"BINARY UPDATE"
DIAG5: ASCIZ"KATAKANA"
DIAG6: ASCIZ"IMAGE"
DIAG7: ASCIZ"SERIAL"
DIAG8: ASCIZ"SERIAL UPDATE"
DIAG9: ASCIZ"NATIVE"
DIAG10: ASCIZ"NATIVE UPDATE"

; PRINT "COPY STARTED" MESSAGE
COPYST: TROE F,MSGSW    ;SET SWITCH TO AVOID PRINTING AGAIN
 POPJ P,
 MOVEI R1,[ASCIZ"
UPDATING"]
 MOVE TMODE
 CAIE 4
 MOVEI R1,[ASCIZ"
COPY"]
 OUTSTR (R1)
 OUTSTR [ASCIZ" STARTING"]
 SKIPE VERBOSE
 PUSHJ P,PRDAT
 OUTSTR [ASCIZ"
"]
 SETZM TRTOT    ;ZERO TOTAL TRANSMISSION TIME
 MSTIME R0,
 MOVEM STIME    ;TIME COPY STARTED (MILISECONDS)
 POPJ P,

; PRINT "COPIED TO" OR "UPDATED TO" MESSAGES
COPFIN: TIMER OFF
 OUTSTR [ASCIZ"
"]
 MOVEI R1,DBLK
 PUSHJ P,EROUN
 PUSHJ P,EROFN
 MOVE TMODE
 MOVEI R1,[ASCIZ" UPDATED TO "]
 CAIE 4
 MOVEI R1,[ASCIZ" COPIED TO "]
 OUTSTR (R1)
 MOVEI R1,RBLK
 PUSHJ P,EROUN
 PUSHJ P,EROFN
 OUTSTR [ASCIZ"
"]
 PUSHJ P,PRLIC
 POPJ P,

PRDAT: OUTSTR [ASCIZ" AT "]
 MOVE R1,[10,,11]
 GETTAB R1,
 SETZ R1,
 MOVEM R1,CNVTIM
 MOVE R1,[11,,11]
 GETTAB R1,
 SETZ R1,
 MOVEM R1,CNVDAT
 MOVEI R1,CNVDAT
 DATUUO R1,
 JFCL
 MOVE R0,CNVTIM
 IDIV R0,JFYHR
 PUSHJ P,PR2D   ;PRINT TWO DIGITS
 OUTCHI 72
 IDIV R0,JFYMIN
 PUSHJ P,PR2D
 OUTCHI 72
 IDIV R0,JFYSEC
 PUSHJ P,PR2D
 POPJ P,
PR2D: PUSH P,R1 ;SAVE R1
 IDIVI R0,12
 ADDI R0,60
 OUTCHR R0
 ADDI R1,60
 OUTCHR R1
 POP P,R0       ;RETURN R1 IN R0
 POPJ P,

; PRINT LICENSES FROM FDLIC
PRLIC: MOVE FDLIC
        SKIPE SW10      ;NOT APPLICABLE FOR 940 OR 370
        TRNN 777777
        POPJ P,
        OUTSTR [ASCIZ"
**** LICENSE "]
        MOVE [POINT 12,LIC-1,35]
        MOVEM TLIC#
        MOVE R3,[-12,,0] ;BITS 18-27 INDICATE 10 UNIQUE LICENSES
        MOVE R2,FDLIC
        ILDB R0,TLIC
        TRNE R2,400000
        PUSHJ P,PLIC
        LSH R2,1
        AOBJN R3,.-4
        LDB R1,[POINT 2,FDLIC,33]
        MOVE READL
        XCT ILIC(R1)
        PUSHJ P,PLIC
        LDB R1,[POINT 2,FDLIC,35]
        MOVE WRITL
        XCT ILIC(R1)
        PUSHJ P,PLIC
        OUTSTR [ASCIZ"IS NOT PORTABLE.
"]
        POPJ P,
PLIC:   ANDI 7777
        TRNN 7777
        POPJ P,
        LSHC R0,-6
        ADDI 40
        OUTCHR R0
        LSHC R0,6
        ANDI 77
        ADDI 40
        OUTCHR R0
        OUTCHI BL
        POPJ P,
LIC: SIXBIT /WCRCOPSYGDTDSTHFJLAC/
ILIC:   SETZ R0,
        LSH -30
        LSH -14
        TRN
READL: SIXBIT /RPRFRA/
WRITL: SIXBIT /WPWFWA/


PRTIME: MOVEI R1,1
 MOVEI R2,1
 MOVE BPARAM
 CAIN XMODE
 JRST [ MOVEI R1,^D9            ;TEN "BYTES"= 72 BITS = 9 BYTES
        MOVEI R2,^D10
        JRST PRTIM1]
 MOVE BYTESIZE
 CAIN 7
 JRST [ MOVEI R1,7              ;EIGHT "BYTES"= 56 BITS = 7 BYTES
        MOVEI R2,^D8
        JRST PRTIM1]
 CAIN 11
 JRST [ MOVEI R1,^D94812        ;BYTESIZE = LOG 192/LOG 2 = 7.5849625 BITS
        MOVEI R2,^D100000       ;SINCE THERE ARE 192 CHARS IN KATAKANA SET
        JRST PRTIM1]
PRTIM1: MOVE CCNT
 MUL R1                         ;PRODUCT TO R0-R1
 DIV R2
 CAIL R1,4
 AOS R0
 MOVEM RLCCNT                   ;REAL BYTE COUNT
 OUTSTR [ASCIZ"
"]
 MOVE RLCCNT
 PUSHJ P,DECOUT ;PRINT NO. OF CHARS TRANSMITTED
 OUTCHI BL
 OUTCHI LPAREN
 MOVE TRTOT
 PUSHJ P,DECOUT ;PRINT NETWORK CHARS TRANSMITTED
 OUTSTR [ASCIZ") CHARS. TRANSMITTED
"]
 MOVE TMODE
 CAIE 4
 SKIPN TYPTIM
 POPJ P,      ;NO RATES FOR UPDATE MODE
 MOVE ETIME
 SUB STIME
 IDIVI ^D1000   ;CONVERT MILISECOND TIME TO SECONDS
 CAIL R1,500
 ADDI 1         ;ROUND UP
 CAIG 0
 POPJ P,      ;NO DIVISION BY ZERO
 MOVEM TMP      ;SAVE TRANSMISSION TIME, SECONDS
 MOVE RLCCNT
 IDIV TMP
 CAIG 77        ;DON'T PRINT RATE IF LESS THAN 64
 POPJ P,      ;DON'T BOTHER PRINTING
 PUSHJ P,DECOUT
 OUTCHI BL
 OUTCHI LPAREN
 MOVE TRTOT
 IDIV TMP
 PUSHJ P,DECOUT
 OUTSTR [ASCIZ ") CHARS. PER SEC
"]
 POPJ P,

; PRINT CHARS. TRANSMITTED AND RATE
CKTIM: SKIPN VERBOSE
 JRST [ SKIPN TYPTIM
        POPJ P,
        JRST CKTIM1]
 OUTSTR [ASCIZ"
CHECKPOINT"]
 PUSHJ P,PRDAT
CKTIM1: MSTIME R1,
 MOVEM R1,ETIME
 PUSHJ P,PRTIME
 POPJ P,

; CHANGE FROM ASCII TO KATAKANA MODES FOR KANA FILES.
KKM: SKIPN URMODE       ;IS USER OVERRIDING
 SKIPE UTMODE       ;IS USER OVERRIDING
 POPJ P,                ;YES
 MOVE TMODE
 MOVE R1,SYSTYP
 TRNE R1,KATCAP
 CAIE 1
 POPJ P,        ;CANNOT DO IT
 MOVE R1,FDLIC  ;COPIED FROM RBLIC WHEN CONTROLLER IS DONOR
;                OR SENT BY PDP-10 DONOR  (SEE SPATH)
 SKIPN DONOR    ;ARE WE DONOR.
 SKIPE SW10     ;NO, IS SLAVE A PDP-10?
 JRST .+2
 POPJ P,        ;CANNOT SWITCH TO KATAKANA MODE
 TLNN R1,200    ;TEST KATAKANA BIT
 POPJ P,
 MOVEI 5
 MOVEM TMODE
 MOVEM RMODE
 POPJ P,

; CHANGE FROM BINARY TO BINARY UPDATE MODE.
UUM: MOVE TMODE
 MOVE R1,SYSTYP ;CHECK IF SLAVE CAN DO IT
 TRNE R1,UPDCAP
 CAIE 3
 POPJ P,
 SKIPE UPDNO    ;IS USER FORBIDDING IT
 POPJ P,        ;YES
 AOS TMODE
 AOS RMODE
 POPJ P,

YESNO:  TIMER OFF
        INCHWL R0
        PUSH P,R0               ;SAVE FIRST CHAR INPUT
        INCHWL R0
        CAIE R0,LF              ;LOOP TIL LINE FEED FOUND
        JRST .-2
        POP P,R0                ;GET FIRST CHAR INPUT
        CAIE R0,"N"+40
        CAIN R0,"N"
        POPJ P,0
        CAIE R0,"Y"+40
        CAIN R0,"Y"
        JRST CPOPJ1
        SKIPE CIN
        JRST YESN2
        OUTSTR [ASCIZ /
PLEASE TYPE 'Y' OR 'N': /]
        CLRBFI
        JRST YESNO

; NEW FILE SEND CODE.
NEWSEN: SETOM DONOR     ;WE ARE THE DONOR
 SETOM MASTER   ;AS WELL AS THE CONTROLLER
 PUSHJ P,SAVREG  ;SAVE PARSER REGISTER SET
 MOVE R1,[DBLK,,RBCNT]
 BLT R1,RBLIC
 MOVE RBLIC
 MOVEM FDLIC
 LNKMSG RCVCOD
 PUSHJ P,SPATH
 TRNN F,FILFND  ;ARE WE WRITING ON AN OLD FILE
 JRST NEWFILE   ;NO
;ASK IF O.K.
 SKIPN CIN      ;DON'T PRINT MESSAGE IF RUNNING FROM COMMANDS FILE
 SKIPE ASK
 JRST .+2
 OUTSTR [ASCIZ"
OKAY TO WRITE ON OLD FILE? "]
 SKIPE ASK
 JRST CHKO
 PUSHJ P,YESNO
 JRST ABORT
 SETZM TIMFLG   ;START HONORING TIMEOUTS
NEWF1: PUSHJ P,UUM    ;CHANGE TO UPDATE MODE
NEWFILE: PUSHJ P,KKM     ;CHANGE TO KATAKANA MODE
 PUSHJ P,TF     ;TRANSMIT FILE DATA
 JRST NEWR2     ;COPY FINISHED
CHKO: SKIPG ASK
 OUTSTR [ASCIZ"
REPLACING FILE "]
 SKIPL ASK
 OUTSTR [ASCIZ"
KEEPING FILE "]
 MOVEI R1,RBLK
 PUSHJ P,EROUN
 PUSHJ P,EROFN
 OUTSTR [ASCIZ"
"]
 SKIPL ASK
 JRST ABORT
 JRST NEWF1     ;REPLACE FILE

; NEW FILE RECEIVE CODE
NEWREC: SETZM DONOR     ;WE ARE THE RECEIVER
 SETOM MASTER   ;AS WELL AS THE CONTROLLER
 PUSHJ P,SAVREG ;SAVE PARSER REGISTER SET
 MOVE R1,[RBLK,,RBCNT]
 BLT R1,RBLIC
 LNKMSG DNRCOD
 PUSHJ P,SPATH
 TRNN F,FILFND   ;DID WE LOCATE THE FILE
 JRST FLKR      ;FILE NOT FOUND.  **CHANGE THIS IF ERROR MESSAGE
;               AT FLKR (ERR5) GETS TOO SCARY.
 SKIPN UPDSW    ;ARE WE REPLACING A FILE AT THIS END
 JRST NEWR1     ;NO, NEW FILE
; OLD FILE-- USE UPDATE MODE IF SLAVE CAN SEND IT
 PUSHJ P,UUM
NEWR1: PUSHJ P,KKM      ;SWITCH TO KATAKANA MODE WHEN POSSIBLE
 PUSHJ P,RF
NEWR2: PUSHJ P,COPFIN
 PUSHJ P,PRTIME
 JRST RESET             ;RETURN TO COMMAND PARSER.

 END
  