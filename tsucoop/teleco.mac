TITLE TELECOPY - THE CONTROLLER FOR PDP-10 TELECOPY

INTERN GD,RESET,ABORT,ESCRTN,PANIC,CH7RTN
;STORAGE REFERENCED ELSEWHERE:
INTERN RBLK,DBLK,LCOPT,SUP
;ROUTINES REFERENCED ELSEWHERE:
; USE WITH CAUTION.  ABT PRESERVES REGISTERS.  CLRCIR RESTORES REGISTERS
;       TO THE SET DEFINED IN THIS PACKAGE
ENTRY SAVREG,RESREG,ABT,CLRCIR,ZAPINT,YESN2

INTERN SYSLST, SYSBLK, SLVTYP
INTERN SIZDON, RETZAP, CROUT


JOBREN==124
LOCVER==137
TYMSPC==22
TYMVER==3
LOC LOCVER
XWD TYMSPC,TYMVER

;22.3 - Update map to define 60 as a X (new Paris 2020).  /DJB
;22.2 - Back out edit 22.1, update map to define 158 as a 370. /LFL
; 22.0 2/82 - Back out edits 17, 20 and 21 (holey files and 4 digit hosts).
;  Reimplement 4 digit hosts.  Fix many, many bugs. /DWS
;
; ** note ** edits 17 and 20 are not reflected in this source, but may
; be recovered from archives.

;16.0 2/80 - Added COMMANDS,HOST,GATEWAY, and PASSWORD commands
;Converted system # from two digits to three digits /J.R.
;12.6 8/17/76 - INPUT CHAR AND WAIT INSTEAD OF LOOPING
;3/1/76 - IGNORE LEADING SPACES IN FILE NAME, USER NAMES
;   FIX ERRONEOUS SYNC ERROR MESSAGE
;1/23/76 - ADD SYSTEM 42
;3/19/75 - NEW COMMAND FORMAT & 370 COPIES
;1/20/75 - MAKE ? = HELP

RELOC

;AC'S
R0=0
F=10	;FLAG WORD
A=1	;TEMPS ARE A-E
B=2
C=3
D=4
E=5
N=11
N1==12
SIDE=12	;INDEX INTO INFORMATION BLOCK
N2==13
AUX==13	;AUX. CHANNEL BEING USED NOW
N3==14
WD=14
OPTR=13	;POINTER TO WORD BEING INPUT
INDEX==15
BP==15
PTR=15
CH=16	;CHARACTER FOR TRANS. - DO NOT CHANGE
P=17	;DO NOT CHANGE

;I/O CHANNELS
DISK==1
COM==3 ;COMMANDS IN CHANNEL
OUT==4 ;COMMANDS OUT CHANNEL
;ASSEMBLY SWITCHES
TESTNG==1       ;ASSEMBLE CRASH CODE
;UUO SYMBOLS
FORTY==40
FORTY1==41
UUOFLG==1
;FLAGS IN RIGHT HALF OF F
F370==1		;WE ARE GOING TO OR FROM A 370
NOSTRT==2	;HAVEN'T STARTED CONVERSATION WITH SLAVE YET
STAROK==4	;ALLOW STAR ON INPUT (FOR USER NAME)
DOTOK==10	;ALLOW DOT ON INPUT (FOR USER NAME)
SLAOK==20	;ALLOW SLASHES ON INPUT (FOR USER NAME)
SPOK==40	;ALLOW SPACES ON INPUT FOR USERNAME
NAMFLG==SPOK!STAROK!DOTOK!SLAOK
CANAGN==100	;ON IF SYSTEM ONLY IS ALLOWED TO BE INPUF
OLDFORM==200	;ON IF SO FAR COMMAND LOOKS LIKE THE OLD FORM
ALLNUM==400	;ON IF ALL CHARS INPUT SO FAR ARE DIGITS
MOREIN==1000	;ON IF WE HAVE NOT EXHAUSTED STRING
NOTOFIL==2000	;ON IF THERE ARE NO "TO" FILES
HAS2BOLD==4000	;ON IF INPUT MUST IN IN OLD FORMAT
SPCDEL==10000	;ALLOW COMMA, CARRET, LPAREN OR COLON ONLY TO BE DELIM.
SKPCOM==20000	;SKIP LEADING COMMA
NEWSYS==40000	;REPEAT PREVIOUS COMMAND FOR NEW SYSTEM
NEEDCT==100000	;LOOKING FOR CONTROL
NONSPC==200000	;ON IF FOUND A NON-SPACE CHARACTER

;FLAGS IN LEFT HALF OF F
NOSPACE==1	;NO SPACES FOUND YET
MORETR==2		;THERE ARE MORE FILES TO TRANSFER

;SOME MAX'S
SIZLIN==^D72
MAXFIL==SIZLIN/5+1	;NO. OF WORDS FOR A LOONG 940 FILE NAME
MAXCHR==^D300		;MAXIMUM NUMBER OF CHARS. TO BE INPUT IN A LINE
MAXWDS==MAXCHR/5	;MAXIMUM NUMBER OF WORDS NEEDED IN  BUFFER

;CHARACTER DEFINITIONS
BELL==7
HT==11
LNFEED==12
CARRET==15
SPACE==40
PCT==45
LPAREN==50
RPAREN==51
STAR==52
COMMA==54
DOT==56
dash==55
SLASH==57
ZEROCH==60
NINECH==71
COLON==72
SEMICOL==73
EQUALS==75
QMARK==77
ATSIGN==100
ALPHA==101
PHI==106
UPSILON==126
ZETA==132
CONTRL==252		;CONTROL CHARACTER
PROMPT==276		;PROMPT FROM 370
;GETTAB DEF'S
LICENSE==20	;TO GET USER'S LICENSE
JBTUNM==-22
JBTUN1==-21

;INTERRUPT CHANNEL ASSIGNMENTS
DISABLE== 377777 ;DISABLE ALL CHANNELS
ENABLE== 400000 ;ENABLE INTERRUPTS
CH1== 200000    ;CHANNEL 1.  LOSS OF AUX CIRCUIT
CH2== 100000    ;CHANNEL 2.  TIMER.
CH3== 040000    ;CHANNEL 3.  BUFFER ZAP ON AUX CIRCUIT
CH4== 020000    ;CHANNEL 4.  ALTMODE TYPED ON COMMAND PORT
CH5== 010000    ;CHANNEL 5.  CHAR PRESENT ON AUXILLIARY CIRCUIT.
CH6== 004000    ;CHANNEL 6.  BREAK CHAR PRESENT ON COMMAND PORT.CUIT
;VALUES FOR SLVTYP
IX==1		;940
SUMEX==2	;SUMEX SYSTEM
X==3		;PDP-10
X370==4		;370


;CONTROL INFORMATION (PRECEEDED BY CONTRL)
STAGN==107		;START AGAIN
ONLC==122
OFFLC==123
ONSUP==125
OFFSUP==126

;AUXCAL OPERATIONS
AUXIN==0
AUXINS==1
AUXOUT==4
AUXPTR==5
LVDEF==11
AUXIMG==13
BRKLIN==50              ;BREAK CHAR PRESENT IN INPUT LINE

;INDEXES INTO DBLK, RBLK
FSIZE==-1
PPN==1
FILE==2
EXT==3
ERR==3
SIZE==5
UNAME==15
SYS==17
FNAME==20
;MISC
ASCNUM==60		;ASCIZ TO NUMERIC
ASCSIX==40		;ASCIZ TO SIXBIT
SIXNUM==20		;SIXBIT TO NUMERIC
SIZUSR==^D12		;12 CHARACTER USER NAME
SIZFIL==6
SIZEXT==3
START:	JRST STPT2       ;NORMAL PROGRAM ENTRY

	LOC	JOBREN
	REENTER
	RELOC

REENTER:
	RESET
        SETZ	WD,
havnam: MOVE	P,PDLPTR
        SETZM	CIN##		;NO COMMANDS FILE
        SETZM	COUT##		;NO COMMANDS OUT FILE
        SETZM	DETACH##	;DO NOT DETACH
        INIT    COM,0
        SIXBIT  /DSK/
        0,,INBF
	 EXIT
        jumpn   wd,skpnam               ;Already have a name for commands file
        PJOB	R0,
        MOVE	B,WDPTR
        MOVEI	A,20
        CAIG	R0,^D100-1
         IDPB	A,B
        CAIG	R0,^D10-1
         IDPB	A,B
        PUSHJ	P,MAKJOB
        HRRI	WD,(SIXBIT /TEL/)
skpnam: MOVEM	WD,INFNAM
        MOVEM	WD,OUTNAM
        HRLZI	WD,(SIXBIT /TMP/)
        MOVEM	WD,INFEXT
        LOOKUP  COM,INFIL
         JRST	LKERR
STPT1:  SETOM	CIN
        INBUF	COM,1
        skipn   noinit#
         JRST	STPT3
        jrst    getcmd

MAKJOB: IDIVI	R0,12
        HRLM	A,(P)
        SKIPE	R0
         PUSHJ	P,MAKJOB
        HLRZ	R0,(P)
        ADDI	R0,20
        IDPB	R0,B
        POPJ	P,

ENTERR: OUTSTR	[ASCIZ/
UNABLE TO ENTER COMMANDS OUT FILE
/]
	EXIT

LKERR:	HRLZI	WD,(SIXBIT /COM/)
	MOVEM	WD,INFEXT
	LOOKUP	COM,INFIL
	 JRST	[SKIPN NOINIT	; program already initialized?
		  JRST STPT2	; treat like normal startup
		 OUTSTR [ASCIZ/
NO COMMANDS FILE FOUND
/]
		 JRST GETCMD]	; get the next command

        INIT    OUT,0
        SIXBIT  /DSK/
        OUTBF,,0
         JRST	ENTERR
        ENTER   OUT,OUTFIL
         JRST	ENTERR
        OUTBUF  OUT,1
        SETOM	COUT
	HRROI	A,52
	AUXCAL	A,[ASCIZ/
DETACHING .../]
        SETO	A,
        ATTACH  A,
         JFCL
        SETOM	DETACH##
        JRST	STPT1

; NORMAL PROGRAM ENTRY

STPT2:  RESET
        MOVE	P,PDLPTR
STPT3:  SETZ	F,
        PUSHJ	P,INITEL
RESET:
GETCMD:	PUSHJ	P,RESREG	;RESTORE ALL REGISTERS MODIFIED BY "NEW" CODE
        HRLOI	A,DISABLE
        INTENB	A,		;DISABLE ALL INTERRUPT CHANNELS
         JFCL
        MOVE	P,PDLPTR
        SETOM	TALKSW		;NOT IN TALK COMMAND
        setzm   pwdcmd#		;Password command default
	SETOM	TIMFLG##	;NO. OF CHARS. IN SINCE LAST TIME-OUT
				;-1 IF TIME-OUTS SHOULD BE IGNORED
	SETZM	LSTIME#
        CLOSE	DISK,40		;CLOSE WITHOUT WRITING IF OPEN
        MOVEI	A,ALTTYP
        MOVEM	A,ESCRTN
        MOVSI	A,ENABLE+CH1+CH2+CH3+CH4
        INTENB	A,		;ENABLE INTERRUPT CHANNELS
         JFCL
	TLNE	F,MORETR	;MORE FILES TO COPY?
	 JRST	GETNXT		;YES - GO PROCESS IT
	SETZM	ZERO
	MOVE	A,[XWD ZERO,ZERO+1]
	BLT	A,ZEREND
	TRZ	F,HAS2BOLD+OLDFORM+NOTOFIL
	MOVEI	SIDE,DBLK
	OUTSTR	[ASCIZ /
: /]
	JRST GETC
SUBTTL UUO DISPATCHER

IFDEF UUOFLG<UUOTAB: BLOCK 0>
DEFINE  OPD     (A,B) <U==U+1
OPDEF A [U*1000000000]
IFDEF UUOFLG<JRST B>>

	U==0

	OPD OUTSTR,OSTRUU
	OPD INCHWL,INCHUU
	OPD OUTCHR,OCHRUU
	OPD OUTCHI,OCHIUU
	OPD CLRBFO,CLROUU
	OPD CLRBFI,CLRIUU

	LOC	FORTY1
	PUSHJ	P,UUO
	RELOC

UUO:    EXCH    R0,STKCNT#      ;EXCHANGE R0 AND STACK CONTENTS OF UUO
        HRRZS   (P)             ;SET ALL FLAGS TO ZERO
        EXCH    P,STKPTR        ;EXCHANGE OLD AND NEW STACK POINTERS
        CAMGE   P,STKPTR#       ;SKIP IF LOWER OR SAME LEVEL UUO CALL
        CAME    R0,(P)          ;SKIP IF NOT A NEW UUO CALL
        MOVE    P,STKPTR        ;SAVE NEW STACK POINTER
        CAMN    P,STKPTR        ;SKIP IF NOT A NEW UUO CALL
	 MOVE	R0,(P)          ;SAVE CURRENT CONTENTS OF RETURN CELL
        EXCH    P,STKPTR        ;RE-EXCHANGE P AND STACK POINTER
        EXCH    R0,STKCNT       ;RE-EXCHANGE R0 AND STACK CONTENTS
        PUSH    P,R0
        LDB     R0,[POINT 9,FORTY,8]
        CAILE   R0,U            ;SKIP IF UUO NOT TOO BIG
	 JRST   UUOERR          ;GO TO ERROR ROUTINE
        ADDI    R0,UUOTAB-1
        JRST    @R0
UUOERR: SETZM   .               ;EXECUTE THIS ROUTINE ONLY ONCE
        OUTSTR  [ASCIZ "ATTEMPT TO EXECUTE AN ILLEGAL UUO"]

; OUTPUT STRING

OSTRUU:	PUSH	P,A
	HRRZ	A,FORTY
	HRLI	A,10700
	SUBI	A,1
TYP1:	ILDB	R0,A
	CAIN	R0,0
	 JRST	UUORE1
	SKIPE	COUT
	 PUSHJ	P,PUT
	SKIPN	COUT
	 TTCALL	1,R0    ;OUTCHR
	JRST	TYP1
UUORE1:	POP	P,A
UUORET: POP	P,R0
	POPJ	P,

OCHIUU: PUSH	P,A
	HRRZ	R0,FORTY
OCHI1:	SKIPE	COUT
	 PUSHJ	P,PUT
	SKIPN	COUT
	 TTCALL	1,R0    ;OUTCHR
	JRST	UUORE1

OCHRUU: PUSH	P,A
	HRRZ	A,FORTY
	HRRZ	R0,(A)
	TRNN	A,-2
	 XCT	OCHI2(A)
	JRST	OCHI1
OCHI2:	HRRZ	R0,-1(P) ;REG 0
	HRRZ	R0,(P)     ;REG 1

INCHUU: PUSH	P,A
	SKIPE	CIN
	 PUSHJ	P,GET
	SKIPN	CIN
	 TTCALL	4,R0    ;INCHWL
	HRRZ	A,FORTY ;CONTAINS EFFECTIVE ADDR
	TRNE	A,-2
	 JRST	[MOVEM R0,(A)
		 JRST UUORE1]
	XCT	INCHU1(A)
	JRST	UUORE1
INCHU1: MOVEM	R0,-1(P)	;REG 0
	MOVEM	R0,(P)		;REG 1

CLROUU: SKIPN	COUT
	TTCALL	12,
	JRST UUORET

CLRIUU: SKIPN	CIN
	TTCALL	11,
	JRST	UUORET

GET:    SOSGE   INBF+2          ;IS BUFFER EMPTY?
	 JRST	GETBUF          ;IF SO, GO TO GETBUF
        ILDB    R0,INBF+1        ;GET BYTE FROM BUFFER
        CAIG	R0,0
         JRST	GET             ;SKIP NULS
        POPJ    P,              ;RETURN TO CALLING POINT
GETBUF: INPUT   COM,            ;GET BUFFER FROM MONITOR
        STATZ   COM,740000      ;ERROR WHILE READING FILE?
         JRST   RDERR           ;IF SO, GO TO RDERR
        STATZ   COM,20000       ;END OF FILE?
         JRST   ENDIT          ;IF SO, GO TO ENDIT
        JRST    GET             ;ELSE, GO TO GET

PUT:    SOSG    OUTBF+2         ;IS BUFFER FULL?
         JRST   PUTBUF          ;IF SO, GO TO PUTBUF
PUTBYT: IDPB    R0,OUTBF+1      ;PUT BYTE IN BUFFER
        POPJ    P,              ;RETURN TO CALLING POINT
PUTBUF: OUTPUT  OUT,            ;GIVE BUFFER TO MONITOR
        STATZ   OUT,740000      ;ERROR WHILE WRITING FILE?
         JRST   WTERR           ;IF SO, GO TO WTERR
        JRST    PUTBYT          ;ELSE, GO TO PUTBYT

RDERR:  OUTSTR	[ASCIZ/
ERROR READING COMMANDS FILE
/]
	JRST	ENDIT

WTERR:  OUTSTR	[ASCIZ/
ERROR WRITING COMMANDS OUT FILE
/]
	JRST ENDIT
SUBTTL COMMAND DISPATCHER

CHKCMD:	MOVE	N,[XWD -CMDSIZ,CMDTAB]
	MOVEI	N1,CMDISP
GOCMD:	SETO	B,		;MASKSWD
	MOVE	A,WD		;WORD WE JUST INPUT
GETMSK:	JUMPE	A,GOTMSK	;WE HAVE MASK ALL SET UP
	LSH	A,6
	LSH	B,-6
	JRST	GETMSK
GOTMSK:	MOVE	A,N
NXTENT:	JUMPL	B,TRYNXT
	MOVE	C,0(A)
	ANDCM	C,B
	CAMN	WD,C
	 JRST	GOTMAT
TRYNXT:	AOBJN	A,NXTENT
;[22]	OUTSTR	[ASCIZ/
;[22]?
;[22]/]
	XCT	SYNERR		;[22] Try a better error message
	CLRBFI
	JRST	GETCMD

GOTMAT:	HRRZS	A,A
	HRRZS	N,N
	SUB	A,N
	ADD	N1,A
	MOVE	N,@N1
	JRST	@N
SUBTTL - COPY COMMAND

RUNROU:	OUTSTR	[ASCIZ/
COPY FROM FILE: /]
	TRO	F,HAS2BOLD
	JRST	GETC

GETOLD:	TRZ	F,MOREIN
	TRNE	F,HAS2BOLD
	 OUTSTR	[ASCIZ/
COPY TO FILE: /]
	MOVEI	SIDE,RBLK
	MOVEI	SIDE,RBLK
	MOVEI	N,SIZFIL
	MOVE	OPTR,WDPTR
	PUSHJ	P,GETCHS
	CAIE	CH,LPAREN
	 JRST	SYNERR
	PUSHJ	P,GTNSYS
	PUSHJ	P,GTFNM
	CAIE	CH,CARRET
	 JRST SYNERR
	JRST RUNIT

;CHECK TO MAKE SURE WE HAVE ALL VALUES

RUNIT:	MOVE	A,THISYS
	CAME	A,DBLK+SYS
	 CAMN	A,RBLK+SYS
	  SKIPA
	JRST	CIR2		;NEITHER DONOR OR RECIP. IS THIS SYSTEM
	MOVE	A,DBLK+SYS
	CAMN	A,RBLK+SYS
	 camn	a,hstsys        ;If 2 networks are involved, the system #'s can
	  skipa                 ;possibly be the same; otherwise,
	JRST	CIR0		;THE DONOR AND RECIP CANNOT BE THE SAME
	MOVE	B,NXTSYS#
	TRNE	F,NEWSYS
	 MOVEM	B,RBLK+SYS	;UPDATE "TO" SYSTEM
        skipn   strsys
	 CAME	A,THISYS
	JRST RECIP
SUBTTL DONOR ROUTINE

DONOR:	TRO	F,CANAGN	;ALLOW USER TO REPEAT COMMAND FOR A NEW SYS
        MOVEI	SIDE,RBLK	;TALK ABOUT RECEIVER
        PUSHJ	P,PREP		;BUILD CIRCUIT TO HIM
	MOVEI	A,14
	MOVEM	A,DBLK
	MOVEI	SIDE,DBLK       ;TALK ABOUT DONOR
	MOVEI	A,DBLK+UNAME
	MOVEM	A,DBLK+PPN
	LOOKUP	DISK,DBLK
	 JRST	CHKERR
        SKIPE	NEWSW		;ARE WE USING NEW TRANSMISSION LOGIC
         JRST	NEWSEN##	;YES
        JRST	RECIP2
SUBTTL RECIPIENT ROUTINE

RECIP:	TRZ	F,CANAGN	;DON'T ALLOW USER TO REPEAT COMMAND
        MOVEI	SIDE,DBLK	;TALK ABOUT DONOR
        PUSHJ	P,PREP		;BUILD CIRCUIT TO HIM
	MOVEI	A,14
	MOVEM	A,RBLK
	MOVEI	SIDE,RBLK	;TALK ABOUT RECEIVER
	MOVEI	A,RBLK+UNAME
	MOVEM	A,RBLK+PPN
	LOOKUP	DISK,RBLK
	 JRST	[HRRZ A,RBLK+ERR
		 JUMPN A,NOOPN
                 SETZM RBLK+SIZE
		 JRST RECIP1]
	TRO	F,NOSTRT
	PUSHJ	P,CHKOLD
        SETOM	UPDSW		;REPLACING OLD FILE
RECIP1: SKIPE	NEWSW		;ARE WE USING NEW TRANSMISSION LOGIC
         JRST	NEWREC##	;YES
RECIP2: CLOSE	DISK,
        OUTSTR	[ASCIZ"
CANNOT SYNCHRONIZE WITH OLD-STYLE SLAVE--ABORT
"]
        JRST	ABORT
NOOPN:	PUSHJ	P,ABT ;TELL DONOR TO FORGET IT
	JRST	CHKERR
SUBTTL - OTHER COMMANDS

VERSIO:	OUTSTR	[ASCIZ/
VERSION /]
	HLRZ	A,LOCVER
	PUSHJ	P,OUTOCT
	OUTCHI	DOT
	HRRZ	A,LOCVER
	PUSHJ	P,OUTOCT
	OUTSTR	[ASCIZ/
/]
	JRST	GETCMD

HELP:	OUTSTR	HELMSG
	SKIPE	TYMBIT
	 OUTSTR TYMHELP
	JRST	RESETIT

;USER WANTS ALL INSTRUCTIONS

INSTRU:	TRNE	F,MOREIN
	 JRST	SYNERR
	OUTSTR 	INSMSG
	SKIPE	TYMBIT#		;USER'S TYMSHARE BIT
	 OUTSTR	TYMINS
	JRST	GETCMD

;SPECIAL 370 INSTRUCTIONS

SYS370:	OUTSTR	MSG370
	JRST	GETCMD

;SPECIAL 940 INSTRUCTIONS

SYS940:	OUTSTR	MSG940
	JRST	GETCMD

;TYMSHARE USER WANTS TO TURN ON TIME FLAG

SETIME:	TRNE	F,MOREIN
	 JRST	SYNERR
	SKIPN	TYMBIT
	 JRST	TRYNXT+1
	MOVEI	A,1
	MOVEM	A,TYPTIM##
	JRST	GETCMD

;AN ALT-MODE WAS TYPED - RETURN TO COMMAND LEVEL

PANIC:	PUSHJ	P,ZAPIT		;LOSE CIRCUIT.  ZAPIT DOES RESREG
ALTTYP: PUSHJ	P,RESREG	;RESTORE REGISTERS
        SETOM	TIMFLG
        HRLOI	A,177777	;DISABLE ALL CHANNELS EXCEPT CIRCUIT ZAP
        INTENB	A,
         JFCL
	CLRBFI			;CLEAR INPUT BUFFER
        CLRBFO			;CLEAR OUTPUT BUFFER
	SKIPN	AUXPRT
	 JRST	ALT1
        HRLZ	A,AUXPRT
        HRRI	A,CLRBO##
;       AUXCAL	A,      	 ;** SEND GOBBLER WHEN BASE BUG FIXED  7/21/77
	PUSHJ	P,CLRIT		;TELL SLAVE WE ARE THRU AND CLEAR ALL I/O
ALT1:	MOVEI	A,GETCMD
	MOVEM	A,ESCRTN-1
	DISMIS
;SET AND RE-SET LC
RESTLC:	SKIPA	A,[OFFLC]
SETLC:	 MOVEI	A,ONLC
	TRNE	F,MOREIN
	 JRST	SYNERR
	MOVEM	A,LC
	JRST	GETCMD

;USER HAS HAD ENUF
ENDIT:	PUSHJ	P,ZAPIT		;TRY TO GET RID OF CIRCUIT
        SKIPE	AUXPRT		;DID WE LOSE IT
         PUSHJ	P,CONFIRM	;MAKE USER CONFIRM SENDING ZAPPER
        HRLOI	A,DISABLE
        INTENB	A,
         JFCL
	SETO	A,
	ZAPCIR	A,
	RELEASE	DISK,
        SKIPE	COUT
	 CLOSE	OUT,
	SKIPE	CIN
	 CLOSE	COM,
	RESET
	SKIPN	DETACH
	 EXIT	1,
        MOVE    A,[1,,RUNBLK]
        RUN     A,
	 JFCL
        EXIT

RUNBLK: SIXBIT  /SYS/
        SIXBIT  /LOGOUT/
        BLOCK 4

CONFIRM:SKIPE	CIN      ;ASSUME YES IF RUNNING FROM COMMANDS FILE
	 JRST	CONF1
	OUTSTR	[ASCIZ"
EXIT, OK? "]
	PUSHJ P,YESNO
	 JRST DEADCR	;COMMAND DISPATCH WILL CLEAN UP STACK POINTER
CONF1:	OUTSTR [ASCIZ"
BE SURE TO CHECK SYSTEM "]
	MOVE	A,AUXCIR
	PUSHJ	P,OUTDEC
	OUTSTR [ASCIZ" FOR DETACHED JOB
"]
	POPJ P,
NEGATE:	MOVEI N,6
	MOVE OPTR,WDPTR
	PUSHJ P,GETCHS
	MOVE N,[XWD -NEGSIZ,NEGTAB]
	MOVEI N1,NEGDIS
	JRST GOCMD

NOSUP:	SKIPA A,[OFFSUP]
SUPRES:	MOVEI A,ONSUP
	TRNE F,MOREIN
	JRST SYNERR
	MOVEM A,SUP
	JRST GETCMD

NOUPDA: SKIPA A,[-1]
UPDATE: SETZ A,
        MOVEM A,UPDNO##
        JRST NOVER1

VERBO: SKIPA A,[-1]
NOVER: SETZ A,
        MOVEM A,VERBOSE##
NOVER1: TRNE F,MOREIN
        JRST SYNERR
        JRST GETCMD
NOTRU:	TRNE F,MOREIN
	SETZ N,
	JRST SETTRU

MAXTRU==^D255
MINTRU==2

TRUNCA:	TRNN F,MOREIN
	JRST ASKTRU
TRUNC1:	PUSHJ P,NUMIN
	CAIE CH,CARRET
	JRST SYNERR
	CAIG N,MAXTRU
	CAIGE N,MINTRU
	JRST ASKTRU
SETTRU:	MOVEM N,TRU
	JRST GETCMD

ASKTRU:	TRZ F,MOREIN
	OUTSTR [ASCIZ /
TRUNCATE STARTING WITH COLUMN: /]
	JRST TRUNC1


; ASK,REPLACE,KEEP
ASKIT: SETZM ASK##
 JRST GETCMD
KEEP: SKIPA A,[1]
REPLACE: SETO A,
 MOVEM A,ASK
 JRST GETCMD

; MODE COMMAND.
SCRAT: SETZM UCOPT
 MOVE A,[UCOPT,,UTPARAM]
 BLT A,URBLOCK##
        OUTSTR [ASCIZ"
COMPRESSION AND MODE SETTING CLEARED--RESUMING NORMAL DEFAULTS
"]
        JRST GETCMD
MODE: MOVEI A,UTPARAM##
 MOVEM A,T1##
 MOVEI A,URPARAM##
 MOVEM A,T2##
 TRNN F,MOREIN
 JRST SYNERR
 MOVE OPTR,WDPTR
 MOVEI N,6
 PUSHJ P,GETCHS         ;READ MODE TYPE
 MOVE N,[-MODSZ,,MODT]
 MOVEI N1,MODTB1
 JRST GOCMD

MODT: SIXBIT /TRANSM/
 SIXBIT /RECEIV/
MODT1: SIXBIT /ASCII/
 SIXBIT /BINARY/
 SIXBIT /SERIAL/
 SIXBIT /EBCDIC/
 SIXBIT /KATAKA/
 SIXBIT /IMAGE/
 SIXBIT /NATIVE/
MODSZ==.-MODT
MODSZ1==.-MODT1

MODTB1: EXP TRMOD,REMOD
MODTB2: EXP ASCMOD,BIMOD,SEMOD,EBCMOD,KATAKANA,IMAGE,NAMOD

BIOT: SIXBIT /BLOCKI/
 SIXBIT /BYTESI/
 SIXBIT /FILL/
BIOSIZ==.-BIOT

BIOT1: EXP BIM3,BIM4,BIM5

;TRANS.  TURN T2 OFF
TRMOD: MOVEI A,SCRATCH##
 MOVEM A,T2
 JRST REMOD1

;REC.  TURN T1 OFF
REMOD: MOVEI A,SCRATCH
 MOVEM A,T1
REMOD1: TRNN F,MOREIN
 JRST SYNERR
 MOVEI N,6
 MOVE OPTR,WDPTR
 PUSHJ P,GETCHS
 MOVE N,[-MODSZ1,,MODT1]
 MOVEI N1,MODTB2
 JRST GOCMD

;BINARY MODE.  READ FILL, BYTESIZE, AND BLOCKING OPTIONS
BIMOD: TRNN F,MOREIN    ;ANY MORE ON COMMAND LINE
 JRST SEMOD             ;NO, DEFAULT TO SERIAL MODE
; SET DEFAULT FOR 940 (MOST LIKELY TARGET) FILL=LEFT, BLOCKING=3, BYTESIZE=8
 MOVEI A,IXMODE##       ;111+0010+1
 MOVEM A,T##            ;SAVE TEMPORARILY
BIM1: TRNE F,MOREIN     ;ANY MORE LEFT
 JRST BIM2              ;YES
 MOVEI B,3              ;MODE = 3
 MOVE A,T
 JRST MOD6
BIM2: MOVEI N,6
 MOVE OPTR,WDPTR
 PUSHJ P,GETCHS         ;READ KEYWORD
 TRNE F,MOREIN
 CAIE CH,EQUALS
 JRST SYNERR
 MOVE N,[-BIOSIZ,,BIOT]
 MOVEI N1,BIOT1
 JRST GOCMD

;BLOCKING
BIM3: PUSHJ P,NUMIN
 CAIG N,16
 CAIG N,0
 JRST SYNERR
 SUBI N,1
 DPB N,[POINT 4,T,34]
 JRST BIM1

;BYTESIZE
BIM4: PUSHJ P,NUMIN
 CAIG N,10
 CAIG N,0
 JRST SYNERR
 SUBI N,1
 DPB N,[POINT 3,T,30]
 JRST BIM1

;FILL
BIM5: MOVEI N,6
 MOVE OPTR,WDPTR
 PUSHJ P,GETCHS
 MOVE N,[-BIM5S,,BIM5T]
 MOVEI N1,BIM5T1
 JRST GOCMD
BIM5T: SIXBIT /LEFT/
 SIXBIT /RIGHT/
BIM5S==.-BIBIM5T1: EXP BIM5L,BIM5R
BIM5L: SKIPA B,[1]
BIM5R: SETZ B,
 DPB B,[POINT 1,T,35]
 JRST BIM1

;EBCDIC MODE
EBCMOD: SKIPA B,[2]
;ASCII MODE
ASCMOD: MOVEI B,1
 MOVEI A,AMODE##

;STORE MODE AND PARAMETERS
MOD6: TRNE F,MOREIN
 JRST SYNERR
 MOVE N,T1
 MOVEM A,(N)
 MOVEM B,1(N)
 MOVE N,T2
 MOVEM A,(N)
 MOVEM B,1(N)
 JRST GETCMD

;KATAKANA
KATAKANA: MOVEI B,5
 MOVEI A,KMODE##
 JRST MOD6

;SERIAL MODE
SEMOD: SKIPA B,[3]
;IMAGE MODE
IMAGE: MOVEI B,6
 MOVEI A,SMODE##
 JRST MOD6

NAMOD: MOVEI A,XMODE
 MOVEI B,3
 JRST MOD6

;COMPRESS COMMAND
COMPRESS: TRNN F,MOREIN
 JRST SYNERR
 MOVE OPTR,WDPTR
 MOVEI N,6
 PUSHJ P,GETCHS
 MOVE N,[-COMSZ,,COMT]
 MOVEI N1,COMT1
 JRST GOCMD

COMT: SIXBIT /OFF/
 SIXBIT /DUPLIC/
 SIXBIT /RANK/
COMSZ==.-COMT

COMT1: EXP COMT2,COMT3,COMT5

COMT2: SKIPA A,[NONE##]
COMT3: MOVEI A,1
COMT4: TRNE F,MOREIN
 JRST SYNERR
 MOVEM A,UCOPT##
 JRST GETCMD

COMT5: MOVEI A,2
 TRNN F,MOREIN
 JRST COMT4
 MOVEI N,6
 MOVE OPTR,WDPTR
 PUSHJ P,GETCHS
 MOVE N,[-1,,COMT5A]
 MOVEI N1,COMT5B
 JRST GOCMD
COMT5A: SIXBIT /AUTO/
 COMT5B: EXP .+1
 MOVEI A,3
 JRST COMT4

;WHY COMMAND
WHY: TRNE F,MOREIN
 JRST SYNERR
 SKIPE PGMSW##    ;DO WE HAVE A PROGRAM
 SKIPN NEWSW##    ;YES, BUT IS IT NEW VARIETY
 JRST GETCMD
 SETZM TIMFLG   ;ENABLE TIMEOUT
 MOVEI CH,WHYCOD##
 PUSHJ P,LNKMSG
 MOVE A,AUXGET
WHY1: PUSHJ P,GAB
 CAIE CH,CAN##
 JRST WHY2
 PUSHJ P,GAB
 JRST WHY1
WHY2: CAIE CH,EM
 JRST WHY1
 PUSHJ P,GAB
 CAIE CH,RSPCOD##
 JRST GETCMD    ;GETCMD TO SET TIMFLG=-1
WHY3: PUSHJ P,GAB
 CAIG CH,0
 JRST ABORT
 OUTCHR CH
 JRST WHY3

; SAVE REGISTERS (EXCEPT P)
SAVREG: MOVEM P,SAV+17
 HRRZI P,SAV
 BLT P,SAV+16
 SETZ R0,
 MOVEI P,A      ;R1
 BLT P,16     ;ZERO REGISTERS 1-16
 MOVE P,SAV+17
 SETOM SAVFG
 POPJ P,

; RESTORE REGISTERS
RESREG: AOSE SAVFG
 POPJ P,
 MOVEM P,SAV+17
 HRLZI P,SAV
 BLT P,16
 MOVE P,SAV+17
 POPJ P,

SAVFG: 0
SAV: BLOCK 20

; GET ASCII BYTE (IMAGE CHAR WITHOUT PARITY)
;       A MUST CONTAIN AUXGET
GAB: AUXCAL A,CH
 JRST .-1
 ANDI CH,177
 POPJ P,

;SEND LINK MESSAGE.  DUPLICATED FROM NEW MODULES
;       CH CONTAINS MESSAGE TYPE
LNKMSG: MOVE A,AUXPUT
 AUXCAL A,EM##
 AUXCAL A,0(CH)
 TRNE F,F370
 JRST [ AUXCAL A,HT
        POPJ P,]
 HRRI A,YELLOW##
 AUXCAL A,
 POPJ P,

;OPTION TO CAUSE 370 TO CREATE FIXED-LENGTH RECORDS
FIX1: MOVEI N,^D80
 PUSHJ P,VAR2
 MOVEM N,VAROPT##
 SETOM FIXOPT##
 SETOM FIX##
 JRST GETCMD

;OPTION TO CAUSE 370 TO CREATE VARIABLE LENGTH RECORDS (DEFAULT)
VAR1: MOVEI N,^D65536
 PUSHJ P,VAR2
 MOVEM N,FIXOPT
 SETZM VAROPT
 SETZM FIX
 JRST GETCMD

VAR2: TRNN F,MOREIN
 JRST VAR3
 PUSHJ P,NUMIN
 CAIG N,^D65536
 CAIG N,0
 JRST SYNERR
VAR3: MOVEM N,VRECL##
 MOVEM N,FRECL##
 POPJ P,


; Password command.  This command is used with the Gateway command.
;This command saves the password input through the tty for later.

PASSWO: pushj   p,clrpas                ;Clear the password
        move    ptr,pasptr              ;Points to the block for the password
        hrroi   a,64                    ;Want to turn on the no echo
        auxcal  a,200                   ;No echo
inpas:  inchwl  ch                      ;Now get the password from the tty
        cain    ch,carret               ;Check for the end of the password
          jrst  [inchwl ch              ;Get the line feed character
                 hrroi  a,64            ;Turn on the echo
                 auxcal a,0             
                 skipe  pwdcmd          ;Is the password retrieved independent
                   popj p,              ;of a command? If so, return to origin
                 jrst   getcmd]         ;Otherwise, get the next command
        idpb    ch,ptr                  ;Save the password
        jrst    inpas                   ;Get the next character

;Clear the data block that stores the password.

Clrpas: setzm   passwd                  ;Set the first word to zero
        move    a,[xwd passwd,passwd+1] ;Get the moving from and to address
        blt     a,pwdend                ;and zero out the block
        popj    p,                      ;End of clear

;The password will be put into the login string for logging into another
;network.

getpas: move    optr,pasptr             ;Point to the password
gp:     ildb    ch,optr                 ;Get a character from the password
        cain    ch,0                    ;Is it the end of the password?
          jrst  clrpas                  ;Yes, then clear password & you're done
        idpb    ch,bp                   ;Attach it to the login string
        jrst    gp                      ;Get the next character


;The Command command is to let Telecopy know that the rest of the commands
;will be coming from a commands file.

COMMAN: setom   noinit#                 ;Do not want to re-init the program
        cain    ch,carret               ;Is the commands filename given?
          jrst  reenter+1               ;Begin Telecopy over as if it had a 
                                        ;commands file
        tro     f,namflg                ;Prepare to retrieve the filename
        movei   n,6                     ;We want only the first six characters
        move    optr,cfnptr             ;Put the commands filename into comfil
        trz     f,dotok                 ;Period needed for extension
        pushj   p,getchs                ;Get the filename
        move    wd,comfil               ;Want the filename stored in wd
        caie    ch,carret               ;Is there an extension given?
         pushj  p,getchs                ;Yes, don't leave it lying around
        jrst    havnam                  ;Begin executing from the commands file


;The Gateway command is used when interfacing Telecopy with another network.

GATEWA: 
;Get the username for logging into the host on the other network,
;and save it in auxstr.

        pushj   p,sysnum                ;Get the system # of the gateway
        movem   n,sysno                 ;Save it
        caie    ch,semicol              ;Is the next character a semicolon?
          jrst  synerr                  ;If not, bad input
        movei   n,sizusr                ;Username may be up to 14 ascii ch's
        tro     f,namflg                ;We will be looking for a name
        move    optr,usrptr             ;The name will be stored in usrnam
        pushj   p,getchs                ;Get the name
        caie    ch,colon                ;Is the next character a colon?
          jrst  synerr                  ;If not, bad input
        pushj   p,sysnum                ;Get the system # of the host
        skipn   passwd                  ;If there is already a password, skip
          pushj p,[outstr [asciz /
Password: /]
                 setom  pwdcmd          ;We are not in password command mode
                 jrst   passwo]         ;Retrieve the password
        movem   n,hstsys                ;This system is in another network
        exch   n,sysno                  ;Exchange the gateway # and the host #
        move    a,n                     ;Gateway #

;Build the circuit to the Gateway.

        setzm   talksw                  ;Mimic talk mode so that the slave program
                                        ;doesn't get run yet
        pushj   p,getcir                ;Set up the circuit
        setom   talksw                  ;Get back out of talk mode

;Want to build the login string and put it into the output buffer of the aux
;port.  This will log us into the host on the other network.  Then the slave
;program is started up.

        pushj   p,bldstr                ;Get the login string
        hrlz    a,auxprt                ;Get the number of the auxport
        hrri    a,auxptr                ;Set up the auxcal to put a string
        move    b,[point 7,auxstr]      ;into the output buffer
        auxcal  a,b                     ;Point to the login string
        pushj   p,crouts                ;In case of detached job, cr
        pushj   p,sndbal                ;Get the input from the other system
        move    a,hstsys                ;Get the host # we just logged into
        movem   a,auxcir                ;Aux circuit system number
        pushj   p,getcir                ;Start the slave program
        pushj   p,zerstr                ;Clear out the login string
        jrst    getcmd                  ;Gateway command completed

;Send a yellow ball to the auxport and wait until an orange ball is received.
;This is done to ensure the receipt of all the output from the foreign host 
;after logging into it.

Sndbal: move    a,auxprt                ;Get the # of the auxport
        getlch  a                       ;Get rid of any previous orange balls
getall: move    a,[xwd 40,74]           ;Send a yellow ball and wait for the
        hiber   a,                      ;return of an orange ball-sometimes 
          jrst synerr                   ;they get lost, so set a time limit
        move    a,auxprt                ;Get the auxport # again to check
        getlch  a                       ;for the return of the orange ball
        tlne    a,2000                  ;from the host
          jrst  getall                  ;No ball, output from host not finished

;Print the input on the command port.
 
        hrlz    a,auxprt                ;Get the # of the aux port
        hrri    a,auxins                ;Set the auxcal up for input
        auxcal  a,ch                    ;Get the input
          popj  p,                      ;No more input
        outchr  ch                      ;Print it out to the terminal
        jrst    .-3                     ;Get the next character input

;Build the login string.

bldstr: move    bp,strptr               ;Point to the login string
        move    a,usrnam                ;Want the username to be in ascii
        pushj   p,strsix                ;Turn the sixbit string into ascii
        move    a,usrnam+1
        pushj   p,strsix
        movei   ch,colon                ;Deposit an ascii colon in the string
        idpb    ch,bp                   ;after the username
        move    a,sysno                 ;Convert the system number into ascii
        pushj   p,maksys                ;and put it in the string
        movei   ch,semicol              ;After that, place a semicolcolon
        idpb    ch,bp                   ;in the string
        pushj   p,getpas                ;Last, put in the password
        movei   ch,carret               ;and a crlf
        idpb    ch,bp
        movei   ch,carret
        idpb    ch,bp
        popj    p,                      ;Finished with the string

;Get the system number to be logged into.

Sysnum: tro     f,allnum                ;We will be looking for a number
        pushj   p,numin                 ;Go get the number
        caig    n,hghsys                ;Does it fall within range
          caig  n,0
            jrst synerr                 ;If not, command is in error
        popj    p,





; Host command.  This command overwrites the system table with system type.

HOST:   skipe   auxprt          ;See if an aux circuit has been built yet
          jrst  [outstr [asciz/
 HOST COMMAND NOT PERMITTED AFTER CIRCUIT HAS BEEN BUILT
                /]
                jrst    getcmd]
        trnn    f,morein        ;Are any more characters to be input?
          jrst  synerr          ;The system number and type are still needed
        pushj   p,numin         ;Get the system number
        caig    n,hghsys        ;Check to see if it falls within range
          caig  n,0             
            jrst synerr         ;The system # is either too big or too small
        movem   n,hstsys#       ;Save the system number
        trnn    f,morein        ;More characters to be input?
          jrst  synerr          ;Still need system type
        movei   n,6             ;Doesn't deserve a comment
        move    optr,wdptr      ;Optr now points to the next word (which will
                                ;be in wd) from the command string
        pushj   p,getchs        ;Get the system type
        move    n,typsiz        ;Get the size of the Type System table (typtab)
        pushj   p,fndtyp        ;Search the System Type table for a match
        jumpl   n,synerr        ;None was found

        move    n,hsttab(n)     ;Change the system table accordingly
        move    a,hstsys        ;Retrieve the system number
        movem   n,systab-1(a)   ;Change the system type
        jrst    getcmd          ;Host command completed

;Find the system type

fndtyp: move    a,typtab(n)     ;Shorten the host type to the number of letters
        andm    wd,a            ;typed in
        came    wd,a            ;Compare each item of the type table with wd
          sojge n,fndtyp        ;Not found, try again
        popj    p,              ;Search done

; TALK COMMAND.  USES CHANNELS 1,6, AND 7.   GETCIR CLEARS ALL
;       CHANNELS AND TAES CARE OF ASSIGNMENTS
;       ESCAPE INTERRUPT SHOULD CLEAR REMAINING INTERRUPTS
TALK: SKIPN CIN ;TALK NOT PERMITTED FROM COMMANDS FILE
 JRST .+3
 OUTSTR [ASCIZ"
TALK NOT PERMITTED FROM COMMANDS FILE
"]
 JRST GETCMD
 SETZM TALKSW## ;NOW DOING TALK
 SKIPE AUXPRT           ;DO WE HAVE A CIRCUIT
 TRNE F,MOREIN          ;YES, BUT HE TYPED A SYSTEM NO.
 JRST TALK2             ;GO READ A SYSTEM NO.
 MOVE A,AUXCIR
TALK3: PUSHJ P,GETCIR   ;EXIT PROGRAM PERHAPS.  ALSO SET UP INTERRUPT SYSTEM
;                       FOR TALK COMMAND
 MOVE A,JBTPRV# ;GETTAB TABLE 6
 MOVEI B,ESC ;ESC FOR GE OR TYMEX MODE
 CAIE A,600
 CAIN A,0
 MOVEI B,ETX##  ;CONTROL-C FOR SUDS OR PDP MODE
 MOVE A,SLVTYP
 CAIE A,X       ;ARE WE TALKING TO X
 MOVEI B,ESC##  ;NO, JUST SEND ESCAPE
 MOVEM B,ESCCHR#
;POLL REMOTE CONSOLE
OPOLL: HRLZ A,AUXPRT
 HRRI A,AUXINS
 AUXCAL A,CH
 JRST TSTINL
 OUTCHR CH
 JRST .-3

TSTINL: HRRI A,LVDEF    ;LEAVE DEFFERED ECHO
 AUXCAL A,
 HRROI A,BRKLIN  ;TEST FOR BREAK CHAR ON COMMAND PORT
        AUXCAL A,
        JRST IWAIT      ;WAIT FOR SOMETHING TO BE INPUT
        JRST IPOLL      ;READ COMMAND LINE
IWAIT: HRROI A,LVDEF
 AUXCAL A,
 MOVEI A,1
 SLEEP A,
 JFCL
 JRST OPOLL

;ALTMODE TYPED
CH4INT: HRLOI A,DISABLE
 INTENB A,
 JFCL
 MOVE A,AUXPUT
 MOVE B,ESCCHR
 AUXCAL A,0(B)
 JRST CH5IN1

;CHAR PRESENT ON AUX PORT
CH5INT: HRLOI A,DISABLE
 INTENB A,
 JFCL
CH5IN1: MOVEI A,OPOLL
 MOVEM A,ESCRTN-1
 MOVSI A,ENABLE+CH1+CH4
 INTENB A,
 JFCL
 DISMIS

;BREAK CHAR PRESENT ON COMMAND PORT
CH6INT: HRLOI A,DISABLE
 INTENB A,      ;INHIBIT ALL CHANNELS
 JFCL
 MOVEI A,IPOLL
 MOVEM A,CH6RTN-1
 MOVSI A,ENABLE+CH1+CH4
 INTENB A,
 JFCL
 DISMIS

IPOLL: MOVE A,AUXPUT
IPOLL1: INCHWL CH
 CAIE CH,PCT
 JRST IPOLL5
 INCHWL CH
 CAIE CH,PCT
 JRST IPOLL4
 INCHWL CH
 CAIN CH,UPSILON
 JRST USEP
 CAIE CH,PHI
 JRST IPOLL3
 INCHWL CH
 CAIE CH,CARRET
 JRST .-2
 INCHWL CH
 JRST GETCMD
USEP: AUXCAL A,US##
 JRST OPOLL
IPOLL3: AUXCAL A,PCT
IPOLL4: AUXCAL A,PCT
IPOLL5: CAIE CH,LNFEED
 JRST IPOLL6
 PUSHJ P,CRLF
 JRST IPOLL1
IPOLL6: CAIN CH,CARRET
 JRST IPOLL7
 AUXCAL A,0(CH)
 JRST IPOLL1
IPOLL7: INCHWL CH
 AUXCAL A,CARRET
 JRST OPOLL

TALK2: PUSHJ P,NUMIN
 TRNE F,MOREIN
 JRST SYNERR
 CAIG N,HGHSYS
 CAIG N,0
 JRST SYNERR
 MOVE A,SYSTAB-1(N)
 HLRZM A,SLVTYP#
 MOVE A,N
 JRST TALK3

;ROUTINE TO MAKE A SIXBIT STRING ASCII
STRSIX:	MOVE C,[POINT  6,A]
	MOVEI B,SIZFIL
STRS1:	ILDB CH,C
	JUMPE CH,RET
	ADDI CH,ASCSIX	;MAKE ASCII
	IDPB CH,BP
	SOJG B,STRS1
RET:	POPJ P,0


;ROUTINE TO MAKE SYSTEM NUMBER ASCII
MAKSYS:	IDIVI A,^D10
	HRLM B,(P)
	SKIPE A
	PUSHJ P,MAKSYS
	HLRZ A,(P)
	ADDI A,ASCNUM
	IDPB A,BP
	POPJ P,0


;ROUTINE TO DETERMINE THE LENGTH OF A SIXBIT STRING
;MAX. LENGTH IN "A"
;PTR IN "BP"
;RETURN SIZE OF STRING IN "N"
GETSIZ:	SETZB B,N
	ILDB CH,BP
	JUMPE CH,GETS2
	JUMPE B,GETS1
	ADD N,B
	AOJ N,
	SETZ B,
	JRST GETNX1
GETS1:	AOJA N,GETNX1
GETS2:	AOJ B,
GETNX1:	SOJG A,GETSIZ+1
	POPJ P,0
SUBTTL INITIALIZATION ROUTINES

INITEL:	SETZ F,
	SETZM TRU#
	pjob	a,		;pick up current trus
	runtim	a,
	movem	a,statru#	;save current tru useage
        setzm   hstsys#		;Host system # for the HOST command
        setzm   strsys#

	HRROI A,6		;GETTAB TABLE 6 FOR JBTPRV
	GETTAB A,
	 EXIT
	ANDI A,600		;XEXEC-PDP-GE-SUDS MODE BITS
	CAIN A,400		;EXCLUDE GE (KDL USERS)
	 EXIT
	MOVEM A,JBTPRV

	MOVEI A,ONSUP
	MOVEM A,SUP#
        PUSHJ P,ZAPCIR          ;ZERO OUT CIRCUIT CELLS
	INIT	DISK,17		;INIT SYS IN DUMP MODE
	SIXBIT	/SYS/
	0
	 JRST SYSERR

	HRROI N,JBTUNM
	GETTAB N,		;GET THIS USERS NAME(FIRST HALF)
	 SETZ N,		;ZERO IF WE CAN'T GET IT
	MOVEM N,THISUSR		;SAVE IT
	HRROI N1,JBTUN1
	GETTAB N1,		;GET HIS USERS NAME(2ND HALF)
	 SETZ N1,		;ZERO IF WE CAN'T GET IT
	MOVEM N1,THISUSR+1	;AND SAVE IT
	PUSHJ P,GETUSR		;SEE IF TYMSHARE BIT IS ON
	JRST SYSERR		;CAN'T FIND USR IN LUD
	RELEASE DISK,
	INIT DISK,17
	SIXBIT /DSK/
	0
	 JRST SYSERR
;[22]	LOOKUP DISK,GSYS	;[22] We no longer need to look
;[22]	 JRST NOSYSN		;[22] at (PJ)JOBS.DAT for the system
;[22]	INPUT DISK,SYSLST	;[22] number.
;[22]	STATZ DISK,760000
;[22]	JRST NOSYSN
;[22]	MOVE A,SYSBLK+1
;[22]	MOVEM A,THISYS		;SAVE SYSTEM NUMBER

	MOVE	A,[33,,11]	;[22] GETTAB system number from config table
	GETTAB	A,		;[22] (This is available as far back as /H
	 JRST	SYSERR		;[22]  monitors, but not documented.)
	MOVEM	A,THISYS	;[22] Save system number

	MOVNI A,LICENSE
	GETTAB A,		;GET LICENSE FOR THIS PROCESS
	 SETZ A,		;ZERO IF WE CAN'T GET IT
	TRO A,400		;KEEP AC LICENSE
	HRLS A,A
	SETLIC A,		;RETAIN ONLY USERS LICENSE

	MOVE A,[26,,11]
	GETTAB A,
	 MOVEI A,^D60
	MOVEM A,JFYSEC##      ;JIFFIES/SEC
	IMULI A,^D60
	MOVEM A,JFYMIN##      ;JIFFIES/MIN
	IMULI A,^D60
	MOVEM A,JFYHR##       ;JIFFIES/HR
	MOVEI A,INTTAB		;SET UP INTERRUPTS
	HRLI A,6		;ENABLE AND CLEAR INTERRUPT SYSTEM
	INTADR A,
	 JFCL
	HRLOI A,4
	TINASS A,		;ESCAPE(0) ON 4
	 JFCL
	MOVSI A,ENABLE+CH4      ;ENABLE CHANNEL 4
	INTENB A,
	 JFCL
	POPJ P,0
SETCIR:	HRRZ A,AUXPRT
	HRLI A,7001	;CIR. ZAP (7) ON CHANNEL 1
	TINASS A,
	 JFCL
	HRLI A,4
	HRRI A,2	;TIMER (4) ON CHANNEL 2
	INTASS A,
	 JFCL
	HRRZ A,AUXPRT
	HRLI A,5003	;CHARS. LOST (5) ON CHANNEL 3
	TINASS A,
	 JFCL
        HRLOI A,4       ;ESCAPE (0) ON CHANNEL 4
        TINASS A,
        JFCL
        HRRZ A,AUXPRT
        HRLI A,1005     ;CHAR PRESENT (1) ON CHANNEL 5
        TINASS A,
        JFCL
        HRLOI A,2006    ;COMMAND PORT BREAK CHAR PRESENT (2) ON CHANNEL 6
        TINASS A,
	 JFCL
;	HRRZ A,AUXPRT
;	HRLI A,3007     ;AUX PORT CAUSING TERMINAL WAIT (3) ON CHANNEL 7
;	TINASS A,
;	 JFCL
;	HRRZ A,AUXPRT
;	HRLI A,4010     ;AUX PORT OUTPUT ROOM (4) ON CHANNEL 8
;	TINASS A,
;	 JFCL
	POPJ P,

SETUPT: SETZ A,
        SETTIM A,       ;IN CASE ONGOING TIMER
        JFCL
	MOVE A,[XWD 1,^D300]	;5 MINUTES
	SETTIM A,
	 JFCL
	POPJ P,0
SUBTTL - CIRCUIT BUILDING ROUTINE

;ROUTINE TO CALCULATE SIZE OF REMOTE USER NAME AND FILE NAME, AND BUILD
;       CIRCUIT TO REMOTE SYSTEM.

PREP:   MOVEI A,SIZLIN  ;MAXIMUM SIZE OF FILENAME
        MOVE BP,FILPTR
        PUSHJ P,GETSIZ  ;GET SIZE OF FILE NAME
        MOVEM N,FNCT##  ;AND SAVE
        MOVEI A,SIZUSR  ;MAXIMUM SIZE OF USERNAME
        MOVE BP,NAMPTR
        PUSHJ P,GETSIZ  ;GET SIZE OF USER NAME
        MOVEM N,UNCT##  ;AND SAVE
        SETZM UPDSW##   ;NOT REPLACING OLD DISC FILE
        MOVE A,SLVTYP
        CAIN A,X370
        JRST [ DMOVE A,THISUSR  ;ON 370 TARGET NAME MUST BE LOGGED IN USERID
                CAMN A,UNAME(SIDE)
                CAME B,UNAME+1(SIDE)
                JRST BADNAM
                JRST .+1]
        MOVE A,SYS(SIDE)
        PUSHJ P,GETCIR
        POPJ P,

;ROUTINE TO BUILD A CIRCUIT, START UP SLAVE AND HANDSHAKE

GETCIR:	SETOM TIMFLG	;IGNORE TIMEOUTS
        MOVE B,SYSTAB-1(A)
        HLRZM B,SLVTYP
        HRRZM B,SLVMSG
	CAMN A,AUXCIR
        JRST GETC1      ;CHANNEL ALREADY EXISTS TO THIS SYSTEM
LOSCIR: PUSH P,A        ;SAVE DESIRED SYSTEM NO. ON STACK
	HRLOI B,DISABLE
	INTENB B,	;DISABLE ALL CHANNELS
	 JFCL
	SKIPE AUXPRT    ;SKIP IF NO CIRCUIT EXISTS ALREADY
	PUSHJ P,ZAPIT   ;CLEAR AUXPRT,AUXCIR,PGMSW
        SKIPE AUXPRT    ;DID WE SHAKE IT OFF
        JRST DEADCR     ;NO, THE BEAST REMAINS
	PUSHJ P,ZERSTR		;ZERO AUXSTR BLOCK
	TRZ F,F370      ;NOT GOING TO 370
	MOVE CH,SLVTYP		;GET SLAVE TYPE
	CAIN CH,X370		;IS IT 370?
	TRO F,F370		;YES, SET FLAG
	MOVE BP,STRPTR
	MOVE A,THISUSR
	PUSHJ P,STRSIX
	MOVE A,THISUSR+1
	PUSHJ P,STRSIX
        POP P,AUXCIR
	PUSHJ P,BLDIT
GETC1:  SKIPN PGMSW     ;DO WE HAVE PROGRAM
        JRST GETC3      ;NO, SEND PROGRAM STRING
        SKIPE TALKSW    ;IS THIS THE TALK COMMAND
        JRST GETCX1     ;NO, EXIT
        SKIPE NEWSW     ;IS PROGRAM NEW VARIETY
        JRST GETC2      ;YES
        MOVE A,AUXCIR   ;NO, MUST LOSE EXISTING CIRCUIT AND START OVER
        JRST LOSCIR
GETC2:  MOVEI CH,QUICOD## ;****** TIMER?
        PUSHJ P,LNKMSG
        SETZM PGMSW     ;NO PROGRAM NOW
        SETZM NEWSW
        PUSHJ P,BIOEND## ;EXIT BLOCK I/O MODE
GETCEX: HRLOI A,DISABLE
        INTENB A,       ;DISABLE ALL CHANNELS
        JFCL
        MOVEI A,CH4INT
        SKIPN TALKSW    ;IS THIS THE TALK COMMAND
        MOVEM A,ESCRTN  ;YES
        MOVEI A,CH5INT
        MOVEM A,GOTRTN
        MOVEI A,INTTAB
        HRLI A,6
        INTADR A,       ;CLEAR AND REENABLE INTERRUPT SYSTEM
        JFCL
        PUSHJ P,SETCIR  ;SET UP INTERRUPT CHANNELS
        MOVSI A,ENABLE+CH1+CH4
        INTENB A,
        JFCL
        POPJ P,
GETC3:  SKIPN TALKSW    ;ARE WE IN TALK COMMAND
        JRST GETCEX     ;YES
; SEND PROGRAM NAME STRING
        PUSHJ P,CROUTS  ;2ND CR LOGS INTO PDP-10 IF A DET JOB EXISTS
        HRLZ A,AUXPRT
	HRRI A,AUXPTR
	HRLZI B,440700
	HRR B,SLVMSG		;POINTER TO MESSAGE
	AUXCAL A,B		;SEND START-UP STRING
	PUSHJ P,CROUTS
	TRNE F,F370
	JRST [HRLZI B,440700
		HRRI B,GOME2B
		HRRI A,AUXPTR
		AUXCAL A,B
		PUSHJ P,CROUT
		JRST .+1]
        HRLOI A,DISABLE
        INTENB A,       ;DISABLE ALL CHANNELS
        JFCL
        MOVEI A,SYNMSG  ;SET "UNABLE TO SYNCHRONIZE" MESSAGE FOR TIMEOUT
        MOVEM A,TIMMSG
	MOVEI A,INTTAB	;INTERRUPT TABLE ADDRESS
	HRLI	A,6
	INTADR	A,	;CLEAR AND REENABLE INTERRUPT SYSTEM
	 JFCL
	PUSHJ P,SETCIR  ;ASSIGN CHANNELS
	MOVSI A,ENABLE+CH1+CH2+CH3+CH4
	INTENB A,	;ENABLE INTERRUPT 1, 2, 3, AND 4
	 JFCL
	PUSHJ P,SETUPT  ;START TIMER GOING
	SETZM TIMFLG

	MOVE A,AUXGET#
TRYAGN:	PUSHJ P,GAB
	CAIE CH,1
	JRST TRYAGN
TRYA1:	PUSHJ P,GAB
	CAIE CH,2
	JRST TRYAGN
        PUSHJ P,GAB
	CAIE CH,3
	JRST TRYAGN
        PUSHJ P,GAB
	CAIE CH,4
	JRST TRYAGN
        PUSHJ P,GSYSTYP         ;RETURN CH=LOW ORDER 3 BITS OF SYSTYP
        CAIL CH,IX
        CAILE CH,X370
        JSR TIMINT    ;FAKE TIMER INTERRUPT
        MOVEM CH,SLVTYP
        TRZ F,F370
        CAIN CH,X370
        TRO F,F370
	MOVE A,AUXPUT#
	AUXCAL A,1
	AUXCAL A,2
	AUXCAL A,3
	AUXCAL A,4
        PUSHJ P,SNDTYP
        SKIPN NEWSW     ;DON'T SEND CR TO NEW SLAVE
	PUSHJ P,CROUT
        SETOM PGMSW     ;WE HAVE PROGRAM NOW
        PUSHJ P,BIOMOD## ;ENTER BLOCK I/O MODE
        HRLOI A,DISABLE
        INTENB A,       ;DISABLE ALL CHANNELS
        JFCL
        MOVEI A,RESMSG  ;SET "CIRCUIT NOT RESPONDING" MESSAGE
        MOVEM A,TIMMSG
GETCX1: MOVSI A,ENABLE+CH1+CH2+CH3+CH4
        INTENB A,
        JFCL
        PUSHJ P,SETUPT
        SETZM TIMFLG
        POPJ P,

GSYSTYP: PUSHJ P,GAB
        SETZM NEWSW
        MOVEM CH,SYSTYP##
        TRNN CH,170
        POPJ P,
        SETZM SW10
        SETZM SW940
        SETZM SW370
        PUSHJ P,GAB
        CAIE CH,VERNUM##
GD:    JSR TIMINT    ;FAKE TIMER INTERRUPT
; ZERO PREVIOUSLY SENT OPTIONS
        HRREI B,-^D69
        MOVE A,[-OPTSZ1##,,OPT2##]
        MOVEM B,(A)
        AOBJN A,.-1
        MOVEI A,FIXOPT
        SKIPN FIX
        MOVEI A,VAROPT
        MOVEM B,(A)
; SET DEFAULT PARAMETERS FOR THIS TYPE OF SLAVE
        MOVE A,SYSTYP
        SETOM NEWSW
        ANDI A,7
        MOVE B,SLVTYP   ;SLAVE TYPE FROM TABLE
        JRST .+1(A)
        JSR TIMINT    ;0
        JRST SET940     ;1
        JSR TIMINT    ;2
        JRST SET10      ;3
        JRST SET370     ;4
        JSR TIMINT    ;5
        JSR TIMINT    ;6
        JSR TIMINT    ;7
SET370: CAIE B,X370
        JSR TIMINT
        SETOM SW370##
        MOVEI B,AUTO##
        JRST SET9A
SET10:  CAIE B,X
        JSR TIMINT
        SETOM SW10##
        MOVEI A,XMODE##
        MOVEM A,DTPARAM##
        MOVEM A,DRPARAM##
        MOVEI A,3       ;BINARY MODE
        MOVEI B,AUTO+ASCII##
        JRST SET9B
SET940: CAIE B,IX
        JSR TIMINT
        SETOM SW940##
        MOVEI B,AUTO+ASCII
SET9A:  MOVEI A,1
        MOVEM A,DTMODE##
        MOVEI A,AMODE
        MOVEM A,DTPARAM
        MOVEM A,DRPARAM
        SKIPA A,DTMODE
SET9B:  MOVEM A,DTMODE
        MOVEM A,DRMODE##
        MOVEM B,DCOPT##
        MOVEI A,^D768
        MOVEM A,DTBLOCK##
        MOVEM A,DRBLOCK##
        MOVE CH,SYSTYP
        ANDI CH,7
        POPJ P,

SNDTYP: MOVEI CH,X
        SKIPE NEWSW
        IORI CH,ACRCAP##+KATCAP##+BINCAP##+UPDCAP##
        AUXCAL A,(CH)
        MOVEI CH,VERNUM
        SKIPE NEWSW
        AUXCAL A,(CH)
        POPJ P,

CROUT:	TRNN F,F370
	POPJ P,0
CROUTS:	MOVE AUX,AUXPUT
	AUXCAL AUX,CARRET
	POPJ P,0

BLDIT:	MOVEI CH,COLON
	IDPB CH,BP
	MOVE A,AUXCIR
	PUSHJ P,MAKSYS
	MOVEI CH,SEMICOL
	IDPB CH,BP
	MOVEI A,AUXSTR
	CREAUX A,
	 JRST NOCIR
	MOVEM A,AUXPRT
	HRLZ A,AUXPRT
	HRRI A,AUXIN
	MOVEM A,AUXGET
	HRRI A,AUXOUT
	MOVEM A,AUXPUT
	AUXCAL A,CARRET ;FIRST OF 2 CR'S IN CASE DET JOB ON PDP-10
			;SECOND CR WILL BE SENT WITH PROGRAM NAME
	PUSHJ P,ZERSTR
        MOVE A,SLVTYP
        MOVEM A,AUXTYP# ;SAVE TYPE OF CIRCUIT FOR TEARING DOWN
	POPJ P,0

ZERSTR:	SETZM AUXSTR
	MOVE A,[XWD AUXSTR,AUXSTR+1]
	BLT A,AUXSTR+SIZLIN/5-1
	POPJ P,0
;ROUTINE TO OUTPUT SYNC CHARACTER AND A CODE
PUTSPC:	PUSH P,CH
	MOVEI CH,CONTRL
	PUSHJ P,PUTCH
	POP P,CH
	PUSHJ P,PUTCH
	POPJ P,0

;ROUTINE TO OUTPUT 1 CHARACTER OVER AUX. CIRCUIT
PUTCH:	TRNE F,F370		;IF 370,
	JUMPE CH,[MOVEI CH,SPACE	;AND SENDING A NULL,
		JRST .+1]	;SEND A SPACE INSTEAD
	MOVE AUX,AUXPUT
	AUXCAL AUX,(CH)
	POPJ P,0
;TELL SLAVE TO STOP WHAT IT IS DOING
ABORT: PUSHJ P,ABT     ;PRESERVES REGISTERS IN "NEW" CODE
        JRST GETCMD

ABT:
CLRIT: PUSH P,A
        PUSH P,CH
        PUSH P,AUX
        SKIPN PGMSW     ;DO WE HAVE A PROGRAM
        JRST CLR2       ;NO, JUST EMPTY PIPE
        SKIPN NEWSW     ;IS IT NEW VARIETY
        JRST OLDABT     ;NO, OLD
        MOVEI CH,ABTCOD##
        PUSHJ P,LNKMSG
        JRST CLR2
;SEND 107 AND LOOP TIL ALL CHARACTERS FROM SYSTEM ARE IN
OLDABT: MOVE A,SLVTYP
        CAIN A,IX
        JRST [ MOVEI CH,11
                PUSHJ P,PUTCH
                JRST CLR2]
        MOVEI CH,STAGN
	PUSHJ P,PUTSPC
	PUSHJ P,CROUT
CLR2: MOVE AUX,AUXGET
	HRRI AUX,AUXINS ;INPUT AND SKIP IF CHAR THERE
CLR1:	MOVEI A,1
	SLEEP A,
	AUXCAL AUX,CH
	SKIPA
	JRST CLR1
        POP P,AUX
        POP P,CH
        POP P,A
	POPJ P,0
;ASK USER IF ITS OK TO WRITE ON OLD FILE

CHKOLD:	SKIPN CIN       ;DON'T ASK IF RUNNING FROM COMMANDS FILE
        SKIPE ASK       ;OR IF AUTOMATIC REPLACE/KEEP
        JRST .+2
	OUTSTR	[ASCIZ/
OKAY TO WRITE ON OLD FILE? /]
        SKIPE ASK
        JRST CHKO
	PUSHJ P,YESNO
        JRST ABORT
	POPJ P,0
CHKO:   SKIPG ASK
        OUTSTR [ASCIZ"
REPLACING FILE "]
        SKIPL ASK
        OUTSTR [ASCIZ"
KEEPING FILE "]
        PUSHJ P,NAMOUT
        PUSHJ P,SYSOUT
        PUSHJ P,FILOUT
        OUTSTR [ASCIZ"
"]
        SKIPL ASK
        JRST ABORT
        POPJ P,
SUBTTL MAKE USER ANSWER YES OR NO

YESNO: SETOM TIMFLG
	INCHWL CH
	PUSH P,CH		;SAVE FIRST CHARACTER INPUT
	INCHWL CH
	CAIE CH,LNFEED		;LOOP TIL LINE FEED FOUND
	JRST .-2
	POP P,CH		;GET FIRST CHARACTER INPUT
	CAIE CH,"N"+40
	CAIN CH,"N"
	POPJ P,0
	CAIE CH,"Y"+40
	CAIN CH,"Y"
	JRST SKPRET
        SKIPE CIN       ;IF RUNNING FROM COMMANDS FILE, EXIT
        JRST YESN2
	OUTSTR [ASCIZ /
PLEASE TYPE 'Y' OR 'N': /]
	CLRBFI
	JRST YESNO

YESN2: OUTSTR [ASCIZ"
COMMANDS FILE NEEDS 'Y' OR 'N' RESPONSE--PROGRAM TERMINATED
"]
 JRST ENDIT
SKPRET:	AOS (P)
	POPJ P,0
;GET CONTROLLING USER'S NAME IN LUD AND KEEP HIS TYMSHARE BIT

GETUSR:	LOOKUP 1,LUDLOK
	JRST SYSERR
	PUSHJ P,HASHER
	MOVEM N,HUNAME#	;HASHED USR NAME
	MOVEM N1,HLOC#	;HASH LOCATION
RD:	USETI 1,@HLOC
	INPUT 1,SYSLST
	STATZ 1,760000
	JRST SYSERR
	MOVE A,HUNAME
	SETZ INDEX,
SETDEX:	SKIPN ,SYSBLK(INDEX)
	POPJ P,0
	LDB C,[POINT 7,SYSBLK+2(INDEX),35]
	CAMN A,SYSBLK+4(INDEX)
	JRST [LDB A,[POINT 1,SYSBLK+3(INDEX),21]
		MOVEM A,TYMBIT
		AOS (P)
		POPJ P,0]
	SKIPG ,SYSBLK(INDEX)
	JRST .+3
	ADD INDEX,C
	JRST SETDEX
	HRRZ C,SYSBLK(INDEX)
	MOVEM C,HLOC
	JRST RD
HASHER:	MOVEI B,0
	MOVE C,[555555555555]
	MOVE D,[361275431652]
	MOVE E,[612754316523]
	JSR RND
	JSR RND
	JSR RND
	JSR RND
	XOR E,D
	MOVE  N,E
	TLZ N,400000
	IDIVI N,^D887
	MOVE N,E
	XOR N,C
	ADDI N1,1
	POPJ P,0

RND:	0
	ADD D,N
	ROTC N,-22
	MOVEI A,5
RND1:	MOVE N2,C+1(B)
	MUL N2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM N3,D+1(B)
	AOJE B,RND2
	MOVNI B,1
	TRNE D,1
	SKIPL E
	MOVEI B,0
	EXCH C,E
RND2:	SOJG A,RND1
	JRST  @RND
SUBTTL - INPUT INFO ABOUT FILES FROM TELETYP

GETC:	MOVEI SIDE,DBLK		;SET UP FOR FROM SIDE FIRST
	MOVEI N,SIZFIL
	MOVE OPTR,WDPTR
	PUSHJ P,GETCHS
	CAIN CH,LPAREN		;IS IT A LEFT PARENTHESES?
	 JRST GTFUN		;YES, GO GET THE FROM USER NAME
	TRNE F,HAS2BOLD
	 JRST SYNERR
	TRZ F,OLDFORM		;IT IS NOT OF THE OLD STANDARD FORM
	CAIE CH,SPACE
	 CAIN CH,CARRET
	  JRST [CAIN CH,CARRET
		 TRZ F,MOREIN
		TRNE F,MOREIN
		 JRST CHKCMD
		MOVE A,WD
		AND A,MASK	;GET FIRST CHARACTER ONLY
		CAME A,YES
		 CAMN A,NO
		  JRST GETCMD	;IGNORE A YES OR NO
		JRST CHKCMD]	;GO TO COMMAND DISPATCHER
	CAIE CH,COLON
	 JRST SYNERR
	TRNE F,CANAGN
	 JRST SAMFIL		;NEW SYSTEM ONLY FOR OLD FILES
	JRST SYNERR		;ERROR
GTFUN:	SETOM NOPRMT
	TRO F,OLDFORM		;INPUT MAY BE IN STANDARD FORM
	PUSHJ P,GTNSYS		;GET NAME AND SYSTEM
        setzm   strsys
	PUSHJ P,GTFNM		;GET FILE NAME
	CAIN CH,CARRET
	 JRST [TRNE F,OLDFORM
		JRST GETOLD	;GO GET SECOND LINE OF STANDARD FORM
		JRST SYNERR]
	TRNE F,HAS2BOLD
	 JRST SYNERR
	TRZ F,OLDFORM
	MOVEM PTR,LSTFPT
	MOVEM CH,LSTFCH
	MOVEI SIDE,RBLK		;NOW DO TO SIDE
	PUSHJ P,FIN2ND		;SCAN TO TO SIDE
	CAIN CH,LPAREN
	 JRST [PUSHJ P,GTNSYS	;GET NAME AND SYS
		JRST GTTFIL]	;AND GO GET TO FILE
	CAIE CH,COLON
	 JRST SYNERR
	PUSHJ P,GETSYS		;GET SYSTEM
GTTFIL:	PUSHJ P,CHKNXT
	CAIE A,CARRET
	 CAIN CH,CARRET
	  SKIPA
	   JRST [PUSHJ P,GTFNM
		 JRST GTTF1]
	TRO F,NOTOFIL
	PUSHJ P,SETTF		;MAKE TO FILE SAME AS FROM FILE
GTTF1:	PUSHJ P,SETUNM
	SKIPN RBLK+SYS		;IF NO SYSTEM
	 JRST SYNERR		;IT'S AN ERROR
	MOVEM PTR,LSTTPT
	MOVEM CH,LSTTCH
	JRST RUNIT


SAMFIL:	MOVEI SIDE,RBLK		;PRETEND WE ARE GETTING NEW "TO" SYSTEM
	PUSHJ P,GETSYS		;AND GO GET IT
	MOVE A,SYS(SIDE)
	MOVEM A,NXTSYS		;AND SAVE IT
	MOVE C,[XWD OLDCMD,INBLK]
	BLT C,INBLK+MAXWDS-1	;AND RESTORE IT
	TRO F,NEWSYS+MOREIN+OLDFORM
	MOVE PTR,INBPTR
	JRST GETC
GETNXT:	MOVE PTR,LSTFPT
	MOVE CH,LSTFCH
	PUSHJ P,CHKNXT
	CAIN CH,COMMA
	 JRST [MOVE A,SAVPTR
		CAME A,FIRPTR
		 JRST GETN1
		JRST GETN2]
GETN1:	CAME PTR,FIRPTR
	 JRST GETN3
GETN2:	TLZ F,MORETR
	TRZ F,MOREIN
	JRST GETCMD+2

GETN3:	MOVEI SIDE,DBLK
	TRO F,MOREIN
	PUSHJ P,GTFNM
	MOVEM CH,LSTFCH
	MOVEM PTR,LSTFPT
	TRNE F,NOTOFIL
	 JRST [PUSHJ P,SETTF+2
		JRST RUNIT]
	MOVEI SIDE,RBLK
	MOVE PTR,LSTTPT
	MOVE CH,LSTTCH
	CAIE CH,COMMA
	 JRST SYNERR
	PUSHJ P,GTFNM
	MOVEM CH,LSTTCH
	MOVEM PTR,LSTTPT
	JRST RUNIT

CHKNXT:	MOVEM PTR,SAVPTR#
	ILDB A,PTR
	EXCH PTR,SAVPTR
	POPJ P,0

GETUNAM:
	MOVEI N,SIZUSR
	TRO F,NAMFLG
	MOVE OPTR,NAMPTR
	PUSHJ P,GETCHS
	TRZ F,NAMFLG
	POPJ P,0
GTFNM:	TRO F,SKPCOM+SPOK	;SKIP A LEADING COMMA
	MOVE A,SYS(SIDE)
;[22]	HLRZ A,SYSTAB(A)	;GET SYSTEM TYPE
	HLRZ A,SYSTAB-1(A)	;[22] Fencepost error
	CAIN A,X370		;IF 370,
	 TRO F,SPOK		;ALLOW SPACES
	SETZM FNAME(SIDE)
	MOVEI N,SIZLIN
	MOVE A,SIDE
	ADDI A,FNAME
	HRLS A
	HRRZ B,A
	ADDI A,1
	ADDI B,SIZUSR
	BLT A,@B		;ZERO ENTIRE FILE NAME
	TRO F,SPCDEL
	MOVE OPTR,FILPTR
	PUSHJ P,GETCHS
	MOVEI A,SIZLIN
	SUB A,N
	MOVEM A,FSIZE(SIDE)	;SAVE LENGTH OF FILE NAME
	SETZM FILE(SIDE)
	SETZM EXT(SIDE)
	MOVE OPTR,FILPTR
	MOVE B,[POINT 6,FILE(SIDE)]	;IN CASE PDP10
	MOVEI C,SIZLIN
	MOVEI A,SIZFIL
FN1:	ILDB D,OPTR
	CAIN D,DOT-ASCSIX
	 JRST SETEXT
	SOSL A
	IDPB D,B
	SOJG C,FN1
	POPJ P,0

SETEXT:	MOVEI A,SIZEXT
	MOVE B,[POINT 6,EXT(SIDE)]
SETEX1:	ILDB D,OPTR
	SOSL A
	 IDPB D,B
	SOJG C,SETEX1
	POPJ P,0

FIN2ND:	MOVEM PTR,FIRPTR
	CAIE CH,LPAREN
	 CAIN CH,COLON
	  POPJ P,0
	CAIN CH,CARRET
	 JRST SYNERR
	TLO F,MORETR
	ILDB CH,PTR
	JRST FIN2ND
SETTF:	SKIPE RBLK+FILE
	 POPJ P,0
	MOVE A,DBLK+FSIZE
	MOVEM A,RBLK+FSIZE
	MOVE A,DBLK+FILE
	MOVEM A,RBLK+FILE
	MOVE A,DBLK+EXT
	MOVEM A,RBLK+EXT
	HRLZI A,DBLK+FNAME
	HRRI A,RBLK+FNAME
	BLT A,RBLK+FNAME+MAXFIL
	POPJ P,0

SETUNM:	SKIPE RBLK+UNAME
	 POPJ P,0
	MOVE A,DBLK+UNAME
	MOVEM A,RBLK+UNAME
	MOVE A,DBLK+UNAME+1
	MOVEM A,RBLK+UNAME+1
	POPJ P,0
SUBTTL - INPUT AND CHECK SYSTEM

GETSYS:	PUSHJ P,NUMIN
	JUMPN N,GETSY1
	CAIN CH,STAR
	 JRST [MOVE N,THISYS
		MOVEM N,SYS(SIDE)
		ILDB CH,BP
                setom   strsys
		JRST CHKSYS]
	JUMPE N,SYNERR
GETSY1:	CAILE N,HGHSYS
	 JRST SYNERR
	MOVEM N,SYS(SIDE)
	CAIN CH,RPAREN
CHKSYS:	 ILDB CH,PTR
	CAMN N,THISYS		;IF NOT CONTROLLING SYSTEM...
	 POPJ P,0
	MOVE A,SYSTAB-1(N)	;SAVE INFO
	JUMPE A,BADSYS
	HLRZM A,SLVTYP
	HRRZM A,SLVMSG#		;SAVE ADDRESS OF LOGIN MESSAGE
	POPJ P,0

BADSYS: OUTSTR [ASCIZ /
TELECOPY IS NOT AVAILABLE ON THAT SYSTEM.
/]
	JRST GETCMD
; [22]  Define the system table (aka system map).

define MKSTB(Low,High,SysTyp,SysMsg)<
 ifn zz-Low+1,<printx Error in macro at SYSTAB:>
 xlist
 repeat High-Low+1,< SysTyp,,SysMsg >
 list
 zz==zz+High-Low+1
>

; This table is correct as of 9-APR-85 /DJB

	zz==0
SYSTAB:	MKSTB(    ^d1,   ^d21,   IX, GoMes1 )	; systems 1-21
	MKSTB(   ^d22,   ^d39,    X, GoMes  )	; systems 22-39
	MKSTB(   ^d40,   ^d53, X370, GoMe2A )	; systems 40-53
	MKSTB(   ^d54,   ^d60,    X, GoMes  )	; systems 54-60
	MKSTB(   ^d61,   ^d64, X370, GoMe2A )	; systems 61-64
	MKSTB(   ^d65,   ^d65,    X, GoMes  )	; system  65
	MKSTB(   ^d66,   ^d69, X370, GoMe2A )	; systems 66-69
	MKSTB(   ^d70,   ^d81,    X, GoMes  )	; systems 70-81
	MKSTB(   ^d82,   ^d82, X370, GoMe2A )	; system  82
	MKSTB(   ^d83,   ^d84,    X, GoMes  )	; systems 83-84
	MKSTB(   ^d85,   ^d85, X370, GoMe2A )	; system  85
	MKSTB(   ^d86,  ^d141,    X, GoMes  )	; systems 86-141
	MKSTB(  ^d142,  ^d142, X370, GoMe2A )	; system  142
	MKSTB(  ^d143,  ^d157,    X, GoMes  )	; systems 143-157
	MKSTB(  ^d158,  ^d158, X370, GoMe2A )	; system  158
	MKSTB(  ^d159, ^d9999,    X, GoMes  )	; systems 159-9999
HGHSYS==.-SYSTAB
GTNSYS:	PUSHJ P,GETUNAM
	CAIE CH,COLON
	 JRST [TRZ F,OLDFORM
		TRNE F,HAS2BOLD
		 JRST SYNERR
		MOVE A,THISYS
		MOVEM A,SYS(SIDE)
		JRST GETNS1]
	PUSHJ P,GETSYS
GETNS1:	CAIE CH,RPAREN
	 JRST SYNERR
	POPJ P,0
; ROUTINE TO TAKE CHARACTERS FROM THE INPUT LINE AND
; RETURN THEM IN PTR
; N/ MAXIMUM NUMBER OF CHARS TO RETAIN(UP TO 72)
; PTR/ POINTER TO INPUT STRING
; OPTR/ POINTER TO OUTPUT STRING

GETCHS:	TRZ	F,NONSPC	;HAVE NOT FOUND A NON-SPACE CHAR. YET
	MOVE	A,OPTR		;ZERO OUT FIRST 2 WORDS
	SETZM	@A
	ADDI	A,1
	CAILE	N,SIZFIL	;IF WE ARE LOOKING FOR MORE THAN 1 WORD,
	 SETZM	@A		;ZERO THE SECOND
	TRNN	F,MOREIN
	 PUSHJ	P,GETLIN	;IF WE NEED MORE FROM THE TTY, GO GET IT
GTNXTC:	PUSHJ	P,GETONE	;GET THE NEXT CHARACTER
	JRST	DELFND		;IT WAS NOT AN ALPHA-NUMERIC
	TRO	F,NONSPC	;FOUND A NON-SPACE CHARACTER
NODEL:	TRZ	F,SKPCOM	;THIS IS NOT A LEADING COMMA
	SUBI	CH,SPACE	;CONVERT TO SIXBIT
	SOJL	N,GTNXTC	;JUST GET ANOTHER IF COUNT HAS NOT BEEN EXHAUSTED
	IDPB	CH,OPTR		;SAVE CHARACTER
	JRST	GTNXTC		;AND GET ANOTHER
DELFND:	TRZE	F,SKPCOM	;TURN OFF FIRST TIME THRU
	CAIE	CH,COMMA	;FOUND A LEADING COMMA!
	 SKIPA
	JRST	GTNXTC		;IGNORE IT
	CAIN	CH,CARRET	;IF CARRIAGE RETURN
	 JRST	[TRZ F,MOREIN
		 JRST RETCHS]
	CAIN	CH,SPACE	;IF WE HAVE A SPACE
	 JRST	[TRNN F,NONSPC
		  JRST GTNXTC
		 TRNE F,SPOK
		  JRST NODEL
		 JRST RETCHS]
	TRNE	F,SPCDEL	;IF WE ARE WAITING FOR SPECIAL DELIMITER,
	 JRST	CHKSPC		;GO SEE IF WE GOT ONE
	TRO	F,NONSPC
	CAIN	CH,STAR		;IF WE HAVE A STAR,
	 JRST	[TRNE F,STAROK	;AND IT IS ACCEPTABLE,
		  JRST NODEL	;CONSIDER IT A REGULAR CHARACTER
		 JRST RETCHS]
	CAIN	CH,SLASH	;IF WE HAVE A SLASH,
	JRST	[TRNE F,SLAOK	;AND IT IS ACCEPTABLE,
		  JRST NODEL	;CONSIDER IT A REGULAR CHARACTER
		 JRST RETCHS]
	CAIN	CH,DOT		;IF WE HAVE A PERIOD,
	JRST	[TRNE F,DOTOK	;AND IT IS ACCEPTABLE,
		  JRST NODEL	;CONSIDER IT A REGULAR CHARACTER
		 JRST RETCHS]
        cain    ch,dash		;A dash may be used in the host type
         jrst	nodel		;If it is a dash, then act natural
RETCHS:	TRZ	F,NAMFLG+SPCDEL+ALLNUM
	POPJ	P,0
CHKSPC:	MOVE	A,[XWD -SPCLEN,SPCTAB]	;GET TABLE OF SPECIAL DELIMITERS
	CAMN	CH,0(A)		;IS IT A SPECIAL DELIMITER?
	 JRST	RETCHS		;YES, RETURN
	AOBJN	A,.-2		;CHECK NEXT ONE
	JRST	NODEL		;NO, KEEP LOOING FOR ONE.

SPCTAB:	COMMA
	COLON
	LPAREN
	CARRET
SPCLEN==.-SPCTAB

GETONE:	MOVEM	PTR,OLDPTR#	;SAVE IN CASE WE HAVE TO GET LAST NON-NUMERIC
	ILDB	CH,PTR		;GET NEXT CHAR. FROM BUFFER
	CAIN	CH,QMARK	;IF A "?" - GO HELP THE POOR SHNOOK
	 JRST	[TRZ F,MOREIN+NAMFLG+SPCDEL+ALLNUM
		 JRST HELP]
	CAIL	CH,140
	 SUBI	CH,SPACE	;IF LOWER CASE, CONVERT TO UPPPER
	CAIL	CH,ZEROCH
	 CAILE	CH,NINECH
	  SKIPA			;NOT A NUMERIC
	JRST	GOTONE		;FOUND A NUMBER!
	trnn	f,allnum	;are we looking for only numerics?
	 jrst	nonum		;no, we are not 
	caie	ch,semicol	;a # and ";" suggest a gateway command
	 cain	ch,"="		;a # and "=" suggest a host command
	  popj	p,		;If so, don't restore the poineter
	move	ptr,oldptr	;If not, do restore the pointer
	popj	p,
nonum:  CAIL    CH,ALPHA
	 CAILE	CH,ZETA
	  POPJ	P,		;NOT AN ALPHANUMERIC
GOTONE:	AOS	(P)
	POPJ	 P,
GETLIN:	MOVE	C,[XWD INBLK,OLDCMD]	;SAVE THIS COMMAND
	BLT	C,OLDCMD+MAXWDS-1	;IN CASE WE NEED TO DO A REPEAT
	SETZM	INBLK
	MOVE	C,[XWD INBLK,INBLK+1]
	BLT	C,INBLK+MAXWDS-1	;ZERO OUT BUFFER AREA
	MOVE	PTR,INBPTR		;SET UP POINTER
	MOVEI	C,MAXCHR		;MAX NUMBER OF CHARACTERS
GETLN1:	INCHWL	CH			;GET A CHARACTER FROM USER
	CAIN	CH,LNFEED		;IGNORE LF WITHOUT A CARRET(LINE CONTINUATION)
	JRST	[PUSHJ P,CRLF		;JUST PUT OUT A CR
		 JRST GETLN1]
	IDPB	CH,PTR			;SAVE IT IN THE BUFFER
	CAIN	CH,CARRET		;WAS IT A CARRIAGE RETURN?/
	JRST	[INCHWL	CH		;YES - GET LINE FEED
		 CAIN	C,MAXCHR
		  JRST	GETLIN		;IF ONLY A CRLF, WAIT FOR ANOTHER
		 MOVE	PTR,INBPTR	;RESET POINTER TO START OF BUFFER
		 TRO	F,MOREIN+OLDFORM ;AND SET FLAGS
		 POPJ	P,]
	SOSL	C			;DECREMENT COUNT
	 JRST	GETLN1			;IF LESS THAN MAXCHR, KEEP GOING...
	OUTSTR [ASCIZ /
LINE IS RESTRICTED TO 300 CHARACTERS - REENTER IT:
/]
	JRST GETLIN
OUTDEC:	SKIPA N,[^D10]
OUTOCT:	MOVEI N,10
	IDIV A,N
	HRLM B,(P)
	SKIPE A
	PUSHJ P,OUTOCT+1
	HLRZ A,(P)
	OUTCHI "0"(A)
	POPJ P,0


NUMIN:	TRO F,ALLNUM
	MOVEI N,6
	MOVE OPTR,WDPTR
	PUSHJ P,GETCHS
	CAIN N,6
	JRST [SETZ N,
		POPJ P,0]
	SETZB N,B
	PUSH P,CH
	MOVE OPTR,WDPTR
MAK1:	ILDB C,OPTR
	JUMPE C,MAKE2
	SUBI C,SIXNUM
	PUSH P,C
	AOJA B,MAK1
MAKE2:	MOVEI C,1
	POP P,CH
	IMUL CH,C
	ADDM CH,N
	IMULI C,^D10
	SOJG B,MAKE2+1
	POP P,CH
	POPJ P,0
SUBTTL - MESSAGES TO BE SENT OVER CIRCUITS

GOMES:	ASCIZ /SET LOGOUT
R AUXMOX/
GOMES1:	ASCIZ /AUXMOX/
GOME2A: ASCIZ /CP TERM DISC OFF/
GOME2B:	ASCIZ /AUXMOX SLAVE/
SUBTTL - INSTRUCTION AND HELP MESSAGES

HELMSG:	ASCIZ/

LEGAL COMMANDS ARE:

RUN		BEGINS EXECUTION
VERSION		PRINTS VERSION NUMBER
ONLC		SET LOWER CASE MODE (FOR TYMCOM X TO TYMCOM 370
                OR TYMCOM X TO TYMCOM IX FILE TRANSFERS ONLY)
OFFLC		TURN OFF LOWER CASE MODE (DEFAULT)
TRUNCATE NNN	TRUNCATE STARTING AT COLUMN NNN (MIN=2, MAX=255)
		WHEN TRANSFERRING FILES FROM THE TYMCOM 370
NO TRUNCATE	TURNS OFF TRUNCATE (DEFAULT)
SUPRESS		REMOVES TRAILING BLANKS WHEN TRANSFERRING FILES FROM
		THE TYMCOM 370 (DEFAULT)
NO SUPRESS	STOPS THE REMOVAL OF TRAILING BLANKS
FIXED NNN       PADS RECORDS WITH BLANKS WHEN TRANSFERRING FILES TO
                THE TYMCOM 370 IN ORDER TO PRODUCE FIXED LENGTH
                RECORDS OF LENGTH NNN.
VARIABLE NNN    CAUSES THE TYMCOM 370 TO CREATE VARIABLE
                RATHER THAN FIXED LENGTH RECORDS WHEN TRANSFERRING
                FILES TO THE TYMCOM 370.  NNN, IF SPECIFIED, WILL BE
                THE MAXIMUM RECORD LENGTH ALLOWED. (DEFAULT).
MODE (OPTIONS)  USED TO OVERRIDE DEFAULT TRANSMISSION MODES.  THE
                ORDINARY USER NEVER NEED USE THIS COMMAND.  FOR MORE
                INFORMATION, CONTACT YOUR TYMSHARE REPRESENTATIVE.

                [ NATIVE ]
                [ SERIAL ]
MODE [TRANSMIT] [ BINARY ] [BLOCKING= N] [FILL= RIGHT] [BYTESIZE= N]
     [RECEIVE ]                          [FILL= LEFT ]
                [ ASCII ]
                [ IMAGE ]
                [ KATAKANA ]

COMPRESS (OPTIONS)  USE TO OVERRIDE DEFAULT COMPRESSION SELECTION.  THE
                ORDINARY USER NEVER NEED USE THIS COMMAND.  FOR MORE
                INFORMATION, CONTACT YOUR TYMSHARE REPRESENTATIVE.

         [ OFF ]
COMPRESS [DUPLICATE]
         [ RANK ] [ AUTO ]

SCRATCH         CLEARS ALL MODE AND COMPRESSION SETTINGS SO THAT NORMAL
                DEFAULTS WILL BE REINSTATED.
TALK NN         BUILDS A CIRCUIT TO SYSTEM NN (OR USES EXISTING CIRCUIT
                IF ONE EXISTS.  ALLOWS USER TO CONVERSE WITH REMOTE 
                SYSTEM FROM HIS TERMINAL.  IN GENERAL, ECHO
                WILL OCCUR BOTH LOCALLY AND AT THE REMOTE SYSTEM.
                TALK MODE MAY BE EXITED BY TYPING %%F(CARRIAGE RETURN).
                A %%U SENDS A UNIT SEPARATOR TO THE REMOTE SYSTEM.
                ESCAPES TYPED WILL BE SENT TO THE REMOTE SYSTEM.
WHY             CAUSES THE SLAVE PROGRAM TO SEND ITS LATEST ERROR 
                EXPLANATION MESSAGE (370 ONLY).
LOQUACIOUS      CAUSES CHECKPOINT AND OTHER MESSAGES TO BE PRINTED ON
                THE TERMINAL.  USEFUL FOR OBTAINING PROGRESS REPORTS
                WHEN TELECOPYING VERY LARGE FILES.
NOT LOQUACIOUS  STOPS PRINTING OF CHECKPOINT INFORMATION.  (DEFAULT).
ASK             ASK FOR CONFIRMATION BEFORE REPLACING AN OLD FILE
                (DEFAULT).
REPLACE         DO NOT REQUIRE CONFIRMATION WHEN REPLACING AN OLD FILE.
KEEP            KEEP OLD FILES.  NO CONFIRMATION REQUIRED.
UPDATE          USE UPDATE MODE WHEN REPLACING AN OLD FILE (DEFAULT).
                ONLY PORTIONS OF THE FILE THAT ARE DIFFERENT WILL BE
                TRANSMITTED.  EFFECTIVE ONLY WHEN TRANSFERRING FILES
                FROM TYMCOM X TO TYMCOM X, UNLESS ONE OF THE BINARY
                MODES IS BEING USED.
NO UPDATE       FORCES COPYING THE ENTIRE FILE EVEN IF UPDATE
                MODE COULD BE USED.  MAY BE FASTER THAN UPDATE MODE IF
                OLD FILE BEING REPLACED HAS BEEN MODIFIED SEQUENTIALLY
                OR GREATLY DIFFERS FROM THE REPLACEMENT.
HELP (OR ?)	REPRINTS THIS LIST
INSTRUCTIONS	HOW TO EXECUTE THIS PROGRAM
SYS940		ADDITIONAL INSTRUCTIONS RELATIVE TO TYMCOM IX-TYMCOM X
		FILE TRANSFERS
SYS370		ADDITIONAL INSTRUCTIONS RELATIVE TO TYMCOM 370-TYMCOM X
		FILE TRANSFERS
HOST NNN=XXXXXX CHANGES THE DEFAULT SYSTEM TYPE OF SYSTEM NNN TO XXXXXX,
                WHERE XXXXXX MAY BE TYM-IX, TYM-X, TYM-XX, TYM370, IX, X,
                XX, 370, TYMCMS, CMS, MVS, OR ANY UNIQUE SUBSET OF THESE.
COMMANDS FILE   CAUSES TELECOPY TO BEGIN EXECUTING FROM A COMMANDS FILE.
                ONLY THE EXTENSIONS ".COM" AND ".TMP" ARE CURRENTLY VALID.
                IF THE EXTENSION IS ".COM" TELECOPY WILL DETACH THE JOB.
PASSWORD        USED IN CONJUNCTION WITH A GATEWAY COMMAND AND A COMMANDS
                COMMAND, THIS ALLOWS THE USER TO PUT THE GATEWAY COMMAND IN A
                COMMANDS FILE, AND USE THE PASSWORD COMMAND BEFORE ISSUING
                THE COMMANDS COMMAND TO ENSURE THE SECRECY OF THE PASSWORD
GATEWAY NNN;USERNAME:SSS
                BUILDS A CIRCUIT TO A SYSTEM IN ANOTHER NETWORK VIA A GATEWAY
                NODE, WHERE NNN IS THE GATEWAY NODE NUMBER, AND USERNAME:SSS
                IS THE LOGIN STRING TO SYSTEM SSS.  IF A PASSWORD HAS NOT
                ALREADY BEEN GIVEN VIA THE PASSWORD COMMAND, THIS COMMAND WILL
                PROMPT FOR ONE
QUIT		EXITS TO EXEC

ANY OF THESE COMMANDS MAY BE SHORTENED TO THE SHORTEST UNIQUE COMMAND.
/

TYMHELP:	ASCIZ/

AS TYMSHARE PERSONNEL THERE IS ONE ADDITIONAL LEGAL COMMAND:

  TIME		SETS TIME SWITCH TO PROVIDE PROGRESS REPORTS
/

INSMSG:	ASCIZ/

NOTE: ADDITIONAL INSTRUCTIONS RELATIVE TO TRANSFERS BETWEEN TYMCOM IX,
TYMCOM 370 AND TYMCOM X ARE AVAILABLE BY ISSUING THE COMMAND SYS940 AND
SYS370.

WHEN THE RUN COMMAND IS ISSUED, THE PROGRAM WILL REQUEST THE
"COPY FROM" FILE AND THEN THE "COPY TO" FILE.  THE SYNTAX FOR
ENTERING THIS INFORMATION IS:

	(USERNAME:SYSTEM NUMBER)FILE NAME

FOR EXAMPLE:  (JONES:7)DATA

TO INPUT THE STRING WITHOUT THE PROMPTS, TYPE THE INFORMATION
DIRECTLY AFTER THE ":".

	IF THE USER NAME OR FILE NAME IS OMITTED ON THE "TO" SIDE,
THE "FROM" INFORMATION IS USED.  MORE THAN ONE FILE MAY BE TRANSFERRED
(USING SAME SYSTEM, SAME USER NAME) BY TYPING A STRING OF FILE
NAMES SEPARATED BY COMMAS.   AN * MAY BE USED IN PLACE OF THE LOGGED IN
SYSTEM.  IF THE SYSTEM NUMBER IS OMITTED ON THE 'FROM' SIDE, THE 
LOGGED IN SYSTEM IS ASSUMED.


SOME EXAMPLES:

	(SMITH:31)FILE,:35
		COPY THE FILE (SMITH)FILE FROM SYSTEM 31 TO THE FILE
		(SMITH)FILE ON SYSTEM 35.

	(SMITH:*)FILE1,(JONES:32)FILE.NEW
		COPY THE FILE (SMITH)FILE1 FROM THE SYSTEM THE USER IS
		LOGGED IN ON TO THE FILE (JONES)FILE.NEW ON SYSTEM 32.

	(SMITH:*)A,B,:36C,D
		COPY THE FILE (SMITH)A FROM THE SYSTEM THE USER IS
		LOGGED IN ON TO THE FILE (SMITH)C ON SYSTEM 36.  COPY
		THE FILE (SMITH)B FROM THE SYSTEM THE USER IS LOGGED
		IN ON TO THE FILE (SMITH)D ON SYSTEM 36.


THE COMMAND STRING IS LIMITED TO 300 CHARACTERS, AND MAY BE TYPED
ON SEVERAL LINES BY ENDING INTERMEDIATE LINES WITH A LINE FEED.
THE STRING IS TERMINATED WHEN A CARRIAGE RETURN IS ENCOUNTERED.

IF AN ATTEMPT IS MADE TO COPY TO AN EXISTING FILE, THE PROGRAM
WILL ASK FOR CONFIRMATION.

THE MESSAGE "COPY STARTING" WILL BE TYPED WHEN THE TRANSFER OF THE
DATA STARTS.

UPON COMPLETION, THE MESSAGE "FILE COPIED TO FILE" WILL BE DISPLAYED ON
THE TERMINAL, ALONG WITH THE NUMBER OF CHARACTERS TRANSMITTED.  TWO
FIGURES ARE GIVEN:  THE NUMBER IN PARENTHESES IS THE NO. OF CHARS
TRANSMITTED BY THE NETWORK, AND THE OTHER FIGURE IS THE NUMBER OF FILE
CHARS (IN 8-BIT BYTES) ACTUALLY TRANSMITTED.
/

TYMINS:	ASCIZ/

YOU ARE RECIEVING THE FOLLOWING PRINTOUT BECAUSE YOU HAVE
TYMSHARE LICENSE:

IN ADDITION TO THE COMMANDS LISTED ABOVE, THE FOLLOWING
APPLIES TO TYMSHARE PERSONNEL ONLY:

YOU MAY OBTAIN PROGRESS REPORTS BY ISSUING THE "TIME" COMMAND
AT THE COMMAND DISPATCHER.  THIS WILL PRINT THE RATES AT WHICH
CHARACTERS ARE BEING TRANSMITTED.  TWO FIGURES WILL BE GIVEN.
THE RATE PER SECOND GIVEN IN PARENTHESES IS THE NETWORK RATE.
THE OTHER FIGURE IS THE RATE OF TRANSFER OF INFORMATION IN
8-BIT BYTES.  RATES ARE PRINTED APPROXIMATELY EVERY 120K
CHARACTERS, BUT ONLY IF THE RATE IS MORE THAN 64 CPS.
/

MSG370:	ASCIZ /
ADDITIONAL INSTRUCTIONS FOR TYMCOM X, TYMCOM 370 TRANSFERS:

THE FOLLOWING DIFFERENCES PREVAIL WHEN USING THIS PROGRAM TO
TRANSFER FILES FROM OR TO A TYMCOM 370:

ONLY SYMBOLIC FILES MAY BE TRANSFERRED UNLESS THE "MODE" COMMAND IS USED
TO SELECT AN ALTERNATE TRANSMISSION MODE.
TYMCOM X LINE NUMBERED FILES, SUCH AS CREATED WITH LINED, WILL
HAVE THE LINE NUMBERS STRIPPED WHEN TRANSFERRING TO THE TYMCOM 370
SYSTEM.
TYMCOM X TABS WILL BE EXPANDED TO SPACES BASED ON STANDARD TAB
SETTINGS WHEN TRANSFERREING TO THE TYMCOM 370.
BLANK LINES IN THE TYMCOM X FILE WILL CAUSE A RECORD OF ONE
BLANK ON THE TYMCOM 370, AND IF THE FILE IS RETURNED TO 
THE TYMCOM X BY A SUBSEQUENT FILE TRANSFER, THE BLANK WILL BE
SUPRESSED UNLESS "NO SUPRESS" HAS BEEN SET.
FILE NAMES MUST MEET THE SYNTAX REQUIREMENTS OF THE APPROPRIATE
SYSTEM.
/

MSG940: ASCIZ /
ADDITIONAL INSTRUCTIONS FOR TYMCOM X, TYMCOM IX TRANSFERS:

THE FOLLOWING DIFFERENCES PREVAIL WHEN USING THIS PROGRAM TO
TRANSFER FILES FROM OR TO A TYMCOM IX:

ONLY SYMBOLIC FILES MAY BE TRANSFERRED UNLESS THE "MODE" COMMAND IS USED
TO SELECT AN ALTERNATE TRANSMISSION MODE.
TYMCOM X LINE NUMBERED FILES, SUCH AS CREATED WITH LINED, WILL
HAVE THE LINE NUMBERS STRIPPED WHEN TRANSFERRING TO THE TYMCOM IX
SYSTEM.
TYMCOM X TABS WILL BE EXPANDED TO SPACES BASED ON STANDARD TAB
SETTINGS WHEN TRANSFERRING TO THE TYMCOM IX.
TYMCOM IX COMPRESSED BLANKS WILL BE EXPANDED WHEN TRANSFERRING TO
THE TYMCOM X.
FILE NAMES MUST MEET THE SYNTAX REQUIREMENTS OF THE APPROPRIATE
SYSTEM.
/
SUBTTL - INTERRUPT AND ERROR HANDLING

ERRRET:	GETCMD
	SYSERR	;OUT OF SYNC
	SYSERR	;DISK ERROR
	SYSERR	;TOO MANY RETRIES

TIMADR:	MOVEM A,SAVA
	SKIPN A,TIMFLG
	JRST	[PUSHJ P,TIMIN1
		SETZM RETFLG
		JRST TIMA1]
	SKIPL TIMFLG
	SETZM TIMFLG
	SETOM RETFLG
TIMA1:	PUSHJ P,SETUPT
	MOVE A,SAVA
	SKIPE RETFLG
	DISMIS
	MOVEI A,RESETIT
	MOVEM A,INTTAB+2
	DISMIS

RESMSG: ASCIZ/
CIRCUIT NOT RESPONDING - ABORTING.
/
SYNMSG: ASCIZ/
UNABLE TO SYNCHRONIZE WITH SLAVE PROGRAM - ABORTING.
/

;DUMMY INTERRUPT ROUTINE USED BY ZAPIT
ZAPIT1: MOVEI A,ZAPCIR
 MOVEM A,INTTAB
 DISMIS

;BUFFER ZAP OCCURRED.  ABANDON CURRENT OPERATION.
ZAPIT3: PUSHJ P,ZAPIN1  ;LOSE CIRCUIT IF POSSIBLE
        MOVEI A,RESETIT
        MOVEM A,BUFRTN-1
        DISMIS

;CIRCUIT LOST.
ZAPADR: PUSHJ P,RESREG  ;RESTORE REGISTERS
        HRLOI A,DISABLE
        INTENB A,
        JFCL
        OUTSTR  [ASCIZ/
CIRCUIT ZAPPED - ABORTING,  SYSTEM /]
	MOVE A,AUXCIR			;[22] tell us who lost
	PUSHJ P,OUTDEC			;[22]
	PUSHJ P,CRLF			;[22]
        PUSHJ P,ZAPCIR
	MOVEI A,RESETIT
	MOVEM A,INTTAB
	DISMIS

;HERE TO BAIL OUT OF SERIOUS DIFFICULTY.  TRY TO LOSE CIRCUIT
ZAPINT: 0       ;SIMULATE LOSS OF CIRCUIT
 PUSHJ P,ZAPIN1
 JRST RESETIT

;HERE IN CASE OF DIFFICULTY WHICH WOULD EVENTUALLY PRODUCE TIMEOUT
TIMINT: 0       ;SIMULATE TIMEOUT
 PUSHJ P,TIMIN1
 JRST RESETIT

TIMIN1: PUSHJ P,RESREG
        HRLOI A,DISABLE
        INTENB A,
        JFCL
        OUTSTR @TIMMSG
        MOVEI A,RESMSG
        MOVEM A,TIMMSG#
        PUSHJ P,ZAPIT
        POPJ P,

ZAPIN1: PUSHJ P,RESREG ;RESTORE REGISTERS
 HRLOI A,DISABLE         ;DISABLE ALL CHANNELS
 INTENB A,
 JFCL
 OUTSTR [ASCIZ/
SYNCHRONIZATION PROBLEM--ABORTING.
/]
 IFG TESTNG,<JSR CRASH##>
 PUSHJ P,ZAPIT
 POPJ P,
CLRCIR:
ZAPIT:	SETOM TIMFLG
        PUSHJ P,RESREG  ;RESTORE REGISTERS MODIFIED BY "NEW" CODE
	HRLOI B,DISABLE
	INTENB B,       ;DISABLE ALL CHANNELS
	JFCL
        MOVEI A,ZAPIT1
        MOVEM A,ZAPRTN
        MOVSI B,ENABLE+CH1
        INTENB B,       ;ENABLE CIRCUIT LOSS
        JFCL
        SKIPE AUXPRT
        JRST LOS370
; RESET ALL CIRCUIT SWITCHES
ZAPCIR: SETZM AUXPRT##          ;AUXILLIARY CHANNEL NO.
        SETZM AUXTYP            ;SLVTYP FOR THIS CIRCUIT
        SETZM AUXCIR#           ;SYSTEM NO. CIRCUIT IS BUILT TO
        SETZM BMOD##    ;NOT IN BLOCK I/O MODE
ZAPC1:  SETZM PGMSW     ;NO PROGRAM
        SETZM NEWSW     ;NOTHING IS NOT NEW
        PUSH P,A
        MOVEI A,ZAPADR
        MOVEM A,ZAPRTN
        POP P,A
	POPJ P,
LOS370: MOVE A,AUXTYP
        CAIE A,IX       ;IS IT A 940
        SKIPE PGMSW     ;NO, DO WE HAVE A PROGRAM?
        JRST LOS37A     ;JUST SEND ZAPPER
        OUTSTR [ASCIZ"
CONNECTION REMAINS TO SYSTEM "]
        MOVE A,AUXCIR
        PUSHJ P,OUTDEC
        OUTSTR [ASCIZ"--USE TALK COMMAND TO LOGOUT
"]
        JRST ZAPC1
LOS37A: MOVE A,AUXPRT
        ZAPCIR A,
        JRST ZAPCIR
; command to track tru usage

trus:	pushj	p,clrtru		; clear the tru string
	pjob	a,
	runtim	a,			; get current trus
	sub	a,statru#		; make incremental
	addm	a,statru		; store for next command
	idivi	a,^d10000
	movem	b,fracti#		; save fractional trus
	jumpe	a,trus1
	move	ptr,truptr		; pointer to ascii string
	pushj	p,maksys		; convert a to ascii
	outstr	trustr			; print it
trus1:	outchr	["."]			; print a decimal point
	pushj	p,clrtru
	move	ptr,truptr
	move	a,fracti		; pick up fractional trus
	pushj	p,maksys		; convert to ascii string
	move	b,fracti
trus2:	imuli	b,^d10			; move fraction a digit
	cail	b,^d10000		; need to preing leading zeros
	 jrst	trus3
	outchr	["0"]
	jrst	trus2
trus3:	outstr	trustr
	outstr	[asciz/ TRUS/]
	jrst	getcmd			; done

clrtru:	setzm	trustr			; clear the tru string
	move	a,[trustr,,trustr+1]
	blt	a,trustr+5
	popj	p,

CMDTAB:	SIXBIT /RUN/
	SIXBIT /HELP/
	SIXBIT /INSTRU/
	SIXBIT /VERSIO/
	SIXBIT /QUIT/
	SIXBIT /ONLC/
	SIXBIT /OFFLC/
        SIXBIT /SCRATC/
        SIXBIT /MODE/
        SIXBIT /COMPRE/
        SIXBIT /WHY/
	SIXBIT /FIXED/		;[22] changed from 'fix'
	SIXBIT /VARIAB/		;[22] changed from 'var'
        SIXBIT /TALK/
	SIXBIT /TIME/
	SIXBIT /SYS940/
	SIXBIT /SYS370/
        SIXBIT /KEEP/
        SIXBIT /REPLAC/
        SIXBIT /ASK/
	SIXBIT /NOT/
	SIXBIT /HOST/
	SIXBIT /PASSWO/
	SIXBIT /GATEWA/
        SIXBIT /COMMAN/
	SIXBIT /TRUS/
NEGTAB:	SIXBIT /TRUNCA/
	SIXBIT /SUPRES/
        SIXBIT /UPDATE/
        SIXBIT /LOQUAC/
CMDSIZ==.-CMDTAB
NEGSIZ==.-NEGTAB

CMDISP:	RUNROU
	HELP
	INSTRU
	VERSION
	ENDIT
	SETLC
	RESTLC
        SCRAT
        MODE
        COMPRESS
        WHY
        FIX1
        VAR1
        TALK
	SETIME
	SYS940
	SYS370
        KEEP
        REPLACE
        ASKIT
	NEGATE
        HOST
        PASSWO
        GATEWA
        COMMAN
	TRUS
	TRUNCA
	SUPRES
        UPDATE
        VERBO

NEGDIS:	NOTRU
	NOSUP
        NOUPDA
        NOVER

typtab:
        sixbit /tym-x /
        sixbit /x     /
        sixbit /tym-xx/
        sixbit /xx    /
        sixbit /370   /
        sixbit /cms   /
        sixbit /mvs   /
        sixbit /tym370/
        sixbit /tymcms/
        sixbit /940   /
        sixbit /tymix /
        sixbit /ix    /
typsiz: .-typtab-1

hsttab:
        xwd     x,gomes
        xwd     x,gomes
        xwd     x,gomes
        xwd     x,gomes
        xwd     x370,gome2a
        xwd     x370,gome2a
        xwd     x370,gome2a
        xwd     x370,gome2a
        xwd     x370,gome2a
        xwd     ix,gomes1
        xwd     ix,gomes1
        xwd     ix,gomes1

SYNERR:	OUTSTR	[ASCIZ/
? SYNTAX ERROR - PLEASE RE-ENTER OR TYPE INSTRUCTIONS.
/]
RESETIT:	MOVE C,[XWD OLDCMD,INBLK];RESTORE PREVIOUS COMMAND SO
	BLT C,INBLK+MAXWDS-1	;WE DON'T LOOSE IT
	TRZ F,MOREIN+OLDFORM+HAS2BOLD+NOTOFIL	;MAKE HIM TYPE IT OVER
	TLZ F,MORETR
	JRST GETCMD

SYSERR:	OUTSTR	[ASCIZ/
SYSTEM ERROR - ABORTING.
/]
	PUSHJ P,ZAPIT
	JRST RESETIT

CIR2:	OUTSTR	[ASCIZ/
YOU MUST EITHER COPY FROM OR COPY TO SYSTEM /]
	MOVE A,THISYS
	PUSHJ P,OUTDEC
	PUSHJ P,CRLF
	JRST RESETIT

CIR0:	OUTSTR	[ASCIZ/
"COPY TO" AND "COPY FROM" SYSTEM MUST NOT BE THE SAME.
/]
	JRST RESETIT

BADNAM:	OUTSTR	[ASCIZ/
USER NAME INVALID OR NO UFD/]
	JRST FINERR

PROERR:	OUTSTR	[ASCIZ/
ERROR ON FILE /]
	PUSHJ P,FILOUT
	JRST FINERR

NOSYSN:	OUTSTR	[ASCIZ/
CANNOT GET THIS SYSTEM NUMBER.
CALL Q.A.
/]
	EXIT

FNFERR:	OUTSTR	[ASCIZ/
UNABLE TO LOCATE FILE /]
	PUSHJ P,FILOUT
	JRST FINERR

CHKERR:	HRRZ B,ERR(SIDE)
	JUMPE B,FNFERR
	CAIN B,1
	JRST BADNAM
	CAIN A,2
	JRST PROERR
	OUTSTR	[ASCIZ/
CANNOT OPEN FILE /]
	PUSHJ P,FILOUT
	JRST FINERR
;OUTPUT NAME
NAMOUT:	MOVEI CH,LPAREN
	OUTCHR CH
	MOVE BP,NAMPTR
	MOVEI N,SIZUSR
	TLO F,NOSPACE
NAMO1:	ILDB CH,BP
	JUMPE CH,[TLZE F,NOSPACE
		MOVE C,N
		JRST .+2]
	TLO F,NOSPACE
	SOJG N,NAMO1
	TLZE F,NOSPACE
	SETZ C,
	MOVEI N,SIZUSR
	SUB N,C
	MOVE BP,NAMPTR
	ILDB CH,BP
	ADDI CH,SPACE
	OUTCHR CH
	SOJG N,.-3
	MOVEI CH,COLON
	OUTCHR CH
	POPJ P,0

SYSOUT:	MOVE A,SYS(SIDE)
	PUSHJ P,OUTDEC
	MOVEI CH,RPAREN
	OUTCHR CH
	POPJ P,0

;OUTPUT SIXBIT FILE NAME TO TTY
FILOUT:	MOVE N,FSIZE(SIDE)		;GET LENGTH OF FILE NAME
	MOVE BP,FILPTR
FILO1:	ILDB CH,BP	;GET A SIXBIT CHARAACTERR
	ADDI CH,SPACE	;MAKE ASCII
	OUTCHR CH	;OUTPUT IT TO TTY
	SOJG N,FILO1	;AND GET NEXT
	POPJ P,0

FINERR:	OUTSTR [ASCIZ/ ON SYSTEM /]
	MOVE A,SYS(SIDE)
	PUSHJ P,OUTDEC
	PUSHJ P,CRLF
	JRST GETCMD
; WE HAVE A CIRCUIT BUT NO WAY TO LOSE IT.
DEADCR: TLZ F,MORETR    ;DO NOT TRY ANY MORE XFERS FROM THIS COMMMAND
        JRST GETCMD
; WE WERE UNABLE TO BUILD A CIRCUIT
NOCIR:	PUSHJ P,ZAPCIR  ;CLEAR CIRCUIT SWITCHES
	TLZ F,MORETR	;DO NOT TRY ANY MORE XFERS FROM THIS COMMAND
	HLRZ B,A
	JUMPE B,SUPERR
	CAIL B,ER10NM
	JRST ENX10
	OUTSTR @ERR10(B)
	JRST GETCMD

ENX10:	OUTSTR	[ASCIZ/
UNRECOGNIZED ERROR FROM MONITOR NUMBER /]
	MOVE A,B
COMAXR:	PUSHJ P,OUTDEC
	PUSHJ P,CRLF
	JRST GETCMD
ERR10:	0
	[ASCIZ /
TOO MANY CIRCUITS
/]
	[ASCIZ/
BAD USER NAME
/]
	[ASCIZ/
OUT OF CHANNELS FROM HOST
/]

	[ASCIZ/
NO RESPONSE TO REQUEST
/]
	[ASCIZ/
NO RESPONSE TO STRING
/]
	[ASCIZ/
NO CIRCUIT ESTABLISHED
/]
	[ASCIZ/
ERROR RESPONSE TO REQUEST
/]
ER10NM=.-ERR10

SUPERR:	SKIPE A				;[22]
	 CAIL A,ERSPNM
	  JRST ENXSUP	;UNRECOGNIZED SUP RESPONSE
	PUSHJ P,CRLF
	OUTSTR @ERRSUP(A)
	MOVE A,AUXCIR			;[22]
	SKIPN A				;[22] don't bother if circuit zapped
	  JRST GETCMD			;[22]
	OUTSTR [ASCIZ/, SYSTEM /]	;[22] tell us what system
	JRST COMAXR			;[22]

ENXSUP:	OUTSTR [ASCIZ/
UNRECOGNIZED ERROR FROM SUPERVISOR - NUMBER /]
	JRST COMAXR

ERRSUP:	0
	[ASCIZ/BAD NAME FORMAT/]
	[ASCIZ/NAME NOT IN MUD/]
	[ASCIZ/BAD MUD - CONTACT SQA/]
	[ASCIZ/SYSTEM UNAVAILABLE/]
ERSPNM==.-ERRSUP

CRLF:	OUTSTR [ASCIZ/
/]
	POPJ P,0
SUBTTL - DEFINED WORDS AND POINTERS

;[22] GSYS:	SIXBIT /JOBS/	;[22] left in place for historical
;[22]	SIXBIT /DAT/		;[22] purposes.
;[22]	0
;[22]	XWD 6,214

LUDLOK:	SIXBIT /LUD/
	SIXBIT /SYS/
	0
	0
SYSLST:	IOWD 200,SYSBLK
	0
INTTAB:	0
ZAPRTN: ZAPADR
	0
TIMRTN:	TIMADR
	0
BUFRTN: ZAPIT3
	0
ESCRTN:	ALTTYP
	0
GOTRTN: CH5INT
        0
CH6RTN: CH6INT
        0
CH7RTN: 0
        0
CH8RTN: 0
        BLOCK ^D54
LCOPT:
LC:	OFFLC

;POINTERS
NAMPTR:	POINT 6,UNAME(SIDE)
STRPTR:	POINT 7,AUXSTR
FILPTR:	POINT 6,FNAME(SIDE)
WDPTR:	POINT 6,WD

PDLPTR:	IOWD 40,PDL
INBPTR:	POINT 7,INBLK
pasptr: point 7,passwd
usrptr: point   6,usrnam
cfnptr: point   6,comfil
truptr:	point	7,trustr
MASK:	770000000000	;MASK FOR FIRST CHARACTER ONLY
NO:	560000000000	;N
YES:	710000000000	;Y
SUBTTL - STORAGE LOCATIONS

sysno:  block 1
INFIL:  3
        0
INFNAM: 0
INFEXT: 0
INBF: BLOCK 3
OUTFIL: 3
        0
OUTNAM: 0
OUTEXT: SIXBIT /OUT/
OUTBF: BLOCK 3
THISYS:	0	;THIS SYSTEM NO.
THISUS:	BLOCK 2	;CONTROLLING USER
FIRPTR:	0	;FIRST POINTER FOR "TO" SIDE
LSTTPT:	0	;LAST "TO" POINTER
LSTFPT:	0	;LAST "FROM" POINTER
LSTTCH:	0	;LAST "TO" CHARACTER
LSTFCH:	0	;LAST "FROM" CHARACTER
INBLK:	BLOCK MAXWDS	;BUFFER FOR TTY LINE
OLDCMD:	BLOCK MAXWDS	;BACKUP BUFFER FOR PREVIOUS TTY LINE
passwd: block 5         ;Buffer for the password
pwdend: .-1             ;The last word of the password string
usrnam: block 2
comfil: block 1
trustr:	block 6		; for building tru string
;TO BE ZEROED BETWEEN RUNS
ZERO:	0
NOPRMT:	0	;0 IF PROMPT IS NEDED
DSIZE:	0	;SIZE OF DONOR FILE NAME
DBLK:	BLOCK SYS+MAXFIL
RSIZE:	0	;SIZE OF RECIP FILE NAME
RBLK:	BLOCK SYS+MAXFIL
AUXSTR:	BLOCK SIZLIN/5

SYSBLK:	BLOCK ^D161
SIZDON:	0	;SIZE OF DONOR FILE IN WORDS.
RETZAP:	0	;ADDRESS FOR RETURN FROM BUFFER LOST
SAVA:	0	;SAVE AC ON INTERRUPT
RETFLG:	0	;RETURN TO GETCMD IF = 0, DISMISS IF # 0
ZEREND:	0
PDL:	BLOCK 40
END	START
  a