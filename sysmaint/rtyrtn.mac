TITLE RTYRTN - VERSION 18 DEC  78 - - ROYALTY ACCOUNTING ROUTINE
        EXTERN DYNDV., JOBJDA
        ENTRY   RTYRTN, VATRTY, RTYERR
        OPDEF   PUTROY [CALLI -124]
; * = RESERVED LOCATIONS
; ** = CONSTANTS

.GTLOG==-24                             ;LOGGED IN NODE, PORT INFORMATION
.FORFA==36                              ;READ FRAME ATTRIBUTES

        RADIX   ^D10    ;* * * NOTE: DECIMAL MODE SET HERE * * *

X==0
LENI==0
LENZ==0

        DEFINE  RESRVE (A,B)
        <A==X
        X==X+B>

        DEFINE  VAR ( A )
        <IRP    A
        <I'A==VARI+LENI
        C'A==VARC+LENI
        LENI==LENI+1>>

        DEFINE  VARZ ( A )
        <IRP    A
        <A==VARZ+LENZ
        LENZ==LENZ+1>>

        DEFINE  S ( A )<A(14)>

        DEFINE  CPUTN ( A,B,C,D,E )
<       PUSHJ   STK,PUTNR
        BYTE    (10)A(4)C(4)E(18)D
        Y1==B-1
        Y1==Y1*7
        Y2==Y1/35
        Y3==Y2*35
        Y3==Y1-Y3
        IFN Y3,<
        POINT 7,S (CTRLB+Y2),Y3-1 >
        IFE Y3,<
        POINT 7,S (CTRLB+Y2) > >

        DEFINE  CPUTCH ( A, B )
        <MOVEI  1,A
        Y1==B-1
        Y1==Y1*7
        Y2==Y1/35
        Y3==Y2*35
        Y3==Y1-Y3
        IFN Y3,<
        MOVE    2, [ POINT 7,S (CTRLB+Y2),Y3-1 ]>
        IFE Y3,<
        MOVE    2, [ POINT 7,S (CTRLB+Y2) ]>
        ADD     2,S BUFIDX
        IDPB    1,2>

;* DEFINE RESERVED LOCATIONS
        RESRVE  (RETLOC,1)      ;DO NOT MOVE
        VAR     <TRU,NODE,PORT,PCN,MONTH,JIFF,DAYS>
        VARZ    <ACCPRU,ACCTRU,NCALLS,NWRITE,CHGDPRU>
        VARZ    <PACCTRU,ETIME,CUMVAT,VAT>
        RESRVE  (VARI,LENI)
        RESRVE  (VARC,LENI)
        RESRVE  (VARZ,LENZ)
        RESRVE  (CGPN,1)
        RESRVE  (CDATE,1)
        RESRVE  (CTIME,1)
        RESRVE  (CFPN,1)
        RESRVE  (CPPN,1)
        RESRVE  (CJOB,1)
        RESRVE  (RTYPE,1)
        RESRVE  (CHGCODE,1)
        RESRVE  (RCHAN,1)
        RESRVE  (CHAN,1)
        RESRVE  (PRURAT,1)
        RESRVE  (CTRLB,BLKSIZ)
        RESRVE  (CHGIDX,1)
        RESRVE  (BUFIDX,1)
        RESRVE  (RECTYPE,1)
        RESRVE  (PPN2,1)
        RESRVE  (Q1,1)
        RESRVE  (Q2,1)
        RESRVE  (Q3,1)
        RESRVE  (TRULIM,1)
        RESRVE  (SIZE,1)
        RESRVE  (STR,3)
        RESRVE  (VER,1)
        RESRVE  (WTFLAG,1)
        RESRVE  (ERCODE,1)
        RESRVE  (SEQCHAR,1)
        RESRVE  (ANAME,1)
        RESRVE  (APPN,1)
        RESRVE  (APROT,1)
        RESRVE  (MINSEQ,1)
        RESRVE  (MAXSEQ,1)
        RESRVE  (NODEADR,1)
        RESRVE  (PCNADR,1)
        RESRVE  (BASEPCN,1)
        RESRVE  (NNODE,1)
        RESRVE  (NSIZE,1)
        RESRVE  (E,2)
;*END
;** DEFINE CONSTANTS
STK==15
BUFL==32
BLKSIZ==128
MINPCN==10000
MAXPCN==19999
MAXCALLS==999999
MAXWRITE==999999
MINCHAN==1
MAXCHAN==15
HOMEFILES==^O2000000000
ZERO==0
TSCALE==10000
PSCALE==10000
MAXPRU==127
CARRET==^O15
LNFEED==^O12
EOLIT==0
VERSION==308
C.PCN==CTRLB
C.PPN1==CTRLB+1
C.NAME==CTRLB+2
C.PPN2==CTRLB+3
C.LIM==CTRLB+6
C.CHRG==CTRLB+7
C.WT==CTRLB+8
C.PRI==CTRLB+8
C.RATE==CTRLB+10
ACTG==CTRLB
A.CNT==ACTG
A.PPN==ACTG+1
A.NAME==ACTG+2
A.EXT==ACTG+3
A.DT1==ACTG+3
A.PROT==ACTG+4
A.DT2==ACTG+4
A.SIZE==ACTG+5
HEAD==CTRLB
H.NODE==HEAD
H.PCN==HEAD+1
H.UPPN==HEAD+2
H.DPPN==HEAD+3
H.DATE==HEAD+4
H.TIME==HEAD+5
H.NNODE==HEAD+6
H.NPCN==HEAD+7
H.BPCN==HEAD+8
H.BNSET==HEAD+9
H.ENSET==HEAD+10
H.MINSEQ==HEAD+11
H.MAXSEQ==HEAD+12
;**END
;*** BEGIN PROCEDURES

RTYRTN: MOVEI    4, 0           ; NORMAL CALLS DO NOT HAVE VALUE-ADDED-TRUS
VATRTY: HRRZ    14,15           ;COPY STACK ADDRESS
        ADDI    15,X            ;ALLOW ROOM FOR STORAGE, REST IS STACK
        PUSH    STK,0(14)
        MOVEM   1,S RTYPE
        MOVEM   2,S CPCN
        MOVEM   3,S RCHAN
        MOVEM   4,S VAT
        PUSHJ   STK,CDATA       ;GET ALL DATA VALUES
        MOVE    1,S RTYPE
        CAIE    1,1             ;RTYPE=1
        JRST    RMAIN1          ;NO
        MOVE    1,S CPCN
        CAIG    1,MAXPCN      ;CHECK RANGE OF PCN
        CAIGE   1,MINPCN
        JRST    [ MOVEI 1,2
                  JRST  RTYERR ]
        CAME    1,S IPCN
        JRST    RMAIN2
        MOVE    1,S CNODE
        CAME    1,S INODE
        JRST    RMAIN2
        MOVE    1,S CPORT
        CAME    1,S IPORT
        JRST    RMAIN2
        MOVE    1,S CMONTH
        CAME    1,S IMONTH
        JRST    RMAIN2
        MOVEI   1,2             ;INDICATE RECORD TYPE = 2
        PUSHJ   STK,CHGPRU      ;CHARGE PRUS
        POPJ    STK,            ;RETURN TO HOST PROGRAM
RMAIN1: CAIE    1,2             ;RTYPE=2
        JRST    [ MOVEI 1,1
                  JRST  RTYERR ]
        PUSHJ   STK,TERM
        POPJ    STK,
RMAIN2: PUSHJ   STK,TERM        ;TERMINATE OLD PCN
        PUSHJ   STK,INITR       ;INITIALIZE NEW PCN
        POPJ    STK,            ;RETURN TO HOST POGRAM

INITR:  MOVEI   1,VERSION
        MOVEM   1,S VER
        PUSHJ   STK,RTYCTL      ;GET INFORMATION FROM CONTROL FILE
        PUSHJ   STK,OPACTG      ;OPEN ACCOUNTING FILE
        PUSHJ   STK,CDATA       ;RE-GET DATA TO EXCLUDE START-UP
                                ;OVERHEAD FROM SURCHARGE
        HRLI    1,S VARC
        HRRI    1,S VARI
        HRRZI   2,S (VARI+LENI-1)
        XOR     2,[ BLT 1,0 ]
        XCT     2
;ZERO OUT CERTAIN VARIABLES HERE
        MOVEI   1,LENZ
        MOVEI   2,S VARZ
INITR1: SETZM   0,0(2)
        AOS     2,2
        SOJG    1,INITR1
        PUSHJ   STK,BLDREC
        MOVEI   1,1             ;INDICATE RECORD TYPE 1
        PUSHJ   STK,CHGPRU
        POPJ    STK,

TERM:   SKIPN   0,S IPCN
        POPJ    STK,            ;IPCN=0 SO NO PREVIOUS PCN
        MOVEI   1,3             ;INDICATE RECORD TYPE 3
        PUSHJ   STK,CHGPRU
        PUSHJ   STK,CLACTG      ;CLOSE ACCOUNTING FILE
        SETZM   0,S IPCN
        POPJ    STK,

CHGPRU: MOVEM   1,S RECTYPE
        MOVE    1,S CDAYS
        SUB     1,S IDAYS
        IMULI   1,86400         ;SECONDS IN ONE DAY
        MOVE    2,S CJIFF
        SUB     2,S IJIFF
        IDIVI   2,60            ;JIFFIES PER SECOND
        ADD     1,2
        MOVEM   1,S ETIME         ;ELAPSED TIME IN SECONDS
        AOS     2,S NCALLS
        CAMLE   2, [ MAXCALLS ]
        MOVE    2, [ MAXCALLS ]
        MOVEM   2,S NCALLS
        MOVE    1,S CTRU
        SUB     1,S ITRU
        SUB     1,S CHGDPRU
        CAIGE   1,0
        SETZ    1,1
        MOVEM   1,S ACCTRU
        MOVE    1,S CHGCODE
        CAIG    1,CHRGTE-CHRGTB
        CAIG    1,0
        JRST    [ MOVEI 1,20
                  PUSHJ STK,RTYERR ]
        JRST    .(1)
CHRGTB: JRST    CHRG1
        JRST    CHRG2
CHRGTE=.

CHRG1:  MOVE    1,S VAT         ; CALCULATE VALUE-ADDED-TRUS TOTAL
        ADDM    1,S CUMVAT
        MOVE    1,S ACCTRU      ; NOW CALCULATE SCALED MARK-UP
        ADD     1,S CUMVAT      ; INCLUDING VALUE-ADDED-TRUS AS 'ACTUAL' TRUS
        MUL     1,S PRURATE
        MOVEI   3,TSCALE
        PUSHJ   STK,DROUND
        ADD     1,S CUMVAT      ; ADD IN VALUE-ADDED-TRUS
        MOVEM   1,S ACCPRU
CHGP4:  SUB     1,S CHGDPRU
        CAIGE   1,0
        SETZ    1,1
        ASHC    1,-35
        MOVEI   3,PSCALE
        PUSHJ   STK,DROUND
        MOVEM   1,S Q1
        IMULI   1,PSCALE
        ADDM    1,S CHGDPRU
CHGP1:  MOVE    1,S Q1
        JUMPLE  1,CHGP2
        CAILE   1,MAXPRU
        MOVEI   1,MAXPRU
        MOVEM   1,S Q2
        MOVNS   1,1
        ADDM    1,S Q1
        MOVE    1,S Q2
        PUSHJ   STK,RTYUUO
        JRST    CHGP1

CHRG2:  MOVE    1,S RECTYPE
        CAIE    1,2
        JRST    CHGP3
        MOVE    1,S PRURATE
        ADDB    1,S ACCPRU
        JRST    CHGP4

CHGP2:  MOVE    1,S RECTYPE
        CAIE    1,2
        JRST    CHGP3
        MOVE    2,S ACCTRU
        SUB     2,S PACCTRU
        CAML    2,S TRULIM
CHGP3:  PUSHJ   STK,WTREC
        POPJ    STK,

BLDREC: MOVEI   1,BUFL
        MOVEI   2,S CTRLB
        ADD     2,S BUFIDX
        MOVE    3, [ ASCII "     " ]
BLDR1:  MOVEM   3,0(2)
        AOS     2,2
        SOJG    1,BLDR1
        CPUTN   VER, 1, 10, 0, 4
        CPUTN   CPCN, 7, 10, 0, 5
        CPUTN   CPPN, 12, 8, 0, 12
        CPUTN   CNODE, 24, 8, 0, 4
        CPUTN   CPORT, 28, 8, 0, 4
        CPUTN   CHGIDX, 32, 10, 0, 2
        CPUTN   CDATE, 34, 10, 0, 6
        CPUTN   CTIME, 40, 10, 0, 4
        CPUTN   TRULIM, 78, 10, 10000, 12
        CPUTN   PRURATE, 90, 10, 10000, 12
        CPUTN   CJOB, 102, 10, 0, 3
        CPUTN   NSIZE, 117, 10, 0, 3
        CPUTN   CHGCODE, 120, 10, 0, 2
        CPUTN   CGPN, 122, 8, 0, 12
        CPUTN   CFPN, 134, 8, 0, 12
        CPUTCH  CARRET, 159
        CPUTCH  LNFEED, 160
        CPUTCH  EOLIT, 161
        POPJ    STK,

MODREC: MOVEM   1,S Q1          ;SAVE RECORD TYPE
        CPUTN   Q1, 5, 10, 0, 2
        CPUTN   ETIME, 44, 10, 0, 10
        CPUTN   ACCTRU, 54, 10, 10000, 12
        CPUTN   CHGDPRU, 66, 10, 10000, 12
        CPUTN   NCALLS, 105, 10, 0, 6
        CPUTN   NWRITE, 111, 10, 0, 6
; THIS CODE PRINTS THE RTYCHG RECORD IF RECORD TYPE=3
;       MOVEI   1,S CTRLB
;       ADD     1,S BUFIDX
;       MOVE    2,S Q1
;       CAIN    2,3
;       PUSHJ   STK,RTYMSG
        POPJ    STK,

RTYCTL: PUSHJ   STK,RTYCH
        PUSHJ   STK,RTYINT
        HRRI    1,S ACTG
        HRLI    1,RTYCNM
        BLT     1,S (ACTG+3)
        MOVE    1,S CHAN
        MOVEI   2,S ACTG
        PUSHJ   STK,RTYLKP
        JUMPN   1, [ MOVEI 1,16
                     PUSHJ STK,RTYERR ]
        PUSHJ   STK,RTYHDR
        PUSHJ   STK,NODSET
        MOVEI   1,BLKSIZ
        MOVEI   2,S CTRLB
        PUSHJ   STK,RTYSIO
        MOVE    1,S CHAN
        MOVE    2,S CPCN
        SUB     2,S BASEPCN
        ADD     2,S PCNADR
        PUSHJ   STK,RTYUSI
        MOVE    1,S CHAN
        PUSHJ   STK,RTYINP
        JUMPN   1, [ MOVEI 1,11
                     PUSHJ STK,RTYERR ]
        PUSHJ   STK,RTYRLS      ;CLOSE FILE AND RELEASE CHANNEL
        MOVE    1,S C.PCN
        CAME    1,S CPCN
        JRST    [ MOVEI 1,15
                  PUSHJ STK,RTYERR ]
        SKIPN   1,S C.PPN2
        JRST    [ MOVEI 1,6
                  PUSHJ STK,RTYERR ]
        MOVEM   1,S PPN2
        MOVE    1,S C.LIM
        MOVEM   1,S TRULIM
        MOVEI   1,S C.RATE
        ADD     1,S CHGIDX
        MOVE    1,0(1)
        MOVEM   1,S PRURATE
        HLRZ    1,S C.WT
        MOVEM   1,S WTFLAG
        MOVE    1,S C.CHRG
        MOVEM   1,S CHGCODE
        PUSHJ   STK,PROT
        MOVEM   1,S APROT
        PUSHJ   STK,PPN
        MOVEM   1,S APPN
        MOVE    1,S C.NAME
        MOVEM   1,S ANAME
        POPJ    STK,

RTYHDR: MOVEI   1,BLKSIZ
        MOVEI   2,S CTRLB
        PUSHJ   STK,RTYSIO
        MOVE    1,S CHAN
        MOVEI   2,1
        PUSHJ   STK,RTYUSI
        MOVE    1,S CHAN
        PUSHJ   STK,RTYINP
        JUMPN   1, [ MOVEI 1,13
                     PUSHJ STK,RTYERR ]
        MOVE    1,S H.BPCN
        MOVEM   1,S BASEPCN
        MOVE    1,S H.MINSEQ
        MOVEM   1,S MINSEQ
        MOVE    1,S H.MAXSEQ
        MOVEM   1,S MAXSEQ
        MOVE    1,S H.PCN
        IDIVI   1,BLKSIZ
        AOS     1,1
        MOVEM   1,S PCNADR
        MOVE    1,S H.NODE
        IDIVI   1,BLKSIZ
        AOS     1,1
        MOVEM   1,S NODEADR
        MOVE    1,S H.NNODE
        MOVEM   1,S NNODE
        POPJ    STK,

NODSET: SETZ    1,1             ;BUILD COMPARE WORD FOR NODE TABLE
        LSHC    1,-13           ;NODE SET NUMBER AND BEGINNING
                                ; PORT NUMBER AS ZERO
        MOVE    1,S CPORT
        LSHC    1,-8            ;ENDING PORT NUMBER AS CPORT
        MOVE    1,S CNODE
        LSHC    1,-14           ;NODE NUMBER AS CNODE
        SETZ    1,1
        LSHC    1,-1            ;BIT ZERO ALWAYS ZERO
        MOVEM   2,S Q1          ;SAVE FOR LATER COMPARE TO NODE TABLE
        MOVE    1,S NNODE
        MOVEM   1,S Q2
        SETZM   1,S NSIZE
NODE1:  MOVEI   1,BLKSIZ        ;MAX NSIZE OF BUFFER
        CAMLE   1,S Q2          ;COUNT OF NODE TABLE ENTRIES NOT CHECKED
        MOVE    1,S Q2          ;TAKE COUNT IF SMALLER THAN BLKSIZ
        JUMPLE  1, [ MOVEI 1,19
                     PUSHJ STK,RTYERR ]
        MOVEM   1,S Q3
        MOVNS   1,1
        ADDM    1,S Q2
        MOVE    1,S Q3
        MOVEI   2,S CTRLB
        PUSHJ   STK,RTYSIO
        MOVE    1,S CHAN
        MOVE    2,S NODEADR
        PUSHJ   STK,RTYUSI
        MOVE    1,S CHAN
        PUSHJ   STK,RTYINP
        JUMPN   1, [ MOVEI 1,18
                     PUSHJ STK,RTYERR ]
        AOS     0,S NSIZE
        MOVE    1,S Q3
        MOVEI   2,S CTRLB
        MOVE    3,S Q1
NODE2:  CAMG    3,0(2)
        JRST    NODE3
        AOS     2,2
        SOJG    1,NODE2
        AOS     1,S NODEADR
        JRST    NODE1
NODE3:  LDB     1, [ POINT 14,0(2),14 ]
        CAME    1,S CNODE
        JRST    [ MOVEI 1,19
                  PUSHJ STK,RTYERR ]
        LDB     1, [ POINT 8,0(2),22 ]
        CAMGE   1,S CPORT
        JRST    [ MOVEI 1,19
                  PUSHJ STK,RTYERR ]
        LDB     1, [ POINT 8,0(2),30 ]
        CAMLE   1,S CPORT
        JRST    [ MOVEI 1,19
                  PUSHJ STK,RTYERR ]
        LDB     1, [ POINT 5,0(2),35 ]
        MOVEM   1,S CHGIDX
        POPJ    STK,

        ;WTFLAG VALUES
        ;  0 == WRITE ASCII FILE ONLY
        ;  1 == DO NOT WRITE ANYTHING
        ;  2 == WRITE ASCII FILE AND STREAM RECORD
        ;  3 == WRITE STREAM RECORD ONLY
OPACTG: MOVE    1,S WTFLAG      ;SAME LOGIC USED AT CLACTG
        CAIN    1,0
        JRST    OPAC1           ;WRITE ASCII FILE ONLY
        CAIN    1,1
        JRST    OPACNO          ;DO NOT WRITE ANYTHING
        MOVEI   2,0             ;SET ROYALTY RECORD FLAG TO ZERO
        IOR     2,S CPCN        ;SET ROYALTY RECORD PRODUCT ID
        PUTROY  2,
        JRST    [ MOVEI 1,21
                  PUSHJ STK,RTYERR ]
        CAIE    1,3             ;SUCCESS RETURN, CHECK IF STREAM ONLY
        JRST    OPAC1           ;ASCII ALSO
OPACNO: SETZM   0,S BUFIDX
        POPJ    STK,
OPAC1:  PUSHJ   STK,RTYCH
        PUSHJ   STK,RTYINT
        MOVEI   1,5
        MOVEM   1,S A.CNT
        MOVE    1,S CMONTH
        PUSHJ   STK,RTYMTH
        HRLM    1,S A.EXT
        MOVE    1,S APROT
        DPB     1, [ POINT 9,S (A.PROT),8 ]
        MOVE    1,S APPN
        MOVEM   1,S A.PPN
        MOVE    1,S MINSEQ
        MOVEM   1,S SEQCHAR
OPAC2:  MOVE    3,S CJOB
        IDIVI   3,10
        ADDI    4,^O20
        MOVE    1,4
        LSHC    1,-6
        IDIVI   3,10
        ADDI    4,^O20
        MOVE    1,4
        LSHC    1,-6
        ADDI    3,^O20
        MOVE    1,3
        LSHC    1,-6
        MOVE    1,S SEQCHAR
        LSHC    1,-6
        MOVE    1,S ANAME
        LSHC    1,-12
        MOVEM   2,S A.NAME
        MOVE    1,S CHAN
        MOVEI   2,S ACTG
        PUSHJ   STK,RTYLKP
        MOVE    1,S A.SIZE
        MOVEM   1,S SIZE
        MOVE    1,S APROT
        DPB     1, [ POINT 9,S (A.PROT),8 ]
        SETZ    1,1
        HRRM    1,S A.DT1
        DPB     1, [ POINT 27,S (A.DT2),35 ]
        MOVE    1,S CHAN
        MOVEI   2,S ACTG
        PUSHJ   STK,RTYENT
        JUMPE   1,OPAC3
        AOS     1,S SEQCHAR
        CAMG    1,S MAXSEQ
        JRST    OPAC2
        MOVEI   1,17
        PUSHJ   STK,RTYERR
OPAC3:  MOVE    1,S SIZE
        IDIVI   1,BLKSIZ
        AOS     1,1
        MOVEM   1,S SIZE
        MOVEI   1,BLKSIZ
        MOVEI   2,S CTRLB
        PUSHJ   STK,RTYSIO
        MOVE    1,S CHAN
        MOVE    2,S SIZE
        PUSHJ   STK,RTYUSI
        MOVEI   1,BLKSIZ
        MOVEI   2,S CTRLB
OPAC4:  SETZM   0,0(2)
        AOS     2,2
        SOJG    1,OPAC4
        MOVE    1,S CHAN
        PUSHJ   STK,RTYINP
        SETZB   1,S BUFIDX
OPAC5:  ADDI    1,S CTRLB
        SKIPE   0,0(1)
        JRST    [ PUSHJ STK,ADVREC
                  MOVE  1,S BUFIDX
                  JRST  OPAC5 ]
        MOVE    1,S BUFIDX
        ADDI    1,BUFL
        MOVEI   2,S CTRLB
        PUSHJ   STK,RTYSIO
        POPJ    STK,

CLACTG: MOVE    1,S WTFLAG      ;SEE COMMENT PRECEEDING OPACTG
        CAIN    1,0
        JRST    CLAC1           ;WERE WRITING ASCII FILE ONLY
        CAIN    1,1
        JRST    CLAC2           ;DID NOT WRITE ANYTHING
        HRLZI   2,^O400000      ;SET ROYALTY RECORD FLAG TO ONE
        IOR     2,S IPCN        ;SET ROYALTY RECORD PRODUCT ID
        PUTROY  2,              ;WRITE STREAM RECORD
        JRST    [ MOVEI 1,22
                  PUSHJ STK,RTYERR ]
        CAIE    1,3             ;SUCCESS RETURN, CHECK IF STREAM ONLY
CLAC1:  PUSHJ   STK,RTYRLS
CLAC2:  POPJ    STK,

RTYCH:  SKIPL   1,S RCHAN
        JRST    RTYCH3
        MOVEI   1,MAXCHAN
RTYCH2: SKIPE   0,JOBJDA(1)
        JRST    RTYCH1
        SKIPE   0,DYNDV.(1)
        JRST    RTYCH1
        MOVE    2,SPEC1
        MOVEM   2,DYNDV.(1)
RTYCH3: LSH     1,23
        MOVEM   1,S CHAN
        POPJ    STK,
RTYCH1: SOS     1,1
        CAIL    1,MINCHAN
        JRST    RTYCH2
        MOVEI   1,14
        PUSHJ   STK,RTYERR

CDATA:  PUSHJ   STK,RTYPPN      ;GET PPN OF USER LOGGED IN
        MOVEM   1,S CPPN
        PUSHJ   STK,RTYJOB      ;GET JOB NUMBER
        MOVEM   1,S CJOB
        PUSHJ   STK,RTYGPN      ;GET PPN OF DIRECTORY WHERE USER IS
        MOVEM   1,S CGPN
        PUSHJ   STK,RTYTRU      ;GET CURRENT TRUS
        MOVEM   1,S CTRU
        PUSHJ   STK,RTYFPN      ;GET CURRENT FPN
        MOVEM   1,S CFPN
        PUSHJ   STK,RTYNODE     ;GET CURRENT NODE AND PORT
        JUMPE   1,DETCHD        ;USE CURRENT VALUES IF DETACHED
        MOVEM   1,S CNODE
        MOVEM   2,S CPORT
DETCHD:	PUSHJ   STK,RTYDAT      ;GET CURRENT DATE AND TIME
        MOVEM   1,S CDAYS
        MOVEM   2,S CJIFF
        IMULI   1,4             ;MULTIPLY DAYS BY 4
        IDIVI   1,1461          ;CORRECTS FOR LEAP YEARS (365*4+1)
        ADDI    1,64            ;ADJUST FOR PDP10 BASE YEAR OF 1964
        MOVEM   1,S Q1
        IDIVI   2          ;COMPUTE DAYS SINCE JAN 0 THIS YEAR
        ADDI    2,1             ;ADJUST TO JAN 1 THIS YEAR
        MOVEM   2,S Q2
        SETZ    1,1
        MOVEM   1,S CMONTH
CDATA2: AOS     1,S CMONTH
        MOVE    2,S Q1
        PUSHJ   STK,LDAY        ;GET NUMBER OF DAYS IN THIS MONTH
        MOVNS   1,1             ;NEGATE
        ADD     1,S Q2          ;ADD NUMBER OF DAYS REMAINING
        JUMPLE  1,CDATA1        ;NOW HAVE CORRECT MONTH AND DAY
        MOVEM   1,S Q2
        JRST    CDATA2
CDATA1: MOVE    1,S Q1
        IMULI   1,10000         ;COMPUTE YYMMDD DATE
        MOVE    2,S CMONTH
        IMULI   2,100
        ADD     1,2
        ADD     1,S Q2
        MOVEM   1,S CDATE
        MOVE    1,S CJIFF
        IDIVI   1,3600          ;CONVERT JIFFIES TO MINUTES
        IDIVI   1,60            ;MINUTES/60=HOURS+MINUTES
        IMULI   1,100           ;CONVERT TO FORMAT HHMM
        ADD     1,2
        MOVEM   1,S CTIME
        POPJ    STK,

PPN:    MOVE    1,S PPN2
        POPJ    STK,

PROT:   MOVEI   1,^O047         ;ALL RD NO
        MOVE    2,S C.PPN2
        CAME    2,S CFPN
        MOVEI   1,^O100         ;CP ALL ALL
        POPJ    STK,

DROUND: DIV     1,3
        AOS     3
        ASH     3,-1
        CAML    2,3
        AOS     1
        POPJ    STK,

ADVREC: MOVEI   1,BUFL
        ADDB    1,S BUFIDX
        CAIGE   1,BLKSIZ
        POPJ    STK,
        AOS     1,SIZE
        SETZB   1,S BUFIDX
        MOVEI   2,BLKSIZ
        MOVEI   3,S CTRLB
ADVR1:  SETZM   0,0(3)
        AOS     3,3
        SOJG    2,ADVR1
        POPJ    STK,

WTREC:  MOVE    1,S WTFLAG      ;SEE COMMENT PRECEEDING OPACTG
        CAIE    1,0             ;WRITE ASCII ONLY
        CAIN    1,2             ;WRITE ASCII AND STREAM
        JRST    WTREC1
        POPJ    STK,            ;RETURN
WTREC1: MOVE    2,S ACCTRU
        MOVEM   2,S PACCTRU
        AOS     2,S NWRITE
        CAMLE   2, [ MAXWRITE ]
        MOVE    2, [ MAXWRITE ]
        MOVEM   2,S NWRITE
        MOVE    1,S RECTYPE
        PUSHJ   STK,MODREC
        MOVE    1,S CHAN
        MOVE    2,S SIZE
        PUSHJ   STK,RTYUSO
        MOVE    1,S BUFIDX
        ADDI    1,BUFL
        MOVEI   2,S CTRLB
        PUSHJ   STK,RTYSIO
        MOVE    1,S CHAN
        PUSHJ   STK,RTYOUT
        JUMPN   1, [ MOVEI 1,12
                     PUSHJ STK,RTYERR ]
        POPJ    STK,            ;RETURN

DAYMTH: BYTE    (18)31,'JAN'
        BYTE    (18)28,'FEB'
        BYTE    (18)31,'MAR'
        BYTE    (18)30,'APR'
        BYTE    (18)31,'MAY'
        BYTE    (18)30,'JUN'
        BYTE    (18)31,'JUL'
        BYTE    (18)31,'AUG'
        BYTE    (18)30,'SEP'
        BYTE    (18)31,'OCT'
        BYTE    (18)30,'NOV'
        BYTE    (18)31,'DEC'

RTYMTH: CAIG    1,12            ;CHECK MONTH FOR RANGE 1-12
        CAIGE   1,1
        JRST    [ MOVEI 1,10
                  JRST  RTYERR ]
        HRRZ    1,DAYMTH-1(1)   ;FETCH RIGHT HALF OF WORD
        POPJ    STK,

LDAY:   CAIG    1,12            ;CHECK MONTH FOR RANGE 1-12
        CAIGE   1,1
        JRST    [ MOVEI 1,9
                  JRST  RTYERR ]
        IDIVI   2,4             ;YEAR/4 FOR LEAP YEAR TEST
        HLRZ    1,DAYMTH-1(1)   ;LOAD LEFT HALF (-1 TO RANGE 0-11)
        CAIE    1,28            ;IS IT FEBRUARY (28 DAYS)
        POPJ    STK,            ;NO, SO RETURN
        CAIN    3,0             ;IS IT LEAP YEAR (YEAR/4=0)
        ADDI    1,1             ;YES, SO ADD ONE TO NUMBER OF DAYS
        POPJ    STK,

;R1=NUMBER, R2=STRING POINTER, R3=BASE, R4=SCALE
;RETURNS COUNT OF CHARACTERS IN R1, EXCLUDING EOLIT
;IF NUMBER IS NEGATIVE, PUTS LEADING MINUS SIGN (INCLUDED IN COUNT)
CNTSC:  PUSH    STK, [ 0 ]      ;PUT A ZERO ON STACK
        SETZ    9,9             ;INDICATES POSITIVE NUMBER
        JUMPGE  1,CNTSC1        ;TEST FOR NEGATIVE NUMBER
        MOVNS   1,1             ;NO, SO MAKE NUMBER POSITIVE
        MOVEI   9,"-"           ;CHAR 'MINUS SIGN'
CNTSC1: MOVE    5,1
        JUMPLE  4,CNTSC2        ;TEST FOR SCALE
        IDIV    5,4             ;YES, SO PROCESS FRACTION FIRST
        MOVE    8,3
CNTSC4: CAMLE   8,4             ;TEST FOR TERMINATION OF FRACTION
        JRST    CNTSC3
        IDIV    6,3
        ADDI    7,"0"           ;CHAR 'ZERO'
        PUSH    STK,7
        IMUL    8,3             ;MULTIPLY BASE BY BASE UNTIL > SCALE
        JRST    CNTSC4
CNTSC3: PUSH    STK, [ ^O56 ]   ;CHAR 'DECIMAL POINT'
CNTSC2: IDIV    5,3             ;PROCESS WHOLE NUMBER
        ADDI    6,"0"           ;CHAR 'ZERO'
        PUSH    STK,6
        JUMPG   5,CNTSC2        ;CONTINUE WHILE WHOLE > ZERO
        MOVNI   1,1             ;SET CHAR COUNT TO -1, ALLOWS FOR EOLIT
        CAIE    9,0             ;TEST FOR MINUS SIGN TO BE ADDED
        PUSH    STK,9           ;PUT MINUS SIGN INTO STACK
CNTSC5: POP     STK,3           ;GET CHAR FROM STACK
        IDPB    3,2             ;PUT CHAR INTO STRING
        AOS     1,1             ;INCREMENT CHAR COUNT
        JUMPN   3,CNTSC5        ;STOP ON ZERO
        POPJ    STK,            ;RETURN WITH CHAR COUNT IN R1

PUTNR:  MOVE    6,0(STK)
        MOVEI   1,2
        ADDM    1,0(STK)
        LDB     1,[ POINT 10,0(6),9 ]
        ADDI    1,S 0
        MOVE    1,0(1)
        MOVE    2,1(6)
        LDB     3,[ POINT 4,0(6),13 ]
        HRRZ    4,0(6)
        LDB     5,[ POINT 4,0(6),17 ]
PUTNR1: ADD     2,S BUFIDX      ;ADJUST FOR RECORD OFFSET INTO BUFFER
        JRST    PUTN            ;JUMP TO PUTN ROUTINE DIRECTLY

;R1=NUMBER, R2=BEGINNING BYTE POINTER, R3=BASE, R4=SCALE, R5=MAX LENGTH
PUTN:   PUSH    STK,2           ;SAVE BYTE POINTER
        PUSH    STK,5           ;SAVE MAX LENGTH
        MOVE    2, [ POINT 7,S (STR-1),35 ]
        PUSHJ   STK,CNTSC
        MOVE    2, [ POINT 7,S (STR-1),35 ]
        POP     STK,5           ;RESTORE MAX LENGTH
        POP     STK,4           ;RESTORE BYTE POINTER
        CAMLE   1,5             ;COMPARE CHAR COUNT TO MAX LENGTH
        PUSHJ   STK,[ MOVE 1,5
                      MOVE 2, [ POINT 7, [ ASCII "***************" ] ]
                      POPJ STK, ]
        SUB     5,1             ;COMPUTE NUMBER OF LEADING BLANKS
        MOVEI   3," "           ;CHAR 'BLANK'
PUTN2:  SOJL    5,PUTN1         ;TEST IF LEADING BLANKS DONE
        IDPB    3,4             ;PUT BLANK INTO BUFFER
        JRST    PUTN2
PUTN1:  SOJL    1,PUTN3         ;TEST IF NUMBER STRING XFER DONE
        ILDB    3,2             ;GET CHAR FROM NUMBER STRING
        IDPB    3,4             ;PUT CHAR INTO BUFFER
        JRST    PUTN1
PUTN3:  POPJ    STK,            ;RETURN

;ERROR CODES -
;  1 - RECORD TYPE ARGUMENT OUT OF RANGE
;  2 - PROGRAM CODE NUMBER ARGUMENT OUT OF RANGE
;  3 - TYMCHG UUO FAILURE
;  4 - ERROR ON GETTAB FOR GMT DATE
;  5 - ERROR ON GETTAB FOR GMT JIFFIES
;  6 - PPN2 IN CONTROL FILE IS ZERO
;  7 - ERROR ON INIT OF I/O CHANNEL
;  8 - ERROR ON GETTAB FOR JBTAUN, PPN FOR THIS JOB
;  9 - MONTH OUT OF RANGE ON DATE CONVERSION
; 10 - MONTH OUT OF RANGE ON MONTH EXTENSION
; 11 - INPUT ERROR ON CONTROL FILE
; 12 - OUTPUT ERROR
; 13 - ERROR READING HEADER BLOCK OF CONTROL FILE
; 14 - NO I/O CHANNELS AVAILABLE
; 15 - PROGRAM CODE NUMBER FROM CONTROL FILE IS NOT EQUAL TO
;      PROGRAM CODE NUMBER ARGUMENT
; 16 - ERROR ON LOOKUP FOR  CONTROL FILE
; 17 - EXCEEDED MAXIMUM SEQUENCE CHARACTER IN FILE NAME
;      INDICATING COMPLETE FAILURE ON OPENING ACCOUNTING FILE
; 18 - ERROR READING NODE TABLE FROM CONTROL FILE
; 19 - DID NOT FIND ENTRY IN NODE TABLE FOR NODE NUMBER
; 20 - ILLEGAL CHARGE CODE FROM CONTROL FILE
; 21 - ERROR RETURN ON INITIAL PUTROY
; 22 - ERROR RETURN ON ENDING  PUTROY
RTYERR: IMULI   1,1000
        ADD     1,S CJOB
        MOVEM   1,S ERCODE
        PUSHJ   STK,RTYCR
        MOVEI   1,[ ASCIZ / FATAL ERROR A/ ]
        PUSHJ   STK,RTYMSG
        CPUTN   ERCODE, 1, 10, 1000, 6
        CPUTCH  EOLIT, 7
        MOVEI   1,S CTRLB
        PUSHJ   STK,RTYMSG
        MOVEI   1, [ ASCIZ /
 PLEASE CONTACT YOUR TYMSHARE REPRESENTATIVE IMMEDIATELY./ ]
        PUSHJ   STK,RTYMSG
        PUSHJ   STK,RTYCR
        EXIT

RTYUUO: TYMCHG  1,
        JRST    [ MOVEI 1,3
                  PUSHJ STK,RTYERR ]
        POPJ    STK,

RTYDAT: MOVE    1, [ XWD 9,9 ]
        GETTAB  1,                      ;GET GMT DATE
        JRST    [ MOVEI 1,4
                  PUSHJ STK,RTYERR ]
        MOVE    2, [ XWD 8,9 ]
        GETTAB  2,                      ;GET GMT JIFFIES
        JRST    [ MOVEI 1,5
                  PUSHJ STK,RTYERR ]
        MOVE    3, [ XWD 9,9 ]
        GETTAB  3,                      ;GET GMT DATE AGAIN
        JRST    [ MOVEI 1,4
                  PUSHJ STK,RTYERR ]
        CAME    1,3                     ;MAKE SURE DATE IS THE SAME
        JRST    RTYDAT                  ;DO IT TILL YOU GET IT RIGHT
        POPJ    STK,

RTYTRU: SETZ    1,              ;SET TO CURRENT JOB
        CALLI   1,^O27                  ;GET TRU
        POPJ    STK,

RTYJOB: SETZ    1,              ;SET TO CURRENT JOB
        CALLI   1,^O30                  ;GET JOB NUMBER
        POPJ    STK,


	RADIX 8		; Some ^&%^%$%$ can't write in OCTAL
	
RTYNOD: HRROI   3,.GTLOG        ;GET TYMNET NODE, LINENUMBER, ETC.
        GETTAB  3,
          SETZ  3,              ;SHOULD NOT HAPPEN
        TDNE    3,[77B19!77B27] ;CHECK FOR NODE ZERO
          JRST  LOGCTL          ;NO, WE GOT A GOOD ONE
        HRLOI   3,.FORFA        ;READ FRAME ATTRIBUTES
        MOVE    2,[1,,3]        ;ATTRIBUTE NUMBER 1 IN ACC 1
        FRMOP   3,2     
          SETZ  3,              ;IGNORE ERROR, MAKE ZERO
LOGCTL:	LDB	1, [ POINT 6,3,27 ]
	LDB	2, [ POINT 6,3,19 ]
	DPB	2, [ POINT 6,1,29 ]
	LDB	2, [ POINT 7,3,35 ]
        POPJ    STK,		;  RETURN WITH 1==NODE, 2==PORT, 3=xxxLOG

	RADIX ^D10		; For the poor &*^&* that can't write in OCTAL

RTYPPN: MOVNI   1,^O23
        GETTAB  1,              ;GET THIS JOB'S PPN
        JRST    [ MOVEI 1,^D8
                  PUSHJ STK,RTYERR ]
        POPJ    STK,             ;RETURN

RTYFPN: MOVNI   1,^O25
        GETTAB  1,              ;GET PPN OF WHERE PROGRAM CAME FROM
        MOVEI   1,0             ;ZERO ON ERROR RETURN
        POPJ    STK,             ;RETURN

RTYLIC: MOVNI   1,^O20
        GETTAB  1,              ;READ LICENSE
        MOVEI   1,0             ;ZERO ON ERROR RETURN
        POPJ    STK,             ;RETURN

RTYINT: XOR     1, [ OPEN 0,SPEC ]        ;(1)=CHANNEL
        XCT     1
        JRST    [ MOVEI 1,7
                  PUSHJ STK,RTYERR ]
        POPJ    STK,

SPEC:   EXP     ^O17              ;DUMP MODE
SPEC1:  SIXBIT  /DSK/
        XWD     0,0

RTYLKP: XOR     1, [ LOOKUP 0,0 ] ;(1)=CHANNEL
        XOR     1,2     ;(2)=ADDRESS
        XCT     1
        JRST    [ MOVNI 1,1
                  POPJ  STK, ]
        MOVEI   1,0
        POPJ    STK,

RTYENT: XOR     1, [ ENTER 0,0 ]
        XOR     1,2
        XCT     1
        JRST    [ MOVNI 1,1
                  POPJ  STK, ]
        MOVEI   1,0
        POPJ    STK,

RTYCNM: SIXBIT  /RTYACT/
        SIXBIT  /CTL/
        0
        XWD     4,^O125552        ;PPN FOR (UAS)

RTYSIO: SETZM   0,S (E+1)
        MOVNS   1,1
        HRLM    1,S E
        SUBI    2,1
        HRRM    2,S E
        POPJ    STK,

RTYINP: MOVE    2,1
        XOR     2, [ INPUT 0,0 ]
        ADDI    2,S E
        XCT     2
        XOR     1, [ STATZ 0,^O340000 ]
        XCT     1
        JRST    [ MOVEI 1,1
                  POPJ  STK, ]
        SETZ    1,1
        POPJ    STK,

RTYOUT: MOVE    2,1
        XOR     2, [ OUTPUT 0,0 ]
        ADDI    2,S E
        XCT     2
        XOR     1, [ STATZ 0,^O740000 ]
        XCT     1
        JRST    [ MOVEI 1,1
                  POPJ  STK, ]
        SETZ    1,1
        POPJ    STK,

RTYUSI: XOR     1, [ USETI 0,0 ]
        XOR     1,2
        XCT     1
        POPJ    STK,

RTYUSO: XOR     1, [ USETO 0,0 ]
        XOR     1,2
        XCT     1
        POPJ    STK,

RTYRLS: MOVE    1,S CHAN
        LSH     1,-23
        SETZM   0,DYNDV.(1)
        MOVE    1,S CHAN
        MOVE    2,1
        XOR     2, [ CLOSE 0,0 ]
        XCT     2
        XOR     1, [ RELEASE 0,0 ]
        XCT     1
        POPJ    STK,

RTYMSG: TTCALL  3,(1)
        POPJ    STK,

RTYDSK: MOVE    1,SPEC1
        POPJ    STK,

RTYCR:  TTCALL  3, [ ASCIZ /
/]
        POPJ    STK,

RTYGPN: GETPPN  1,              ;GET PPN OF DIRECTORY USER IS IN
        POPJ    STK,

END1:   LIT
END2=.
        END
 c0)…