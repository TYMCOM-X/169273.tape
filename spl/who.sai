
begin

require "(SAILIB)SAIL.DEF" source!file;
! require "(SAILIB)UUOSYM.DEF" source!file;
require "(SAILIB)DULWLD.REQ" source!file;

Own Integer Ucnt, Brkcom, Brk;
Own String  Name, Line;

record!class User( string Usr; r!p (user) Nxt );
r!p (user) Ulist, Uptr;


simple integer procedure StrMatch( String A,B );
! ----------------------------------------------------------------------;
!									;
!	StrMatch	String matching routine.  Returns the position	;
!			of the character following the "matched" string	;
!			found in string B (True), otherwise 0 (False).	;
!									;
! ----------------------------------------------------------------------;
begin
    String SaveA, SaveB;
    Integer C,L;

    SaveA_ A;					! Save a copy of "A";
    L_ Length(B) + 1;				! Get beginnng length of "B";
    While Length(B)				! Scan "B";
     do If 0 = (C_(Lop(B) xor A)) or C = " "	!  until matches "A";
	 then begin
	    Lop(A);				! Chop 1 character;
	    If Length(A) = 0			! If none left - done;
	     then done;
	    If Length(SaveB) = 0		! If first char, save B;
	     then SaveB_ B;
	 end
	 else If Length(A) neq Length(SaveA)	! No match...;
		then begin
		    A_ SaveA;			! Reset "A";
		    B_ SaveB;			! Reset "B" to next position;
		    SaveB_ null;		! Clear save'd "B";
		end;
    Return(If Length(A)				! If "A" is non-null;
	    then 0				!  then no match;
	    else L-Length(B) );			!  else return next char pos;
end;

Simple procedure UserProc(Reference Integer Ppn; Reference String Name );
begin
    Uptr_ Ulist;
    While ( null!record neq Uptr_ user:Nxt[ Uptr ] )
     do If ( StrMatch( user:Usr[ Uptr ], Name ) )
	 then begin
	    Print( (Name&"            ")[1 for 14]," [",
		   cvos( !lh( Ppn ) ), ",",
		   cvos( !rh( Ppn ) ), "]", crlf );
	    Return
	 end;
end;


ttyup( true );
setbreak(BrkCom_ getbreak, ",", Null, "INS");

while ( true )
 do begin
    Print( "Userlist: " );
    Ulist_ Uptr_ new!record( user );
    Line_ Inchwl;
    If Length( Line )
     then begin
	While length( Line )
	 do begin
	    Name_ Scan( Line, BrkCom, Brk );
	    If Length( Name )
	     then begin
		user:Nxt[ Uptr ]_ new!record( user );
		Uptr_ user:Nxt[ Uptr ];
		user:Usr[ Uptr ]_ Name;
	     end
	 end;
	DulWld( 0, -1, UserProc )
     end
     else call( 0, "EXIT" )
 end;

end;
