begin "pruner"
require '10 version;
define debug=false;
		
require "(SAILIB)SAIL.DEF" source!file;
define ctrlX="('30&null)", ctrlD="('4&null)";
let andthen=and, orelse=or;
!get!module(APPEND);
!get!module(EXTEND);
!get!definitions(ASSERT);

simple procedure destroy( r!p(any!class) rp);
start!code	define p='17;
		external integer $recfn;
		!  actually: procedure(integer op: r!p(any!class)r);
! just call $recfn(5,rp) [deletes space];
	pop	p,0;	! return address;
	pop	p,1;	! record pointer;
	push	p,[5];
	push	p,1;
	push	p,0;
	jrst	$recfn;
end;

! Prune line:
......: ......: ......: .........\
Name<ht>ext<ht>username<ht>explanation
ARYSER	      	RUN   	Array Service package(formerly part of IOSER)
Sorted by: Extension|Name|User
;
record!class fdes ( r!p(fdes) prev, next; 
			integer name,ext,u1,u2; string des );

r!p(fdes) procedure newf( string name, ext(null), 
			user(null), des(null) );
begin	r!p(fdes) it;
it_new!record(fdes);
fdes:name[it]_cvsix(name);
fdes:ext [it]_cvsix(ext );
fdes:u1  [it]_cvsix(user);
fdes:u2  [it]_cvsix(user[7 for 6]);
fdes:des [it]_des;
	! make it into a one-element ring;
fdes:next[it]_it; fdes:prev[it]_it;	
return(it);
end;

r!p(fdes) procedure xrxf( r!p(fdes) old );
begin	r!p(fdes) it;
it_new!record(fdes);
forlc field_(name,ext,u1,u2,des) 
 doc "fdes:field[it]_ fdes:field[old];" endc
	! make it into a one-element ring;
fdes:next[it]_it; fdes:prev[it]_it;	
return(it);
end;

string procedure prtf( r!p(fdes) it );
return(	cv6str(fdes:name[it]) & (#ht&".") & cv6str(fdes:ext[it]) 
	& #ht & cv6str(fdes:u1[it]) & cv6str(fdes:u2[it]) 
	& (#ht&";") & fdes:des[it] );
integer procedure compare( r!p(fdes) first, second );
! order = extension|name|userName 
 returns 0 if equal, 1 if first<second, 2 if first>second
;
begin	integer v1,v2;
if first=second			then return(0)
 else if first=null!record	then return(-1)
 else if second=null!record	then return(1);
if	(v1_fdes:ext [first]) = (v2_fdes:ext [second])
andthen (v1_fdes:name[first]) = (v2_fdes:name[second])
! add these when user names are included, not until!;
! andthen (v1_fdes:u1  [first]) = (v2_fdes:u1  [second]);
! andthen (v1_fdes:u2  [first]) = (v2_fdes:u2  [second]);
  then return(0)
  else return( if v1 > v2 then -1 else 1 );
end;

r!p(fdes) procedure merge( r!p(fdes) l1, l2 );
! merges two ordered lists into one single sorted list.
	Order= compare(r,next[r]) geq 0
;
begin	r!p(fdes) res, chase, l2tail;
if null!record=l1 then return(l2);
if null!record=l2 then return(l1);
fdes:next[fdes:prev[l1]]_null!record;	! make into linked list; 
fdes:next[fdes:prev[l2]]_null!record;	! make into linked list;
if compare(l1,l2)<0
 then l1 swap l2;
res_l1; l2tail_fdes:prev[l2];
  do  begin
	do l1_fdes:next[chase_l1] 
	 until l1=null!record or compare(l1,l2)<0;
	fdes:next[chase]_l2;
	fdes:prev[l2]_chase; 
	l1 swap l2;
      end
 until l2=null!record;

if null!record=fdes:next[l2tail]
 then begin
	fdes:next[l2tail]_ res;	! then this is the true tail;
	fdes:prev[res]_ l2tail;	! and res is wrong;
      end
 else fdes:next[fdes:prev[res]]_res;	! l1 surrounds, link OK;
return(res);
end;
r!p(fdes) procedure sort( r!p(fdes) r );
!	Order= compare(r,next[r]) geq 0
;
begin	r!p(fdes) run1, run2, chase;
if r=null!record or fdes:next[r]=r then return(r);
fdes:next[fdes:prev[r]]_null!record;	! make into a list;

! fill run1 until a record doesn't fit;
run1_r;
while null!record neq (r_fdes:next[chase_r])
 and compare(chase,r) geq 0
   do;

! turn run1 into a ring, and cut between chase and r [r=next(chase)];
fdes:next[chase]_run1;
fdes:prev[run1]_chase;

while r neq null!record		! that is, while we have more to sort;
   do begin	! get another run, and merge it into what we have now;
	! collect a second run on run2;
	run2_r;
	while null!record neq (r_fdes:next[chase_r])
	 and compare(chase,r) geq 0
	   do;

	! turn run2 into a ring;
	fdes:next[chase]_run2;
	fdes:prev[run2]_chase;

	! merge the two rings onto run1;
	run1_ merge(run1,run2);
      end;
return(run1);
end;
! for now, only name/extension criteria;
integer !name!v,!name!m, !ext!v,!ext!m, !u1!v,!u1!m, !u2!v,!u2!m, 
	!des!length!criteria;
boolean abort;
r!p(fdes) main; 

procedure applies( procedure doit );
! calls named routine with each element of MAIN. 
[watch out for Remove and Delete]
;
begin	r!p(fdes) it,nxt;
	define matches(val,cval,cmsk)={(((val xor cval) land cmsk)=0)};
abort_false;
nxt_fdes:next[main];		! stay one ahead to allow removals;
while main neq (it_nxt) and not abort
   do begin
	nxt_fdes:next[it];	! stay one ahead to allow removals;
	if matches(fdes:name[it], !name!v, !name!m)
	and matches(fdes:ext[it],  !ext!v,  !ext!m)
	and matches(fdes:u1 [it],   !u1!v,   !u1!m)
	and matches(fdes:u2 [it],   !u2!v,   !u2!m)
	and case !des!length!criteria of 
		(true, length(fdes:des[it])=0, length(fdes:des[it])>0)
	 then doit(it);
	end;
end;

integer fileBreaks;	! for now, only name/extension criteria;
	simple procedure setupX;
	 setbreak(fileBreaks_getbreak,"().",#sp&#ht,"IS");
	require setupX initialization;

procedure setFileCriteria(string filename);
! sets up file name criteria for above
;
begin	define SixbitMask=lnot '77;
	integer brk,ch,bit;
	string name;
      procedure EatUser;
      begin "EatUser" 
	!u1!v_ cvsix(name_scan(fileName, fileBreaks, brk));
	!u2!v_ cvsix(name[7 for 6]);
	!u1!m_ !u2!m_ -1;	! means "examine all bits";
	for bit_30 step -6 until 0
	   do if "*" = (ch_lop(name)) 
		then begin
			!u1!m_ !u1!m land (SixbitMask lsh Bit);
			!u2!m_ 0;
		      end
		else if ch = "#"
		  then !u1!m_ !u1!m land (SixbitMask rot Bit);
	for bit_30 step -6 until 0
	   do if "*" = (ch_lop(name)) 
		then !u2!m_ !u2!m land (SixbitMask lsh Bit)
		else if ch = "#"
		  then !u2!m_ !u2!m land (SixbitMask rot Bit);
      end "EatUser";
!name!m_ !ext!m_ !u1!m_!u2!m_ 0; 
!name!v_cvsix(name_scan(fileName, fileBreaks, brk));
if length(name)=0 and brk="("
 then begin
	eatUser;
	if brk=")"
	 then !name!v_cvsix(name_scan(fileName, fileBreaks, brk));
      end;
if !name!v 
 then begin
	!name!m_ -1;	! means "examine all bits";
	for bit_30 step -6 until 0
	   do if "*" = (ch_lop(name)) 
		then !name!m_ !name!m land (SixbitMask lsh Bit)
		else if ch = "#"
		  then !name!m_ !name!m land (SixbitMask rot Bit);
      end;
if brk="."
 then begin
	!ext!v_cvsix(name_scan(fileName, fileBreaks, brk));
	!ext!m_ -1 lsh 18;	! means "examine all bits";
	for bit_30 step -6 until 18
	   do if "*" = (ch_lop(name)) 
		then !ext!m_ !ext!m land (SixbitMask lsh Bit)
		else if ch = "#"
		  then !ext!m_ !ext!m land (SixbitMask rot Bit);
      end;
end;
boolean procedure ringSearch( reference r!p(fdes) result;
			r!p(fdes) target, universe );
! searches an ordered list for a match on target.  
 Returns:
TRUE:	found, result= the record which matched target
FALSE:	not found, result= record which should follow target
	Order= compare(r,next[r]) geq 0
;
begin	r!p(fdes) chase; integer cv;
if universe=null!record
 then begin result_null!record; return(false); end;
chase_universe;
while (cv_compare(chase,target))<0
   do if universe=(chase_fdes:next[chase]) then done;
result_chase;
return(cv=0);
end;
IFCR FALSE THENC
r!p(fdes) procedure find( string name, ext(null), user(null) );
begin	r!p(fdes) perhaps, copy;
copy_perhaps_newf(name,ext,user);
if not ringSearch( perhaps, perhaps, main )
andthen (length(user) or perhaps = null!record
	 orelse fdes:name[perhaps] neq fdes:name[copy] 
		 or fdes:ext[perhaps] neq fdes:ext[copy])
 then perhaps_ null!record;
destroy(copy);
return(perhaps);
end;
ENDC

procedure rpt( r!p(fdes) ring );
begin		r!p(fdes) chase;
if chase_ ring
 then do print(crlf,prtf(chase))
	until (chase_fdes:next[chase])=null!record 
	orelse chase=ring;
if ring=null!record then print(crlf,"----Null----");
end;

boolean procedure verify( r!p(fdes) ring );
begin	r!p(fdes) n,chase;
if ring=null!record then return(true);

chase_ring;
while ring neq (n_fdes:next[chase]) neq null!record 
		! ring=n => we have made full circuit;
		! ring=n!r => BAD (no linkage should be null);
  do if fdes:prev[n] neq chase		! NEXT and PREV disagree;
	orelse compare(n,chase)>0	! wrong order;
	 then return(false)
	 else chase_n;

return(n neq null!record);
end;
IFCR DEBUG THENC
record!class cell( r!p(any!class) car, cdr );

r!p(cell) procedure cons(r!p(any!class) car, cdr );
begin	r!p(cell) cons;
cons_new!record(cell);
cell:car[cons]_car; cell:cdr[cons]_cdr;
return(cons);
end;

define $X(a1,a2,a3,a4)={
 redefine field={   cell:c}&{a1}&
	IFCR length("a2") THENC {r[cell:c}&{a2}& ENDC
	IFCR length("a3") THENC {r[cell:c}&{a3}& ENDC
	IFCR length("a4") THENC {r[cell:c}&{a4}& ENDC
						{r[it]}
	IFCR length("a4") THENC &{]} ENDC
	IFCR length("a3") THENC &{]} ENDC
	IFCR length("a2") THENC &{]} ENDC;
   r!p(cell) simple procedure c} &{a1}&{a2}&{a3}&{a4}& {r(r!p(cell) it);
	return(field);
   simple procedure rplc} &{a1}&{a2}&{a3}&{a4}& {(r!p(cell) it,nv);
	field_nv
};
$X(A);		$X(D);						! CxR;
$X(A,A);	$X(D,A);	$X(A,D);	$X(D,D);       ! CxxR;
 IFCR FALSE THENC	! these just dont seem necessary;
 $X(A,A,A);	$X(D,A,A);	$X(A,D,A);	$X(D,D,A);    ! CxxAR;
 $X(A,A,D);	$X(D,A,D);	$X(A,D,D);	$X(D,D,D);    ! CxxDR;
 $X(A,A,A,A);	$X(D,A,A,A);	$X(A,D,A,A);	$X(D,D,A,A); ! CxxAAR;
 $X(A,A,D,A);	$X(D,A,D,A);	$X(A,D,D,A);	$X(D,D,D,A); ! CxxDAR;
 $X(A,A,A,D);	$X(D,A,A,D);	$X(A,D,A,D);	$X(D,D,A,D); ! CxxADR;
 $X(A,A,D,D);	$X(D,A,D,D);	$X(A,D,D,D);	$X(D,D,D,D); ! CxxDDR;
 ENDC
ENDC
IFCR FALSE THENC	
Acceptable input:
	file .ext user   Descr
	file  ext user   Descr
	file .ext (user) Descr
	file  ext (user) Descr
	file      (user) Descr
	file .ext        ;descr
	file  ext        ;descr
	file             ;descr
	(user) file .ext Descr
	(user) file  ext Descr
	(user) file      ;descr
ENDC

integer passClose, brkPiece, noWhite, lineBreak;
	simple procedure inpbrks;
	begin		define white={#sp&#ht&crlf&#ff};
	setbreak(passClose_getbreak,")",       "(", "IS");
	setbreak(brkPiece_getbreak,White&"(.;",null,"IR");
	setbreak(noWhite_getbreak,White,White,"XR");
	setbreak(lineBreak_getbreak,#lf & #ff, #cr,"INS");
	end;
	require inpbrks initialization[0];

r!p(fdes) procedure cvfd(string line);
begin	r!p(fdes) cvfd; string user; integer brk;

scan(line,noWhite,brk);
if brk="("
 then begin 
	user_scan(line,passClose,brk);
	scan(line,noWhite,brk);
      end
 else user_null;
if length(line)=0 or brk=";" then return(null!record);

cvfd_new!record(fdes); fdes:next[cvfd]_ fdes:prev[cvfd]_ cvfd;
fdes:name[cvfd]_cvsix(scan(line,brkPiece,brk));
scan(line,noWhite,brk);

if brk neq ";"
 then begin		! have file name, get Ext and possibly user;

	if brk neq "("
	 then begin	! must be extension;
		if brk="." then brk_lop(line);
		fdes:ext[cvfd]_cvsix(scan(line,brkPiece,brk));
		scan(line,noWhite,brk);
	      end;

	! now we have a file name and extension;

	if length(user)=0 and brk neq ";"	! if we still need user;
	 then if brk="(" 
		 then user_scan(line,passClose,brk)
		 else user_scan(line,brkPiece,brk);
      end;

scan(line,noWhite,brk);
while brk=";"
   do begin	! remove leading semicolons and blanks;
	brk_lop(line);
	scan(line,noWhite,brk);
      end;
fdes:u1[cvfd]_ cvsix(user);
fdes:u2[cvfd]_ cvsix(user[7 to inf]);
fdes:des[cvfd]_ line;
return(cvfd);
end;
procedure splice(r!p(fdes) a,b);
! cuts or joins rings [A/B symetric, A=B is a no-op, A&B non-null]
 If A and B are the same ring, A becomes [A,B) and B becomes [B,A)
 If A and B are different rings, A becomes [A...prev(A),B...prev(B)]
;
begin
fdes:next[fdes:prev[a]]_ b;
fdes:next[fdes:prev[b]]_ a;
fdes:prev[a] swap fdes:prev[b];
end;

IFCR DEBUG THENC
procedure addon;
begin	r!p(fdes)it;string line;
while true
   do begin
	print(crlf,"Add files:");
	if it_cvfd(inchwl) then splice(it,main) else done;
      end;
main_sort(main);
end;
ENDC
r!p(fdes) procedure ReadData( string Prunefile );
begin "ReadData"	integer chan,brk,eof; 
			string line;
			r!p(fdes) ReadData,rp;
open(chan_getchan, "DSK",0, 4,0, 200,brk,eof_false);
assert(not eof);
lookup(chan,PruneFile,eof);
if eof 
 then begin
	print("Cannot read Prune file: `", PruneFile, "'.");
	return(null!record);
      end;
do ReadData_cvfd(input(chan,lineBreak))
 until ReadData or eof;
while not eof
   do if (rp_cvfd(input(chan,lineBreak)))
	then splice(ReadData,rp);
release(chan);
return(sort(ReadData));
end "ReadData";
r!p(fdes) procedure GrabFiles( integer PPN(0); string user(null) );
begin "GrabFiles"	integer ufdchan,eof; 
			r!p(fdes) head, rp;
			r!p(any!class) ufdptr;
			own safe integer array unm[0:1];
if ppn=0 and length(user)
 then begin
	ppn_location(unm[0]);
	unm[0]_cvsix(user); unm[1]_cvsix(user[7 to inf]);
      end;
open(ufdChan_getchan, "DSK",'17, 0,0, 0,0,eof_false);
assert(not eof);
lookup(ufdChan, "(UFD)" & #esc&cvxstr(ppn) & ".UFD", eof);
if eof
 then begin
	print(" Cannot read directory for ",
		(if length(user) then user 
		else "[" & cvos(ppn lsh-18) & 
			"," & cvos(ppn land '777777)& "]") );
	return(null!record);
      end;
ufdptr_ iniufr("*","*", UfdChan );
head_null!record;
while nxtufr(unm, ufdptr)
   do begin
	if (unm[1]_unm[1] land (-1 lsh 18))=cvsix("TMP")
	or (unm[0]=cvsix("PRUNE") and unm[1]=cvsix("DAT"))
	 then continue;
	rp_ new!record(fdes);
	fdes:name[rp]_ unm[0];
	fdes:ext [rp]_ unm[1];
	fdes:next[rp]_ head;
	if head
	 then begin
		fdes:prev[rp]_fdes:prev[head]; 
		fdes:prev[head]_rp;
	      end
	 else fdes:prev[rp]_rp;
	head_ rp;
      end;
if head
 then fdes:next[fdes:prev[head]]_head;
return(sort(head));
end "GrabFiles";
procedure writeData( R!p(fdes) ring; string FileName(null) );
if ring then
begin	r!p(fdes) chase;
setprint(FileName,"F");	! print output goes to file;
chase_ring;
do if length(fdes:des[chase]) then print(prtf(chase),crlf)
 until ring=(chase_fdes:next[chase]);
setprint(null,"T");	! close output file, redirect prints to TTY;
print("-written-");
end;
Integer FileCounter, W!chan;
r!p(fdes) deleteRing;

procedure C!command( reference r!p(fdes) it );
	FileCounter_ FileCounter+1;

procedure F!command( reference r!p(fdes) it );
	print( crlf, prtf(it) );

procedure W!command( reference r!p(fdes) it );
begin
	FileCounter_ FileCounter+1;
	cprint( W!chan, crlf, prtf(it) );
end;

procedure R!command( reference r!p(fdes) it );
begin
fdes:next[fdes:prev[it]]_fdes:next[it];
fdes:prev[fdes:next[it]]_fdes:prev[it];
end;

procedure D!command( reference r!p(fdes) it );
begin
if deleteRing
 then splice(xrxf(it),deleteRing)
 else deleteRing_ xrxf(it); 
R!command(it);
end;

procedure M!command( reference r!p(fdes) it );
begin	string str; own string lastResponse; boolean oldtty;
print(crlf, prtf(it) );
print(crlf,"new comment (^X for previous): ");
oldtty_ttyup(false);
if length(str_inchwl)
 then fdes:des[it]_ lastResponse_ str
 else if !skip! = ctrlX then fdes:des[it]_ lastResponse; 
if !skip!=ctrlD then abort_true;
ttyup( oldtty );
end;

procedure U!command( reference r!p(fdes) it );
begin	string str; own string lastResponse; boolean oldtty;
print(crlf, prtf(it) );
print(crlf,"new User (^X for previous): ");
if length(str_inchwl) or !skip!=ctrlX
 then begin
	if length(str)=0 then str_ lastResponse;
	fdes:u1[it]_ cvsix(str);
	fdes:u2[it]_ cvsix(str[7 for 6]);
	lastResponse_ str;
      end;
if !skip!=ctrlD then abort_true;
end;

integer t!chan,t!eof;
simple procedure setup!Text!Channel;
	open(t!chan_getchan,"DSK",0, 1,0, 200,0,t!eof_0);
require setup!Text!Channel initialization;

procedure T!command( reference r!p(fdes) it );
begin	integer ch;
print(crlf, prtf(it) );
if cvsix("REL")=(t!eof_fdes:ext[it]) or cvsix("SAV")=t!eof 
or cvsix("SHR")=t!eof or cvsix("SWR")=t!eof or cvsix("HGH")=t!eof 
or cvsix("LOW")=t!eof
 then begin
	print(" Sure?: ");
	if (ch_inchrw)=ctrlD then abort_true; 
	if (ch land '137)neq "Y"
	 then return;
      end;
lookup(t!chan, cv6str(fdes:name[it])&"."&cv6str(fdes:ext[it]),t!eof_0);
if t!eof
 then print(" *** Cannot find: `",
		cv6str(fdes:name[it])&"."&cv6str(fdes:ext[it]),"'.") 
 else do print(input(t!chan,0),crlf,"More?: ") 
	until (((ch_inchrw) land '137) neq "Y") or t!eof;
abort_ch=ctrlD;
end;
IFCR compiler!switch("B") THENC
  external procedure bail;
ENDC
IFCR debug THENC
simple procedure raid;
  start!code	external integer $i;
	jsr	$i;
  end;
ENDC
boolean procedure one!command(string text);
begin	integer c;
!des!length!criteria_-1;
if "A"=(c_lop(text)) then begin !des!length!criteria_0; c_lop(text) end
 else if "B"=c	     then begin !des!length!criteria_1; c_lop(text) end
 else if "N"=c	     then begin !des!length!criteria_2; c_lop(text) end;
setFileCriteria(text); 
case c
   of begin	
	define CC(let,default,setup,finish)={
	["let"] begin 
		if !des!length!criteria<0
		 then !des!length!criteria_default;
		setup;
		applies(let}&{!command);
		finish
		end};
	["Q"]["X"] return(true);
	cc(F,0);	! FIND		default: all;
	cc(M,1);	! MODIFY	default: Blank;
	cc(T,1);	! TYPE		default: Blank;
	cc(R,0);	! REMOVE	default: all;
	cc(U,0);	! CHANGE USER	default: all;
	cc(D,1);	! DELETE	default: Blank;
			! COUNT		default: all;
	cc(C,0, FileCounter_0,
		[print( FileCounter, " Files." ) 
		]);
			! OVERWRITE	default: Non-blank;
	cc(O,2,	[define O!Command={W!command}
		  ;setprint(null,"F")
		  ;FileCounter_0
		  ;W!chan_-1],
		 [setprint(null,"T")
		  ;print( FileCounter, " Files written." )
		]);
	["W"] begin	! WRITE/APPEND	default: Non-blank;
			own string file,rsp; integer flag;
		if !des!length!criteria<0
		 then !des!length!criteria_2;
		print(crlf,"Append to [", file, "]:" );
		if length(rsp_inchwl)=0 then rsp_file;
		open(W!chan_getchan,"DSK",'10000, 4,1, 0,0,flag_true);
		assert(not flag);
		if 0 leq append(W!chan,rsp,flag)
		 then begin file_rsp;
			FileCounter_0; applies(W!command);
			print( FileCounter, " lines written to ",file )
		      end
		 else print("Couldn't set up append on ",rsp);
		release(W!chan);W!chan_-1;
		end;
IFCR compiler!switch THENC
	["`"] bail;
ENDC
IFCR debug THENC
	["'"] raid;
ENDC
	ELSE print("	Quit, Xit, "
IFCR compiler!switch("B") THENC & "` -> BAIL, " ENDC
IFCR debug		  THENC & "' -> RAID, " ENDC & "
	<c-spec> <command-letter> <file-group-spec>
[ All | Blank | Nonblank | ]
	[ Count | Delete | Find | Modify | Overwrite
	| Remove | Type | UserChange | Write(append) ]
")
      end;
return(false); 
end;
r!p(fdes) procedure mergein( r!p(fdes) lis, Blanks );
begin	r!p(fdes) lisChase, BlankChase, tmp, noMore;
if lis=null!record then return(Blanks);
if Blanks=null!record then return(lis);
LisChase_lis;
blankChase_Blanks;
noMore_ null!record;	! files which are no longer there;
! if a file appears:
! on both LIS and BLANKS, [documented existing file] remove from blanks;
! on only LIS [documented non-existant file] transfer to NoMore;
! on only BLANKS [undocumented file] leave alone;
while true
   do begin		integer comparison; 
	if (comparison_ compare(blankChase,LisChase)) > 0
	 then begin		! t(blank) > t(lis);
			! top(BLANKS) not in LIS: undocumented file;
			! advance BLANKS;
		if (BlankChase_ fdes:next[BlankChase])=Blanks
		 then begin
			! ran out of BLANKS, rest of LIS is doc-nofile;
			if Lis=LisChase
			 then Lis_null!record ! all docs are non-files;
			 else splice(Lis,Lischase);
			! now Lischase is a ring of non-files;
			if noMore
			 then splice(noMore,lisChase)
			 else noMore_lisChase;
			done;
		      end
	      end
	 else if comparison
	 then begin	! t(blank) < t(lis);
			! t(LIS)=  documented non-file, advance LIS;
		tmp_ fdes:next[LisChase];
		splice(LisChase,tmp);		! cut out LisChase;
		if noMore 			! move it to NoMore;
		 then splice(noMore,lisChase)
		 else noMore_lisChase;
		if tmp=Lis
		 then begin	! finished. if lisChase was lis, no lis;
			if lis=lisChase then lis_null!record;
			done;
		      end
		 else begin	! more left.  Assure Lis not in NoMore;
			if lis=LisChase then lis_tmp;	! keep Lis good;
			LisChase_ tmp;	! advance along Lis;
		      end;
	      end
	 else		! if comparison = 0;
	      begin	! top(BLANKS) = top(LIS): documented file;
		do LisChase_ fdes:next[LisChase]	! advance Lis;
		 until lisChase=Lis or compare(BlankChase,Lischase);
					! past all copies for this file;
		tmp_ fdes:next[BlankChase];
		splice(BlankChase, tmp); ! cut BlankChase from BLANKS;
		destroy(BlankChase);	! recycle its storage;
		if tmp=Blanks
		 then begin		! we reached end of blanks;
			if blanks=blankChase then blanks_null!record;
			if lis neq lischase ! rest of LIS is nofiles;
			 then begin
				splice(lis,lischase);
				if nomore
				 then splice(nomore,lischase)
				 else nomore_lischase;
			      end;
			done;
		      end
		 else if blanks=blankchase then blanks_ tmp;
		BlankChase_ tmp;
		if LisChase = Lis then done;
			! ran out of LIS, keep all BLANKS;
	      end;
      end;
if noMore
 then begin	! deleted described files;
		integer ch, oldtty;
	oldtty_ttyup(true);
	do print(crlf,"Some described files are gone.  You may:
	Write the descriptions to an exception file
	Review descriptions (drop or keep individually)
	Drop descriptions
	Keep descriptions
Option: ") until (ch_inchrw)="R" orelse ch="D" or ch="K" or ch="W"; 
	if ch="W"
	 then begin
		setprint("","F"); rpt(sort(NoMore)); setprint("","T");
		ch_"D";
	      end;
	if ch="D"
	 then begin	! just destroy all;
		fdes:next[fdes:prev[Nomore]]_ null!record;
		do begin tmp_ fdes:next[Nomore]; destroy(NoMore); end
		 until (Nomore_tmp)=null!record;
	      end;
	NoMore_ sort(NoMore);
	if Ch="R"		! review;
	 then begin
		LisChase_Nomore;
		while true
		   do begin
			tmp_fdes:next[LisChase];
			do print(crlf,prtf(LisChase),
					crlf,"Keep or Drop?:")
			until (ch_inchrw)="K" orelse ch="D";
			if ch="D"
			 then begin
				splice(lisChase,tmp);
				destroy(lisChase);
				if tmp=NoMore
				 then begin
					if tmp=lisChase
					 then Nomore_null!record;
					done;
				      end;
				if NoMore=lisChase then Nomore_tmp;
			      end
			 else if NoMore=tmp then done;
			lisChase_tmp;
		      end;
	      end;
	! "Keep" just didn't bother to remove any NOMOREs;
	LIS_ merge(LIS,NOMORE);
	ttyup(oldtty);
      end;

return(merge(LIS,BLANKS));
end;
external integer !jbver;
R!p(fdes) Dirfiles;
integer baseCase; 
simple procedure restoreCase; ttyup( baseCase ); cleanup restoreCase;
IFCR DEBUG THENC
r!p(fdes) f,f1,f2,f3,f4;
r!p(any!class) a,a1,a2,a3,a4;
r!p(cell) c,c1,c2,c3,c4;

main_newf(null,null,null,"Null file and extension");
f_f1_f2_f3_f4_null!record;
c_c1_c2_c3_c4_null!record;
a_a1_a2_a3_a4_null!record;
usererr(0,1,"go","B");
ENDC

print(crlf,"Prune v ", 
	cvos(!JBVER lsh -18),".", cvos(!JBVER land (-1 lsh -18))
	IFCR debug THENC ," [DEBUG]" ENDC );
deleteRing_null!record;
main_readData("PRUNE.DAT");
baseCase_ ttyup(true);

main_ mergein(main, sort( grabFiles(call(0,"GETPPN")) ));

! Kluge: to safely delete, need a non-file for the first entry in main;
splice(	newf(null), main ); main_ fdes:prev[main];

do print(crlf,":") until one!command(inchwl);

! Kluge: no one else expects the non-file for the first entry in main;
main_ fdes:next[main]; splice(main, fdes:prev[main]); 

writeData(main,"PRUNE.DAT");
if main_deleteRing
 then begin "deleteRing"	integer ch;
	ttyup(true);
	for FileCounter_1 step 1		! count files on MAIN;
	 while (main_fdes:next[main]) neq deleteRing 
	  do;
	do print(crlf, FileCounter, " files to delete, OK? ")
	 until "Y"=(ch_inchrw) orelse "N"=ch;
	if ch="Y"
	 then begin "reallyDelete"
		fdes:next[fdes:prev[main]]_null!record;	! make a list;
		do begin "DeleteOne" 
		    lookup(t!chan,cv6str(fdes:name[main])&"."&
				cv6str(fdes:ext[main]), ch);
		    if ch
		     then print(crlf,"Couldn't find:", crlf,prtf(main))
		     else begin
			    rename(t!chan,null,0,ch);
			    if ch then print(crlf,"Couldn't delete:", 
						crlf,prtf(main))
			  end;
		   end "DeleteOne"
		 until (main_fdes:next[main])=null!record;
	      end "reallyDelete" 
      end "deleteRing";
end;

 |e ·