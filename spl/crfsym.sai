begin	"CRFSYM"

require ('6 lsh 24) lor '1 version;
require "(SAILIB)SAIL.DEF" source!file;
require "(CARL)DAYTIM.REQ" source!file;

require 10000 new!items,  pnames;

Define Calli!PJOB = '30, Calli!TMPCOR = '44, !TCRDF = '2;
Define Default!Ext = {".SAI"};	! default file extension;
Define White(x) = {Scan(x, B!Wht, Brk)};
Define Spaces =
    {("                                                                " &
      "                                                                ") };

!	Variable definitions for outer block;

external boolean RPGSW;		! true if run at start address plus one;
boolean Ext!Found;		! true if "." found in filename;
boolean File!Error;		! true if illegal filename;
boolean CCLSW,IACSW;		! true if command found, if interactive;
boolean In!Macro, In!Def;	! true if inside "define = {...}";
boolean In!Comment;		! true if inside a comment;
boolean Not!Defined;		! true if ITEM is not defined;

integer swBAIL;			! debugging kluge;
integer	swCREF;			! wants cref .. symbols;
integer swEXIT;			! how to get out of interactive state;
integer swHELP;			! status holder of /HELP switch;
integer swLIST;			! wants lst file too;
integer swLONG;			! wants long 88 line paper;
integer swMACRO;		! wants cref with macros;
integer swRESERVED;		! wants cref with reserved words;
integer swSYMBOL;		! wants cref with symbols;
integer swWIDE;			! wants wide 132 wide paper ;

integer	lines;			! per-page line counter;
integer LPP, Width;		! lines per page, width;
integer pages,ppages,subpage;	! page counters;
integer LineNo,last!brk;	! input/output line number.;
integer	BRK,EOF,IChan,OChan;	! I/O variables;
string	Line,brks;		! current text line;
string	IDevice, ODevice;	! current input/output device;
string	CmdFile,LogFile;	! I/O file names;
string	CmdName;		! Temporary CmdFile holder;
string	CmdDate, CrefDate;	! Daytime file created, listing generated;
string	OldType;		! Remember last type used;
string	Title,Subtitle;		! Listing line info;

integer B!Lin, B!Nam;		! break table for reading a line, name;
integer B!Usr, B!Ppn;		! break table for end of username;
integer B!Wht, B!Num;		! break table for skipping whitespace, nums;
integer B!Brk, B!Tok;		! break table for finding break chars;
integer B!Quote;		! break table for finding a quote;
integer B!Sub;			! break table for building parameters;
integer B!Par;			! break table for reading parameters;
integer Dummy;			! dummy variable so we don't lose Brk;

record!class Occur( Integer Here; r!p ( occur ) Next );
record!class Heads( r!p ( occur ) Here, This; r!p ( heads ) Next );

r!p ( heads ) ResHead, Head, That;
r!p ( occur ) This;

r!p ( heads ) Itemvar ResItm;
List item $$LIST$$RES, $$LIST$$SYM, $$LIST$$MAC;
List itemvar LI;

!	Reserved Word Array Definition
;
Preset!With
    "ABS", "ACCESS", "AFTER", "ALL", "ALLGLOBAL",
    "AND", "ANY!CLASS", "APPLY", "ARG!LIST", "ARRAY",
    "ASH", "ASSIGN", "ASSIGNC", "ASSOC", "BBPP",
    "BEFORE", "BEGIN", "BIND", "BOOLEAN", "BUCKETS",
    "BUILT!IN", "CASE", "CASEC", "CAUSE", "CHECK!TYPE",
    "CLEANUP", "COMMENT", "COMPILER!SWITCHES", "CONOK", "CONTEXT",
    "CONTINUE", "COP", "CPRINT", "CVI", "CVLIST",
    "CVMS", "CVN", "CVPS", "CVSET", "DATUM",
    "DECLARATION", "DEFINE", "DELETE", "DELIMITERS", "DEPENDENTS",
    "DIV", "DO", "DOC", "DONE", "DPB",
    "ELSE", "ELSEC", "END", "ENDC", "ENTRY",
    "EQV", "ERASE", "ERROR!MODES", "EVALDEFINE", "EVALREDEFINE",
    "EXPR!TYPE", "EXTERNAL", "FAIL", "FALSE", "FIRST",
    "FOR", "FORC", "FOREACH", "FORGET", "FORLC",
    "FORTRAN", "FORWARD", "FROM", "GEQ", "GLOBAL",
    "GO", "GOTO", "IBP", "IDPB", "IF",
    "IFC", "IFCR", "ILDB", "IN", "IN!CONTEXT",
    "INF", "INITIALIZATION", "INTEGER", "INTER", "INTERNAL",
    "INTERROGATE", "ISTRIPLE", "ITEM", "ITEM!START", "ITEMVAR",
    "KILL!SET", "LABEL", "LAND", "LDB", "LEAP!ARRAY",
    "LENGTH", "LEQ", "LET", "LIBRARY", "LIST",
    "LISTC", "LISTO", "LNOT", "LOAD!MODULE", "LOCATION",
    "LOP", "LOR", "LSH", "MAKE", "MATCHING",
    "MAX", "MEMORY", "MESSAGE", "MIN", "MOD",
    "NEEDNEXT", "NEQ", "NEW", "NEW!ITEMS", "NEW!RECORD",
    "NEXT", "NIL", "NOMAC", "NOT", "NOW!SAFE",
    "NOW!UNSAFE", "NULL", "NULL!CONTEXT", "NULL!DELIMITERS", "NULL!RECORD",
    "OF", "OFC", "OR", "OWN", "PHI",
    "PNAMES", "POLL", "POLLING!INTERVAL", "PRELOAD!WITH", "PRESET!WITH",
    "PRINT", "PROCEDURE", "PROCESSES", "PROTECT!ACS", "PUT",
    "QUICK!CODE", "REAL", "RECORD!CLASS", "RECORD!POINTER", "RECURSIVE",
    "REDEFINE", "REF!ITEM", "REFERENCE", "REMEMBER", "REMOVE",
    "REPLACE!DELIMITERS", "REQUIRE", "RESERVED", "RESTORE", "RETURN",
    "ROT", "SAFE", "SAMEIV", "SECOND", "SEGMENT!FILE",
    "SEGMENT!NAME", "SET", "SETC", "SETCP", "SETIP",
    "SETO", "SHORT", "SIMPLE", "SOURCE!FILE", "SPROUT",
    "SPROUT!DEFAULTS", "START!CODE", "STEP", "STEPC", "STRING",
    "STRING!PDL", "STRING!SPACE", "SUCCEED", "SUCH", "SWAP",
    "SYSTEM!PDL", "THAT", "THEN", "THENC", "THIRD",
    "TO", "TRUE", "UNION", "UNSTACK!DELIMITERS", "UNTIL",
    "UNTILC", "VALUE", "VERSION", "WHILE", "WHILEC",
    "XOR";
Safe String Array Reserved!Word[1:206];


Preset!With
    "+", "-", "*", "/", "%", "**", "^", ":=", "_", "=",
    "<", ">", "<=", ">=", "|", "&", "?", "!";
Safe String Array Reserved!Symbol[1:18];

!	Initialization routines;

procedure ListIni;
begin
    Datum( $$List$$Sym ) _ Nil;		! Symbols;
    Datum( $$List$$Res ) _ Nil;		! Reserved Words/graphics;
    Datum( $$List$$Mac ) _ Nil;		! Macros;
end;
require ListIni initialization;


Procedure InitReserve( String Array A );
begin
    Integer Idx, B, E;  r!p (heads) ItemVar SI;

    B_ ArrInfo( A,1 );
    E_ ArrInfo( A,2 );

    For Idx _ B step 1 until E
     do begin
	That_ Heads:Next[ That ]_ new!record( heads );
	Heads:This[ That ]_ Heads:Here[ That ]_ new!record( occur );

	New!Pname( SI_ New( That ), "RES;" & A[ Idx ] );
	Put SI in Datum( $$List$$Res ) after Inf;
     end;
 end;

Procedure ReservedWordIni;
begin

    That _ ResHead_ New!Record( Heads );
    InitReserve( Reserved!Symbol );
    InitReserve( Reserved!Word );

end;
require ReservedWordIni initialization;

!	More initialization routines ;

procedure BrkIni;
begin
    Define ##Wht = {" "&#ht&#cr};
    Define ##Brk = {" !@#$%^&*()_+-=~`[]|\:;'<>,.?/" & '42 & '173 & '175};
    Define ##Tok = {##Wht&"'@^&*_+-~`[]|\:<>?/" & '173 & '175};
    Define ##Sub = {'42 & ";"};
    Define #Upper = {"ABCDEFGHIJKLMNOPQRSTUVWXYZ"};
    Define #Lower = {"abcdefghijklmnopqrstuvwxyz"};
    Define #Alpha = {#Upper & #Lower};
    Define #Digit = {"0123456789"};
    Define #Chars = {#Alpha & #Digit};
    Define #Vars  = {#Chars & "!%$.#"};

    setbreak(B!Lin_Getbreak,#lf&#ff,     null, "SINF");	! line;
    setbreak(B!Nam_Getbreak,#Chars,      crlf, "RXNK");	! name or token;
    setbreak(B!Usr_Getbreak, ")",        null, "SINK");	! end of username;
    setbreak(B!Ppn_Getbreak,"01234567,", null, "SXNK");	! only allow these;
    setbreak(B!Num_Getbreak,#Digit&##Wht,null, "RXNK");	! scrap numbers;
    setbreak(B!Wht_Getbreak,#lf & ##Wht, crlf, "RXNK");	! white space;
    setbreak(B!Brk_Getbreak,#lf & ##Brk, #cr,  "RINK");	! all break chars;
    setbreak(B!Tok_Getbreak,#Vars,       ##Tok,"STXNK");! tokens?;
    setbreak(B!Sub_Getbreak, ##Sub,      null,"STINF");	! substitutions;
end;
require BrkIni initialization;


procedure Initialize!switches;
begin
  swWIDE_				! use wide 132 paper format ;
  swLONG_				! use long  88 paper format ;
  swHELP_swEXIT_			! HELP & EXIT commands ;
  swBAIL_				! enter BAIL if available ;
  swLIST_swCREF_			! list and cross-reference ;
  swMACRO_swRESERVED_			! not usually included ;
  swSYMBOL_ -1;				! include symbols in cref ;
end;
require Initialize!switches initialization;


Boolean Procedure LLT(String A;  String B);
begin "LLT"
    If EQU(a,b) then return(FALSE);
    While (Length(a) and Length(b)) do begin "checking strings"
	if (a < b) then return(TRUE);
	if (a > b) then return(FALSE);
	lop(a);    lop(b);
    end "checking strings";
    if length(b) then return(TRUE) else return(FALSE);
end "LLT";

string procedure TOK (reference string S);
begin
    string S1;
    White( S );			! clear whitespace;
    S1 _ scan(S, B!Brk, BRK);	! get the next word;
    if length(S1) = 0		! null?;
	then if length(S) = 0	!   null source also?;
	    then return (null)	!    yes - return null;
	    else S1 _ lop(S);	!    no  - get a character;
    White( S );			! clear whitespace;
    return(S1);			! return the token;
end;

string procedure EXIT;		! string so it can be "printed" [ugh];
call(0,"EXIT");

simple boolean procedure Numeric(String Str);
begin
    Scan( Str, B!Num, Dummy);
    Return(If Length( Str ) then False else True);
end;

simple string procedure GetFil( Reference string Str, Dev;
				Reference Boolean Dot, Err);
begin "read a file name"
    String Usr, Fil, Ext, Ppn;
    Dot _ Err _ False;					! Initially false;
    Dev _ Usr _ Fil _ Ext _ Ppn _ Null;			! Initially empty;
    Fil _ Scan(Str, B!Nam, Brk);			! Read name?;
    If Brk = ":" then begin
	Lop(Str);					! eat ":";
	Dev _ Fil;					! setup device;
	Fil _ Scan(Str, B!Nam, Brk);			! reread filename;
    end;
    If Brk = "(" then begin
	Lop(Str);					! eat "(";
	Usr _ "(" & Scan(Str, B!Usr, Brk) & ")";	! get username;
	If Brk neq ")" then err _ True;			! everything ok?;
	Fil _ Scan(Str, B!Nam, Brk);			! then filename;
    end;
    If Brk = "." then begin
	Lop(Str);					! eat ".";
	Dot _ True;					! declare extension;
	Ext _ "." & Scan(Str, B!Nam, Brk);		! and read it;
    end;
    If Brk = "[" then begin
	Lop(Str);					! eat "[";
	Ppn _ "[" & Scan(Str, B!Ppn, Brk) & "]";	! get PPN;
	Lop(Str);					! eat "]";
	If Brk neq "]" then err _ True;			! closed ok?;
	If Length(Usr) then err _ True;			! (user) & [ppn] ?;
    end;
    White( Str );					! clear whitespace;
    Return ( Usr & Fil & Ext & Ppn );			! All done;
end "read a file name";

simple procedure Print!Header;
begin
    Integer W,D;

    Getformat( W,D );
    Setformat( 0,0 );

    Cprint( Ochan, (Title&Spaces)[1 to if swWIDE then 32 else 20],
	    "created ", CmdDate, spaces[1 to if swWIDE then 32 else 20],
	    "  Page ",PPages,If SubPage > 0 then "."&Cvs(SubPage) else "",
	    Crlf,(Subtitle&Spaces)[1 to if swWIDE then 32 else 20],
	    "by CRFSYM %5 at "& CrefDate, Crlf & Crlf);

    Lines _ 4;				! 3 lines in header, now on line #4;
    Setformat( W,D );

End;

simple procedure Print!Line(String Num!Tab,Line);
Begin
    If last!brk = #FF and Brk = #FF and length(Line) = 0 then Return;
    If Lines = 0 then Print!header;
    Brks _ null;
    If brk = #FF then begin PPages _ PPages + 1; SubPage _ 0; End;
    If brk = 0 then line _ line & Crlf;   		! wrap ?;
    If brk = #LF or brk = 0 then
	If Lines < LPP
	    then Lines _ Lines + 1
	    else begin brk _ #FF; SubPage _ SubPage + 1; End;
    If brk = #FF then begin Pages _ Pages + 1; Lines _ 0; end;
    If brk = #LF or brk = #FF then Brks _ brk&Null;
    If brk = #FF
	then If Length(line) then begin "catch FF at eol or page overflow"
	    Brks _ #FF;					! Change brk;
	    Cprint(OChan, Num!Tab, Line, Brks);		! Write line;
	end "catch FF at eol or page overflow"
	else begin "don't allow FF FF"
	    If last!brk = #FF
		then Pages _ Pages - 1			! Don't add;
		else Cprint(OChan, Brks);		! Print the formfeed;
	end  "don't allow FF FF"
	else Cprint(OChan, Num!Tab, Line, Brks);	! Write line;
    Last!brk _ brk;
End;

simple procedure GetLine;
While Length(Line) = 0 and not EOF do begin "Get Line"
    Line _ input(IChan,B!Lin);				! Read a line;
    If not EOF or Length(Line) then begin
	If Length(Line) then LineNo _ LineNo +1;	! count & list it;
	If swLIST then Print!Line(CvS(LineNo)&#HT,Line);! print line?;
    End;
end "Get Line";

simple string procedure Multi!Line(Integer Char);
While not EOF do begin "read until break"
    If Length(Line) = 0 then GetLine;		! Read more if needed;
    Scan(Line, B!Sub, Brk);			! Ignore other breaks;
    If Brk = Char or EOF then Return( Null );	! Wait for match;
end;

simple string procedure NextName;
begin "get a name"
    String S;

    Do begin
	If In!Macro and Brk = "=" then In!Def _ True;
	If In!Macro and Brk = "," then In!Def _ False;
	If Brk = ";" then In!Macro_In!Comment_In!Def_False;

	While not ( EOF or Length(Line) ) do Getline;	! Get munchies;
	if Length(Line) = 0 and EOF then Return (Null);	! None Left;
	S _ Scan( Line, B!Tok, Brk );			! Get Token;
	If In!Comment then begin
	    S _ Multi!Line(";"); In!Comment_False; end;	! Skip comments;
	If Brk = '42 then S _ Multi!Line("""");		! Skip quotes;
	If Numeric(S) then S _ Null;			! Don't pass numbers;
    end until Length(S) or EOF;

    If equ(S,"!") or equ(S,"COMMENT") then In!Comment _ True
    else If equ(S,"DEFINE") or equ(S,"REDEFINE") then In!Macro _ True;
! Print( If In!Macro then "M" else "",If In!Def then "D" else ""," ",S,Crlf);
    Return( S );
end "get a name";

Recursive Procedure New!Symbol(String Name, Type; Integer New!Value);
begin
    If not ( equ(Type,"RES") )
     then ResItm _ Cvsi( Type&";"&Name, Not!Defined);	! See if PNAME exists;

    If Not!Defined					! No, then  define it;
     then If equ(Type,"MAC") and In!Def
	   then New!Symbol(Name, "SYM", New!Value)
	   else begin

	      That_ Heads:Next[That]_ new!record( heads );
	      This_ Heads:This[That]_ Heads:Here[That]_ new!record( occur );

	      This_ Heads:This[That]_ Occur:Next[This]_ new!record( occur );
	      Occur:Here[ This ]_ New!Value;		! Add line # to list;

	      New!Pname( ResItm _ New(That), Type&";"&Name );

	      If not equ(Type,OldType)
	       then begin
		  OldType _ Type;

		  LI _ Cvsi( "$$LIST$$"&Type, Not!Defined);

		  If Not!Defined
		   then Print( "S:Fatal error - $$LIST$$",Type,
			       " is unknown!",Crlf,Exit);
	       end;

	      Put ResItm in Datum( LI ) after inf;		! ... Add it to list;
	   end

     else begin
	That_ Datum( Resitm );
	This_Heads:This[That]_Occur:Next[Heads:This[That]]_new!record(occur);
	Occur:Here[ This ]_ New!Value;			! Add line # to list;
     end;
end;

Procedure Print!Sorted!List(Integer Size; List Itemvar LS; Boolean Flag);
Begin
    String Array Strs[1:Size];    Integer Itm,Len;    String Type;

    Arrclr(Strs);
    Type _ Cvis( LS, Not!Defined )[9 to inf];
    While Length( Datum( LS ) ) do begin
	String Name;

	ResItm _ LOP( Datum( LS ) );				! get Item;
	Name _ Cvis( ResItm, Not!Defined )[5 to inf];		! get PNAME;

	Itm _ 1;						! setup count;
	While LLT( Name, Strs[Itm] ) Do Itm _ Itm + 1;		! Itm _ Loc;

	While Length(Name) Do
	    Begin Name Swap Strs[Itm];  Itm _ Itm + 1;  End;	! Dirtybubble;
    end;							! Sort Done;

    Lines_ LPP; SubPage_ 0;					! Force page;
    For Itm _ Size step -1 until 1
     do begin "Print them"

	ResItm _ Cvsi( Type&";"&Strs[Itm], Not!Defined);	! Get Item;
	That_ Datum( ResItm );
	This_ Heads:Here[ That ];

	If ( Occur:Next[ This ] )				! ? Print it;
	 then begin "Something to print"

	    If ( LPP leq Lines_ Lines + 1 )			! ? Page over;
	     then begin
		CPrint( OChan, #FF );
		PPages_ PPages + 1;
		Print!Header;
	     end;

	    Cprint( Ochan, (Strs[Itm]&Spaces)[1 to 24] );	! Print it;

	    Len_ 4;
	    While ( This_ Occur:Next[ This ] )
	     do begin

		If ( 0 = Len )
		 then begin
		    If ( LPP leq Lines_ Lines + 1 )		! ? Page over;
		     then begin
			CPrint( OChan, #FF );
			PPages_ PPages + 1;
			Print!Header;
		     end;
		    Len_ 4;
		    Cprint( Ochan, Spaces[1 to 24] );
		 end;

		Cprint( Ochan, Occur:Here[ This ] );

		If ( Width < Len_ Len + 1 )
		 then begin
		    Len_ 0;
		    Cprint( Ochan, Crlf );
		 end;
	     end;

	    If ( Len ) then CPrint( Ochan, Crlf );		! Terminate;

	    Heads:This[ That ]_ This_ Heads:Here[ That ];	! Delete;
	    Occur:Next[ This ]_ null!record;			!  the chain;

	 end "Something to print";

	If ( Flag )						! ? Delete;
	 then Delete( ResItm );					!  the item;

    end "Print them";
End;

Procedure Print!Symbols(String Type, Banner; Boolean Flag(True) );
Begin

    LI _ Cvsi( "$$LIST$$"&Type, Not!Defined);		! Get list of these;

    If Not!Defined then Print
       ( "P:Fatal error - $$LIST$$",Type," is unknown!",Crlf,Exit);

    If Length( Datum( LI ) )
     then begin
	Title_ CmdFile;
	Subtitle_ Banner;
	Print!Sorted!List( Length(Datum( LI )), LI, Flag );
     end;

end;
	


Simple String Procedure TokenType(String Name);
Begin "Token type"

    ResItm _ Cvsi( "RES;"&Name, Not!Defined );

    If not ( Not!Defined )
     then Return( "RES" );

    If ( In!Macro )
     then Return( "MAC" )
     else Return( "SYM" );

end "Token type";

simple procedure HELP(String Inout);
begin "give user help"
If Kequ(Inout,"IN") then print("
Command format:

    [ listing-file = ] input-file [.SAI] [ /optional-switches ]

Current switches:
    /HELP	Type out this message
    /LIST	Produce program listing
    /LONG	Use long 8 LPI (88 lines) paper (default 66)
    /WIDE	Use wide 132 character paper (default 80)
    /CREF	Produce a cross reference symbol listing
		(implies /SYMBOL)
    /MACRO	Include MACROs in CREF listing
    /SYMBOL	Include user symbols in CREF listing
    /RESERVED	Include SAIL reserved words in listing
    /EXIT	Exit to EXEC when done

")
else if Kequ(Inout,"SWITCHES"[1 for length(inout)]) then print("
/HELP:SWITCHES
");
end "give user help";

simple integer procedure checksw(String Nam, Txt);
Return( If Kequ( Nam, Txt[1 for length(Nam)] )	! Compare Nam & Txt;
	then 1					! A match here is true;
	else if length(nam)>2 and Kequ(Nam,("NO"&Txt)[1 for length(Nam)])
	    then 0				! A match here is false;
	    else -1 );				!   else no match - default;

boolean procedure legal!switch(Reference string S; String Inout);
begin
    string SW;    integer V;
    if Brk = "/" then begin "switch search"
	Brk _ Lop(S);
	SW  _ scan(S, B!Nam, BRK);
	if length(SW)=0 then
		begin print("Null switch",crlf); return(false); end;
	i< V _ checksw(SW,"BAIL") then
		begin swBAIL _ V; usererr(v,3,"BAILing ","B"); end
	else if -1 < V _ checksw(SW,"HELP") then swHELP _ V
	else if -1 < V _ checksw(SW,"LIST") then swLIST _ V
	else if -1 < V _ checksw(SW,"LONG") then swLONG _ V
	else if -1 < V _ checksw(SW,"EXIT") then swEXIT _ V
	else if -1 < V _ checksw(SW,"CREF") then swCREF _ V
	else if -1 < V _ checksw(SW,"MACRO") then swMACRO _ V
	else if -1 < V _ checksw(SW,"SYMBOL") then swSYMBOL _ V
	else if -1 < V _ checksw(SW,"RESERVED") then swRESERVED _ V
	else if -1 < V _ checksw(SW,"WIDE") then swWIDE _ V
	else begin "switch error"
	    print("?unrecognized or duplicate switch: ""/",SW,"""",crlf);
	    Return (False);
	end "switch error";
	If swCREF = 0 and (swMACRO > 0 or swSYMBOL > 0 or swRESERVED > 0)
	    then begin "switch error"
		Print("?illegal switch combination /",SW," and /NOCREF",crlf);
		Return(False);
	    end "switch error";
    end "switch search";
    If swHELP > 0 then HELP(Inout);
    Return ( not swHELP > 0 );
end;

simple string procedure MyTime( integer D, T );
begin
    own safe integer array Time[0:2];
    redefine calli!DATUUO = -'55;

    Time[0]_ ((D lsh -2) land '30000) lor (T land '7777);
    Time[1]_ ((T lsh -12) land '3777) * 3600;
    Time[2]_ !xwd( '400020, '500000 );
    calli( location(Time[0]), calli!DATUUO );
    return( TymDay( Time[0] )[1 to inf-3] );

end;
procedure FRONT!END;
begin
    Integer Array Finf[1:6];	! file info array ;
    string S;			! a handy string;
    label INPUT.PROMPT;		! goto here for purpose of reprompting;

!    Print("RPGSW: ",RPGSW,Crlf);
    if RPGSW then begin "RPG"	! was our start address +1?;
	integer C,B,E; string F; integer array T[0:1];
	S _ TMPIN( "SYM", E );	comment SAIL function;
	if E then begin
	    F _ cvs(1000+calli(0,calli!PJOB))[2 to 4]&"SYM.TMP";
	    open(C_getchan,"DSK",1,4,0,256,B,E);
	    lookup(C,F,E);
	    if E then print("?cant read ",F," error code '",E, EXIT);
	    S _ null;		! make sure no ill side-effects;
	    Do S _ S & input(C,0) until E;
	    rename(C,null,0,E);
	    release(C);
	end
	else begin
	    T[0] _ Cvsix("SYM");
	    T[1] _ 0;
	    calli(!Xwd(!TCRDF,T[0]), calli!TMPCOR);	! Delete it;
	end;
    end "RPG";

!	next try for CCL type call:
	  o  look for semicolon in command line
	  o  see if first token is the magic BATCH or TTY command
;
    if not RPGSW then begin
	backup;				! back up monitor TTY read pointer;
	S _ inchsl(EOF);		! read monitor command line;
	while length(S) and not CCLSW do
	    CCLSW _ ( ";" = Tok(S) );	! look for a semicolon;
    end;
    IACSW _ not (CCLSW or RPGSW);	! set interactive flag appropriately;

    INPUT.PROMPT: if IACSW then do begin "prompt"
	Release(IChan); Release(OChan);
	Print("CRFSYM>");
	S _ inchwl;
    end "prompt" until Length(S);

    Initialize!Switches;		! Set swXXX variables to default;

    if S = "?" then begin		! give help and re-prompt?;
	HELP("IN");			!   Type help message;
	If IACSW
	   then goto INPUT.PROMPT	!   If interactive then ask again;
	   else EXIT;			!      else go away;
    end;
    White( S );				! clear out whitespace (if any);
    CmdFile _ GetFil(S, IDevice, Ext!found, File!error);
    If File!Error then begin
	Print("?Error in filename """,CmdFile,"""",crlf);
	If IACSW then goto INPUT.PROMPT else EXIT;
    end;
    If Brk = "=" or Brk = "_" then Lop(S);
    LogFile _ ODevice _ null;		! default file name;
    case BRK of begin
	["="]["_"] begin		! maybe get LOG file name too;
	    White( S );			! clear out whitespace;
	    LogFile _ CmdFile;		! set LogFile;
	    ODevice _ IDevice;		!  and it's directory device;
	    CmdFile _ GetFil(S, IDevice, Ext!found, File!error);
	    If File!Error then begin
		Print("?Error in filename """,CmdFile,"""",crlf);
		If IACSW then goto INPUT.PROMPT else EXIT;
	    end;
	end;
	else
    end;

!	Read off any switches and react to them
;
    while length(S) do
	if not legal!switch(S,"IN") then
	    if IACSW then goto INPUT.PROMPT else EXIT;
    if swEXIT > 0 then IACSW _ False;
    if swRESERVED < 0 then swRESERVED_ 0;
    if swMACRO < 0 then swMACRO_ 0;	! default /NOMACRO ;
    if swLONG < 0 then swLONG_ 0;	! default /NOLONG ;
    if swWIDE < 0 then swWIDE_ 0;	! default /NOWIDE ;

    if length(IDevice&CmdFile) = 0 then
	if IACSW then goto INPUT.PROMPT else EXIT;	! Null filename?;

    If Length(IDevice) = 0 then IDevice _ "DSK";
    open(IChan_getchan,IDevice,0,4,0, 256,BRK,EOF_-1);
    if EOF then begin
	print( "?Illegal INPUT device """,IDevice,"""",Crlf);
	If IACSW then goto INPUT.PROMPT else EXIT;
    end;
    lookup(IChan,CmdName_CmdFile,EOF_-1);	! input file;
    if EOF and not Ext!Found then
	lookup(IChan,CmdName_CmdFile&Default!Ext,EOF);	! alternate?;
    if EOF then begin
	lookup(Ichan,CmdName_CmdFile,EOF_-1);	! Get right error code, etc;
	print ("?input file """,CmdFile,""" ",case !rh(EOF) of (
	    [0] "not found",
	    [1] "invalid user",
	    [2] "protection failure",
	    [else] "LOOKUP error '"&cvos(!rh(EOF)) ),crlf);
	if IACSW then goto INPUT.PROMPT else EXIT;
    end;
    CmdFile _ CmdName;
    FileInfo(Finf);
    CmdDate _ MyTime( Finf[2], Finf[3] );
    CrefDate_ TymDay( CvtDTM( GetTDT, 0, '20 ) )[1 to inf-3];
    if length(Odevice&LogFile)=0 then begin
	ODevice _ IDevice;
	Logfile _ Cvxstr(Finf[1])&".LST";
    end;
    if length(ODevice) = 0 then ODevice _ "DSK";
    open(OChan_getchan,ODevice,0,0,4, 0,0,EOF_-1);
    if EOF then begin
	print( "?Illegal OUTPUT device """,ODevice,"""",Crlf);
	If IACSW then goto INPUT.PROMPT else EXIT;
    end;
    enter(OChan,LogFile,EOF);
    if EOF then begin "do error cleanup"
	print("?output file """,LogFile,""" ",case !rh(EOF) of (
	    [0] "illegal name",
	    [1] "invalid user",
	    [2] "protection failure",
	    [3] "being modified",
	    ['14] "exceeds quota or disk full",
	    [else] "ENTER error '"&cvos(!rh(EOF)) ),crlf);
	LogFile _ null;
	if IACSW then goto INPUT.PROMPT else EXIT;
    end "do error cleanup";

end "FRONT!END";



while ( true )
 do begin "Main Program"

    FRONT!END;			! parse command, open files, etc.;

    That _ Head _ New!Record( Heads );

    Title  _ (CmdFile);    Subtitle _ "Program Listing";
    LineNo _ Lines _ 0;    PPages _ Pages _ 1;    Last!Brk _ #FF;
    LPP_ If swLONG then 80 else 60;
    Width_ if swWIDE then 19 else 12;

    while not( EOF )
     do begin "one file"
	String S, T;
	S _ NextName;
	If not EOF and swCREF
	 then If Case T_TokenType(S) of (
		["SYM"] swSYMBOL,
		["MAC"] swMACRO,
		["RES"] swRESERVED,
		[Else] False
	     ) then New!Symbol( S, T, LineNo );
     end "one file";

    Release(IChan);

    If ( swCREF )
     then begin "produce cross-reference"
	Setformat(6,0);
	Subtitle _ "Cross Reference";
	If swSYMBOL   then Print!Symbols( "SYM","User Defined Symbols" );
	If swMACRO    then Print!Symbols( "MAC","User Defined Macros" );
	If swRESERVED then Print!Symbols( "RES","Reserved Words (SAIL)",False );
    end;

    CPrint( OChan, #FF );
    If swLIST or swCREF then else Rename(OChan, null, 0, EOF);

    Release(OChan);

    if ( swEXIT )
     then done;

end "Main Program";

EXIT;				! Done;

end 	"CRFSYM";
   #@GÍ