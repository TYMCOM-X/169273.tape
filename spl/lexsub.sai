entry;

begin "Lexical Routines"

comment	this set of routines does a full set of lexical comparisons
	equivilent to the eq, le, lt, ge, gt integer routines on
	string variables.

LLT("A","B")	TRUE if "A" is less than "B"
LLE("A","B")	TRUE if "A" is less than or equal to "B"
LGT("A","B")	TRUE if "A" is greater than "B"
LGE("A","B")	TRUE if "A" is greater than or equal to "B"

there is no routine to check for equality of strings since there
is a built-in function EQU("A","B") which is TRUE if "A" = "B"

in the above routines, partial strings will be less than complete
strings with similar beginnings, thus "THE" is LLT "THEN" but is
not EQU to "THEN".  Remember, these are boolean procedures and must
be called as such.
;
Internal Boolean Procedure LLT(String A;  String B);
begin "LLT"
    If EQU(a,b) then return(FALSE);
    While (Length(a) and Length(b)) do begin "checking strings"
	if (a < b) then return(TRUE);
	if (a > b) then return(FALSE);
	lop(a);    lop(b);
    end "checking strings";
    if length(b) then return(TRUE) else return(FALSE);
end "LLT";


Internal Boolean Procedure LGE(String A;  String B);
begin "LGE"
    Return( NOT LLT(A,B) );
end "LGE";

Internal Boolean Procedure LGT(String A;  String B);
begin "LGT"
    If EQU(a,b) then return(FALSE);
    While (Length(a) and Length(b)) do begin "checking strings"
	if (a > b) then return(TRUE);
	if (a < b) then return(FALSE);
	lop(a);    lop(b);
    end "checking strings";
    if length(a) then return(TRUE) else return(FALSE);
end "LGT";


Internal Boolean Procedure LLE(String A;  String B);
begin "LLE"
    Return( NOT LGT(A,B) );
end "LLE";


End "Lexical Routines";
