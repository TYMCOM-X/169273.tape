
begin

require "(SAILIB)SAIL.DEF" source!file;
require "(SAILIB)UUOSYM.DEF" source!file;
require "(SAILIB)MFDSCN.REQ" source!file;
require "(SAILIB)ATFILE.REQ" source!file;
require "(SAILIB)CVPPN.REQ" source!file;

Own string FileLine, FileSpec, CopySpec, Text;
Own Integer Inbrk, InLst, Quest, Brk, File, Ext, UserPPN;
Own integer Tpages, HTpages;


boolean procedure WantFile( Reference Integer Filename, Extension );
begin
    Return( Extension neq CvSix("TMP") );
end;

procedure FileProc( Reference Integer Chan, Cnt, Brk, FileEof; Integer Array LKB );
Begin "Each File"
    Integer Pages, HPages, Lines, LstBrk;
    Pages _ Lines _ 0;
    HPages _ 1;  LstBrk _ #FF;
    While not FileEof do begin "Reading between the lines"
	Text _ input( Chan, inbrk);
	If not FileEof then begin "Counting breaks"
	    If Brk = '12 or Brk = 0
	     then
		If Lines < 60
		 then Lines _ Lines + 1
		 else Brk _ '14;
	    If Brk = '14
	     then begin "count any form-feeds"
		If Lines > 0 or LstBrk neq #FF
		 then HPages _ HPages + 1;
		Pages _ Pages + 1;
		Lines _ 0
	     end "count any form-feeds"
	 end "Counting breaks";
	LstBrk _ Brk;
     end "Reading between the lines";

    If Lines = 0 and Pages = 0
     then If Length(Text)
      then begin
	 Pages _ 1;
	 Hpages_ 2
      end;

    If Lines neq 0
     then begin
	Pages _ Pages + 1;
	HPages_ HPages+ 1
     end;
    SetFormat(3,0);
    Tpages_ Tpages + HPages;  HTPages_ HTPages + (HPages+12) div 13;
    Print(cvxstr(LKB[!RBNAM]),".",cvxstr(LKB[!RBEXT] land '777777000000),
	    "	", Pages, " H[", HPages, "]  ", (HPages+12) div 13 );
    If HPages neq Tpages
     then Print("	(", Tpages, ",", HTpages, ")" );
    Print( Crlf );
    Close( Chan );
    FileEof_ 0;
end "Each File";

setbreak(inbrk_getbreak,#LF&#FF,null,"INS");	! break on LF FF;
setbreak(inlst_getbreak,",",Null,"KINS");	! break on comma;
setbreak(quest_getbreak,"?",Null,"INS");	! break on qmark;
MFDset( 0, 4, 0, 132 );				! ascii, in, out, count;

while ( true )
 do begin
    Print( "File> " );
    FileLine_ AtFile( True );
    If Length( FileLine ) = 0 then Calli(0,Calli!EXIT);

    Tpages_ HTpages_ 0;				! totals cleared;
    While Length( FileLine )
     do begin
	FileSpec_ Scan(FileLine,inlst,brk);	! gobble one file-spec;
	If Length( FileSpec )
	 then begin
	    CopySpec_ Null;
	    Brk_ 0;
	    While length( FileSpec )
	     do CopySpec_ CopySpec & (If Brk = "?" then "#" else null)
			& Scan( FileSpec, quest, Brk );
	    File_ cvfil( CopySpec, Ext, UserPpn );
	    If ( 0 neq (File lor Ext lor UserPpn) )
	     then begin
		If UserPpn = 0
		 then UserPpn_ Calli( !Xwd(-1,!GtPPN), Calli!GETTAB );
		If !lh( UserPpn ) = 0
		 then UserPpn_ CvPPN( Cv6str( memory[ UserPpn   ] ) &
				      Cv6str( memory[ UserPpn+1 ] ) );
		UfdScn( UserPpn, File, Ext, -1, WantFile, FileProc );
	     end
	 end
     end
 end;


end;
