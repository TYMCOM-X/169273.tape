START:	jrst	.+2
	 outstr	[asciz " --CCL start
"]

lc.crd==1b7	;delay CR-style (not LF)
lc.ncm==1b13	;no case mapping (=TTY LC)
lc.hht==1b14	;hardware tabs
lc.hff==1b17	;hardware VT/FF

.axwid==27	;terminal line width
.axrvx==55	;reverse XON enable
.axtyp==56	;terminal type
.axbs==57	;BS for character delete
.axera==62	;erase (BS-space-BS) for character delete
.axede==63	;enter deferred-echo mode permanently
.axo8i==4	;output an image mode character

define set(auxID,v)=<
	HRROI	auxID
	MOVEI	1,v
	AUXCAL	1
>

	SETO	1,
	GETLCH	1
	TLNE	1,(lc.crd)
	 jrst	crd.OK
waitva:
	outstr	[asciz " *Warning: delay = LF*  LCH='"]
	hrlzi	0,(POINT 3,0,35)
	ildb	2,0
octlup:	outchi	"0"(2)
	ildb	2,0
	trnn	0,777776
	 jrst	octlup
	outstr	[asciz "
"]
	move	0,[xwd 40+1,5]	;wait for output complete (5 secs)
	hiber	0,		;this sends a yellow ball and waits
	 jfcl			;for an orange ball (ignore error)
	move	2,1
	seto	1,
	getlch	1
	came	1,2
	 jrst	waitva
crd.ok:	TLO	1,(lc.ncm+lc.hff+lc.crd+lc.hht)
	SETLCH	1

set(.axede,1)	; Force deferred-echo bit
set(.axbs,1)	; TTY BACKSPACE
set(.axera,1)	; TTY ERASE
set(.axrvx,1)	; REVERSE XON
set(.axwid,^D79); SET LINE WIDTH TO 79 (80 CAUSES AUTO-CRLF TROUBLE)

	outstr	[byte (7) 15,33,"3",15,0]	;CR, clear all tabs, CR
	hrroi	1,-^D9		;number of tab stops
	hrroi	.axo8i		;for image outputs
setlup: outstr	[byte (7) 40,40,40,40,40,40,40,40,0] ;8 spaces
	auxcal	33		;set tab
	auxcal	"1"		;(must be image to stop auto CRLF
	aojl	1,setlup
	outchi	"*"
	outchi	15		;CR to get back to beginning of line
	; IDENTIFY SYSTEM
	OUTSTR	[ASCIZ /System /]
	hrlzi	2,11	;system name in table 11, words 0-4
	setz	4,	;so that ac3 will be ASCIZ
namlup:	movs	3,2	;pick up index,,table
	gettab	3,	;read in table
	 jrst	gtfal
	outstr	3
	addi	2,1
	camg	2,[xwd 11,4]	;system name in words 0-4
	 jrst	namlup
	jrst	.+2
gtfal:	outstr	[ASCIZ / GETTAB failure?!/]
	OUTSTR	[ASCIZ / [K/]

	setz	1,	;now check for KI or KL
	BLT	1,
	movei	2,"I"
	skipe	1
	movei	2,"L"
	outchr	2	;and output processor letter
	outstr	[ASCIZ /-10]/]
	movei	[sixbit /ZEN/
		 [sixbit /MPL/
		  0]]
	setalp
	 jfcl
	EXIT	1,
	EXIT
END START
