begin "helasc"
integer onai;
string handled, ctxt;
own safe integer array counts,seen[0:'177];
own safe string array substitute[0:'177];
integer och,ch, cnt,brk,eof;

simple procedure subs( integer ch; string txt );
	begin seen[ch]_true; substitute[ch]_ txt; end;
simple procedure brkSub( string txt ); subs(brk,txt);

procedure interact;
begin	string txt;external integer !skip!;
print(" I saw '", cvos(brk), " [^", char!w(brk+'100), 
	"]. Substitute: ");
txt_ inchwl; 
if !skip!='15
 then brkSub(txt)
 else begin
	outstr("
");
	if !skip!='33	then substitute[brk]_ txt
	 else if !skip! neq '12 then usererr(0,1,"Handle it");
      end;
end;

handled_ '177;
for onAi_ 1 step 1 until '10, '13, '16 step 1 until '37
  do handled_ handled & onAi;
setbreak(onAi_getbreak, handled, null,"INS");
open(ch_getchan,"DSK",1, 4,0, cnt_500,brk,eof);
open(och_getchan,"DSK",1, 0,4, cnt,brk,eof);
do begin print("HEL File: "); lookup(ch,inchwl,eof); end
 until eof=false;
do begin print("output File: "); enter(och,inchwl,eof); end
 until eof=false;
arrclr(seen);
subs(0,null);
subs('30, "!");
subs('31, "->");
subs('37, "<or>");
subs('04, "<and>");
subs('33, "<neq>");
subs('34, "<leq>");
subs('35, "<geq>");
while not eof
   do begin
	do out(och,ctxt_ input(ch,onAi)) until brk or eof;
	if brk='31
	 then if '31 neq brk_ wordin(ch)
	     then usererr(brk,3," Unexpected character after arrow: ")
	     else
	 else if not seen[brk] then interact;
	out(och,substitute[brk]);
	quick!code aos access(counts[brk]); end;
      end;
release(ch);
release(och);

open(och_getchan,"DSK",1, 0,4, cnt,brk,eof);
do begin print("Summary File: "); enter(och,inchwl,eof); end
 until eof=false;

for brk_0 step 1 until '177
   do if seen[brk] or length( substitute[brk] )
	 then begin
		cprint( och,"
subs('", cvos(brk), ", """, substitute[brk], """);");
		if not seen[brk]
		 then cprint(och, " seen['", cvos(brk), "]_ false;");
	      end;
cprint(och,"

COMMENT	counts: ");
for brk_1 step 1 until '177
   do if counts[brk] then cprint( och," '", cvos(brk), 
		": ", counts[brk] );
cprint(och,"
;
");
release(och);
end;
