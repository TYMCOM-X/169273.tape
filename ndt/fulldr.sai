begin
require 2 version;
require "[][]" delimiters;
define	extendedLookup(ch,base)=[chnior(ch,base,4)];
define crlf=[('15&'12)], tab=[('11&null)], !=[comment], space=[" "];
define lookSize='37; 
integer array lukb[0:lookSize];
external integer !skip!;
require "(SAILIB)BRKFNS.HDR" source!file;	! gets rvscan;

define !ind=0;
forlc xx=
 (PPN,NAM,EXT,PRV,SIZ,VER,FUT,EST,ALC,POS,FT1,LIC,MTA,DEV,STS,ELB,XT1,
	QTF,QTO,MXA,USD,AUT,UNM,UN1,TRU,XT2,ALP,SNM,PJC,PJ1,PJ2)
 doc [redefine  !ind=!ind+1; define !RB]&[xx=!ind;] endc;
integer passWhite;

string simple procedure cvhalf(integer x); 
begin integer w,d; string res;
getformat(w,d); setformat(6,d);
res_ cvos(x lsh -18)&",,"&cvos(x land (-1 lsh -18));
setformat(w,d);
return( res );
end;

string simple procedure clip( string s );
begin
rvscan(s,passWhite,!skip!);
return( s );
end;

simple procedure aBreak;
	setbreak(passWhite_getbreak,tab&space,null,"XR");
require abreak initialization;
string simple procedure cvuser( integer ppn );
begin	boolean flag; integer ch;
	own safe integer array lb[0:!RBUNM+1];
arrclr(lb); 
lb[0]_!RBUNM+1; 
lb[!RBPPN]_(1 lsh 18)+1; lb[!RBNAM]_ppn; lb[!RBEXT]_cvsix("UFD");
open(ch_getchan,"DSK",'17, 0,0, 0,0,flag_false);
if flag then usererr(0,1,"Can't get DSK")
 else extendedLookup(ch,lb[0]);
release(ch);
return( if lb[!RBUNM]
 then "("&clip(cvxstr(lb[!RBUNM])&cvxstr(lb[!RBUNM+1]))&")" 
 else "["&cvos(ppn lsh -18)&","&cvos(ppn land (-1 lsh -18))&"]"
);
end;

string procedure cvfname( integer ppn, nam, ext );
begin	string name, user;
name_clip(cvxstr(nam))&"."&clip(cvxstr(ext land (-1 lsh 18)));
return( if "("=(user_cvuser(ppn)) then user & name else name & user );
end;
simple procedure showLukb;
begin
preload!with	null
 forlc xx=
 (PPN,NAM,EXT,PRV,SIZ,VER,FUT,EST,ALC,POS,FT1,LIC,MTA,DEV,STS,ELB,XT1,
	QTF,QTO,MXA,USD,AUT,UNM,UN1,TRU,XT2,ALP,SNM,PJC,PJ1,PJ2)
 doc [,".RB"&"xx"] endc;
own safe string array indexName[0:lookSize];
integer ind;
print( crlf, cvfname( lukb[!RBPPN], lukb[!RBNAM], lukb[!RBEXT] ),
	tab, cvuser(lukb[!RBAUT]) );
setformat(6,0);
for ind_!RBEXT step 3 until lookSize-2
   do print(	crlf, indexName[ ind ], ": ", cvhalf(lukb[ ind ]),
		 tab, indexName[ind+1], ": ", cvhalf(lukb[ind+1]),
		 tab, indexName[ind+2], ": ", cvhalf(lukb[ind+2]) );
if ind<=lookSize
 then print(	crlf, indexName[ ind ], ": ", cvhalf(lukb[ ind ]));
if (ind_1+ind)<=lookSize
 then print(	 tab, indexName[ ind ], ": ", cvhalf(lukb[ ind ]));
end;
integer ch,eof,rtv;
open(ch_getchan,"DSK",'17, 0,0, 0,0,eof_false);
if eof then usererr(0,0,"cannot get DSK","X");
arrclr(lukb);
lukb[0]_lookSize;
  do  begin
	print(" File: ");
	lukb[2]_cvfil(inchwl,lukb[3],lukb[1]);
	extendedLookup(ch,lukb[0]);
      end
 until !skip!;
release(ch);
showLukb;

end;

