begin "postFiles"
require 3 version;
require "ct9.hst" source!file;
require "(sailib)assert.sai" source!file;
require "(sailib)randio.req" source!file;
require "{}{}" delimiters;
define	!="COMMENT"
	,crlf="('15&'12)", tab="('11&null)", space="('40&null)"
	,eif={ELSE IF}
	;
external integer !skip!, rpgsw;
let	class=record!class
	,r!p=record!pointer
	,function=procedure
	;

define unwritten(type,name,arglist,text)={ 
  type PROCEDURE name argList;
	begin require "["&cvps(name)&"]" message; end};

define gettab( index!Table )={calli(index!table,'41)};
define getUUN= {(gettab(-'23) land '777777)};

string ThisSystem;
simple procedure readSystem;
	ThisSystem_ scanc( cvastr(gettab('11)), 
		"ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789",null,"XS");
 require readSystem initialization[0];

boolean procedure leads(string test, pattern);
return( equ(test, pattern[1 for length(test)]) );

COMMENT \ actual sample for file

AUTHORS: [211135]=SDD,[4]=*,.

PRODUCT:POST	2, 21-JUN-80/1233
 MAIN	C33:(SDANIELS)POST.SAI	1,1	21-JUN-80/1233	??????
 DATA	C33:(SDANIELS)POST.POS	1,2	21-JUN-80/1233	??????

HISTORY:productName
 2 21-JUN-80/1233, DATA(SDD)1: Create sample data file for POST
 1 21-JUN-80/1233, MAIN(SDD)1: Write program

\;


comment FILE FORMAT:
<leader>

AUTHORS: [211135]=SDD, [4]=*, ...,
 [211135]=SDD, [4]=*, ... .

PRODUCT:name	Pseq, date/time
 piece	sys:(dir)file.ext  fSeq,pSeq	date/time	cksums
 piece	sys:(dir)file.ext  fSeq,pSeq	date/time	cksums

HISTORY:productName
 pSeq date/time, piece(AUT)Fseq: text
 pSeq date/time, piece(AUT)Fseq: text
 pSeq date/time, piece(AUT)Fseq: text

<trailer>
;

comment TYPICAL "SESSION"
.post post
(POST.POS by SDD)
:post main=sail.sai:added new feature to crash system
checksum:
 MAIN C33:(SDANIELS)SAIL.SAI 2,5 01-JAN-84/00:01 ??????
 5 01-JAN-84/00:01, MAIN(SDD)2: added new feature to crash system
:abort
:post main
file [C33:(SDANIELS)POST.SAI]:
Comment: added new feature to crash system
 MAIN C33:(SDANIELS)POST.SAI 2,5 01-JAN-84/00:08 ??????
 5 01-JAN-84/00:08, MAIN(SDD)2: added new feature to crash system
:done
.
.
.start
product: sail.hst
(SAIL.HST by SDD)
:post hel=(sail)hel:removed declaration productions
checksum: mumbul
 HEL C33:(SAIL)HEL. 23,255 01-JAN-84/0025 MUMBUL
 255 01-JAN-84/0025, MAIN(SDD)23: added new feature to crash system
:done
;
boolean function confirm(string proposition);
begin	string s; 
	boolean oldtty,result;
	integer brk;
oldtty_ ttyup(true);
while true
  do  begin
	print("  ", proposition, " [CONFIRM]");
	s_inchwl; brk_!skip!;
	if (result_ leads(s,"OK") or leads(s,"YES"))
	 then if brk='15 or brk='33 then done else
	 else if leads(s,"NO") then done;
	print(" YES or NO please. ");
      end;
ttyup( oldtty );
return(result);
end;

string function nowString;
begin	integer w,d, date, time;
	string result;
	preset!with
		"-JAN-","-FEB-","-MAR-","-APR-","-MAY-","-JUN-", 
		"-JUL-","-AUG-","-SEP-","-OCT-","-NOV-","-DEC-";
	 own safe string array month[0:11];

date_calli(0,'14);

getformat(w,d); setformat(-2,d);	! 2-digit numbers for CVS;
result_ cvs((date mod 31)+1) 
	& month[(date_date div 31) mod 12]
	& cvs((date div 12)+64)
	;
setformat(w,d);				! restore CVS format;
return(result);
end;
boolean	changedBase;
string	outFileName;		! name of file to write to;
integer	ouch,oEof;		! output Channel (named above);

string	inFileName;		! name of file reading from;
integer	inch,icnt,ibrk,iEof;	! channel reading from;

class part(	string	name	! part name (not fileName);
			,file	! file current version is on;
			,when	! date/time last changed;
			,cksum	! checksum of file (if available);
			,des	! short description of this part;
		;integer lVer	! local (part) version this change;
			,gVer	! global (product) version this change;
		;r!p(part) link	! the obvious;
	);

class hist(	string	when	! date/time this change was entered;
			,why	! reason/explanation for change;
		;integer lVer	! local (part) version this change;
			,gVer	! global (product) version this change;
		;r!p(part) what	! object (part) changed;
		;r!p(hist) link	! the obvious;
	);

string	authorID	! initials for commenting changes;
	,productName	! name to call product;
	,productDate	! previous write date/time;
	;
integer	productVersion;	! version number of product (change count);
r!p(part) productPieces;	! linked list of pieces of product;
r!p(hist) newHistory;	! history elements added this session;
class author(	integer	uun		! UUN of user (0 for named);
			,initials	! cvsix(initials) * for many;
		;string	name
		;r!p(author)	link	! the obvious;
	);
r!p(author) authorChain; 	! linked list of author data;

integer	leaderPoint	! character pointer to the AUTHORS: line;
	,trailerPoint	! character pointer after the HISTORY: line;
	;


integer ParenBreak, authorDelimiters, lineBreak, passWhite, toWhite
	,passDot, UpToWhite, postBreak, fileBreaks
	;
simple procedure someBreaks;
begin
setbreak( ParenBreak_ getbreak, "({[<>]})", null, "IS");
setbreak( authorDelimiters_ getbreak, "=(,.", space&tab&crlf, "IS");
setbreak( lineBreak_ getbreak, '12&'14, '15, "INS");
setbreak( passWhite_ getbreak, space&tab, space&tab, "XR");
setbreak( toWhite_   getbreak, space&tab, space&tab, "IR");
setbreak( UpToWhite_ getbreak, space&tab, space&tab, "IRK");
setbreak( passDot_   getbreak, ".", null, "IA");
setbreak( postBreak_ getbreak, "=-/", space&tab, "INKS");
setbreak( fileBreaks_getbreak, ":().", space&tab, "INKS");
end;
require someBreaks initialization[0];

string function WhiteEdge(reference string s);
begin 
scan(s,passWhite,!skip!); 
return( scan(s,toWhite,!skip!) ); 
end;

string function spacedUp(reference string s);
begin 	string rslt;
scan(s,passWhite,!skip!); 
rslt_ scan(s,UpToWhite,!skip!); 
scan(s,passWhite,!skip!); 
return( rslt ); 
end;
procedure newAuthorEntry( integer uun,id; string name(null) );
begin r!p(author) chase,r;
r_chase_authorChain;
while chase
 do if author:initials[chase]=id and (uun=0 or author:uun[chase]=0)
	 then done
	 else chase_ author:link[r_chase];
if chase=null!record
 then begin
	chase_new!record(author);
	if r then author:link[r]_ chase else authorChain_chase;
      end;
if uun then author:uun[chase]_ uun;
if id  then author:initials[chase]_ id;
if length(name) then author:name[chase]_ name;
end;


string procedure findVal( integer uun );
! returns initals for user with the specified UUN.  If no such UUN is
  listed, this returns null.  If the UUN is listed as "ask always",
  this returns "*".
;
begin r!p(author) chase;
chase_authorChain;
while chase
   do if author:uun[chase] = uun
	 then return( cv6str( author:initials[chase] ) )
	 else chase_author:link[chase];
return(null);
end;


string procedure AuthorPrint( r!p(author) r );
! returns print element for record r (no comma).
;
  return( 
    cv6str(author:initials[r])
    & (if length(author:name[r]) 
	then "(" & author:name[r] & ")" else null)
    & (if author:uun[r] then "=" & cvos(author:uun[r]) else null)
  );

boolean function AuthorEat( string s );
! reads author elements from S and puts them in the author data base.
  returns true if line processed should be the last line.
;
while true
   do begin	integer uun,id,brk; string token;
	id_cvsix(token_ scan(s, authorDelimiters, brk));
	if id=0 and brk="," then continue;
	if id=0
	 then begin assert(brk="." or length(s)=0); return(brk=".") end;
	assert({ equ(cv6str(id),token) and (brk="=" or brk="(") });
	if brk="("
	 then begin
		token_ scan(s, ParenBreak, brk);
		assert({ length(token) and brk=")" });
		newAuthorEntry(0,id,token);
		token_ scan(s, authorDelimiters, brk);
		assert({ length(token)=0 and brk neq "(" });
	      end;
	while brk="="
	   do begin
		uun_cvo(token_ scan(s, authorDelimiters, brk));
		assert( equ(cvos(uun),token) );
		newAuthorEntry(uun,id,null);
	      end;
	if brk="." or brk=0 then return(brk=".");
	assert({ brk="," });
      end;	
boolean procedure readAuthors( string firstLine );
begin
authorChain_null!record;
while not ieof and not authorEat( firstLine )
   do firstLine_ input(inch,Linebreak);
return( length(input(inch,Linebreak))=0 and ieof=false );
end;

procedure writeAuthors;
begin	r!p(author) chase; string element; integer chars;
cprint(ouch, "AUTHORS: "); chars_ 72-length("AUTHORS: "); 
chase_ authorChain;
while chase
  do  begin
	element_ authorPrint(chase);
	if chars < length(element)
	 then begin cprint(ouch, crlf&tab); chars_ 72-8; end;
	cprint(ouch, element );
	chars_ chars-length(element)-1;
	if (chase_ author:link[chase])
	 then cprint(ouch, ",")
	 else done;
      end;
cprint(ouch, "." & crlf & crlf);
end;

boolean procedure setRead(string Name);
begin	string passed;integer brk;
passed_ scan(Name,passDot,brk);
inFileName_ passed & (if brk="." then Name else ".HST");
open(inch_getchan, "DSK",'20000, 2,0, icnt_150,ibrk,ieof_0);
			! ^ let me handle illegal file names;
	assert(not ieof,no DSK:?);
lookup(inch, inFileName, ieof);
if ieof then release(inch);
return(ieof=false);
end;
r!p(part) procedure findPiece( string pieceName );
begin	r!p(part) r;
r_productPieces;
while r
  do if equ(pieceName,part:name[r]) then done else r_ part:link[r];
return(r);
end;

procedure addPart(string name, file; integer vF, vP
		;string date, cksum, descr(null) );
begin	r!p(part) r;
r_new!record(part);
part:name[r]_name;
part:file[r]_file;
part:when[r]_date;
part:cksum[r]_cksum;
part:lVer[r]_vF;
part:gVer[r]_vP;
scan(descr,passWhite,!skip!);	! clip off any leading whiteSpace;
part:des[r]_descr;
part:link[r]_productPieces;
productPieces_r;
end;

boolean procedure readProduct;
! True if well-formed
;
begin	string s;
	integer brk;
s_input(inch,LineBreak);
if equ(s[1 for 8],"PRODUCT:")
 then begin
	s_s[9 to inf];
	productName_scan(s,toWhite,brk);
	productVersion_intscan(s,brk);
	if brk neq "," then return(false);
	productDate_ s[3 to inf];
	productPieces_ null!record;
	while length(s_input(inch,lineBreak))
	   do begin	string part,file,date,cksum;
			integer vF,vP;
		if length(part_WhiteEdge(s))=0 then done;
		file_whiteEdge(s);
		vF_intscan(s,brk); 
		vP_intscan(s,brk);
		date_whiteEdge(s);
		cksum_whiteEdge(s);
		assert(length(date) and vP leq productVersion);
		addPart(part,file,vF,vP,date,cksum,s);
	      end;
	return(true);
      end;
return(false);
end;
boolean procedure spotHistory;
! True if well-formed;
begin	string s;
do s_ input(inch,lineBreak) until length(s) or ieof;
TrailerPoint_ rchptr(inch);
return( equ( s, "HISTORY:"&productName ) );
end;
procedure getFile;
while true do
begin	string s,token; integer brk;
if rpgsw then begin rpgsw_false; backup; end else print("Product: ");
s_ inchwl; 
scan(s,passWhite,brk); 
  do  begin
	token_ scan(s,toWhite,brk); 
	scan(s,passWhite,brk); 
      end
 until length(s)=0;
if length(token) and setRead(token)
 then begin
	  do  begin
		do s_input(inch,lineBreak)
		 until ieof or length(s)=0;
		  do  begin 
			leaderPoint_rchptr(inch);
			s_input(inch,lineBreak)
		      end
		 until ieof or length(s);
	      end
	 until ieof or equ(s[1 for 8],"AUTHORS:");
	if not ieof
	and readAuthors(s[9 to inf])
	and readProduct
	and spotHistory
	 then return;
	print( inFileName, " is not in POST history format." & crlf );
	release(inch);
      end
 else print( "Cannot read history file." & crlf );
end;
boolean procedure acceptable(string initials);
! checks out input initials, 
  will ask for or confirm personal name before success return
  returns true if initials ok.
;
begin	integer initl;
	string s;
	r!p(author) chase;
initl_cvsix(initials);
if length(cv6str(initl)) neq length(initials)
or not (1<length(initials)<5)
or initials="*"
 then return(false);

chase_authorChain;
while chase
   do if author:initials[chase] = initl
	and length(author:name[chase])
	 then return( confirm( cv6str(initl)&"="&author:name[chase] ) )
	 else chase_author:link[chase];

print(" New author.  Type full name: ");
if length(s_inchwl)=0 then return(false);
newAuthorEntry( 0, initl, s );
return(true);
end;
string procedure initials(integer uun);
! returns appropriate initials for this user;
begin	string stored, inp;

stored_ findVal(uun);
if length(stored) and stored neq "*" then return(stored);
	
do print(crlf,"Enter your initials: ")
  until equ(inp_inchwl,"*") or acceptable(inp);

if length(stored)=0 then newAuthorEntry(uun, cvsix(stored_inp), null);

if inp="*"
 then do print(crlf,"Enter your initials: ")
	  until acceptable(inp_inchwl);

return( cv6str(cvsix(inp)) );
end;
procedure writeOnePart( r!p(part) r );
  cprint( ouch, crlf&space, part:name[r], tab, part:file[r]
	,tab, part:lVer[r], ",", part:gVer[r] 
	,tab, part:when[r], space, part:cksum[r], space, part:des[r]
	);

procedure writeProduct;
begin	r!p(part) r;
cprint(ouch, "PRODUCT:" 
	,productName, tab, productVersion, ", ", productDate );

r_null!record;	! reverse chain for printOut;
while productPieces
   do begin 
	r swap part:link[productPieces];
	if r
	 then part:link[r] swap productPieces
	 else r swap productPieces;
      end;
! now print (note: data is gone when done);
while r do begin writeOnePart(r); r_ part:link[r]; end;
cprint(ouch,crlf);
end;


procedure oneHistory( r!p(hist) r );
cprint(ouch, space, hist:gVer[r], space, hist:when[r], ","&space
	,part:name[ hist:what[r] ], "(", authorID, ")", hist:lver[r] 
	,"/ ", hist:why[r], crlf );

procedure addHistory(r!p(part)piece; string file, cksum, text );
begin	r!p(hist)r;
r_new!record(hist);
hist:link[r]_newHistory;
hist:what[r]_piece;
hist:when[r]_part:when[piece]_ nowString;
hist:lVer[r]_part:lVer[piece]_ part:lVer[piece] + 1;
hist:gVer[r]_part:gVer[piece]_ productVersion_ productVersion + 1;
if length(file)
 then begin
	if length(part:file[piece]) and not equ(part:file[piece],file)
	 then print("(was on file: ", part:file[piece], ")" & crlf );
	part:file[piece]_ file;
      end;
part:cksum[piece]_cksum;
hist:why[r]_ text;	require "[massage message]" message;
newHistory_ r;
end;
procedure writeFile;
begin
open(ouch_getchan,"DSK",'20000, 0,4, 0,0,oeof);
assert(oeof=false,DSK must be available for output);
enter( ouch, outFileName, oeof );
while oeof
  do  begin	string name,ext;
		integer brk;
	print(crlf&"Couldn't create output file DSK:", outFileName
		," try again: ");
	name_ scan( ext_inchwl, passDot, brk );
	if length(name)
	 then outFileName_ name & (if brk="." then ext else ".HST");
	enter( ouch, outFileName, oeof );
      end;
schptr(inch,0);
do cprint( ouch, input(inch,lineBreak), crlf )
 until leaderPoint leq rchptr(inch);
WriteAuthors;
writeProduct; 
cprint( ouch, crlf & "HISTORY:", productName, crlf );
while newHistory 
   do begin 
	oneHistory( newHistory ); 
	newHistory_ hist:link[ newHistory ];
      end;

schptr(inch,trailerPoint);
do cprint( ouch, input(inch,0) ) until ieof;
release(ouch); ouch_-1;
end;
procedure ShowLastChange;
if newHistory 
 then begin
	ouch_-1;	! output to terminal;
	oneHistory( newHistory );
	writeOnePart( hist:what[ newHistory ] );
      end
 else print(" <no changes> ");

string procedure massageFile( string fileName );
begin	string fc, sys,dir,name,ext, token;
	integer brk;
	label retry;

	procedure failed(string why);
	begin
	  print(" file `", fileName, "' :", why, " please re-enter: ");
	  fileName_ inchwl;
	  go reTry;
	end;

	procedure sets(reference string vbl; string val, name);
	begin
	if length(vbl)=0 then vbl_val
	eif length(val)>0 then failed("Multiple " & name);
	token_null;
	end;

RETRY:
fc_fileName;
token_ scan(fc,fileBreaks,brk);
if brk=":"
 then begin sys_ token; token_ scan(fc,fileBreaks,brk) end
 else sys_ null;

name_ null; ext_null; dir_null;
   do begin
	sets(name,token,"File Names");
	if brk="("
	 then begin
		sets(dir, scan(fc,fileBreaks,brk), "Directories");
		if brk=":"
		 then sets(sys, scan(fc,fileBreaks,brk), "Systems");
		if brk neq ")" then failed("Bad directory name");
		token_ scan(fc,fileBreaks,brk);
	      end
	eif brk=":"
	 then sets(sys, scan(fc,fileBreaks,brk), "Systems")
	eif brk="."
	 then sets(ext, scan(fc,fileBreaks,brk), "Extensions")
	eif brk then failed("Unrecognizable punctuation");
      end
 until brk=0;
if length(name)=0 then failed("No Name");
if length(dir )=0 then dir_cv6str(gettab(-'22))& cv6str(gettab(-'21));
if length(sys )=0 then sys_ThisSystem;
return(sys & ":(" & dir & ")" & name & "." & ext);
end;
procedure describe(string line);
begin	string pieceName, explanation;
	integer brk;
	r!p(part) piece;
pieceName_scan(line,postBreak,brk);
if length(pieceName)=0
 then begin
	print(" Product piece name: ");
	pieceName_scan(line_inchwl,postBreak,brk);
	if length(pieceName)=0
	 then begin print("Must describe something"); return; end;
      end;
explanation_ (if brk="/" then scan(line,0,brk) else null);
if brk or length(line)
 then begin print("Bad format"); return; end;

if null!record= piece_findPiece(pieceName)
 then begin print("POST this piece first"); return end;

scan(explanation, passWhite, brk);
if length(explanation)=0
 then begin 
	print(crlf & "Description of ", part:name[piece], ": ");
	explanation_inchwl;
	while !skip!='12 do explanation_ explanation & space & inchwl;
	scan(explanation, passWhite, brk);
      end;
if length(explanation)=0
 then print("Null descriptions are not allowed.")
 else begin
	part:des[piece]_ explanation;
	changedBase_true;
      end;
ouch_-1;	! output to terminal;
writeOnePart( piece );
end;
procedure post(string line);
begin	string token, pieceName, fileName, explanation, checksum;
	integer brk;
	r!p(part) piece;
pieceName_scan(line,postBreak,brk);
if length(pieceName)=0
 then begin
	print(" Product piece name: ");
	pieceName_scan(line_inchwl,postBreak,brk);
	if length(pieceName)=0
	 then begin print("Need something to change"); return; end;
      end;
fileName_ (if brk="=" then scan(line,postBreak,brk) else null);
checksum_ (if brk="-" then scan(line,postBreak,brk) else null);
explanation_brk="/" then scan(line,0,brk) else null);
if brk or length(line)
 then begin print("Bad format"); return; end;

if null!record= piece_findPiece(pieceName)
then if confirm("New product Part `" & pieceName & "'.")
	 then begin
		addPart( pieceName, "unknown", 0,0,nowString, checksum);
		piece_ findPiece(pieceName);
		changedBase_true;
	      end
	 else begin
		print( " Current parts are: ");
		if piece_ productPieces
		 then do print( crlf, part:name[piece], 
				"=", part:file[piece], 
				tab, part:des[piece] ) 
			until null!record = piece_part:link[piece];
		return;
	      end;
if length(fileName)=0
 then begin
	print(" File name [", part:file[piece], "]: ");
	if length( fileName_scan(line_inchwl,postBreak,brk) )=0
	 then fileName_ part:file[piece];
	if brk="-" then checksum_ scan(line,postBreak,brk);
	if brk="/" then explanation_explanation & scan(line,0,brk);
	if brk then begin print("Invalid Format"); return; end;
      end;
if length(checksum)=0
 then begin
	print(" Checksum (if known): ");
	if length( checkSum_ scan(line_inchwl,postBreak,brk) )=0
	 then checkSum_ "??????";
	if brk="/" then explanation_explanation & scan(line,0,brk);
	if brk then begin print("Invalid Format"); return; end;
      end;
scan(explanation, passWhite, brk);
while length(explanation)=0
   do begin
	print(crlf,"Comments: ");
	explanation_inchwl;
	while !skip!='12 
	   do begin 
		print( '15 & "(C):" ); 
		explanation_ explanation & space & inchwl;
	      end;
	scan(explanation, passWhite, brk);
      end;
addHistory(piece, massageFile(fileName), checksum, explanation);
ShowLastChange;
end;
procedure setOut(string name);
begin	string ext;
	integer brk;
if length( ext_spacedUp(name) )=0
 then begin 
	print(" New history file name: ");
	ext_spacedUp(name_inchwl);
      end;
if length(name)=0
 then name_scan( ext, passDot, brk );
if length(name)
 then print(" Now: ", 
	outFileName_ name & (if brk="." then ext else ".HST") )
 else print("[ignored]");
end;
boolean procedure processCommands;
! return true if file should be written out
;
while true do
begin	string command,rest;
print(crlf&">");
if length(command_ spacedUp(rest_inchwl))=0 then continue;

if equ(command,"ABORT") then return(false);

if  leads(command,"POST") then post(rest)
eif leads(command,"DESCRIBE") then describe(rest)
eif leads(command,"OUTPUT") then setOut(rest) 
eif equ(command,"BAIL") then usererr(0,1,"You want BAIL?", "B")
eif equ(command,"DDT")	then usererr(0,1,"You want DDT?", "D")
eif leads(command,"HELP") or command="?"
			then print(
"	DESCRIBE [<piece>] [/ <piece description>]
	POST [<piece>] [=<file>] [-<checksum>] [/ <change description>]
	OUTPUT [<new history file name>] 
	ABORT
     or DONE ")
eif leads(command,"DONE") or leads(command,"EXIT") 
 or leads(command,"QUIT") then return(true)
else print(" try HELP");
end;
procedure setup; 
begin	integer uun;
UUN_ getUUN;
getFile; 
authorID_ initials(uun);
print("  ", productName, "[", inFileName, "] by ", authorID);
outFileName_ inFileName;
end;
integer V;

setup; V_ productVersion; changedBase_ false;
if processCommands
 then if V=productVersion and equ(inFileName,outFileName)
      and not changedBase
	then print(" <no changes> ")
	else writeFile
 else print(" <aborted> ");

require "end" message;
end "postFiles" $

  wFØ