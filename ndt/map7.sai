begin "Zork Compressor"
require 7 version;
require "{}{}" delimiters;
define	tab={('11&null)}, lf={('12&null)}, ff={('14&null)}, 
	cr={('15&null)}, esc={('33&null)}, eol={('37&null)}, 
	spc={('40&null)}, del={('177&null)}, crlf={('15&'12)},
	white={('11&'12&'14&'15&'40)},
	!={Comment};
ifcr not declaration(TENX)
thenc	ifcr declaration(openfile) 
	thenc	define TENX(x)={x}, T10(x)={};
	elsec	define TENX(x)={}, T10(x)={x};
endc	endc

T10({ require "(SAILIB)ASSERT.SAI" })
TENX({ define assert(x,why)={if x then else 
		usererr(0,0,cvps(x)&" failed " &cvps(why))}; 
	COMMENT })
		source!file;

external integer !skip!
		 ;
define	LowAlpha={"abcdefghijklmnopqrstuvwxyz"}
	,UpAlpha={"ABCDEFGHIJKLMNOPQRSTUVWXYZ"}
	,Digits={"0123456789"}
	,Letters={(LowAlpha&UpAlpha&Digits&"-_")}
	,Separators={",;:&=<>()[]\|^"}
	,Punctuation "be" =b(""",./?;+!#$%&'*=-:<>()[]{}\|^@`~")e
	;


integer  RdLine 	! INCHWL-style Break (read one line);
	,EatWhite	! consumes White Space (none out);
	,ToWhite	! Stops on White Space (none out);
	,OctDig 	! passes octal digits;
	,LetDig 	! passes letters and digits;
	,UpLetDig	! passes letters and digits, forces Up;
	,PassPunct	! passes Punctuation;
	,PassPunct!W	! passes Punctuation,Ignoring WhiteSpace;
	,NoWhite	! ignore WhiteSpace, pass all else;
	,ForceUp	! passes everything, forces up;
	,ToSep		! stops on separators, ignores them.;
	,NoUnder	! drops Underscores;
	,ToBreaker	! passes all "name" parts;
	;
simple procedure brkset;
begin
setbreak( rdLine	_getbreak, lf & ff, cr, "INS" );
setbreak( EatWhite	_getbreak, white, white, "XNR" );
setbreak( ToWhite	_getbreak, white, white, "INS" );
setbreak( OctDig	_getbreak, "01234567", null, "XNR" );
setbreak( LetDig	_getbreak, Letters, null, "XNR" );
setbreak( UpLetDig	_getbreak, Letters, null, "XNRK" );
setbreak( PassPunct	_getbreak, Punctuation, null, "XNRK" );
setbreak( PassPunct!W	_getbreak, Punctuation&white, white, "XNRK" );
setbreak( NoWhite	_getbreak, null, white, "INR" );
setbreak( ForceUp	_getbreak, null, null, "INK" );
setbreak( ToSep		_getbreak, separators, null, "IS" );
setbreak( NoUnder	_getbreak, null, "_"&white, "INR" );
setbreak( ToBreaker	_getbreak, Separators&white, null, "INR" );
end;
require brkset initialization;


string simple procedure ScanTail(string s; integer breakTable);
begin scan( s, breaktable, !skip! ); return(s) end;

boolean simple procedure csEqu(string s,t);
start!code	define sp='16, cS=1,cT=2; label match,nogo, ckloop,loop;
 hrrz cS,-3(sp);
 hrrz cT,-1(sp);
 caie cS,(cT);
  jrst nogo;
ckloop:
 sojl	cS,match;
loop:
 ildb cT,(sp);
 ildb -2(sp);
 xor	cT,0;
 jumpe	cT,ckLoop;
 caie	cT,'40;
  jrst	nogo;
 trz	'40;
 cail	0+"A";
  caile	0+"Z";
nogo:
   tdza	cS,cS;
   sojge cS,loop;
match:
end;

string simple procedure ScanS(reference string s; integer breakTable);
	return( scan( s, breaktable, !skip! ) ); 

string simple procedure upper(string s);
	return( scan( s, forceUp, !skip! ) );

string simple procedure scanOut(reference string s;integer breakTable);
begin	string result;
result_ scan( s, breaktable, !skip! ); 
scan(s,eatWhite,!skip!);
return( result );
end;

integer  inp
	,brk
	,eof
	;
String	 CurrentLine	! just for error reporting use;
	;

simple procedure fatal(string x);
begin
print(crlf & "*Error in: ", CurrentLine, crlf, x, crlf );
inchrw;
end;


string simple procedure ReadLine;
begin	string Line;
do scans( Line_ CurrentLine_ input(inp,Rdline), EatWhite )
 until eof or length(Line);
return(line);
end;


string simple procedure StripName( Reference String LongName, Source );
begin	string	 result
		;
	integer	 brk
		;
if length(result_ scanS(Source,UpLetDig)) = 0
or not equ( "=", scans(Source,PassPunct!W) )
 then usererr(0,0,"Badly formatted line: " & Source);
scan( Source, EatWhite, !skip! );
LongName_ scanOut( Source, ToBreaker );
while true
  do  begin
	scan( Source, EatWhite, brk );
	if length(Source)=0 or brk="[" then return(result);
	LongName_ LongName & "_" & scanOut( Source, ToBreaker );
      end;
end;
require 400 Pnames;
require 2000 new!items;
item	 Synonym	! abbreviations: Synonym XOR short EQV longName;
	,Home		! where object begins: Home XOR object EQV room;
	,Use		! where object useful: Use XOR object EQV room;
	,Loc		! where object is: Loc XOR object EQV room;
	;
set	 rooms		! the actual rooms;
	,directions	! N,E,S,W, ...;
	,objects	! treasures or tools;
	,pending	! undefined things;
	;

COMMENT Data structures:

Direction:	ITEM			in directions
2000  SW	= SouthWest
	props(direction) = directionBit
	datum(direction) = "Full-Case name"
	SYNONYM XOR direction EQV fullName

Room:		ITEM  			in rooms
0576 UM6 = UpperMaze{6} [NW:UM6, D:UM9, W:UM7, E:UM10, S:UM8, N:UM1]
	props(room) = `OR' of all investigated directionBits
			+ roomBit
			+ pendingBit if "forward referenced"
	datum(room) = "Full-Case name"
	FromRoom XOR direction EQV ToRoom
	SYNONYM XOR room EQV fullName

Tool:		ITEM  			in objects
	props(object) = 0
	datum(object) = "Full-Case name"
	SYNONYM XOR object EQV fullName
	HOME XOR object EQV room
	USE XOR object EQV room
	LOC XOR object EQV room

Treasure:	ITEM  			in objects
	props(object) = point value
	datum(object) = "Full-Case name"
	SYNONYM XOR object EQV fullName
	HOME XOR object EQV room
	LOC XOR object EQV room

directions:	set of all valid directions (N,S,E,W,U,D,...)
;


define code!undef=0, code!object=1, code!direction=2, code!room=3; 
preset!with 
    "<too small>", "<nada>", "Direction", "Room", "Object", "<too big>";
	own safe string array CodeN[-1:4];
integer procedure getCode(itemvar v);
return( if v in directions then code!direction
   else if v in objects then code!object
   else if v in rooms then code!room
   else code!undef );

define typeName(typeCode)="CodeN[-1 max (typeCode) min 4]";

define	PendingBit= 1 lsh 11, RoomBit=1;
require "kluge" message;
integer procedure ObjectValue( itemvar object );
return( (props(object) land lnot pendingBit) - 1);

procedure setObjectValue( itemvar object; integer val); 
begin
assert(-1 leq val leq 2^5);
props(object)_ props(object) xor (val+1);
end;
procedure prtThing( itemvar thing );
begin	set sy;
	define string!type=3, untyped!type=1;

print("props=", props(thing) land (lnot pendingBit) );
if props(thing) land pendingBit then print("+pendingBit ");
if typeit(thing)=string!type 
	then print("[[", datum(thing,string), "]]")
else if typeit(thing) neq untyped!type 
	then print("[[? type ", typeit(thing), "]]");
if thing in pending then print(" [pending] ");
if thing in rooms then print(" [room] "); 
if thing in directions then print(" [dir] ");
if thing in objects then print(" [obj] ");

if length(sy_ synonym xor thing)
 then print(crlf, "Short for ", sy);
if length(sy_ synonym assoc thing)
 then print(crlf, "Long for ", sy);
if length(sy_ home xor thing)
 then print(crlf, "Lives at ", sy);
if length(sy_ use xor thing)
 then print(crlf, "Can be used at ", sy);
if length(sy_ loc xor thing)
 then print(crlf, "Is currently at ", sy);
end;
define isPending(it,type)=
	"(props(it) land pendingBit) and getCode(It)=type";

procedure mayPend(itemvar it; integer code);
if (props(it) land pendingBit) then
begin
assert(code=code!room);
if props(it)=pendingBit
 then begin put it in rooms; remove it from pending; end;
props(it)_pendingBit+roomBit;
end;

procedure clearPending(itemvar it);
begin
props(It) _ props(It) xor pendingBit;
remove It from pending;
end;

procedure setDirectionCode(itemvar dir; integer number);
begin assert(number land '3776=number=number land -number);

end;
string itemvar procedure makeIt( string longName, abbrev; integer type);
begin	string itemvar makeIt;
	string longUp;
	boolean flag;

if length(longName)=0 then longName_abbrev;
longUp_ upper(longName);
makeIt_ cvsi( abbrev, flag );
if flag
 then begin
	new!pname( makeIt_ new(longName), abbrev );
      end
 else if isPending(makeIt,type) 
   then begin 
	clearPending(makeIt);
	datum(makeIt)_ longName;
	end
   else print(crlf,"Conflict: ", makeIt, "=", longName, typeName(type),
	crlf, " [was ", datum(makeIt), typeName(getCode(makeIt)), "]");

case type of begin
	[code!direction] put makeit in directions; 
	[code!room] put makeit in rooms; 
	[code!object] put makeit in objects;
	[ELSE] usererr(0,0,"Illegal type constructed")
	end;

if not equ(abbrev,longName)
 then begin	string itemvar full,other;
	Full_ cvsi(longUp,flag);
	if flag
	 then new!Pname( Full_ new(abbrev), longUp )
	 else if Synonym XOR full EQV makeIt then return(makeIt)
	 else begin
		print(crlf, "Warning: ", longName, " multiply defined.",
		"  This time as: ", abbrev, "," & crlf &
		"  Formerly:" );
		foreach other | Synonym XOR Full EQV other 
		   do print(" ", other, "=", Full );
	     end;
	MAKE Synonym XOR full EQV makeIt;
     end;

return(makeIt);
end;
string procedure GetDirections;
begin "getdirections"
	string	Line;
	integer	LastProp, AssignedProps;
	string itemvar dir; 

if "=" neq lop(Line_ReadLine)
 or not equ("DIRECTIONS",scanOut(line, UpLetDig))
 or length(line)
  then fatal("Bad start of File");
assignedProps_ lnot '3776;	require "kluge" message;
while "=" neq Line_ ReadLine
   do begin "new direction"	string num,abrev,longName;
				string itemvar OtherName; 
				integer number;
	if eof then fatal("Unexpected EOF in DIRECTIONS");
	num_ scanOut(Line,octDig);
	abrev_ stripName(longName,line);
	if length(line) then fatal( "DIRECTION line too long");
	dir_ makeIt(longName, abrev, code!direction);

	if (number_cvo(num)) land AssignedProps 
	 then print( crlf, "Bad number for ", dir, "[", datum(dir), "]")
	 else begin 
		AssignedProps_ AssignedProps lor number;
		setDirectionCode(dir,number);
	      end;
      end;
return(Line);
end "getdirections";


integer item Punct;

itemvar procedure scan1( reference string line );
begin	string name;
	integer itemvar result;
scans(line,EatWhite);
if length(name_scanS(line,UpLetDig))
 then begin	integer flag;
	result_ cvsi(name,flag);
	if flag
	 then begin	string itemvar gensym;
		new!pname( gensym_ new("{Unassigned}"),name);
		props(gensym)_ pendingBit;
		return(gensym);
	      end
      end
 else begin
	result_ Punct;
	datum(result)_lop(line);
      end;
return(result);
end;



procedure DoDirections( string itemvar base; string Transitions);
if length(Transitions) then
begin "DoDirections"
	string line;
	integer itemvar way, lway, target;
	label fail;
lway_ any; line_ transitions;
if punct neq (way_scan1(line)) 
or datum(way) neq "["
or punct = (lway_scan1(line)) 
or getCode(lway) neq code!direction
 then go fail;
! now way=punct("[") and lway=direction;
way_ scan1(line);
neednext while length(line) 
   do begin "one assoc"		label colok;
	if punct = way
	 then if datum(way) = ":"
		 then begin next; way_scan1(line) end
		 else done
	 else print("Inserting missing `:'"); 
require "kluge" message;
	if props(base) = props(base) lor props(lway)	! already set;
	 then print(crlf, "Multiple ", lway, " for ", base );
	
COLOK:
	! now lway=direction and way=target room;
	if (not lway in directions) or PendingBit land props(lway)
	or any=way or way=punct
	 then done;
	if props(way) land PendingBit
	 then put way in Rooms;
	if getcode(way) neq code!room then done;
	MAKE lway XOR base EQV way;		! connect places;
	props(base)_ props(base) lor props(lway); ! show it;
	next; way_ scan1(line);
	if way=punct 
	 then if datum(way)="]" then RETURN
	      else if datum(way)=":" 
		 then begin next; way_scan1(line); go COLOK end
	      else if datum(way)="," 
		 then begin	next; lway_scan1(line); 
				next; way_scan1(line); end
      end "one assoc";
FAIL:
print( crlf& "Bad Transition notation: ",
	Transitions[1 to inf-length(line)], "<-"&crlf&"->", line );

end "DoDirections";


procedure objectLine(string line);
begin	! typical line: SWORD= Ancient_Sword [HOME:LVR,VALUE:0,USE:TRL];
!	 Object:	ITEM  			in objects
		props(object) = treasure value
		datum(object) = "Full-Case name"
		SYNONYM XOR fullName EQV object 
		HOME XOR object EQV room
		USE XOR object EQV room
	Line: SHORT= LongName [Home:room, Use:room, Value:number]
;
string itemvar it;
string id,name;
boolean flag;

id_ StripName(name,line);
if length(id)=0 then id_upper(name);
if length(name)=0 then name_id;
if length(id)=0
 then begin 
	print(crlf,"Objects must be named: ",line);
	return;
      end;
it_ makeIt(name, id, code!object);
put it in objects;

begin "oblist"	integer itemvar prop, sep, room;
		label fail;
if punct neq (sep_scan1(line)) then go fail;
if datum(sep) = 0 then return;
if datum(sep) neq "[" then go fail;
if punct = (prop_scan1(line)) and datum(sep)="]" then return;
sep_ scan1(line);
neednext while length(line) 
   do begin "one assoc"		label propok;
				item val;
propok:	if punct = sep
	 then if datum(sep) = ":"
		 then if prop=val
			 then setObjectValue(it, intscan(line,!skip!))
			 else begin next; room_scan1(line) end
		 else done
	 else begin print("Inserting missing `:'"); room_sep; end;
	if prop=loc or prop=use or prop=home
	 then begin
		mayPend(room,code!room);
		if getcode(room) neq code!room then done;
		MAKE prop XOR it EQV room;	! put in places;
	      end
	 else if prop neq val then done;
	next; sep_ scan1(line);
	if sep=punct 
	 then if datum(sep)="]" then RETURN
	      else if datum(sep)=":" 
		 then begin next; room_scan1(line); go PROPOK end
	      else if datum(sep)="," 
		 then begin	next; prop_scan1(line); 
				next; sep_scan1(line); end;
      end;
fail:
usererr(0,0,"Trouble in oblist");
end "oblist";
end;
set Conflicts;
string	 abbrev
	,name
	;


procedure DumpMap;
begin "DumpMap"
	string itemvar dir,room;
	integer w,d;
getformat(w,d); setformat(-4,0); setprint(null,"F");
print(crlf,"  =Directions");
foreach dir | dir in directions  
   do print( crlf, cvos(props(dir)),"  ", dir, tab&"= ", datum(dir) );
require "kluge" message;
print(crlf," =Map");
foreach room | room IN rooms
   do begin	itemvar dest;
		string separator; 
	print(crlf, cvos(props(room)), " ", room,
		tab & "= ", datum(room), tab );
	separator_ "[";
	foreach dir,dest | dir in directions and dir XOR room EQV dest
	   do begin
		print(separator,dir,":",dest);
		separator_ ", ";
	      end;
	if separator neq "[" then print("]");
      end;
if length(objects)
 then begin	string itemvar object; itemvar prop; integer w,d;
	getformat(w,d); setformat(0,d);
	print(crlf," =Objects");
	foreach object | object in objects
	   do begin	integer val;
		print(crlf," ", object, tab &"= ", datum(object),
			tab & "[VAL:", ObjectValue(object) );
		foreach prop,room  
		| prop in {home,loc,use} and prop xor object eqv room
			do print(",", prop, ":", room);
		print("]");
	      end;
	setformat(w,d);
      end;
print(crlf,crlf);
setformat(w,d); setprint(null,"T");
end "DumpMap";


procedure RoomLine( string line ); ! format: ABR = Long Name [RoomList];
begin "RoomLine"	boolean flag;
	string itemvar room,full; ! the room ITEMVAR (datum=full name);
	integer investigated;
investigated_ scanOut(line,OctDig);
abbrev_ StripName( name, Line );
room_ makeIt( Name, abbrev, code!room );
DoDirections(room,Line);
require "kluge" message;
props(room)_ props(room) lor investigated;
end "RoomLine";


itemvar procedure TrueNameGet(reference string s);
begin	itemvar var;
	set derived;
if (var_scan1(s))=ANY then return(ANY);
case length(derived_synonym XOR var)
 of begin [0];[1]var_lop(derived); 
      ELSE begin 
	     print(var," ambiguous: ", derived );
	     return(ANY); 
	   end
    end;
return(var);
end;


procedure Path(string l; boolean addit);
begin	itemvar r1,d,r2, triple;
	define eif={else if}, erm(x)={outstr(crlf&"--"&cvps(x)&" ")};

if (any = r1_TrueNameGet(l)) or punct=r1
					then erm(Bad First Room)
eif punct neq (d_TrueNameGet(l)) or datum(punct) neq ":"
					then erm(Missing colon)
eif any = (d_TrueNameGet(l))		then erm(Bad Direction)
eif not d in directions			then erm(Non-direction)
eif punct neq (r2_TrueNameGet(l)) or datum(punct) neq "="
					then erm(missing Equals)
eif any = (r2_TrueNameGet(l)) or r2=punct
					then erm(bad destination room)
 else begin
	if (d xor r1 eqv r2) = addit
	 then print("[bad Path ",(if addit then "Add" else "Drop"),"]")
	 else if addit
		then make d xor r1 eqv r2
		else erase d xor r1 eqv r2;
	return;
      end;
print("[Improper format: -",r1,"-",d,"-",r2,"-...", l, "...Ignored]");
end;


simple string procedure pnice( string itemvar iv; integer style );
return( case style land 3 of 
	( [0,1]datum(iv), [2]cvis(iv,!skip!),
	 [3] datum(iv) & "[" & cvis(iv,!skip!) & "]" ) );
integer verbosity;
procedure ptrip( string itemvar r1; itemvar dir; string itemvar r2 );
if verbosity=0
 then print( " ", cvis(r1,!skip!), ":", dir, "=", cvis(r2,!skip!) )
 else print( crlf, pnice(r1,verbosity lsh -3),
		":", dir, "=", pnice(r2,verbosity) );
set hidden;

procedure ShowPaths(string l; Integer PathMax);
if PathMax leq 0 then print("What?? max <= zero??") else
begin "showPaths"
	itemvar token;
	set known,NewTokens;
	integer level;
if any=(token_ TrueNameGet(l)) or length(l)
 then begin print(" Ill-formed token "); return; end;
newTokens_ {token};
known_ hidden; put token in known;
for level_1 step 1 until PathMax
   do begin		itemvar r1,dir,r2; set candidates;
	print(crlf,level,"==>");
	candidates_ newTokens;
	newTokens_phi;
	foreach r1,dir,r2 | r1 in candidates
			and dir in directions
			and dir xor r1 eqv r2
			and (not r2 in known)
	   do begin 
		ptrip( r1,dir,r2 );
		put r2 in NewTokens; 
		put r2 in known;
	      end;
	if length(newTokens)=0 then done;
	print("<==",length(newTokens));
      end;
end "showPaths";


procedure FindPath(string l; Integer PathMax);
if PathMax leq 0 then print("What?? max <= zero??") else
begin "findPaths"
	itemvar room1,dir,dest;
	set known,NewTokens;
	set array path[0:pathMax];
	list result;
	integer level;
	let orelse=or;
if any=(room1eNameGet(l))
orelse punct neq TrueNameGet(l) 
orelse any=(dest_ TrueNameGet(l))
orelse room1=dest
 then begin print(" Ill-formed Plan "); return; end;

known_ hidden; put room1 in known;
newTokens_ {room1};
for level_1 step 1 until PathMax
   do begin "combine"	itemvar r2; set candidates;
	path[level-1]_ candidates_ newTokens;
	newTokens_phi;
	foreach room1,dir,r2 | room1 in candidates
			and dir in directions
			and dir xor room1 eqv r2
			and (not r2 in known)
	   do begin 
		if r2=dest then done "combine";
		put r2 in NewTokens; 
		put r2 in known;
	      end;
	if length(newTokens)=0 then return;
      end;
if level > PathMax then return;
print(crlf,level, " Steps: ");
result_ {{dir,dest}};
for level_level-2 step -1 until 0
   do begin	itemvar room0;
	foreach room0,dir | room0 in path[level] 
			and dir in directions
			and dir xor room0 eqv room1
		do done;
	put room1 in result before 1;
	put dir in result before 1;
	room1_room0;
      end;

print( pNice(room1,verbosity) );
while length(result) 
   do print( if verbosity land 3 > 1 then crlf&":" else ":",
		lop(result), "=", pNice(lop(result),verbosity) );

end "findPaths";


boolean procedure find(reference itemvar result; string line);
begin	string formal;
result_ cvsi(line,!skip!); if !skip!=0 then return(true);
formal_ scans(line,toWhite);
while length(line_ scanTail(line,eatWhite))
   do formal_ formal & "_" & scans(line,toWhite);
result_ cvsi(formal,!skip!);
if !skip! then result_ cvsi(line_upper(formal),!skip!);
if !skip! then result_ cvsi(scans(formal,noUnder),!skip!);
if !skip! then result_ cvsi(scans(line,noUnder),!skip!);
return(!skip!=0);
end;


require "25" message;
procedure hideOp;
begin	integer leadch; string token,line; itemvar iv; set trn;
print(" ", length(hidden), " rooms currently hidden: ", hidden,
	crlf, " `-' to un-hide them all, `-room' to unhide one, " &
	" or `room' to hide another room.", crlf, "-->");
scan(line_inchwl, EatWhite, leadch);
if "-"=leadch or "+"=leadch then scanS(line_line[2 to inf],Eatwhite);
if length(line)=0
 then begin if leadch="-" then hidden_phi; return; end;
while length(line)
   do begin
	token_ scanOut(line, ToSep);
	if find(iv,token)
	 then begin
		if length(trn_synonym xor iv)=0 then put iv in trn;
		hidden_ if leadch="-"
			 then hidden-trn else hidden union trn;
	      end
	 else print(" no `", token, "' found. ");
      end;
end;

procedure ProcessCommands;
while true do
begin "ProcessCommands"
		string line,tk; 
		own integer pathmax, searchMax; 
		itemvar room;
		item pend,quit,dump,size,hide;
pathMax_ pathMax max 1; if searchMax leq 0 then searchMax_25;
print(crlf&"->");
scan( line_inchwl, EatWhite, !skip! );
case line of begin
 [0];
 ["\"] if find(room,line[2 to inf]) then prtThing(room) else print("??");
 ["*"] RoomLine(line[2 to inf]);
 ["@"] objectLine(line[2 to inf]);
 ["^"] PathMax_cvd(line[2 to inf]);
 ["%"] SearchMax_cvd(line[2 to inf]);
 ["+"] Path(scantail(line[2 to inf],EatWhite),true);
 ["-"] Path(scantail(line[2 to inf],EatWhite),false);
 ["="] ShowPaths(scantail(line[2 to inf],EatWhite),PathMax);
 ["."] FindPath(scantail(line[2 to inf],EatWhite),SearchMax);
 ["|"] print(" From ", cvos(verbosity land '333), " to ",
	cvos(verbosity_'333 land cvo(cvs(intscan(line,!skip!)))) ); 
 ["?"] print(crlf, "`* room-line' to add a new room,",
	crlf, "`@ object-line' to add a new object,",
	crlf, "`^ number' sets pathMax, ",
	crlf, "`% number' sets searchMax, ",
	crlf, "`= room' shows rooms nearby,",
	crlf, "`| number' switches verbosity settings (1+2),",
	crlf, "`. room1:room2' to find a path from room1 to room2,",
	crlf, "`+ room:dir=room' to add a new path",
	crlf, "`- room:dir=room' to remove an old path,",
	crlf, "`DUMP' to save map,",
	crlf, "`PEND' to list rooms accessed but undefined,",
	crlf, "`HIDE' to move rooms to and from the hidden set,",
	crlf, "`SIZE' to list list number of stored objects,",
	crlf, "Or `QUIT' to exit.");
 ELSE if find( room, line )
    then case cvn(room) of begin
	[cvn(dump)] dumpMap;
	[cvn(PEND)] begin
			print( "Unspecified rooms: " );
			foreach room | room in rooms 
			   do if isPending(room,code!room)
				then print(" ", room);
		      end;
	[cvn(HIDE)] hideOp;
	[cvn(SIZE)] print( " Rooms: ", length(rooms), 
			", Directions: ", length(directions),
			", Objects: ", length(objects),
			", Hidden: ", length(hidden) );
	[cvn(QUIT)] done;
	 ELSE begin	itemvar iv;
		print(" Associations: ");
		foreach iv | iv xor room eqv any
		   do print(crlf, iv, "(", room, ")=", iv xor room );
		print(crlf,"----");
		foreach iv | iv xor any eqv room
		   do print(crlf, iv, "(", iv assoc room, ")=", room );
	      end
	end
    else print("Try a `?'.")
 end;
end "ProcessCommands";


procedure readMap(string fileName);
begin	string line;
if length(fileName)=0
 then begin print( crlf, "Source: "); fileName_inchwl; end;
TENX({ setinput( inp_openfile(fileName,"RO"), 200, brk_0, eof_false ); })
T10({	open( inp_getchan, "DSK",0, 4,0, 200, brk_0, eof_false ); 
	if eof then Fatal("No Disk Input today!");
	lookup( inp, fileName, eof );
	while eof
	   do begin 
		print(" Sorry, try again: ");
		lookup( inp, fileName_inchwl, eof_ false );
	      end;
})
line_ getDirections; 
if not csEqu(line,"=MAP") then usererr(0,1,"No `=MAP' section")
 else begin
	pending_phi;
	while length(Line_ReadLine) and line neq "=" do RoomLine(line);
      end;
if csEqu(line,"=OBJECTS")
 then begin
	objects_phi;
	while length(Line_ReadLine) and line neq "=" 
	  do objectLine(line);
      end;
release(inp); 
end;
itemvar abr;
string line;
external integer jobver;

print(crlf,"Mapper version ", cvos(jobver) );
verbosity_'333;
readMap(null);
print( crlf );

ProcessCommands;
require "end" message;
End "Zork Compressor" $$
    > &ö