COMMENT *   VALID 00015 PAGES 
RECORD PAGE   DESCRIPTION
 00001 00001
 00005 00002	Sail Tester
 00006 00003	Item Consistency Check
 00007 00004	Allocator Test, Comment in Funny Places Test.
 00008 00005	Forward Procedure Checks
 00010 00006	Binary Operation Tests
 00012 00007	Start!Code Opcode Test
 00013 00008	Inchwl Test.
 00015 00009	String-Creation Test
 00020 00010	Constant Procedures Test
 00022 00011	Scan Test
 00026 00012	Fullword Test
 00030 00013	Real String Conversion Test, Preload Test
 00034 00014	Constant Boolean Expression Test
 00036 00015	Random Simple Things Tests.
 00038 ENDMK
*;
COMMENT Sail Tester;
BEGIN "TEST"

COMMENT This program is a comprehensive test of new features added to
	SAIL since February, 1972.

 The first section includes features added or modified as of 2-7-72;


define ai!version = false;

ifcr ai!version thenc
	DEFINE crlf = "('15&'12)", alt!code='175, ctl!d!brk='304;
	require " SUAI sail tester. " message;
elsec
	DEFINE CRLF="('15&'12)", alt!code='33, ctl!d!brk='4;
	require " Non-SUAI sail tester. " message;
endc
 EXTERNAL INTEGER !SKIP!;
 BOOLEAN PROCEDURE WANT(STRING TEST);
 BEGIN OUTSTR(TEST&"? ");
	RETURN(INCHWL="Y");
 END "WANT";

TTYUP(TRUE); COMMENT accept "y" for "Y";
COMMENT Item Consistency Check
Be sure that item numbers start at the same place, with or with
	declared items.  First try it with ITEM IT as a declaration, then
	without.  IT should have the value 11.  The NEW should be 11 if IT
	is not declared, else 12;
COMMENT ITEM IT; REQUIRE 3 NEW!ITEMS; COMMENT IT SHOULD BE NUMBER 11;

 IF WANT("ITEMVAR TEST") THEN BEGIN "ITEMVARS"
 	ITEMVAR X;
	X_NEW; COMMENT THIS SHOULD BE NUMBER 12 -- 11 IF ITEM DECL REMOVED;
	OUTSTR(CVS(CVN(X)) & CRLF);
 END "ITEMVARS";
COMMENT Allocator Test, Comment in Funny Places Test.
Make sure that double-pp request and smaller-than-default
	String-pp request work ok.
  Also (5-12-72) make sure that Comments after declared names
don't foul up the declarations;

 REQUIRE 125 SYSTEM!PDL, 300 SYSTEM!PDL, 30 STRING!PDL;

BEGIN
 INTEGER IXI COMMENT IF THINGS ARE BROKEN, "COMMENT" WILL BE DEFINED;;
 PROCEDURE XXX; IXI_3;
 COMMENT IF THINGS ARE BROKEN, THIS COMMENT WILL BE BROKEN;
END;
COMMENT Forward Procedure Checks
This test made entirely at compile time. The following procedures have
	errors:
  P2 -- FORWARD has too few formals
  P3 -- FORWARD has too many formals
  P4 -- FORWARD has too few formals
  P5 -- FORWARD has too many formals
  P6 -- FORWARD and actual have different names, but types match, OK.
  P7 -- Type mismatch
  P8 -- No FORWARD, no error -- all OK

Enable this test by Truifying the IFC;

IFC FALSE THENC
BEGIN "FORWARDS"
	FORWARD PROCEDURE P1;
	PROCEDURE P1;;
	FORWARD PROCEDURE P2;
	PROCEDURE P2(INTEGER X);;
	FORWARD PROCEDURE P3(INTEGER X);
	PROCEDURE P3;;
	FORWARD PROCEDURE P4(INTEGER X);
	PROCEDURE P4(INTEGER X,Y);;
	FORWARD PROCEDURE P5(INTEGER X,Y);
	PROCEDURE P5(INTEGER X);;
	FORWARD PROCEDURE P6(INTEGER X);
	PROCEDURE P6(INTEGER Z);;
	FORWARD PROCEDURE P7(INTEGER X);
	PROCEDURE P7(REAL X);;
	PROCEDURE P8(INTEGER X;STRING Y);;
 END "FORWARDS";


ENDC

COMMENT Binary Operation Tests
This test was designed specifically to test binary operations which
	have two STTEMPs as operands.  However, it is mildly useful
	to check other binary operations as well.  Just watch the
	flashing lights.
;
IF WANT("BINARY TEST") THEN BEGIN "BINARY OPS"
	DEFINE BTEST(OP,RES)="
IF (OP) neq RES THEN OUTSTR(""BINARY OPERATION `OP' YIELDS ""&
	CVS(I)&"", SHOULD BE RES""&CRLF)";
	INTEGER I,J; STRING S,T;
	J_5;
	BTEST(I_2-3,-1);
	BTEST(I_J-3,2);
	BTEST(I_I-3,-1);
	I_2;
	BTEST(I_I^J,32);
	S_"EFG"; T_"ABCD";
	BTEST(I_S-T,4);
	S_3&4&5&6&7; I_2;
	T_7&6&5&4&3;
	BTEST(I_I^S,8);
	BTEST(I_S[3 FOR 1]+I,13);
	BTEST(I_S[3 FOR 1]^T[4 FOR 1],625);
	BTEST(I_IF I>J THEN 3 ELSE 4,3);
	BTEST(I_IF S[3 FOR 1]>T[4 FOR 1] THEN 3 ELSE 4,3);
 END "BINARY OPS";
COMMENT Start!Code Opcode Test
All opcodes are now available in START!CODE.
	CALL is the first opcode, and has the value 40.
	INTUUO is the last, with value 723 (SUAI only).
	MOVE is in the middle, with value 200.
Correct compilation of these instructions does not guarantee
  correctness of all opcodes, but it does eliminate systematic error.
This code will not be executed -- just examine it;

 IF FALSE THEN START!CODE "OPCODES"
	CALL	1,1;	COMMENT TEST FIRST OPCODE;
	tson	1,1;	comment last REAL opcode;
ifcr ai!version thenc
	INTUUO	1,1;	COMMENT TEST LAST uuo;
endc
	MOVE	1,1;	COMMENT TEST SOMETHING IN  THE MIDDLE;
 END "OPCODES";
COMMENT Inchwl Test.
INCHWL now stops on ANY activation character, returning that
	character in !SKIP!, not in the string.  It sucks up
	the LF after a CR, returning the CR.
	This test will tell you if it doesn't like you;

IF WANT("INCHWL TEST") THEN  BEGIN "INCHWL"
	STRING S; integer brk;
	outstr ( " (NOTE: case is ignored here) " & crlf );
	define Check!break(name,code) "[]" = [
		OUTSTR( "TYPE ""THIS IS A LINE"", TERMINATE WITH name"
			& CRLF );
		S_ INCHWL; brk_ !skip!;
		IF  not EQU( "THIS IS A LINE", S )
		 THEN OUTSTR(crlf &  "STRING RESULT WRONG: """ &
				s & """" & CRLF );
		IF brk neq code
		 THEN OUTSTR( crlf &  "BREAK CHAR WRONG: '" & cvos(brk) &
				" SHOULD BE '" & cvos(code)  & CRLF )];


	Check!break(<CR>,'15);
	Check!break(<LF>,'12);
	Check!break(<ALT>,alt!code);
	Check!break(<CTRL-D>,ctl!d!brk);

 END "INCHWL";
COMMENT String-Creation Test
This just runs CAT and friends through their paces. It was designed
	to test the new optimized CAT, CATCHR, CHRCAT, CHRCHR, and to
	make sure that the removal of TOPSTR didn't blow the other
	string-making routines. Sometimes help is requested from the TTY.
     A new test (5-12-72) assures that if SGLIGN(USER) is on, strings are
	aligned on full-word boundaries after concatenation, even if
	optimization must be removed. ;

IF WANT("STRING TEST") THEN BEGIN "STRING CREATION"
	INTEGER I,J; STRING S,T; EXTERNAL INTEGER SGLIGN;
	BOOLEAN PROCEDURE UNALIGNED(REFERENCE STRING S);
	BEGIN INTEGER I;
	  START!CODE MOVE 1,S; LSH 1,-30; MOVEM 1,I END;
	  RETURN(IF 1 neq I neq '44 THEN TRUE ELSE FALSE);
	END "UNALIGNED";
	S_NULL;
	T_"ABC";
	IF  not EQU(S_T&"CDE","ABCCDE") THEN
	   OUTSTR("CAT WITH TWO COPIES DOESN'T WORK");
	IF  not EQU(S&"CDE"&CVS(32456),"ABCCDECDE32456") THEN
	   OUTSTR("CAT WITH ONE COPY, OR WITH NO COPY DOESN'T WORK"&CRLF);
	I_"G";
	IF  not EQU(T&I,"ABCG") THEN OUTSTR("CATCHR DOESN'T WORK"&CRLF);
	IF  not EQU(I&T,"GABC") THEN OUTSTR("CHRCAT DOESN'T WORK"&CRLF);
	IF  not EQU(I&I,"GG") THEN OUTSTR("CHRCHR DOESN'T WORK"&CRLF);
     FOR J_1 STEP 1 UNTIL 20 DO BEGIN
	T_S;
	S_NULL;
	FOR I_1 STEP 1 UNTIL 200 DO S_S&" "&CVS(I); COMMENT JUST APPEND;
     END;
	IF  not EQU(S,T) THEN OUTSTR("STRNGC MESSED UP CAT"&CRLF);
	USERCON(SGLIGN,I_-1,1);
	S_"ABCDE"; S_S&"FGHIJ"; T_S[3 FOR 8];
	T_T&CVS(25);
	BEGIN "SGLIGN TEST" I_0 END;
	IF UNALIGNED(S) THEN OUTSTR("SGLIGN DIDN'T WORK AT ALL"&CRLF);
	IF UNALIGNED(T) THEN
	   OUTSTR("SGLIGN DIDN'T WORK IF ALREADY CATTED"&CRLF);
	S_"ABCDE"; S_S&"FGHIJ"; T_S[3 FOR 8];  T_T&"ABCDE";
	IF UNALIGNED(T) THEN
	   OUTSTR("SGLIGN DIDN'T WORK IF ARG1 ON TOP"&CRLF);
	USERCON(SGLIGN,I_0,1);
	SETBREAK(1,"ABCDE",NULL,"I");
	S_"THIS IS A LINE";
	T_SCAN(S,1,I);
	IF  not EQU(T,"THIS IS ") or  not EQU(S," LINE") THEN
	  OUTSTR("SCAN DOESN'T WORK"&CRLF);
	IF INTSCAN(S_"12345ABCDE",J) neq 12345 THEN 
	  OUTSTR("INTSCAN DOESN'T WORK"&CRLF);
	IF  not (1.1@-4 leq REALSCAN(S_"1.23@-4ABCDE",J) leq 1.3@-4) THEN
	  OUTSTR("REALSCAN DOESN'T WORK"&CRLF);
	IF  not EQU(CVSTR(CVASC("ABCDE")),"ABCDE") THEN
	  OUTSTR("CVSTR OR CVASC DOESN'T WORK"&CRLF);
	IF  not EQU(CVXSTR(CVSIX("ABCDEF")),"ABCDEF") THEN
	  OUTSTR("CVXSTR OR CVSIX DOESN'T WORK"&CRLF);
	OPEN(1,"TTY",0,2,0,100,I,I);
	OUTSTR("TYPE ""THIS IS A LINE""<CRLF>"&CRLF);
	SETBREAK(1,10,13,"I");
	S_INPUT(1,1);
	IF  not EQU("THIS IS A LINE",S) or I neq '12 THEN
	  OUTSTR("INPUT DOESN'T WORK"&CRLF);
	OUTSTR("TYPE ""12345  1.23@-4""<CRLF>"&CRLF);
	IF INTIN(1) neq 12345 THEN 
	  OUTSTR("INTIN DOESN'T WORK"&CRLF);
	IF  not (1.1@-4 leq REALIN(1) leq 1.3@-4) THEN
	  OUTSTR("REALIN DOESN'T WORK"&CRLF);
	RELEASE(1);
 END "STRING CREATION";
COMMENT Constant Procedures Test
   This test assures that CAT, SUBSTR, CVS, CVOS, ... CVD, CVO, EQU
will happen correctly at compile time.  Part of it assures that
procedures with wrong numbers of arguments will not be evaluated.;

IF WANT("CONSTANT PROCEDURES TEST") THEN BEGIN "CONST PROC"
   INTEGER I, w,d; STRING ST;
	getformat(w,d); 
   IFC  not EQU("ABC"&"DEF","ABCDEF") THENC
	OUTSTR("CONSTANT CAT DOESN'T WORK"&CRLF); ENDC
   IFC  not EQU("ABC"&CRLF,"ABC
") THENC OUTSTR("CONSTANT CAT DOESN'T WORK"&CRLF); ENDC
   IFC  not EQU(("ABCDE")[2 TO 4],"BCD") THENC
	OUTSTR("CONSTANT SUBSTR DOESN'T WORK"&CRLF); ENDC
   SETFORMAT(0,0);
   IF  not EQU(CVS(25),"25") THEN OUTSTR("CONSTANT CVS DOESN'T WORK"&CRLF);
   SETFORMAT(10,5);
  IFC TRUE  THENC
   BEGIN "CVG TEST" I_0 END;
   IF  not EQU(CVG(-3.5),"-3.500000    ") THEN
	OUTSTR("CONSTANT CVG DOESN'T WORK"&CRLF); 
  ENDC
   IFC CVD("12345") neq 12345 THENC
	OUTSTR("CONSTANT CVD DOESN'T WORK"&CRLF); ENDC

   IFC FALSE THENC  COMMENT WE'LL DO OUR BEST, BUT ... ;
	I_EQU("ABC");
	I_EQU("ABC","CDE","EFG");
	I_CVD;
	I_CVD("ABC","DEF");
	ST_CVS;
	ST_CVS(2,3,4);
   ENDC
	setformat(w,d); 
END "CONST PROC";
COMMENT Scan Test
This was designed to test SCAN in all its many guises.
  It will tell you what it doesn't like;

IF WANT("SCAN TEST") THEN BEGIN "SCAN TEST"
	preset!with null, "A LINE IS THIS", "THIS IS A LINE", 
		"THIS LINE IS A", "THIS IS LINE", "THIS IS LINE ", 
		"This is a Line"; 
	own string array sources[0:6];
	STRING brknam; INTEGER brkt,number;
	PROCEDURE establish( INTEGER tbl; STRING breaks, omits, modes );
	BEGIN 
	SETBREAK( brkt := tbl, breaks, omits, modes );
	brknam := "(" & breaks & "," & omits & "," & modes & ")";
	number := -1;
	END;

	PROCEDURE TST( string result; integer brk; string left );
	begin	string s,t; integer n;
	  number_number+1;
	  s_ sources[number];
	  if equ(t_scan(s,brkt,n),result) and equ(s,left) and n=brk
	   then return;
	  PRINT("Bug: """, t, """=SCAN(""", sources[number], """,",
		brkt, ":", brknam, ",['", cvos(n), "=" & n, 
			"]) leaves """, s, """." & crlf & 
		"    not: """, result, """ / '", cvos(brk), "=" & brk,
			" / """, left, """." & crlf );
	end;

	brkt_0; brknam_"(table 0)"; number := -1;
	tst(sources[0], 0, null); 
	tst(sources[1], 0, null); 
	tst(sources[2], 0, null); 
	tst(sources[3], 0, null); 
	tst(sources[4], 0, null); 
	tst(sources[5], 0, null); 
	tst(sources[6], 0, null);

	establish(1,"ABCD",NULL,"I");
	tst( null,	0,	null);
	tst( null,	"A",	" LINE IS THIS");
	tst( "THIS IS ","A",	" LINE");
	tst( "THIS LINE IS ", "A", NULL);
	tst( "THIS IS LINE", 0, NULL);
	tst( "THIS IS LINE ", 0, NULL);
	tst( "This is a Line", 0, NULL);

	establish(2,"ABCD",NULL,"IS");
	tst( null,	0,	null);
	tst( null,	"A",	" LINE IS THIS");
	tst( "THIS IS ","A",	" LINE");
	tst( "THIS LINE IS ", "A", NULL);
	tst( "THIS IS LINE", 0, NULL);
	tst( "THIS IS LINE ", 0, NULL);
	tst( "This is a Line", 0, NULL);

	establish(3,"ABCD"," ","I");
	tst( null,	0,	null);
	tst( null,	"A",	" LINE IS THIS");
	tst( "THISIS","A",	" LINE");
	tst( "THISLINEIS", "A", NULL);
	tst( "THISISLINE", 0, NULL);
	tst( "THISISLINE", 0, NULL);
	tst( "ThisisaLine", 0, NULL);

	establish(4,"ABCD",NULL,"IA");
	tst( null,	0,	null);
	tst( "A",	"A",	" LINE IS THIS");
	tst( "THIS IS A","A",	" LINE");
	tst( "THIS LINE IS A", "A", NULL);
	tst( "THIS IS LINE", 0, NULL);
	tst( "THIS IS LINE ", 0, NULL);
	tst( "This is a Line", 0, NULL);

	establish(5,"ABCD",NULL,"IR");
	tst( null,	0,	null);
	tst( null,	"A",	"A LINE IS THIS");
	tst( "THIS IS ","A",	"A LINE");
	tst( "THIS LINE IS ", "A", "A");
	tst( "THIS IS LINE", 0, NULL);
	tst( "THIS IS LINE ", 0, NULL);
	tst( "This is a Line", 0, NULL);

	establish(6,"ABCD","Ia","IR");
	tst( null,	0,	null);
	tst( null,	"A",	"A LINE IS THIS");
	tst( "THS S ","A",	"A LINE");
	tst( "THS LNE S ", "A", "A");
	tst( "THS S LNE", 0, NULL);
	tst( "THS S LNE ", 0, NULL);
	tst( "This is  Line", 0, NULL);
	
	establish(7,"ABCDs","Ia","IRK");
	tst( null,	0,	null);
	tst( null,	"A",	"A LINE IS THIS");
	tst( "THS S ","A",	"A LINE");
	tst( "THS LNE S ", "A", "A");
	tst( "THS S LNE", 0, NULL);
	tst( "THS S LNE ", 0, NULL);
	tst( "THS S ","A",	"a Line");

	establish(8,"ABCDs","Ia","IAK");
	tst( null,	0,	null);
	tst( "A",	"A",	" LINE IS THIS");
	tst( "THS S A","A",	" LINE");
	tst( "THS LNE S A", "A",null);
	tst( "THS S LNE", 0,	null);
	tst( "THS S LNE ", 0,	null);
	tst( "THS S A","A",	" Line");
	
	establish(9,"ABCDl","Ia","IRC");
	tst( null,	0,	null);
	tst( " ",	"l",	"LINE IS THIS");
	tst( "this is  ","l",	"LINE");
	tst( "this ",	"l",	"LINE IS A");
	tst( "this is ","l",	"LINE");
	tst( "this is ","l",	"LINE ");
	tst( "this is  ","l",	"Line");
	
	establish(10,"ABCDl","Ia","IAC");
	tst( null,	0,	null);
	tst( " l",	"l",	"INE IS THIS");
	tst( "this is  l","l",	"INE");
	tst( "this l",	"l",	"INE IS A");
	tst( "this is l","l",	"INE");
	tst( "this is l","l",	"INE ");
	tst( "this is  l","l",	"ine");
	
	relbreak(1); 
	if (brkt := getbreak) neq 1
	 then print("Bad getbreak return A: ", brkt, " (not 1)" & crlf);
	setbreak(1,"THSL", "AIE", "INRK");
	relbreak(1);
	brknam := sources[6];
	if (brkt := getbreak) neq 1
	 then print("Bad getbreak return B: ", brkt, " (not 1)" & crlf);
	breakset(1,null,"N");	COMMENT something safe;
	if not equ(sources[6], scan( brknam, 1, brkt )) 
	or brkt neq 0 or length(brknam) neq 0
	 then print("RELBREAK/GETBREAK does not reset tables!" & crlf );
	establish(1,null, null, "S");
	tst(sources[0], 0, null); 
	tst(sources[1], 0, null); 
	tst(sources[2], 0, null); 
	tst(sources[3], 0, null); 
	tst(sources[4], 0, null); 
	tst(sources[5], 0, null); 
	tst(sources[6], 0, null);
END "SCAN TEST";
COMMENT Fullword Test
This tests WORDIN, WORDOUT, ARRYIN, ARROUT in many ways.  It
	complains if it doesn't like you;

IF WANT("FULLWORD IO TEST") THEN BEGIN "FULLWORD TRANSFERS"
	INTEGER EOF,I,J;
	INTEGER ARRAY X[0:383];
	PROCEDURE CHECKB(INTEGER FLAG);
	BEGIN "CHECKB"
	  INTEGER L,H;
	  I_POINT(36,X[0],35);
	  IF FLAG THEN L_LDB(I-1) ELSE IF
	    L neq LDB(I-1) THEN OUTSTR("LOWER BOUND CLOBBERED IN FULLWORD"&CRLF);
	  IF FLAG THEN H_LDB(I+384) ELSE IF
	    H neq LDB(I+384) THEN OUTSTR("UPPER BOUND CLOBBERED IN FULLWORD"&CRLF);
	END "CHECKB";
	CHECKB(TRUE);
	OPEN(1,"DSK",'10,0,2,0,I,EOF);
	ENTER(1,"TEST.DAT",I);
	FOR I_0 STEP 1 UNTIL 383 DO X[I]_I;
	BEGIN "WR FILE" END "WR FILE";
	ARRYOUT(1,X[0],128); "ONE RECORD"
	ARRYOUT(1,X[I_128],29); "WITHIN A RECORD"
	ARRYOUT(1,X[I_I+29],113); "ACROSS A RECORD BOUNDARY"
	WORDOUT(1,X[I_I+113]); "A SINGLE WORD"
	ARRYOUT(1,X[I_I+1],113); "LAST GROUP, 3 RECORDS TOTAL"
	RELEASE(1);
	CHECKB(FALSE); "MAKE SURE NOTHING HAS MOVED"
	X[0]_-1; ARRBLT(X[1],X[0],383);
	OPEN(1,"DSK",'10,2,0,0,I,EOF);
	LOOKUP(1,"TEST.DAT",I);
	BEGIN "RD FILE" END "RD FILE";
	ARRYIN(1,X[0],113); "READ WITHIN RECORD"
	X[I_113]_WORDIN(1); "ONE WORD, SAME RECORD"
	ARRYIN(1,X[I_I+1],28); "ACROSS BOUNDARY"
	ARRYIN(1,X[I_I+28],114); "FINISH UP THE RECORD"
	ARRYIN(1,X[I_I+114],129); "READ ONE PAST EOF"
	BEGIN "EOF CHK" END "EOF CHK";
	IF  not EOF THEN OUTSTR("EOF DOESN'T WORK IN WORDIN"&CRLF) ELSE
	  IF EOF neq ('20010 LSH 18)+128 THEN OUTSTR("EOF WITH BAD VALUE"&CRLF);
	RELEASE(1);
	CHECKB(FALSE);
	FOR I_0 STEP 1 UNTIL 383 DO IF X[I] neq I THEN
	  OUTSTR("WORD "&CVS(I)&" HAS VALUE "&CVS(X[I])&CRLF);
	OPEN(1,"DSK",'17,0,0,0,I,EOF);
	ENTER(1,"TEST.DAT",I);
	FOR I_1 STEP 1 UNTIL 10 DO WORDOUT(1,I);
	RELEASE(1);
	OPEN(1,"DSK",'17,0,0,0,I,I);
	LOOKUP(1,"TEST.DAT",I);
	BEGIN "DMP RD TEST" END "DMP RD TEST";
	FOR J_1 STEP 1 UNTIL 10 DO IF (I_WORDIN(1)) neq J THEN
	 OUTSTR("DUMP MODE: WORD "&CVS(J)&" HAS VALUE "&CVS(I)&CRLF);
	RELEASE(1)
 END "FULLWORD TRANSFERS";
COMMENT Real String Conversion Test, Preload Test
This tests CVE, CVF, CVG.  I test so much because once I modified
	the spacing algorithm, and now that I put it back I don't
	trust it;

IF WANT("REALSTRING TEST") THEN BEGIN "REALSTRING CONVERSIONS"
	STRING S,COD; REAL X;INTEGER I, w,d;
	getformat(w,d); 
	SETFORMAT(0,0);
	X_3.5;
	IF  not EQU(CVE(X),"  @0  ") THEN OUTSTR("CVE 1 IS WRONG"&CRLF);
	IF  not EQU(CVF(X)," 4.") THEN OUTSTR("CVF 2 IS WRONG"&CRLF);
	IF  not EQU(CVG(X),"  @0  ") THEN OUTSTR("CVG 3 IS WRONG"&CRLF);
	SETFORMAT(10,5);
	IF  not EQU(CVE(X)," .35000@1  ") THEN OUTSTR("CVE 4 IS WRONG"&CRLF);
	IF  not EQU(CVF(X),"   3.50000") THEN OUTSTR("CVF 5 IS WRONG"&CRLF);
	IF  not EQU(CVG(X)," 3.5000    ") THEN OUTSTR("CVG 6 IS WRONG"&CRLF);
	SETFORMAT(-1,0);
	IF  not EQU(CVE(X),"  @0  ") THEN OUTSTR("CVE 7 IS WRONG"&CRLF);
	IF  not EQU(CVF(X)," 4.") THEN OUTSTR("CVF 8 IS WRONG"&CRLF);
	IF  not EQU(CVG(X),"  @0  ") THEN OUTSTR("CVG 9 IS WRONG"&CRLF);
	SETFORMAT(-10,5);
	IF  not EQU(CVE(X)," .35000@1  ") THEN OUTSTR("CVE 10 IS WRONG"&CRLF);
	IF  not EQU(CVF(X)," 003.50000") THEN OUTSTR("CVF 11 IS WRONG"&CRLF);
	IF  not EQU(CVG(X)," 3.5000    ") THEN OUTSTR("CVG 12 IS WRONG"&CRLF);
	X_-3.5;SETFORMAT(0,0);
	IF  not EQU(CVE(X),"- @0  ") THEN OUTSTR("CVE 13 IS WRONG"&CRLF);
	IF  not EQU(CVF(X),"-4.") THEN OUTSTR("CVF 14 IS WRONG"&CRLF);
	IF  not EQU(CVG(X),"- @0  ") THEN OUTSTR("CVG 15 IS WRONG"&CRLF);
	SETFORMAT(10,5);
	IF  not EQU(CVE(X),"-.35000@1  ") THEN OUTSTR("CVE 16 IS WRONG"&CRLF);
	IF  not EQU(CVF(X),"  -3.50000") THEN OUTSTR("CVF 17 IS WRONG"&CRLF);
	IF  not EQU(CVG(X),"-3.5000    ") THEN OUTSTR("CVG 18 IS WRONG"&CRLF);
	SETFORMAT(-1,0);
	IF  not EQU(CVE(X),"- @0  ") THEN OUTSTR("CVE 19 IS WRONG"&CRLF);
	IF  not EQU(CVF(X),"-4.") THEN OUTSTR("CVF 20 IS WRONG"&CRLF);
	IF  not EQU(CVG(X),"- @0  ") THEN OUTSTR("CVG 21 IS WRONG"&CRLF);
	SETFORMAT(-10,5);
	IF  not EQU(CVE(X),"-.35000@1  ") THEN OUTSTR("CVE 22 IS WRONG"&CRLF);
	IF  not EQU(CVF(X),"-003.50000") THEN OUTSTR("CVF 23 IS WRONG"&CRLF);
	IF  not EQU(CVG(X),"-3.5000    ") OUTSTR("CVG 24 IS WRONG"&CRLF);
	setformat(w,d); 
 END "REALSTRING CONVERSIONS";

IFC TRUE  THENC
 IF FALSE THEN BEGIN "PRELOAD WARNINGS"
	DEFINE PRELOAD!WITH="COMMENT";
	INTEGER N;
	PRELOAD!WITH 0,0,0,0,0;
	INTEGER ARRAY FOO[1:N];
	PRELOAD!WITH 1,2,3,45;
	EXTERNAL INTEGER ARRAY BAZ[1:N];
	FOO[1]_3
 END "PRELOAD WARNINGS";
ENDC
COMMENT Constant Boolean Expression Test
	Enable and look at the code if you suspect something
	SDD -- might as well leave the code compiled, its only CPU;
IFC true THENC
   BEGIN "BOOL"
    INTEGER A,B;
    IF A THEN B_3;
    IF 3 THEN B_3;
    IF A<B THEN B_3;
    IF A<3 THEN B_3;
    IF 3<A THEN B_3;
    IF 3<5 THEN B_3;
    IF 3>5 THEN B_3;
    IF A and B THEN B_3;
    IF A and (3<5) THEN B_3;
    IF A and (5<3) THEN B_3;
    IF 5<3 and A THEN B_3;
    IF A and 3 THEN B_3;
    IF 3<5 and 4<7 THEN B_3;
    IF 3<5 and 4>7 THEN B_3;
    IF 3>5 and 4<7 THEN B_3;
    IF 3>5 and 4>7 THEN B_3;
    IF A or B THEN B_3;
    IF A or (3<5) THEN B_3;
    IF A or (5<3) THEN B_3;
    IF 5<3 or A THEN B_3;
    IF A or 3 THEN B_3;
    IF 3<5 or 4<7 THEN B_3;
    IF 3<5 or 4>7 THEN B_3;
    IF 3>5 or 4<7 THEN B_3;
    IF 3>5 or 4>7 THEN B_3;
    A_3<5 and (4>7 or 2<3);
    A_IF 3<5 and (4>7 or 2<3) THEN 2 ELSE 3;
    A_3<5 and (4>7 or 2>3);
    A_IF 3<5 and (4>7 or 2>3) THEN 2 ELSE 3;
   END "BOOL";
ENDC
COMMENT Random Simple Things Tests.
  These should take very little time, and should always work.
It's just a way to assure that trivial bugs don't creep back in;

BEGIN "RANDOM"
  STRING SS;
  INTEGER I,J, K;

 Comment be sure that NEGAT bug in Loop code is gone;
  SS_"ABCDE";
  J_1; K_0;
  FOR I_1 STEP 1 UNTIL 10-LENGTH(SS) DO K_-1;
  IF I neq 6 or  not K THEN OUTSTR("NEGAT BUG IN FOR CODE IS BACK"&CRLF);
  J_1; K_0;
  FOR I_1 STEP J UNTIL 10-LENGTH(SS) DO K_-1;
  IF I neq 6 or  not K THEN OUTSTR("NEGAT BUG IN FOR CODE IS BACK"&CRLF);
 Comment type the version number.  Load this with something whose
    Version numbers don't exist, are the same, are not the same;
START!CODE MOVE 1,'137; MOVEM 1,I END;
  OUTSTR("VERSION IS "&CVOS(I)&CRLF);
END "RANDOM";
if want("String comparisons test") then begin "comparisons"
require "[][]" delimiters;
define assertc(bp)=[IFCR not(bp) THENC require cvps(bp) & ":bug 
" message; ENDC];

assertc( CMPSTR("abcdefghijklmn","abcdefghijklmn")=0)
IFCR CMPSTR(null,0&0&0&0)
 THENC require " CMPSTR(null,0&0&0&0)=" & cvs( CMPSTR(null,0&0&0&0) ) &"
" message; ENDC
IFCR CMPSTR(0&0&0&0,null)
 THENC require " CMPSTR(0&0&0&0,null)=" & cvs( CMPSTR(0&0&0&0,null) ) &"
" message; ENDC

assertc([CMPSTR("abcdefghijklm", "abcdefghijklmn")=-14])
IFCR -14 neq CMPSTR("abcdefghijklm", "abcdefghijklmn")
 THENC require "-14 neq CMPSTR(""abcdefghijklm"", ""abcdefghijklmn"") ="
		& cvs( CMPSTR("abcdefghijklm", "abcdefghijklmn") ) &"
" message; ENDC
assertc( CMPSTR("accdefghijklm", "abcdefghijklmn")=2)
integer raiseTable, printAble;

simple string procedure upper(string s);
	return(scan(s,raiseTable,!skip!));

string procedure pname( string s );
if length(s)=0 then return("Null") else
begin	string result; integer c;
result := null;
do result:= result & 
	(if '37 < (c_lop(s)) < '177
	 then " & """ & c & scan(s, printAble, !skip!) & """"
	 else " & '" & cvos(c))
 until length(s)=0;
return( result[4 to inf] );
end;

procedure check2( string s1, s2 );
begin "check2"
		boolean failureSeen;

  procedure fail(string why);
  begin
  if not failureSeen
   then print("Failure on ", pName(s1), " and ", pName(s2), "." & crlf);
  print("   --> ", why, crlf );
  failureSeen := true;
  end;

  procedure XMPSTR( integer pos; string a, b, name );
  begin	integer len;
  len := length(a) min length(b) 
	min (if pos=0 then length(a) else (abs pos)-1);
  if not equ(a[1 for len], b[1 for len])
   then fail(name & " miscalled a few things equal");
  if pos
   then begin	integer c1,c2;
	c1 := a[abs pos for 1];
	c2 := b[abs pos for 1];
	if c1=c2 then fail(name & " too short")
	 else if (pos>0) neq (c1>c2) then fail(name & " backwards");
        end
   else begin	string s;
	s := a[len+1 to inf] & a[len+1 to inf];
	while length(s)
	   do if lop(s)
	       then begin fail(name &"  mistakenly matched"); done end;
        end;
  end;
	string us1, us2;
	integer cpos,kpos,len;
failureSeen := false;
if (cpos := cmpstr(s1,s2)) neq -cmpstr(s2,s1)
 then fail("CMPSTR not reversible");
if (kpos := kmpstr(s1,s2)) neq -kmpstr(s2,s1)
 then fail("KMPSTR not reversible");
us1 := upper(s1);
us2 := upper(s2);
if cmpstr(us1,us2) neq kpos
 then fail("KMPSTR(a,b) not truly CMPSTR(up(a),up(b))");

if equ(us1,us2) neq kequ(s1,s2)
or equ(us2,us1) neq kequ(s2,s1)
 then fail("KEQU not truly EQU(up(a),up(b))");
XMPSTR( cpos, s1, s2, "CMPSTR" );
XMPSTR( kpos, us1, us2, "KMPSTR" );
end "check2";

	integer i;
	string r,s,t;

setbreak( printAble_getbreak, 
	   FORC ##=0 STEPC 1 UNTILC '37 DOC "## &" ENDC '177,
	   null, "IR" );
setbreak(raiseTable := getbreak, null,null,"1CIOS");
s := t := 1&" @ABC:XYZ[`abc:xyz{"&'177;	
if not equ( r := scan(s,raiseTable,i), 1&" @abc:xyz[`abc:xyz{"&'177 )
or i neq 0 or length(s) neq 0
 then print("Force-down failed: ",
		" got ", pName(r), "['", cvos(i), "] ", pName(s), 
		crlf & " from ", pName( t ), crlf );

breakset(raiseTable, null, "K");
s := t;					
if not equ( r := scan(s,raiseTable,i), 1&" @ABC:XYZ[`ABC:XYZ{"&'177 )
or i neq 0 or length(s) neq 0
 then print("Force-up failed: ",
		" got ", pName(r), "['", cvos(i), "] ", pName(s),
		crlf & " from ", pName( t ), crlf );

check2(null,null); check2(0,null); check2(1,null);
check2(1&0,1&null); check2(1&0&0,1&null); check2(1&0&0&1,1&null); 
check2( "@ABC:XYZ[`abc:xyz{", "@ABC:XYZ[`abc:xyz{" );
check2( "@ABC:XYZ[`ABC:XYZ{", "@abc:xyz[`abc:xyz{" );
check2( "@ABC:XYZ[`abc:xyz{", "@abc:xyz[`ABC:XYZ{" );
check2( "@AZ[`az{", "`AZ[`az{" ); check2( "@AZ[`az{", "@aZ[`az{" );
check2( "@AZ[`az{", "@Az[`az{" ); check2( "@AZ[`az{", "@AZ[`az[" );
check2( "abcdefg...wxyz", "abcdefg...wxyz" );
check2( "abcdefG...wxyz", "abcdefg...wxyz" );

setbreak(raiseTable," "&"."," ","6ITFS");	comment 6 should work;
setbreak(i:= getbreak,"/",null,"IS");
s := "This is    an..example     .      of .a token. -style    table  ";
t := "This/ is/ an/./.example/ /.of/ /.a/ token/.-style/ table/ /";
while length(t)
   do begin	string expected, got; integer expectedBreak, gotBreak;
	expected := scan(t, i, !skip!); expectedBreak := lop(t);
	got := scan(s, raiseTable, gotBreak);
	if not equ(expected,got) or expectedBreak neq gotBreak
	 then print("Token scan: ", 
		pName(got), "['", cvos(gotBreak), "] should be ",
		pName(expected), "['", cvos(expectedBreak), "]"&crlf);
      end;
relbreak(i);
relbreak(raiseTable);
relbreak(printable);
end "comparisons";

begin "Version A"
	boolean procedure temp!size!confusion;
	begin	long real l; integer i;
		long real procedure long!noop( long real v ); return(v);
		integer procedure noop( integer k ); return(k);
		simple long real procedure l!bits;
		 start!code move 1,['123456654321]; movs 2,1; end;
	COMMENT first make and use two long real temps;
	l_ long!noop(l!bits)
		xor long!noop( long!noop(0.) xor long!noop(0.) );

	COMMENT make and use two integer temps (did force size 1 temps);
	i_ noop(0) + noop( noop(0) + noop(0) );
	return( l neq l!bits);
	end;

	boolean procedure SimpleRefBad( string outS );
	begin
		simple string procedure innerRef( string la ); 
			return( outS & null );
	return( not equ( InnerRef("inner-arg"), outS ) );
	end;

	boolean procedure UpRetBad( string outS );
	begin string procedure innerRef( string la ); return( outS );
	return( not equ( InnerRef("inner-arg"), outS ) );
	end;

	boolean procedure SimUpRetBad( string outS );
	begin string simple procedure innerRef(string la); return(outS);
	return( not equ( InnerRef("inner-arg"), outS ) );
	end;

	procedure check!negation;
	begin	integer int1, int2; real x;
	x := 1.5; int1 := -x; 
	x := -1.5; int2 := x; 
	start!code end; COMMENT flush acs;
	if int1 neq int2
	 then print("Arithmetic bug back: fix(-(1.5))=",int1, 
			", but fix(-1.5)=", int2, crlf );
	end;

	recursive procedure recall(integer depth; procedure foo);
	begin
		procedure check!depth;
		if depth neq 2
		 then print("Parametric procedures bug is back" & crlf);
	if depth=2
	 then recall(depth-1,check!depth)
	 else if depth=0 then foo else recall(depth-1,foo);
	end;

	procedure notcalled; 
	usererr(0,0,"Should never be called");

 Comment make sure LONG temps are big enough;
  if temp!size!confusion then OUTSTR("LONG temp size bug is back"&CRLF);

 Comment make sure up-level accessing of value strings works;
  if SimpleRefBad("Outer-Arg")
   then OUTSTR("Up-reference of valStr from SIMPLE bug is back"&CRLF);
 Comment also make sure result up-level access of value strings works;
  if UpRetBad("Outer-Arg")
   then OUTSTR("Up-reference of valStr RESULT bug is back"&CRLF);
  if SimUpRetBad("Outer-Arg")
   then OUTSTR("Up-reference of valStr RESULT:SIMPLE bug is back"&CRLF);
  check!negation;
  recall(3,notcalled);

 Comment check funny numbers for define and assignc;
  define bit0=1 rot -1, 
	 bitx=1 rot -1 + 1,
	 rbit0=0,
	 rbitx=0;
  redefine rbit0=1 rot -1,
	   rbitx=1 rot -1 + 1;
  define asgc0(x)=" assignc x=1 rot -1; x ",
	 asgcx(x)=" assignc x=1 rot -1 + 1; x ";
  IFCR bit0 rot 1 neq 1
	THENC REQUIRE CRLF & " [DEFINE 1B0 bug back] " MESSAGE; ENDC
  IFCR rbit0 rot 1 neq 1
	THENC REQUIRE CRLF & " [REDEFINE 1B0 bug back] " MESSAGE; ENDC
  IFCR asgc0(bomb) rot 1 neq 1
	THENC REQUIRE CRLF & " [ASSIGNC 1B0 bug back] " MESSAGE; ENDC
  IFCR bitx rot 1 neq 3
	THENC REQUIRE CRLF & " [DEFINE 1B0 bug back] " MESSAGE; ENDC
  IFCR rbitx rot 1 neq 3
	THENC REQUIRE CRLF & " [REDEFINE 1B0 bug back] " MESSAGE; ENDC
  IFCR asgcx(bomb) rot 1 neq 3
	THENC REQUIRE CRLF & " [ASSIGNC 1B0 bug back] " MESSAGE; ENDC
end "Version A"
 
END "TEST";

    ,@	¥