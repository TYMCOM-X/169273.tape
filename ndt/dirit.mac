TWOSEG

TYMSPC==33	;TYMSHARE SPEC NO. (bumped once per transmittal)
TYMREL==2	;TYM RELEASE NO. (bump this for development)
JOBVER==137	; address of where to put version number

DEFINE TITL(TS,TR)<
TITLE DIRIT VERSION TS'.'TR
VCOMPIL=<TS,,TR>>

TITL(\TYMSPC,\TYMREL)

IF2<PRINTX License required: none>


LOC JOBVER
EXP VCOMPILE
RELOC 0





;ACS
P=17
CS=16
C=15
FREPTR=14
GOP=13
UAC1=12		;USED FOR UUOS
UAC2=11
FL2=10	;SECOND FLAG REGISTER
FRM=7	;FOR MOVING THINGS FROM ONE LIST TO ANOTHER
TO=6	;SAME BUT SOURCE


T5=5
T4=4
T3=3
T2=2
T1=1
FL=0	;FLAG REGISTER



;DEVICES
TTY==1
OUTP==2
UFD==3
RPGDV==4	;RPG FILE READER
MFD==5		;CHANNEL TO LOOKUP UFD'S ON FOR GETTING USERNAMES

INI==6		;CHANNEL FOR DIRIT.INI
;CALLI INFORMATION FOR DEVCHR

	DIRBT==4
	DSKBIT==200000
	LPTBIT==40000
	TTYBIT==10


;LOCATIONS & SIZE OF EXTENDED LOOKUP BLOCKS.AAA.

EXLLEN==40	;LENGTH.AAA.
EXTPJC==35	;WORDS 35-6-7 HAVE PROJ CODE;AAA
EXTAUT==26	; LOOKUP BLOCK LOCATION OF AUTH PPN
EXTUNM==27	;USER NAME IN THE LOOKUP BLOCK OF A UFD!
EXTPID==40      ;UUN,,INDEX OF PID

.CHUFD==33      ;READ UFD CHANIO

.ERTRN==6	;TRANSMISSION ERROR


RIBPPN==1
RIBNAM==2
RIBEXT==3

RIBUNM==27


OPDEF PUTLSA [CALLI -63]



ARRAY TMPBF[2],OBUF,TIBUF[3],FNM,OUTNM[5],PDL[40]
ARRAY BLNKN[40]
ARRAY NEWNAM[5],FILLK[EXLLEN+1],DATARY[3],UFDARG[10],UFDBUF[6*^D102]
ARRAY	T3SAVE[2]		; PLACE TO SAVE T3 AND T4
CACHES==10	; SIZE OF CACHE
ARRAY	CACHE[CACHES*4]		; PLACE FOR CACHE
ARRAY	MFDX[EXTUNM+2]		; PLACE FOR UFD'S LOOKUP BLOCK
INTEGER	CACHEC			; CACHE COUNTER FOR REFIL STATS
MFDDIR:				;PARAM BLOCK FOR READ UFD UUO
        XWD     -2,MFDBLK	;-LEN,,ADDR2
        SIXBIT /###/            ;SEARCH ALL GANS
        SIXBIT /UFD/
	BLOCK	5
ARRAY   MFDBLK[2]               ;AREA TO READ PORTION OF MFD INTO

INIPG==377	;PAGE TO MAP DIRIT.INI INTO
INIYCR:	POINT 7,INIPG_^D9	;BYTE POINTER TO COMMAND STRING
INIBLK:	SIXBIT /DIRIT/		;LOOKUP BLOCK FOR DIRIT.INI
	SIXBIT /INI/
	BLOCK 3

VAR
	LOC 41
	PUSHJ P,UUOH
	RELOC 400000
;A COUPLE OF OPDEFS FOR LOCAL UUOS

OPDEF OASC[1B8]	;OUTPUT ASCIZ
OPDEF ODEC[2B8]	;OUTPUT DECIMAL AC HAS MIN WIDTH ADR CHR TO FILL WITH
OPDEF OOCT[3B8]		;SAME BUT OCTAL
OPDEF OCHRI[4B8]	;PUT OUT CHR IN ADRESS FIELD
OPDEF OCHRIS[5B8]	;LIKE OCHRI BUT SKIPS

JFYITM==26
JFYTAB==11

	DEFINE FOR (INDEX,START,END,TEXT)
<INDEX==START
REPEAT <END-START+1>,<TEXT
INDEX==INDEX+1>>


;FLAGS RH OF FL
IDF==	1	;IDENT SEEN
FILSN== 2	;FILE SEEN BY + OR - CONSTRUCT
INISW== 4	;READ FROM DIRIT.INI
RSRT== 10	;REVERSE ORDER OF SORT
INIDN==20	;FINISHED WITH DIRIT.INI
;???== 40	;??unused??
;???== 100	;??unused??
;???== 200	;??unused??
;???== 400	;??unused??
;???== 1000	;??unused??
;???== 2000	;??unused??
TEMPS==4000	;INCLUDE INFO ON TEMPS
RPGSW==10000	;READING RPG FILE
TMPFL==20000	;TMPCORE FILE
SWMOD==40000	;IN SWITCH MODE SEPERATE ##LET LET
NUMF== 100000	;NUMBER SEEN
;???== 200000	;??unused??
SHPSN==400000	;SHARP SIGN SEEN IN IDENT

;FLAGS LEFT HALF
STREXT==1	;STAR FOR EXTENSION
STRNAM==2	;STAR FOR FILE NAME
SHPEXT==4	;SHARP IN EXT
SHPNAM==10	;SHARP IN NAME
TMPDIR==20
;???==  40	;??unused??
NONSTR==100	;A NAME SEEN
FILERR==200	;.ERTRN OCCURED UPON LOOKUP
;???==  400	;??unused??
;???==  1000	;??unused??
;???==  2000	;??unused??
OTTY==  4000	;OUTPUT TO TTY
RPRO==  10000	;FILE IS READ PROTECTED
UNSRT== 20000	;DO NOT SORT
SZSRT== 40000	;SORT BY SIZE
HDPRN== 100000	;HEADING HAS BEEN PRINTED
;???==  200000	;UNUSED
DCDFLG==400000	;ALLOW DECISIONS ON DELETE
STRF==STRNAM!STREXT	;STAR SOMEWHERE



;FLAGGS IN RIGHT HALF OF FL2

RENFLG==1	;DO RENAME
SONM==2		;* ON OUTPUT NAME
SOEX==4		;* ON OUTPUT EXT
SHONM==10	;# ON OUTPUT NAME
SHOEX==20	;# ON OUTPUT EXTENSION
DELFLG==40	;DO A DELETE
TSRT==100	;SORT ON TIME
ESRT==200	;SORT ON EXTENSION
GRTRF==400	;LOOK FOR AFTER
LSF==1000	;LOOK FOR BEFORE
RDLPRT==2000	;IF ON, DO NOT PRINT DELETE AND RENAME INFO
NOPRNF==4000	;DO NOT PRINT ANYTHING BUT TOTALS
DELBAD==10000	;DELETE FILE WITH RIB ERROR
DELTMP==20000	;DELETE BY RENAMING TO TMP FILE

;LEFT HALF
SIZ==1	;INCLUDE SIZE
DAT==2	;INCLUDE DATE
TIM==4	;INCLUDE TIME
SEC==10	;INCLUDE SECONDS
PROT==20	;INCLUDE PROTECTION
ACCES==40	;ACCESS DATE
MODE==100	;INCLUDE MODE
TOTAL==200	;TOTAL REQUESTED
LICSW==400	;LICENSE
PRVB==1000	;PRIV BITS
USDSW==2000	;AMOUNT USED
STATUS==4000	;FILE STATUS (ERROR BITS)
PROJF==10000	;PROJ CODE FROM RIB--INCLUDE. AAA.
AUTH==20000	;AUTH USERNAME FROM RIB IS WANTED
PID==40000	;PID FROM RIB IS WANTED
SCAN:	TRZ FL,NUMF!SHPSN!IDF	;RESET FLAGS
	SKIPE C,SAVCHR#	;CHECK TO SEE IF WE LEFT ONE LAST TIME
	SKIPA CS,CTBL(C)	;GET BITS
SL10:	PUSHJ P,GNCH	;GET A CHR
	JUMPE CS,SL10
	JUMPL CS,SL1	;SPECIAL CHARACTER?
	MOVE T3,[POINT 6,ACCUM]	;SET TO SAVE IDENT
	SETZM ACCUM#
	TLNE CS,SNUMF	;CHECK FOR NUMBER
	JRST SNUM1	;AND GO RACING OFF TO NUMBER ROUTINE
SL2P:	TRO FL,IDF	;IT IS AN IDENT
SL2:
	TLNE T3,770000	;HAVE WE STORED ENOUGH?
	IDPB CS,T3	;NO, STORE ANOTHER (RH OF CHR TABLE HAS SIXBIT)
	CAIE C,77
	CAIN C,43
	TRO FL,SHPSN
	PUSHJ P,GNCH	;CONTINUE
	JUMPG CS,SL2	;CHECK FOR ANOTHER NUMBER OR LETTER
SOK1:	MOVEM C,SAVCHR	;SAVE THE CHARACTER (MUST BE A SPECIAL CHR)
	MOVEI C,0	;ZERO IN C FOR NUMBERS AND IDNETS
	POPJ P,

SL1:	HRRZ C,CS
	CAIN	C,12	;IF 'LF' DONE WITH CURRENT INPUT SOURCE
	JRST	INIINI	;TRY READING FROM DIRIT.INI
SL1C:	SETZM SAVCHR	;ZERO SAVCHR FOR LATER
	POPJ P,		;NO RETURN

SNUM1:	MOVEI T2,0	;SET ACCUMULATED NUM TO 0
SN1A:
	TLNE T3,770000	;WILL STORE THE SIXBIT FOR FILE NAMES
	IDPB CS,T3	;BUT ONLY IF LESS THAN 6
SN1B:
	IMULI T2,^D10	;DECIMAL IN T2
	ADDI T2,-"0"(C)
	PUSHJ P,GNCH	;GET NEXT AND CONTINUE
	JUMPLE CS,SOK2	;CHECK FOR END OF NUMBER
	TLNE CS,SNUMF	;CHECK FOR NUMBER
	JRST SN1A	;CONTINUE SCANNING NUMBER
	TRNN FL,SWMOD	;IF IN SWITCH MODE
	JRST SL2P	;WE WOULD NOT GO HERE TO ACT LIKE AN IDENT
SOK2:	TRO FL,NUMF!IDF	;IT WAS A NUMBER
	JRST SOK1	;SAVE DELIM AND RETURN
GNCH:	TRNE	FL,INIDN	;DONE WITH DIRIT.INI?
	JRST	GNCH3		;YES
	TRNE	FL,INISW	;READING FROM DIRIT.INI?
	JRST	INIIN		;YES
GNCH3:	TRNE FL,RPGSW
	JRST RPGIN
GNCH1:	SOSG TIBUF+2
	INPUT TTY,0	;GET MORE
	ILDB C,TIBUF+1
	JUMPE C,GNCH1
GNCH2:	MOVE CS,CTBL(C)	;GET STATUS
	POPJ P,

RPGIN:	SOSG TIBUF+2	;SOME LEFT?
	JRST DORPGI
DNRPGI:	ILDB C,TIBUF+1	;GET ONE
	JUMPE C,RPGIN	;IGNORE NULLS
	MOVE CS,CTBL(C)
	POPJ P,
DORPGI:	TRNE FL,TMPFL	;IS IT A TEMPCORE FILE??
	JRST XIT	;YES, EXIT
	INPUT RPGDV,0
	STATO RPGDV,760000
	JRST DNRPGI	;NO ERRORS
	STATO RPGDV,20000	;WAS IT EOF
	OUTSTR [ASCIZ /

***ERROR READING COMMAND FILE***
/]
	CLOSE RPGDV,0
	SETZM BLNKN
	SETZM BLNKN+3	;DELETE COMMAND FILE
	RENAME RPGDV,BLNKN
	JFCL	;IGNORE FAILURE
XIT:	RESET
	EXIT 1,	;EXIT
	JRST XIT

.CHMFP==35
.GTAUN==-23	;FRAME AUN

INIINI: TROE	FL,INISW	;INIT FOR DIRIT.INI READ
	JRST	[	TRO	FL,INIDN   ;ALREADY PROCESSED DIRIT.INI
			JRST	SL1C]
	OPEN	INI,[	0
			SIXBIT /DSK/
			0]
	 JRST	INI1
	PUSH	P,T1
	MOVE	T1,[-1,,.GTAUN]
	GETTAB	T1,		;GET FRAME AUN
	 JRST	INI1
	MOVEM	T1,INIBLK+3	;LOOKUP AUN DIRIT.INI
	LOOKUP	INI,INIBLK
	 JRST	INI2
	MOVE	T1,[.CHMFP,,INI]	;MAP IN DIRIT.INI
	CHANIO	T1,[	1B7+1B17+INIPG
			1]
	 JRST	INI1
	SKIPE	INIPG_^D9+777	;ASSUME FILE BAD IF LAST WORD NOT ZERO
	JRST	INI1
	MOVE	T1,[	POINT	7,INIPG_^D9]	;INIT CHAR POINTER
	MOVEM	T1,INIYCR
	POP	P,T1
	JRST	SL10
INI2:	TRO	FL,INIDN
	HRRZ	T1,INIBLK+1	;FILE NOT FOUND?
	JUMPE	T1,[		;IF YES, GO PROCESS COMMAND
			POP	P,T1
			JRST	SL1C]
INI1:	OUTSTR	[	ASCIZ/

***ERROR ATTEMPTING TO READ DIRIT.INI***
/]
	JRST	XIT

INIIN:	SKIPN	INIYCR	;POSSIBLY MORE?
	JRST	INIIN1	;NO
	ILDB	C,INIYCR	;PICK UP NEXT CHARACTER
	JUMPE	C,[	SETZM	INIYCR	;INDICATE END OF STRING
			JRST	INIIN1]
	MOVE	CS,CTBL(C)
	POPJ	P,
INIIN1:	MOVE	CS,CTBL+12	;RETURN A 'LF'
	POPJ	P,

SNUMF==200000	;NUMBER INDICATOR

CTBL:	0				;null
	FOR (I,1,10,<XWD 400000,I>)	;^A through ^H (backspace)
	0				;tab
	XWD 400000,12			;line feed
	REPEAT 3,<0>			;vertical tab, form feed, cr
	FOR (I,16,37,<XWD 400000,I>)	;^N through Space-1
	0				;Space
	FOR (I,41,42,<XWD 400000,I>)	; ! and Quote
	3				; Sharp	
	FOR (I,44,51,<XWD 400000,I>)	; $, %, &, ', (, and )
	12				; *	
	FOR (I,53,57,<XWD 400000,I>)	; +, Comma, -, Period
	FOR (I,60,71,<XWD SNUMF,I-40>)	; 0 through 9
	FOR (I,72,76,<XWD 400000,I>)	; Colon, SemiColon, <, =, >
	3				; Question-mark
	XWD 400000,100			; At-sign
	FOR (I,101,132,<I-40>)		; A through Z
	FOR (I,133,140,<XWD 400000,I>)	; [, \, ], UnderScore, and Grave
	FOR (I,141,172,<I-100>)		; a though z
	FOR (I,173,174,<XWD 400000,I>)	; Open-curly, vertical-bar
	XWD 400000,12			; close-curly (archaic: Altmode)
	FOR (I,176,177,<XWD 400000,I>)	; Tilde and Delete
INRPG:
	HRRZ T1,JOBFF
	MOVEI T2,-1(T1)
	HRLI T2,-200
	MOVEM T2,TMPBF+1
	MOVSI T2,(<SIXBIT /DIR/>)
	MOVEM T2,TMPBF
	HRRM T1,TIBUF+1
	ADDI T1,200	;CHECK TO SEE IF ENOUGH CORE
	CAMLE T1,JOBREL
	JRST	[IORI T1,1777
		CORE T1,
		JRST NOCOR	;GIVE UP
		JRST .+1]
	MOVE T1,[XWD 2,TMPBF]
	TMPCOR T1,	;GET THE FILE
	JRST RPGDSK	;NO, TRY DSK
	ADDM T1,JOBFF	;REMEMBER NEW END OF WORLD
	IMULI T1,5	;GET NUMBER OF CHRS
	ADDI T1,1	;FOR SOSG PROBLEMS
	MOVEM T1,TIBUF+2
	MOVSI T1,(<POINT 7,0>)	;SET INPUT POINTER
	HLLM T1,TIBUF+1
	TRO FL,TMPFL	;SET TEMP CORE FLAG
	JRST RPGRET	;GO
RPGDSK:	INIT RPGDV,1	;GET THE COMMAND DEVICE
	SIXBIT /DSK/
	TIBUF
	JRST STPT	;GIVE UP IF NOT THERE
	PJOB T1,	;JOB NUMBER
	MOVEI T4,3	;NUMBER OF DIGITS
INRPG1:	IDIVI T1,12	;CONVERT TO DECIMAL
	ADDI T2,20	; USING T1,T2,T3, WHICH MUST BE
	LSHC T2,-6	; CONTIGUOUS
	SOJG T4,INRPG1
	HRRI T3,(<SIXBIT /DIR/>)	;REST OF COMMAND FILE NAME
	MOVEM T3,BLNKN	;TO LOOKUP BLOCK
	MOVSI T3,'TMP'	;EXTENTION
	MOVEM T3,BLNKN+1
	SETZM BLNKN+3	;STANDRAD [P,P]
	LOOKUP RPGDV,BLNKN	;SEE IF IT IS THERE FROM COMPIL
	JRST STPT	;NOT THERE
	JRST RPGRET
STPT:	TDZA FL,FL
	MOVEI FL,RPGSW
	RESET
LPRET:	TRNE	FL,RPGSW!TMPFL
	JRST	INRPG
	INIT TTY,1
	SIXBIT /TTY/
	TIBUF
	HALT
	OUTSTR [ASCIZ /
*/]
RPGRET:	MOVEI T1,OUTNM
	MOVE P,[IOWD 40,PDL]
	SETZB FL2,SAVCHR
	SETZM ODEV#
	SETZM OPPN#
	SETZM SVPRT#	;SAVE PROTECTION FOR RENAME
	MOVSI T2,'TTY'
	MOVEM T2,DEFDEV#	;DEFAULT OUTPUT DEV TO TTY
	MOVE T2,[XWD JFYITM,JFYTAB]
	GETTAB T2,
	 MOVEI T2,^D60
	MOVEM T2,JFYSEC#	;SAVE JIFFIES/SEC
	PUSHJ P,SCNMS
	CAIN C,"!"	;SHOULD WE RUN SOMETHING
	JRST RUNIT	;YES
	CAIE C,"<"
	JRST NOPRT	;NOT PROTECTION
	PUSHJ P,SCAN
	TRNN FL,NUMF
	JRST SYNERR
	HRROM T2,SVPRT
	PUSHJ P,SCAN
	CAIE C,">"
	JRST SYNERR
	PUSHJ P,SCAN
NOPRT:	TLZE FL,STREXT
	TRO FL2,SOEX
	TLZE FL,STRNAM
	TRO FL2,SONM
	TLZE FL,SHPEXT
	TRO FL2,SHOEX
	TLZE FL,SHPNAM
	TRO FL2,SHONM
	MOVE T1,[XWD OUTNM,NEWNAM]
	BLT T1,NEWNAM+4
	CAIE C,"_"
	JRST SYNERR
	TRO FL2,RDLPRT	;IN CASE THIS IS A RENAME OR DELETE
IGN:	PUSHJ P,SCAN
IGN1:	TRNN FL,IDF	;SHOULD BE NAME
	CAIN C,"["
	JRST NAME
	CAIN C,"-"
	JRST RDSUB	;REMOVE NAMES
	CAIN C,","
	JRST IGN
	CAIN C,12
	JRST GO		;END OF COMMAND STRING
	CAIE C,"/"
	JRST SYNERR
	TRO FL,SWMOD
	MOVEI T2,0
	PUSHJ P,SCAN
	TRNN	FL,NUMF
	JRST	IGN2
	MOVE	T1,SAVCHR
	CAIE	T1,"<"		;IF DELIM IS "<"
	CAIN	T1,">"		; OR ">" THEN NUM IS NOT A SWITCH
	PUSHJ	P,SCAN		;GET DELIM
IGN2:	TRZ FL,SWMOD	;NOT ANY MORE
	TRNE FL,IDF	;AN IDENT?
	SKIPA C,ACCUM	;GET IT
	ROT C,-7	;CONVERT SPECIAL TO ASCII
	MOVSI T1,-SWNUM
	CAME C,SWWD(T1)
	AOBJN T1,.-1
	JUMPGE T1,SYNERR
	XCT SWACT(T1)
	JRST IGN

LPTDEF:	MOVSI T1,'LPT'
	MOVEM T1,DEFDEV	;CHANGE DEFAULT DEVICE
	JRST IGN
DEFINE SWITCH <
X E,<TRO FL2,ESRT>
X R,<TRO FL,RSRT>
X T,<TRO FL2,TSRT>
X S,<TLO FL2,SIZ>
X D,<TLO FL2,DAT!TIM>
X C,<TLO FL2,DAT>
X J,<TLO FL2,DAT!TIM!SEC>
X N,<TLO FL2,TOTAL>
X B,<TLO FL2,LICSW>
X P,<TLO FL2,PROT>
X A,<TLO FL2,ACCES>
X M,<TLO FL2,MODE>
X K,<TRO FL,TEMPS>
X F,<TLO FL2,SIZ!DAT>
X G,<TLO FL2,PRVB>
X W,<TLO FL2,PROJF>
X O,<TLO FL2,AUTH>
X 1,<TLO FL2,PID>
X U,<TLO FL,UNSRT>
X H,<TLO FL,SZSRT>
X X,<TRO FL2,DELFLG>
X 2,<TRO FL2,DELBAD>
X 3,<TRO FL2,DELTMP>
X L,<TRO FL2,RENFLG>
X Q,<TLO FL,DCDFLG>
X Y,<TLO FL2,USDSW>
X Z,<TLO FL2,STATUS>
X V,<TRO FL2,NOPRNF>
X I,<JRST LPTDEF>
>
DEFINE X(A,B)
<SIXBIT /A/
>
SWWD:	SWITCH
	ASCII />/
	ASCII /</
SWNUM==.-SWWD
DEFINE X(A,B)
<B
>
SWACT:	SWITCH
	JRST GRDAT
	JRST LSDAT

RUNIT:	SKIPN T1,OUTNM+4	;GET DEVICE
	MOVSI T1,'SYS'	;ASSUME SYS
	MOVEM T1,FNM
	MOVE T1,[XWD OUTNM,FNM+1]
	BLT T1,FNM+4
	SETZM FNM+3
	SETZM FNM+5
	MOVEI T1,FNM
	TRNE FL,RPGSW	;+1 IN RPG MODE
	HRLI T1,1
	RUN T1,
	HALT		;SO SYS GIVES ERROR MSG
NAME:	PUSHJ P,COMNM
	JRST IGN1
	MOVEI T1,DIRPTR
	TLO FL,NONSTR
	MOVEI FRM,DIRPTR
	MOVEI TO,USEPTR
MOVFIL:	TRZ FL,FILSN	;NONE SEEN YET
GETLP:	HRRZ T1,(T1)
GETLP1:	JUMPE T1,TRNDON	;DONE
	MOVE T4,1(T1)
	AND T4,T2
	CAME T4,FNM	;SAME FILE NAME?
	JRST GETLP
	MOVE T4,2(T1)
	AND T4,T3
	CAME T4,FNM+1	;AND SAME EXT?
	JRST GETLP
	TRO FL,FILSN	;ONE WAS TRANSFERED
	PUSHJ P,PTRTRN	;MOVE FROM ONE LIST TO ANOTHER
	JRST GETLP1

PTRTRN:	SKIPE T4,(TO)	;MOVE TO OTHER CHAIN
	HRLM T1,(T4)
	HRRM T1,(TO)
	TLZN T4,-1
	HRLM T1,(TO)
	EXCH T4,(T1)
	TRNN T4,-1
	HLLM T4,(FRM)
	TLNN T4,-1
	HRRM T4,(FRM)
	TRNE T4,-1
	HLLM T4,(T4)
	MOVSS T4
	TRNE T4,-1
	HLRM T4,(T4)
	HLRZ T1,T4	;GET NEXT ENTRY
	POPJ P,

TRNDON:	TRNN FL,FILSN
	TRZ FL2,RDLPRT	;DID NOT FIND THAT OONE
	JRST IGN1
SETDIR:	SETZM DIRPTR#
	SETZM USEPTR#
	SETZM TOTSIZ#
	SETZM NUMFIL#	;RECORD THIS ON INPUT
	SKIPN T1,FNM+4
	MOVSI T1,'DSK'
	MOVE T2,T1
	SKIPN FNM+3	;WAS ONE SPECIFIED?
	DEVPPN T2,	;NO, USE ONE FROM DEV NAME
	MOVE T2,FNM+3
	MOVEM T2,OPPN	;SAVE PPN AND DEVICE
	MOVEM T2,FNM+3
	MOVEM T1,ODEV
	MOVEM T1,FNM+4
	DEVCHR T1,	;FIND OUT ABOUT IT
	JUMPE T1,CKTMP	;MAY BE TMP
	TLNN T1,DIRBT
	JRST NONDV	;NO DIRECTORIES FOR NON-DIRECTORY DEVICES
	TLNE T1,DSKBIT
	TLNE T1,LPTBIT	;CHECK FOR LPT JUST IN CASE
	JRST NONDV
	INIT UFD,17
	SIXBIT /DSK/
	0
	  JRST DNA
	INIT MFD,17
	SIXBIT /DSK/
	0
	  JRST DNA
	MOVE T2,OPPN
	MOVEM T2,BLNKN+RIBNAM
	MOVSI T2,'UFD'
	MOVEM T2,BLNKN+RIBEXT
	MOVE T2,[XWD 1,1]
	MOVEM T2,BLNKN+RIBPPN
	MOVEI T2,37
	MOVEM T2,BLNKN	; SIZE OR EXTENDED LOOKUP
	LOOKUP UFD,BLNKN
	  JRST DIRNVA	;CAN NOT GET IT
	MOVE T2,BLNKN+RIBUNM
	GETPPN T1,
	CAME T1,BLNKN+RIBNAM
	  PUTLSA T2,
	PUSHJ P,SETWD	;SET UP DEPOSIT POINTER
	MOVE T1,[-6*^D102,,UFDBUF]
	MOVEM T1,UFDARG
	MOVSI T1,'*  '	;SET UP TO READ UFD
	MOVEM T1,UFDARG+1
	MOVEM T1,UFDARG+2
	SETZM UFDARG+4
	MOVSI T1,360200	;STATUS, SIZE,DATE,LIC, DATE IN DEC LOCAL
	MOVEM T1,UFDARG+3
NXTUFD:	MOVE T1,[.CHUFD,,UFD]
	CHANIO T1,UFDARG
	 JRST COMSTR	;JUST ASSUME EOF
	MOVE T3,UFDARG
NXTNAM:	SOSGE UFDARG+7
	JRST NXTUFD	;RAN OUT
	PUSHJ P,INSENT
	SKIPGE T1,2(T3)
	JRST	[MOVSI T1,2	;JUST CALL IT BIG
		MOVEM T1,2(T3)
		JRST .+1]
	ADDM T1,TOTSIZ
	HRLI T3,-6
	MOVE T1,(T3)
	PUSHJ P,INSWD
	AOBJN T3,.-2
	JRST NXTNAM
SETWD:	SOS FREPTR,JOBFF
	SUB FREPTR,JOBREL	;SET UP POINTER
	HRLZI FREPTR,-1(FREPTR)
	HRR FREPTR,JOBFF	;AND REMEMBER AOBJN BEFOR DEPOSIT
	POPJ P,

INSENT:	HRRZ T1,DIRPTR	;PUT IN A LINK
	PUSHJ P,INSWD
	SKIPN T1	;IN CASE DIRPTR WAS 0
	HRLM FREPTR,DIRPTR	;BACK LINK SHOULD BE SE
	HRRM FREPTR,DIRPTR	;FORWARD LINK IN ANY CASE
	SKIPE T1	;UNLESS FIRST ENTRY
	HRLM FREPTR,(T1)	;UPDATE BACK LINK
	AOS NUMFIL
	POPJ P,

INSWD:	AOBJN FREPTR,GOIN
	PUSH P,T3
	MOVE T3,JOBREL	;GET MORE CORE
	ADDI T3,2000
	CORE T3,
	JRST NOCOR
	POP P,T3
	ADD FREPTR,[XWD -2000,0]	;1K MORE
GOIN:	MOVEM T1,(FREPTR)
	POPJ P,

CKTMP:	MOVS T1,ODEV
	CAIE T1,'TMP'
	JRST NONDV	;NOT FOR US
	HRRZ T1,JOBFF	;GET SPACE TO READ DIR INTO
	HRRZ T2,T1
	HRLI T2,-200
	MOVEM T2,TMPBF+1
	ADDI T1,200
	CAMLE T1,JOBREL	;ENOUGH SPACE?
	JRST	[IORI T1,1777	;MAKE EVEN 1K BOUNDARY
		CORE T1,
		JRST NOCOR	;LOSE
		JRST .+1]
	MOVE T4,[XWD 4,TMPBF]	;DIR READ OP
	TMPCOR T4,
	JRST NONDV	;NO TMPCOR ON THIS SYSTEM
	TLO FL,TMPDIR	;TELL THE WORLD
	AOS T3,JOBFF
	ADDM T4,JOBFF	;UPDATE FOR SIZE
	PUSHJ P,SETWD
	JUMPE T4,COMSTR	;NOTHING THERE
TDLP:	PUSHJ P,INSENT
	HLLZ T1,(T3)
	PUSHJ P,INSWD	;NAME
	MOVEI T1,0
	PUSHJ P,INSWD	;NO EXT
	HRRZ T1,(T3)
	PUSHJ P,INSWD	;SIZE
	ADDI T3,1
	REPEAT 3,<PUSHJ P,INSWD>	;NO DATE, LIC,STATUS
	SOJG T4,TDLP
COMSTR:	MOVE T2,ODEV
	POPJ P,

NONDV:	OUTSTR [ASCIZ /
NON-DIRECTORY DEVICE /]
DEVPR:	MOVE T2,ODEV
	PUSHJ P,OUTSIX
	JRST XIT

OUTSIX:	MOVEI T1,0
	LSHC T1,6
	ADDI T1,40
	OUTCHR T1
	JUMPN T2,OUTSIX
	POPJ P,

MULDIR:	OUTSTR [ASCIZ /
MULTIPLE DEVICES SPECIFIED/]
	JRST XIT

NOCOR:	OUTSTR [ASCIZ /
CAN NOT GET SUFFICIENT CORE/]
	JRST XIT

ODNA:	MOVE T2,BLNKN+1
	MOVEM T2,ODEV	;SET NAME FOR ERROR MESSAGE
DNA:	OUTSTR [ASCIZ /
UNAVAILABLE DEVICE /]
	JRST DEVPR

DIRNVA:	OUTSTR [ASCIZ /
CAN NOT READ DIRECTORY FOR THIS PROJECT-PROGRAMMER/]
	JRST XIT

SYNERR:	OUTSTR [ASCIZ /
SYNTAX ERROR/]
	JRST XIT

EXTERNAL JOBFF,JOBREL,JOBSA

NOENT:	OUTSTR [ASCIZ /
CAN NOT ENTER OUTPUT FILE/]
	JRST XIT

ABORT:	OUTSTR [ASCIZ/
COMMAND ABORTED/]
	JRST XIT
SCNMS:	PUSHJ P,SCAN
SCNM:	SETZM (T1)
	MOVEI T2,1(T1)
	HRL T2,T1
	BLT T2,4(T1)	;SET CELLS TO ZERO
	MOVE T2,ODEV
	MOVEM T2,4(T1)
	MOVE T2,OPPN
	MOVEM T2,3(T1)
	TLZ FL,STRF!SHPEXT!SHPNAM	;NO STARS SEEN
	TRNN FL,IDF
	JRST CKPPN	;SEE IF JUST PPN
	PUSH P,ACCUM	;SAVE IT
	TRNE FL,SHPSN
	TLO FL,SHPNAM
	PUSHJ P,SCAN	;FIND OUT IF FILE NAME OR DEVICE
	CAIE C,":"
	JRST FILNM	;MUST BE A FILE NAME
	TLZ FL,SHPNAM	;NOT REALLY SEEN YET
	POP P,4(T1)	;GET DEVICE
	PUSHJ P,SCAN	;NOW GET FILE NAME
	TRNN FL,IDF
	JRST CKPPN
	PUSH P,ACCUM
	TRNE FL,SHPSN
	TLO FL,SHPNAM
	PUSHJ P,SCAN
FILNM:	POP P,T2	;GET NAME IN PROPER PLACE
	CAMN T2,[SIXBIT /*/]
	TLOA FL,STRNAM
	MOVEM T2,(T1)
CKEXT:	CAIE C,"."
	JRST CKPPN
	PUSHJ P,SCAN
	TRNN FL,IDF
	POPJ P,
	TRNE FL,SHPSN
	TLO FL,SHPEXT
	HLLZ T2,ACCUM
	CAMN T2,[SIXBIT /*/]
	TLOA FL,STREXT
	MOVEM T2,1(T1)
CKPPNP:	PUSHJ P,SCAN
CKPPN:	CAIE C,"["
	POPJ P,
	PUSHJ P,RDOCT
	HRLZM T2,3(T1)
	CAIE C,","
	JRST SYNERR
	PUSHJ P,RDOCT
	HRRM T2,3(T1)
	CAIE C,"]"
	JRST SYNERR
	JRST SCAN

RDOCT:	MOVEI T2,0
RDOC1:	PUSHJ P,GNCH
	CAIG C,"7"
	CAIGE C,"0"
	POPJ P,
	LSH T2,3
	ADDI T2,-"0"(C)
	JRST RDOC1
GRDAT:	TRO FL2,GRTRF
	PUSHJ P,T2CNV
	MOVEM T1,SVGRT#
	TRZ FL2,RDLPRT
	JRST IGN

LSDAT:	TRO FL2,LSF
	PUSHJ P,T2CNV
	MOVEM T1,SVLSD#
	TRZ FL2,RDLPRT	T IF ASKING FOR GREATER OR LESS
	JRST IGN

COMNM:	MOVEI T1,FNM
	PUSHJ P,SCNM
	SKIPN T2,ODEV
	PUSHJ P,SETDIR
	CAME T2,FNM+4
	JRST MULDIR
	MOVE T2,OPPN
	CAME T2,FNM+3
	JRST MULDIR
	TLNN FL,STRF
	SKIPE FNM	;AND NAME GIVEN?
	SKIPA
	POPJ P,		;JUST DEVICE
	TLNE FL,STRNAM!STREXT!SHPNAM!SHPEXT
	TRZ FL2,RDLPRT	;MUST PRINT
	TLNE FL,STRNAM
	TDZA T2,T2	;T2 HAS MASK FOR FILE NAME
	MOVNI T2,1
	TLNE FL,SHPNAM	;IF S #'S IN NAME
	JRST	[MOVE T3,FNM
		PUSHJ P,SHPMSK	;MAKE A MASK
		ANDM T3,FNM
		MOVE T2,T3
		JRST .+1]
	TLNE FL,STREXT
	TDZA T3,T3
	MOVSI T3,-1
	TLNE FL,SHPEXT
	JRST	[HLLZ T3,FNM+1
		PUSHJ P,SHPMSK
		HLLZS T3	;MAKE SURE BITS IN RH ARE OFF
		ANDM T3,FNM+1
		JRST .+1]
	AOS (P)
	POPJ P,

T2CNV:	LDB T1,[POINT 17,T2,21]	;TIME IN SECONDS
	ANDI T2,37777
	HRLM T2,T1	;STORE DATE IN LH, TIME IN RH
	POPJ P,
RDSUB:	PUSHJ P,SCAN
	PUSHJ P,COMNM
	JRST SYNERR
	TLOE FL,NONSTR
	JRST NOASST
	MOVEI T1,0
	EXCH T1,DIRPTR
	MOVEM T1,USEPTR
NOASST:	MOVEI T1,USEPTR
	MOVEI FRM,USEPTR
	MOVEI TO,DIRPTR
	JRST MOVFIL

SHPMSK:	XOR T3,[SIXBIT /######/]
	MOVEI T5,77
SHPLP:	TDNE T3,T5
	IOR T3,T5
	LSH T5,6
	JUMPN T5,SHPLP	;MORE TO DO?
	POPJ P,
TMPELM:	MOVEI T1,USEPTR
TMPEL1:	HRRZ T1,(T1)
TMPEL2:	JUMPE T1,CPOPJ	;FINISHED LOOKING
	HLRZ T2,2(T1)
	CAIE T2,'TMP'	;IS EXTENSION TMP?
	JRST TMPEL1	;NO, CONTINUE LOOKING
	MOVEI FRM,USEPTR
	MOVEI TO,DIRPTR
	PUSHJ P,PTRTRN	;MOVE TO NOT USED LIST
	JRST TMPEL2	;AND COONTINUE LOOKING

DATLIM:	MOVEI T1,USEPTR
DLMNXT:	HRRZ T1,(T1)
DLMNX1:	JUMPE T1,CPOPJ
	MOVE T2,5(T1)	;TIME AND DATE
	TRNN FL2,GRTRF	;LOOKING FOR TIME AFTER?
	JRST LSDCK	;NO, TRY BEFORE
	CAMGE T2,SVGRT
	JRST DTLMRM	;AFTER AND DATE WAS BEFORE, ELIM
LSDCK:	TRNN FL2,LSF
	JRST DLMNXT	;THIS ONE OK, TRY NEXT
	CAMGE T2,SVLSD
	JRST DLMNXT
DTLMRM:	MOVEI FRM,USEPTR
	MOVEI TO,DIRPTR
	PUSHJ P,PTRTRN
	JRST DLMNX1	;TRY NEXT
GO:	SETZM FNM+4
	SETZM FNM+3
	TRNN FL2,DELFLG!DELBAD!RENFLG
	JRST JSTDIR	;NOT RENAME OR DELETE
	TLNE FL,NONSTR
	TLNE FL,TMPDIR
	JRST SYNERR	;TMPCOR OR NO NAME SPECIFIED
	SETZM OUTNM+4	;FORCE OUTPUT TO TTY
	SKIPA
JSTDIR:	TRZ FL2,RDLPRT	;NOT RENAME OR DELETE, CLEAR FLAG
	SKIPN ODEV
	PUSHJ P,SETDIR
	TLNE FL,TMPDIR
	TDZ FL2,[XWD DAT!TIM!SEC!PROT!ACCES!MODE!LICSW!PRVB!USDSW!STATUS!PROJF!AUTH!PID,LSF!GRTRF!ESRT!TSRT]
	ADDI FREPTR,1
	HRRZM FREPTR,JOBFF
	RELEASE TTY,0
	CLOSE UFD,0
	TRNE FL2,DELFLG!DELBAD!RENFLG
	JRST NOREL	;WE WILL NEEDD IT FOR DELETE
	TLNE FL,TMPDIR
	RELEASE UFD,0	;LET GO IF NOT DSK
NOREL:	SKIPN T1,OUTNM+4
	MOVE T1,DEFDEV
	MOVEM T1,BLNKN+1
	MOVEM T1,FNM+4
	DEVCHR T1,
	TLNN T1,TTYBIT
	TLZA FL,OTTY
	TLO FL,OTTY
	MOVEI T1,1
	MOVEM T1,BLNKN
	MOVSI T1,OBUF
	MOVEM T1,BLNKN+2
	OPEN OUTP,BLNKN
	JRST ODNA
	ENTER OUTP,OUTNM
	JRST NOENT
	TLNN FL,NONSTR
	JRST	[SKIPN T1,DIRPTR
		JRST ALGONT	;NULL DIR, PRINT TOTALS
		MOVEM T1,USEPTR	;USE ALL NAMES
		SETZM DIRPTR
		TRNN FL,TEMPS
		PUSHJ P,TMPELM	;REMOVE TMP FILES
		TRNN FL2,GRTRF!LSF	;IF RESTRICTED
		TLON FL2,TOTAL	;NO TOTAL, IF TOTAL JUST THAT
		JRST .+1
		JRST ALGONT]	;ELSE PRINT TOTALS
	TRNE FL2,GRTRF!LSF
	PUSHJ P,DATLIM		;REMOVE UNWANTED FILES FROM LIST
	SETZM OCNT#
	SETZM OTOT#	;NUMBER OF BLOCK IN FILES PRINTED
	TRNE FL2,DELFLG	;IF NOT DELETE
	SKIPE DIRPTR	;OR NOT ALL FILES
	JRST PRL1	;GO PRINT
	TLNE FL,DCDFLG
	JRST PRL1	;SKIP QUESTION IF CONFIRMING INDIVIDUAL FILES
	OUTSTR [ASCIZ /
DELETING ALL FILES OK? /]
ALLASK:	PUSHJ P,YESNO	;ASK IF IT'S OK
	JRST ABORT	;"N" NOPE, FORGET IT
	JRST [	OASC [ASCIZ/  WHAT? /]	;"G" NOT MEANINGFUL HERE
		OUTPUT OUTP,
		JRST ALLASK  ]
	JRST PRL1	;"Y" YES INDEEDEY
HDPRNT:	TLOE FL,HDPRN	;IF PRINTED ONCE
	POPJ P,		;DO NOT PRINT AGAIN
	TRNE FL2,RENFLG
	PUSHJ P,RENINI	;RENAME, INITIALIZE OUTPUT NAME
RTDL1:	TRNE FL2,RDLPRT!NOPRNF
	JRST CRLF
	TRNN FL2,DELFLG
	JRST	RTDL4
	TRNE	FL2,DELTMP
	JRST	RTDL3
	OASC	[ASCIZ/
FILES DELETED:/]
	JRST	RTDL2
RTDL3:	OASC [ASCIZ/
Files "deleted":/]
	JRST	RTDL2
RTDL4:	TRNE FL2,RENFLG
	OASC [ASCIZ/
FILES RENAMED:/]
RTDL2:	TLNE FL,OTTY!TMPDIR
	JRST NONMHD
	OASC [ASCIZ /
DIRECTORY /]
	HLRZ T1,OPPN
	OOCT
	OCHRI ","
	HRRZ T1,OPPN
	OOCT
	OCHRI 11
	TIMER T1,
	IDIVI T1,^D3600
	PUSHJ P,OUTTIM
	OCHRI 11
	DATE T1,
	PUSHJ P,ODATE
	PUSHJ P,CRLF
NONMHD:
	TLNE FL,OTTY
	JRST NOHD2	;NO HEADING ON TTY
	OASC [ASCIZ /   FILE/]
	TLNN FL2,DAT!ACCES!USDSW
	JRST EHL1	;NO MORE ON THIS LINE
	MOVEI T2,5
	TLNE FL2,SIZ	;PRINTING SIZE?
	MOVEI T2,15
	TLNN FL2,DAT
	JRST ACPRH	;WANT TO PRINT ACCES
	TLNE FL2,TIM
	ADDI T2,4	;EXTRA SPACES
	PUSHJ P,SPACES
	OASC [ASCIZ /CREATION/]
	TLNN FL2,ACCES
	JRST EHL1	;NO ACCES DATE
	MOVEI T2,2
	TLNE FL2,TIM
	MOVEI T2,6
ACPRH:	TLNE FL2,PROT
	ADDI T2,^D13
	TLNN FL2,ACCES
	JRST USDPRH
	PUSHJ P,SPACES
	OASC [ASCIZ /   ACCESS/]
	MOVEI T2,2
USDPRH:	TLNN FL2,USDSW
	JRST EHL1
	ADDI T2,4
	TLNE FL2,MODE
	ADDI T2,4
	PUSHJ P,SPACES
	OASC [ASCIZ /USED/]
EHL1:	PUSHJ P,CRLF
	OASC [ASCIZ /NAME    EXT /]
	TLNE FL2,SIZ
	OASC [ASCIZ /  PAGES /]
	TLNE FL2,DAT
	OASC [ASCIZ /   DATE   /]
	TLNE FL2,TIM
	OASC [ASCIZ / TIME   /]
	TLNE FL2,PROT
	OASC [ASCIZ /   ALLOWED   /]
	TLNE FL2,ACCES
	OASC [ASCIZ /    DATE   /]
	TLNE FL2,MODE
	OASC [ASCIZ /MODE/]
	TLNE FL2,USDSW
	OASC [ASCIZ /    WORDS    /]
	TLNE FL2,LICSW
	OASC [ASCIZ / LICENSE /]
	TLNE FL2,STATUS
	OASC [ASCIZ / STATUS /]
	TLNE FL2,PRVB
	OASC [ASCIZ / BITS/]
	TLNE FL2,AUTH
	  JRST [OASC [ASCIZ / AUTHOR/]
		TLNE FL2,PROJF
		  OCHRI ";"
		JRST .+2]
	  OCHRI " "
	TLNE FL2,PROJF
	  OASC [ASCIZ /PROJECT/]
	PUSHJ P,CRLF
NOHD2:	JRST CRLF	;[PJRST] PRINT & RETURN
;NOW THAT WE HAVE THE HEADING OUT, PROCEED TO SORT
PRL1:	MOVEI T4,0	;NEXT ONE FOUND
	MOVEI T1,USEPTR	;SORT POINTER
PRLP:	HRRZ T1,(T1)
	JUMPE T1,ENDATA	;NOTHING LEFT
	SKIPN T3,1(T1)	;NAME IS ZEROED AFTER PRINTING
	JRST PRLP
	JUMPE T4,USEIT	;THIS ONE IS GOOD
	TLNE FL,UNSRT	;UNSORTED?
	JRST ENDATA	;TREAT AS END (SECOND FILE NAME)
	TLNE FL,SZSRT
	JRST SSRT
TMSRT:
	TRNE FL2,ESRT	;SORT ON EXTENSION?
	JRST EXSRT	;YES
NSCK:	TRNE FL2,TSRT	;ON NAME?
	JRST NMSRT
TIMSRT:	MOVE T3,5(T1)
	CAMGE T3,5(T4)
	JRST BIG
	CAME T3,5(T4)
	JRST SMALL
	MOVE T3,1(T1)	;SAME, SORT ON NAME OR EXT
NMSRT:	MOVE T3,1(T1)	;GET NAME
	CAMLE T3,1(T4)	;CHECK NAME
	JRST BIG	;THIS IS BIGGER
	CAME T3,1(T4)	;SAME?
	JRST SMALL	;NO SMALLER
	MOVE T3,2(T1)	;TRY EXT
	CAMLE T3,2(T4)
	JRST BIG
SMALL:	TRNN FL,RSRT	;REVERSE ORDER?
USEIT:	MOVE T4,T1	;OK, PICK UP
	JRST PRLP	;NEXT

BIG:	TRNE FL,RSRT
	MOVE T4,T1
	JRST PRLP

EXSRT:	MOVE T3,2(T1)	;ON EXT FIRST
	CAMLE T3,2(T4)
	JRST BIG
	CAME T3,2(T4)
	JRST SMALL
	JRST NSCK


RENINI:	SKIPN NEWNAM
	TRNE FL2,SONM!SHONM	;IS THERE A NAME
	SKIPA
	TRO FL2,SONM!SOEX	;SET TO *.*
	TRNE FL2,SONM
	TDZA T3,T3
	MOVNI T3,1
	TRNE FL2,SHONM
	JRST	[MOVE T3,NEWNAM
		PUSHJ P,SHPMSK
		ANDM T3,NEWNAM
		JRST .+1]
	MOVEM T3,RNMSK#	;SET UP A MASK
	TRNE FL2,SOEX
	TDZA T3,T3
	MOVNI T3,1
	TRNE FL2,SHOEX
	JRST	[HLLZ T3,NEWNAM+1
		PUSHJ P,SHPMSK
		ANDM T3,NEWNAM+1
		JRST .+1]
	MOVEM T3,RNXMSK#
	ANDM T3,NEWNAM+1	;JUST IN CASE
	POPJ P,
ENDATA:	JUMPE T4,ALGON	;ALL DONE WITH THE ONES TO PRINT
	AOS OCNT
	MOVE T1,3(T4)
	ADDM T1,OTOT
	PUSHJ P,HDPRNT	;PRINT HEADER (IF NEEDED) BEFORE FILE
	MOVEI GOP,0	;SPACING TO GO TO
	MOVEI CS,EXLLEN	;SIZE EXT LOOKUP BLK
	MOVEM CS,FILLK	;SET UP FOR LOOKUP
	MOVE CS,OPPN
	MOVEM CS,FILLK+1
	MOVE CS,1(T4)	;FN
	MOVEM CS,FILLK+2	;SAVE IN CASE NEED LOOKUP
	HLRZ T3,2(T4)	;EXT
	MOVSM T3,FILLK+3
	DMOVEM T3,T3SAVE	; SAVE AC'S SO WE CAN USE T1-T5
	TRNN FL2,RENFLG!DELFLG!DELBAD	;IF RENAME OR DELETE
	TLNE FL2,ACCES!MODE!USDSW!STATUS!PROJF!AUTH!PID	;OF STUFF NOT IN UFD
	JRST [	TLZ FL,RPRO
		LOOKUP UFD,FILLK	;THEN MUST DO A LOOKUP
		JRST [	HRRZ T3,FILLK+3	
			CAIE T3,.ERTRN	;TRANSMISSION ERROR?
			TLOA FL,RPRO	;NO, LOOKUP ERROR
			TLO FL,FILERR	;YES
			JRST ENDAT1]
		JRST .+1  ]
ENDAT1:	TRNE FL2,RDLPRT!NOPRNF
	TLNE FL,DCDFLG
	SKIPA
	JRST DNPRNT	;SKIP PRINTING
	MOVE T3,FILLK+1	;PPN OF THIS FILE
	CAMN T3,[1,,1]	;MFDPPN? (THIS FILE IS A UFD)
	JRST UFDPR	;PRINT UFD "NAME" AS A PPN, RTN TO RTUFD BELOW
	PUSHJ P,OUTSX2	;PRINT NAME
	ADDI GOP,10
	HLLZ CS,2(T4)	;EXT
	JUMPE CS,NOEXT
	PUSHJ P,SETSP
	PUSHJ P,OUTSX2
NOEXT:	ADDI GOP,4
RTUFD:
	TLNN FL2,SIZ	;PRINT SIZE?
	JRST NOSZ
	PUSHJ P,SETSP
	OCHRI " "
	MOVE T1,3(T4)
	LSH T1,-2 	;-SCALE BLOCKS TO PAGES. 29 OCT 76 AAA
	;TLNE FL,OTTY
	;TLNN FL,DTADIR	;REMOVE THESE DTADIR INSTRS. AAA.
	;SKIPA
	;JRST	[ODEC 3," "
	;	ADDI GOP,5	;SMALL BECAUSE OF FILE CARDS
	;	JRST NOSZ]
	ODEC 6," "
	ADDI GOP,10
NOSZ:	TLNN FL2,DAT	;CREATION?
	JRST NOCRT	;NO
	PUSHJ P,SETSP
	OCHRI " "
	HLRZ T1,5(T4)
	PUSHJ P,ODATE
	ADDI GOP,^D10
	TLNN FL2,TIM
	JRST NOCRT	;NO TIME TO PRINT
	PUSHJ P,SETSP
	OCHRI " "
	HRRZ T1,5(T4)
	IDIVI T1,^D60	;TO MIN
	PUSH P,T2	;SAVE SEC
	PUSHJ P,OUTTIM
	POP P,T1
	TLNN FL2,SEC
	JRST NOCR1	;MOVE OVER TO RIGHT PLACE
	OCHRI "."
	ODEC 2,"0"
NOCR1:	ADDI GOP,10
NOCRT:	TLNN FL2,PROT
	JRST NOPRO
	PUSHJ P,SETSP
	LDB T1,[POINT 9,6(T4),35]
	PUSHJ P,PPRT	;PRINT A PROTECTION FIELD
	PUSHJ P,PPRT	;SETS FOR NEXT AND PRINTS SPACE TOO
	PUSHJ P,PPRT
	ADDI GOP,^D13
NOPRO:	TLNN FL2,ACCES
	JRST NOAC
	TLNE FL,RPRO
	JRST NOAC1	;CAN NOT GET IT
	PUSHJ P,SETSP
	OCHRI " "
	LDB T1,[POINT 14,FILLK+3,35]
	MOVEM T1,DATARY
	MOVE T1,[^D2592000]	;NOON TO ELIM INTERFERENCE
	MOVEM T1,DATARY+1
	MOVSI T1,400020
	MOVEM T1,DATARY+2
	MOVEI T1,DATARY
	DATUUO T1,
	 JFCL
	MOVE T1,DATARY		;GET REAL DATE
	PUSHJ P,ODATE
	TLNE FL,OTTY
	OASC [ASCIZ /(A)/]
NOAC1:	ADDI GOP,^D11
NOAC:	TLNN FL2,MODE
	JRST NOMODE
	TLNE FL,RPRO
	JRST NOMD1
	PUSHJ P,SETSP
	OCHRI " "
	LDB T1,[POINT 4,FILLK+4,12]
	OOCT 2," "
	TLNE FL,OTTY
	OASC [ASCIZ /(M)/]
NOMD1:	ADDI GOP,4
NOMODE:	TLNN FL2,USDSW
	JRST NOUSD
	TLNE FL,RPRO
	JRST NOUSD1
	PUSHJ P,SETSP
	OCHRI " "
	MOVE T1,FILLK+5
	ODEC 7," "
NOUSD1:	ADDI GOP,^D9
NOUSD:	TLNN FL,RPRO
	JRST NOLKER
	HRRZ T1,FILLK+3
	OASC @ERRTBL(T1)
NOLKER:	TLNN FL2,LICSW
	JRST NOLIC
	MOVE T1,4(T4)
	TRZ T1,17
	MOVEI T2,0
LICPR1:	TRZE T1,400000
	OASC LICTB(T2)
	ADDI T2,1
	LSH T1,1
	JUMPN T1,LICPR1
	LDB T1,[POINT 2,4(T4),33]
	OASC LICRTB(T1)
	LDB T1,[POINT 2,4(T4),35]
	OASC LICWTB(T1)
	ADDI GOP,^D9
NOLIC:	TLNE FL2,STATUS
	TLNE FL,RPRO
	JRST NOSTS
	PUSHJ P,SETSP
	HRRZ T1,FILLK+17
	MOVEI T2,0
STPR1:	TRZE T1,400000
	OASC STAB(T2)
	ADDI T2,1
	LSH T1,1
	JUMPN T1,STPR1
	ADDI GOP,^D8
NOSTS:	TLNN FL2,PRVB
	JRST NOPVB	;AAA.
	PUSHJ P,SETSP
	LDB T1,[POINT 8,6(T4),26]
	TRZE T1,200
	OASC [ASCIZ / DUMPED/]
	OCHRI " "
	OOCT 3,"0"
	OCHRI ","
	LDB T1,[POINT 4,6(T4),18]
	OOCT 2,"0"
	ADDI GOP,^D19	;AAA.
NOPVB:	TLNN FL2,AUTH!PROJF!PID	; IF NOT AUTH OR PROJ OR PID THEN ALL DONE
;	 TLNE FL,TMPDIR		; IF TMP: NO SUCH THING - SO DONE
	  JRST DNPRNT		; ALL DONE - NOTHING MORE ON LINE
	; HERE WE KNOW EITHER AUTH OR PROJ OR BOTH
	PUSHJ P,SETSP		; TO NEXT TAB STOP
	OCHRI " "
	; THE PROJ-CODE IS ALREADY IN FILLK+EXTPJC
	TLNN FL2,AUTH
	 JRST PTUNM2		; NO AUTH - JUST PJC
	MOVE T1,FILLK+EXTAUT	; GET AUTH PPN
	PUSHJ P,PPNNAM		; GET NAME (HOPEFULLY FROM CACHE)
	 JRST ANOUNM		; CAN'T CONVERT PPN TO USER
        PUSHJ   P,PTUNAM        ;PRINT USERNAME
        JRST    PTUNM2
ANOUNM: PUSHJ   P,NOUNM         ;PRINT PPN
PTUNM2: TLNN    FL2,PROJF       ;IF NOT PROJ THEN CHECK FOR PID
        JRST    PID1
        TLNE    FL2,AUTH        ;IF AUTH SEP. FOR PROJ WITH ";"
	OCHRI ";"
NOAUT:	SKIPN FILLK+EXTPJC	; DON'T PRINT BLANK PJC'S
	 JRST DNPRNT
	OCHRI """
	OASC FILLK+EXTPJC
	OCHRI """
	TLNN FL2,AUTH
	 OASC [ASCIZ "(PJC)"]	; LABEL IF ONLY ONE
	JRST DNPRNT
PID1:   TLNN    FL2,PID         ;IF NOT PID THEN DONE
        JRST    DNPRNT
        TLNE    FL2,PROJF!AUTH  ;IF PROJ OR AUTH SEP. FROM PID WITH ";"
        OCHRI   ";"
        SKIPN   T1,FILLK+EXTPID         ;DON'T PRINT ANYTHING IF NO PID
        JRST    DNPRNT
        HLRZS   T1              ;GET UUN
        PUSHJ   P,PPNNAM        ;TRY TO MAP TO USERNAME OR AT LEAST PPN
         JRST   PID2            ;NO LUCK
        PUSHJ   P,PTUNAM        ;PRINT USERNAME
        JRST    PID3
PID2:   PUSHJ   P,NOUNM         ;PRINT PPN OR UUN
PID3:   OCHRI   ":"             ;SEPERATE FROM INDEX
        HRRZ    T1,FILLK+EXTPID ;GET PID INDEX
        ODEC                    ;PRINT IT
        JRST    DNPRNT
PTUNAM:         ;PRINT SIXBIT USERNAME POINTED TO BY T1
	OCHRI "("
PTUNM:	ILDB C,T1		; GET CHAR OF USERNAME
	JUMPE C,PTUNM1		; STOP ON NULL (BLANK)
	OCHRI 40(C)		; TYPE IT (SIXBIT)
	JRST PTUNM
PTUNM1:	OCHRI ")"
        POPJ    P,
NOUNM:	        ;NOW PRINT PPN AS A PPN IN "[N,N]" FORM
	OCHRI "["
        PUSH    P,T1
        HLRZS   T1              ;PRINT AS A PPN
        JUMPE   T1,NOUN2        ;ACTUALLY ONLY A UUN
	OOCT
	OCHRI ","
NOUN2:  HRRZ    T1,(P)
	OOCT
	OCHRI "]"
        POP     P,T1
        POPJ    P,
; HERE IS THE STUFF TO FIND A USERNAME GIVEN A PPN BY LOOKING
; UP THE UFD.  IT KEEPS A 10 NAME CACHE TO AVOID UNNECESSARY
; LOOKUPS.

	; CACHE FORMAT:
	; +0	PPN
	; +1	USERNAME (SIXBIT)
	; +2	USERNAME (SIXBIT)
	; +3	REFERENCE COUNT
	; NOTE: BITS 0-5 OF +3 WORD MUST BE 0 FOR +1,+2 TO BE
	; ASSURED OF BEING NULL TERMINATED.

MFDX1:	EXTUNM+1
	XWD 1,1
	Z
	SIXBIT "UFD"
MFDX1S==.-MFDX1-1

PPNNAM: MOVE    T5,T1           ;PRESERVE PPN
	MOVEI T2,CACHES*4	; POINTER TO END
	MOVE T3,CACHE-1(T2)	; GOING TO FIND THE OLDEST ENTRY...
	MOVEI T4,CACHES*4-4	; IN CASE WE NEED TO DO A LOOKUP
PPNNM1:	SUBI T2,4		; MOVE POINTER BACK AN ENTRY
	CAMGE T3,CACHE+3(T2)	; IS THIS ONE OLDER
	 JRST  [MOVE T3,CACHE+3(T2)
		MOVE T4,T2
		JRST .+1]
        TLNN    T5,-1           ;ONLY UUN KNOWN?
        HLL     T1,CACHE(T2)    ;YES, DON'T COMPARE AGAINST GAN
	CAME T1,CACHE(T2)	; IS IT A MATCH?
	 JUMPG T2,PPNNM1	; IF NOT MATCH AND MORE TRY AGAIN
	JUMPLE T2,PPNMFD	; IF NOT FOUND - GET IT'S UFD
        TLNN    T5,-1           ;IF ALREADY HAVE PPN
        JRST PPNFND
        SKIPN   CACHE+1(T2)     ;AND NO USERNAME
        POPJ    P,              ;THEN NON-SKIP RETURN
PPNFND:	AOS T3,CACHEC		; INCR THE TIME LAST REF'D
	MOVEM T3,CACHE+3(T2)	; AND SAVE IT
        SKIPN   CACHE+1(T2)     ;NON-SKIP RETURN IF DON'T HAVE USERNAME
        POPJ    P,
	MOVSI T1,(POINT 6,0)	; RETURN POINTER
	HRRI T1,CACHE+1(T2)	; EFFECTIVE ADDRESS
	AOS (P)			; SKIP RETURN
	POPJ P,

PPNMFD:	; NOT IN CACHE - HAVE TO DO DISK IO
        TLNE    T5,-1           ;PPN ALREADY KNOWN?
        JRST    PPNMF2          ;YES
	MOVE	T1,[MFDDIR+3,,MFDDIR+4]
	BLT	T1,MFDDIR+7
        MOVE    T1,[XWD 1,1]
        MOVEM   T1,MFDX
        MOVEM   T1,MFDX+3
        MOVSI   T1,'UFD'
        MOVEM   T1,MFDX+1
        LOOKUP  MFD,MFDX        ;LOOKUP MFD SO CAN DO DIR SEARCH ON IT
        HRRZ    T1,T5
        HRRM    T5,MFDDIR+1     ;PPN IS RT. HALF OF FILENAME FOR .CHUFD
        MOVE    T2,[.CHUFD,,MFD]        ;SEARCH MFD FOR ###<UUN>.UFD
PPNMF4: CHANIO  T2,MFDDIR
        POPJ    P,
        HRRZ    T2,MFDBLK       ;GET UUN
        CAME    T5,T2           ;DOES IT MATCH?
        JRST    PPNMF4          ;NO,CONTINUE SEARCH
        MOVE    T1,MFDBLK
PPNMF2:	MOVE T2,T4		; POINT TO OLDEST ENTRY
	MOVE T4,[MFDX1,,MFDX]	; SETUP LOOKUP BLOCK
	BLT T4,MFDX+MFDX1S
	MOVEM T1,MFDX+2		; SAVE PPN IN LOOKUP BLOCK
	LOOKUP MFD,MFDX		; TRY
        JRST    PPNMF3
	CLOSE MFD,		; BINGO
	MOVEM T1,CACHE(T2)	; SAVE PPN IN CACHE
	DMOVE T3,MFDX+EXTUNM	; GET USERNAME
	DMOVEM T3,CACHE+1(T2)	; SAVE IN CACHE
	JRST PPNFND		; EXIT AND UPDATE REF TIME
PPNMF3: TLNE    T5,-1           ;IF ONLY UUN WAS KNOWN
        POPJ    P,
        MOVEM   T1,CACHE(T2)    ;THEN SAVE PPN IN CACHE
        SETZM   CACHE+1(T2)
        JRST    PPNFND
	DEFINE SASC (A)
<IRP A,<ASCIZ / A/>>

LICTB:	SASC <WC,RC,OP,SY,GD,TD,ST,HF,JL,AC,XC,F1,F2,F3>

LICWTB:	0
	SASC <WP,WF,WA>

LICRTB:	0
	SASC <RP,RF,RA>

STAB:	0
	SASC <NDL>
	0
	SASC <NFS,ABC>
	0
	0
	0
	0
	SASC <SCE,HWE,HRE>
	0
	0
	SASC <BFA,CRH>
	0
	SASC <BDA>
ERRTBL:	[ASCIZ / **CAN'T READ FILE**/]
	[ASCIZ / **CAN'T READ FILE**/]
	[ASCIZ / **PROTECTED**/]
	[ASCIZ / **FILE BEING MODIFIED**/]
	[ASCIZ / **NOT RENAMED - DESTINATION FILE ALREADY EXISTS**/]
	[ASCIZ / **SYSTEM ERROR**/]
	[ASCIZ / **DEVICE ERROR**/]
	REPEAT 5,<[ASCIZ / **SYSTEM ERROR**/]>
	[ASCIZ / **NO ROOM**/]
	[ASCIZ / **WRITE LOCK**/]
	REPEAT 3,<[ASCIZ / **SYSTEM ERROR**/]>
DNPRNT:	DMOVE T3,T3SAVE		; RESTORE SAVED AC'S
	TRNE FL2,RENFLG
	PUSHJ P,DOREN	;DO RENAME
	TRNE FL2,DELFLG!DELBAD
	PUSHJ P,DODEL	;DO DELETE
	TRNE FL2,RDLPRT!NOPRNF
	TLNE FL,DCDFLG
	PUSHJ P,CRLF
	SETZM 1(T4)
	CLOSE UFD,0	;JUST TO MAKE SURE
	JRST PRL1

PPRT:	OCHRI " "	;LEADING BLANK
	HLRZ T2,BLNKN+1
	CAIE T2,'UFD'	;IS IT UFD?
	JRST PPRT2	;NO
	TRZE T1,400	;PROTECT PROTECT FIELD
	OCHRIS "T"	;PERMITTED (WILL SKIP)
	OCHRI "-"
	TRZE T1,200
	OCHRIS "C"
	OCHRI "-"
	TRZE T1,100
	OCHRIS "R"
	OCHRI "-"
PPRTC:	LSH T1,3	;SET UP FOR NEXT TIME
	POPJ P,

PPRT2:	LDB T2,[POINT 3,T1,29]
	OASC PTBLA(T2)
	JRST PPRTC

PTBLA:	ASCIZ /ALL/
	ASCIZ /CP /
	ASCIZ /UPD/
	ASCIZ /AP /
	ASCIZ /RD /
	ASCIZ /RUN/
	ASCIZ /LK /
	ASCIZ /NO /
DOREN:	PUSHJ P,DECIDE	;ASK YES/NO IF /WAIT MODE
	 JRST NOREN	;HE SAID NO!
	TLNE FL,RPRO
	JRST NOREN	;FILE IS READ PROTECTED
	SKIPE T1,NEWNAM+3
	MOVEM T1,FILLK+1	;SET UP NEW PPN
	MOVE T1,FILLK+2
	ANDCM T1,RNMSK
	IOR T1,NEWNAM	;SET UP NEW FILE NAME TAKING CARE OF WILDCARD STUFF
	MOVEM T1,FILLK+2
	HLLZ T1,FILLK+3
	ANDCM T1,RNXMSK
	IOR T1,NEWNAM+1	;DITTO FOR EXTENSION
	HLLM T1,FILLK+3
	SKIPE T1,SVPRT
	DPB T1,[POINT 9,FILLK+4,8]	;SET UP PROTECTION IF GIVEN
	MOVSI	T1,(1B4)	;MAKE SURE THAT DUMPED
	ANDCAM	T1,FILLK+14	; BIT GETS CLEARED
	RENAME UFD,FILLK	;RENAME IT!!
	 JRST DRFAIL
	POPJ P,

NOREN:	OASC [ASCIZ/ **NOT RENAMED**/]
	POPJ P,


DODEL:	PUSHJ P,DECIDE	;ASK YES/NO IF NECESSARY
	 JRST NODEL	;NO NO!
	TLNE FL,RPRO
	JRST NODEL	;FILE IS READ PROTECTED

	TLNE FL,FILERR	;DOES FILE HAVE RIB ERROR?
	JRST DODEL2	;YES
	TRNN FL2,DELFLG	;NO, IS DELETE FLAG ON?
	JRST NODEL	;NO, DON'T DELETE
	JRST DELTYP	;YES, DO DELETE
DODEL2:	TRNN FL2,DELBAD	;IS DELETE BAD FILE FLAG ON?
	JRST NODEL	;NO
	
DELCAN:	SETZM FILLK+2
	RENAME UFD,FILLK	;DELETE IT!
	 JRST DRFAIL
	POPJ P,

DELTYP:	TRNN	FL2,DELTMP	;.TMP DELETE?
	JRST	DELCAN		;NO
	MOVE T1,FILLK+1	; SAVE PPN
	MOVEM T1,FILLK+6	; IN .RBVER
	MOVE T1,FILLK+2	; SAVE NAME
	MOVEM T1,FILLK+7	; IN .RBFUT
	MOVE T1,FILLK+3	; SAVE EXTENSION
	MOVEM T1,FILLK+13	; IN .RBFT1
	SETZM FILLK+1	; FORCE INTO GFD'd DIRECTORY
	HLLZ T1,FILLK+3	; ADD IN THE EXTENSION
	ADDM T1,FILLK+2
	MSTIME T1,	; TIME OF DAY FOR RANDOMNESS
	ADDM T1,FILLK+2
	MOVE T1,[1,,1]	; MAKE SURE IT'S A LEGAL FILE NAME
	IORM T1,FILLK+2
	MOVSI T1,'TMP'	; FORCE EXT TO ".TMP"
	HLLM T1,FILLK+3
DODEL1:	RENAME UFD,FILLK	;DELETE IT! (SORT OF)
	 SKIPA
	  POPJ P,

	; DIDN'T WORK 'CAUSE IT'S ALREADY THERE - ADD ONE, TRY AGAIN

	HRRZ T1,FILLK+3	; GET ERROR CODE
	CAIE T1,4	; "DEST. FILE ALREADY EXISTS"
	  JRST DRFAIL	;   NO - DIDN'T WORK FOR OTHER REASON
	AOS FILLK+2	; TRY AGAIN WITH DIFFERENT NAME
	JRST DODEL1

NODEL:	OASC [ASCIZ/ **NOT DELETED**/]
	POPJ P,
;PERFORMS APPROPRIATE ACTION ON A Y, N, OR G RESPONSE FOR A
;FILE.  Y MEANS DO IT, N MEANS DON'T DO IT, AND G MEANS DO IT AND
;DON'T GET CONFIRMATION ON SUBSEQUENT FILES.

DECIDE:	TLNN FL,DCDFLG	;ARE WE IN /WAIT MODE?
	JRST CPOPJ1	;NO, JUST LIKE A Y RESPONSE (SKIP RETURN)
	OASC [ASCIZ/ ? /]
	OUTPUT OUTP,	;ASK THE QUESTION
	PUSHJ P,YESNO	;GET THE RESPONSE CHARACTER
	 POPJ P,	;"N" RETURN NO SKIP TO ABORT IT
	 TLZ FL,DCDFLG	;"G" TURN OFF /WAIT AND TREAT LIKE "Y"
	 JRST CPOPJ1	;"Y" SKIP RETURN TO DO IT


;YESNO GETS A SINGLE CHARACTER FROM THE TERMINAL.  RETURNS NO SKIP
;IF "N", SKIP 1 IF "G", SKIP 2 IF "Y".  DOESN'T LIKE ANYTHING ELSE.

YESNO:	CLRBFI
	INCHRW T1	;GET THE CHARACTER
	CLRBFI
	CAIL T1,"A"+40
	SUBI T1,40	;CONVERT LOWER CASE TO UPPER
	CAIN T1,"Y"
	JRST CPOPJ2	;"Y" SKIP 2
	CAIN T1,"G"
	JRST CPOPJ1	;"G" SKIP 1
	CAIN T1,"N"
	JRST CPOPJ	;"N" NO SKIP
	OASC [ASCIZ/  WHAT? /]
	OUTPUT OUTP,
	JRST YESNO

CPOPJ2:	AOS (P)
CPOPJ1:	AOS (P)
CPOPJ:	POPJ P,
DRFAIL:	TRNE FL2,RDLPRT!NOPRNF
	TLNE FL,DCDFLG
	JRST DRFL1
	PUSHJ P,CRLF
	MOVEI GOP,0
	MOVE CS,1(T4)
	PUSHJ P,OUTSX2
	ADDI GOP,10
	HLLZ CS,2(T4)
	JUMPE CS,DRFL2
	PUSHJ P,SETSP
	PUSHJ P,OUTSX2
DRFL2:	ADDI GOP,4
	PUSHJ P,SETSP
DRFL1:	HRRZ T1,FILLK+3
	OASC @ERRTBL(T1)
	POPJ P,
SSRT:	MOVE T2,3(T1)
	CAMN T2,3(T4)
	JRST TMSRT	;SAME, SORT ON OTHER INFO
	CAMG T2,3(T4)
	JRST BIG	;SORT LARGEST FIRST
	JRST SMALL

UFDPR:	HLRZ T1,1(T4)
	OOCT
	OCHRI ","
	HRRZ T1,1(T4)
	OOCT
	OASC [ASCIZ /.UFD/]
	ADDI GOP,^D12
	JRST RTUFD

OUTAS1:	SKIPA CS,40
CRLF:	MOVEI CS,[ASCIZ /
/]
OUTASC:	HRLI CS,(<POINT 7,0>)
OUTA1:	ILDB C,CS
	JUMPE C,CPOPJ
	PUSHJ P,OCHR
	JRST OUTA1

OCHS:	AOS (P)
OCH1:	HRRZ C,40
OCHR:	SUBI GOP,1
	SOSG OBUF+2
	OUTPUT OUTP,0
	IDPB C,OBUF+1
	POPJ P,

SPACES:	OCHRI " "
	SOJG T2,.-1
	POPJ P,

SETSP:	JUMPLE GOP,CPOPJ
	OCHRI " "
	JRST SETSP

UUOH:	LDB UAC1,[POINT 9,40,8]	;GET UUO
	JRST @UUDSP-1(UAC1)

UUDSP:	JRST OUTAS1
	JRST OUTDC
	JRST OUTOC
	JRST OCH1
	JSR OCHS	;OUTPUT CHR AND SKIP

OUTDC:	SKIPA UAC1,[^D10]
OUTOC:	MOVEI UAC1,10
	LDB UAC2,[POINT 4,40,12]
OUTRAD:	IDIV T1,UAC1
	HRLM T2,(P)
	SUBI UAC2,1
	SKIPE T1
	PUSHJ P,OUTRAD
	JUMPLE UAC2,RADHD
	PUSHJ P,OCH1
	SOJG UAC2,.-1
RADHD:	HLRZ C,(P)
	ADDI C,"0"
	JRST OCHR
OUTTIM:	IDIVI T1,^D60	;CONVERT TO MIN AND SEC
	IMULI T1,^D100	;RECONVERT TO A NUMBER
	ADD T1,T2
	ODEC 4," "
	POPJ P,

OUTSX2:	MOVEI C,0
	LSHC C,6
	ADDI C,40
	PUSHJ P,OCHR
	JUMPN CS,OUTSX2
	POPJ P,

ODATE:	IDIVI T1,^D31
	PUSH P,T1
	MOVEI T1,1(T2)
	ODEC 2," "
	POP P,T1
	IDIVI T1,^D12
	PUSH P,T1
	OASC @MONTAB(T2)
	POP P,T1
	ADDI T1,^D64
	ODEC
	POPJ P,


	DEFINE ZOT (A)
<IRP A,<[ASCIZ /-A-/]>>

MONTAB:	ZOT <JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC>

ALGON:	SKIPN OCNT
	JRST	[CLOSE OUTP,0
		OUTSTR [ASCIZ /
NO FILES SATISFYING REQUEST
/]
		JRST ENDPAS]
ALGONP:	TLNN FL2,TOTAL	;DOES HE WANT TOTALS
	JRST ENDPAS	;NO
	MOVE T1,OTOT	;USE TOTALS FROM FILES PRINTED
	MOVEM T1,TOTSIZ
	MOVE T1,OCNT
	MOVEM T1,NUMFIL
ALGONT:	PUSHJ P,CRLF
	;TLNE FL,DTADIR	;WHAT KIND?   (10/76 AAA)
	;JRST DTATOT	;(10/76 AAA)
	TLNE FL,TMPDIR
	JRST TMPTOT
	OASC [ASCIZ /TOTAL PAGES /]
	MOVE T1,TOTSIZ
	LSH T1,-2	;-SCALE BLOCKS TO PAGES. 29 OCT 76 AAA
	ODEC
	OASC [ASCIZ / FILES /]
	MOVE T1,NUMFIL
ENDPP:	ODEC
	PUSHJ P,CRLF
ENDPAS:	RELEASE OUTP,0
	HLRZ T1,JOBSA
	MOVEM T1,JOBFF
	ANDI FL,RPGSW!TMPFL!INISW!INIDN	;DUMP ALL BUT GOOD ONES
	TRNN	FL,RPGSW!TMPFL
	JRST	LPRET
	JRST RPGRET
COMMENT !	(10/76 AAA)
DTATOT:	OASC [ASCIZ /FREE BLOCKS /]
	MOVEI T1,1076	;MAK NUMBER
	SUB T1,TOTSIZ	;- USED
	ODEC
	OASC [ASCIZ / FILES /]
	MOVEI T1,^D22
	SUB T1,NUMFIL
	JRST ENDPP
;END OF REMOVED CODE 10/76 AAA. !

TMPTOT:	OASC [ASCIZ /WORDS FREE /]
	MOVEI T1,0
	TMPCOR T1,	;GET SPACE
	JFCL
	JRST ENDPP

RELOC
VAR
END STPT
    !Z,‚