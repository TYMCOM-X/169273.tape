ENTRY CONSTR;
TITLE CONSTR
	SEARCH SLBPRE
	PREAMBLE
CONSTR: ; simple string procedure constr( integer bytptr, count );
define bytptr,<-2(P)>
define count,<-1(P)>
EXTERNAL X33;
;	returns a string of length count that first character is 
;	the character ILDB(bytepointer).  The string is not garbage
;	collected (SAIL thinks of it as a constant string), and it
;	will change any time the corresponding bytes of information
;	in the area pointed to change.  Hence, the string (and all
;	substrings taken from it) is extremely unstable, so be very
;	careful when using this procedure.  Causes a continueable
;	USERERR if the bytepointer or count are invalid.
;
	SKIPL	LPSA,COUNT;	! verify string length not ridiculous;
	 CAIL	LPSA,377777;
	  JRST	[ERR. [Asciz "CONSTR: bad count"]; ! ridiculous count;
		 MOVEI LPSA,0
		 JRST  RETURN]
	JUMPE	LPSA,RETURN;	! 0 count, return null string;
	MOVE	TEMP,BYTPTR;	! now check bytepointer;
	TDNN	TEMP,[XWD -1,-20];	! check for very nasty values;
	 JRST	BADPTR;
	TLCE	TEMP,-1;
	 TLCN	TEMP,-1;
	  HRLI	TEMP,440700;
	TLNN	TEMP,77;	! make sure no special addressing;
	 TLNN	TEMP,7700;	! make sure real byte size;
BADPTR:	  JRST	[ERR. [Asciz "CONSTR: bad bytptr"]
		 MOVEI LPSA,0
		 JRST  RETURN]
RETURN:	PUSH	SP,LPSA;	! count goes first;
	PUSH	SP,TEMP;	! then bytepointer (maybe corrected);
	SUB	P,X33;			! unwind arguments, and ;
	JRST	@3(P);		! return  (string);
PRGEND	
ENTRY DYNSTR;
TITLE DYNSTR
	SEARCH SLBPRE
	PREAMBLE
EXTERNAL STMAKE
;obsolete function, made a dynamic string (STMAKE is now preferred)
DYNSTR:	;string procedure dynstr( integer bytepointer, count );
DEFINE	BYTPTR,<-2(P)>
DEFINE	COUNT,<-1(P)>
	;returns a 7-bit byte dynamic string EQU to source (if size ok)
	HLRE	TEMP,BYTPTR	;check for "ASCIZ" indicators
	JUMPE	TEMP,.+2	;0,,address
	AOJN	TEMP,LHOK	;or -1,,address
	MOVEI	TEMP,440700
	HRLM	TEMP,BYTPTR
LHOK:	PUSH	P,(P)		;convert to call to STMAKE
	MOVEI	TEMP,7		;by adding a "byte size" argument
	MOVEM	-1(P)
	JRST	STMAKE
PRGEND	
ENTRY ASZSTR;
TITLE ASZSTR
	SEARCH SLBPRE
	PREAMBLE
EXTERNAL STMAKE
ASZSTR:	;string procedure aszstr( integer bytepointer );
;makes a string up to but not including a 0 byte from the byte pointer
DEFINE	BYTPTR,<-1(P)>
	MOVE	LPSA,BYTPTR	;check for "ASCIZ" indicators
	TLCE	LPSA,-1		;0,,address
	 TLCN	LPSA,-1		;or -1,,address
	  HRLI	LPSA,440700	;and make like an ASCIZ
	MOVEM	LPSA,BYTPTR
	MOVEI	USER,0
REDLUP:	ILDB	TEMP,LPSA
	CAIE	TEMP,0
	 AOJA	USER,REDLUP
	;construct call to STMAKE [bptr,len,wid]
	PUSH	P,[0]		;stack= bptr,return address,[0]
	PUSH	P,-1(P)		;stack= bptr,return address,[0],ret addr
	MOVEM	USER,-2(P)	;stack= bptr,len,[0],return address
	JRST	STMAKE
PRGEND	
ENTRY SUBEQU;
TITLE SUBEQU
	SEARCH SLBPRE
	PREAMBLE
SUBEQU: ; simple integer procedure subequ( string target, source );
EXTERNAL X44;
;	Returns the position of target in source or 0 if no match.
;	if subequ(t,s) then equ( t, s[subequ(t,s) for length(t)] ).
;	A null target NEVER produces a match.
; like:   if length(target)=0 then return(0)
;	  for i_ 1 step 1 until length(source)
;		do if equ(target,source[i for length(target)])
;		   then return(i)
;	  return(0)
;
;A:	source chars left
B__2	;target characters left
C__3	;current target character
;LPSA: source byte pointer
;TEMP: char from source
;USER:	;target byte pointer (in lup2)
;	PUSH	P,B	;save mangled ACs (when becomes SAFE)
;	PUSH	P,C	;save mangled ACs (when becomes SAFE)
	HRRZS	B,-3(SP);	! get target count;
	HRRZS	A,-1(SP);	! and source count;
;		! also: make simple numbers for lengths, constant strs;
;		! for minimal interrupt problems (since does ILDB);
	CAIG	B,0(A);		! longer target than source;
	 SOSGE	B,-3(SP);	! or null length target;
	  JRST	FAIL;		! are both considered mismatches;
;! Now we know that both byte pointers are good (ie at least 1-ch strs);
	IBP	-2(SP);		! point to first target character;
	SUBI	A,0(B);		! figure out how many matches to try;
;				! 1 + length(source)-length(target);
;! Now we are set.  Run along source looking for 1-char match;
LUP1E:	MOVE	LPSA,0(SP);	! get byte pointer to source;
	LDB	C,-2(SP);	! and first target character;
LUP1:	ILDB	TEMP,LPSA;	! lead character of source;
	CAME	TEMP,C;		! stop going if match;
	 SOJG	A,LUP1;		! or we can no longer match;

	JUMPE	A,FAIL;		! if we stopped on count, all done;
	JUMPE	B,GOOD1;	! if the target was only 1 char long, ;
;				!  then we have a match;
;! first character of target matches, how about the others;
	MOVE	USER,-2(SP);	! get private byte pointer for target;
	MOVEM	LPSA,0(SP);	! and save source pointer to match+;

LUP2:	ILDB	TEMP,LPSA;	! pull one from each;
	ILDB	C,USER;
	CAMN	C,TEMP;		! and loop until they dont match;
	 SOJG	B,LUP2;		! or we have exhausted the target;

	JUMPE	B,GOOD;		! if target exhausted, it was a match;
	MOVE	B,-3(SP);	! otherwise, reset the target count;
	SOJG	A,LUP1E;	! And go for next try on source;
;				! unless no more (we fail totally);
FAIL:;	MOVEI	A,0;		! return zero (failure) (A always 0);
RETURN:
;	POP	P,C	;restore mangled ACs (when becomes SAFE)
;	POP	P,B	;restore mangled ACs (when becomes SAFE)
	SUB	SP,X44;		! drop the argument strings;
	POPJ	P,;		! and leave;
GOOD:;				! match A=source chars trailing match;
	ADD	A,-3(SP);	! +l(target)-1= l(source[* to inf]);
GOOD1:	SUB	A,-1(SP);	! now get leading characters;
	MOVN	A,A;		! convert to positive;
	AOJA	A,RETURN;	! (+1 since the first character is #1);
PRGEND
ENTRY STRSTF;
TITLE STRSTF
	SEARCH SLBPRE
	PREAMBLE
STRSTF: ; simple integer procedure strstf( INTEGER byptr; string str
					;boolean nonull(false) );
DEFINE BYPTR,<-2(P)>
DEFINE NONULL,<-1(P)>
EXTERNAL X22,X33
;	Deposits str into byptr via idpb.  If nonull is false, (which
;	means that a trailing null is desired), then a null character
;	(ASCII 0) is deposited past the last character in the string.
;	Returns a bytepointer to the last character deposited from the
;	string (not the trailing null), or 0 if byptr is invalid.
;	This is useful for setting up JSYS calls that want either ASCIZ
;	strings, or bytepointer and updated bytepointer values.  The
;	return value is precisely the byte pointer that you want to
;	use if you want to deposit another string after the first one.
;	That is: strstf( strstf( byp, str1 ), str2 ) is the same as
;	strstf( byp, str1 & str2 ), except the concatenation is not 
;	actually performed.
;
	MOVE	A,BYPTR;	! get start byte pointer;
	TLCE	A,-1;		! apply 0 and -1 defaults;
	 TLCN	A,-1;
	  HRLI	A,(<POINT 7,0,-1>)
	MOVE	LPSA,(SP);	! str's byte pointer;
	HRRZ	USER,-1(SP);	! str's count;
	JRST	MOVCHK;		! and go to check in loop;

MOVLUP:	ILDB	TEMP,LPSA;	! pick up byte from string;
	IDPB	TEMP,A;		! place it in output area;
MOVCHK:	SOJGE	USER,MOVLUP;	! next byte (string still has chars);

	MOVEI	TEMP,0;		! get null byte to deposit after string;
	MOVE	LPSA,A;		! get a copy of the now-updated byte;
	SKIPN	NONULL;		! verify null byte to be deposited;
	 IDPB	TEMP,LPSA;	! and deposit it (if called for);
;				! Note: AC1 points to last REAL byte;
	SUB	SP,X22;
	SUB	P,X33;
	JRST	@3(P);
PRGEND
ENTRY FLOOR;
TITLE FLOOR
	SEARCH SLBPRE
	PREAMBLE
FLOOR: ;internal simple Real procedure floor( Real val );
DEFINE VAL,<-1(P)>
EXTERNAL X22,FINT
; approximation to the mathematical "FLOOR" function.
;WARNING: No floor function can be correct for floating point --
;   This one violates the following identity:   floor(x)+1 = floor(x+1)
;   In particular, x-floor(x) may be equal to 1 (try x=-(2^-60))
;   [the alternative is to violate "floor(x) LEQ x"]
;
;	begin	real intp;
;	intp_ fint(val);
;	return( if intp leq val then intp else intp-1 ); 
;	end;
	PUSH	P,VAL	;push the arg off and call FINT
	PUSHJ	P,FINT
	CAMLE	A,VAL
	 FSBRI	A,(1.0)
	SUB	P,X22
	JRST	@3(P)
PRGEND
ENTRY DFLOOR;
TITLE DFLOOR
	SEARCH SLBPRE
	PREAMBLE
DFLOOR: ;internal simple long Real procedure dfloor( long Real val );
DEFINE VAL,<-2(P)>
DEFINE VAL.LO,<-1(P)>
EXTERNAL X33,DFINT
; approximation to the mathematical "FLOOR" function.
;WARNING: No floor function can be correct for floating point --
;   This one violates the following identity:   floor(x)+1 = floor(x+1)
;   In particular, x-dfloor(x) may be equal to 1 (try x=-(2^-60))
;   [the alternative is to violate "floor(x) LEQ x"]
;
;	begin	long real intp;
;	intp_ dfint(val);
;	return( if intp leq val then intp else intp-1 ); 
;	end;
	PUSH	P,VAL	;push the arg off and call DFINT
	PUSH	P,VAL
	PUSHJ	P,DFINT
	CAMN	A,VAL
	 CAMG	A+1,VAL.LO
	  CAMLE	A,VAL
	   DFSB	A,[1.0
		   0]
	SUB	P,X33
	JRST	@3(P)
PRGEND
ENTRY CEIL;
TITLE	CEIL
	SEARCH SLBPRE
	PREAMBLE
CEIL: ;internal simple Real procedure ceil( Real val );
DEFINE VAL,<-1(P)>
EXTERNAL X22,FINT
; approximation to the mathematical "CEILING" function.
;WARNING: No ceiling function can be correct for floating point --
;   This one violates the identity:   ceiling(x)+1 = ceiling(x+1)
;   In particular, ceil(x)-x may be equal to 1 (try x=2^-60)
;   [the alternative is to violate "ceiling(x) GEQ x"]
;	begin	real intp;
;	intp_ fint(val);
;	return( if intp geq val then intp else intp+1 ); 
;	end;
;
	PUSH	P,VAL	;push the arg off and call FINT
	PUSHJ	P,FINT
	CAMGE	A,VAL
	 FADRI	A,(1.0)
	SUB	P,X22
	JRST	@2(P)
PRGEND
ENTRY DCEIL;
TITLE	DCEIL
	SEARCH SLBPRE
	PREAMBLE
DCEIL: ;internal simple long Real procedure dceil( long Real val );
DEFINE VAL,<-2(P)>
DEFINE VAL.LO,<-1(P)>
EXTERNAL X33,DFINT
; approximation to the mathematical "CEILING" function.
;WARNING: No ceiling function can be correct for floating point --
;   This one violates the identity:   ceiling(x)+1 = ceiling(x+1)
;   In particular, dceil(x)-x may be equal to 1 (try x=2^-60)
;   [the alternative is to violate "ceiling(x) GEQ x"]
;	begin	long real intp;
;	intp_ dfint(val);
;	return( if intp geq val then intp else intp+1 ); 
;	end;
;
	PUSH	P,VAL	;push the arg off and call DFINT
	PUSH	P,VAL
	PUSHJ	P,DFINT
	CAMN	A,VAL
	 CAML	A+1,VAL.LO
	  CAMGE	A,VAL
	   DFAD	A,[1.0
		   0]
	SUB	P,X33
	JRST	@3(P)
PRGEND
ENTRY SETXP;
TITLE SETXP
	SEARCH SLBPRE
	PREAMBLE
SETXP: ;internal real simple procedure setxp( real x; integer exp );
DEFINE X,<-2(P)>
DEFINE EXP,<-1(P)>
EXTERNAL X33
	MOVM	A,X;
	JUMPLE	A,RETURN;
	MOVE	TEMP,EXP;
	ADDI	TEMP,=128;
	DPB	TEMP,[POINT 8,A,8];
	SKIPGE	X;
	 MOVN	A,A; 
RETURN:
	SUB	P,X33
	JRST	@3(P)
PRGEND
ENTRY DSETXP;
TITLE DSETXP
	SEARCH SLBPRE
	PREAMBLE
DSETXP: ;internal long real procedure setxp( long real x; integer exp );
DEFINE X,<-3(P)>
DEFINE EXP,<-1(P)>
EXTERNAL X44
	DMOVE	A,X;
	CAIGE	A,0;
	 DMOVN	A,A; 
	JUMPLE	A,RETURN;
	MOVE	A,EXP;
	ADDI	TEMP,=128;
	DPB	TEMP,[POINT 8,A,8];
	SKIPGE	X;
	 DMOVN	A,A; 
RETURN:
	SUB	P,X44;
	JRST	@4(P);
PRGEND
ENTRY INTXP;
TITLE INTXP
	SEARCH SLBPRE
	PREAMBLE
INTXP: ;internal integer simple procedure intxp( real x );
DEFINE X,<-1(P)>
EXTERNAL X22
	MOVM	A,X;
	LSH	A,-=27;
	SUBI	A,=128;
	SUB	P,X22;
	JRST	@2(P);
PRGEND
ENTRY DINTXP;
TITLE DINTXP
	SEARCH SLBPRE
	PREAMBLE
DINTXP: ;internal integer simple procedure dintxp( real x );
DEFINE X,<-2(P)>
EXTERNAL X33
	MOVM	A,X;
	LSH	A,-=27;
	SUBI	A,=128;
	SUB	P,X33;
	JRST	@3(P);
PRGEND
ENTRY EPSLN;
TITLE EPSLN
	SEARCH SLBPRE
	PREAMBLE
EPSLN: ; internal real simple procedure epsln( real x );
DEFINE X,<-1(P)>
EXTERNAL X22
	MOVM	A,X;
	LSH	A,-=26;
	SUBI	A,=26*2;
	CAIG	A,0;
	 MOVEI	A,1;
	ROT	A,-=10;
	SUB	P,X22;
	JRST	@2(P);
PRGEND
ENTRY DEPSLN;
TITLE DEPSLN
	SEARCH SLBPRE
	PREAMBLE
DEPSLN: ; internal real simple procedure depsln( long real x );
DEFINE X,<-2(P)>
EXTERNAL X33
	MOVM	A,X;
	LSH	A,-=26;
	SUBI	A,=61*2;
	CAIG	A,0;
	 MOVEI	A,1;
	ROT	A,-=10;
	SUB	P,X33;
	JRST	@3(P);
PRGEND
ENTRY FINT;
TITLE FINT
	SEARCH SLBPRE
	PREAMBLE
FINT: ; internal simple Real procedure fint( Real x );
DEFINE X,<-1(P)>
EXTERNAL X22
;	integer part function.  fint(x)+fract(x)=x (exactly).
;	Returns sign(x) * floor(abs(x)).
;	If x is negative, 
;		then this is the mathematical function "CEILING"
;		otherwise, this is the mathematical function "FLOOR"
;
;		label rtrn, rtrn0, rtrnArg;
; exponent GEQ '233 means all integer;
; exponent LEQ '200 means all fraction;
	MOVM	A,X;
	LDB	LPSA,[POINT 8,A,8];
	CAIG	LPSA,200;
	 TDZA	A,A;		! pure fraction;
	CAIL	LPSA,233;
	 JRST	SIGNGO;		! pure integer;
	MOVNI	LPSA,-233(LPSA);! count bits of fraction;
	MOVNI	TEMP,1;
	LSH	TEMP,(LPSA);	! set up mask to remove fraction;
	AND	A,TEMP;		! remove fractional part;
SIGNGO:	SKIPGE	X;		! restore sign;
	 MOVN	A,A;
RETURN:
	SUB	P,X22;
	JRST	@2(P);
PRGEND
ENTRY DFINT;
TITLE DFINT
	SEARCH SLBPRE
	PREAMBLE
DFINT: ; internal simple Long Real procedure dfint( Long Real x );
DEFINE X,<-2(P)>
EXTERNAL X33
;	integer part function.  dfint(x)+dfract(x)=x (exactly).
;	Returns sign(x) * floor(abs(x)).
;	If x is negative, 
;		then this is the mathematical function "CEILING"
;		otherwise, this is the mathematical function "FLOOR"
;
;		label rtrn, rtrn0, rtrnArg;
; exponent GEQ '276 means all integer;
; exponent LEQ '200 means all fraction;
	DMOVE	A,X;
	MOVM	TEMP,A;
	LDB	LPSA,[POINT 8,TEMP,8];
	CAIG	LPSA,200;
	 JRST	[SETZB A,A+1;	! pure fraction;
		 JRST  RETURN]
	CAIL	LPSA,276;
	 JRST	RETURN;		! pure integer;
	MOVNI	LPSA,-276(LPSA);! count bits of fraction;
	SETOB	TEMP,USER;
	ASHC	TEMP,(LPSA);	! set up mask to remove fraction;
	JUMPL	A,[DMOVN A,A;
		   AND	 A,TEMP;	! remove fractional part;
		   AND	 A+1,USER;	! remove fractional part;
		   DMOVN A,A;
		   JRST   RETURN] 
	AND	A,TEMP;		! remove fractional part;
	AND	A+1,USER;	! remove fractional part;
RETURN:
	SUB	P,X33;
	JRST	@3(P);
PRGEND
ENTRY FRACT;
TITLE FRACT
	SEARCH SLBPRE
	PREAMBLE
FRACT:	;internal simple Real procedure fract( Real x );
DEFINE X,<-1(P)>
EXTERNAL X22
;	fractional part function.  fint(x)+fract(x)=x (exactly).
;	The result lies strictly between -1 and 1.  If x is an
;	integer, the result is 0, otherwise the sign of the result
;	is the same as the sign of x.
;
; exponent GEQ '233 means all integer;
; exponent LEQ '200 means all fraction;
	MOVM	A,X;
	LDB	LPSA,[POINT 8,A,8];
	CAIL	LPSA,233;
	 TDZA	A,A;		! pure integer;
	CAIG	LPSA,200;
	 JRST	SIGNGO;		! pure integer;
	MOVNI	LPSA,-233(LPSA);! count bits of fraction;
	MOVNI	TEMP,1;
	LSH	TEMP,(LPSA);	! set up mask to remove fraction;
	AND	TEMP,A;		! put integer part (of abs) in temp;
	FSB	A,TEMP;		! result is original-integer part;
SIGNGO:	SKIPGE	X;		! restore sign;
	 MOVN	A,A;
RETURN:
	SUB	P,X22;
	JRST	@2(P);
PRGEND
ENTRY DFRACT;
TITLE DFRACT
	SEARCH SLBPRE
	PREAMBLE
DFRACT: ; internal simple Long Real procedure dfract( Long Real x );
DEFINE X,<-2(P)>
EXTERNAL X33
;	fractional part function.  dfint(x)+dfract(x)=x (exactly).
;	The result lies strictly between -1 and 1.  If x is an
;	integer, the result is 0, otherwise the sign of the result
;	is the same as the sign of x.
;
;		label rtrn, rtrn0, rtrnArg;
; exponent GEQ '276 means all integer;
; exponent LEQ '200 means all fraction;
	DMOVE	A,X;
	MOVM	TEMP,A;
	LDB	LPSA,[POINT 8,TEMP,8];
	CAIL	LPSA,276;
	 JRST	[SETZB A,A+1;	! pure integer;
		 JRST  RETURN]
	CAIG	LPSA,200;
	 JRST	RETURN;		! pure fraction;
	JUMPGE	A,ISPOS
	 DMOVN	A,A
ISPOS:
	MOVNI	LPSA,-276(LPSA);! count bits of fraction;
	SETOB	TEMP,USER;
	ASHC	TEMP,(LPSA);	! set up mask to remove fraction;
	AND	TEMP,A;		! get fractional part;
	AND	USER,A+1;	! get fractional part;
	DFSB	A,TEMP;		! and subtract it out;
	SKIPGE	X;		! finally get proper sign for result;
	 DMOVN	A,A;
RETURN:
	SUB	P,X33;
	JRST	@3(P);
PRGEND
ENTRY BFLOAT;
TITLE BFLOAT
	SEARCH SLBPRE
	PREAMBLE
BFLOAT: ; internal simple long Real procedure bfloat( integer H, L );
DEFINE H,<-2(P)>
DEFINE L,<-1(P)>	;must also be next to H (for DMOVEs)
EXTERNAL X33
;	returns a long real with the value of the two-word integer
;	(direct conversion).  The args are interpreted as a 71-bit 
;	value (sign of lowVal is ignored). 1B0,0 is not done properly?
;
	DMOVE	A,H;		! get absolute value;
	CAIGE	A,0;
	 DMOVN	A,A;
	TLNN	A,777000;	! can we just fiddle exponent?;
	 JRST	[TLON	A,276400; ! yes, make lsbit = 1, set msbit;
		  DFSB	A,[XWD 276400,0;! subtract msbit unless was on;
			   0];		! (this will normalize);
		 JRST	RETSGN];	! all done!;
	MOVE	LPSA,A;		! get count of leading zeros;
	JFFO	LPSA,.+1;	! into TEMP;
	LSH	A+1,1;		! make a continuous bit stream;
	LSHC	A,-=9(TEMP);	! make room for exponent;
	ROT	A+1,-1;		! wrd2: B0="round up", b1:b35=low part;
	MOVNI	LPSA,-276-=9(TEMP);	! decide value of exponent;
	DPB	LPSA,[POINT 9,A,8]; ! insert it;
	JUMPG	A+1,RETSGN;	! if no rounding needed, just get out.;
	MOVSI	LPSA,-=35-=27(LPSA);	! construct 1/2 LSB;
	LSH	LPSA,=9;
	TLO	LPSA,400;		! low word wont affect result;
	DFAD	A,LPSA;
RETSGN:	SKIPGE	H;
	 DMOVN	A,A;
	SUB	P,X33
	JRST	@3(P);
PRGEND
ENTRY DFLOAT;
TITLE DFLOAT
	SEARCH SLBPRE
	PREAMBLE
DFLOAT: ; internal simple long Real procedure dfloat( integer val );
DEFINE val,<-1(P)>
EXTERNAL X22
;	returns a long real with the value of the (signed) integer.
	MOVSI	A,276400;	! yes, make lsbit = 1, set msbit;
	MOVE	A+1,VAL;	! get value to be converted in low half
	TLZE	A+1,400000;	! if negative, its cool
	 HRRI	A,1;		! if positive add one to fix subtract 
	DFSB	A,[XWD 276400,1;! subtract away, magically ...;
			   0];		! (this will normalize);
	SUB	P,X22
	JRST	@2(P)
PRGEND
ENTRY DFIX;
TITLE DFIX
	SEARCH SLBPRE
	PREAMBLE
DFIX: ; internal simple integer procedure dfix( long Real val );
DEFINE val,<-2(P)>
DEFINE lo.val,<-1(P)>
EXTERNAL X33
;	returns an (signed) integer with the value of the long real .
;note: change to make AC safe
	dmove	a-1,val;	! get value to be converted, save ac 0
;	EXCH	A-1,VAL;	! get value to be converted, save ac 0
;	MOVE	A,LO.VAL;
	DFAD	A-1,[XWD 276400,1;! coerce so LSB = 1;
			   0];	! (this will normalize);
	CAMN	A-1,[XWD 276400,0];	! borrowed 1, set sign bit;
	 TLOA	A,400000
	CAMN	A-1,[XWD 276400,1];	! no borrow, value is correct;
	 JRST	ALLOK;		! result is correctremove the magic constant;
	HRLOI	A,377777;	! assume positive result;
	CAML	A-1,[XWD 276400,1];	! which wa the problem?
	 MOVSI	A,400000;	! negative result;
	ERR. 1,[ASCIZ "DFIX: magnitude of result is too large"]
ALLOK:
;note: change to make AC safe
;	MOVE	A-1,VAL;	! recover saved AC;
	SUB	P,X33
	JRST	@3(P)
PRGEND
ENTRY ENDLOP;
TITLE ENDLOP
	SEARCH SLBPRE
	PREAMBLE
ENDLOP: ;internal simple integer procedure endlop( reference string s );
DEFINE L.S,<-1(P)>
EXTERNAL X22
;	Returns the last character in the string, 0 if string null.
;	Removes the character from the string
	MOVE	TEMP,L.S	;get address of string
	HRRZ	A,-1(TEMP)	;get length
	JUMPE	A,RETURN	;return if null
	ADJUBP	A,(TEMP)	;advance to point at byte
	SOS	-1(TEMP)	;remove last byte
	LDB	A,A		;read last character
RETURN:	SUB	P,X22
	JRST	@2(P)
PRGEND
ENTRY ENDCHR;
TITLE ENDCHR
	SEARCH SLBPRE
	PREAMBLE
ENDCHR: ;internal simple integer procedure endchr( string s );
EXTERNAL X22
;	Returns the last character in the string, 0 if string null.
	HRRZ	A,-1(SP)	;get length
	JUMPE	A,RETURN	;return if null
	ADJUBP	A,(SP)		;advance to point at byte
	LDB	A,A		;read last character
RETURN:	SUB	SP,X22
	POPJ	P,
PRGEND
ENTRY FRMOPV;
TITLE FRMOPV
	SEARCH SLBPRE
	PREAMBLE
FRMOPV: ;internal simple integer procedure frmopv( integer frame,arg,op)
EXTERNAL .SKIP.,X44
DEFINE	frame,<-3(P)>
DEFINE	arg,<-2(P)>
DEFINE	op,<-1(P)>
;	Does a FRMOP monitor call, !SKIP! set true if skips.
	HRLZ	A,FRAME
	HRR	A,OP
	SETOM	.SKIP.
	FRMOP	A,ARG
	 SETZM	.SKIP.
	SUB	P,X44
	JRST	@4(P)
PRGEND
ENTRY FRMOPR;
TITLE FRMOPR
	SEARCH SLBPRE
	PREAMBLE
FRMOPR: ;internal simple integer procedure frmopr( integer frame;
;				reference integer arg; integer op );
EXTERNAL .SKIP.,X44
DEFINE	frame,<-3(P)>
DEFINE	arg,<@-2(P)>
DEFINE	op,<-1(P)>
;	Does a FRMOP monitor call, !SKIP! set true if skips.
	HRLZ	A,FRAME
	HRR	A,OP
	SETOM	.SKIP.
	FRMOP	A,ARG
	 SETZM	.SKIP.
	SUB	P,X44
	JRST	@4(P)
PRGEND
ENTRY OPFILE
TITLE OPFILE
	SEARCH SLBPRE
	PREAMBLE
OPFILE: ; simple integer procedure opfile( string FileName, options;
;				integer channel(-1) );
; gets file on channel in specified mode.
;	Always does OPEN, does LOOKUP, ENTER as necessary.
; options: Read, Write  New file, Supercede file, Old file, Version
; H (thawed, multi-user enter used), Base mode (old if exists, else
; create and use),  ###P protection (everyone,group,owner)
; Mapping access (must use for now)
;

acZ__0
;acA__1
;acB__2
;acC__3
;acD__4

EXTERNAL NOPOLL,$PDLOV,INCHWL,CVFIL,CVSIX,.SKIP.,X44

.CHCLS__1
 CL.ACS__10	;inhibit access update
.CHLK__4
.CHENT__5
.CHOPN__13
.CHMEN__41

.RBPPN__1
.RBNAM__2
.RBEXT__3
.RBPRV__4
	PUSH	P,NOPOLL	;save nopoll value
	SETOM	NOPOLL		;inhibit process switching
	ADD	P,[XWD 10,10]	;get local variables storage space
	JUMPL	P,.+2		;check for PDL exceeded
	 JSP	USER,$PDLOV	;it was, cause error

;now define some pointers to the p-stack
DEFINE	CHAN.DSP	<-12(P)>
DEFINE	POLL.DSP	<-10(P)>
DEFINE	OPNBLK.DSP	<-7(P)>
DEFINE	  MODEWD.DSP	<-7(P)>
DEFINE	  DEV.DSP	<-6(P)>
DEFINE	  BUF.DSP	<-5(P)>
DEFINE	LUKBLK.DSP	<-4(P)>

; first translate option string into option bits
; bits left in acD
	HRRZ	A,-1(SP)	;pick up length of options
	MOVE	B,(SP)		;and byte pointer
	SETZ	D,		;Start with a clear options word
NEWOPT:	SETZ	ACZ,		;mark "no number"
MORDIG:	SOJL	A,OPTSET	;no more options to set...
	ILDB	C,B		;get next character
	CAIL	C,"0"
	 CAILE	C,"7"		;is it a number?
	  JRST	NUMDUN		;no, number collected try for option
	LSH	ACZ,3		;make room for digit
	IORI	-"0"(C)		;and put it in
	JRST	MORDIG		;and go get more data
NUMDUN:	TRZ	C,40		;force option to upper case
	MOVEI	TEMP,1
	LSH	TEMP,-"A"(C)	;get option bit & check for validity
; valid options:	zy xWV utS ,, RqP ONM lkj iHg fed cBa
	TDNN	TEMP,[XWD 031,570202]
	 JRST	SWTERR		;option invalid, complain and leave
	IORM	TEMP,D		;mark it requested in the options word
	CAIN	C,"P"		;protection specified?
	 JRST	[LSH	ACZ,=27	;protection code in top 9 bits
		 IOR	D,ACZ	;put bits in saved option word
		 JRST	NEWOPT]
;put any number-valued options here
	JUMPE	ACZ,NEWOPT	;none of the option bits take an arg
SWTERR:	ERR.	1,[ASCIZ /OPFILE: bad option/]	;1=>continueable error
	JRST	ARGERR		;return error code in switch parse (-1)

; now translate option bits into a Enter,Lookup,Menter/enter sequence
; option bits obtained from acD, results placed in acA,
; acZ used as a temporary (which must start out as 0)
; resulting ELM spec (masks below)
DEFINE .FIELD & (nam,mask,len,endbit) {
	DEFINE P.&nam(x) {POINT len,x,endbit}
	DEFINE M.&nam(x) {mask}
}
.FIELD(PROT,777000,9,26)	;protection to use for file
.FIELD(MODE,000740,4,30)	;file data mode to use (only 17 for now)
.FIELD(ENT1,000030,2,32)	;initial enter
;				0=don't, 1=normal, 2=supercede, 3=create
.FIELD(LOOK,000006,2,34)	;lookup
;				0=return, 
;				1=close-lookup,
;				2=close-lookup[fail:create,close,lookup]
;				3=close-lookup-return
.FIELD(MENT,000001,1,35)	;Menter/enter (always post-lookup)
;				0=enter, 1=multiple-enter(.CHMEN)

OPTSET:	JUMPN	ACZ,SWTERR	;trailing number
	TRNE	D,010000	;this only works for "mapped" files
	 TDNN	D,[XWD 020,400000]	;have to read or write
	  JRST	SWTERR		;ah well, he didn't say enough
;this could have a lot more checking at this point
	MOVEI	ACZ,740		;clear flag bits, set dump mode

; note: "Old" and "Read" bits are actually ignored here for now
;	(their effect happens, though by default) --don't tell
	TDNE	D,[XWD 11,020000] ;need initial enter [V,S,or N]
	 JRST	[TLNE	D,10	;Version update (create or supercede)
		  TRO	ACZ,10
		 TLNE	D,1	;Supercede only
		  TRO	ACZ,20
		 TRNE	D,020000;New file
		  TRO	ACZ,30
		TRNE	D,000202 ;if multiple access or Base mode
		  JRST	.+1	;then we will need to set up a lookup
		 JRST	ELMSET]	;otherwise do protection and done
	TRO	ACZ,02		;assume normal lookup
	TRNE	D,2		;unless Base file (best mode)
	 TRC	ACZ,06		;normal bit off, special lookup on
	TLNN	D,20		;if no write access desired,
	 TRO	ACZ,06		;then make lookup return when done
	TRNE	D,200		;if H (thawed=multiple access)
	 TRO	ACZ,01		;use .CHMEN, not enter for final enter

ELMSET:	HLRZ	A,D		;get protection he specified
	TRNN	D,100000	;was this really from user?
	 HRRZI	A,047000	;no, use default = ALL,RD,NO
	ANDI	A,M.PROT	;clear out garbage
	IOR	A,ACZ		;and set protection part for result
	HRLM	A,CHAN.DSP	;save options codes in lh of chan arg

;now strip device name from file name

	HRRZ	A,-3(SP)	;get length(filename)
	JUMPG	A,GOTNAM	;have a name, proceed
	ERR. 1,[asciz /OPFILE: null file name, <CR> to specify/]
	PUSHJ	P,INCHWL	;no name, go get one from terminal
	POP	SP,-4(SP)	;and put it in as arg
	POP	SP,-4(SP)
	HRRZ	A,-3(SP)	;file name length
	JUMPE	A,ARGERR	;if we have a null name now, error

GOTNAM:		;we have a non-null file name at -3(sp), length in 1
	; should do getchan here if channel arg is =-1 at notymshare
	;determine if device there
	MOVE	B,-2(SP)	;get byte pointer
	MOVEI	A,-1(A)		;decrement count for first load
LUKDEV:	ILDB	B
	CAIN	":"
	 JRST	HASDEV		;he included a device to use
	SOJGE	A,LUKDEV	;go for next character
				;here he didn't include device
	MOVSI	A,(<SIXBIT /DSK/>);so supply "DSK:" for him
	JRST	GOTDEV

HASDEV:	EXCH	B,-2(SP)	;remove device from file name
	PUSH	SP,-3(SP)	;and put device name on top of stack
	PUSH	SP,B
	HRRM	A,-5(SP)	;put updated count out to file name
	MOVNI	A,1(A)		;and subtract off file size + 1 (colon)
	ADDM	A,-1(SP)	;from device name string
	PUSHJ	P,CVSIX		;convert to sixbit
GOTDEV:
	MOVEM	A,DEV.DSP	;save device name
	MOVEI	ACZ,4		;set size of lookup/enter/rename block
	MOVEM	LUKBLK.DSP	; (my version, let his be different)
	PUSH	SP,-3(SP)	;now send file name to cvfil
	PUSH	SP,-3(SP)
	MOVEI	D,LUKBLK.DSP	;get frame for lookup/enter/rename blk
	MOVEI	A,.RBEXT(D)	;address of where extension should go
	MOVEI	B,.RBPPN(D)	;address of where ppn should go
	PUSH	P,A
	PUSH	P,B
	PUSHJ	P,CVFIL		;go convert file name string
	MOVEI	D,LUKBLK.DSP	;get frame for lookup/enter/rename blk
	MOVEM	A,.RBNAM(D)	;store file name
	TLNN	A,-1		;don't allow file names that prevent
	 JRST	ARGERR		;short lookup/enter/rename block
	MOVEI	ACZ,17		;logical search for dev name, dump mode
	MOVEM	ACZ,MODEWD.DSP	;bits,,initial status
	SETZM	BUF.DSP		;unbuffered
	MOVSI	ACZ,047740	;(all rd no, dump mode)
	MOVEM	ACZ,.RBPRV(D)	;set default protection, mode
	MOVE	A,CHAN.DSP	;pick up channel provided
	HRLI	A,.CHOPN	;and open device
	CHANIO	A,OPNBLK.DSP
	 JRST	OPFAIL
	HRRM	A,CHAN.DSP	;save channel number in case changed

OPNDUN:	LDB	B,[POINT =13,CHAN.DSP,=12]	;copy in protection
	DPB	B,[POINT =13,.RBPRV(D),=12]	;and file data mode

	LDB	B,[POINT 2,CHAN.DSP,=14];get initial Enter spec of ELM
	JUMPE	B,LOOKOP		;said to skip it
	HRLI	A,.CHENT	;perform enter
	CHANIO	A,0(D)
	 JRST	[HRLI	A,-3	;couldn't enter (code=-3),
		 JRST	SYSERR]	;use system error for RH, release, err
	HRLZ	ACZ,.RBEXT(D)	;get "already existed" bit into sign
	CAIN	B,3		;if want error on attempt to supercede
	 JUMPL	ACZ,ERRCLO	;and in fact it was, error
	CAIN	B,2		;or want error on attempt to create
	 JUMPGE	ACZ,ERRCLO	;and in fact it was, error
LOOKOP:			;now prepare for the lookup part
	LDB	B,[POINT 2,CHAN.DSP,=16];get instructions
	JUMPE	B,GOODRT	;tada we finished... return
		;no need to set various prot... since this is a lookup
	HRLI	A,.CHCLS	;better close in case was entered
	CHANIO	A,CL.ACS	;causing file to really exist,...
				;but don't fiddle access date yet
	HRLI	A,.CHLK		;do the lookup
	CHANIO	A,0(D)
	 JRST	[CAIN	B,2	;well, twould seem we have an error
		  JRST	RELOOK	;we have to try harder (Base mode)
		 HRLI	A,-5	;error code -5
		 JRST	SYSERR]	;normal error non-recovery (cry and go)
	CAIN	B,3		;lookup said "return after completion"
	 JRST	GOODRT		;yup, so that we will do

				;***protection/data mode fiddling??
	HLRZ	B,CHAN.DSP	;pick up flags again
	HRLI	A,.CHENT
	TRNE	B,1		;this supposed to be "multiple enter"?
	 HRLI	A,.CHMEN	;yes, make it so
	CHANIO	A,0(D)		;do menter/enter
	 JRST	[HRLI	A,-7	;this failed, code -7
		 JRST	SYSERR]	;use system code for RH, release & go
	JRST	GOODRT

RELOOK:				;"Base" mode, change to a "Create"
	MOVEI	B,15		; make it "create .. lookup" (3,1)
	DPB	B,[POINT =4,CHAN.DSP,=16]
	HLLZS	.RBEXT(D)	;clear rh of ext word
	SETZM	.RBPRV(D)	;and date stuff
	JRST	OPNDUN		;and go back as if from top
ERRCLO:	MOVSI	A,-4		;error during first enter
	MOVE	B,CHAN.DSP
	HRLI	B,.CHCLS
	CHANIO	B,50		;close (no create/supercede or access)

SYSERR:	HRR	A,.RBEXT(D)	;get error code from RH of EXT word
	HRRZ	B,CHAN.DSP	;release: (op=0, so no hrli)
	CHANIO	B,
	JRST	RTRN

ARGERR:	SKIPA	A,[-1]		;argument error (-1,,-1)
OPFAIL:	HRLI	A,-2		;open error
	JRST	RTRN

GOODRT:	HRRZ	A,CHAN.DSP
RTRN:	MOVE	POLL.DSP	;restore the NOPOLL flag
	MOVEM	NOPOLL
	SUB	SP,X44
	SUB	P,[XWD 10+1+2,10+1+2]	;remove locals,nopoll,call
	JRST	@2(P)
END

   3nXò