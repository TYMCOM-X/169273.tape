        TITLE   HARDCOPY
        ENTRY   HRDCPY,SWITCH
        EXTERNAL GDBUFF,GETGDI,RELFD
        EXTERNAL  INNTMP,PAGESP
        EXTERNAL  OUTLIN,CHROUT,OUTAC1
        EXTERNAL   GETNAM,REMPNT
        EXTERNAL   PNTLST,DUPREQ
        EXTERNAL   BINDEC,WDATE,REPORT,WTIME
        EXTERNAL  MAIL,COUR,SDEL,AIRM,AIRE,HOLD,SPEI,DEL
        EXTERNAL  MAILA,INTERO
        EXTERNAL  PNTOUT,FILPGE,LINOUT
        EXTERNAL  HEAD,FORTRN,TWOBLE,USRLPP,TMARGN
        INTERNAL  SWTOUT,SMALLP,LARGEP,TTYP
;
;       PURPOSE IS TO SUPPLY THE OPERATOR A HARDCOPY
;       OF THE REQUEST MADE BY THE USER
;
;       ACCUMULATOR ASSIGNMENTS
;
        AC= 0                   ;BASIC ACCUMULATOR
        AC1= 1                  ;
        AC2= 2                  ;
        AC3= 3                  ;
        AC4= 4                  ;
        AC5= 5                  ;
        REQNUM= 6               ;HOLD THE REQUEST NUMBER
        BUFADR= 10              ;CONTAINS ADDRESS OF GD BUFFER
        LSTCHL=15
        P==17
        FTNMSK==100
        FULMSK==200000
        TWOMSK==40
        HEDMSK==100000
        QSTMSK==200             ;SPECIAL CONTROL CHAR.
        TRAMSK==400
;
TOP:    064241,,205024          ;CR,LF,LF,LF,LF
        ASCIZ/REQUEST NUMBER     /      ;MUST FOLLOW TOP
        ARRAY TEMP [4]
HRDCPY: MOVEI   AC,TOP          ;PUT OUT HEADING
        PUSHJ   P,OUTLIN        ;PUT IT OUT
        MOVE    AC3,GDBUFF      ;GET REQUEST NUMBER
        TLZ     AC3,770000      ;GET REQ. NO.
        MOVE    REQNUM,AC3      ;STORE FOR LATER USE
        LSH     AC3,6           ;LEFT JUSTIFY REQ.NO.
        MOVE    AC1,[POINT 6,AC3] ;POINT TO INPUT
        MOVEI   AC5,5           ;MAX CHAR TO PUT OUT
        ILDB    AC,AC1          ;GET A CHARACTER
        ADDI    AC,40           ;CONVERT TO ASCII
        PUSHJ   P,CHROUT
        SOJG    AC5,.-3         ;DO REST OF CHARACTER
        MOVEI   AC,[ASCIZ/               /]
        PUSHJ   P,OUTLIN
        MOVEI   AC,[ASCIZ/CONTROL # ------ QA BY:-------/]
        PUSHJ   P,OUTLIN
;
;       PUT OUT TIME OF REQ.
;
        MOVEI   AC,[ASCIZ/
DATE & TIME        /]
        PUSHJ   P,OUTLIN
        HLRZ    AC1,GDBUFF+3    ;GET DATE
        PUSHJ   P,WDATE         ;PUT IT OUT
        MOVEI   AC,[ASCIZ/   /]
        PUSHJ   P,OUTLIN
        HRRZ    AC2,GDBUFF+1    ;GET TIME
        PUSHJ   P,WTIME         ;PUT IT OUT
;
;       GET WHO ENTERED REQUEST AND WHO IT WAS DONE FOR
;
        MOVEI   BUFADR,GDBUFF+6 ;POINT TO REQUESTOR NAME
        MOVEI   AC5,30          ;MAX SIZE OF NAME
        MOVEI   AC,[ASCIZ/
REQUESTED BY       /]
        PUSHJ   P,OUTLIN
        PUSHJ   P,OUTSIX
        MOVEI   AC,ENTRBY       ;GET LITERAL
        PUSHJ   P,OUTLIN
        DMOVE   AC1,GDBUFF+27   ;GET IT
        PUSHJ   P,GETNAM        ;PUT IT OUT
        MOVEI   AC,ENTRFR       ;GET STRING
        PUSHJ   P,OUTLIN
        DMOVE   AC1,GDBUFF+31   ;GET IT
        PUSHJ   P,GETNAM        ;PUT IT OUT
        MOVEI   BUFADR,GDBUFF+12;POINT TO PROJECT CODE
;
;       COME HERE TO DO THE PROJECT CODE
;
        MOVEI   AC5,14          ;MAX PROJECT CODE
        MOVEI   AC,[ASCIZ/
PROJECT CODE       /]
        PUSHJ   P,OUTLIN
        PUSHJ   P,OUTSIX
;
;       DO TELEPHONE NUMBER
;
        MOVEI   AC,[ASCIZ/
TELEPHONE NUMBER   /]
        PUSHJ   P,OUTLIN
        MOVEI   BUFADR,GDBUFF+14;POINT TO TELE
        MOVEI   AC5,30          ;MAX. TELE
        PUSHJ   P,OUTSIX
;
;       PUT OUT DISTRICT NO.
;
        MOVEI   AC,[ASCIZ/
DISTRICT        /]
        PUSHJ   P,OUTLIN        
        HLRZ    AC,GDBUFF+21    ;GET DISTRICT WD.
        ANDI    AC,777          ;KEEP THE DISTRICT
        PUSHJ   P,BINDEC        ;GO PUT IT OUT
        MOVE    AC,GDBUFF+3     ;GET SWITCHES including tran bit
        TRNN    AC,TRAMSK       ;SEE IF TRANSFER DONE
        JRST    SWTNO1          ;NO, SKIP THIS and get other switches
        MOVEI   AC,[ASCIZ/
SPOOLED TO LOCATION/]
        PUSHJ   P,OUTLIN
        MOVE    AC,GDBUFF+33    ;GET IT
        ANDI    AC,777          ;KEEP IT
        PUSHJ   P,BINDEC        ;PUT IT OUT
        MOVEI   AC,[ASCIZ/
FOR TRANSFER TO /]
        PUSHJ   P,OUTLIN
	MOVEM	AC5,SAV5	; save ac5 just in case /mch
	MOVEI	AC5,^D25	; max. no. of chars to print /mch
	MOVE    AC1,[POINT 7,GDBUFF+22]  ; POINT TO transfer loc
TRPNT:	ILDB    AC,AC1
        CAIN    AC,12           ;END OF LINE?
        JRST    SWTNO1          ;YUP
        PUSHJ   P,CHROUT
        SOJG    AC5,TRPNT	; get next char. if more there
	MOVE	AC5,SAV5	; restore AC5 to what it was
SWTNO1: MOVE	AC,GDBUFF+5	; get switch settings
SWTNOW: PUSHJ   P,SWITCH        ;PUT OUT SWITCHES
        JRST    DESCRP          ;GO DO DESCRIPTION
;
;       PUT OUT SIXBIT STRING
;
OUTSIX: MOVE    AC1,[POINT 6,AC]
        HRR     AC1,BUFADR
        ILDB    AC,AC1
        ADDI    AC,40
        PUSHJ   P,CHROUT
        SOJG    AC5,.-3
        POPJ    P,
;
;       PUT OUT USERS SWITCHES
;
SWITCH: SETZM   SWTOUT#         ;SHOWS GOING TO TTY
        PUSH    P,AC3           ;SAVE AC3
        MOVEI   AC3,[ASCIZ/
OPTIONS: /]
        PUSHJ   P,PUTSWT        ;GO PUT IT OUT
        HRRZ    AC,GDBUFF+3     ;GET SWITHCES WD.
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        MOVE    AC,HEAD         ;GET ACTUAL PRINT SWITCH
        MOVEI   AC3,OPTION      ;FOR HEAD SWITCH
        TRNE    AC,HEDMSK       ;SEE IF HEADING DESIRED
        PUSHJ   P,PUTSWT        ;GO PUT OUT HEADING
        MOVEI   AC3,OPTION+22   ;QUEST SWITCH
        TRNE    AC,QSTMSK
        PUSHJ   P,PUTSWT
        MOVEI   AC3,OPTION+4    ;FOR FORTRAN
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        MOVE    AC,FORTRN       ;GET ACTUAL PRINT SWITCH
        TRNE    AC,FTNMSK               ;TEST FOR FORTRAN
        PUSHJ   P,PUTSWT        ;GO PUT IT OUT
        MOVEI   AC3,OPTION+12   ;FOR DOUBLE SWITCH
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        MOVE    AC,TWOBLE       ;GET DOUBLE SWITCH
        TRNE    AC,TWOMSK       ;TEST FOR DOUBLE
        PUSHJ   P,PUTSWT        ;GO PUT IT OUT
        MOVEI   AC3,OPTION+14   ;FOR NO. LINES
        HRRZ    AC,GDBUFF+5     ;GET NO. LINES PER PAGE
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        MOVE    AC,USRLPP       ;GET ACTUAL MAX LINES
        PUSHJ   P,PUTCMA        ;OUT WITH VALUE AND COMMA
        HLRZ    AC,GDBUFF+1     ;GET THEM
        ANDI    AC,377          ;GET MARGIN ONLY
        MOVEI   AC3,OPTION+16   ;FOR MARGIN
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        MOVE    AC,TMARGN       ;GET ACTUAL MARGIN IF ANY
        SKIPE   AC              ;SKIP IF ZERO
        PUSHJ   P,PUTCMA        ;PUT IT OUT
        HRRZ    AC,GDBUFF+3     ;GET SWITCHES AGAIN
        MOVEI   AC3,OPTION+20   ;GET FULLCH OPTION
        TRNE    AC,FULMSK       ;TEST FOR FULLCH
        PUSHJ   P,PUTSWT        ;OUT WITH IT IF THERE
        LSH     AC,-11          ;GET COPIES
        ANDI    AC,77           ;KEEP THEM
        MOVEI   AC3,OPTION+2    ;COPIES LITERAL
        PUSHJ   P,PUTCMA        ;PUT THEM OUT
        HRRZ    AC1,GDBUFF+3    ;GET SWITCHES AGAIN
        ANDI    AC1,7           ;KEEP WHERE TO PRINT
        MOVEI   AC2,6           ;POINT TO CENTER
        CAILE   AC1,1           ;SEE IF CENTER
        MOVEI   AC2,10          ;POINT TO REMOTE
        MOVEI   AC3,OPTION(AC2)
        PUSHJ   P,PUTSWT        ;PUT IT OUT
        POP     P,AC3           ;RETURN AC3
        POPJ    P,
PUTSWT:
PUTSW1: PUSH    P,AC            ;SAVE CONTENTS
        MOVE    AC,AC3          ;MOVE ADDRESS OVER
        SKIPN   SWTOUT          ;SKIP IF TO PRINTER
        JRST    PUTTTY          ;PUT TO TTY
BRKOPN: PUSHJ   P,LINOUT        ;OUT TO PRINTER
        SKIPA
PUTTTY: PUSHJ   P,OUTLIN        ;OUT TO TTY. MAYBE
        POP     P,AC            ;RESTORE AC
        POPJ    P,              ;RETURN
PUTCMA: PUSHJ   P,PUTSWT        ;GO PUT OUT LITERAL
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        JRST    PUTCM1          ;JUMP IF TO LPT
        PUSHJ   P,BINDEC        ;PUT TI TTY
        SKIPA                   ;SKIP OUTPUT TO PRINTER
PUTCM1: PUSHJ   P,FILPGE        ;PUT TO PRINTER
        MOVEI   AC,","          ;GET SEPERATOR
        SKIPE   SWTOUT          ;SKIP IF TO TTY
        JRST    PUTCM2          ;JUMP IF TO PRINTER
        PUSHJ   P,CHROUT
        POPJ    P,              ;RETURN
PUTCM2: PUSHJ   P,PNTOUT        ;PUT TO PRINTER
        POPJ    P,              ;RETURN
;
;       PUT OUT THE TYPE OF PAPER AND ANY SPECIAL
;       INSTRUCTIONS THAT MY GO WITH THE REQUEST
;
DESCRP: MOVEI   AC,[ASCIZ/
DESCRIPTION OF WORK:
PRINT ON /]
        PUSHJ   P,OUTLIN
        HLRZ    AC,GDBUFF+4
        MOVEM   AC,TNOPG#
        HLRZ    AC,GDBUFF+5     ;GET PAPER/PARTS WORD
        MOVE    AC2,AC          ;SAVE IT
        LSH     AC,-^D12        ;RIGHT JUSTIFY IT
        SETZ    AC1,            ;ZERO FOR NEXT INST.
        LSHC    AC,-3           ;PUT PARTS IN NEXT REGISTER
        CAIN    AC,4            ;CHECK FOR OTHER THAN SPECIAL
        JRST    SPECAL          ;GO DO SPECIAL FORMS
        ROT     AC1,3           ;RIGHT JUST. PARTS
        ADDI    AC1,60          ;CONVERT TO ASCII
        CAIN    AC,1            ;SEE IF SMALL PAPER
        MOVEI   AC,SMALLP
        CAIN    AC,2            ;CHECK FOR LARGE
        MOVEI   AC,LARGEP
        CAIN    AC,3            ;CHECK FOR TTYP PAPER
        MOVEI   AC,TTYP
        CAIE    AC,5
        JRST    DESCP1
        MOVEI   AC,SMALLP
        PUSHJ   P,OUTLIN
DESCP0: MOVEI   AC,WHITP
        JRST    DESCP2
DESCP1:  CAIE    AC,6
        JRST    DESCP2
        MOVEI   AC,LARGEP
        PUSHJ   P,OUTLIN
        JRST    DESCP0
DESCP2: PUSHJ   P,OUTLIN
        PUSHJ   P,OUTAC1        ;OUT NO PART PAPER
        MOVEI   AC,[ASCIZ/ PART PAPER /]
        PUSHJ   P,OUTLIN
        MOVE    AC,TNOPG
        PUSHJ   P,BINDEC
        MOVEI   AC,[ASCIZ / PAGES /]
        PUSHJ   P,OUTLIN
        PUSHJ   P,DECSTR        ;GO PUT OUT DECOLATED STRING
        JRST    GETFIL          ;GO GET NAMES OF FILES
SPECAL: MOVEI   AC,[ASCIZ/SPECIAL FORMS AS DESCRIBED BELOW /]
        PUSHJ   P,OUTLIN
        PUSHJ   P,DECSTR        ;DO DECOLATED STRING
        MOVE    AC1,[POINT 7,AC] ;DUMMY POINTER
        HRRI    AC1,GDBUFF+145  ;CORRECT POINTER to special forms
	MOVEM	AC5,SAV5	; save AC5 just in case
	MOVEI	AC5,^D135	; max. no. of chars to print
SPEC1:	ILDB    AC,AC1          ;GET A CHARACTER
        CAIN    AC,4            ;CHECK FOR CONTROL D
        JRST    SPCEND		; nothing more to print
        PUSHJ   P,CHROUT
        SOJG	AC5,SPEC1	; keep printing until get all chars
SPCEND: MOVE	AC5,SAV5	; restore AC5
	JRST	GETFIL		;and go print list of file names

DECSTR: MOVEI   AC,[ASCIZ/ DECOLATED/]
        TRNE    AC2,7000        ;SKIP IF NOT DECOLATED
        PUSHJ   P,OUTLIN        ;OUT LITERAL
        MOVEI   AC,[ASCIZ/
/]
        PUSHJ   P,OUTLIN
        POPJ    P,
;
;       HAVE PUT OUT FORMS NEEDED AND ANY SPECIAL
;       INSTRUCTIONS.  NOW IT IS TIME TO GET A LIST
;       OF THE FILES SPOOLED.
;
GETFIL: MOVEI   AC,[ASCIZ/
THE FOLLOWING FILES:
/]
        PUSHJ   P,OUTLIN
        SKIPE   ,REPORT         ;BR. IF DOING THIS FROM TAPE
        JRST    OUTFIL          ;GO DO FILE FROM TAPE
        MOVE    13,GDBUFF+3     ;GET DATE WD FOR CALL
        PUSHJ   P,GETGDI        ; MAKE SURE CURREQ SET UP
        PUSHJ   P,RELFD         ; RELEASE SO SURE GET NEW COPY
        PUSHJ   P,PNTLST        ;OUT LIST OF FILES
;
;       PUT OUT THE DELIVERY INSTRUCTIONS
;
OUTDEL: MOVEI   AC,DEL
        PUSHJ   P,OUTLIN
        HLRZ    AC,GDBUFF+5     ;GET MAILING MODE
        ANDI    AC,700          ;SEPERATE MAILING MODE
        LSH     AC,-6           ;RIGHT JUST. FIGURE
        MOVE    AC3,AC          ;SAVE VALUE
        CAIN    AC,1            ;CHECK FOR PICKUP MODE
        MOVEI   AC,HOLD
        CAIN    AC,0            ;CHECK FOR MAIL
        MOVEI   AC,MAIL
        CAIN    AC,2            ;CHECK FOR COURIER SERVICE
        MOVEI   AC,COUR
        CAIN    AC,3            ;CHECK FOR SPECIAL DELIVERY
        MOVEI   AC,SDEL
        CAIN    AC,4            ;CHECK FOR AIR MAIL
        MOVEI   AC,AIRM
        CAIN    AC,5            ;CHECK FOR AIR EXPRESS
        MOVEI   AC,AIRE
        CAIN    AC,6            ;LOOK FOR INTER-OFFICE MAIL
        MOVEI   AC,INTERO
        PUSHJ   P,OUTLIN
;
;       GET MAILING ADDRESS
;
ENDMES: CAIE    AC3,6           ;NO ADDRESS ON INTEROFFICE
        CAIN    AC3,1           ;NONE ON PICKUP
        JRST    ENDADR          ;BYPASS PUTTING OUT ADDR.
        MOVEI   AC3,^D240       ;MAX CHAR TO ADDRESS
        MOVEI   AC,MAILA
        PUSHJ   P,OUTLIN
        MOVE    AC1,[POINT 6,GDBUFF+40] ; BUFF PTR TO MAIL ADDR
ENDMS1: ILDB    AC,AC1          ;GET A CHAR.
        CAIE    AC,77
        CAIN    AC,76           ;CHECK TERMINATORS
        JRST    ENDMS2
        ADDI    AC,40           ;CONVERT TO ASCII
        PUSHJ   P,CHROUT
ENDMS3: SOJG    AC3,ENDMS1
        JRST    ENDMS4
ENDMS2: PUSH    P,AC
        MOVEI   AC,[ASCIZ/
/]
        PUSHJ   P,OUTLIN
        POP     P,AC
        CAIE    AC,77
        JRST    ENDMS3
ENDMS4: SETZ    AC3,            ;POSTAGE NEEDED INDICATOR
ENDADR: MOVE    AC1,[POINT 7,GDBUFF+110]        ; SPEC FOR INFO
        MOVEI   AC,SPEI         ;GET LITERAL ADDRESS
        ILDB    AC2,AC1         ;GET 1ST CHAR.
        CAIN    AC2,4           ;SEE IF CONT-D
        JRST    ENDINS          ;END OF INST. FIRST OFF
	PUSHJ   P,OUTLIN        ;PUT OUT LITERAL
	MOVEM	AC5,SAV5	;save AC5 (probably not needed)
	MOVEI	AC5,^D145	;max. no. of chars. to print
        MOVE    AC,AC2          ;GET CHAR. BACK
ENDAD1: PUSHJ   P,CHROUT        ;PUT IT OUT
        ILDB    AC,AC1          ;GET NEXT CHAR.
        CAIE    AC,4            ;SKIP IF CONTROL D
        SOJG	AC5,ENDAD1	;print more chars. if needed
ENDAD2: MOVE	AC5,SAV5	; restore AC5
ENDINS: MOVEI   BUFADR,GDBUFF   ;RESTORE POINTER
        SKIPE   ,DUPREQ         ;SKIP IF NOT DOING DUP. REQ.
        JRST    LEAVE           ;PREPARE TO LEAVE PROGRAM
        MOVE    AC,PAGESP       ;GET NO. PAGES SPOOLED
        OUTSTR  [ASCIZ/

/]
        PUSHJ   P,BINDEC        ;OUT NO. PAGES SPOOLED
        OUTSTR  [ASCIZ/ PAGES
/]
        MOVE    AC5,AC3         ;SAVE POSTAGE INDICATOR
        DATE    AC1,            ;GET DATE
        PUSHJ   P,WDATE         ;OUT DATE
        OUTSTR  [ASCIZ/  /]     ;FILLER
        MSTIME  AC,             ;GET TIME
        IDIVI   AC,^D60000      ;GET MINUTES
        IDIVI   AC,^D60         ;GET HR AND MINUTES
        MOVE    AC2,AC1         ;SAVE MINUTES
        IDIVI   AC,12           ;GET TENS OF HRS
        ADDI    AC,60           ;CONVERT TO ASCII
        OUTCHR  AC              ;PUT OUT TENS OF HOURS
        OUTCHI  "0"(AC1)        ;PUT OUT HOURS
        OUTCHI  ":"             ;PUT OUT SEPERATOR
        MOVE    AC,AC2          ;GET MINUTES
        IDIVI   AC,12           ;GET TENS OF MINS
        ADDI    AC,60           ;CONVERT TO ASCII
        OUTCHR  AC              ;PUT OUT TENS OF MINS
        OUTCHI  "0"(AC1)        ;PUT OUT MINUTES
        OUTSTR  [ASCIZ/
COMPLETED BY -----------------------------/]
        CAIN    AC5,6           ;CHECK FOR INTEROFFICE
        JRST    NOPOST          ;BR. ON INTER-OFFICE
        CAIE    AC5,1           ;SKIP IF NO POSTAGE
        JRST    ENDRTN          ;GO GIVE POSTAGE NEEDED
NOPOST: OUTSTR  [ASCIZ/
DO NOT RUN /]
        MOVEI   AC,"B"          ;FOR BCHARG
        SKIPE   ,REMPNT         ;SKIP FOR CENTER PRINT
        MOVEI   AC,"R"          ;FOR REMOTE PRINT
        OUTCHR  AC              ;PUT OUT WHERE TO RUN
        OUTSTR  [ASCIZ/CHARG AGAINST THIS REQUEST/]
        JRST    LEAVE1          ;GO LEAVE THE PROGRAM
ENDRTN: OUTSTR  [ASCIZ/

DELIVERY CHARGE --------------------------/]
        OUTSTR  [ASCIZ/

ENTERED BY ----------------------------/]
        OUTSTR  [ASCIZ/
/]
        MOVEI   AC,"B"          ;FOR CENTER PRINT
        SKIPE   ,REMPNT         ;SKIP FOR CENTER PRINT
        MOVEI   AC,"R"          ;FOR REMOTE PRINT
        OUTCHR  AC              ;PUT OUT TYPE
        OUTSTR  [ASCIZ/CHARG MUST BE RUN AGAINST THIS REQUEST/]
LEAVE:
        movei   ac,bottom
        pushj   p,outlin
        POPJ P,
LEAVE1: MOVEI   AC,BOT1
        PUSHJ   P,OUTLIN
        POPJ    P,              ;RETURN
BOTTOM: 064241,,205000
BOT1:   ASCIZ /












/
;
;       PUT OUT FILE NAME WHEN DOING HRDCPY FROM TAPE
;
OUTFIL: SKIPL   REPORT          ;SKIP IF THIS FROM TAPE
        JRST    FLASIS          ;GO IF CENTER SUPCHECK STORE
        MOVE    AC1,[POINT 6,AC4] ;POINT TO STORAGE
        SOSGE   INNTMP+2        ;IS BUFFER EMPTY
        PUSHJ   P,INLST         ;YES, REFILL IT
        ILDB    AC4,INNTMP+1    ;GET FILE NAME
        SOSGE   INNTMP+2        ;BUFFER EMPTY?
        PUSHJ   P,INLST
        ILDB    AC5,INNTMP+1    ;GET EXT.
        MOVSS   ,AC5            ;SWAP IT AROUND
        MOVEI   AC2,11          ;NO. CHAR TO DO
OUTFL1: ILDB    AC,AC1          ;GET FILE NAME
        ADDI    AC,40           ;CONVERT TO ASCII
        OUTCHR  AC              ;PUT OUT FILE NAME
        CAIN    AC2,4           ;SEE IF TIME FOR SEPERATOR
        OUTCHI  "."             ;PUT OUT SEPERATOR
        SOJG    AC2,OUTFL1      ;DO REST OF FILE NAME
        OUTSTR  [ASCIZ/   (/]
        MOVEI   AC2,3           ;NO. CHAR. TO DO
        ILDB    AC,AC1          ;GET EXT. NAME
        ADDI    AC,40           ;CONVERT TO ASCII
        OUTCHR  AC              ;PUT TI OUT
        SOJG    AC2,.-3         ;DO REST OF IT
        OUTSTR  [ASCIZ/)
/]
        JRST    OUTFIL          ;DO NEXT NAME
INLST:  SOS     (P)             ;FOR RETURN JUMP
        SOS     (P)
        IN      LSTCHL,         ;READ NEXT. BLK
        POPJ    P,              ;ON GOOD READ
        POP     P,              ;RID OF THIS PUSH
        JRST    OUTDEL          ;GO DO DEL. INST
;
;       TAKE FILE AS IS
;
FLASIS: SOSGE   INNTMP+2        ;SEE IF BUFFER EMPTY
        PUSHJ   P,INLST         ;GO REFILL IT
        ILDB    AC,INNTMP+1     ;GEHAR.
        PUSHJ   P,CHROUT        ;PUT IT OUT
        JRST    FLASIS          ;GET NEXT CHAR.
;
OPTION: ASCIZ/HEAD,/
        ASCIZ/COPIES=/
        ASCIZ/FORTRAN,/
        ASCIZ/CENTER/
        ASCIZ/REMOTE/
        ASCIZ/DOUBLE,/
        ASCIZ/LINES=/
        ASCIZ/INDENT=/
        ASCIZ/FULLCH,/
        ASCIZ/QUEST,/
ENTRBY: ASCIZ/
ENTERED BY         /
ENTRFR: ASCIZ/
ENTERED FOR        /
;*****  DO NOT SEPERATE, MUST BE IN THIS ORDER
SMALLP: ASCIZ/SMALL /
LARGEP: ASCIZ/LARGE /
TTYP:   ASCIZ/TTY   /
SPX:    ASCIZ/SPECI/
SMAWHI: ASCIZ/SMAL-WHI /
LARWHI: ASCIZ/LARG-WHI /
WHITP:  ASCIZ/WHITE /
SAV5:	0			; a place to save AC5 /MCH
        END
 x]|Fè