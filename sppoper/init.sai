begin "INIT"

require IFCR compiler!version lsh -24 = '1022
	THENC "(sailib)randio"
	ELSEC "(sailib)rndio"
	ENDC library;

require 128 pnames;
require 128 new!items;

require "{}()" delimiters;
define
	!	=	{comment};

Simple procedure SetLch (integer Port,Value, BitNo(-1));
begin "SetLch"
   own integer T,PTR;
   if BitNo < 0
     then PTR _ point(36,T,35)
     else PTR _ point(1,T,BitNo);
     start!code
        define GETLCH = '051300000000;
        define SETLCH = '051340000000;
        MOVE 1,port;
        MOVEM 1,T;
        GETLCH T;
        MOVE 1,value;
        DPB 1,PTR;
        SETLCH T;
   end;
end "SetLch";

define	Bsd	=	{auxclv(-1,1,'77)},
	Width(x)=	{auxclv(-1,x,'27)},
	Backspace=	{auxclv(-1,1,'57)},
	Erase	=	{auxclv(-1,1,'62)},
	exit    =	{call(0,"EXIT")},
	LC	=	{setlch(-1,1,13)};

LC;			! lower case input allowed;		
Backspace;		! backspace on erase character;
Erase;			! erase with bs-sp-bs;
Width(80);		! length of line=80;
Bsd;			! delete on backspace;
Auxclv(-1,CVSIX("TYM425"),'100);	! set terminal type to TYM425;
exit;
end "INIT"
