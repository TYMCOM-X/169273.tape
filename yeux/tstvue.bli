Module TEST = Begin

!
!  This module contains nothing but testing routines.
!  Each routine tests a specific condition and returns its result.
!
!  Most (if not all) of these routines are part of VIEW.MAC in
!  a slightly optimized form.  For compatibility and possible
!  conversion to a machine other than a PDP-10, these routines
!  are included here.

external Getchxr, sosmod;

!
! Test for EOL condition::
!

Global routine EolBreak ( character ) =
    ( .character eql "?M" ) or ( .character eql "?L" );
Global routine EolChxr ( position ) = EolBreak( GetChxr( .position ) );

!
!  Test for horizontal whitespace condition::
!

Global routine WhtBreak ( character ) =
    ( .character eql " " ) or ( .character eql "?I" );
Global routine WhtChxr ( position ) = WhtBreak( GetChxr( .position ) );

!
!  Test for PUB or RUNOFF or SCRIBE formatting characters::
!

Global routine FmtBreak ( character ) =
    ( .character eql "." ) or ( .character eql "@" );
Global routine FmtChxr ( position ) = FmtBreak( GetChxr( .position ) );

!
!  Test for any type of whitespace::
!

Global routine WhiteSpace ( character ) =
    WhtBreak( .character ) or EolBreak( .character );

!
!  Test for EOL type::
!  <cr> := line := 0
!  <ff> := page := 1
!

Global routine EolType( position ) =
    If not .sosmod
     then 0
     else ( "?L" eql GetChxr( .position ) );

!
!  Test for alphanumeric::
!

Global routine AlfBreak( character ) =
    ( .character geq "a" and .character leq "z" ) or
    ( .character geq "A" and .character leq "Z" ) or
    ( .character geq "0" and .character leq "9" );
Global routine AlfChxr( position ) = AlfBreak( Getchxr( .position ) );


end eludom

