<file spec> <file defaults> <bad spec> ;

"alphas" ::= [ 'a_thru_p' 'q_thru_z' ] ;
"alphanums" ::= [ 'a_thru_p' 'q_thru_z' 'digit' ] ;
"null" ::= [ ] ;

<identifier> ::= "alphanums"+ ;
<digit sequence> ::= 'digit'+ ;

<key letter> ::= 'a_thru_p' ;
<key pair> ::= <key letter> <key letter> ;
<opt key> ::= '(' <key pair> <key pair> ')'  !
              '(' <key pair> ')'  !
	      '' ;

<extension> ::= <identifier>  !  '' ;
<file name> ::= <identifier>  ;
<catalog> ::= <identifier>  !  '' ;
<user> ::= <digit sequence>  !  '' ;
<volume> ::= <identifier>  !  '' ;
<user file>::= <user> '.' <catalog> '.' <file name> '.' <extension> <opt key> ;
<opt user file> ::= <user file>  !  '' ;

<device name> ::= '#' ( ''  !  <identifier> ) ;

<file descriptor> ::= <device name> !
                      <volume> ':' <opt user file>  !
                      <user file>  !
                      <catalog> '.' <file name> '.' <extension> <opt key>  !
                      <file name> '.' <extension> <opt key>  !
                      <file name>  ;

<rw access> ::= "alphas"  !  '-' "alphas" ;
<access code> ::= <rw access>  !
                  <rw access> <rw access> ;
<option> ::= "alphas"  !
             "alphas" '=' <digit sequence>  !
             <access code> ;
<another option> ::= ',' <option> ;
<option list>::= ';' <option> <another option>* ;

<resource name string> ::= <file descriptor> <option list>  !
                           <file descriptor>  !
                           <option list> ;

<file spec> ::= <resource name string> ;


<file defaults> ::= "null" ;
<bad spec> ::= "null" 
