	TITLE	RUNPRG	- RUNS AN ARBITRARY FILE -
	TWOSEG

;
;    RUNPRG IS A PASCAL CALLABLE PROCEDURE WHICH RUNS A PROGRAM WHOSE NAME IS
;	GIVEN AS A STRING.  IF ANY ERROR OCCURS (BAD NAME, PROGRAM NOT FOUND)
;	THE PROCEDURE RETURNS;  OTHERWISE, THE NEW PROGRAM IS STARTED. USAGE:
;
;	   EXTERNAL PROCEDURE RUNPRG (NAME: FILE_ID);
;
;    RUNCLL IS SIMILAR TO THE ABOVE EXCEPT THAT IT RUNS THE PROGRAM AT THE 
;	CL ENTRY POINT.
;
;	   EXTERNAL PROCEDURE RUNCLL (NAME: FILE_ID);
;

	SEARCH	PASSYM
	EXTERN	PRFID.			; PARSES FILNAM INTO SIXBIT

;
;	STACK VARIABLES
;
	RUNDEV=	1		; RUN UUO CONTROL BLOCK
	RUNNAM=	2
	RUNEXT=	3
	RUNMBZ=	4		; MUST BE ZERO
	RUNPPN=	5
	RUNCOR=	6		; SHOULD BE ZERO

	; A PASCAL FILBLOCK (FILDEV TO FILPPN) IS ALSO USED ON THE STACK.
	;    ASSUMES FILDEV= > RUNCOR=

	RELOC	400000
	ENTRY	RUNPRG
RUNPRG:	PUSHJ	TOPP,PARSE		; GET FILE TO RUN
	HRLZI	0,0			; RUN AT OFFSET 0
	HRRI	0,RUNDEV+1(TOPP)	; +1 ACCOUNTS FOR PUSH
	RUN	0,
	HALT


	ENTRY	RUNCLL
RUNCLL:	PUSHJ	TOPP,PARSE		; GET FILE TO RUN
	HRLZI	0,1			; RUN AT OFFSET 1
	HRRI	0,RUNDEV+1(TOPP)
	RUN	0,
	HALT

PARSE:	MOVE	REG2,0(REG)		; GET LENGTH OF NAME STRING
	MOVEI	REG1,1(REG)		; BYTE PTR TO 0TH CHAR
	HRLI	REG1,440700
	MOVEI	REG,FILPTR(TOPP)	; USE DUMMY FILEBLOCK ON STACK TO GET INFO
	PUSHJ	TOPP,PRFID.		;    FROM PARSED NAME
	JRST	ERR			; BAD NAME, RETURN
	SKIPE	FILPRO(TOPP)		; NOT KOSHER IF PROTECTION SPECIFIED
	JRST	ERR

	MOVE	0,FILDEV(TOPP)		; COPY INFO INTO RUN FORMAT BLOCK
	MOVEM	0,RUNDEV(TOPP)
	MOVE	0,FILNAM(TOPP)
	MOVEM	0,RUNNAM(TOPP)
	MOVE	0,FILEXT(TOPP)
	MOVEM	0,RUNEXT(TOPP)
	SETZM	RUNMBZ(TOPP)
	SETZM	RUNCOR(TOPP)
	POPJ	TOPP,

ERR:	POP	TOPP,			; UNWIND CALLS
	POPJ	TOPP,

	END
