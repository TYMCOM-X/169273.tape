
require "  Definitions for command scanning routines" message;


Define $$C$DATA( CmdName, CmdText, CmdInfo, CmdFlag ) = {

require "
	Building tables..." message;

Ifcr ( Declaration( $$C$QUIT ) = 0 )
 thenc

Define $$C$QUIT = {

$$C$CMD( Quit, "        Exit from the program", ["
            This is the normal way to return to EXEC level."] )

};

Ifcr ( Declaration( $$C$HELP ) = 0 )
 thenc

Define $$C$HELP = {

$$C$CMD( Help, [" or ?   Reprint this message (Also HELP command-name)"], ["
            To display a list of brief one-line messages
            about all of the commands, type:

                HELP

            To display more in-depth information about a
            specific command, type:

                HELP command-name

            Note:  ? may be used in place of HELP."] )

    };

endc

Ifcr Length( CvPS(CmdName) )
 thenc
    Require " Commands " message;
    Redefine $$C$Length = 0;
    Redefine $$C$CMD(Nam,Hlp,Inf,Flg,Alt) = {
	Ifcr $$C$Length > 0 thenc , endc CvPS(Nam)
	Redefine $$C$Length = $$C$Length + 1;};
    Preset!with $$C$ALL $$C$QUIT $$C$HELP;
    Own safe string array CmdName[ 0 : $$C$Length-1 ];
 endc

Ifcr Length( CvPS(CmdText) )
 thenc
    Require " Help texts " message ;
    Redefine $$C$Length = 0;
    Redefine $$C$CMD(Nam,Hlp,Inf,Flg,Alt) = {
	Ifcr $$C$Length > 0 thenc , endc Cvps(Nam) & Hlp
	Redefine $$C$Length = $$C$Length + 1;};
    Preset!with $$C$ALL $$C$QUIT $$C$HELP;
    Own safe string array CmdText[ 0 : $$C$Length-1 ];
 endc

Ifcr Length( CvPS(CmdInfo) )
 thenc
    Require " Info Texts " message ;
    Redefine $$C$Length = 0;
    Redefine $$C$CMD(Nam,Hlp,Inf,Flg,Alt) = {
	Ifcr $$C$Length > 0 thenc , endc Inf
	Redefine $$C$Length = $$C$Length + 1;};
    Preset!with $$C$ALL $$C$QUIT $$C$HELP;
    Own safe string array CmdInfo[ 0 : $$C$Length-1 ];
 endc

Ifcr Length( CvPS(CmdFlag) )
 thenc
    Require " Flags " message;
    Redefine $$C$Length = 0;
    Redefine $$C$CMD(Nam,Hlp,Inf,Flg,Alt) = {
	Ifcr $$C$Length > 0 thenc , endc
	Ifcr Length( CvPS(Flg) )
	 thenc Flg
	 elsec 0
	 endc
	Redefine $$C$Length = $$C$Length + 1;};
    Preset!with $$C$ALL $$C$QUIT $$C$HELP;
    Own safe integer array CmdFlag[ 0 : $$C$Length-1 ];
 endc

};


Define $$C$PROCS = {

    Redefine $$C$CMD(Nam,Hlp,Inf,Flg,Alt) = {
	Ifcr Length(CvPS(Alt))
	 thenc Alt;
	 elsec C.}&{Nam}&{;
	endc};
    $$C$ALL $$C$QUIT $$C$HELP

};

