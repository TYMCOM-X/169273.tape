	TITLE DAYWK

	ENTRY	DAYWK
	ENTRY	NIGHT

	N1=20		;TRIAL AC FOR TESTING IF F10 OR F IV
	T1=1		;AC DEFS
	T2=T1+1		;GENERL PURP. AC'S
	T3=T2+1
	T4=T3+1
	T5=T4+1
	DATE=T5+1	;HOLDS CURRENT DAY OF WEEK (0-6)
	BP=15		;BYTE POINTER

	SETO	N1		;SET AC > 0
DAYWK:	0
NIGHT:	RESET
;ROUTINE TO GET THE DAY OF THE WEEK
;
	CALLI	T1,14			;GET DATE
	IDIVI	T1,^D31*^D12		;YEAR INTO T1
	IDIVI	T2,^D31			;MONTH IN T2, DAY IN T3
	MOVEI	T5,4(T3)		;01-JAN-64 WAS A WED
	MOVE	BP,[XWD 440300,[EXP 033614625035]]
	MOVE	T4,T2			;COPY MONTH
	ILDB	T3,BP			;GET OFFSET
	SOJGE	T4,.-1			;FOR RIGHT MONTH
	ADDM	T3,T5			;PUT IN DAY
	TRNE	T1,3			;LEAP YEAR?
	AOJA	T5,.+3			;NO
	CAILE	T2,1			;JAN OR FEB?
	AOS	T5			;NO
	ADDM	T1,T5			;PLUS YEAR
	ASH	T1,-2			;PLUS LEAP YEARS
	ADDM	T1,T5			;..
	IDIVI	T5,7			;SAYS PER WEEK
;
;THIS NOW LEAVES THE DAY OF THE WEEK IN AC DATE
	MOVEM	DATE,@0(16)
	JUMPE	N1,FX10		;JUMP IF F-10
	JRA	16,1(16)
FX10:	POPJ	17,		;THIS IS FOR F-10
	END
