.TITLE	KOD11 - PDP11 CODE FOR THE DCP
	.ASECT
	.ENABL	LC

;-----
;This bootstrap runs in the DCP 11 and features automatic
; line scanning. Loading may be accomplished in NX format
; (local & remote nodes) and Tymshare format (remote only).
; An optional prefix file can be used to change the device CSR
; and vector addresses. This file might look like:
;
;	DR0CSR=	170000		;Assign device CSR addr
;	DR1CSR=	170010
;	DR2CSR=	170020
;	DR3CSR=	170030
;	DU0CSR=	170040
;	DU1CSR=	170050
;	DU2CSR=	170060
;	DU3CSR=	170070
;	DR0VEC=	300		;Assign device vector addresses
;	DR1VEC=	310
;	etc.
;-----

	.MACRO	PUSH	REG
		MOV	REG,-(SP)
	.ENDM

	.MACRO	POP	REG
		MOV	(SP)+,REG
	.ENDM

.PAGE
.SBTTL	DEFINITIONS

;-----
;Device definitions
;-----

REBOOT=	173000				;Starting addr of bootstrap

.IIF NDF WATDOG		WATDOG=	177566	;Watch dog timer (console DL)

.IIF NDF DR0CSR		DR0CSR=	167770	;Defalt device addresses
.IIF NDF DR1CSR		DR1CSR= 167760
.IIF NDF DR2CSR		DR2CSR=	167750
.IIF NDF DR3CSR		DR3CSR=	167740

.IIF NDF DU0CSR		DU0CSR=	160010
.IIF NDF DU1CSR		DU1CSR=	160020
.IIF NDF DU2CSR		DU2CSR=	160030
.IIF NDF DU3CSR		DU3CSR=	160040

.IIF NDF DR0VEC		DR0VEC=	300	;Defalt device vectors
.IIF NDF DR1VEC		DR1VEC=	310
.IIF NDF DR2VEC		DR2VEC=	320
.IIF NDF DR3VEC		DR3VEC=	330

.IIF NDF DU0VEC		DU0VEC=	340
.IIF NDF DU1VEC		DU1VEC=	350
.IIF NDF DU2VEC		DU2VEC=	360
.IIF NDF DU3VEC		DU3VEC=	370

DCPCSR=	162000				;DCP address
RXFUN=	DCPCSR
RXPORT=	DCPCSR+1
RXDATA=	DCPCSR+2
TXFUN=	DCPCSR+10
TXPORT=	DCPCSR+11
TXDATA=	DCPCSR+12

DROBUF=	2				;DR output buffer
DRIBUF=	4				;DR input buffer

RXDBUF=	2				;DUV recieve buffer (read only)
PARCSR=	2				;DUV parameter csr (write only)
TXCSR=	4				;DUV Transmit csr
TXDBUF=	6				;DUV transmit buffer

.PAGE
;-----
; Relocation calculations
;-----
NXMERR=	440				;Addresses for relocated routines
OFSET=	NXMERR-NXMERZ
BLAERR=	BLAERZ+OFSET
TMRCHK=	TMRCHZ+OFSET
RTIX=	RTIZ+OFSET
NXINPT=	NXINPZ+OFSET
TBUF=	ENDZ+OFSET
TYSIZ=	TYEND-TYINIT
NXSIZ=	NXEND-NXINIT

FUDGEX=	4000				;Address offset from DCP ROM to RAM addr
.PAGE
;-----
;Impure storage
;-----

FMTOGL=	10				;Format toggle. This location is saved
					; through the entire boot process.
TIMER=	12				;Timer variable. Used for all formats
TIMERW=	14				;Watch dog timer
TIMEC=	16				;Time out constant
TYSTAT=	20				;TY format DUV line scan state table
					; This is 4 consec words!
LODTOP=	30				;Max address to be loaded

;-----
;Misc definitions
;-----

TIMEC0=	1130				;Time out constant (10 sec)
TIMEC1=	2260				;  "  (20 sec)
STACK=	300				;Stack starts below impure storage
TRANAD=	1000				;Transfer address
BT11=	6				;Boot 11 function from DCP

.PAGE
.SBTTL	INITIALIZE MACHINE
;-----
;Initialize machine - Boot entry point.
; This is once only code and is not relocated
;-----

	.=	FUDGE+BRMADR		;ROM location for PDP11 boot entry
START:	RESET
	MTPS	#340
	MOV	#STACK, SP		;Initialize stack
	MOV	#123, @#WATDOG		;Poke watchdog timer
2$:	TSTB	@#TXFUN			;Wait for DCP to initialize itself
	BNE	2$
	MOVB	#2, @#TXFUN		;Start DCP internal diagnostics
3$:	CMPB	@#RXFUN, #16		;Wait for DCP to pass diagnostics
	BNE	3$			; jmp till DCP ready
	MOVB	#4, @#TXFUN		;Load bootstrap into PDP11
	MOV	#777, @#776		;Mov a BR . Inst to RAM
	JMP	@#776			;Loop till DCP changes inst

;-----
; Relocate interrupt routines down to unused vector space
;-----
	.=	FUDGE+174000		;Rom location for PDP11 bootstrap
					; code to be copied to 11 RAM
					; before execution

RAMSTR:	MOV	#STACK, SP		;init stack
	MOV	#NXMERZ-FUDGEX, R0	;Load source pointer
	MOV	#NXMERR, R1		;Load destination pointer
4$:	MOV	(R0)+, (R1)+		;Relocate routines
	CMP	#ENDZ-FUDGEX, R0	;Done?
	BNE	4$			;Br till done

;-----
; Size memory
;-----
	MOV	#GOTSIZ-FUDGEX, @#4	;Return vector
	MOV	#340, @#6
	MOV	#10, R0			;Loop cnt for max mem siz
	CLR	R1
MEMSIZ:	ADD	#20000, R1		;Inc memory addr
	MOV	R1, (R1)		;Test for real mem
	SOB	R0, MEMSIZ		;Real mem, br if not in device space
GOTSIZ:	MOV	R1, @#LODTOP		;Store max load addr
	CLR	@#TIMERW		;Init watch dog timer
	CLR	@#TIMER			;Init timer
	MOV	#TIMEC0, @#TIMEC	;Init timer for scanning mode
	INC	@#FMTOGL		;Togle boot format

;-----
;Set up vectors for scan
;-----

	MOV	#VTAB-FUDGEX, R2	;Point to vector table
	MOV	#NPCTAB-FUDGEX, R3	;Pointer to new PC table
SETVEC:	MOV	(R2)+, R5		;Get vector from table
	BEQ	2$			;Br if done
	MOV	(R3)+, (R5)+		;Store new PC
	MOV	#340, (R5)		;Store new PSW
	BR	SETVEC

2$:

.PAGE
.SBTTL	INIT DEVICES FOR AUTO SCAN

;-----
;Initialize devices for automatic line scanning. They are
;	set up alternatly in NX format and TY format till boot
;	is completed.
;-----

	MOV	#DTAB-FUDGEX, R1	;Point to device addr table
	MOV	#7, R2			;Loop counter and device type (bit 2)
	MOV	#111, @#WATDOG		;POKE WATCHDOG TIMER
INITDV:	MOV	(R1)+, R4		;Get device addr
	BIT	#4, R2			;Which device type?
	BEQ	2$			;Br if DUV
	MOV	#100, (R4)		;Init DR
	BR	4$			;Do next device

2$:	BIT	#1, @#FMTOGL		;Test load format
	BNE	3$			;Br if TY fmt
	CALL	NXDUV			;Init DUV
	BR	4$

3$:	MOV	#400, TXCSR(R4)		;Reset DUV
	CMP	(PC), (PC)		; long nop
	MOV	#36377, PARCSR(R4)	;Sync on a marking line
	BIC	#20, (R4)
	MOV	#526, (R4)		;Init recieve reg
4$:	SOB	R2, INITDV		;Br till all dev are initialized
;-----
;Init TY fmt state table
;-----
	MOV	#TYSTAT-FUDGEX, R3
	CLR	(R3)+
	CLR	(R3)+
	CLR	(R3)+
	CLR	(R3)
	CLR	R3

;-----
;Initialization for line scanning is done! Fake an RTI and wait for
;	someone to speak.
;-----

	MOV	#NXMERR, @#4		;All further nxm are fatal
	CLR	-(SP)			;Set PSW to 0
	JSR	PC, @#RTIX
SCAN:	BR	SCAN			;Wait here for first device to interrupt

.PAGE
.SBTTL	CHECK FOR VALID LOAD HEADER

;-----
; Entry here is from interrupting device.  This routine makes
; free use of registers because main program level does not
; use them.
;	DR header check routine.  If data is a valid prefix
; lock onto one DR, point vector to NXINPT, set state to
; 2 and initialize mem for NX boot.
;-----

DR3:	INC	R3			;Calc device address table offset
DR2:	INC	R3
DR1:	INC	R3
	ASL	R3
DR0:	MOV	DTAB-FUDGEX(R3), R4	;Load device CSR address
HDRCHK:	CMP	#PREFIX, DRIBUF(R4)	;Check for valid prefix word
	BEQ	2$			;Br if valid
	CLR	R3			;Must be set for DU's
	RTI				;Not header, try again

2$:
;-----
;Lock onto DR
;-----
	RESET				;Turn rest of devices off
	BIS	#100, (R4)		;Turn interrupts back on for this DR
	BIC	#2, (R4)		;Clr input done bit
	BIS	#2, (R4)		;Set input done bit
	MOV	VTAB-FUDGEX(R3), R3	;Get vector addr
	MOV	#NXINPT, (R3)		;Point vector to NX input routine
	CLR	R3			;R3 => device type
	MOV	#2, R5			;Fix state (just got prefix)
	BR	NXREL			;Br to NX common initialization

.PAGE
.SBTTL	CHECK DUV FOR VALID HEADER

;-----
; Entry here from DUx interrupt.
;-----

DU3:	INC	R3			;Calc device addr table offset
DU2:	INC	R3
DU1:	INC	R3
	ASL	R3
DU0:	MOV	DUTAB-FUDGEX(R3), R4	;Load device addr
	MOV	RXDBUF(R4), R1		;Copy data reg
	BPL	RDGOOD			;Br if no read errors
DISRTI:	CLR	TYSTAT(R3)		;Dismis interrupt, not locked yet
DISRTT:	CLR	R3			;Reset device index
	RTI				;Read err, no good

RDGOOD:	BIT	#1, @#FMTOGL		;Check load fmt
	BEQ	NXHDR			;Br if NX fmt
;-----
;TY format header check
;-----
	ADD	TYSTAT(R3), PC		;Dispatch on current device state
	BR	BY0
	BR	BY1
;-----
;	BR	BY2			;This byte must be zero
;-----
BY2:	TST	R1
	BNE	DISRTI			;Br if not header
	BR	TYLOCK			;Looks like TY format, Lock on

BY0:	CALL	TYDUV			;Reset device to sync on regular TY sync
BY1:	ADD	#2, TYSTAT(R3)		;Inc state
	BR	DISRTT			; And return

;-----
;NX format header check
;-----
NXHDR:	CMPB	#307, R1		;Check for valid header
	BEQ	NXDULK			;Br if valid
	BR	DISRTT			;Not header, try again

NXDULK:	RESET				;Turn off all interrupts
	CALL	NXDUV			;Init DUV
	MOV	VDTAB-FUDGEX(R3), R3	;Get vector addr
	MOV	#NXINPT, (R3)		;Load vector for NX input
	MOV	#1, R3			;Save device type
	CLR	R0			;Init state
.PAGE
;-----
; Reloc to top of memory
;-----
NXREL:	SUB	#NXSIZ, @#LODTOP
	MOV	@#LODTOP, R2		;
	MOV	#NXINIT-FUDGEX, R1	;Load source pointer
RELO:	MOV	(R1)+, (R2)+		;Relocate boot routine
	CMP	#NXEND-FUDGEX, R1	;Done?
	BNE	RELO			;Br till done
	CLR	@#RDERR			;Init for NX boot
	CLR	@#WRDRDY
	CLR	@#BYTEHI
	MOV	#NAK, @#LSTRSP
	MOV	@#LODTOP, R1
	JMP	(R1)			;Relocation done, jmp to boot routine

.PAGE
.SBTTL	TY FORMAT DEVICE LOCK & INITIALIZATON

;-----
;TY format device lock
;-----

TYLOCK:	RESET				;Turn off all interrupts
	CLR	@#TIMER			;Reset timer
	MOV	#TIMEC1, @#TIMEC	;Timeout = 20 sec
	CALL	TYDUV			;Init load device
	MOV	#26, (R4)		; "
	MOV	#SYNWRD, @#SYNC		;store sync word for retransmit
	SUB	#TYSIZ, @#LODTOP
	MOV	@#LODTOP, R2
	MOV	#TYINIT-FUDGEX, R1	;LOAD SOURCE POINTER
TYREL:	MOV	(R1)+, (R2)+		;Relocate routine
	CMP	#TYEND-FUDGEX, R1	;Done?
	BNE	TYREL			;Br till done
	MOV	@#LODTOP, R1
JMP	(R1)			;Jmp to TY boot routine

.PAGE
.SBTTL	DEVICE INDEPENDANT INITIALIZATION

;-----
;register assignments:
;R0=Temporary for data received
;R1=Nak/ack code
;R2=Word count for data
;R3=Device type		DR=0	DU=0
;R4=Address of device
;R5=State
;-----

;-----
;state:
; 0 wait for prefix
; 2 wait for count and packet type
; 4 wait for data


MAXDR=	300.				;maximum word count per DR11 data blk
MAXSIZ=	36				;Maximum word count per data block
ACK=	100307				;ack code
NAK=	140307				;nak code
SYNC=	426				;Sync charactor + TSOM bit for DUP-11
PREFIX=	2307				;prefix code for start of a block

CHKSM=	104				;block checksum
FLAGS=	106
LDADR=	110
TOGLE=	112				;Bit 0 is used as togle in acks
LSTRSP=	114				;Last response is an ACK or NAK
BYTEHI=	116				;Input byte selector
WRDRDY=	120				;Word ready for processing
RDERR=	122				;Read error flag
NEWWRD=	124				;New data word returned by NXINPT

;-----
; Bit definitions and protocal information
;-----

$RXMIT=	10000		;bit to set for rexmit response
			;setting this bit combines the ACK for the
			;rexmit request with the last response requested
$TOGLE=	400		;Sync bit for ACK responses. Bit is set for
			;initial ACK and is toggled on each ACK there after.

.PAGE

;-----
;Device independant initialization
;
;	R3 => Device type (0=DR, 1=DUV)
;	R4 => Device address
;-----

NXINIT:	CLR	@#TIMER			;Reset timer
	MOV	#TIMEC1, @#TIMEC	;Set for long timeout
	MOV	SP,@#FLAGS		;Make flags > 0
	MOV	#TRANAD,@#LDADR		;Transfer address = initial load addr
	CLR	@#TOGLE			;set togle to zero
	MOV	#STACK, SP		;REINIT STACK UPON RESET
	CLR	-(SP)			;Set processor priority to 0
	JSR	PC, @#RTIX

.PAGE
;-----
; Main program - loops till a word is ready
;-----

NXMAIN:	TST	@#WRDRDY		;Is a word ready?
	BEQ	NXMAIN			;Br if not
	TST	@#RDERR			;Check for read error
	BEQ	2$			;Br if err

;-----
; Read err, abort this block
;-----
	CLR	@#RDERR			;Dismis read err
	BR	NEWBLC			;Abort this block

2$:	CLR	@#WRDRDY
	TST	R3
	BEQ	4$
	MOV	@#NEWWRD, R0		;Get new data word
	BR	6$

4$:	MOV	4(R4), R0		;Copy DR data reg
	BIS	#2, (R4)		;Set Input done

;-----
; Dispatch on state
;-----

6$:	ADD	R5,PC			;update PC for jmp to required state
	BR	GETPRF			;0
	BR	GETCNT			;2
					;4

.PAGE
;-----
; Input n bytes of data
;-----

GETDAT:	DEC	R2			;Decrement word count
	BLT	HADBCS			;Count<0, data done. got bcs
	MOV	R0,(R1)+		;Count>=0, got data . store in mem
	ADD	R0,@#CHKSM		;Accumulate block checksum
	BR	NXMAIN			;Wait for next word

;-----
; Start program just loaded after waiting about 2 sec
;-----

STRTPR:	MOV	#40,R1
1$:	MOV	#141520,R0		;Wait for about 2 sec
2$:	DEC	R0
	BLT	2$
	DEC	R1
	BGT	1$
	MTPS	#340
	JMP	@#TRANAD		;start the new program

;-----
; Get prefix - Sync on prefix word
;-----

GETPRF:	CMP	#PREFIX,R0		;Is it the prefix?
	BNE	NEWBLC			;No. wait for it again.
INCSTA:	TST	(R5)+			;Increment state
	BR	NXMAIN			;Wait for next word

;-----
; Get word count and check for reasonable value
;-----

GETCNT:	CLR	@#CHKSM			;Clear block checksum
	MOV	R0,R2			;save word count
	BLE	SETFLG			;If not data block BR to set flags
	TST	R3			;If dr we dont check for blk size
	BEQ	1$			;
	CMP	#MAXSIZ,R0		;If oversize block
	BR	2$

1$:	CMP	#MAXDR,R0		;test for over size blk
2$:	BLT	SNAK			; br to snak
	MOV	@#LDADR,R1		;Update block load address
	BR	INCSTA			;Advance state

SETFLG:	MOV	R0,@#FLAGS		;Save flags and get checksum
	BR	INCSTA			;Advance state


;-----
; compare checksums
;-----

HADBCS:	CMP	R0,@#CHKSM		;Compare block cs's
	BNE	SNAK			;No match: send nak
	CLR	@#TIMER			;Good block, reset timer

;-----
; block is good, check for REXMIT_LAST_RESPONSE msg
;-----

	CMP	#-2,@#FLAGS		;Check for a retransmit request
	BNE	NRXMIT			;No, br to response code

;-----
; REXMIT_LAST_RESPONSE with rexmit bit set
;-----

	NEG	@#FLAGS			;Clr message flags
	MOV	@#LSTRSP,R1		;restore last response
	BIS	#$RXMIT,R1		;set retransmit bit in previous
					; response to indicate rexmit blk good
	BR	RXMIT			;send that last response again

;-----
; send ACK response
;-----

NRXMIT:	MOV	R1,@#LDADR		;update load address for next block
	MOV	#ACK,R1			;Put ack code in R1

;-----
; toggle sync bit in response.
;	note:	Bit is set for first msg. This bit is toggled
;		only for each block ACKed.
;-----

	INC	@#TOGLE
	BIT	#1,@#TOGLE
	BNE	1$			;br if toggle bit is clr
	BIS	#$TOGLE,R1		;set toggle bit

.PAGE
;-----
; Send ACK
;-----

1$:	CALL	SNDBAK			;send ack

; check for LAST_BLOCK msg and RESET msg

	TST	@#FLAGS			;Is last block flag set?
	BEQ	STRTPR			;Last one! lets start it.
	BLT	RESET

;-----
;new block - reset state and device if necessary
;-----

NEWBLC:	CLR	R5			;Reset state to 0 for new block
	TST	R3			;If DR we
	BEQ	NXMAIN			; wait for next word
BITWID:	CALL	NXDUV			;Reset DUV
	BR	NXMAIN			;Wait for next word

;-----
; NAK response
;-----

SNAK:	MOV	#NAK,R1			;No match:  put nak code in R1
RXMIT:	CALL	SNDBAK			;Send nak
	BR	NEWBLC

;-----
; RESET bootstrap.
;-----

RESET:	MTPS	#340
	CLR	R5			;Set state to 0
	TST	R3			;Is this a DR interface?
	BEQ	2$			;Br if so
	CALL	NXDUV			;Reset DUV
2$:	MOV	@#LODTOP, R0
	JMP	(R0)			;Reinit NX boot

.PAGE

;-----
; SNDBAK - Send response back and output routines.
;-----

SNDBAK:	TST	R3			;If DR we
	BEQ	DRSND			; BR to DR code
	MOV	#12,R0			;Set up to send 12 sync char
SNDSYN:	BIS	#20, TXCSR(R4)		;Set send bit
	CALL	XWAIT			;Wait till last xmition done and
	MOV	#SYNC,TXDBUF(R4)	; send sync
	DEC	R0			; till done
	BGE	SNDSYN			;
	MOV	R1,@#LSTRSP		;Save last response for possible rxmit
	CALL	SENDU			;Send data twice, as data and checksum
	CALL	SENDU			;
	CALL	XWAIT			;wait till done
	MOV	#SYNC,TXDBUF(R4)	;flush buffer
	CALL	XWAIT
	BIC	#20, TXCSR(R4)		;Clr send bit
	RETURN

XWAIT:	TSTB	TXCSR(R4)		;Wait for xmit done from DUV
	BPL	XWAIT
	RETURN

;-----
;SENDU - DUV output routine
;-----

SENDU:	CALL	XWAIT			;Wait till last xmition done
	MOVB	R1,TXDBUF(R4)		; send first byte
	SWAB	R1			;Swap to get second byte
	CALL	XWAIT			;When ready we
	MOVB	R1,TXDBUF(R4)		; send second byte
	SWAB	R1			;leave set for next entry
	RETURN				; and return

.PAGE
;-----
;DRSND - DR output routine
;-----

DRSND:	BIC	#1,(R4)			;Clear output ready bit
	MOV	R1,2(R4)	; Put ack or nak code in DRV output register
	BIS	#1,(R4)			;Set input ready bit in csr
	RETURN

;-----
;Nexilis format DUV initializer.
;	R4 => Device addr
;-----

NXDUV:	MOV	#400, TXCSR(R4)		;Hit master reset on DUV
	CMP	(PC), (PC)		; long nop while DUV resets
	MOV	#36026, PARCSR(R4)	;Init parameter csr
	MOV	#526, (R4)		;Init recieve csr
	MOV	#SYNC, TXDBUF(R4)
	RETURN

NXEND:

.PAGE
.SBTTL	TY FORMAT INITIALIZATION

;-----
;	DCP11 boot - tymnet format
;
;	register definition:
;	r0=	input data shift reg 0
;	r1=	input data shift reg 1
;	r2=	small loop counter
;	r3=	decoded data buffer pointer
;	r4=	device address
;	r5=	inverted data buffer pointer
;-----

SYNCHR=	114				;inverted sync chr
SYNWRD=	46114				;double sync chr
;-----
;	data buffers:
;	SYNC -> inverted data buffer for retransmission
;	DATCNT -> data buffer pending checksum verification
;-----

CHKSMV=	104				;computed vertical checksum
CHKSMD=	106				;computed diagonal checksum
SYNC=	110
INVDAT=	112
INVCKV=	206
INVCKD=	210
DATCNT=	TBUF
DATADR=	TBUF+2
DATA=	TBUF+4
CHKV=	TBUF+74				;vertical checksum
CHKD=	TBUF+76				;diagonal checksum
.PAGE
.SBTTL	TY FORMAT MAIN LOOP

;-----
; Initialize for new data block
;-----
TYINIT:	MTPS	#0			;Turn off interrupts
NEWBLK:	BIC	#20,(R4)		;clear SEARCH sync
	MOV	#26,(R4)		;set up recieve csr
	CLR	@#CHKSMV		;clr checksums
	CLR	@#CHKSMD		;
	MOV	#INVDAT,R5		;init inverted data buffer pointer
	MOV	#DATCNT,R3		;init decoded data buffer pointer
;-----
; Input data from DUx
;-----
2$:	TSTB	(R4)
	BPL	2$			;wait for data word
	MOV	RXDBUF(R4),R1		;COPY DATA AND ERR BITS FROM DEVICE
	BMI	NEWBLK			;BR ON READ ERR
	CMPB	#114,R1			;TEST FOR SYNC CHR
	BNE	NEWBLK			;BR IF NOT SYNC CHR
INPUT:	TSTB	(R4)			;wait for reciever done
	BPL	INPUT
	MOV	RXDBUF(R4),R1		;copy data and err bits from device
	BMI	NEWBLK			;br on read err
	MOVB	R1,(R5)+		;save inverted byte
	SWAB	R1			;save first byte in upper half
4$:	TSTB	(R4)			;wait for next byte
	BPL	4$			;
	MOV	RXDBUF(R4),R0		;copy data and err bits from device
	BMI	NEWBLK			;br on read err
	MOVB	R0,(R5)+		;save inverted byte
	BISB	R0,R1			;assemble word (bits are still inverted
	SWAB	R1			;bits now read 0-16 (inverted bitwise)
	CALL	INVRT$			;inverT bits end to end
;-----
;here the corrected word is stored in input buffer pending completion of frame
;and verification of checksums
;-----
	MOV	R0,(R3)+		;store word
	CMP	#CHKD+2,R3		;check for end of block
	BEQ	CHKSUM			;br when frame complete
	CLC				;calculate diagonal checksum
	ROL	@#CHKSMD
	ADC	@#CHKSMD
	XOR	R0,@#CHKSMD
	CMP	#CHKD,R3		;skip vertical checksum on last word
	BEQ	INPUT
	XOR	R0,@#CHKSMV		;calculate vertical checksum
	BR	INPUT			;wait for more data

.PAGE
.SBTTL	TY FORMAT CHECKSUM

;-----
;block is now complete! lets see if the checksums match
-
CHKSUM:	CMP	@#CHKSMV,@#CHKV		;check vertical checksum
	BNE	NEWBLK			;br if no match
	CMP	@#CHKSMD,@#CHKD		;check diagonal checksum
	BNE	NEWBLK			;br if no match
	CLR	@#TIMER			;Reset timer
;-----
;block is good, process it
;-----
	MOVB	@#DATCNT+1,R2		;test for boot block
	BEQ	LSTBLK			;br if boot block
	MOV	#DATA+1,R0		;get data block addr
	MOV	@#DATADR,R1		;get load addr starting with left byte
	BIT	#1,R1			;
	BEQ	2$
	DEC	R1
	BR	4$

2$:	INC	R1
4$:	CMP	#TRANAD,R1		;do not allow data to be loaded in buff
	BHI	FIXCKS			;skip data loading if < 1000
	CMP	@#LODTOP, R1		;Don't load data on boot
	BLO	BADLDA			;Br if bad
	BR	STORD$			;br to entry point in stor data

;-----
; Bad load address trap. An attempt to load over bootstrap will
; cause a halt at location BLATRP.
;-----

BADLDA:	JMP	@#BLAERR		;Jump to fatal error routine

.PAGE
.SBTTL	TY FORMAT DATA STORAGE

;-----
; load data in core with data decompression.
;	204=	initiate zero fill of n bytes
;	205=	initiate formfeed fill of n bytes
;	200=	load next chr as a literal
;	n=	byte following flag byte
;-----

STORDA:	DECB	R2			;dec byte count
	BLE	FIXCKS			;br if done with this block
STORD$:	CMPB	#200,(R0)		;test for literal flag
	BNE	TSTZFL			;br if not
	DECB	R2
	BIT	#1,R0			;skip magic byte
	BNE	2$
	ADD	#4,R0
2$:	DEC	R0
NORMDA:	MOVB	(R0),(R1)		;MOVE DATA BYTE
	BIT	#1,R0			;test for odd byte
	BNE	2$			;br if even
	ADD	#4,R0
2$:	DEC	R0
	BIT	#1,R1
	BNE	4$
	ADD	#4,R1
4$:	DEC	R1
	BR	STORDA			;br and check for more data

TSTZFL:	CMPB	#204,(R0)		;test for zero fill
	BNE	TSTFFL			;br if not
	CLRB	R5			;clr fill chr
	BR	FILL$			;br to fill entry point

TSTFFL:	CMPB	#205,(R0)		;test for formfeed fill
	BNE	NORMDA			;br if normal data
	MOV	#377,R5			;load fill char
FILL$:	BIT	#1,R0			;skip magic byte
	BNE	2$
	ADD	#4,R0
2$:	DEC	R0
	DECB	R2			;dec byte count
	CLR	R3
	BISB	(R0),R3			;GET BYTES TO FILL
	BIT	#1,R0
	BNE	4$
	ADD	#4,R0
4$:	DEC	R0
FILL:	MOVB	R5,(R1)		;load fill char
	BIT	#1,R1
	BNE	2$
	ADD	#4,R1
2$:	DEC	R1
	DEC	R3			;dec fill byte count
	BGT	FILL			;br if more to fill
	BR	STORDA			;br and check for more data

.PAGE
.SBTTL	TY FORMAT RESPONSE
;-----
;here the inverted checksum is corrected for rexmit
;-----
FIXCKS:	CLRB	@#INVDAT		;clr data count for retransmittion
	CLR	@#INVCKD
	CLR	@#INVCKV
	MOV	#INVDAT,R0
CALCKS:	CMP	#INVCKD,R0		;test for end of block
	BEQ	LSTBLK			;br if done
	MOV	(R0)+,R1		;get data
	CLC
	ROR	@#INVCKD
	BCC	2$
	BIS	#100000,@#INVCKD
2$:	XOR	R1,@#INVCKD		;calc diagonal checksum
	CMP	#INVCKD,R0
	BEQ	CALCKS
	XOR	R1,@#INVCKV		;calc vertical checksum
	BR	CALCKS

LSTBLK:	MOV	#SYNC,R5		;point to beginning of data buffer
	CALL	SNDBLK			;send data block
	TSTB	@#DATCNT+1		;test for boot block
	BEQ	BOOTND			;br if boot block
	JMP	NEWBLK			;do next block

.PAGE
BOOTND:	MTPS	#340
	JMP	@#TRANAD		;Boot node!

SNDBLK:	MOV	#20,TXCSR(R4)		;set send bit in DUx
	MOV	#577,TXDBUF(R4)		;set tsom bit for DUP
SNDBCK:	MOVB	(R5)+,TXDBUF(R4)	;send byte
2$:	TSTB	TXCSR(R4)		;wait till last chr sent
	BPL	2$
	CMP	#INVCKD+2,R5		;check for end of block
	BNE	SNDBCK			;br if not done
	MOVB	#377,TXDBUF(R4)		;pad chr
4$:	TSTB	TXCSR(R4)		;wait for xmit done bit
	BPL	4$
	CLR	TXCSR(R4)
	RETURN

;-----
;here the bits are flopped end to end since the T201 sends them backwards
;-----
INVRT$:	MOV	#16.,R2			;set up for invert bits loop
INVERT:	ROR	R1			;shift one bit into carry
	ROL	R0			;shift one bit from carry
	DEC	R2			;loop till all 16. bits are done
	BNE	INVERT
	RETURN
TYEND:
.PAGE
.SBTTL	SUBROUTINES

TYDUV:	MOV	#400, TXCSR(R4)		;Reset device
	CMP	(PC), (PC)		; Long nop
	MOV	#36114, PARCSR(R4)	;Init PARCSR
	BIC	#20, (R4)		;Clr search sync
	MOV	#526, (R4)		;Init RXVCSR
	RETURN

.PAGE
.SBTTL	Fatal error traps
;-----
; fatal error routines. These conditions will cause the machine to halt.
;-----

BLAERZ:
NXMERZ:	HALT				;Inform user about error of his ways

.PAGE
.SBTTL	TIMEOUT CHECKER

;-----
;Timeout checker - this routine is called once a second to
;	make sure loading is working. If not, restart line scanning.
;-----

TMRCHZ:	INC	@#TIMER

;-----
; Watchdog timer routine for tymshare
;-----
	INC	@#TIMERW
	CMP	#74, @#TIMERW		;See if watch dog timer needs poked
	BNE	2$			;Br if not
	CLR	@#TIMERW		;Reset timer
	MOV	#45, @#WATDOG		;Poke timer
2$:	CMP	@#TIMEC, @#TIMER	;Have we timed out?
	BNE	RTIZ			;Br if not

;-----
;Timed out!
;-----
	JMP	@#173000		;LINE SCAN RESTART

RTIZ:	RTI				;Back to main

.PAGE
;-----
; NX format input interrupt routine:
;-----

NXINPZ:	TST	@#WRDRDY		;Check to see if word has been read
	BNE	RERR			;Br if so
	TST	R3			;If DR input
	BEQ	READDR			; BR to DR inputr

;-----
;DUV input
;-----
	PUSH	R1
	TST	@#BYTEHI		;Test for high byte
	BNE	BYT2			;Br if byte 1
;-----
;First byte for DUV
;-----
	INC	@#BYTEHI		;Set high byte bit
	BIC	#400,(R4)		;Turn off strip sync
	MOV	RXDBUF(R4),R1		;Get data and error flags
	BMI	RERRR			;BR on read error
	MOVB	R1, @#NEWWRD		;Save first byte
	BR	RTIXX

;-----
;Second byte for DUV
;-----
BYT2:	CLR	@#BYTEHI		;Set for next word
	MOV	RXDBUF(R4), R1		;Copy data and flags
	BMI	RERRR			;Br if err
	MOVB	R1, @#NEWWRD+1		;Assemble data word
	INC	@#WRDRDY		;Set word ready flag
RTIXX:	POP	R1
	RTI

;-----
; Read error on DUV input, set read err flag
;-----
RERRR:	POP	R1
RERR:	INC	@#RDERR			;Set error bit
	RTI

;-----
;DR input
;-----
READDR:	BIC	#2, (R4)		;Clear input done bit
	INC	@#WRDRDY		;Set word ready flag
	RTI
ENDZ:

.PAGE
.SBTTL	DEVICE ADDRESS TABLE

;-----
;Device address table for all format boots
;-----

DTAB:	.WORD	DR0CSR			;DR0
	.WORD	DR1CSR			;DR1
	.WORD	DR2CSR			;DR2
	.WORD	DR3CSR			;DR3
DUTAB:	.WORD	DU0CSR			;DU0
	.WORD	DU1CSR			;DU1
	.WORD	DU2CSR			;DU2
	.WORD	DU3CSR			;DU3

;-----
; Vector addresses
;	This table must be arranged with the devices first and
;	in this order.  Others must follow
;-----

VTAB:	.WORD	DR0VEC			;DR0
	.WORD	DR1VEC			;DR1
	.WORD	DR2VEC			;DR2
	.WORD	DR3VEC			;DR3
VDTAB:	.WORD	DU0VEC			;DU0
	.WORD	DU1VEC			;DU1
	.WORD	DU2VEC			;DU2
	.WORD	DU3VEC			;DU3
	.WORD	4			;Nxm
	.WORD	100			;Clk
	.WORD	0			;This must be here!

;-----
;New PC table for vectors while line scanning.
;	The number of items in this table must equal
;	the number of items in vtab
;-----

NPCTAB:	.WORD	DR0-FUDGEX
	.WORD	DR1-FUDGEX
	.WORD	DR2-FUDGEX
	.WORD	DR3-FUDGEX
	.WORD	DU0-FUDGEX
	.WORD	DU1-FUDGEX
	.WORD	DU2-FUDGEX
	.WORD	DU3-FUDGEX
	.WORD	RTIX		;nxm
	.WORD	TMRCHK		;clk

ENDCOD:
	.END	START
                                   3 j“