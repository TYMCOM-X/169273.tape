	.SBTTL	DCPDBA ROM Contents
	.TITLE	DCP D-bus address decoder ROM

;  This source builds the contents of the D-bus address decoder for the
;  DCP unit.  The ROM contains 512 4-bit words.  Each 4-bit word specifies
;  the characteristics of a 2048-byte block of D-bus address space under
;  one of 16 combinations of four condition signals.  There are two
;  bits which independently enable the high- and low-order halves of each
;  RAM word location when the value of the bit is a zero.  There is one
;  bit which enables the external bus (X-bus) for memory-mapped I/O when
;  its value is a zero, and one bit to enable generation of a Q-bus
;  interrupt when its value is zero.
;
;  There are 9 ROM address inputs, 5 of which are used to select the 2k
;  byte block of D-bus address space.  These inputs are connected to the
;  5 highest-order address bits of the D-bus (DA15 thru DA11).  The
;  remaining 4 inputs of the ROM select the particular combination of the
;  following 4 conditions:
;
;	DA0 and DBHEL	These conditions indicate discriminate between
;			a high-order byte refereence, a low-order byte
;			reference, and a full word reference.  The low-
;			order byte is enabled by a zero on DA0, while the
;			high-order byte is enabled by a zero on DBHEL.
;			Exception: all slave references are word references,
;			no matter what the condition of these two bits are.
;	DIOL		This condition disables D-bus memory and devices
;			during an 8086 I/O cycle since these cycles
;			reference the Q-bus via DMA transfers.  A zero
;			on this line indicates an I/O reference, during
;			which memory is to be disabled.
;	HLDA		This condition indicates that the D-bus address
;			originates in the Q-bus slave circuitry, not in
;			the 8086.  This is important because not all D-bus
;			address lines are driven by the slave circuits.
;			Specifically, address lines DA13 and DA14 must be
;			ignored during slave references.

;  Initialize for our type of ROM; start ROM addresses at zero.
	ROMINIT	,,,<DCP D-bus address decoder>
	.DOT.=0				;Set starting ROM addr to zero

;  Define the ROM data bits used for each function.
	RAMH0L=D0			;D0 indicates high byte enable
	RAML0L=D1			;D1 indicates low byte enable
	XBUSS=D2			;D2 indicates X-bus enable
	INTDMA=D3			;D3 indicates interrupt/DMA

;  Define our INVERT mask - all outputs of this ROM are active-low except XBUSS.
	INVERT=RAMH0L!RAML0L!INTDMA

;  Define the ROM address bits.
	DA15=A8
	DA14=A7
	DA13=A6
	DA12=A5
	DA11=A4
	DA0=A3
	DBHEL=A2
	DIOL=A1
	HLDA=A0


;  We will define the ROM contents in order; we define all 16 combinations
;  of conditions for each 2K block before going on to the next block.  The
;  following macro is used to define 16 words of the ROM, handling all
;  combinations of conditions for one 2K byte block.  The DOIT macro has
;  one argument which may be any of the following character strings.
;
;	RAM	This block is occupied by RAM memory
;	SRAM	This block is occupied by RAM memory only for slave references
;	XBUS	This block maps to X-bus I/O devices
;	INTDMA	A read from any address in this block causes an interrupt
;		on the Q-bus; a write writes the low-order 6 data bits into
;		the DMA control register and initiates a Q-bus DMA request.
;
	.MACRO	DOIT,TYPE
	.DOIT1=0
	.IIF	IDN,TYPE,RAM,.DOIT1=RAMH0L!RAML0L
	.IIF	IDN,TYPE,XBUS,.DOIT1=XBUSS
	.IIF	IDN,TYPE,INTDMA,.DOIT1=INTDMA
	.DOIT2=.DOIT1 & ^CINTDMA		;Disable interrupts from slave
	.IIF	IDN,TYPE,SRAM,.DOIT2=.DOIT2!RAMH0L!RAML0L

	DATA				;Master I/O ref - disable memory
	DATA				;Slave I/O ref - should never happen
	DATA	.DOIT1			;Master memory word ref
	DATA	.DOIT2			;Slave memory word ref

	DATA				;Master I/O ref - disable memory
	DATA				;Slave I/O ref - should never happen
	DATA	<.DOIT1 & ^CRAMH0L>	;Master even byte ref
	DATA	.DOIT2			;Slave even byte ref (enable whole word)

	DATA				;Master I/O ref - disable memory
	DATA				;Slave I/O ref - should never happen
	DATA	<.DOIT1 & ^CRAML0L>	;Master odd byte ref
	DATA	.DOIT2			;Slave odd byte ref (enable whole word)

	DATA				;Master I/O ref - disable memory
	DATA				;Slave I/O ref - should never happen
	DATA				;Neither even nor odd - never happen
	DATA	.DOIT2			;Neither even nor odd - enable for slave

	.ENDM	DOIT


;  Here to define the 32 1K D-bus memory space blocks
	DOIT	RAM			;Block 0 is RAM
	DOIT				;Block 1 is reserved for optional RAM
	DOIT				;Block 2 is nothing
	DOIT				;Block 3 is nothing
	DOIT	SRAM			;Block 4: DA13 slave shadow of block 0
	DOIT				;Block 5 is nothing
	DOIT				;Block 6 is nothing
	DOIT				;Block 7 is nothing
	DOIT	SRAM			;Block 8: DA14 slave shadow of block 0
	DOIT				;Block 9 is nothing
	DOIT				;Block 10 is nothing
	DOIT				;Block 11 is nothing
	DOIT	SRAM			;Block 12: DA13!DA14 shadow of block 0
	DOIT	INTDMA			;Block 13 is Q-bus interrupt/DMA
	DOIT	XBUS			;Block 14 is X-bus I/O space
	DOIT	XBUS			;Block 15 is more X-bus I/O space
	.REPT	16.			;Blocks 16 thru 31 are ROM
	   DOIT
	.ENDR

	ENDRMC				;Mark end of .RMC file
	.END
