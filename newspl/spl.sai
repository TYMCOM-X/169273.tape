Begin "SPL"

require 0 lsh 18 lor 0 version;
require "(SAILIB)SAIL.DEF" source!file;
require "UUOSYM.DEF" source!file;
require "SPLDEF.DEF" source!file;
require "SPLFMT.DEF" source!file;
require "SPLCMD.DEF" source!file;
require "SPLARR.DCL" source!file;
require "SPLPAG.DCL" source!file;
require "SPLSUB.DCL" source!file;
require "SPLLIC.DCL" source!file;
require "SPLDAY.DCL" source!file;
require "SPLSTR.DCL" source!file;


Preset!with CvSIX("PEND"), CvSIX("HOLD"), CvSIX("TRAN"),
	    CvSIX("PROC"), CvSIX("TAPE"), CvSIX("TPRT"),
	    CvSIX("COMP"), CvSIX("COMP"), CvSIX("CANC"),
	    CvSIX("PEND"), CvSIX("PEND"), CvSIX("PEND"),
	    CvSIX("PROC"), CvSIX("PROC"), CvSIX("PROC"),
	    CvSIX("COMP"), CvSIX("COMP"), CvSIX("CANC");
	    Own safe integer array StatusCode[1:2,C!PEND:C!CANC];
Preset!with "ALL","CENTER","REMOTE","USER","FORMS";
	    Own safe string array Report!Sub[1:5];
Preset!with "ALL","CENTER","REMOTE","USER","FORMS","TAPE";
	    Own safe string array Print!Sub[1:6];
Preset!with "Valley Forge, PA",    "Houston, Texas",
	    "Lockheed        ",    "Cupertino,    CA",
	    "Paris,    France",    "Dallas,    Texas",
	    "Fremont,      CA",    "Cupertino,    CA",
	    "* eXperimental *";
	    Own safe string array CenterName[1:SiteCount];
Preset!with "V","H","L","C","P","D","F","C","X";
	    Own safe integer array SiteDesignator[1:SiteCount];
Preset!with 2,  3,  5,  6,  7,  47, 94, 1,  6;
	    Own safe integer array SitePrinterLoc[1:SiteCount];


Internal Own Integer Brk, BrkLin, BrkNam, BrkUsr, BrkWht, BrkBrk, BrkCmd;
Internal Own Integer RsDone, RsDate, RsNum, RsSite, RsFlag, RsDot;
Internal Own Integer RsCount, SixCrlf, Index, FrmLic, LudDist;
Internal Own Integer OptIndent, OptFullCase, OptHeading;
Internal Own Integer OptCopies, OptTransfer, OptQuest, OptFortran;
Internal Own Integer OptDouble, OptSite, OptPtrSite, OptPaper;
Internal Own Integer OptParts, OptDecolate, OptDeliver, OptCustomer;
Internal Own Integer OptChgOther, OptKatakana, OptLPPage;
Internal Own String Line, RsUser, RsDev, RsFile, RsForm, RsName, SvOper;
Internal Own Integer Array RsBlk[F!Usr:F!Xtr];

Own Integer Array RunBlock[R!Dev:R!Lic];
Own Integer RequestPPN, RequestStatus;
Own Integer UserAUN, UserPPN, UserDist, UserSite, UserNode, UserLoc;
Own Integer UserName1, UserName2, Count, CmdFlags;
Own Integer FrmPrv, UserLic, UserPrv, SetopLic, LudPrv;
Own Integer Tymshare, AcctSuper, TymnetLine, AuxCircuit;
Own Integer OperLic, RemoteOper, CenterOper;

Own String UserName, Command, PrinterName, PhoneAtPrinter, ThrowAway;

simple procedure BrkIni;
! ----------------------------------------------------------------------;
!									;
!	BrkIni		Define and initialize the breakset tables	;
!			to be used by various INPUT and SCAN calls	;
!			throughout the program.				;
!									;
! ----------------------------------------------------------------------;
begin
    Define ##Cmd = {";=/ "&#ht}
    ,      ##Wht = {" "&#ht&#cr}
    ,      ##Brk = {" !@#$%^&*()-_+=~`[]|\:;'<>,.?/" & '42 & '173 & '175}
    ,      ##Chr = {"ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz"}
    ,      ##Num = {"0123456789"}
    ,      ##Nam = {##Chr & ##Num}
    ,      ##End = {#lf&#vt&#ff&#cr&#esc};

    setbreak(BrkLin_Getbreak,#lf,         null, "SINF");  ! line;
    setbreak(BrkNam_Getbreak,##Nam,       crlf, "RXNF");  ! name or token;
    setbreak(BrkUsr_Getbreak, ")",        null, "SINK");  ! end of username;
    setbreak(BrkWht_Getbreak,#lf & ##Wht, crlf, "RXNK");  ! white space;
    setbreak(BrkBrk_Getbreak,#lf & ##Brk, #cr,  "RINK");  ! all break chars;
    setbreak(BrkCmd_Getbreak,#lf & ##Cmd, #cr,  "SINK");  ! command line;
end;
require BrkIni initialization;


simple procedure UserIni;
! ----------------------------------------------------------------------;
!									;
!	UserIni		Makes a snapshot of the current users known	;
!			environment so that the information may be	;
!			made available elsewhere.			;
!									;
! ----------------------------------------------------------------------x;
begin
    Integer T,Err;    String S;
    T _ calli( !Xwd( -1,!GTLOG ), calli!GETTAB );	! port info;
    AuxCircuit_ 0 = (T land ('17 lsh 24));		! Aux Circuit Flag;
    TymnetLine_ T land '177;				! Line # on Node;
    UserNode_ ((T lsh -8) land '77) lor ((T lsh -10) land '7700);
    T _ calli( !Xwd( !CNFG0,!GTCNF ), calli!GETTAB);	! system name (0);
    S _ Lop(S_Cv6str( T ));				! first character;
    UserSite_ DefaultUserSite;				! set default site;
    For T_ 1 step 1 until SiteCount			! then step through;
	do If Equ(S,SiteDesignator[ T ])		! table to set the;
	    then UserSite_ T;				! site, *if found*;
    UserLoc_  GetLoc( UserNode );			! try setting loc;
    PrinterName_ PtrLoc(If UserLoc neq 0		! then get it's name;
		    then UserLoc			! use loc if nonzero;
		    else SitePrinterLoc[UserSite],	! else use site name;
		   PhoneAtPrinter );			! get phone # too;
    FrmLic_ calli(!Xwd(-1,!GTLIC),calli!GETTAB);	! license;
    FrmPrv_ calli(!Xwd(-1,!GTPRV),calli!GETTAB);	! privileges;
    UserAUN_  calli(!Xwd(-1,!GTAUN),calli!GETTAB);	! account #;
    UserPPN_  calli(!Xwd(-1,!GTPPN),calli!GETTAB);	! ppn;
    UserName1_calli(!Xwd(-1,!GTUNM),calli!GETTAB);	! username 1st;
    UserName2_calli(!Xwd(-1,!GTUN1),calli!GETTAB);	! username 2nd;
    UserName_ Cv6str(UserName1) & Cv6str(UserName2);	! username str;
    If 0 leq Err_ LukLud( Null, LudDist, LudPrv )	! lud info;
     then usererr(0,1, "Lud error: "&CvS( Err ),"X");
    AcctSuper_ 0 neq (LudPrv land !bit(22));		! supervisor;
    Tymshare_  0 neq (LudPrv land !bit(21));		! tymshare prop;
    If LudDist = 0 and 0 neq (FrmLic land LC!OP)	! check for OPER;
     then begin
	OperLic_ !rh(FrmLic); CenterOper_ True; end;	! and set status;
end;
require UserIni initialization;


simple procedure Not!Imp;
begin
    Print( "Command Not Implemented", Crlf );
    RsDone_ True;
end;

simple boolean procedure ReqNumMatch;
! ----------------------------------------------------------------------;
!									;
!	ReqNumMatch	True if request number matches or is 0.		;
!									;
! ----------------------------------------------------------------------;
Return( RsNum = 0 or
	RsNum =	Cvd( CvXstr(!GDF( Request ) rot 6 )[1 for 5]) );


simple boolean procedure ReqUserMatch;
! ----------------------------------------------------------------------;
!									;
!	ReqUserMatch	True if username matches or is null.		;
!									;
! ----------------------------------------------------------------------;
Return( Length( RsUser ) = 0 or
	Equ( Cv6str( !GDF( RequestUser ) ) &
	     Cv6str( !GDF( RequestUser+1 ) ), RsUser )  );


simple boolean procedure GdfMatch;
! ----------------------------------------------------------------------;
!									;
!	GdfMatch	True if request numbers and/or usernames	;
!			match for this user and that in the current	;
!			GDF record being examined.			;
!									;
! ----------------------------------------------------------------------;
Return( ReqNumMatch and ReqUserMatch );


simple boolean procedure ReqPpnMatch;
! ----------------------------------------------------------------------;
!									;
!	ReqPpnMatch	True if AUN or PPN matches requestor PPN.	;
!									;
! ----------------------------------------------------------------------;
Return( UserAUN = !GDF( UserPPN ) or
	UserPPN = !GDF( UserPPN )    );


simple boolean procedure AcctOverride;
! ----------------------------------------------------------------------;
!									;
!	AcctOverride	True if the user is in the same GAN "and"	;
!			the user is an Account-Supervisor.		;
!									;
! ----------------------------------------------------------------------;
Return( AcctSuper and
	( !lh(UserAUN) = !lh(!GDF( UserPPN )) or
	  !lh(UserPPN) = !lh(!GDF( UserPPN ))    )  );


simple boolean procedure ReqDistMatch;
! ----------------------------------------------------------------------;
!									;
!	ReqDistMatch	True if district matches requestor district.	;
!									;
! ----------------------------------------------------------------------;
Return( LudDist = !lh(!GDF( District )) land '377 );


simple boolean procedure ReqLocMatch;
! ----------------------------------------------------------------------;
!									;
!	ReqLocMatch	True if printer loc matches requested location.	;
!									;
! ----------------------------------------------------------------------;
Return( UserLoc = !GDF( PrinterLoc ) land '777 );


simple boolean procedure SameSite;
! ----------------------------------------------------------------------;
!									;
!	SameSite	True if the print site (where the user is)	;
!			is the same as the request site.		;
!									;
! ----------------------------------------------------------------------;
Return( !GDF( OptionsRh ) land RsSite neq 0 );

simple procedure Command!QUIT;
! ----------------------------------------------------------------------;
!									;
!	"QUIT"		Exit from SPL and take care of the "funny"	;
!			array space build for mapping database files.	;
!			Also allow the user to type "CONTINUE"<CR>.	;
!									;
! ----------------------------------------------------------------------;
begin
    PageDn;			! cleanup so "START" works;
    Calli( 1, calli!EXIT );	! Bye;
    PageUp;			! Just in case you type "continue";
end;

simple procedure Command!RUN;
! ----------------------------------------------------------------------;
!									;
!	"RUN"		Run the specified program at the proper start	;
!			offset.  Possibly only from CCL mode.		;
!									;
! ----------------------------------------------------------------------;
begin
    ArrClr( RunBlock );
    RunBlock[ R!Dev ]_ RsBlk[ F!Dev ];
    RunBlock[ R!Nam ]_ RsBlk[ F!Nam ];
    RunBlock[ R!Ext ]_ RsBlk[ F!Ext ];
    RunBlock[ R!Ppn ]_ Location( RsBlk[ F!Usr ] );
    Calli( location(RunBlock[0]) + (If RPGSW then 1 lsh 18 else 0),
		 Calli!RUN );
    Print( "? run failed for file ",RsFile,".", Crlf );
    Command!QUIT;
end;

simple procedure Command!HELP;
! ----------------------------------------------------------------------;
!									;
!	"HELP"		Print out a semi-fancy HELP message.  This	;
!			takes into account the user's license and	;
!			what commands should be visible.		;
!									;
! ----------------------------------------------------------------------;
begin
    Integer K;
    Print( "Valid commands are:", Crlf, Crlf );
    For K _ 1 step 1 until CommandLength
     do If 0 = CmdFlag[ K ] land C$INVIS and
	   ( Operlic or 0 = CmdFlag[ K ] land C$OPER )
	 then Print( CmdMsg[ K ], Crlf );
    Print( "
Note:  Any of these commands may be shortened to any length
       which will allow the command to be uniquely recognized.
       Typing a ""?"" after any of the commands or questions
       asked will print out information to explain the command
       or question.

");
end;

simple procedure Command!IDENT;
! ----------------------------------------------------------------------;
!									;
!	"IDENT"		Maintanence command to display various info	;
!			about the current user's status for SPL.	;
!									;
! ----------------------------------------------------------------------;
begin
    Print( "Username: ", UserName,
	    "  PPN: ",Cvos(!lh(UserPPN)),",",Cvos(!rh(UserPPN)),
	    "  AUN: ",Cvos(!lh(UserAUN)),",",Cvos(!rh(UserAUN)),Crlf,
	    "License: <",Cvos(!lh(FrmLic)),">,<",
			 Cvos(!rh(FrmLic)),">  Setoplic: ",
			 Cvos( SetopLic ), Crlf,
	    "Priv word: ",Cvos(FrmPrv)," Lud: ",Cvos(LudPrv),Crlf,
	    "District: ",LudDist,
	    If CenterOper then "  Ctr" else "",
	    If RemoteOper then "  Rmt" else "",
	    If AcctSuper  then "  Sup" else "",
	    If Tymshare   then "  Tym" else "", Crlf,
	    "Default Center: ", CenterName[ UserSite ], "  ",
	    "Node or origin: ", Cvos(UserNode), ":", Cvos(TymnetLine),
	    If AuxCircuit then " (Aux)" else "", Crlf,
	    "Printer Location: ", UserLoc, Crlf,
	    PrinterName,
	    "Telephone: ", PhoneAtPrinter, Crlf, Crlf,
	    "RsUser: <", RsUser, ">", "	RsSite: <", RsSite, ">", Crlf,
	    "RsNum:  <", RsNum,  ">", "	RsForm: <", RsForm, ">", Crlf,
	    "RsDate: <", RsDate, ">", "	RsDone: <", RsDone, ">", Crlf );
end;

simple procedure Command!SET;
! ----------------------------------------------------------------------;
!									;
!	"SET"		Set operator status for a specified OPER name.	;
!									;
! ----------------------------------------------------------------------;
begin
    If Length( SvOper_ Token(Line) ) = 0
     then begin
	Print( "Oper Name: " );
	While "?" = SvOper_ Inchwl do
	    Print("
Enter your operator license name as it appears in the SETOP file.
Oper name: " );
     end;
    SetopLic_ If Length( SvOper ) then GetLic( SvOper ) else 0;
    CenterOper_ SetopLic land (LC!OP lsh -18);	! OP license;
    RemoteOper_ SetopLic land ('40 lsh 18);	! SP license;
    OperLic_ If CenterOper or RemoteOper	! Set OperLic if...;
	      then SetopLic lor !rh( FrmLic )	!  we are oper;
	      else 0;				!  else un-set it;
    Print(  If RemoteOper and not CenterOper
	     then "Remote o" else "O", "perator license ",
	    If OperLic then "" else "NOT ", "set", Crlf);
end;


simple procedure Command!CLEAR;
! ----------------------------------------------------------------------;
!									;
!	"CLEAR"		Clear operator status set by initialization or	;
!			by the user having set operator license.	;
!									;
! ----------------------------------------------------------------------;
begin
    Print( If RemoteOper and not CenterOper
	    then "Remote o" else "O", "perator license cleared", Crlf );
    OperLic_ CenterOper_ RemoteOper_ False;
end;

simple procedure Command!LOCATION;
! ----------------------------------------------------------------------;
!									;
!	"LOCATION"	Maintanence command to change the default	;
!			printer location to a specific site.		;
!									;
! ----------------------------------------------------------------------;
begin
    While TRUE
     do begin
	If Length( Line ) and SubSet( Line, "0123456789" )
	 then begin UserLoc_ Cvd( Line ); Done; end
	 else begin
	    If Length( Line )
	     then If "?" = Line
		then Print( "Enter the number for your default",
			    " printer location.", Crlf,
			    "a carriage-return means your",
			    " default printer location.", Crlf )
		else Print( "Location number expected.", Crlf);
	    Print( "Printer location: " );
	    If Length( Line_ Inchwl ) = 0 then done;
	 end;
     end;
    If Length( Line ) = 0 or UserLoc = 0 then UserLoc_ GetLoc( UserNode );
    PrinterName_ PtrLoc(If UserLoc neq 0 then UserLoc
		    else SitePrinterLoc[UserSite], PhoneAtPrinter );
end;


simple procedure Command!NODE;
! ----------------------------------------------------------------------;
!									;
!	"NODE"		Maintanence command to change the default	;
!			entry node.  Also changes the printer location	;
!			to the one used by the "new" node.		;
!									;
! ----------------------------------------------------------------------;
begin
    While TRUE
     do begin
	If Length( Line ) and "?" = Line
	 then Print( "Enter the Node number to use",
		     " for your pseudo-origin.", Crlf,
		     "a carriage-return defaults to",
		     " your logged-in node.", Crlf )
	 else UserNode_ AskNod( "Node Number: " );
	Line_ null;
     end;
    If UserNode = 0
     then UserNode_ GetNod;
    UserLoc_  GetLoc( UserNode );
    PrinterName_ PtrLoc(If UserLoc neq 0 then UserLoc
		    else SitePrinterLoc[UserSite], PhoneAtPrinter );
end;


procedure PrtGDF;
! ----------------------------------------------------------------------;
!									;
!	PrtGDF		Print the GDF record in the REPORT or STATUS	;
!			format.  This is also used by LIST prior to	;
!			displaying the file list.			;
!									;
! ----------------------------------------------------------------------;
begin
    Integer FileMult;
    SetOptLine;			! copy Opts;
    If RsCount = 0
     then Print( Crlf, "Request    Date   Stat  Loc  Username     Files  ",
	If OperLic then "Pages  Paper-Pts" else "Requestor",
	Crlf, Crlf );
    Print(CvXSTR( !GDF( Request ) lsh 6 ), "  ",
	DateSt( !lh( !GDF( Date ) ) ), "  ",
	CvXSTR( StatusCode[ If OperLic then 1 else 2,
			    !GDF( Request ) lsh -30] ),
	If OptPtrSite = 1
	 then "CEN  "
	 else if OperLic
		then "R"& CvS( 1000 +
			    ( !GDF( PrinterLoc ) land '777 )
					)[2 for 3] & " "
		else if ReqLocMatch
		      then "REM* "
		      else "REM  ",
	CvmSix( location(!GDF( RequestUser )), 12 ),
	("    "&CvS(FileMult_ !GDF( FileCount ) lsh -26)
		    )[inf-4 for 5], "   ");
    If OperLic
     then Print(
	("    "&CvS( 5 + (3*FileMult) + !GDF( Pages ) lsh -18)
		    )[inf-4 for 5],
	"  ",Case ( OptPaper ) of
	    ( [1] "SMALL-",	[2] "LARGE-",	[3] "  TTY-",
	      [4] "SPECIAL",[5] "SMA-WHI", [6] "LAR-WHI"),
	If OptPaper < 4 then CvS( OptParts ) else "",
	If OptFullCase then "-FC" else "", Crlf)
     else Print( CvmSix( location(!GDF( RequestName )), 24 ), Crlf );
    RsCount_ RsCount + 1;
end;

simple procedure Command!REPORT;
! ----------------------------------------------------------------------;
!									;
!	"REPORT"	Produce a 1-line status report about a list	;
!	"STATUS"	of requests.  The list may be a single request,	;
!	"LIST"		or a subset of the entire database.		;
!									;
! ----------------------------------------------------------------------;
begin
  If (ReqPpnMatch or				! ? same ppn or;
      OperLic or				! ? oper license or;
      AcctOverride or				! ? account superv or;
      (Tymshare and				! ? tymshare and;
	(ReqDistMatch or ReqLocMatch) ) )	!   same dist or site;
    and
     (0 = CmdFlags land C$STAT  or		! ? non-status reqired or;
	(!GDF( Status ) lsh -30) < C!Tape ) and	! ? status req: pending;
     (0 neq CmdFlags land C$LIST or SameSite)	! ? non-list same site;
   then begin
	PrtGDF	;				! print report;
	If 0 neq CmdFlags land C$LIST		! ? list files;
	 then FDFlst("Files spooled:",OperLic);	!  then do
	If RsNum neq 0 then RsDone_ True;	! If only 1 then done;
    end;
end;


simple procedure Command!HARDCOPY;
! ----------------------------------------------------------------------;
!									;
!	"HARDCOPY"	Print a full-sheet copy of each request that	;
!			the operator asks to print.  (One at a time).	;
!									;
! ----------------------------------------------------------------------;
begin
    If IsFrom					! ? from this date;
     then begin "got right GDF"
	RsCount_ RsCount + 1;			! count requests;
	RsDone_ True;				! flag we're done;
	ReqPrt( True, OperLic );		! print duplicate record;
     end "got right GDF";
end;

simple procedure Command!WHERE;
! ----------------------------------------------------------------------;
!									;
!	"WHERE"		Print out the address where the particular	;
!			"COMPLETED" request was printed.		;
!									;
! ----------------------------------------------------------------------;
begin
    If (CenterOper or				! ? center oper or;
	ReqPpnMatch or				! ? same user or;
	AcctOverride)   and			! ? users supervisor;
       (C!TPRT leq (!GDF( Status ) lsh -30) leq C!Comd)	! ? completed;
	and IsFrom				! ? from this date;
     then begin "got right GDF"
	Print( PtrLoc(
	    If !GDF( OptionsRh ) land '7 =	! ? requested = completed;
		(!GDF( OptionsRh ) lsh -3) land '3 and
	       !GDF( PrinterLoc ) land '177	! ? and remote printer;
	     then !GDF( PrinterLoc ) land '177	!  then use remote site;
	     else SitePrinterLoc[ UserSite ], ThrowAway),
	    Crlf );				!  else use site at center;
	RsCount_ RsCount + 1;			! count requests;
	RsDone_ True;				! flag we're done;
     end "got right GDF";
end;


simple procedure Command!HOLD;
! ----------------------------------------------------------------------;
!									;
!	"HOLD"		Puts a request on hold by setting the status	;
!			field in the MAP and GDF records to C!HOLD.	;
!									;
! ----------------------------------------------------------------------;
begin
    If (CenterOper or ReqLocMatch) and		! ? center oper or same loc;
        IsFrom					! ? from this date;
     then begin "check holding"
	Case !GDF( Status ) lsh -30
	 of begin "check status"
	    [C!PEND] begin "do hold"
		DPB( C!HOLD,
		     Point( 6, memory[MapAdr+((Count+'20) land '777)], 23 ));
		DPB( C!HOLD,
		     Point( 6, memory[Location(!GDF( Status ))], 5 ));
		Print( "Request put on hold.",Crlf);
	     end "do hold";
	    [C!HOLD] Print( "Request put on hold.",Crlf);
	    [else] Print( "Cannot hold that request.", Crlf)
	 end "check status";
	RsCount_ RsCount + 1;			! count requests;
	RsDone_ True;				! flag we're done;
     end "check holding";
end;

simple procedure Command!CANCEL;
! ----------------------------------------------------------------------;
!									;
!	"CANCEL"	Cancels a request.  Only the original user or	;
!			a center operator or the remote operator at the	;
!			requested printer may cancel a request.		;
!									;
! ----------------------------------------------------------------------;
begin
    RequestStatus_ !GDF( Status ) lsh -30;
    If IsFrom
     then begin "cancel request"
	If ( (RequestStatus leq C!TRAN and
	     ( ReqPpnMatch or (OperLic and LicOk) ) ) or
	     (RequestStatus leq C!TPRT and OperLic and LicOk) )
	 then begin "do cancel"			! if conditions ok, cancel;
	    DPB( C!CANC,
		 Point( 6, memory[MapAdr+((Count+'20) land '777)], 23 ));
	    DPB( C!CANC, Point( 6, memory[Location(!GDF( Status ))], 5 ));
	    If not ReqPpnMatch then begin	! if oper, record cancel;
		!GDF( Telephone   )_ CvSix( SvOper[1 to  6] );
		!GDF( Telephone+1 )_ CvSix( SvOper[7 to 12] );
	     end;
	    FDFdel;				! delete the files;
	    UpdCnt( -1 );			! update the spool count;
	    Print( "Request cancelled.", Crlf );
	 end "do cancel"
	 else Print( "Cannot cancel that request.", Crlf );
	RsCount_ RsCount + 1;			! increment counter;
	RsDone_ True;				! flag we're done;
     end "cancel request";
end;    


simple procedure Command!DELETE;
! ----------------------------------------------------------------------;
!									;
!	"DELETE"	Deletes a single file from a request.		;
!									;
! ----------------------------------------------------------------------;
Not!imp;

simple procedure Command!TRANSFER;
! ----------------------------------------------------------------------;
!									;
!	"TRANSFER"	Move a request with a transfer status from	;
!			the spooled site to another node which has a	;
!			printer associated with it.  Only center oper	;
!			and oper at the spooled site may transfer a	;
!			request.					;
!									;
! ----------------------------------------------------------------------;
begin
    Integer UseLoc, NewLoc;
    If C!TRAN = !GDF( Status ) lsh -30 and	! ? transfer request;
       (CenterOper or				! ? center oper;
         (!GDF( OptionsRh ) land '7 neq 1	! ? non-center request;
           and ReqLocMatch ) )			!   and same printer loc;
     then begin "transfer request"
	RsDone_ True;				! flag only do this once;
	RsCount_ RsCount + 1;			! count requests handled;
	UseLoc_ !Xwd(!GDF( OptionsRh ),!GDF( PrinterLoc )) land !Xwd('7,'7);
	Print( 	Crlf, "Transfer to: ",
		CvmAsc( location(!GDF( TransferLoc )), 25 ) );
	Do begin "get transfer node"
	    NewLoc_ AskNod( Crlf & "Enter transfer node: " );
	    NewLoc_ If NewLoc < '1000 or '4000 leq NewLoc < '5000
		     then SitePrinterLoc[ UserSite ]	! assume center;
		     else GetLoc( NewLoc );		! else remote loc;
	    If NewLoc = 0
	      then begin "No printer-Abort"
		Print( "No remote printer.", Crlf );	! let them know;
		Return;					! then leave;
	     end "No printer-Abort"
	     else Print( PtrLoc( NewLoc, ThrowAway ));	! try this one;
	end until Ask( "Okay? ");			! if not, try again;

	DPB( C!PEND, Point( 6, memory[MapAdr+((Count+'20) land '777)], 23 ));
	DPB( C!PEND, Point( 6, memory[Location(!GDF( Status ))], 5 ));
	DPB( 1, Point( 1, memory[Location(!GDF( OptionsRh ))], 27 ));
	DPB( NewLoc, Point( 8, memory[Location(!GDF( PrinterLoc ))], 35 ));
	DPB( If NewLoc = 0 then 1 else 2,
		Point( 3, memory[Location(!GDF( OptionsRh ))], 35 ));
	UpdCnt( -1, If !lh( UseLoc ) = 1 then 0 else !rh( UseLoc ) );
	UpdCnt(  1, -1 );			! update spool counts;
	SupCpy( OperLic );			! make Hardcopy;
	Print( "Request transferred.", Crlf );	! positive re-inforcement;
     end "transfer request";
end;

simple procedure Command!MOVE;
! ----------------------------------------------------------------------;
!									;
!	"MOVE"		Move a request from one printer site to center.	;
!			If you are Center Oper and GODGAN then you may	;
!			move the request "anywhere".			;
!									;
! ----------------------------------------------------------------------;
begin
    Integer UseLoc, NewLoc;
    If (CenterOper or				! ? center oper;
         (!GDF( OptionsRh ) land '7 neq 1		! ? non-center request;
           and ReqLocMatch ) )			!   and same printer loc;
     then begin "move request"
	RsDone_ True;				! flag only do this once;
	RsCount_ RsCount + 1;			! count requests handled;
	UseLoc_ !Xwd(!GDF( OptionsRh ),!GDF( PrinterLoc )) land !Xwd('7,'7);
	Do begin "get transfer loc"
	    NewLoc_ If CenterOper		! if qualified;
		then AskLoc( "To: " )		!  then ask for loc;
		else SitePrinterLoc[UserSite];	!  else default to center;
	    Print(PtrLoc( NewLoc, ThrowAway ));	! try this one;
	end until Ask( "Okay? ");		! if not, try again;
	DPB( NewLoc, Point( 8, memory[Location(!GDF( PrinterLoc ))], 35 ));
	DPB( If RemoteOper then 1 else 2,
		Point( 3, memory[Location(!GDF( OptionsRh ))], 35 ));
	UpdCnt( -1, If !lh( UseLoc ) = 1 then 0 else !rh( UseLoc ) );
	UpdCnt(  1, -1 );			! update spool counts;
	SupCpy( OperLic );			! make Hardcopy;
	Print( "Request transferred.", Crlf );	! positive re-inforcement;
    end "move request";
end;


simple procedure Command!SPOOL;		Not!imp;
simple procedure Command!PRINT;		Not!imp;
simple procedure Command!RESTART;	Not!imp;
simple procedure Command!STORE;		Not!imp;

Simple string procedure Perform!Command;
! ----------------------------------------------------------------------;
!									;
!    Perform!Command	This is the CRUX of the main program.  This	;
!			routine reads the user's command line an then	;
!			dispatches to the proper routine to perform	;
!			the specified command.  Initial data requests	;
!			for each command are also read and verified	;
!			by this routine before further processing is	;
!			allowed.					;
!									;
! ----------------------------------------------------------------------;
begin "perform command"
    Integer Cmd, Cdx, C, P;

	simple procedure One!Command;
	! ------------------------------------------------------;
	!							;
	!	One!Command	The table of commands to be	;
	!			dispatched to when ready.	;
	!							;
	! ------------------------------------------------------;
	begin
	    Case Cdx-1 of begin CommandList end;
	end;


    Line_ Inchwl;				! Get a command;
    If Length( Line ) = 0 then Return( Null );	! Ignore blank lines;
    Command_ Token( Line );			! Get the first word;
    If Equ(Command, "?") then Command_ "HELP";	! "?" means "HELP";
    Cdx _ 0;
    For Cmd _ 1 step 1 until CommandLength
     do If (OperLic or				! see if OPER required;
	    0 = CmdFlag[ Cmd ] land C$OPER)
	   and Equ( Command, CmdName[ Cmd ][1 for length(Command)] )
	 then If Cdx neq 0
		then Return( "Ambigious command." )
		else Cdx _ Cmd;

    If 0 < Cdx leq CommandLength
     then begin "setup command defaults"
	Scan( Line, BrkWht, Brk );		! remove any whitespace;
	If Line = "?"				! if first character is "?";
	 then Return( CmdMsg[ Cdx ] );		! give an help message;
	RsDone_ SixCrlf_ False;			! initialize done & LF flags;
	RsDate_ RsCount_ 0;			!  also dates and counts;
	RsNum_ RsFlag_ 0;			! clear counters for request;
	RsUser_ RsFile_ null;			!  and for username searches;
	Command_ CmdName[ Cdx ];		! copy name for later;
	CmdFlags_ CmdFlag[ Cdx ];		! copy flags for later;
	If (0 neq CmdFlags land C$REQ		! check if need REQUEST #;
	    and not GetReq) or
	   (0 neq CmdFlags land C$USER		! check if need USERNAME;
	    and not GetUsr) or
	   (0 neq CmdFlags land C$FILE		! check if need FILENAME;
	    and not GetFil) or
	   (0 neq CmdFlags land C$REP		! check if need LIST element;
	    and not GetLst(Report!Sub) ) or
	   (0 neq CmdFlags land C$PRINT		! check if need PRINT list;
	    and not GetLst(Print!Sub) ) or
	   (0 neq CmdFlags land C$SINCE		! check if SINCE wanted;
	    and not Since )
	 then Return( Command& " command aborted." );

	If 0 neq CmdFlags land C$MAP
	 then begin "per entry"
	    If (0 neq P_ (CmdFlags land C$MAP) lsh -(PS$MAP))
	     then if not OpnMAP(P land 3 )	! do we need the MAP;
		    then Return( "SPL error reading MAP" );
	    If (0 neq P_ (CmdFlags land C$GDF) lsh -(PS$GDF))
	     then if not OpnGDF(P land 3 )	! do we need the GDF;
		    then Return( "SPL error reading GDF" );
	    If (0 neq P_ (CmdFlags land C$FDF) lsh -(PS$FDF))
	     then if not OpnFDF(P land 3 )	! do we need the FDF;
		    then Return( "SPL error reading FDF" );
	    C_ FndMap( -1 );			! read the map count;
	    For Count_ C step -1 until 1	! for each GDF record;
	     do begin "each record"
		If RsNum neq 0			! for a specific request;
		 then Count_ FndMap(Count);	!   scan through the map;
		If Count > 0 and		! ? positive count;
		   0 < (Index_ GetGdf(Count)) and	! ? we can read it;
		   0 neq !GDF( Request ) and	! ? non-null request;
		   GdfMatch and			! ? requested GDF;
		   RsDate leq !lh(!GDF( Date ))	! ? date in range;
		 then One!Command;		!  then perform the command;
		If RsDone			! if only want one;
		 then Done;			!  then we are done;
	     end "each record";
	    If (0 neq CmdFlags land C$FDF)	! if we used the FDF;
	     then ClsFDF;			!  then close it;
	    If (0 neq CmdFlags land C$GDF)	! if we used the GDF;
	     then ClsGDF;			!  then close it;
	    If (0 neq CmdFlags land C$MAP)	! if we used the MAP;
	     then ClsMAP;			!  then close it;
	    If RsCount = 0			! if no requests found;
	     then Return( "No requests." );	!  then tell the user;
	 end "per entry"
	 else One!Command;

     end "setup command defaults"
     else Return( "Invalid command" );
    Return( Null );
end;


TTYUP( True );
Print(	"SPL version ",
	Cvos( !lh(Memory['137]) ), ".", Cvos( !rh(Memory['137]) ),
	If Memory['136] neq 0 then "-"&Cvos( Memory['136] ) else "",
	Crlf, Crlf );

While TRUE
 do begin
    String More;
    Print( If CenterOper then "Ctr" else "",
	   If CenterOper and RemoteOper then "+" else "",
	   If RemoteOper then "Rmt" else "",
	   If OperLic    then ">" else ":" );
    Print( More_Perform!Command );
    If Length(More) then Print( Crlf );
 end;

end;
" ú