;ROUTIN.KAB: THE ISOLATION ROUTINES
 
 
;SEQ747
 
 
.SBTTL ISOLATION ROUTINE SUBROUTINE FILE
        .IF NE,.REGSET
;SUBROUTINE TO ACTUALLY SET BITS IN THE ISOLATION ROUTINE'S
;BIT MAP TABLE. ASSEMBLE ONLY IF ROUTINE IS CALLED.
;ENTER ROUTINE WITH R1 HAS BIT REPRESENTING 'WHICH REG'
;TO SET (I.E. , AR,BR,AD,ARX,ETC.)
;ENTER ROUTINE WITH R0 POINTING TO THE ACTIVE BIT MAP TABLE.
;AND R3 POINTS TO ERROR STACK.
 
REGSET: PUSH    R0              ;SAVE A REGISTER
        SHOVE                   ;SAVE LOTS OF REGISTERS
        CMP     (R3)+,(R3)+     ;ADD 4 TO R3
        MOV     R3,100$         ;SET SOURCE ADDRESS(11) OF ACTUAL KL10 DATA
        MOVWRD                  ;AND MOVE IT AS FOLLOWS
100$:   .WORD   0               ;SOURCE ADDRESS FILLED IN
        .WORD   EBUF1           ;TO BE PLACED AT EBUF1
        CLR     R2              ;REGISTERS R2,R1 AND R0
        CLR     R1              ;WILL HOLD 36 BITS OF EXPECTED KL10 DATA
        BISB    6(R3),R2        ;GET BITS 24-27 OF EXPECTED
        SWAB    R2
        BISB    5(R3),R2        ;GET EXPECTED BITS 28-35
        BISB    10(R3),R1       ;GET EXPECTED BITS 4-11
        SWAB    R1
        BISB    7(R3),R1        ;GET EXPECTED BITS 12-19
        CLR     R0              ;CLEAN R0
        BISB    11(R3),R0       ;GET BITS 0-3
 
;SEQ748
 
;FIRST STEPS OF XOR (-A&B .OR. A&-B)
        MOV     #EBUF1,R4       ;GET ADDRESS OF ACTUAL RECEIVED DATA
        BIC     R2,(R4)+        ;(R4)=B...R2,R1,R0=A
        BIC     R1,(R4)+        ;PERFORM '-A&B'
        BIC     R0,(R4)+        ;USING 'BIC' INSTRUCTION
 
;(R3) ALSO POINTS TO ACTUAL KL10 DATA SO...
        BIC     (R3)+,R2        ;PERFORM THE STEP 'A&-B'
        BIC     (R3)+,R1        ;USING 'BIC' INSTRUCTION
        BIC     (R3)+,R0
 
;COMPLETE THE XOR BY 'ORING' OUR TWO RESULTS
        BIS     R0,-(R4)        ;LEAVE RESULT OF
        BIS     R1,-(R4)        ;XOR IN 36-BIT BUFFER
        BIS     R2,-(R4)        ;EBUF1
 
;NOW GET THE POINTER TO THE 36 BIT MASK
        MOV     -10(R3),R0      ;MASK PNTR WAS ON ERROR STACK
        BICB    (R0)+,(R4)+     ;MASK THE RESULTS
        BICB    (R0)+,(R4)+     ;OF THE XOR IN ORDER
        BICB    (R0)+,(R4)+     ;TO TURN IT INTO A
        BICB    (R0)+,(R4)+     ;'DISCREPANCY'
        BICB    (R0)+,(R4)+
        BICB    #360,-1(R4)     ;NO ODD BITS
        BICB    #377,(R4)       ;TURN 5 BYTES OF 36 BIT DATA INTO 3 WORDS
        GRAB    
        POP     R0              ;RESTORE ALL REGISTERS
 
;GO SET BITS IN THE BIT MAP TABLE - R1 HAD 'WHICH REG' BIT - R0 PNTS TO BIT MAP
 
        SHOVE                   ;SAVE SOME REGISTERS
        PUSH    R5              ;SAVE R5
        MOV     R0,R5           ;R5 WILL HOLD TABLE ADDRESS
        MOV     #EBUF1-1,R2     ;GET DISCREPANCY
        MOV     TURING,R3       ;WE'LL RUN A TURNING MACHINE
        MOV     #BRDPIK,R4      ;GET THE PNTR TO THE 'WHICH BOARD' MASKS
        PUSH    #6              ;COUNTER TO SAY WHEN ALL DONE
 
;SEQ749
 
;SIMPLY STATED, OUR TURING MACHINE MOVES LEFT ALWAYS, WITH
;REG R3 THE TAPE. THE 'C' BIT HOLDS THE ACTION. A '1' SAYS MAKS
;AND CHECK THE DISCREPANCY DATA FOR AN ERROR BIT. A '0' SAYS NO MASK
;ACTION, JUST UPDATE THE POINTERS.
 
1$:     ASL     R3              ;LOOK FOR THE NEXT ACTION
        BCC     4$              ;IF 'C'=0, GO UPDATE POINTERS
        BIT     #BIT0,R4        ;ODD OR EVEN ADDRESS IN R4
        BNE     3$              ;ODD ADDRESS SAYS DON'T DO ANYTHING
        INC     R2              ;EVEN ADDRESS SAYS SUPDATE R2 PNTR
3$:     MOVB    (R2),R0         ;DISCREPANCY DATA BYTE INTO R0
        BICB    (R4)+,R0        ;CHECK FOR AN ERROR BIT
        BEQ     1$              ;NO ERROR, GO BACK TO NEXT STATE
        BISB    R1,(R5)         ;ERROR FOUND, SET BIT IN BIT MAP TABLE
 
;UPDATE POINTERS HERE
 
2$:     JIFFO                   ;HOW MANY DISCREPANCY BITS FOR THIS BRD??
        DEC     R0              ;MORE THAN ONE?
        BLE     1$              ;BR IF ONLY ONE OR LESS
        BISB    R1,1(R5)        ;SO SET 'MORE THAN ONE' BIT IN BIT MAP TABLE
        BR      1$              ;AND CONTINUE
4$:     ADD     #2,R5           ;MAKE R5 EVEN
        DEC     (SP)            ;DOWN COUNTER
        BGT     1$              ;NEXT STATE IF NOT FINISHED
        POP     R3              ;KEEP STACK STRAIGHT
 
        POP     R5              ;RESTORE R5
        GRAB                    ;RESORE REGISTERS
        RTS     PC              ;AND OUT
 
TURING: .WORD   ^B1011011010101100
BRDPIK: .BYTE   300             ;BRD 39 (30-35)
 
 
 
;SEQ750
 
        .BYTE   77              ;BRD 41 (24-29)
        .BYTE   360             ;BRD 41
 
        .BYTE   17              ;BRD 43 (18-23)
        .BYTE   374             ;BRD 43
 
        .BYTE   3               ;BRD 49 (12-17)
 
        .BYTE   300             ;BRD 51 (6-11)
 
        .BYTE   77              ;BRD 53 (0-5)
        .BYTE   360             ;BRD 53
        .EVEN
        .ENDC
 
;SEQ751
 
        .IF NE,.SEVBRD          ;ASEMBLE CODE ONLY IF USED
 
;SUBROUTINE TO GO INTO A BIT MAP TABLE AND SEE HOW MANY
;BRDS OF THE SAME REG HAVE FAILED. 'WHICH REG' IS PASSED TO
;THE ROUTINE IN R0, AND BIT MAP TABLE IS AN IN-LINE PARAMETER
;FOLLOWING THE SUBROUTINE CALL. THE NUMBER OF BRDS HAVING
;A FAILURE IN THE SAME REG IS RETURNED IN R0.
 
SEVBRD: PUSH    R1              ;SAVE REG
        PUSH    #0              ;USE STACK FOR A COUNT
        MOV     (R5)+,R1        ;GET THE TABLE TO BE TESTED
 
1$:     BITB    R0,(R1)         ;BIT SET FOR THIS BRD & REG
        BEQ     2$              ;BR IF NO
        INCB    (SP)            ;ADD 1 TO TOTAL IF YES
 
2$:     INCB    1(SP)           ;THIS COUNT SAYS HOW MANY BRDS HAVE BEEN TESTED
        ADD     #2,R1           ;ADVANCE TO NEXT BOARD INFO
        CMPB    #^D6,1(SP)      ;TESTED ALL YET?
        BGT     1$              ;BR IF NOT ALL TESTED YET
        MOVB    (SP)+,R0        ;PUT TOTAL BRD ERRORS COUNT IN R0
        POP     R1              ;RESTORE REG
  
        RTS     R5              ;AND DONE
 
        .ENDC
 
        .IF     NE,.CLREDP
;SUBROUTINE TO CLEAR THE 6 EDP BOARDS FROM
;AN 'UML' TABLE, ENTER THE SUBROUTINE WITH R0
;POINTING TO TH 'UML' TABLE TO BE CLEARED.
 
CLREDP:
        PUSH    R1              ;SAVE REGISTER
        MOV     #UMLEDP,R1      ;GET PNTR TO A BUFFER
        BIC     (R1)+,(R0)+     ;PROCEED TO CLEAR ALL
        BIC     (R1)+,(R0)+     ;EDP UML BITS FROM
        BIC     (R1)+,(R0)+     ;THE DESIRED UML TABLE
        BICB    1(R1),1(R0)     ;DON'T BOTHER THE 'HI PRIORITY' BOARD
        POP     R1              ;RESTORE THE REG
 
 
        RTS     PC
 
UMLEDP: UML<39,41,43,49,51,53>
        .ENDC
 
 
        .IF     NE,.EDP
 
 
;SEQ776
 
;ROUTINE TO SET ONE OF 6 EDP BOARDS BITS 
;INTO ANY UML TABLE ANYWHERE.
EBRD39: SSET    39,TMPUML       ;SET EDP BRD (BITS 30-35)
        SETHP   39,TMPUML
        BR      BURNIN
 
EBRD41: SSET    41,TMPUML       ;SET EDP BRD (BITS 24-29)
        SETHP   41,TMPUML
        BR      BURNIN
 
EBRD43: SSET    43,TMPUML       ;SET EDP BRD (BITS 18-23)
        SETHP   43,TMPUML
        BR      BURNIN
 
EBRD49: SSET    49,TMPUML       ;SET EDP BRD (BITS 12-17)
        SETHP   49,TMPUML
        BR      BURNIN
 
EBRD51: SSET    51,TMPUML       ;SET EDP BRD (BITS 06-11)
        SETHP   51,TMPUML
        BR      BURNIN
 
EBRD53: SSET    53,TMPUML       ;SET EDP BRD (BITS 00-05)
        SETHP   53,TMPUML
        BR      BURNIN
 
 
 
BURNIN: OR.UML  #TMPUML,#STNDRD ;SET TMPUML INTO STANDARD UML TABLE
 
        RTS     PC              ;AND OUT
TMPUML: .BLKW   4
        .ENDC
;SEQ757
 
        .IF NE,.CLRUML
;ROUTINE TO CLR THE 4 WORDS OF AN UML TABLE
 
CLRUML: PUSH    #10
1$:     CLRB    (R0)+           ;CLEAR THE BYTE
        DEC     (SP)            ;SEE IF DONE 8. TIMES YET
        BGT     1$              ;BR IF NOT YET
        POP     R0              ;ALL DONE IF HERE..FX STACK
        RTS     PC              ;OUT
        .ENDC
 
        .IF NE,.BIC36
;ROUTINE TO DO A 36-BIT 'BIT CLR'
BIC36:  PUSH    <R0,R1>
        MOV     20(SP),R0               ;;GET SOURCE ADDRESS
        MOV     6(SP),R1                ;GET DEST ADDRESS
        BICB    (R0)+,(R1)+              ;DO THE BIT CLEAR
        BICB    (R0)+,(R1)+
        BICB    (R0)+,(R1)+
        BICB    (R0)+,(R1)+
        BICB    (R0)+,(R1)+
        POP     <R1,R0>                 ;RESTORE R0,R1
        MOV     (SP)+,(SP)              ;CLEAN STACK OF PASSED PARAMETERS
        MOV     (SP)+,(SP)
        RTS     PC                      ;AND RETURN
        .ENDC
 
 
;SEQ758
 
        .IF NE,.JIFF
 
;SUBRUTINE TO PERFORM JIFFO INSTRUCTION. INCLUDE THE CODE ONLY IF THE
;JIFFO INSTRUCTION IS USED IN THE ISOLATION ROUTINE - R0 HAS 16 BITS TO 'JIFFO'
 
JIFFO:  PUSH    R1              ;SAVE REG
        PUSH    #^D16                   ;SAVE COUNT OF HOW MANY BITS PER WORD
        CLR     R1              
1$:     ASL     R0              ;PUT A BIT IN THE 'C' BIT
        ADC     R1              ;ADD IT TO R1
        DEC     (SP)            ;DOWN COUNTER
        BGT     1$              ;BACK IF NOT DONE
        MOV     R1,R0           ;RETURN RESULT IN R0
        POP     R1              ;CLEAR CNT FROM STACK
        POP     R1              ;AND RESTORE R1
        RTS     PC              ;AND OUT
        .ENDC
 
 
;SEQ759
 
        .IF NE,.JLS
.REM    %
SUBROUTINE SEAGULL - POINT TO A UML TABLE AND POINT TO A DCOMP ERROR
STACK; SEAGULL WILL SCOPE OUT THE DCOMP FAILURE AND BY EXAMINING
FAILING DIAGNOSTIC FUNCTIONS AND EBUS BIT POSITIONS WILL SET THE UML TABLE BIT
TABLE BIT  FOR EVERY BOARD WHICH HAS PRODUCED AN ERRONEOUS SIGNAL OUT OF
SIGNAL OUT OF ITS DIAGNOSTIC MULTIPLEXORS. IT DOES THIS FOR EVERY
DCOMP INDEX/ACTUAL PAIR ON THE ERROR STACK.
CALLING SEQUENCE IS:
 
        ADDR-2 PUSH #UML        ;PUSH A PNTR TO DESIRED UML TABLE TO HAVE BITS SET
        ADDR-2  PUSH #ERRSTK    ;PUSH A PNTR TO THE 'FT3' ENTRY ON ERROR STACK
        JSR     PC,SEAGULL
 
ONLY R0 IS DESTROYED, ALL OTHER REGISTERS ARE LEFT INTACT.
THE STACK IS CLEANED UP.
NOTE: IF SEAGULL CAN'T FIND 'FT3' IT RETURNS WITH C-BIT SET.
%
 
SEAGULL:        CMP     @2(SP),#FT3     ;CHECK FOR A DCOMP ENTRY FIRST
        BNE     50$                     ;BR IF NONE - TAKE ERROR EXIT
        PUSH    R5                      ;START SAVING REGISTERS
        SHOVE
        MOV     14(SP),R5               ;GET ERR STACK PNTR TO R5
        MOV     R5,R4           ;COPY IN R4
        ADD     #12,R4          ;R4 WILL FOLLOW INDEX/ACTUAL PAIRS
 
;NOW GET EXPECTED & ACTUAL DATA AND DO XOR
 
1$:     CLR     R2              ;CLEAR WORKING REGISTERS
        CLR     R3
        MOV     (R4),R1         ;GET INDEX/ACTUAL PAIR
        BISB    R1,R2           ;COPY OF 'B' ACTUAL
        BISB    R1,R3           ;COPY OF 'B' ACTUAL
        SWAB    R1              ;MAKE ACTUAL/INDEX PAIR
        BIC     #177600,R1      ;MAKE IT INDEX ONLY
        PUSH    R1              ;STACK IT
        MOV     10(R5),R1               ;GET PNTR TO EXPECTED TABLE
        ADD     (SP),R1         ;GET PNTR TO EXPECTED ITSELF
        MOVB    (R1),R1         ;GET EXPECTED
        BIC     #177600,R1              ;OFF TRASH, R1='A' EXPECTED
 
;SEQ760
 
;XOR
 
        BIC     R1,R2           ;R2=-A&B
        BIC     R3,R1           ;R1=A&-B
        BIS     R1,R2           ;R2=XOR (DISCREPANCY)
        MOV     6(R5),R1        ;GET PNTR TO MASK TABLE
        ADD     (SP),R1         ;GET PNTR TO ACTUAL MASK
        BICB    (R1),R2         ;MASK XOR (NOW R2 HAS DISCREPANCY)
        BEQ     4$              ;BR IF NO DISCREPANCY
        $CLRWD  DISBUF          ;CLAR 36-BIT BUFFER
        MOV     4(R5),R0        ;GET PNTR TO BYTPNT TABLE
        ADD     (SP),R0         ;GET PNTR TO ACTUAL BYTPNT
        MOVB    (R0),R0         ;ACTUAL BYTPNT INTO R0
        MOV     R0,R1           ;COPY IN R1
        BIC     #177774,R0      ;MAKE R0 MOD 4
        MOVB    R2,DISBUF(R0)   ;SET DISCREPANCY IN 36-BIT BUFFER
        ASR     R1              ;BYTPNT INDEX GETS ROLLED
        ASR     R1              ;OFF THE END
        BEQ     3$              ;IF CNT IN R1=0, BYPASS NEXT CODE
2$:     $RORWD  DISBUF          ;ROTATE 36 BIT BUFFER
        DEC     R1              ;TO CORRECTLY JUSTIFY DISCREPANCY BYTE
        BNE     2$
3$:     MOV     2(R5),R0                ;GET PNT TO DIAG FCN TABLE
        ADD     (SP)+,R0                ;MAKE IT PNT TO ACTUAL DIAG FCN
        BISB    (R0),R1         ;GET DIAG FCN ITSELF INTO R1
        MOV     16(SP),-(SP)    ;PUSH DEST UML ONTO STACK
        MOV     #DISBUF,R0              ;ADDRESS OF DISCREPANCY IS IN R0
        JSR     PC,BIRDS        ;GO SET BITS
4$:     TST     (R4)+           ;ALL DONE?
        BGE     1$              ;BR IF NO
 
;FINISH UP CODE - CLEAN STACK
 
        GRAB            ;REGISTERS R1-R4
        POP     R5              ;AND R5
        POP     (SP)    ;POP ERR STACK PNTR DATUM
        POP     (SP)    ;POP ERR STACK PNTR DATUM
        CLC
        RTS     PC              ;RETURN
;ERROR EXIT
50$:    POP     (SP)            ;POP ERR STACK PNTR
        POP     (SP)            ;POP UML ADDRESS
        SEC                     ;SET 'C' BIT
        RTS     PC
        .ENDC
 
 
;SEQ761
 
        .IF NE,.BIRDS
.REM    %
BIRDS IS A SUBROUTINE TO ACTUALLY DO THE SETTING OF UML BITS FOR
ANY GIVEN DIAGNOSTIC FUNCTION AND 36 BIT ERROR DISCREPANCY.
CALLING SEQ IS:
        PUSH #UML TABLE ADDRESS
        MOV     ADDR OF 36 BIT DISCREPANCY,R0
        MOV     DIAG FCN,R1
        JSR     PC,BIRDS
%
 
BIRDS:  BIC     #177707,R1      ;NOW BEND
        ASR     R1              ;FOLD
        ASR     R1              ;SPINDLE & MUTILATE
        PUSH    R0              
        MOV     #TMPBUF,R0      ;CLEAR A TEMP UML TABLE
        CLR     (R0)+
        CLR     (R0)+
        CLR     (R0)+
        CLR     (R0)+
        POP     R0
        JMP     @DISPJ(R1)      ;AND DISPATCH TO SET THE UML BITS
DISPJ:  .WORD   PMCC    ;DIAG FCNS 100-107
        .WORD   AM      ;DIAG FCNS 110-117
        .WORD   EDP     ;DIAG FCNS 120-127
        .WORD   SIC     ;DIAG FCNS 130-137
        .WORD   CC      ;DIAG FCNS 140-147
        .WORD   VMAADR  ;DIAG FCNS 150-157
        .WORD   MBOX    ;DIAG FCNS 160-167
        .WORD   MBOX    ;DIAG FCNS 170-177
 
;SEQ762
 
PMCC:   BIT     #77,(R0)        ;CLK BOARD?
        BEQ     1$              ;NO
        SSET    CLK,TMPBUF      ;YES
 
1$:     BIT     #7400,(R0)      ;CTL BOARD?
        BEQ     2$              ;NO
        SSET    CTL,TMPBUF      ;YES
 
2$:     BIT     #170000,(R0)    ;MCL BOARD?
        BEQ     3$              ;NOT YET
        SSET    MCL,TMPBUF      ;YES
        BR      5$
 
3$:     BIT     #3,2(R0)        ;MCL BOARD?
        BEQ     4$              ;NO
        SSET    MCL,TMPBUF      ;YES
        BR      5$              ;END UP
 

4$:     SSET    PI,TMPBUF       ;MUST BE PI
5$:     JMP     GOAWAY
 
AM:     BIT     #7777,(R0)      ;MTR BOARD?
        BEQ     1$              ;NO
        SSET    MTR,TMPBUF      ;YES
        BR      2$
 
1$:     SSET    APR,TMPBUF      ;MUST BE AP
2$:     JMP     GOAWAY
EDP:    BIT     #77,(R0)        ;EDP 30-35?
        BEQ     1$              ;NO
        SSET    EDP30,TMPBUF    ;YES
 
1$:     BIT     #7700,(R0)      ;EDP 24-29?
        BEQ     2$              ;NO
        SSET    EDP24,TMPBUF    ;YES
 
2$:     BIT     #170000,(R0)    ;EDP 18-23?
        BEQ     3$              ;NOT YET
        SSET    EDP18,TMPBUF    ;YES
 
3$:     BIT     #3,2(R0)        ;EDP 18-23?
        BEQ     4$              ;NO
        SSET    EDP18,TMPBUF    ;YES
 
;SEQ763
 
4$:     BIT     #374,2(R0)      ;EDP 12-17?
        BEQ     5$              ;NO
        SSET    EDP12,TMPBUF    ;YES
        
5$:     BIT     #174000,2(R0)   ;EDP6-11?
        BEQ     6$              ;NO
        SSET    EDP6,TMPBUF     ;YES
 
6$:     BIT     #170000,2(R0)   ;EDP0-5?
        BEQ     7$              ;NOT YET
        SSET    EDP0,TMPBUF     ;YES
7$:             BIT     #17,4(R0)       ;EDP0-5?
        BEQ     8$              ;NO
        SSET    EDP0,TMPBUF     ;YES
 
8$:     JMP     GOAWAY
 
SIC:    BIT     #-1,(R0)        ;CON BOARD?
        BEQ     1$              ;NOT YET
        SSET    CON,TMPBUF      ;YES
 
1$:     BIT     #3,2(R0)        ;CON BOARD?
        BEQ             2$      ;NO
        SSET    CON,TMPBUF      ;YES
 
2$:     BIT     #374,2(R0)      ;IR BOARD?
        BEQ     3$              ;NO
        SSET    IR,TMPBUF       ;YES
 
3$:     BIT     #177400,2(R0)   ;SCD BOARD?
        BEQ     4$              ;NOT YET
        SSET    SCD,TMPBUF      ;YES
 
4$:     BIT     #3,4(R0)        ;SCD BOARD?
        BEQ     5$              ;NO
        SSET    SCD,TMPBUF      ;YES
 
5$:     JMP     GOAWAY
 
 
;SEQ764
 
CC:     BIT     #17,4(R0)       ;CRA BOARD?
        BEQ     1$              ;NOT YET
        SSET    CRA     ,TMPBUF ;YES
 
1$:     BIT     #140000,2(R0)   ;CRA BOARD?
        BEQ     2$              ;NO
        SSET    CRA,TMPBUF      ;YES
 
2$:     BIT     #17,(R0)        ;CRM16?
        BEQ     3$              ;NO
        SSET    CRM16,TMPBUF    ;YES
 
3$:     BIT     #1700,(R0)      ;CRM12?
        BEQ     4$              ;NO
        SSET    CRM12,TMPBUF    ;YES
 
 
;SEQ765
 
4$:     BIT     #170000,(R0)    ;CRM8?
        BEQ     5$              ;NO
        SSET    CRM8,TMPBUF     ;YES
 
5$:     BIT     #17,2(R0)       ;CRM4?
        BEQ     6$              ;NO
        SSET    CRM4,TMPBUF     ;YES
 
6$:     BIT     #1700,2(R0)     ;CRM0?
        BEQ     7$              ;NO
        SSET    CRM0,TMPBUF     ;YES
 
7$:     JMP     GOAWAY
 
VMAADR: SSET    VMAPC,TMPBUF    ;IF HERE - MUST BE VMA
        JMP     GOAWAY
 
MBOX:   BR      ZETURN          ;SUPER NO-OP
 
GOAWAY: MOV     2(SP),R0        ;GET PNT TO UML
        MOV     #TMPBUF,R1
        BIS     (R1)+,(R0)+     ;SET BITS IN
        BIS     (R1)+,(R0)+     ;CALLERS
        BIS     (R1)+,(R0)+     ;UMLE
        BIS     (R1)+,(R0)+     
ZETURN: MOV     (SP)+,(SP)      ;CLEAN STACK
        RTS     PC              ;RETURN
 
DISBUF: .BLKW   3
TMPBUF: .BLKW   4
.ENDC
 
 
 
    w cº