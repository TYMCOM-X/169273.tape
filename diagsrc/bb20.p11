
.SBTTL  MA20/MB20 2-WAY INTERLEAVE ERROR ISOLATION ROUTINE
        .NLIST  CND,BEX
.REM    %
        DGKBB ISOLATION ROUTINE FOR TEST#21.
        %
$M21:   JSR     R5,MINIT                ;INIT ISOLATION ROUTINE


;SEQ338

        FWDRDT                  ;CHECK FOR PMA ERROR
        CMP     4(R3),#FTM36    ;RD-RESTORE ERROR?
        BNE     3$              ;NO...MUST BE 4 WD RD ERR
        GPW     32              ;GET ERR WD #
        MOV     #300,R1         ;MAKE IT LOOK LIKE 4 WD RD ERR
        ASL     R0
5$:     BEQ     4$              ;GO TO 4 WD RD ERR RTN
        ROR     R1
        DEC     R0
        BR      5$
3$:     MOV     16(R3),R0       ;GET ACTUAL 8 BIT PTRN
        MOV     20(R3),R1       ;GET EXPECTED 8 BIT PTRN
        EXOR    R0,R1           ;GET DIF
4$:     BIC     #177400,R1
        CLR     R0              ;SET SWITCH TO IND TSTCON AT FAULT
        BIT     #63,R1          ;WAS IT A SCREW-UP ON ODD WDS?
        BNE     1$              ;YES...KEEP PTG TO TSTCON
        INC     R0              ;ELSE TOGGLE SW TO PT TO ALTCON
1$:     CMP     (R4),#1         ;ERR IN SUBTEST 1?
        BEQ     2$              ;YES...WE GOT IT RIGHT!
        INC     R0              ;ELSE REVERSE SENSE OF CONTRS AGAIN
2$:     BIC     #177776,R0      ;ONLY NEED BIT 0
        EXOR    R0,CONTR        ;1 OR 0 FOR ALTCON OR TSTCON
        CCI                     ;CALL IT OUT
        PMSG    <INTERNAL MEMORY INTERLEAVED 4 WORD READ FAULT DUE\>
        PMSG    <MOST PROBABLY TO CONTROLLER BOARD FAILURE.\>
        JMP     (R5)            ;RETURN TO DIACON


;SEQ339

;-----SUBROUTINE TO TAKE THE CONTR # IN R0 AND DUMP THE UML
;       DATA FOR THE SBUS INTERFACE BOARD FOR THAT CONTROLLER.
CCIN:   MOVB    CONTR,R0                ;GET CONTR #
        CMP     R0,#4           ;DMA20 CONTR?
        BNE     1$              ;R IF NO
        SSET    3,UML4          ;ELSE SET DTR BD (M8560) IN DMA20
        BR      3$              
1$:     BIT     #1,R0           ;EVEN OR ODD MA20?
        BNE     2$              ;BR IF ODD
        SSET    26.,UML4                ;SET MAC BD FOR EVEN MA20 CONTR
        BR      3$              
2$:     SSET    29.,UML4        ;SET MAC BD FOR ODD MA20 CONTR
3$:     MOVB    CNTBNT(R0),R0   ;GET & SET BACKPALEN #
        BACKPLANE
        RTS     R5
CCI:    JSR     R5,CCIN         ;SET UP FOR PRINT
        PNTUML  UML4
        RTS     R5              ;RETURN
;----SUBROUTINE TO SEE FIRST IF THE ERROR IN THE TEST WAS CAUSED
;       BY A PMA ERROR BEFORE ATTEMPTING TO ISOLATE THE FAILING MEMORY
        
;       CONTROLLER FOR A 4 WORD READ ERROR.
FWDRDT: CMP     (R3),#FTM36     ;MASK INFOR FIRST ON ERR STK?
        BEQ     1$              ;YES...MEANS FAULT CAUSD BY PMA ERROR
        GPW     0               ;ELSE GET FAILING CONTROLLER #
        MOVB    R0,CONTR                ;AND SAVE IT
        RTS     R5              ;RETURN TO CALLER
1$:     BACKPLANE       4               ;SET CPU BACKPLANE
        SSET    29.,UML1        ;PMA
        PNTUML  UML1
        PMSG    <FAILURE OF PMA BOARD TO GENERATE PROPER ADDRESS\>
        PMSG    <CAUSED 4 WORD READ TEST TO FAIL.\>
        RTS     PC              ;RETURN TO DIACON
;-----SUBROUTINE TO CHECK FOR AND EXTRACT A PARAMETER WORD FROM THE
;       ERROR STACK.  THE DATA PASSED TO THE SUBROUTINE IS THE DIS-
;       PLACEMENT OF THE 'FTPRM' OR 'FT10' ID FROM THE BEGINNING OF THE
;       ERROR STACK, THE ADR OF WHICH SHD B IN R3.
GPW:    MOV     (R5)+,R0        ;GET DISP TO FTPRM IDENTIFIER
GPW1:   ADD     R3,R0           ;CALC ADR OF IDENTIFIER
        CMP     (R0)+,#FTPRM    ;IS IT THE CORRECT IDENTIFIER?
        BEQ     2$              ;YES...OK
        CMP     -(R0),#FT10     ;ELSE MAYBE IT'S THE OTHER
        BEQ     1$              ;YES...OK
        PMSG    <ERR STK PARAM WD EXPECTED BUT NOT FOUND\>
        FATAL
1$:     CMP     (R0)+,(R0)+     ;BYPASS FT10 GARBAGE
2$:     MOV     (R0),R0         ;RETURN PARAMETER WORD IN R0
        RTS     R5              ;DONE
        .EVEN
$$FF:   $$CLIT                  ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
.END    STARTA
