.SBTTL  CONFIGURATION FORMAT CHECK ERROR ISOLATION ROUTINE
        .NLIST  CND,BEX.
.REM    %
        DGKBB ISOLATION ROUTINE, TEST #1.
        %
$M1:    JSR     R5,MINIT                ;INIT THE ISOLATION ROUTINE
;-----THIS ISOLATION ROUUTINE IS CALLED ON FAILURE LF DGKBB TEST#1, THE
;       MEMORY SYSTEM FORMAT CHECK.  DUE TO THE NATURE OF THE SBUS AND
;       ASSOCIATED EMMORY CONTROLLERS IT WILL NOT ALWAYS BE POSSIBLE
;       TO ISOLATE THE FIALING BOARD OR EVEN CONTROLLER.
        GPW     0               ;GET ERROR TYPE NUMBER FORE ERR STK
        PUSH    R0              ;SAVE IT
        BEQ     1$              ;IF ERR TYPE 0, SKIP OTHER PARAMS
        GPW     4               ;GET CONTROLLER #
        MOV     R0,CONTR        ;SAV IT
        GPW     8.              ;GET ADR OF "DMA20I" IN "MCON"
        MOV     R0,R2           ;SAV FOR USE AS BASE ADR
1$:     POP     R0              ;GET BACK ERR TYP
        ASL     R0
        JMP     @2$(R0)         ;GO TO CORRECT REOUTINE
2$:     .WORD   M1ET0,M1ET1,M1ET2,M1ET3, M1ET4
        .WORD   M1ET5,M1ET6,M1ET7,M1ET8,M1ET9
        .WORD   M1ET10


;SEQ242

;-----ROUTINE TO HANDLE SBUS BIT(S) STUCK AT 1.  THIS ROUTINE RETURNS
;       TO DIACON WHEN DONE.
M1RTN2: TST     MA20(R2)                ;ANY MA20 OR MB20?
        BNE     1$              ;BR IF YES...CAN'T ISOLATE
        CMP     DMA20(R2),#1    ;1 DMA20 & NO UNDEFINED TYPES?
        BNE     1$              ;NO...CANT ISOLATE
        SSET    3,UML4          ;DMA SBUS INTERFACE BD 'DTR' M8560
        BACKPLANE       1
        PNTUML  UML4            ;DMA20 SBUS INTERFAC BD IS HANGING SBUS
        PMSG    <THE DMA20 MEMORY CONTROLLER HAS SBUS DATA BIT\>
        MOV     R5,R0           ;BITS MSG ADR TO R0
        PNTAL                   ;PNT MSG
        PMSG    < STUCK AT 1 WHWEN NOT CROW-BARRED.\>
        POP     R5              ;GET BACK RET ADR
        JMP     (R5)            ;RETURN TO DIACON
1$:     PMSG    <A MEMORY CONTROLLER HAS SBUS DATA BIT\>
        MOV     R5,R0           ;BIT NUMBER MSG ADR TO R0
        PNTAL                   ;PNT MSG
        PMSG    < STUCK AT 1 WHEN NOT CROW-BARRED.\>
        PMSG    <SUGGEST TRYING A CONTROLLER "DISCONNECTION" SEARCH TO\>
        PMSG    <FIND FAILING CONTROLLER, THEN RESTARTING DGKBB AT THE\>
        PMSG    <APPROPRIATE TEST (SEE LISTING) TO DIAGNOSE IT.\>
        POP     R5              ;GET BACK RETURN ADR
        JMP     (R5)            ;RETURN TO DIACON

;-----SUBROUTINE TO COUNT THE NUMBER OF TIMES A PARTICULAR SITUATION
;       OCCURS IN THE CONFIGURATOR TABLES.
M1RTN3: MOV     (R5)+,1$+2      ;GET BIT MASK
        MOV     (R5)+,1$+4      ;GET TABLE DISPLACEMENT
        CLR     R0              ;CNT=0
        MOV     #37,R1
        ADD     R2,R1           ;ADR OF LAST BYTE IN TABLE
1$:     BITB    #000000,000000(R1)      ;TEST TABLE BITS
        BEQ     2$              ;BR IF ZERO
        INC     R0              ;ELSE COUNT
2$:     DEC     R1              ;NXT BYTE
        CMP     R1,R2           ;DONE?
        BHIS    1$              ;NO
        DEC     R0              ;TEST FOR <, >, OR = TO ZERO
        RTS     R5              ;RET TO CALLER


;SEQ243

;-----ERROR TYPE 0... NO  SRESPONSE TO SBUS DIAG FUNCTION 1.
M1ET0:  LOADAC  77700,2$        ;LOAD AC PRGM (AC6-16)
        STEXCT  2$              ;START AN SBUS DIAG
        FIND    <171,34.,0>     ;IS 'MEM DIAG L' BEING ASSERTED?
        BCC     3$              ;YES...OK
        BACKPLANE       4
        PNTUML  1$
        PMSG    <'MEM DIAG L' NOT BEING ASSERTED BY MBX BOARD CAUSING\>
        PMSG    <SBUS DIAG FAILURE.\>
        JMP     (R5)            ;RETURN TO DIACON
1$:     UML     <21.>           ;MBX
2$:     WD36    7005,0000,0016  ;6--SBUS DIAG
        I10     IOR,15,,17      ;7--ACCUMULATE RESULTS OF DIAGS FROM MEM
        I10     ADD,16,,14      ;10-NXT CONTR
        I10     TLNE,16,,760000 ;11-SKIP IF DONE
        I10     JRST,,,6        ;12-ELSE LOOP
        I10     JRST,4,,13      ;13-HALT WHEN DOEN
        WD36    0200,0000,0000  ;14-CONTR # INCR
        WD36    0000,0000,0000  ;15-SBUS DIAG 0 FROM MEM OR'ED INTO HERE
        WD36    0060,4000,0000  ;16-SBUS DIAG 0 TO SET INTRLY BITS
        .EVEN
3$:     BACKPLANE 4
        RUN     AC6             ;RUN AC PRGM
        EXAMT,AC15,WD1          ;GET CONTENTS OF AC15
        BIT     #30000,WD1+2    ;WERE ANY INTERLEAVE BITS SENT BACK?
        BNE     4$              ;YES...OK
        SSET    7,UML1          ;M8519 SBUS TRANSLATOR
        PNTUML  UML1

        SSET    3,UML2          ;M8560 IN DMA20
        BACKPLANE 1
        PNTUML  UML2
        PMSG    <THERE ARE 4 EXPLANATIONS FOR THE LACK OF RESPONSE TO\>
        PMSG    <SBUS DIAG FUNCTION 1:\>
        PMSG    <1. THE SBUS TRANSLATOR BOARD (M8519) ISN'T SENDING\>
        PMSG    <   OUT SBUS DIAG ON EITHER SBUS.\>
        PMSG    <2. THE TRANSLATOR ISN'T SENDING SBUS DIAG ON ONE SBUS\>
        PMSG    <   AND ALL MEM CONTROLLERS ARE ON THAT BUS.\>
        PMSG    <3.     THE TRANSLATOR IS WORKING AND THERE IS ONLY 1 MEM\>
        PMSG    <         CONTROLLER (PERFORCE THE DMA 20), AND THE SBUS\>
        PMSG    <       INTERFACE BOARD OF THAT CONTROLLER ISN'T WORKING.\>

        PMSG    <4. NO CLOCK IS GOING OUT ON THE SBUS\>
        JMP     (R5)            ;RETURN TO DIACON
4$:     SSET    8.,UML1         ;M8519  TRANSLATOR
        PNTUML  UML1
        BACKPLANE 1
        SSET    3,UML2          ;DMA20 SBUS INTERFACE BD M8560
        PNTUML  UML2
        PMSG    <THERE ARE TWO POSSIBLE EXPLANATIONS FOR NO RESPONSE\>
        PMSG    <TO SBUS DIAG 1:\>
        PMSG    <1. EITHER SBUS0 D35 OR SBUS1 D35 IS STUCK AT 0 AND\>
        PMSG    <  ALL CONTROLLERS ARE ON THAT SBUS.\>
        PMSG    <2. THERE IS ONLY ONE MEM CONTROLLER (PERFORCE THE\>
        PMSG    <  DMA20), AND IT CAN'T SEE SBUS D35 HIGH.\>
        JMP     (R5)            ;RETURN TO DIACON


;SEQ244
;-----ERROR TYPE 1... UNDEFINED MEMORY TYPE CONTROLLER FOUND.
M1ET1:  CMPB    UNDEF(R2),#1    ;MORE THAN 1 UNDEFINED CONTROLLER?
        BLE     2$              ;NO...CONTROLLER BAD
1$:     JSR     R5,M1TRN2       ;ELSE PROBLEM ON THE SBUS
        .ASCIZ  %D08 AND/OR D09%
        .EVEN
2$:     MOV     CONTR,R0                ;GET CONTR #
        CMP     R0,#4           ; > 4?
        PMSG    <A MEMORY CONTROLLER OF UNDEFINED TYPE HAS RESPONDED\>
        PMSG    <WHERE THERE SHOULD BE NO CONTROLLER.  PROBABLY\>
        BR      1$              ;PRINT BUS PROBLEMS INFO
3$:     CCI                     ;BAD CONTR INTERFACE
        PMSG    <MEMORY CONTROLLER SBUS INTERFACE IS REPORTING\>
        PMSG    <INCORRECT CONTROLLER TYPE\>
        JMP     (R5)            ;RETURN TO DIACON
;-----ERROR TYPE2...MEMORY CONTROLLER OF INCIRRECT YPE FOR
;       CONTROLLER NUMBER.
M1ET2:  MOV     CONTR,R0                ;GET CONTROLLER #
        CMP     R0,#4           ;EXISTING CONTROLLER?
        BLE     2$              ;YES
        PMSG    <A SUPPOSEDLY NON-EXISTANT MEM CONTROLLER (5-37) IS\>
        PMSG    <REPORTING A CONTROLLER BECAUSE EITHER A CONTROLLER\>
        PMSG    <IS RESPONDING TO THE WRONG CONTROLLER NUMBER(S), OR\>
1$:     JSR     R5,M1RTN2               ;SBUS BITS STUCK UP
        .ASCIZ  %D10 AND/OR D11%
        .EVEN
2$:     CCI                     ;CALL FAILING SBUS INTERFACE
        PMSG    <MEMORY CONTROLER GIVING INVALID CONTROLLER TYPE FOR\>
        PMSG    <ITS CONTROLLER # DUE TO EITHER BAD CONROLLER-SBUS\>
        PMSG    <INTERFACE, CONTROLLER RESPONDING TO WRONG CONTEROLLER\>
        PMSG    <NUMBER, OR >
        BR      1$


;SEQ245

;-----ERROR TYPE 3...MA20/MB20 REPORTS NO STORAGE MODULES.
M1ET3:  MOV     CONTR,R0                ;GET CONTR #
        BIT     #1,R0           ;EVEN OR ODD CONTR?
        BNE     1$              ;ODD
        SSET    26.,UML1
        SETHP   26.,UML1
        BR      2$
1$:     SSET    29.,UML1
        SETHP   29.,UML1
2$:     SSET    7.,UML2         ;FOR TRANSLATOR (M8519)
        MOVB    CNTBNT(R0),R0
        BACKPLANE
        PNTUML  UML1
        BACKPLANE 4
        PNTUML  UML2
        PMSG    <MA20/MB20 CONTROLLER REPORTS NO STORAGE MODULES PROBABLY\>
        PMSG    <DUE TO FAULT ON M8561 BOARD.  IT IS POSSIBLE BUT NOT\>
        PMSG    <LIKELY THAT THE SBUS TRANSLATOR BOARD HAS MULTIPLE\>
        PMSG    <BITS STUCK AT 0 ON THE SBUS ATTACHED TO THE FAILING\>
        PMSG    <MA20/MB20 CONTROLLER.\>
        JMP     (R5)            ;RETURN TO DIACON
;-----ERROR TYPE 4.  MA20/MB20 REPORTS STORAGE MODULES 4-7.
M1ET4:  JSR     R5,M1RTN3       ;HAPPENING ON MORE THAN 1 MA20/MB20?
        .WORD   360,MODNUM
        BGT     1$              ;YES...PROBABLY NOT CONTR
        CCI                     ;DUMP CONTR DATA
        PMSG    <MA20/MB20 CONTROLLER DUMPING ONES ON THE SBUS WHEN NOT\>
        PMSG    <CROWBARRED.\>
        JMP     (R5)            ;RETURN TO DIACON
1$:     JSR     R5,M1RTN2               ;HANDLE BUS PROBLEMS
        .ASCIA  %D00,D01,D02, AND/OR D03%
        .EVEN

;-----ERRROR TYPE 5...MA20/MB20 SBUS RQ ENABLES SET AFTER SBUS RESET.
M1ET5:  JSR     R5,M1RTN3               ;ERR ON >1 CONTROLLER?
        .WORD   377,SBUSEN
        BGT     M1ET9           ;YES...BUS ERR
        CCI                     ;DUMP CONTR DATA
        PMSG    <MA20/MB20 CONTROLLER NOT CLEARING RQ ENABLES DURING\>
        PMSG    <SBUS RESET.\>
        JMP     (R5)            ;RETURN TO DIACON


;SEQ246

;-----ERROR TYPE 6... DMA20 REPORTS STORAGE MODULES.
M1ET6:  BACKPLANE 1
        SSET    3,UML1
        PNTUML  UML1
        PMSG    <THE DMA20 IS REPORTING STORAGE MODULES\>
M1ET67: PMSG    <WHICH MAY BE DUE TO IT SENDING BACK AN "SBUS DIAG\>
        PMSG    <0 FROM MEM" WHEN AN "SBUS DIAG 1 TO MEM" WAS SENT.\>
        PMSG    <IT IS ALSO POSSIBLE THAT >
M1SM:   JSR     R5,M1RTN2               ;DUMP SBUS ERR STUFF
        .ASCIA  %D00-D07%
        .EVEN
;-----ERROR TYPE 7... DMA20 REPORTS SBUS RQ ENS AND/OR MARGIN SELS
M1ET7:  JSR     R5,M1RTN3               ;MORE THAN 1 OF THIS PROBLEM TYPE?
        .WORD   377,SBUSEN
        BGT     M1ET9           ;YES...BUS PROBLEMS
        PMSG    <THE DMA20 REPORTS RQ ENABLES AND/OR MARGIN SELECTS\>
        BR      M1ET67

;-----ERROR TYPE 8...NON-EXISTANT CONTROLLER REPORTS STORAGE MDULES
M1ET8:  MOVB    CONTR,R0        ;GET CONTR#
        CMP     R0,#3           ;CONTR 0-3?  (IE IS IT AN MA20/MB20?)
        BGT     M1SM            ;NO...BUS PROBLEMS
        CCI                     ;CALL OUT THE CORRECT MA20/MB20 CONTR BD
        PMSG    <EITHER THE MA20/MB20 CONTROLLER ISN'T GIVING THE\>
        PMSG    <CORRECT TYPE NUMBER, OR\>
        BACKPLANE 4
        SSET    7,UML1
        PNTUML  UML1
        PMSG    <SBUS DATA BIT 11 IS STUCK AT 0 ON THE SBUS CONNECTED\>
        PMSG    <TO THE GIVEN CONTROLLER.\>
        JMP     (R5)            ;RETURN TO DIACON

;-----ERROR TYPE 9... NON-EXISTANT CONTROLLER REPORTS RQ ENABLES
;       AND/OR MARGIN SELECTS.
M1ET9:  JSR     R5,M1RTN2
        .ASCIZ  %D28-D35%
        .EVEN

;-----ERROR TYPE 10...INTERNAAL MEMORY EVEN/ODD PAIR CONTROLLER TYPE
;       MISMATCH.
M1ET10: MOV     CONTR,R0                ;GET CONTROLLER #
        MOV     CNTBNT(R0),R0   ;GET BACKPLANE #
        BACKPLANE                       ;AND SET IT FOR PNTUML
        PNTUML  1$
        PMSG    <THE TWO CONTROLLERS OF AN EVEN/ODD PAIR ARE REPORTING\>
        PMSG    <DIFFERENT TYPES.  THE FAULT IS PROBABLY IN THE ODD\>
        PMSG    <CONTROLLER OF THE PAIR.\>
        JMP     (R5)            ;RETURN TO DIACON

1$:     UML     <29.,26.>       ;MAC (ODD), MAC (EVEN)


;SEQ247

;-----SUBROUTINE TO TAKE THE CONTR # IN R0 AND DUMP THE UML
;       DATA FOR THE SBUS INTERFACE BOARD FOR THAT CONTROLLER.
CCIN:   MOVB    CONTR,R0                ;GET CONTR #
        CMP     R0,#4           ;DMA20 CONTR>
        BNE     1$              ;BR IF NO
        SSET    3,UML4          ;ELSE SET DTR BD (M8560) IN DMA20
        BR      3$
1$:     BIT     #1,R0           ;EVEN OR ODD MA20?
        BNE     2$              ;BR IF ODD
        SSET    26.,UML4        ;SET MAC BD FOR EVEN MA20 CONTR
        BR      3$
2$:     SSET    29.,UML4                ;SET MAC BD FOR ODD MA20 CONTR
3$:     MOVB    CNTBNT(R0),R0   ;GET & SET BACKPLANE #
        BACKPLANE
        RTS     R5
CCI:    JSR     R5,CCIN         ;SET UP FOR PRINT
        PNTUML  UML4
        RTS     R5              ;RETURN
;-----SUBROUTINE TO CHECK FOR AND EXTRACT A PARAMETEF WORD FROM THE
;       ERROR STACK.  THE DATA PASED TO THE SUBROUTINE IS THE DIS-
;       PLACEMENT OF THE 'FTPRM' OR 'FT10' ID FROM THE BEGINNING OF THE
;       ERROR STACK.  THE ADR OF WHICH SHD B IN R3.
GPW:    MOV     (R5)+,R0                ;GET DISP RO FTPRM IDNETIFIER
GPW1:   ADD     R3,R0           ;CALC ADR OF IDENTIFIER
        CMP     (R0)+,#FTPRM    ;IS IT THE CORRECT IDENTIFIER?
        BEQ     2$              ;YES...OK
        CMP     -(R0),#FT10     ;ELSE MAYBE IT'S TJE OTHER
        BEQ     1$              ;YES...OK
        PMSG    <ERR STK PARAM WD EXPECTED BUT NOT FOUND\>
        FATAL
1$:     CMP     (R0+,(R0)+      ;BYPASS FT10 GARBAGE
2$:     MOV     (R0),R0         ;RETURN PARAMETER WORD IN R0
        RTS     R5              ;DONE


;SEQ248

;-----DGKBB ISOLATION ROUTINE INITIALIZATION.
MINIT:  MOV     #UML1,R0        
1$:     CLR     (R0)+           ;CLR OUT SCRATCH AREA
        CMP     R0,#SCREND
        BLO     1$
        BACKPLANE       4       ;SET DEFAULT BACKPLANE TO CPU
        RTS     R5              ;ISOLATION ROUTINE INIT DONE
;-----DISPLACEMENTS OF DATA IN "MCON" FROM THE SYMBOL "DMA20I"
DMA20I=0
DMA2TS=DMA20I+2
DMA20T=DMA2TS+2
DNXMT=DMA20T+100
DMA20=DNXMT+100
UNDEF=DMA20+1
MA20=UNDEF+1
MB20=MA20+1
MX20=BM20+1
MF20=MX20+1
MDCP=MF20+1
STRFLG=MDCP+14
TYPNUM=STRFLG+2
MODNUM=TYPNUM+40
SBUSEN=MODNUM+40
;-----DGKBB ISOLATION ROUTINES UTILITY DATA
CNTBNT: .BYTE   5,5,3,31        ;CONTROLLER # TO BACKPLANE # CONVERT
        .EVEN
;-----SCRATCH AREA FOR ISOLATION ROUTINES.
UML1:   UML     <>              ;SCRATCH UML TABLES
UML2:   UML     <>
UML3:   UML     <>
UML4:   <>
ADR1:   .WORD   0,0,0           ;SCRATCH AREAS FOR ADDRESSES

ADR2:   .WORD   0,0,0
ADR3:   .WORD   0,0,0
ADR4:   .WORD   0,0,0
WD1:    .WORD   0,0,0           ;SCRATH AREAS FOR 36 BIT DATA WORDS
        
WD2:    .WORD   0,0,0   
WD3:    .WORD   0,0,0
WD4:    .WORD   0,0,0
CONTR:  .WORD   0               ;MEMORY CONTROLLER NUMBER
SCREND=.                ;END OF SCRATCH AREA
        .EVEN
$$FF:   $$CLIT          ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
.END    STARTA
 