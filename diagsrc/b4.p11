;SEQ235

.SBTTL          MBOX TEST 4 ISOLATION ROUTINE
.REM    %
        ISOLATION ROUTINE FOR DEVIATIONS FROM THE MBOX MASTER
RESET STATE.  SCANS THE ERROR STACK TO DETERMINE WHICH SIGNALS
ARE INCORRECT AND APPLIES AN ANALYSIS OF FANOUTS FROM LOGIC
NODES WHICH ARE AFFECTED BY MASTER RESET TO DETERMINE PROBABLE
FAILING BOARD(S).
%
A4:     MOV     R3,R0           ;GET ERROR STACK POINTER
        CMP     (R0),16         ;SNAPSHOT ERROR?
        BEQ     1$              ;YES
        JMP     20$             ;NO, EBUS REG
1$:     CMP.F   A4TAB1          ;CHK FOR 1-BOARD ITEMS
        BCS     2$              ;FOUND ONE, PRINT UML
        CMP.F   A4TAB2          ;CHK FOR MEM START
        BCC     3$              ;NOT FOUND
        SSET    29,(R0)         ;SET MBC HIGH PROB
        SETHP   22,(R0)         ;SET MCB HIGH PROB
2$:     JMP     19$             ;PRINT UML
3$:     CMP.F   A4TAB3          ;CHK MEM RD RQ IN
        BCC     4$              ;NOT FOUND
        SSET    23,(R0)         ;SET CSH BOARD
        BR      5$

4$:     CMP.F   A4TAB4          ;TEST MEM RQS IN
        BCC     6$              ;NOT FOUND
5$:     SSET    29,(R0)         ;SET PMA BOARD
        SETHP   21,(R0)         ;MBX HIGHEST PROB
        JMP     19$             ;PRINT UML
6$:     CMP.F   A4TAB5          ;CHK CHAN CORE BUSY
        BCC     7$              ;NOT FOUND
        SSET    23,(R0)         ;SET CSH BOARD
        SETHP   20,(R0)         ;MBZ HIGHEST PROB
        JMP     19$             ;PRINT UML
7$:     CMP.F   A4TAB6          ;TEST MORE 1-BOARD ITEMS


;SEQ236

        BCS     2$              ;FOUND ONE, PRINT UML
        CMP.F   A4TAB7          ;CHK ACKN PULSE
        BCC     9$              ;NOT FOUND
        SSET    7,(R0)          ;SET BOARD CALLOUTS
        SSET    8,(R0)          ;
        SSET    2,(R0)          ;
        SSET    3,(R0)          ;
        JSR     PC,UMLPNT       ;PRINT CALLOUT
        MOV     #A4MSG1,R0      ;PRINT MESSAGE
        PNTAL
        JMP     (R5)            ;RETURN
9$:     CMP.F   A4TAB8          ;CHK CORE DATA VALID -2
        BCC     10$             ;NOT FOUND
        SSET    7,(R0)          ;SET SBUS TRANS
        SSET    8,(R0)          ;
        JMP     19$             ;
10$:    CMP.F   A4TAB9          ;CHK CSH ADR PMA EN
        BCC     11$             ;NOT FOUND
        SSET    21,(R0)         ;SET MBX BOARD
        JMP     19$             ;PRINT UML


;SEQ237

11$:    CMP.F   A4TB10          ;CHK EBUS REG 34, 35
        BCC     12$             ;NOT FOUND
        SSET    29,(R0)         ;SET PMA BOARD
        SETHP   29,(R0)         ;PMA HIGHEST PROB
        JMP     19$             ;PRINT UML
12$:    CMP.F   A4TB11          ;CHK MORE 1-BOARD ITEMS
        BCC     13$             ;NOT FOUND
        JMP     19$             ;PRINT UML
13$:    CMP.F   A4TB12          ;CHK MBOX ADR PAR ERR
        BCC     14$             ;NOT FOUND
        SSET    8,(R0)          ;SET BOARD CALLOUTS
        SSET    3,(R0)          ;
        JSR     PC,UMLPNT       ;PRINT UML
        MOV     #A4MSG2,R0      ;PRINT MESSAGE
        PNTAL
        JMP     (R5)            ;RETURN
14$:    CMP.F   A4TB13          ;CHK MBOX SBUS ERR
        BCC     15$             ;NOT FOUND
        SSET    7,(R0)          ;SET BOARD CALLOUTS
        SSET    2,(R0)              ;
        JSR     PC,UMLPNT               ;PRINT BOARD CALLOUTS
        MOV     #A4MSG3,R0      ;PRINT MESSAGE
        PNTAL
        JMP     (R5)            ;RETURN
15$:    CMP.F   A4TB14          ;CHK CSH VAL WR PULSE
        BCC     16$             ;NOT FOUND
        SSET    23,(R0)         ;SET CSH BOARD
        JMP     19$             ;PRINT UML


16$:    CMP.F   A4TB15          ;XCHK CSH PAR BIT A
        BCC     17$             ;NOT FOUND
        JSR     PC,CSHPNT               ;PRINT BOARD CALLOUTS
        MOV     #A4MSG4,R0      ;PRINT MESSAGE
        JMP     (R5)            ;RETURN
17$:    CMP.F   A4TB16          ;SPEC TABLE POINTER
        BCC     18$             ;NOT FOUND
        JSR     PC,CSHPNT       ;PRINT BOARD CALLOUTS
        MOV     #A4MSG5,R0      ;PRINT MESSAGE
        PNTAL
        JMP     (R5)            ;RETURN
18$:    MOV     4(R4),R0        ;GET LEFTOVERS
19$:    JSR     PC,UMLPNT       ;PRINT BOARD CALLOUTS
        JMP     (R5)            ;RETURN
20$:    CMP     (R0),6          ;CORRECT FORMAT?
        BNE     23$             ;NO
        CLR     A4UML           ;CLEAR UML TABLE
        CLR     A4UML+2         ;
        CLR     A4UML+4         ;
        CLR     A4UML+6         ;
        SSET    29,A4UML        ;SET PMA BOARD

        SETHP   29,A4UML        ;HIGHEST PROB.
        CMPB    4(R0),11(R0)    ;ERROR IN BITS 27-35?
        BEQ     21$             ;NO
        BIT     #3,4(R0)        ;BITS 34 & 35
        BNE     21$             ;YES
        SSET    22,A4UML        ;SET MBC BOARD
        BR      22$             ;
21$:    SSET    20,A4UML        ;SET MBX BOARD
22$:    PNTUML  A4UML
23$:    JMP     (R5)            ;RETURN


;SEQ239

CSHPNT: SSET    17,(R0)         ;SET CHD BOARDS
        SSET    19,(R0)         ;
        SSET    24,(R0)         ;
        SSET    25,(R0)         ;
UMLPNT: MOV     R0,1$           ;SET UML POINTER
        PNTUML
1$:     .WORD   0               ;TRAILING UML POINTER
        RTS     PC              ;RETURN


A4UML:  .WORD   0,0,0,0 ;UML FOR EBUS REG/PMA
A4MSG1: .ASCII  %MBC4 ACKN PULSE ASSERTED INDICATES THAT THE\%
        .ASCII  %SBUS MAY BE POLLUTED.  A TTL L AT SLOT 7 OR 8\%
        .ASCII  %DC1 OR CM1 ISOLATES THE PROBLEM TO A MEMORY\%
        .ASCII  %CONTROLLER OR SBUS CABLE.  AN ECL H AT SLOT\%
        .ASCII  %7 OR 8 EC1 ISOLATES THE PROBLEM TO THE SBUS\%
        .ASCII  %TRANSLATOR.  IF THESE SIGNALS ARE NOT H, THE\%
        .ASCIZ  %MBC BOARD (SLOT 22) IS DEFECTIVE.\%

A4MSG2: .ASCII  %MBZ4 MBOX ADR PAR ERR ASSERTED INDICATES THAT\%
        .ASCII  %THE SBUS MAY BE POLLUTED.  A TTL L AT SLOT 8\%
        .ASCII  %CV2 ISOLATES THE PROBLEM TO A MEMORY CONTROLLER\%
        .ASCII  %OR SBUS CABLE.  AN ECL H AT SLOT 8 DU2 ISOLATES\%
        .ASCII  %THE PROBLEM TO THE SBUS TRANSLATOR.  IF THIS\%
        .ASCII  %SIGNAL IS NOT H, THE MBZ BOARD (SLOT 20) IS\%
        .ASCIZ  %DEFECTIVE.\%

A4MSG3: .ASCII  %MBZ4 MBOX SBUS ERR ASSERTED INDICATES THAT THE\%
        .ASCII  %SBUS MAY BE POLLUTED.  A TTL L AT SLOT 7 CU2\%
        .ASCII  %ISOLATES THE PROBLEM TO A MEMORY CONTROLLER OR\%
        .ASCII  %SBUS CABLE.  AN ECL H AT SLOT 7 DU2 ISOLATES\%
        .ASCII  %THE PROBLEM TO THE SBUS TRANSLATOR.  IF THIS\%
        .ASCII  %SIGNAL IS NOT H, THE MBZ BOARD (SLOT 20) IS\%
        .ASCIZ  %DEFECTIVE.\%

A4MSG4: .ASCII  %MBZ6 CSH PAR BIT A ASSERTED INDICATES THAT A\%
        .ASCII  %CACHE DATA BOARD MAY BE DEFECTIVE.  A HIGH AT\%
        .ASCII  %DD1 OF A CHD BOARD IDENTIFIES THE FAILING\%
        .ASCII  %MODULE.  IF NONE ARE H, THE MBZ BOARD (SLOT 20)\%
        .ASCIZ  %IS DEFECTIVE.\%

A4MSG5: .ASCII  %MBZ6 CSH PAR BIT B ASSERTED INDICATES THAT A\%
        .ASCII  %CACHE DATA BOARD MAY BE DEFECTIVE.  A HIGH AT\%
        .ASCII  %FA1 OF A CHD BOARD IDENTIFIES THE FAILING\%
        .ASCII  %MODULE.  IF NONE ARE H, THE MBZ BOARD (SLOT 20)\%
        .ASCIZ  %IS DEFECTIVE.\%


;SEQ240

        .LIST   BEX
A4TAB1: .BYTE   174,22.         ;E CORE RD RQ
        .BYTE   170,31.,33.     ;CCA REQ MB REQ IN
        .BYTE   174,28.         ;CSH EBOX CYC B
        .BYTE   175,28.         ;CSH BM CYC
        .BYTE   172,28.,26.     ;CSH CCA CYC, READY TO GO A
        .BYTE   174,29.         ;CACHE IDLE
        .BYTE   176,24.         ;CSH EBOX T0
        .BYTE   171,29.         ;CSH EBOX T1
        .BYTE   170,29.         ;CSH EBOX T3
        .BYTE   174,26.         ;CSH T0
        .BYTE   175,26.         ;CSH T3
        .BYTE   175,29.         ;ONE WORD WR T0
        .BYTE   173,27.         ;WRITEBACK T1 A
        .BYTE   177,29.         ;CSH EBOX WR T4
        .BYTE   170,28.,27.     ;CSH REFIL RAM WR
                                ;CSH DATA CLR DONE
        .BYTE   173,22.         ;ONE WORD RD
        .BYTE   177,28.         ;READ PAUSE 2ND HALF

        .BYTE   173,24.         ;RD PSE 2ND REQ EN
        .BYTE   176,27.         ;E CACHE WR CYC
        .BYTE   170,26.,25.     ;ANY VAL HOLD, CSH WR TEST
        .BYTE   161,27.,29.     ;CSH DATA CLR T1,T3
        .BYTE   160,33.         ;CSH DATA CLR DONE IN
        .BYTE   175,22.         ;CSH EBOX RETRY REQ
        .BYTE   172,29.         ;CSH EBOX REQ EN
        .BYTE   175,27.         ;MBOX GATE VMA 27-33
        .BYTE   174,23.         ;PAGE FAIL HOLD
        .BYTE   177,24.         ;PAGE FAIL DLV
        .BYTE   171,24.         ;PAGE FAIL D2
        .BYTE   172,24.         ;CHAN T4
        .BYTE   171,22.,27.     ;CHAN RD T5, CHAN WR T5
        .BYTE   176,29.,23.     ;PAGE REFILL T4,T8
        .BYTE   172,22.         ;CVHAN WR CACHE
        .BYTE   177,22.,23.     ;PAGE REFILL ERROR
                                ;DATA DLY 1
        .BYTE   171,23.,25.             ;CSH WR DATA RDY
                                ;CSH EBOX LOAD REG
        .BYTE   176,22.         ;CCA INVAL T4
        .BYTE   172,23.         ;CCA CYC DONE
        .BYTE   172,27.         ;CCA USE HOLD
        .BYTE   175,23.         ;CSH USE WR EN
        .BYTE   170,23.         ;CACHE WR IN


;SEQ241

        .BYTE   171,26.         ;CSH FILL CACHE RD
        .BYTE   173,28.         ;CSH CCA WRITEBACK
        .BYTE   176,28.         ;CSH E WRITEBACK
        .BYTE   177,35.         ;WRITEBACK T2
        .BYTE   174,30.         ;CACHE TO MB T2
        .BYTE   176,30.        ;CACHE TO MB T4
        .BYTE   173,30.         ;CACHE TO MB DONE
        .BYTE   176,35.         ;SBUS DIAG CYC
        .BYTE   171,34.         ;MEM DIAG
        .BYTE   175,35.         ;SBUS DIAG 3
        .BYTE   173,23.         ;MBOX RESP
        .BYTE   175,24.         ;MB TEST PAR IN
        .BYTE   170,30.         ;CACHE BIT
        .BYTE   171,31.         ;CCA SEL 1
        .BYTE   172,31.         ;CCA SEL 2
        .BYTE   177,30.         ;CCAL ALL PAGGES CYC
        .BYTE   172,32. ;FORCE NO MATCH
        .BYTE   173,31.         ;CHAN WR CYC
        .BYTE   160,19.         ;CSH EN CSH DATA
        .BYTE   161,19.,33.,30. ;MEM TO C DIAG EN

                                ;RQ HOLD OFF
                                ;CSH SEL LRU
        .BYTE   162,27.,28.,29.,30.     ;CACHE WR 00,09,18,27 A

        .BYTE   160,32.         ;CSH ADR WR PULSE
        .BYTE   61,32.          ;CSH WR WR PULSE
        .BYTE   163,33.         ;DATA VALID A OUT
        .BYTE   164,27.,28.     ;DATA VALID B OUT
                                ;MBC INH 1ST MB REQ
        .BYTE   170,34.         ;MBX MEM TO C EN
        .BYTE   164,29.         ;MEM TO C EN
        .BYTE   163,30.,20.,32.,31.,28. ;CSH VAL WR DATA
                                ;CSH VAL SEL ALL
                                ;CSH WR WR DATA
                                ;CSH WR SEL ALL
                                ;CORE BUSY A
        .BYTE   175,34.         ;MEM WR RQ IN
        .BYTE   166,33.         ;MEM WR RQ
        .BYTE   161,23.         ;CHAN MEM REF
        .BYTE   160,25.         ;CHAN NXM ERR
        .BYTE   162,15.         ;CHAN ADR PAR ERR
        .BYTE   160,16.         ;CHAN PAR ERR
        .BYTE   173,34.         ;MEM TO C SEL 1
        .BYTE   174,34.!BIT7    ;MEM TO C SEL 2


;SEQ242

A4TAB2: .BYTE   166,32.!BIT7    ;MEM START

A4TAB3: .BYTE   172,34!BIT7     ;MEM RD RQ IN

A4TAB4: .BYTE   177,34.         ;RQ0 IN
        .BYTE   170,35.         ;RQ1 IN
        .BYTE   171,35.         ;RQ2 IN
        .BYTE   172,35.!BIT7    ;RQ3 IN

A4TAB5: .BYTE   160,22.!BIT7    ;CHAN CORE BUSY

A4TAB6: .BYTE   162,31          ;SBUS ADR HOLD
        .BYTE   160,23.,21.     ;NXM ANY, NXM ACKN
        .BYTE   161,25.         ;NXM DATA VAL
        .BYTE   162,23.         ;NXM T2
        .BYTE   160,24.         ;NXM T6
        .BYTE   173,32.!BIT7    ;MEM DATA TO MEM

A4TAB7: .BYTE   164,31.!BIT7    ;ACKN PULSE

A4TAB8: .BYTE   165,30.!BIT7    ;CORE DATA VALID -2
A4TAB9: .BYTE   174,27.!BIT7    ;CSH ADR PMA EN

A4TB10: .BYTE   160,34.,35.!BIT7        ;EBUS REG 34,35


;SEQ243

A4TB11: .BYTE   166,28.,29.,30.,31.     ;MEM RQ0-3

        .BYTE   162,33.         ;ANY SBUS RQ IN
        .BYTE   166,27.         ;MEM RD RQ B
        .BYTE   165,32.         ;CORE RD IN PROG
        .BYTE   160,27.,28.,29.,30.     ;FORCE VALID MATCH 0-3

        .BYTE   173,35.         ;SBUS ADR 34
        .BYTE   174,35.         ;SBUS ADR 35
        .BYTE   164,32.,33.     ;CORE ADR 34,35
        .BYTE   177,32.         ;MB REQ HOLD
        .BYTE   162,9.          ;CHAN READ
        .BYTE   160,20.         ;MB IN SEL 1
        .BYTE   161,20.         ;MB ISEL 2
        .BYTE   162,20.         ;MB IN SEL 4
        .BYTE   161,21.         ;MBZ1 RD-PSE-WR REF
        .BYTE   165,29.,31.     ;CORE DATA VALID-1
                                ;CORE DATA VALID        
        .BYTE   160,15.         ;CORE BUSY
        .BYTE   162,21.         ;MEM BUSY
        .BYTE   161,22.         ;MBOX NXM ERR
        .BYTE   160,18.         ;MB PAR BIT IN
        .BYTE    173,33.        ;MB SEL HOLD
        .BYTE   171,33.         ;MB SEL 1
        .BYTE   172,33.         ;MB SEL 2
        .BYTE   174,33.         ;MBO HOLD IN
        .BYTE   175,33.         ;MB1 HOLD IN
        .BYTE   176,33.         ;MB2 HOLD IN
        .BYTE   177,33.         ;MB3 HOLD IN
        .BYTE   176,31.         ;CSH WR WD1 EN
        .BYTE   177,31.         ;CSH WR WD2 EN
        .BYTE   170,32.         ;CSH WR WD3 EN
        .BYTE   171,32.         ;CSH WR WD4 EN
        .BYTE   176,34.         ;REFILL HOLD
        .BYTE   176,32.         ;MB PAR
        .BYTE   174,32.         ;MB DATA CODE 1
        .BYTE   175,32.         ;MB DATA CODE 2
        .BYTE   171,30.         ;CACHE TO MB 34
        .BYTE   172,30.         ;CACHE TO MB 35
        .BYTE   160,31.         ;WRITE OK
        .BYTE   174,24.         ;PAGE REFILL T9,13
        .BYTE   177,26.         ;PAGE REFILL T10
        .BYTE   170,22.         ;PAGE REFILL COMP
        .BYTE   162,24.         ;MBOX MB PAR ERR
        .BYTE   170,24.         ;MBOX PT DIR WR


;SEQ244

A4TB12: .BYTE   161,15.!BIT7    ;MBOX ADR PAR ERR

        
A4TB13: .BYTE   161,24.!BIT7    ;MBOX SBUS ERR


A4TB14: .BYTE   161,31.!BIT7    ;CSH VAL WR PULSE
A4TB15: .BYTE   161,26.!BIT7       ;CSH PAR BIT B
        
A4TB16: .BYTE   162,26.!BIT7    ;CSH PAR BIT B
        .EVEN
        .EVEN

$$FF:   $$CLIT                  ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
.END    STARTA

