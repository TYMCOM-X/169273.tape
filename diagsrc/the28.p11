;DHE28.P11
 
;SEQ1057
 
.SBTTL *IAPR4*  ISOLATION ROUTINE FOR TEST 28 (EAPR4)
 
.REM    %
THE TEST CHECKS THE FM ADR LOGIC ON THE APR. FAULTS ARE ON THAT BOARD OR
ON THE IR (WHERE THE AC# IS GENERATED). A S@1 CRAM # INPUT TO THE
APR AC+#09-12 ALU IS DETECTED IN SUBTEST #2.
%
 
IAPR4:  $SAVE           ;SAVE DIACON'S PARAMETERS
        $CLRUML         ;START FRESH
 
;FIRST TAKE CARE OF ACSET FAILURE:
 
        MOV     2(R3),R0                ;GET PTR TO DIAGNOSTIC FUNCTION LIST
        CMP     #132,(R0)               ;SEE IF FIRST ONE IS FR132
        BNE     NOTAC           ;NO, MUST NOT BE ACSET FAILURE
 
        $SSET   IR              ;YES, ACSET FAILURE
 
        $PNTUML                 ;PRINT SLOT
 
        PMSG<\FAILURE IN LOADING AND VERIFYING THE AC# FIELD ON THE IR BOARD.>
 
        JMP     @$$RET          ;EXIT
 
;SEQ1058
 
;NOW TAKE CARE OF OTHER FAILURES:
 
NOTAC:  $SSET   APR             ;APR IS INVOLVED
        CMP     #2,(R4)         ;DOING SUBTEST 2?
        BNE     NOTTWO          ;NO, GO AROUND
        $PARSE  $$ACN1          ;YES, LOOK FOR SOME CRAM # INPUT TO ALU S@1
        TST     R0              ;ALL PRESENT?
        BGE     NOTAC1          ;NO, TEST FURTHER
        JMP     BUTALU          ;COMMON CALLOUT BUTALU
 
NOTAC1: $PARSE  $$ACN2          ;KEEP LOOKING
        TST     R0              ;ALL PRESENT?
        BGE     NOTAC2          ;NO, TEST FURTHER
        JMP     BUTALU          ;COMMON CALLOUT BUTALU
 
NOTAC2: $PARSE  $$ACN3          ;KEEP LOOKING
        TST     R0              ;ALL PRESENT?
        BGE     NOTAC3          ;NO,KEEP TESTING
        JMP     BUTALU          ;COMMON CALLOUT BUTALU
 
NOTAC3: $PARSE  $$ACN4          ;KEEP LOOKING 
        TST     R0              ;ALL PRESENT?
        BGE     NOTTWO          ;NOT HERE
        JMP     BUTALU          ;COMMON CALLOUT BUTALU
 
BUTALU: $SSET   APR             ;THERE, CALL OUT ITS SLOT
 
        MOV     #ACNNN,R1               ;SET UP FOR STUCK MSG
        MOV     #1,R2           
        $PNTUML                 ;CALL OUT BOARDS
        PMSG<\TESTING FM ADR LOGIC ON APR.>
        $PSTUCK                 ;PRINT STUCK MSG
        JMP     HELP            ;EXIT WITH HELP MSG
         
NOTTWO: CMP     #4,(R4)         ;SUBTEST > 4?
        BLT     NOTIR           ;YES, ALL AC# LINES HAVE BEEN CHECKED
        
        $SSET   IR              ;NO, COULD BE AC# LINE
 
        $SSET   CRM16           ;OR FM ADR SEL LINE
 
;SEQ1059
 
        $SETHP  APR             ;MOST LIKELY IS APR THOUGH
 
NOTIR:  $PNTUML                 ;TELL ABOUT IT
        PMSG<\TESTING FM ADR LOGIC ON THE APR BOARD.>
 
        $STST   IR              ;REMEMBER WHICH CASE
 
        BEQ     HELP            ;GO AROUND
        PMSG<\ALSO CHECK FM ADR SEL LINES FROM CRM16 (PREVIOUSLY UNTESTED>
        PMSG<\BUFFERED COPIES) AND AC# LINES FROM IR BOARD.>
 
HELP:   $HELP                   ;EXIT WITH HELP MSG
 
;HERE TO BAIL OUT IF THINGS (SPECIFICALLY THE ERROR STACK) LOOK GRIM:
 
        $ABORT
 
;ONE PARSE ITEM:
 
$$ACN1: $FEND   11,1            ;APR AC+#12
$$ACN2: $FEND   11,2            ;APR AC+#11
$$ACN3: $FEND   11,4            ;APR AC+#10
$$ACN4: $FEND   11,10           ;APR AC+#09
 
;ONE STUCK MSG STRING:
 
ACNNN:  .ASCIZ/APR AC+#09-12 ALU *CRAM # INPUTS*/
 
