
;SEQ 689

.SBTTL  *IFLAG0* ISOLATION ROUTINE FOR TEST 5 (EFLAG0)

.REM    %
THE TEST SETS ALL THE FLAGS AND THEN DOES A MRESET.  THE ISOLATION ROUTINE
FIRST TESTS FOR FAILING BIT GROUPS USING SUBROUTINE "PARSE".
        %

IFLAG0: $SAVE                   ;SAVE DIAGON'S PARAMETERS
                $CLRUML         ;CLEAR OUT UML TABLE
        $SSET   SCD             ;ALL FAILURES INVOLVE SCD BOARD

        $TSTD                   ;SEE HOW MANY FIALING BITS
        BCS     JUSONE          ;GO ON IF JUST ONE
        JMP     IFMANY          ;OTHERWISE, GO TO MULTI-BIT STUFF

;HERE TO SEARCH FOR SINGLE-BIT FAILURES INVOLVING OTHER THAN SCD BOARD.

JUSONE: $PARSE  $$ADX2          ;SEE IF AD EX -2 IS S@1
        BCC     TRYCRM          ;NO, GO ON
        MOV     #PADX2,R1       ;YES, SET UP STUCK SIGNAL MSG PTR
ED0:    $SSET   EDP0            ;SET PROPER BOARD SLOT
        $SETHP  EDP0            ;MAKE IT MOST LIKELY
S1X:    MOV     #1,R2           ;SET S@1 FLAG
        JSR     PC,PUM1         ;CALL OUT BOARDS AND PRINT MSG
        $PSTUCK         ;PRINT STUCK SIGNAL MSG
        JMP     @$$RET          ;EXIT


;SEQ690


TRYCRM: $PARSE  $$VSEL          ;IS "VMA SEL 2 A" S@1?
        BCC     TRYIR           ;NO, GO ON

        MOV     #PSEL,R1        ;YES, SET UP STK MSG PTR
        $SSET   CRM4            ;BOARD

        BR      S1X             ;GO CALLIT OUT

TRYIR:  $PARSE  $$IR            ;SEE IF COULD INVOLVE IR
        BCC     TRYCC1          ;NO, TRY NEXT
        $SSET   IR              ;YES, SET IR
        JSR     PC,PUM1         ;PRINT TABLE & 1ST MTG
        PMSG<\FAULT PROBABLY ON SCD, BUT COULD ALSO BE "-AD CRY -02">

        PMSG<\ON IR OR BACKPLANE RUN OF SAME.>
        JMP     @$$RET          ;BACK TO DIACON

TRYCC1: $PARSE  $$COCK          ;SEE IF CON/CLK FAILURE
        BCC     TRYCRA          ;NO, GO ON

        $SSET   CON             ;YES, SET CON

        $SSET   CLK             ;AND CLK
        $SETHP  CON             ;MOST LIKELY CON
        JSR     PC,PUM1         ;PRINT UML & 1ST MSG
PMSG<\CHECK "CON CLR PRIVATE INSTR" RUN.>
        JMP     @$$RET

TRYCRA: $PARSE  $$CRA           ;SEE IF INVOLVES CRA
        BCC     TRYM            ;NO, GO ON

        $SSET   CRA             ;YES, SET CRA
        $SETHP  SCD             ;SCD FAVORITE
        JSR     PC,PUM1         ;PRINT TABLE & MSG
        PMSG<\CHECK "N1COND 10" RUN.>


;SEQ691

        JMP     @$$RET


;SEQ692

TRYM:   $PARSE  $$M             ;HOW ABOUT MBZ/PAG BRD FAILURES?
        BCC     TRYMCL          ;NO, TRY NEXT
        $SSET   MBZ             ;YES, SET MBZ
        $SSET   PAG             ;AND PAG
        JSR     PC,PUM1 ;PRINT UML & MSG
        PMSG<\CHECK "PT PUBLIC" RUN.  IF THESE BOARDS DON'T FIX THE PROBLEM,>
        PMSG<\RUN MBOXA (DGKBA) TO ISOLATE A FAULTY MBOX INITIALIZATION.>
        JMP     @$$RET          ;BACK TO DIACON

TRYMCL: $PARSE  $$MCL           ;TRY FAILURES INVOLVING MCL
        BCC     TRYCC2          ;NO, GO ON

IFMCL:  $SSET   MCL             ;YES, SET MCL

        $SETHP  SCD             ;ODDS-ON IS SCD
        JSR     PC,PUM1         ;PRINT UML & MSG 1
        PMSG<\CHECK FAILING SIGNAL RUN FROM SCD TO MCL BOARD.>
        JMP     @$$RET

TRYCC2: $PARSE  $$COCT          ;TRY TREAP CLEAR FIALURES
        BCC     JUSSCD          ;NO, MUIST BE ONLY ON SCD
        $SSET   CON             ;YES, SET CON
        $SSET   CTL             ;AND CTL
        $SETHP  SCD             ;SCD MOST PROBABLE
        JSR     PC,PUM1         ;PRINT UML & MSG
        PMSG<\CHECK RUNS INTO "SCD TRAP CLEAR".>
        JMP     @$$RET          ;RETURN


;SEQ693

;HERE IF NONE OF SPECIAL GROUPS:

JUSSCD: JSR     PC,PUM1         ;PRINT SCD & MSG
        JMP     @$$RET          ;TAHT'S ALL FOLKS

;HERE FOR MULTIPLE-BIT FAILURES:

IFMANY: TST     R0              ;MAKE SURE SOME FAILURES
        BNE     GO              ;OK, GO ON
        JMP     .ABORT          ;BOMB OUT!
GO:     CMP     #5,R0           ;IS IT MASSIVE FAILURE?
        BGE     TRYRS1          ;NO, TRY RESET S@1

        $SSET   CLK             ;YES, MUST BE RESET S@0

        $SETHP          SCD             ;SCD MOST LIKELY
        JSR     PC,PUM1         ;PRINT UML & MSG 1
        PMSG<\RESET INTO "LOAD FLAGS" APPEARS TO BE STUCK AT 0.>
        JMP     @$$RET

TRYRS1: $PARSE  $$RS1           ;MAYBE RESET LINE S@1
        BCC     TRYOV           ;NO, TRY NEXT
        $PNTUML $$RS01          ;YES, PRINT CANNED TABLE
        JSR     PC,PM1          ;AND MSG #1
        PMSG<\THE RESET LINE "MR RESET 01" APPEARS TO BE S@1. ALL>
        PMSG<\OF THE ABOVE BOARDS ARE IN THE RUN.>
        JMP     @$$RET          ;RETURN

;SEQ694


TRYOV:  $PARSE  $$OV            ;LOOK AT OVERFLOWS
        BCC     TRYUS           ;NO, GO ON

        MOV     #PAR0,R1        ;YES, STR LINE MSG PTR
        JMP     ED0             ;TAKE COMMON CALL OUT PATH


TRYUS:  $PARSE  $$USER          ;LOOK FOR USER/USER EN PAIR
        BCC     TRYUIO          ;NO, GO ON
        $SSET   CON             ;YES, SET OCN
        $SSET   MTR             ;AND MTR
        $SETHP  SCD             ;SCD MOST LIKELY
        JSR     PC,PUM1         ;PRINT UML & MSG
        PMSG<\CHECK "USER" RUN.>
        JMP     @$$RET          ;RETURN

TRYUIO: $PARSE  $$UIOT          ;CHECK UIOT PAIR
        BCC     TRYADB          ;NO, GO ON
        $SSET   EDP6            ;YES, PUT BORD IN LIST
        $SETHP  EDP6            ;MAKE IT MOST LIKELY
        JSR     PC,PUM1         ;PRINT SLOTS AND MSG # 1
        JMP     @$$RET          ;EXIT

TRYADB: $PARSE  $$ADB           ;ADDRESS BREAK PAIRS?
        BCC     TRYCRY          ;NO, MOVE ON
        BR      IFMCL           ;YES, GO REPORT ERROR

TRYCRY: $PARSE  $$CRY           ;IS "AD EX -01" OR"AD00" S@1?
        BCC     NOTFND          ;NO, BAIL OUT
        MOV     #PADEX,R1       ;YES, STK MSG PTR
        JMP     ED0             ;EXIT WITH COMMON PRINT


;SEQ695

;HERE IF HAVEN'T FOUND ERROR:

NOTFND: JSR     PC,PUM1 ;PRETTY SAFE IN CALLING OUT SCD
        JMP     @$$RET          ;RETURN

;SUBROUTINE TO PRINT UML & A MSG:

PUM1:   $PNTUML                 ;PRINT UML TABLE
PM1:    PMSG<\ONE OR MORE PC FLAG LOGIC SIGNALS DID NOT RESET TO THEIR EXPECTED STATE.>
        RTS     PC              ;RETURN

;HERE IF THERE IS AN INCONSISTENCY IN STACK DATA:
        $ABORT

;HERE IS A CANNED UML TABLE FOR "MR R ESET 0" RUN:

$$RS01:         $UML    <SCD,CLK,CRM0,CRM4,CRM8,CRM12,CRM16>

;SOME STUCK SIGNAL STRINGS:

PADX2:  .ASCIZ/"AD EX -02"/
PSEL:   .ASCIZ/"CRAM VMA SEL 2 A"/
PAR0:   .ASCIZ/"AR 00 D"/
PADEX:  .ASCIZ/"AD EX -01" OR "AD 00"/

;HERE ARE THE PARSE TABLES FOR THIS ISOLATION ROUTINE:

$$ADX2: $FEND   2,20            ;"-AD OVERFLOW 00"
$$VSEL: $FEND   7,40            ;"SCD TRAP MIX 35"
$$IR:   $FEND   2,10            ;"-AD CRY -02"
$$COCK: $FIND   3,20            ;"CLR PRI INSTE"
        $FEND   4,40            ;"-CON PI CYCLE"
$$CRA:  $FEND   3,10            ;"SCD NICOND 10"


;SEQ696

$$M:    $FEND   4,10            ;"SCD PUBLIC PAGE
$$MCL:  $FIND   5,130           ;"SCD PUBLIC" OR "-SCDS PRIV INSTR"
        $FIND   6,20            ;"SCD USER IOT"
        $FEND   7,210           ;"SCD ADR BRK INH" OR "SCD ADR BRK PRE"

$$COCT: $FEND   7,4             ;"-SCD TRAP CLEAR
$$RS1:  $FIND   3,100           ;SCD LOAD FLAGS
        $FIND   4,100           ;SCD LEAVE USER
        $FEND   7,4             ;SCD TRAP CLEAR

$$OV:   $FIND   1,200           ;SCD OV
        $FIND   2,200           ;VMA HELD OR PC 00
        $FEND   3,200           ;SCD PCP

$$USER: $FEND   4,220           ;SCD USER PAIR
$$UIOT: $FEND   6,30            ;SCD USER IOR PAIR
$$ADB:  $FEND   7,230           ;SCD ADR BRK SET

$$CRY:  $FIND   1,20            ;"-AD CRY 01"

;SEQ697

        $FEND   2,20            ;"-AD OVERFLOW 00"


;SEQ698

.SBTTL ERR8 - SUBROUTINE TO RECONSTRUCT ONE DCOMP COMPARE

.REM    %
THISA ROUTINE DOES A "DCOMP" COMPARE OF ONE BYTE ON THE ERROR
STACK AND RETURNS THE XOR OF THE ACTUAL AND EXPECTED DATA IN R0.
R1 CONTAINS THE APPROPRIATE INDEX/ACTUAL DATA FROM THE ERROR STACK
AND R3 POINTS TO THE STACK BASE ON ENTRY.


CALL:   MOV     $$ERS0,R3       ;LOAD PTR TO ERROR STACK
        MOV     14(R3),R1       ;GET 2ND INDEX/DATA WORD OFF STACK
        JSR     PC,ERR8         ;GET DIFFERING BITS IN R0.

        %

ERR8:   PUSH    R5              ;SAVE REGISTERS
        SHOVE
        MOV     R1,R5           ;COPY INDEX/ACTUAL WORD TO R5
        SWAB    R5              ;PUT INDEX ON RIGHT
        BIC     #177600,R5      ;STRIP OFF ACTUAL AND SIGN BIT OF INDEX
        MOV     10(R3),R4       ;GET PTR TO EXPECTED DATA TABLE BASE
        ADD     R5,R4           ;ADD INDEX
        MOVB    (R4),R2         ;AND USE RESULT TO FETCH DATA.
        MOV     6(R3),R4                ;GET PTR TO MASK TABLE BASE
        ADD     R5,R4           ;ADD INDEX
        MOVB    (R4),R3         ;AND USE RESULT TO FETCH MASK.

        BIS     #177400,R3      ;ADD BITS OF LEFT HAND BYTE TO MASK.
        BIC     R3,R1           ;MASK ACTUAL
        BIC     R3,R2           ;MASK EXPECTED.
        MOV     R1,R0           ;SECOND COPY OF ACTUAL FOR XOR
        BIC     R2,R0           ;(-EXP). (ACT) TO R0
        BIC     R1,R2           ;(-ACT). (EXP) TO R2
        BIS     R2,R0           ;"OR" OF RESULT RO R0
        GRAB                    ;RESTORE REGISTEERS
        POP     R5
        RTS     PC              ;AND DONE.


;SEQ699

.SBTTL BITCNT - SUBROUTINE TO COUNT BITS IN BYTE

.REM    %
THIS ROUTINE COUNTS THE "ONE" BITS IN THE RIGHT-HANDBYTE OF R0 AND
RETURNS THE TOTAL IN R0.  IN ADDITION, IF THE RESULT IS 1, THE C-BIT IS SET.

CALL:   MOVB    #ABYTE,R0               ;PUT BYTE TO BE TESTD IN R0
        JSR     PC,BITCNT       ;GET BIT TALLYB ACK IN R0.

        %

BITCNT: SHOVE                   ;SAVE REGISTERS
        CLR     R1              ;CLEAR ONE
        MOV     #^D8,R2         ;SET SECOND TO 8

1$:     ASR     R0              ;SHIFT BYTE TO BE TESTED.
        BCC     2$              ;GO AROUND IF BIT SHIFTED OUT WAS 0.

        INC     R1              ;ELSE COUNT THE 1 BIT IN R1.

2$:     DEC     R2              ;UPDATE BIT-LEFT-TO-TEST COUNT
        BGT     1$              ;CONTINUE TESTING UNTIL DONE
        MOV     R1,R0           ;PUT RESULT IN R0

        CLC                     ;CLEAR ONE-BIT FLAG
        DEC     R1              ;SEE IF RESULT WAS 1.
        BNE     3$              ;DONE IF NOT
        SEC                     ;SET ONE-BIT GFLAG

3$:     GRAB                    ;RESTORE REGISTERS
        RTS     PC              ;AND RETURN


;SEQ700

.SBTTL TSTD - SUBROUTINE TO COUNT FAILING BITS IN DCOMP ERRORS

.REM    %
THIS ROUTINE RETURNS A COUNT OF THE NUMBER OF FAILING BITS ON THE
STACK FROM A DCOMP ERROR IN R0.  IN ADDITION, THE C-BIT IS SET IF THE
RESULT IS ONE.  IF THE STACK HAS THE WRONG ERROR TYPE, TSTD JUMPS TO
LOCATION .ABORT.  IT USES SUBROUTINES ERR8 AND BITCNT AND EXPECTS A
POINTER TO THE ERROR STACK BASE IN LOCATION $$ERS0.

CALL:   JSR     PC,TSTD         ;COUNT FAILING DCOMP BITS

        %

TSTD:   SHOVE                   ;SAVE REGS
        MOV     $$ERS0,R3       ;PICK UP PTR TO ERROR STACK BASE
        CMP     #FT3,(R3)       ;LOOKING AT A DCOMP ERROR?
        BEQ     TSTDOK          ;CONTINUE IF SO...

        JMP     .ABORT          ;OTHERWISE FAIL.

TSTDOK: CLR     R2              ;START COUNT AT 0
        MOV     R3,R4           ;SECOND COPY OF PTR
        ADD     #12,R4          ;MAKE IT POINT TO 1ST INDEX/DATA PAIR

TDLOOP: MOV     (R4),R1         ;GET THIS DATA PAIR
        $ERR8                   ;RECONSTRUCT FAILING BYTE IN R0

        $BITCNT                 ;COUNT BITS IN R0
        ADD     R0,R2           ;ACCUMULATE TOTAL IN R2
        TST     (R4)+           ;WAS THAT LAST DATA PAIR?
        BGE     TDLOOP          ;NO, DO NEXT

        MOV     R2,R0           ;YES, COPY ANSWER TO R0
        DEC     R2              ;LESSEN THE COUNT IN R2
        BEQ     TDONE           ;WAS COUNT 1?
        CLC                     ;NO, CLEAR C-BIT
        BR      TDEXIT          ;AND LEAVE

TDONE:  SEC                     ;ONLY ONE, SET C-BIT

TDEXIT: GRAB                    ;RESTORE REGS
        RTS     PC              ;AND RETURN


;SEQ701

.SBTTL PARSE - SUBROUTINE TO LOOK FOR CERTAIN DCOMP FAILURES ON ERROR STACK

.REM    %
THIS SUBROUTINE LOOKS TO SEE IF ANY OR ALL OF A SPECIFIED LIST OF DIAGNOSTIC
FUNCTION/EBUS BITS ARE PRESENT AS DESCREPENCIES IN A DCOMP DETECTED ERROR
ON THE ERROR STACK.  IT EXPECTS A PTR TO THE LIST OF BITS TO CHECK FOR IN R0
AND A PTR TO THE ERROR STACK BASE IN LOCATION $$ERS0.  THE LIST SHOULD BE
IN THE FORMAT:

LIST:   .BYTE   BITMASK         ;FIRST DATA BYTE WITH 1'S FOR BITS TO CHECK
        .BYTE   DCOMP INDEX     ;FIRST DCOMP INDEX
          ...
        .BYTE   BITMASK         ;LAST DATA BYTE
        .BYTE   INDEX!BIT7      ;LAST INDEX WITH SIGN BIT SET

PARSE INDICATES ITS FINDINGS BY RETURNING R0 AS FOLLOWS:

        R0 = -1 MEANS ALL THE SPECIFIED BITS WERE FOUND
        R0 > 0  MEANS R0 CONTAINS THE NUMBER OF BITS FOUND
        R0 = 0  MEANS NONE OF THE BITS WAS FOUND

IN ADDITION, THE C-BIT I RETURNED SET WHEN AT LEAST ONE SPECIFIED BIT IS FOUND
IF THE STACK DOES NOT CONTAIN A DCOMP ERROR, THE EXIT TO .ABORT IS TAKEN.
THIS ROUTINE USES SUBROUTINES $ERR8 AND $BITCNT.

CALL:   MOV     #LIST,R0        ;PTR TO LIST
        JSR     PC,PARSE                ;CHECK THE STACK

        %
PARSE:  SHOVE                   ;SAVE R1-R4
        MOV     R5,-(SP)        ;AND R5

        MOV     R0,R5           ;SAVE PTR TO START OF LIST
        MOV     $$ERS0,R3       ;PICK UP PTR TO ERROR STACK BASE
        CLR     $$HITS          ;INITIALIZE "HITS" TO ZERO
        MOV     #-1,$$ALL               ;SET "PERFECT-STORE" FLAG
        CMP     #FT3,(R3)       ;DOES STACK HAVE DCOMP ERROR?
        BEQ     GETONE          ;IF YES, GET STARTED

        JMP     .ABORT          ;IF NO, SCUTTE THE SHIP[

;HERE FOR EACH ENTRY IN THE LIST

GETONE: MOV     (R5),R0         ;PUT NEXT LIST ENTRY IN R0
        MOV     (R5)+,-(SP)     ;AND ON THE STACK

        BIC     #100377,R0      ;ELIMINATE ALL BUT INDEX

;HERE TO SEARCH ERROR STACK FOR THIS INDEX:

FINDEM: MOV     R3,R4           ;COPY OF ERROR STACK PTR TO R4
        ADD     #12,R4          ;MAKE IT POINT TO 1ST DATA WORD


;SEQ702

TRYONE: MOV     (R4),R1         ;GET NEXT DATA WORD TO R1
        BIC     #100377,R1      ;MASK ALL BUT INDEX
        CMP     R0,R1           ;IS IT THE ONE?
        BEQ     GOTEM           ;LEAVE LOOP IF SO
        TST     (R4)+           ;WAS IT THE LAST ENTRY ON STACK?
        BGE     TRYONE          ;GO SEE IF MORE LIST ENTRIES TO DO
 
;HERE IF I DIDN'T FIND THIS INDEX:
 
        CLR     $$ALL           ;WELL..THERE GOES OUR PERFECT RECORD
        BR      NXTONE          ;GO SEE IF MORE LIST ENTRIES TO DO
 
;HERE IF I DID:

GOTEM:  MOV     (R4),R1         ;GET ANOTHER COPY OF DATA WORD
        $ERR8                   ;RECONSTRUCT FAILURE BYTE IN R0
        MOV     (SP),R1         ;GET LIST ENTRY OFF THE STACK
        BIC     #177400,R1      ;MASK OUT ALL BUT BITS TO MATCH

;HERE TO FIND OUT HOW MANY MATCH:

        MOV     R1,R2           ;GET SECOND COPY OF BITS TO MATCH
        COM     R2              ;"AND" IT WITH FAILED BITS AND LEAVE
        BIC     R2,R0           ;RESULT ("HITS") IN R0.
        CMP     R0,R1           ;WAS THE RESULT "ALL"?
        BEQ     COUNTEM         ;IF YESM, COUNT BITS FOUND.
        CLR     $$ALL           ;IF NO, CLEAR "PERFECT SCORE" FLAG

COUNTEM:        $BITCNT                 ;COUNT 1'S IN R0 & RETURN COUNT IN R0
        ADD     R0,$$HITS       ;ADD RESULT TO THE TALLY

;HERE TO SEE IF THERE ARE MORE ENTRIES TO DO:

NXTONE: TST     (SP)+           ;SEE IF LAST ENTRY WSAS END OF LIST
        BGE     GETONE          ;NO, GO PROCESS NEXT ONE
        
;HERE TO FINISH UP WHEN ENTIRE LIST HAS BEEN PROCESSED:

        MOV     (SP)+,R5        ;RESTORE R5
        GRAB                    ;AND R1-R4

        TST     $$ALL           ;SEE IF ALL WERE FOUND
        BEQ     NOTALL          ;IF NO, GO AROUND
        MOV     #-1,R0          ;YES, SET R0 NEGATIVE


ONEORM: SEC             ;SET C-BIT FOR ONE OR MORE BIT FOUND
        RTS     PC              ;RETURN


;SEQ703

NOTALL: MOV     $$HITS,R0       ;PUT # BITS MATCHED IN R0
        BGT     ONEORM          ;IF NOT ZERO, TAKE ONE OR MORE EXIT
        CLC                     ;ELSE CLEAR C-BIT TO SHOW NONE FOUND
        RTS     PC              ;AND EXIT

$$ALL:  0                       ;ALL MATCHED FLAG
$$HITS: 0                       ;COUNTER FOR BITS FOUND


;SEQ704


.SBTTL PSTUCK - SUBROUTINE FOR TYPING STUCK LINE MESSAGES

.REM    %
THIS SUBROUTINE PRINTS THE MESSAGE:  "CHECK THAT X IS NOT STUCK AT Y"
WHERE X IS AN ASCII STRING POINTED TO BY R1 AND Y IS THE OCTAL CONTENTS
OF R2 (0 OR 1).
        %

PSTUCK: PMSG<\CHECK THAT >      ;PRINT FIRST PART
        MOV     R1,R0           ;PUT PTR TO LINE I.D. IN R0
        PNTAL                   ;PRINT IT
        PMSG<\IS NOT STUCK AT > ;PRINT NEXT PART
        TST     R2              ;SEE WHICH TYPE OF FAULT
        BGT     STUCK1          ;IT'S S@1
        BEQ     STUCK0          ;IT'S S@0

        PMSG<0 OR 1.>           ;IT'S EITHER

        RTS     PC              ;RETURN

STUCK1: PMSG<1.>                ;S@1 MSG
        RTS     PC              ;RETURN

STUCK0: PMSG<0.>                ;S@0 MSG

        RTS     PC              ;RETURN


;SEQ705

.SBTTL  *ISOEND* STORAGE FOR ISOLATION ROUTINE PARAMETERS

$$RET:  0               ;RETURN ADDRESS TO DIACON
$$TAB:  0               ;PTR TO DIACON'S DATA TABLE
        
$$ERS0: 0                       ;PTR TO ERROR STACK BASE
        
$$ERS:  0                       ;PTR TO CURRENT ERROR STACK
$$UML:  0,0,0,0         ;STORAGE FOR BOARD CALLOUT TABLE
$$BUF:
$$XOR:  0,0,0,0,0,0     ;STORAGE FOR 36-BIT OR 80-BIT WORD


$$LITT  =  .          ART OF LITERAL AREA
$$FF    =  $$CLIT               ;FIRST FREE

        .END    ISOGO           ;TRANSFER ADDRESS
 r+z—