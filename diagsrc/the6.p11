
;SEQ709

.SBTTL *ISCD0* ISOLATION ROUTINE FOR TEST 6 (ESCD0)

.REM    %
THIS TEST CHECKS THAT THE REGISTES ON THE SCD BOARD CLEAR.  THE ISOLATION
ROUTINE LOOKS FOR MULTIPLE-BIT FAULTS WHICH COUD BE CAUSED BY CONTROL SIGNALS
FROM OFF THE BOARD ORE OUTPUT LINE RUNS STUCK HIGH.  MOST OTHER FAULTS
AR ON THE BOARD.
        %

ISCD0:  $SAVE                   ;SAVE DIACON'S PARAMETERS
        $CLRUML                 ;CLEAR OUT UML TABLE TO START
        $SSET   SCD             ;ALL POSSIBLE FAILURES INCLUDE SCD

;LOOK FIRST FOR BITS RUNNING OFF SCD TO SHIFTER:

        $PARSE  $$SH                    ;ANY OF BITS INVOLVING SH/
        BCC     MULTI           ;NO, CHECK NEXT GROUP
        TST     R0              ;YES, ALL OF THEM?
        BLT     JUSSCD          ;YES, ONLY INVOLVES SCD RESET

        $SSET   SHM             ;NO, SINGLES ARE EITHER SCD OR SH
        JSR     PC,PUM1         ;GO PRINT TABLE & MSG 1

        PMSG<\THIS SIGNAL MAY BE STUCK AT 1 ON SCD, SH OR IN BACKPLANE.>
 
        JMP     @$$RET                  ;RETURN TO DIACON


;SEQ710

;HERE TO LOOK FOR THE ONE MULTIPLE-BIT FAILURE WHICH COULD INVOLVE ANOTHER BOARD:

MULTI:  $PARSE  $$FE            ;ANY FE BITS STUCK?
        BCC     TRYCRA          ;N, GO ON
        TST     R0              ;YEA, WAS IT ALL?
        BGT     JUSSCD          ;NO, FAILURE JUST ON SCD
        $SSET   CON             ;YES, SET CON

        JSR     PC,PUM1         ;PRINT TRHE TABLE & MSG 1
        PMSG<\THIS FAULT IS EITHER A RESET FAILURE ON SCD OR THE SIGNAL>
        PMSG<\"COND/FE SHRT" STUCK AT 1 ON SCD, CON OR IN BACKPLANE.>
        JMP     @$$RET          ;RETURN

;NOW LOOK FOR BITS RUNNING TO CRA:

TRYCRA: $PARSE  $$CRA           ;ANY BITS INVOLVING CRA?
        BCC     JUSSCD          ;NO, FAILURE MUST BE ON SCD
        $SSET   CRA             ;YES, SET CRA
        JSR     PC,PUM1         ;GO PRINT TABLE & SG 1
        PMSG<\THIS SIGNAL MAY BE STUCK AT 1 ON SCD, CRA OR IN BACKPLANE.>
        JMP     @$$RET          ;RETURN

;HERE IF FAILURE HAS STILL NOT BEEN FOUND:

JUSSCD: JSR     PC,PUM1         ;PRINT UML & MSG 1
        JMP     @$$RET          ;THAT'S ALL

 
;SEQ711
 
;HERE TO PRINT UML TABLE & MSG:
 
PUM1:   $PNTUML                 ;PRINT THE FAILING BOARD'S SLOT
        PMSG<\\ONE OR MORE SIGNALS ON THE SCD DID NOT RESET CORRECTLY.>
 
        RTS     PC
 
;HERE TO ABANDON SHIP:
 
        $ABORT
 
;HERE ARE THE 'PARSE' TABLES FOR THIS TEST:
$$SH:   $FIND   0,370
        $FEND   1,1
$$FE:   $FIND   2,370
        $FEND   3,37
$$CRA:  $FIND   6,1
        $FEND   10,1
 
 
