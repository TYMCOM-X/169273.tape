;SEQ397

.SBTTL          MBOX TEST 26 ISOLATION ROUTINE

A26:    PNTID
        CMP     (R4),#20.       ;RD-PSE-WR ERROR?
        BEQ     2$              ;YES, ISOLATE IT
        MOV     (R4),R1 ;NO, GET SUBTEST
        ASL     R1              ;MULTIPLY IT BY 2
        MOV     UMLPTR-2(R1),1$ ;GET UML POINTER
        CLRB    UML26A+6        ;CLEAR HIGH PROBABILITY FOR SOME
        CLRB    UML26B+6        ;
        CLRB    UMLMB+6 
        PNTUML
1$:     .WORD   0               ;UML POINTER GOES HERE
        CMP     (R4),#12                ;MORE ISOLATION NEEDED?
        BGT     4$              ;NO, RETURN
        RSTMBX                  ;YES, BACK-UP A TICK
        STEP10  AC0MEM,<164,31,0>       ;AT ACKN PULSE
        MOV     #MBMSG,R0       ;PRINT ISOLATION DATA
        PNTAL
        BR      4$              ;RETURN
2$:     RSTMBX                  
        STEXCT  AOSMB3          ;START RD-PSE-WR
        FIND    <175,24,0>      ;LOOK FOR MB TEST PAR A IN
        BCC     3$              ;FOUND IT, MBZ ERROR
        PNTUML  UMLCSH          ;NOT FOUND, CSH
        BR      4$              ;RETURN
3$:     PNTUML  UMLMBZ          ;LMBZ
4$:     JMP     (R5)            ;RETURN TO DIACON


;SEQ398

UMLPTR:         UML26A          ;1
                UMLMB           ;2
                UMLMB           ;3
                UMLMB           ;4
                UMLMB           ;5
                UMLMB           ;6
                UMLMB           ;7
                UMLMB           ;8
                UMLMB           ;9
                UMLMB           ;10
                UML26A          ;11
                UML26B          ;12
                UMLPAG          ;13
                UMLPAG          ;14
                UMLPAG          ;15
                UMLMBZ          ;16
                UMLAPR          ;17
                UMLMBZ          ;18
                UMLMBZ          ;19

UML26A: UML     <20,20,14,15,16>
UML26B: UML     <30,14,15,16>
UMLMB:  UML     <14,15,16>
UMLPAG: UML     <30>
        
UMLMBZ: UML     <20>
UMLCSH: UML     <23>
UMLAPR: UML     <34>


;SEQ399

AOSMB3: I10     AOS,,,3003

AC0MEM: I10     MOVEM AC0,,3000



MBMSG:  .ASCII  %\IMPROPER PARITY GENERATION MAY BE ISOLATED\%
        .ASCII  %AS FOLLOWS:\%
        .ASCII  %\1)	SCOPE SLOT 30 BC1;\%
        .ASCII  %	IF H, REPLACE THE M8537.\%
        .ASCII  %\2)	SCOPE AU2 FOR EACH OF THE M8517 BOARDS.\%
        .ASCII  %	A DIFFERING POLARITY DEFINES A FAILURE.\%
        .ASCII  %\3	SCOPE DS2 FOR EACH OF THE M8517 BOARDS.\%
        .ASCII  %	A DIFFERING POLARITY DEFINES A FAILURE.\%
        .ASCII  %	NO DIFFERENCE ISOLATES THE PROBLEM TO THE\%
        .ASCIZ  %	M8520.\%
        .EVEN
        .EVEN

$$FF:   $$CLIT                  ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
.END    STARTA

