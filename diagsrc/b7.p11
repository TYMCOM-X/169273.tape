;SEQ264

.SBTTL          MBOX TEST 7 ISOLATION ROUTINE
A7:     PNTID           
        DTEBAS                  ;GET DTE20 BASE ADDRESS
        MOV     #BASE20,R1      ;
1$:     MOV     R0,(R1)+        ;POINTERS TO TABLE
        INC     R0              ;
        INC     R0              ;
        CMP     R1,#.DAT1       ;DONE WITH SETUP?
        BLOS    1$              ;NO, LOOP AGAIN
        MOV     R3,R0           ;GET ERROR STACK POINTER
        MOV     (R4),R1         ;GET SUBTEST #
        ASL     R1              ;MULTIPLY SUBTEST BY 2
        JMP     @A7TAB(R1)      ;DISPATCH ON SUBTEST
A7TAB:          A7S0            ;0
                A7S1            ;1
                A7S2            ;2
                A7S3            ;3
                A7S4            ;4
                A7S5            ;5


;SEQ265

A7S1:   CMP.F   A7TAB1          ;SUBTEST 1
        BCS     A7UPNT          ;NORMAL SEQ ERROR
        
A7S0:   ISORST
A7S2:   CMP.F   A7TAB2          ;SUBTEST 2
        BCS     A7UPNT          ;NORMAL SEQ ERROR
        ISORST
A7S3:   CMP.F   A7TAB3          ;SUBTEST 3
        BCC     1$              ;SBUS ADR 34,35 O.K.
        PUSH    R0              ;SAVE UML POINTER
        DFRDT                   ;CHK EBUS REG
                167
        POP     R0              ;RESTORE UML POINTER
        TBIT    34
        BEQ     A7S5            ;EBUS REG BAD TOO
        TBIT    35

        BEQ     A7S5            ;
        BR      A7UPNT          ;PRINT UML
1$:     CMP.F   A7TAB4  ;CHK NORMAL SEQ
        BCS     A7UPNT          ;ERROR
        ISORST


;SEQ266

A7S4:   XQT     SETUBR          ;SET UBR
        XQT     GETUBR          ;READ UBR
        DFRDT                   ;GET ERG
                167             
        MSKDAT  ERG1S,M14.26            ;UBR FAIL TOO?
        BCC     A7S5            ;NO, PMA BOARD
        PNTUML  A7U4A           ;PMA, MBZ, CSH
        JMP     (R5)            ;RETURN TO DIACON
A7S5:   PNTUML  A7UML5          ;PMA
        JMP     (R5)             ;RETURN TO DIACON
A7UPNT: MOV     R0,1$           ;SET UML POINTER
        PNTUML
1$:     .WORD   0                       ;UML POINTER GOES HERE
        JMP     (R5)            ;RETURN TO DIACON
BASE20: .WORD   174400          ;DTE20 BASE ADDRESS
DEXWDS:                         ;POINTERS TO DTE20 DEXWDS
.DAT3:  .WORD   174402
        
.DAT2:  .WORD   174404
.DAT1:  .WORD   174406
ERBIT:  .WORD   0               ;SCRATCH WORD
SETUBR: IO10    DATAO PAG,,5    ;17777 IN AC5
GETUBR: IO10    DATAI PAG,,17   ;
ERG1S:  WD36    0000,1777,7000  ;ERG DATA
M14.26: WD36    7777,6000,0777  ;MASK 14-26

        .EVEN


;SEQ267

A7TAB1: .BYTE   174,28.         ;CSH EBOX CYC B
        .BYTE   176,24.         ;CSH EBOX T0
        .BYTE   171,25.         ;CSH EBOX LOAD REG
        .BYTE   175,27.         ;MBOX GATE VMA 27-33
        .BYTE   174,27.         ;CSH ADR PMA EN
        .BYTE   172,26.         ;READY TO GO A
        .BYTE   174,29.!BIT7    ;CACHE IDLE
A7TAB2: .BYTE   176,24.         ;CSH EBOX T0
        .BYTE   174,29.         ;CACHE IDLE
        .BYTE   173,23.         ;MBOX RESP
        .BYTE   171,25.         ;CSH EBOX LOAD DEC
        .BYTE   172,26.         ;READY TO GO A
        .BYTE   103,32.!BIT7    ;CLK MB XFER
A7TAB3: .BYTE   173,35.         ;SBUS ADR 34
        .BYTE   174,35.!BIT7            ;SUBS ADR 35
A7TAB4: .BYTE   176,24.         ;CSH EBOX T0
        .BYTE   171,29.         ;CSH EBOX T1
        .BYTE   164,32.,33.     ;CORE ADR 34, 35
        .BYTE   172,27.         ;CSH USE HOLD
        .BYTE   170,29.         ;CSH EBOX T3
        .BYTE   174,29.         ;CASH IDLE
        .BYTE   173,23.         ;MBOZX RESP
        .BYTE   172,26.!BIT7    ;READY TO GO A
        .EVEN

A7U4A:  UML     <29,20,23>      ;PMA, MBZ, CSH
A7UML5: UML     <29>            ;PMA
        .EVEN

$$FF:   $$CLIT                  ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
.END    STARTA

