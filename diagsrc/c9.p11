.SBTTL          CHANNEL CONTROL TEST 9 ISOLATION ROUTINE

C9:     MOV     (R4),R1         ;GET SUBTEST #
        CMP     R1,#1           ;SUBTEST 1?
        BEQ     3$              ;YES, NO KNOWN FAULTS CAUSE THIS{
        PNTID
          TRAP  125             ;PRINT LOAD ID
        CMP     R1,#5           ;SUBTEST 5?
        BNE     1$              ;NO
        JMP     C9S5            ;YES
1$:     DTEBAS                  ;GET DTE20 BASE ADDRESS
        MOV     #BASE20,R1      ;
2$:     MOV     R0,(R1)+        ;;POINTERS TO TABLE
        INC     R0              ;
        INC     R0              ;
        CMP     R1,#.DAT1       ;DONE WITH SETUP?
        BLOS    2$              ;NO, LOOP AGAIN
        MOV     R3,R0           ;GET ERROR STACK POINTER
        MOV     (R4),R1         ;GET SUBTEST
        BIC     #177770,R1      ;CLEAR CHANNEL #
        ASL     R1              ;MAKE INDEX
        JMP     @C9TAB-4(R1)    ;DISPATCH ON SUBTEST
3$:     JMP     (R5)            ;EXIT
        FATAL                   ;SHOULD NOT DISPATCH HERE

C9TAB:          C9X2            ;SUBTEST X2
                C9X3            ;SUBTEST X3
                C9X4            ;SUBTEST X4
                C9X5            ;SUBTEST X5
                C9X6            ;SUBTEST X6


;SEQ281

C9X2:   CMP.F   S2TAB1          ;CHECK SOME 1-BOARD ITEMS
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S2TAB1          ;SPEC TABLE POINTER
        BCS     2$              ;FOUND ONE, DO BOARD CALLOUT
        CMP.F   S2TAB2          ;CHECK CRC ERR IN
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S2TAB2          ;SPEC TABLE POINTER
        BCC     1$              ;NOT FOUND
        
        PNTUML  U6754           ;CALL OUT CCL, MBZ, CRC,CCW
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD U6754           ;UML TABLE POINTER
        BR      4$              ;RETURN
1$:     CMP.F   S2TAB3          ;CHECK SOME MORE 1-BOARD ITEMS
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S2TAB3          ;SPEC TABLE POINTER
        BCS     2$              ;FOUND ONE, DO BOARD CALLOUT
        CHRISO                  ;UNEXPECTED RESET STATE DEVIATION
          TRAP  125             ;LOAD & GO TO A5 ISOLATOR
2$:     MOV     R0,3$           ;SET UML POINTER
        PNTUML
          TRAP  74              ;PRINT BOARD CALLOUTS
3$:     .WORD   0               ;UML POINTER GOES HERE
4$:     JMP     (R5)            ;RETURN TO DIACON


;SEQ282

C9X3:   CMP.F   S3TAB1  ;CHECK SOME 1-BOARD ITEMS
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S3TAB1          ;SPEC TABLE POINTER
        BCS     3$              ;FOUND, PERFORM CALLOUT
1$:     CMP.F   S3TAB2          ;CHECK MB1 HOLD IN
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S3TAB2          ;SPEC TABLE POINTER
        BCC     2$              ;NO FOUND
        PNTUML  UML36           ;CALL OUT CCL
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD UML36           ;UML TABLE POINTER
        BR      5$              ;RETURN
2$:     CHRISO                  ;UNEXPECTED RESET STATE DEVIATION
          TRAP  125             ;LOAD & GO TO A5 ISOLATOR
3$:     MOV     R0,4$           ;SET UML POINTER
        PNTUML
          TRAP  74              ;PRINT BOARD CALLOUTS
        
4$:     .WORD   0               ;UML POINTER GOES HERE
5$:     JMP     (R5)            ;RETURN TO DIACON

C9X4:   CMP.F   S4TAB1          ;CHECK SOME 1-BOARD ITEMS
          TRAP  76              ;SCAN THE EROR STACK
          .WORD S4TAB1          ;SEPC TABLE POINTER
        BCS     3$              ;FOUND ONE

        CMP.F   S4TAB2          ;CHECK MB HOLDS IN
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S4TAB2          ;SPEC TABLE POINTER
        BCS     1$              ;YEP, THEY WERE WRONG
        CHRISO                  ;UNEXPECTED RESET STATE DEVIATION
          TRAP  125             ;LOAD & GO TO A5 ISOLATOR
1$:     DFRDT,173
        TBIT    16.             ;CHECK HARDWARE FOR CCL CH MB SEL 2


        BEQ     2$              ;NOT SET, CALL OUT CCL, CCW
        PNTUML  UML37           ;CCL OUTPUT OK - CALL OUT MBZ
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD UML37           ;UML TABLE POINTER
        BR      5$              ;RETURN
2$:     PNTUML  U3634           ;CALL OUT CCL, CCW
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD U3634           ;UML TABLE POINTER
        BR      5$              ;RETURN
3$:     MOV     R0,4$           ;SET UML POINTER
        PNTUML  
          TRAP  74              ;PRINT BOARD CALLOUTS
4$:     .WORD   0               ;UML POINTER GOES HERE
5$:     JMP     (R5)            ;RETURN TO DIACON


;SEQ283

C9S5:   CMP.F   S5TAB1          ;CHECK CCL CSH CHAN CYC
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S5TAB1          ;SPEC TABLE POINTER
        BCC     1$              ;NOT FOUND
        PNTUML  U1336           ;CSH, CCL
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD U1336           ;UML TABLE POINTER
        BR      4$              ;RETURN
1$:     CMP.F   S5TAB2          ;CHECK MB IN SELS, DATA CODE
          TRAP  76              ;SCAN THE ERROR STACK
          .WORD S5TAB2          ;SPEC TABLE POINTER
        BCC     3$              ;NOT FOUND
        MOV     R0,2$           ;SET UML POINTER TO SOURCE BOARD
        PNTUML
          TRAP  74              ;PRINT BOARD CALLOUTS
2$:     .WORD   0               ;UML POINTER GOES HERE
        BR      4$              ;RETURN
3$:     PNTUML  U3435           ;ALL OTHER PROBLEMS ON CCW, CRC
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD U3435           ;UML TABLE POINTER
4$:     PNTUML  UML34           ;CHANNEL RELATED PROBS ON CCW
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD UML34           ;UML TABLE POINTER
        JMP     (R5)            ;BACK TO DIACON
C9X6:   CMP     (R3),#FTM36             ;36-BIT ERROR FORMAT?
        BEQ     1$              ;YES, DETERMINE WHAT BITS
        PNTUML  UML37           ;NO, MUST BE MBZ
          TRAP  74              ;PRINT BOARD CALLOUTS
          .WORD UML37           ;UML TABLE POINTER
        BR      3$              ;RETURN
1$:     RANGE                   ;DETERMINE BIT RANGE OF FAILURE
          TRAP  32              ;DETERMINE FAILING BITR RANGE
        MOV     UMLTAB(R0),2$   ;GET UML POINTER
        PNTUML                  ;PRINT BOARD CALLOUT
          TRAP  74              ;PRINT BOARD CALLOUTS
2$:     .WORD   0               ;UML POINTER GOES HERE
3$:     JMP     (R5)            ;RETURN TO DIACON

UMLTAB:         U6414           ;CCL, CCW, MB 30-35
                U6415           ;CCL, CCW, MB 27-29
        U6415           ;CCL, CCW, MB 24-26
                U6414           ;CCL, CCW, MB 12-17
                U6415           ;CCL, CCW, MB 09-11
                U6415           ;CCL, CCW, MB 06-08
                U6416           ;CCL, CCW, MB 00-05
                U3634           ;CCL, CCW BOTH HALVES
                U3634           ;CCL, CCW LOWER HALF

                U3634           ;CCL, CCW UPPER HALF


;SEQ284

S2TAB1: .BYTE   160,16.         ;CHAN PAR ERR
        .BYTE   177,16.         ;CCL ERR REQ
        .BYTE   172,8.,9.,10.!BIT7      ;CCW BUF ADR 0,1A, 2

S2TAB2: .BYTE   172,03.!BIT7    ;CRC ERR IN

S2TAB3: .BYTE   170,2,7         ;CCL MEM STORE REQ
        .BYTE   176,11.         ;CBUS ERROR E
        .BYTE   171,10.         ;CCW CCWF WAITING
        .BYTE   174,18.,19.,16. ;CCL CHAN EPT, CCL CHAN TO MEM,
                                ;CCW, WD0, 1, 2 EN
        .BYTE   173,31.         ;MBX CHAN WR CYC
        .BYTE   162,19.         ;CHAN READ
        .BYTE   175,34.         ;MBX MEM WR RQ IN
        .BYTE   160,20.         ;MBIN SEL 1
        .BYTE   162,20.         ;MB IN SEL 2
        .BYTE   162,20.         ;MB IN SEL 4
        .BYTE   170,35.         ;MBX RQ1 IN
        .BYTE   171,35.         ;MBX RQ2 IN
        .BYTE   176,19.!BIT7    ;CCL START MEM

S3TAB1: .BYTE   174,29.         ;CACHE IDLE
        .BYTE   176,16.         ;CCL STORE CCW
        .BYTE   160,16.!BIT7    ;CHAN PAR ERR

S3TAB2: .BYTE   175,33.!BIT7    ;MB1 HOLD IN
S4TAB1: .BYTE   170,05.         ;CCW WD READY
        .BYTE   176,16.,18.!BIT7        ;CCL STORE CCW, CCL BUF ADR 3

S4TAB2: .BYTE   174,33.         ;MB0 HOLD IN
        .BYTE   175,33.         ;MB1 HOLD IN
        .BYTE   176,33.!BIT7            ;MB2 HOLD IN

S5TAB1: .BYTE   177,18.!BIT7    ;CCL CSH CHAN CYC

S5TAB2: .BYTE   160,20.         ;MB IN SEL 1
        .BYTE   161,20.         ;MB IN SEL 2
        .BYTE   162,20.         ;MB IN SEL 4
        .BYTE   174,32.         ;MB DATA CODE 1
        .BYTE   175,32.!BIT7            ;MB DATA CODE 2

        .EVEN

U1336:  UML     <23,11>         ;CSH , CCL

        UML34:  UML     <12>    ;CCW

        UML36:  UML     <11>    ;CCL


;SEQ285

UML37:  UML     <20>            ;MBZ

U3436:  UML     <11,12>         ;CCL  CCW

U3435:  UML     <12,10>         ;CCW, CRC

U6754:  UML     <11,20,10,12>   ;CCL, MBZ, CRC CCW

U6414:  UML     <11,12,14>      ;CCL, CCW MB SLOT 14

U6415:  UML     <11,12,15>      ;CCL, CCW MB SLOT 15

U6416:  UML     <11,12,16>      ;CCL, CCW MB SLOT 16


BASE20: .WORD   174400          ;DTE20 BASE ADDRESS
        DEXWDS: 
        .DAT3:  .WORD   174402          ;POINTERS TO DTE20 DEXWDS
        .DAT2:  .WORD   174404  
        .DAT1:  .WORD   174406
        ERBIT:  .WORD   0               ;SCRATCH WORD
        .EVEN

        $$FF:   $$CLIT          ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
        .END    STARTA
 