 
;ERR80
 
;SEQ766
 
.IF     NE,.ERR80
.SBTTL  ERR80 - SUBROUTINE TO RECONSTRUCT MICROWORD FAILURES
 
.REM    %
THIS SUBROUTINE INSPECTS THE ERROR STACK FOR A 'CHKMIC' DETECTED MICROWORD
FAILURE, RECONSTRUCTS THE MASKED XOR OF THE ACTUAL AND EXPECTED DATA,
AND RETURNS A COUNT OF THE FAILING BITS IN R0. IN ADDITION, THE C-BIT
IS SET FOR SINGLE-BIT FAILURES. IT EXPECTS LOCATION $$ERS0 TO HAVE A PTR
TO THE ERROR STACK BASE AT ENTRY, AND LEAVES THE MASKED XOR MICROWORD AT $$XOR
FOR FURTHER USE. IT JUMPS TO .ABORT IF THE STACK DOESN'T SEEM RIHT.
THIS ROUTINE USES SUBROUTINES XOR AND TST80.
 
CALL:   JSR     PC,ERR80        ;RECONSTITUTE AN 80-BIT ERROR
 
%
 

ERR80:  SHOVE           ;SAVE R1-R4
        MOV     R5,-(SP)        ;AND R5
 
;CHECK ERROR TYPE:
 
        MOV     (R3),R5         ;GET THE ERROR STACK PTR
        CMP     #FT6,(R5)+      ;SEE IF THIS IS CHKMIC STYLE ERROR
        BEQ     MERXOR          ;GO ON IF IT IS
 
        POP     R5              ;
        GRAB            ;RESORE REGS
        SEC                     ;SET BIT FOR ERROR
        RTS     PC              ;RETURN
 
;XOR THE ACTUAL AND EXPECTED AND PUT RESULT IN $$XOR:
 
MERXOR: MOV     #^D11,R1        ;DO 11 BYTES
        TST     (R5)+           ;ADVANCE PTR TO 1ST BYTE OF ACTUAL
        MOV     R5,R2           ;PUT PTR TO ACTUAL IN R2
        ADD     #13,R5          ;ADVANCE PTR TO 1ST BYTE OF EXPECTED
        MOV     R5,R3           ;PUT PTR TO EXPECTED IN R3
        MOV     #$$XOR,R4       ;PUT PTR TO OUTPUT AREA IN R4
        $XOR                    ;COMPUTE THE XOR
 
;MASK THE RESULT:
 
        MOV     #$$CMSK,R5      ;PUT PTR TO NON-EX BITS MASK IN R5
 
MERMSK: BICB    (R5)+,(R4)+     ;MASK A BYTE
        DEC     R1              ;COUNT DOWN BYTES
        BGT     MERMSK          ;DO THEM ALL
        MOV     (SP)+,R5        ;POP R5
        GRAB                    ;AND R1-R4
        CLC                     ;CLR BIT FOR NO ERROR
        RTS     PC              ;RETURN
 
;CRAM 'MISSING IN ACTION' BIT MASK:
 
$$CMSK: 52525,5,0,0,0,177740
 
.ENDC
 
