.SBTTL          CHANNEL CONTROL TEST 5 ISOLATION ROUTINE

STARTA=.
.=70000

C5:     PNTID
        MOV     R3,R0           ;GET ERROR STACK POINTER
        MOV     (R4),R1         ;GET SUBTEST #
        ASL     R1              ;MAKE INDEX
        JMP     @C5TAB-2(R1)    ;DISPATCH ON SUBTEST
        FATAL                   ;SHOULD NOT DISPATCH HERE

C5TAB:          C5S1            ;SUBTEST 1
                C5S2            ;SUBTEST 2
        C5S3                    ;SUBTEST 3
        C5S4                    ;SUBTEST 4



C5S1:   PNTUML  U2934           ;CALL OUT MBX, CCW
        JMP     (R5)            ;RETURN TO DIACON

C5S2:   CMP.F   S2TAB1          ;CHECK CH TIMING
        BCC     1$              ;NO ERROR FOUND
        PNTUML  U2643           ;CALL OUT CTL, CLK
        MOV     #CLKMSG,R0      ;POINT TO MESSAGE
        PNTAL                   ;PRINT ISOLATION MESSAGE
        BR      8$              ;RETURN
1$:     CMP.F   S2TAB2          ;CHECK CHAN MEM REF, CHAN READ
        BCS     6$              ;FOUND, DO BOARD CALLOUT
        CMP.F   S2TAB3          ;CHECK CCW WD READY
        BCS     6$              ;FOUND, DO BOARD CALLOUT
        CMP.F   S2TAB4          ;CHECK CHAN CORE BUSY
        BCS     3$              ;FOUND
        CMP.F   S2TAB5          ;CHECK CCW WDO REQ
        BCC     4$              ;NOT FOUND
3$:     SSET    11,(R0)         ;SET CCL

        BR      6$              ;DO BOARD CALLOUT


;SEQ253

4$:     CMP.F   S2TAB6          ;CHECK CCL CH TEST MB PAR
        BCS     6$              ;DO BOARD CALLOUT
        CMP.F   S2TAB7          ;CHECK CRC READY IN
        BCC     5$              ;NOT FOUND
        PNTUML  UML36           ;CALL OUT CCL
        BR      8$              ;RETURN
5$:     CHRISO                  ;UNEXPECTED RESET STATE DEVIATION
        
6$:     MOV     R0,7$           ;SET UML POINTER
        PNTUML
7$:     .WORD   0               ;UML POINTER GOES HERE
8$:     JMP     (R5)            ;RETURN TO DIACON


;SEQ254

C5S3:   CMP.F   S3TAB1          ;CHECK SOME 1-BOARD ITEMS
        BCS     11$             ;FOUND ONE, DO CLLOUT
        CMP.F   S3TAB2          ;CHECK CCW CCL CCW REG LOAD
        BCC     1$              ;NOT FOUND
        SSET    11,(R0)         ;SET CCL

        BR      11$             ;DO BOARD CALLOUT
1$:     CMP.F   S3TAB3          ;CHECK CHAN PAR ERR
        BCC     7$              ;NOT FOUND
        CCWRD                   ;READ CCW REG
        CMP     #177000,(R0)    ;IS IT RIGHT?
        BEQ     4$              ;SO FAR
        BIS     #170000,(R0)    ;BITS 20-23?
        CMP     #177000,(R0)    ;
        BEQ     3$              ;YES
        BIS     #007000,(R0)    ;BITS 24-26?
        CMP     #177000,(R0)    ;
        BEQ     2$              ;YES
        PNTUML  U1415           ;MB BITS 27-35 - SLOTS 14-15
        BR      12$             ;RETURN
2$:     PNTUML  UMB15           ;MB BITS 24-26 - SLOT 15
        BR      12$             ;RETURN
3$:     PNTUML  UMB16           ;MB BITS 20-23 - SLOT 16
        BR      12$             ;RETURN
4$:     INC     R0              ;REST OF ADDRESS OK?
        CMPB    #77,(R0)                ;
        BEQ     6$              ;NO MUST BE MBZ
        BIS     #3,(R0)         ;BIT 18 OR 19?
        CMP     #77,(R0)                ;
        BEQ     5$              ;YES
        PNTUML  UMB14           ;BITS 13-17 - SLOT 14
        BR      12$             ;RETURN
5$:     PNTUML  UMB15           ;BIRTS 18 & 19 - SLOT 15
        BR      12$             ;RETURN


;SEQ255

6$:     PNTUML  UML37           ;CALL OUT MBZ
        BR      12$             ;RETURN
7$:     CMP.F   S3TAB4Q         ;CHECK SOME 1-BOARD ITEMS
        CMP.F   S3TAB5          ;CHECK SOME 2-BOARD ITEMS
        BCC     8$              ;NOT FOUND
        SSET    11,(R0)         ;SET CCL

        BR      11$             ;DO BOARD CALLOUT
8$:     CMP.F   S3TAB6          ;CHECK CRC ACT CTR 0R-3R
        BCC     9$              ;NOT FOUND
        PNTUML  UML456          ;CALL OUT CCL, CCW, CRC
        BR      12$             ;RETURN
9$:     CMP.F   S3TAB7          ;CHECK CHAN CORE BYSY
        BCC     10$             ;NOT FOUND
        SSET    23,(R0)         ;SET CSH

        BR      11$             ;DO BOARD CALLOUT
10$:    CMP.F   S3TAB8          ;CHECK MBOX CLEANUP
        BCS     11$             ;FOUND ERROR, DO BOARD CALLOUT
        CHRISO                  ;UNEXPECTED RESET STATE DEVIATION
11$:    MOV     (R0),12$                ;SET UML POINTER
        PNTUML
          .WORD 0               ;UML POINTER GOES HERE
12$:    JMP     (R5)            ;RETURN TO DIACON


;SEQ256

C5S4:   TST(R3)+               ;BYPASS ERROR STACK FORMAT
        MOVB    (R3)+,R0        ;CHECK BITRS 28-35
        CMP     R0,#-1  ;
        BEQ     1$              ;NO ERROR
        BIS     #77,R0          ;BITS 30-35?

        CMP     R0,#-1          ;
        BEQ     4$              ;YES,CALL OUT CCW, MB SLOT 14
        MOVB    (R3),R0         ;CHECK BITS 20-27
        BIS     #177760,R0      ;
        CMP     R0,#-1          ;
        BEQ     2$              ;OK, CALL OUT CCW, MB SLOT 15
        PNTUML  U3436           ;CALL OUT CCW, CCL
        BR      6$              ;RETURN
1$:     MOVB    (R3)+,R0                ;CHECK BITS 20-27
        CMP     R0,#-1          ;
        BEQ     3$              ;NO ERROR, MUST BE 14-19
        BIS     #17,R0          ;BITS 24-27?
        CMP     R0,#-1          ;
        BNE     5$              ;NO CALL OUT CCW, MB SLOT 16
2$:     PNTUML  U3415           ;CALL OUR CCW, MB SLOT 15
        BR      6$              ;RETURN
3$:     MOVB    (R3),R0         ;CHECK IF 18-19
        BIS     #3,R0           ;
        CMP     R0,#77          ;
        BEQ     5$              ;YES
4$:     PNTUML  U3414           ;UML TABLE POINTER
        BR      6$              ;RETURN
5$:     PNTUML  U3416           ;CALL OUT CCW, MB SLOT 16
6$:     JMP     (R5)            ;RETURN TO DIACON

CLKMSG: .ASCII  %TIMING SYNCRONIZATION PROBLEMS ARE USUALLY DUE TO THE\%
        .ASCII  %SIGNAL DIAG CHANNEL CLK FROM THE CTL BOARD NOT BEING\%
        .ASCIZ  %ASSERTED ON A FUNCTION WRITE 76 WITH BIT 25 SET.\%


;SEQ257

S2TAB1: .BYTE   170,11.         ;CH T0
        .BYTE   171,11.         ;CH T1
        .BYTE   172,11.         ;CH T2
        .BYTE   173,11!BIT7     ;CH T3

S2TAB2: .BYTE   161,23.         ;MBZ CHAN MEM REF
        .BYTE   162,19.!BIT7     ;CHAN READ

S2TAB3: .BYTE   170,05.!BIT7    ;CCW WD READY

S2TAB4: .BYTE   160,22.!BIT7    ;CHAN CORE BUSY

S2TAB5: .BYTE   171,05.!BIT7            ;CCW WD0 REQ

S2TAB6: .BYTE   171,19.!BIT7    ;CCL CH TEST MB PAR

S2TAB7: .BYTE   172,01.!BIT7    ;CRC READY IN

S3TAB1: .BYTE   177,13. ;CH CONTR CYC
        .BYTE   170,05.         ;CCW WD READY
        .BYTE   171,19.         ;CCL CH TEST MB PAR
        .BYTE   172,16.!BIT7    ;CCL CCWF T2

S3TAB2: .BYTE   176,06.!BIT7    ;CCL CCW REG LOA

S3TAB3: .BYTE   160,16.!BIT7    ;CHAN PAR ERR

S3TAB4: .BYTE   174,02.         ;CRC WR RAM
        .BYTE   171,16.         ;CCL MB RIP A
        .BYTE   172,18.         ;CCL MB REQ T2
        .BYTE   174,18.         ;CCL CHAN EPT
        .BYTE   175,16.         ;CCL CCWF REQ
        .BYTE   177,16.         ;CCL ERR REQ
        .BYTE   172,01.         ;CRC READY IN
        .BYTE   170,02.         ;CRC MEM STORE ENA
        .BYTE   176,18.!BIT7    ;CCL BUF AD 3

S3TAB5: .BYTE   170,06.         ;CCW CCWF REQ ENA
        .BYTE   172,08.,09.,10. ;CCW BUF ADR 0,1,2
        .BYTE   173,04.         ;CRC RAM  CYC
        .BYTE   172,04.!BIT7    ;
        
S3TAB6: .BYTE   173,1,2,3!BIT7  ;CRC ACT CTR 0R-2R
S3TAB7: .BYTE   160,22.!BIT7    ;CHAN CORE BUSY

S3TAB8: .BYTE   162,19.         ;CHAN READ
        .BYTE   171,28.!BIT7    ;CSH MB WR RQ CLEAR NEXT

        .EVEN


;SEQ258

UMB14:  UML     <14>            ;MB SLOT 14

UMB15:  UML     <15>            ;MB SLOT 15

UMB16:  UML     <16>            ;MB SLOT 16

U1415:  UML     <14,15>         ;MB SLOTS 14 & 15

U2643:  UML     <32,36>         ;CLK CTL

U2934:  UML     <21,12>         ;MBX, CCW

U3414:  UML     <12,14>         ;CCW, MB SLOT 14

U3415:  UML     <12,15>         ;CCW, MB SLOT 15

U3416:  UML     <12,16>         ;CCW    MB SLOT 16

U3436:  UML     <12,11>         ;CCW, CCL

UML36:  UML     <11>            ;CCL

UML37:  UML     <20>            ;MBZ

UML456: UML     <12,10,11>      ;CCW, CRC, CCL

        .EVEN

$$FF:   $$CLIT                  ;FIRST FREE
        $$LITT=.                ;START OF LITERAL AREA
.END    STARTA
  