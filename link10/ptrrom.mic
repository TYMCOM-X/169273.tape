        OPCODELOAD 6500
        NOSYM

; THIS IS THE CODE TO SUPPORT THE 16 CHARACTER PRINTER.
; THIS CODE GOES INTO A CONTROL ROM WHICH PLUGS
; INTO THE PRINTER.  THIS CODE IS INTENDED TO WORK ONLY
; WITH THE OLD (NUC65) SYSTEM.  A NEW VERSION MUST BE
; CREATED TO WORK WITH THE FINAL HHC SYSTEM.  -JSF

PSTK    EQU     069H
GETDOTS EQU     0DDH
T00     EQU     025H
T01     EQU     T00+1
 
        ORG     0E0H

PBUFPT: DS      1
IRQCT:  DS      1
DOTCTR1: DS     1
DOTCTR2: DS     1
PDOTS:  DS      2

        ORG     19CH
PBUF:   DS      18

IOB0    EQU     3EFCH
CTROM   EQU     09000H  ; FOR TESTING

        ORG     CTROM
        DB      VCTRS-. ; DISP TO I/O VECTORS
        DB      'COPYRIGHT FRIENDS AMIS, INC. 1980'

VCTRS:  JMP     INTLO
        JMP     STRTIP
        JMP     STRTOP
        JMP     INTHI

STRTOP: LDY     PBUFPT
        LDA     PSTK,X  ; ACC= CHAR
        STA     PBUF,Y  ; TO PTR BUFFER
        INC     PBUFPT
        CPY     #15     ; END OF LINE?
        BNE     PRXIT   ; BRANCH IF NO
PRLP1:  LDA     PBUF,Y  ; SHIFT BUFFER OVER 2
        STA     PBUF+2,Y
        DEY
        BPL     PRLP1
        INY             ; Y=0
        STY     PBUFPT
        STY     IRQCT
        LDY     #3      ; STRT MOTOR & ENABLE IRQ
        STY     IOB0    ; ENABLE IRQ
        LDA     #17
        STA     DOTCTR1
        LDA     #5
        STA     DOTCTR2
        JSR     GETCOL  ; GET DOT COLUMN
        STA     IOB0+1  ; PRINT FIRST COL
PRXIT:  LDA     #1      ; POP CHAR FROM PSTK
        RTS

GETCOL: LDA     #0
        LDY     DOTCTR2
        CPY     #5      ; INTER CHAR GAP?
        BEQ     GCXIT1  ; BRANCH IF YES
        CPY     #4      ; NEED NEW CHAR?
        BNE     GC10    ; BRANCH IF NO
        LDX     DOTCTR1 ; CHAR INDEX
        LDA     PBUF,X  ; CHAR
        JSR     GETDOTS ; GET CHAR IN DOTS
        LDA     T00
        STA     PDOTS
        LDA     T01
        STA     PDOTS+1
        LDY     DOTCTR2
GC10:   LDA     (PDOTS),Y ; A=DOT COL
GCXIT1: DEC     DOTCTR2
        BPL     GCXIT2  ; BRANCH IF NOT DONE YET
        LDY     #5
        STY     DOTCTR2
        DEC     DOTCTR1
GCXIT2: RTS

INTLO:  INC     IRQCT
        LDA     IOB0 
        AND     #20H    ; IS MOTOR ON?
        BNE     LAST    ; BRANCH IF NO
        LDA     IRQCT
        CMP     #97     ; END OF LINE?
        BNE     NOTEND  ; BRANCH IF NO
LAST:   LDA     #0
        STA     IOB0+1  ; RESET IRQ
        STA     IOB0    ; RETURN CARRIAGE
        RTS
NOTEND: CMP     #96     ; LAST COL?
        BNE     ONWARD  ; BRANCH IF NO
        LDA     #0
        STA     IOB0+1
        RTS
ONWARD: JSR     GETCOL  ; GET NEXT COL
        STA     IOB0+1  ; PRINT IT
NOPRT:  RTS
INTHI   EQU     NOPRT
STRTIP  EQU     NOPRT

        END
