        OPCODELOAD 6500
        NOSYM

        ORG     0E8H
CURADDR: DS     2       ; CURRENT VIDEO RAM ADDR
CURCHR: DS      2       ; CURRENT VIDEO CHARACTER
INDEX:  DS      1       ; VRAM FLAGS

PSTK    EQU     69H
IOB0    EQU     3EFCH
CTROM   EQU     9000H
VRAM    EQU     0B000H

        ORG     CTROM
        DB      VCTRS-.
        DB      'COPYRIGHT FRIENDS AMIS, INC. 1980'

VCTRS:  JMP     INTLO
        JMP     STRTIP
        JMP     STRTOP
        JMP     INTHI

STRTOP: LDA     PSTK,X  ; GET CHAR
        CMP     #'A'    ; FLIP ALPHA/GRAPHICS?
        BNE     TSTB    ; BRANCH IF NO
        LDA     #80H    ; SET BIT
        BNE     SETMODE ; CONTINUE
TSTB:   CMP     #'B'    ; CHANGE GRAPHICS?
        BNE     TSTC    ; BRANCH IF NO
        LDA     #40H    ; SET BIT
        BNE     SETMODE
TSTC:   CMP     #'C'    ; SWAP BANKS?
        BNE     STRTOP1 ; NO- GO BUMP SCREEN
        LDA     #20H    ; SET BANK BIT
        BNE     SETMODE
STRTOP1: LDA    CURCHR+1; NEED TO INIT CHAR?
        BNE     NOINIT  ; BRANCH IF NO
        LDA     #(VRAM SHR 8)   ; SET START ADDRESS
        STA     CURADDR+1       ; *
NOINIT: LDA     #80H
SETMODE: EOR    INDEX   ; INCLUDE OTHER FLAGS
        BMI     .+4     ; BRANCH IF ENABLING IRQ
        STA     INDEX
        STA     IOB0+3  ; SET MODE
        LDA     #1      ; POP 1 BYTE FROM PSTK
STRTIP:
INTHI:  RTS


INTLO:  LDA     #3      ; ENABLE VRAM
        STA     IOB0+2  ; *
INTLO1: LDA     CURCHR
        LDX     CURCHR+1
        BIT     IOB0+3  ; IS VIDEO STILL IN RETRACE?
        BMI     LOXIT   ; BRANCH IF NO
        LDY     #0
        STA     (CURADDR),Y
        INY
        TXA
        STA     (CURADDR),Y
        INC     CURCHR
        BNE     .+4
        INC     CURCHR+1
        INC     CURADDR
        INC     CURADDR
        BNE     INTLO1
        INC     CURADDR+1
        LDA     CURADDR+1
        AND     #04     ; AT 1K YET?
        BEQ     INTLO1  ; BRANCH IF NO
        LDA     INDEX   ; RESET IRQ
        STA     IOB0+3  ; DISSABLE IRQ
        LDA     #(VRAM SHR 8)
        STA     CURADDR+1
LOXIT:  RTS

        END
