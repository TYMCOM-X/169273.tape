TITLE	SEDTTY - CSM supported terminal definitions for the editor
SUBTTL	Modified by Joe Smith, 1-May-82

;Load the editor by: .COMPILE SEDSYM.MAC	;Create SEDSYM.UNV
;		     .LOAD SEDTTY.MAC,SED.MAC	;SED.REL must come last


	SEARCH	SEDSYM
IFN TOPS10,<SEARCH UUOSYM>
IFE TOPS10,<SEARCH MONSYM>
	IFNDEF ITTYPE,<ITTYPE=0>	;Do not call ".TTYPE", rely on TRMOP.
IFN ITTYPE,<.REQUIRE REL:ITTYPE >	;Load routine .TTYPE
	IFNDEF BUG102,<BUG102==-1>	;VT100 with printer port has a bug

	TWOSEG				;Start off in the lowseg
	RELOC	400000			;To HISEG

	EXTERN	PUTSQ1,PUTSTG,PUTTYP,PUTNUM ;Output routines
	EXTERN	TRMNAM			;SIXBIT terminal name specified by user

	INTERN	TERMNL			;Default dispatch address

IFE FTDDT,<DEFINE UNDBUG,<REPEAT 0,>	;Undebugged routines>
IFN FTDDT,<DEFINE UNDBUG,<REPEAT 1,>	;Include undebugged routines>

;=============================================================================


	DEFINE TRMS,<
	X ADM3A, DADM3A		;Lear Seigler Dumb Terminal
	X ADM5,  DADM5		;Lear Seigler Dumb Terminal
	X APPLE8,DAPPLE		;APPLE //e with 80-column card
	X CONCEP,DCONCE		;concept 100 from hds
	X CON108,DCON08		;concept 108
	X GIGI,  DGIGI		;VK100
	X H19,   DH19		;Heathkit H19 in VT52 mode
	X H19A,  DH19A		;Heathkit H19 in ANSI mode
	X HP2621,DH2621		;Hewlett Packard 2621
	X TVI910,DTV910		;Televideo 910
	X TVI925,DTV925		;Televideo 925
	X TVI950,DTV950		;Televideo 950
	X VIS200,DVIS20		;VISUAL-200 in VT52+ mode
	X VK100, DVK100		;GIGI
	X VT52,  DVT52		;Any VT52 look-alike
	X VT100, DVT100		;VT100, VT101, VT125, etc
	X VT102, DVT102		;With insert char, delete char
	X VT220, DVT220		;Similar to VT102
>  ;End of DEFINE TRMS
SUBTTL	List of supported terminal types

	DEFINE	X(NAM,DIS),<
	SIXBIT /NAM/	>

	XALL
TNAMES:	TRMS			;Expand table of SIXBIT names
NUMTRM==.-TNAMES

	DEFINE	X(NAM,DIS),<
	EXP	DIS	>

DSPTAB:	TRMS			;Table of dispatch table addresses

	SALL
SUBTTL	Table of Contents

;           Table of Contents for SED Terminal Handler
;
;
;			   Section			      Page
;
;    1. List of supported terminal types . . . . . . . . . . .   2
;    2. Table of Contents  . . . . . . . . . . . . . . . . . .   3
;    3. Get terminal type
;         3.1   Common . . . . . . . . . . . . . . . . . . . .   4
;         3.2   TOPS20 . . . . . . . . . . . . . . . . . . . .   5
;         3.3   TOPS10 . . . . . . . . . . . . . . . . . . . .   6
;    4. Input table
;         4.1   VT52, VT100, VIS200  . . . . . . . . . . . . .   7
;         4.2   CONCEP, HP2621 . . . . . . . . . . . . . . . .   8
;         4.3   ADM3A, TV910, TV950  . . . . . . . . . . . . .   9
;    5. VT52
;         5.1   Dispatch table . . . . . . . . . . . . . . . .  10
;         5.2   Entry, Exit, and cursor positioning routines .  11
;    6. VIS200
;         6.1   Dispatch table (uses VT52 output routines) . .  12
;    7. HP2621
;         7.1   Dispatch table . . . . . . . . . . . . . . . .  13
;         7.2   Entry, Exit, and cursor positioning routines .  14
;    8. CONCEP
;         8.1   Dispatch table . . . . . . . . . . . . . . . .  15
;         8.2   Entry, Exit, and cursor positioning routines .  16
;    9. VT100
;         9.1   Dispatch table . . . . . . . . . . . . . . . .  17
;         9.2   Entry, Exit, and cursor positioning routines .  18
SUBTTL	Get terminal type -- Common

;Note:	The "HRR TM,TERMNL" in SED.MAC has been changed to "PUSHJ P,TERMNL".
;	This routine will set up TM based on the terminal type in TRMNAM.
;	If TRMNAM is blank, get the terminal type from the monitor.

TERMNL:	SKIPN	T3,TRMNAM		;If terminal type not specified,
	 PUSHJ	P,GETYPE		; get it from the monitor
	MOVE	T1,[-NUMTRM,,TNAMES]	;Point to table
	CAME	T3,(T1)			;Match?
	 AOBJN	T1,.-1			;No, try next
	JUMPGE	T1,BADTYP		;Complain if unknown
	SUBI	T1,TNAMES		;Offset into table

;Here with a known terminal type, T1 has offset into TNAMES table

OKTYPE:	HRR	TM,DSPTAB(T1)		;Set up dispatch table
	MOVE	T2,TNAMES(T1)		;Get actual name of terminal
	MOVEM	T2,TRMNAM		;Store for later

IFN TOPS10,<	;Check for slow terminal
	MOVEI	T1,.TOTSP		;Get the transmit speed
	SETO	T2,			;-1 for this terminal
	MOVE	T3,[2,,T1]		;Point to args
	TRMOP.	T3,			;Read the output baud rate
	  MOVEI T3,.TO240		;Assume 2400
	MOVEI	T4,0			;Assume hi-speed
	CAIGE	T3,.TO240		;Less than 2400 baud?
	 MOVEI	T4,SLW			;Yes, slow terminal
	CAIGE	T3,.TO120		;Less than 1200 baud?
	 MOVEI	T4,SLW!NEL		;Yes, don't update line 24
	IORM	T4,TCH(TM)		;Set slow flags as appropriate

ND .TOLNB,<1023>	;Defined in 7.02
	MOVEI	T1,.TOLNB		;Get length base value
	SETO	T2,			;This terminal
	MOVE	T4,[2,,T1]
	TRMOP.	T4,
	  MOVE	T4,LPP(TM)		;Use hardcoded value for 7.01A
	HLRZ	T1,TRMNAM		;Get the terminal type
;*HACK* - Need to change SED.MAC to make this work on VIS200 and VT52
	CAIN	T1,'VT1'		;Special case only VT100 terminals
	CAIE	T4,^D24			;Normal setting?
	 JRST	OKTYP1			;No, use it
;End of *HACK*
	CAIG	T3,.TO060		;600 baud?
	 MOVEI	T4,^D12			;Yes, 12 lines
	CAIG	T3,.TO030		;300 or 110 baud?
	 MOVEI	T4,^D6			;Yes, 6 lines
OKTYP1:	CAIL	T4,^D6			;Must have a minimum of 6
	 MOVEM	T4,LPP(TM)		;Set lines per page

	MOVEI	T1,.TOWID		;Read terminal width
	SETO	T2,			;This terminal
	MOVE	T3,[2,,T1]		;Point to args
	TRMOP.	T3,			;Get terminal width
	  MOVE	T3,CPL(TM)		;Use hardcoded value (80 columns)
	MOVEM	T3,CPL(TM)		;Store characters per line
>  ;End of IFN TOPS10

CPOPJ:	POPJ	P,			;TRMNAM and TM set up properly


SUBTTL	Get terminal type -- TOPS10

IFN TOPS10,<	;Get terminal type from monitor via TRMOP.

GETYPE:	MOVEI	T1,.TOTRM		;Function to read terminal type
	SETO	T2,			;-1 for this terminal
	MOVE	T3,[2,,T1]		;Point to args
	TRMOP.	T3,			;Get terminal type
	  SKIPA				;Must be 603A
	POPJ	P,
>  ;End of IFN TOPS10

;Here when TRMOP. failed

IFE ITTYPE,<	;Use default if no .TTYPE routine
	MOVE	T3,['VIS200']		;Set to default type
	POPJ	P,
>		;Else ask .TTYPE to read DISPLA.INI

IFN ITTYPE,<	;Ask the ".TTYPE" routine in REL:ITTYPE.REL to find the
		;terminal type.  ITTYPE asks the monitor via TRMOP., if
		;no match found, ITTYPE then searches SYS:DISPLA.INI

BADTYP:	MOVE	T1,[-NUMTRM,,TNAMES]	;Point to table
	MOVE	T2,TRMNAM		;Zero means ask the monitor
	PUSHJ	P,.TTYPE##		;Get name of terminal
	JUMPGE	T1,OKTYPE		;T1 has an index from 0 to NUMTRM-1
	MOVE	T1,0			;Don't return aliases
	PUSHJ	P,.GTYPE##		;Get TRMOP. value in T2
>  ;End of IFN ITTYPE

IFE ITTYPE,<
BADTYP:	MOVE	T2,T3			;Put name in expected AC
>		;Here with unknown type
	HLRZ	T1,T2			;Get first 3 chars of name
	CAIN	T1,'VT1'		;VT101 or similar terminal?
	 JRST	[MOVE	T3,['VT100 ']	  ;Close enough
		 MOVEM	T3,TRMNAM
		 JRST	TERMNL	]	  ;Try again
	CAIN	T1,'VT2'		;VT241 or similar terminal?
	 JRST	[MOVE	T3,['VT220 ']	  ;Close enough
		 MOVEM	T3,TRMNAM
		 JRST	TERMNL	]	  ;Try again
	RESET				;Process tabs as normal
	MOVEI	T1,[ASCIZ /? Not a supported terminal type - "/]
	PUSHJ	P,PUTSTG		;Put in string
	PUSHJ	P,PUT6			;Put SIXBIT from T2 into string
	MOVEI	T1,[ASCIZ /"
? This version of SED works with the following CRT terminals only:/]
	PUSHJ	P,PUTSTG		;Put in string
	MOVEI	T3,0			;Start at beginning

BADTY2:	TRNN	T3,7			;Multiple of 8?
	 JRST	[MOVEI	T1,CR		  ;Yes, put in CRLF
		 IDPB	T1,TY
		 MOVEI	T1,LF
		 IDPB	T1,TY
		 JRST	.+1	]
	MOVEI	T1,TAB			;Get a tab
	IDPB	T1,TY			;Put in string
	MOVE	T2,TNAMES(T3)		;Get name
	PUSHJ	P,PUT6			;Put in string
	CAIE	T3,NUMTRM		;Done all?
	 AOJA	T3,BADTY2		;No, loop
	PUSHJ	P,PUTTYP		;Force out buffer
IFN TOPS10,<	EXIT	1,	>	;Quiet exit
IFE TOPS10,<	HALTF%		>	;Quiet exit
	JRST	.-1			;Implied RESET destroys PIM no-echo

PUT6:	SETZ	T1,			;Routine to put SIXBIT in TY string
	LSHC	T1,6			;Get a char from T2
	JUMPE	T1,CPOPJ		;Stop at end
	ADDI	T1,40			;Else convert to ASCII
	IDPB	T1,TY			;Store char
	JRST	PUT6			;And loop
SUBTTL	Get terminal type -- TOPS20

IFE TOPS10,<	;Get terminal type from monitor via GTTYP%

GETYPE:	MOVEI	1,.PRIOU		;File designator
	GTTYP%				;Do JSYS to get TTY type
	  ERJMP [HALT]			;Should never happen
	MOVSI	T3,'???'		;In case out of range
	CAIGE	2,LENT20		;One we know about?
	 MOVE	T3,T20NAM(2)		;Get SIXBIT name for TOPS-20 TTY number
	POPJ	P,

;Table of TOPS-20 terminal names

T20NAM:	SIXBIT	/TTY33/			; 0 - Model 33 TTY
	SIXBIT	/TTY35/			; 1 - Model 35 TTY
	SIXBIT	/TTY37/			; 2 - Model 37 TTY
	SIXBIT	/EXECUP/		; 3 - TI/EXECUPORT
	SIXBIT	/DM1521/		; 4 - Customer defined (Datamedia 1521)
	SIXBIT	/CUSTO2/		; 5 - Customer defined
	SIXBIT	/CUSTO3/		; 6 - Customer defined
	SIXBIT	/CUSTO4/		; 7 - Customer defined
	SIXBIT	/DEFAUL/		;10 - Default
	SIXBIT	/IDEAL/			;11 - Ideal
	SIXBIT	/VT05/			;12 - VT05
	SIXBIT	/VT50/			;13 - VT50
	SIXBIT	/LA30/			;14 - LA30
	SIXBIT	/GT40/			;15 - GT40
	SIXBIT	/LA36/			;16 - LA36
	SIXBIT	/VT52/			;17 - VT52
	SIXBIT	/VT100/			;20 - VT100
	SIXBIT	/LA38/			;21 - LA38
	SIXBIT	/LA120/			;22 - LA120
LENT20==.-T20NAM

>  ;End of IFE TOPS10
SUBTTL	Input table -- VT52, VT100, VIS200

;This input table sets up the keyboard arrangement for when you need to keep
;TTY PAGE in effect because the terminal sends out its own control-S and Q's
;Those two characters cannot be commands since SED does not know whether the
;user has typed them or they were sent by an overworked terminal.

;So ROLL-BACK-PAGE is ^A instead of ^Q,
;   DELETE-SPACE   is ^L instead of ^S,
;   INSERT-SPACE   is ^K instead of ^A,
;   SLIDE-LEFT     is "8" instead of ^K, and
;   SLIDE-RIGHT    is "9" instead of ^L.
;   RESET          is "." instead of DELETE

	.DIRECTIVE FLBLST	;List one line per EXP statement

	EXP	%DC				;Command for rubout (reset)
IVT52:	EXP	  0,%RBP, %FL, %AB, %IL, %SB, %DL, %PT	;^@ thru ^G
	EXP	%CL, %TB, %LF, %IS, %DS, %RT, %SW, %EC	;^H thru ^O
	EXP	%GO,   0, %SF,   0,%RFL, %BT, %PK,%RBL	;^Q & ^S are nothing
	EXP	%EX,%RFP, %XT				;^X thru ^Z
			  -L.VT52,,I.VT52		;ESCape needs a subtable
	EXP		              0,   0,   0,   0	;Cursor movement

;This table is ordered with the most used commands first

I.VT52:	%CR,,"C"	;ESCape C == RIGHT
	%CL,,"D"	;ESCape D == LEFT
	%CU,,"A"	;ESCape A == UP
	%CD,,"B"	;ESCape B == DOWN
	MVT100,,"["	;ESCape [ == cursor movement on VT100
	 KVT52,,"?"	;ESCape ? == numeric keypad, check next character
	KVT100,,"O"	;ESCape O == numeric keypad on VT100
	%CH,,"H"	;ESCape H == HOME (on VIS200)
	 %$,,"P"	;ESCape P == ENTER  (BLUE-VT52, F0-VIS200, PF1-VT100)
	%RC,,"Q"	;ESCape Q == RECALL ( RED-VT52, F1-VIS200, PF2-VT100)
	%CH,,"R"	;ESCape R == HOME   (GRAY-VT52, F2-VIS200, PF3-VT100)
	%IN,,ESC	;ESCape ESCape == INSERT-MODE
	 %$,,CR		;ESCape RETURN == ENTER
L.VT52==.-I.VT52    ;Length of this subtable

;The following 2 definitions depend on KVT52 immediately following I.VT52
MVT100==I.VT52		;VT100 cursor commands are $[A, $[B, $[C, $[D
KVT100==I.VT52		;VT100 keypads are $OP, $OR, $OQ, $OM, $On, $Op, $Ow

KVT52:	%IN,,"m"	;ESCape ? m == INSERT-MODE	("-" on VIS200, VT100)
	 %$,,"M"	;ESCape ? M == ENTER		(ENTER on keypad)
	%RS,,"n"	;ESCape ? n == RESET		("." on keypad)
	%TA,,"p"	;ESCape ? p == REAL TAB		("0" on keypad)
	%MK,,"w"	;ESCape ? w == MARK		("7" on keypad)
	%RC,,"l"	;ESCape ? l == RECALL		("," on VIS200, VT100)
	%SL,,"x"	;ESCape ? x == SLIDE-LEFT	("8" on keypad)
	%SR,,"y"	;ESCape ? y == SLIDE-RIGHT	("9" on keypad)
	0	    ;End of sub-subtable  (1,2,3,4,5,6 on keypad not used)
SUBTTL	Input table -- CONCEP, HP2621

	EXP	%RS				;Command for rubout (reset)
ICONCE:	EXP	  0,%RBP, %FL, %AB, %IL, %SB, %DL, %PT
	EXP	%DC, %TB, %LF, %IS, %DS, %RT, %SW, %EC
	EXP	%GO,   0, %SF,   0,%RFL, %BT, %PK,%RBL	;^Q & ^S are nothing
	EXP	%EX,%RFP, %XT
			  -L.CONC,,I.CONC		;ESCape needs a subtable
	EXP		              0,   0,   0,   0	;Cursor movement

I.CONC:	%CR,,"="	;ESCape =      == RIGHT
	%CU,,";"	;ESCape ;      == UP
	%CD,,"<"	;ESCape <      == DOWN
	%CL,,">"	;ESCape >      == LEFT
	%CH,,"?"	;ESCape ?      == HOME
	 %$,,":"	;MULTCODE      == ENTER
	%IN,,ESC	;ESCape ESCape == INSERT
			;INSRT         == <ESC><ESC> == INSERT MODE
	%TA,,"]"	;TAB SET       == REAL TAB
       %RBL,,"\"	;SHIFT SCROLL  == ROLL BACK LINE
       %RFL,,"["	;SCROLL        == FORWARD LINE
       %RBP,,"."	;SHIFT PAGE    == ROLL BACK PAGE
       %RFP,,"-"	;PAGE          == FORWARD PAGE
	%BT,,"'"	;B TAB         == BACK TAB
	%MK,,"`"	;TAPE/SHIFT TAPE= MARK
	%RC,,"@"	;CONTROL TAPE  == RECALL
	%RC,,"^"	;CONTROL SHIFT TAPE is unused
			;DEL LINE      == <^F> == DELETE LINE
			;INS LINE      == <^D> == INSERT LINE
			;DEL CHAR      == <^K> == DELETE CHARACTER
L.CONC==.-I.CONC    ;Length of this subtable

;===============================================================================

	EXP	%RS				;Command for rubout (reset)
IH2621:	EXP	  0,%RBP, %FL, %AB, %IL, %SB, %DL, %PT
	EXP	%DC, %TB, %LF, %IS, %DS, %RT, %SW, %EC
	EXP	%GO,   0, %SF,   0,%RFL, %BT, %PK,%RBL	;^Q & ^S are nothing
	EXP	%EX,%RFP, %XT
			  -L.2621,,I.2621		;ESCape needs a subtable
	EXP		              0,   0,   0,   0	;Cursor movement

I.2621:	%CR,,"v"	;ESCape v == RIGHT
	%CL,,"u"	;ESCape u == LEFT
	%CU,,"t"	;ESCape t == UP
	%CD,,"w"	;ESCape w == DOWN
	%CH,,"p"	;ESCape p == HOME
	 %$,,"q"	;ESCape q == ENTER
	%IN,,"r"	;ESCape r == INSERT-MODE
	%MK,,"s"	;ESCape s == MARK
	 %$,,15		;ESCape RETURN == ENTER
L.2621==.-I.2621    ;Length of this subtable
SUBTTL	Input table -- ADM3A, TV910, TV950

	EXP	%RS				;Command for rubout (reset)
IADM3A:	EXP	  0,%RBP, %FL, %AB, %IL, %SB, %DL, %PT
	EXP	%DC, %TB, %LF, %IS, %DS, %RT, %SW, %EC
	EXP	%GO,   0, %SF,   0,%RFL, %BT, %PK,%RBL	;^Q & ^S are nothing
	EXP	%EX,%RFP, %XT
			  -L.ADM3,,I.ADM3		;ESCape needs a subtable
	EXP		            %CH,   0,   0,   0	;Cursor movement

I.ADM3:	 %$,,CR		;ESCape RETURN == ENTER
	%IN,,ESC	;ESCape ESCape == INSERT
	%IS,,"Q"	;ESCape Q      == Insert spaces
	%DC,,"W"	;ESCape W      == Delete Character
	%RT,,"I"	;ESCape I      == Real Tab
	%IL,,"E"	;ESCape E      == Insert Line
	%DL,,"R"	;ESCape R      == Delete Line
		;Recall
		;Mark
L.ADM3==.-I.ADM3    ;Length of this subtable

;===============================================================================

	EXP	%RS				;Command for rubout (reset)
ITV950:	EXP	  0,%RBP, %FL, %AB, %IL, %SB, %DL, %PT
	EXP	%DC, %TB, %LF, %IS, %DS, %RT, %SW, %EC
	EXP	%GO,   0, %SF,   0,%RFL, %BT, %PK,%RBL	;^Q & ^S are nothing
	EXP	%EX,%RFP, %XT
			  -L.TV95,,I.TV95		;ESCape needs a subtable
	EXP		            %CH,   0,   0,   0	;Cursor movement

I.TV95:	 %$,,CR		;ESCape RETURN == ENTER
	%IN,,ESC	;ESCape ESCape == INSERT
	%IS,,"Q"	;ESCape Q      == Insert Spaces
	%DC,,"W"	;ESCape W      == Delete Character
	%RT,,"I"	;ESCape I      == Real Tab
	%IL,,"E"	;ESCape E      == Insert Line
	%DL,,"R"	;ESCape R      == Delete Line
		;Recall
		;Mark
L.TV95==.-I.TV95    ;Length of this subtable
SUBTTL	VT52 -- Dispatch table

	RELOC		;Put in LOWSEG
DH19:
DVT52:	BYTE (7) ESC,"A"	;Cursor up
	BYTE (7) ESC,"B"	; down
	BYTE (7) ESC,"C"	; right
	BYTE (7) 10		; left
	BYTE (7) ESC,"H",CR	; home
	BYTE (7) ESC,"J"	;Clear page
	BYTE (7) ESC,"K"	;Clear line
	BYTE (7) 12		;Roll up and clear line
	BYTE (7) ESC,"I"	;Roll down and clear line
	CPOPJ			;Protect on
	CPOPJ			;Protect off
	0			;Insert line
	0			;Insert space
	0			;Delete line
	0			;Delete space
	BYTE (7) ESC,"Y","7",SP	;Move to bottom
	BYTE (7) ESC,"H",ESC,"J",CR ;Home and clear page
	CVT52			;Routine to position to character
	LVT52			;Routine to position to start of line
	^D24			;Lines per page
	^D80			;Characters per line
	TBS!NPG!MRK!LSD		;+TCH Flags (hard tabs, keep tty page, no prot)
	EVT52			;Routine to call on entry
	XVT52			;Routine to call on exit
	0			;No nulls needed
	IVT52			;Address of input character table
	"~"			;Character for MARK on VT52
	RELOC		;Back to HISEG
SUBTTL	VT52 -- Entry, Exit, and cursor positioning routines

;I - Input character table
	;IVT52 defined above

;E - Set up terminal on entry to SED

EVT52:	MOVE	T1,[BYTE (7) ESC,"="]	;Set numeric keypad in application mode
	PUSHJ	P,PUTSQ1		;Put single word string
	PJRST	PUTTYP			;Dump string to terminal now

;X - Exit from SED
				;<
XVT52:	MOVE	T1,[BYTE (7) ESC,">"]	;Restore keypad to normal numbers
	PUSHJ	P,PUTSQ1
	PJRST	PUTTYP

;C - Position to column

CVT52:	MOVE	T1,[BYTE (7) ESC,"Y"]	;Position to row RW, column CM
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,40(RW)	;Get row position
	IDPB	T1,TY
	MOVEI	T1,40(CM)	;Get column position
	IDPB	T1,TY
	POPJ	P,		;Done

;L - Position to line

LVT52:	MOVE	T1,[BYTE (7) ESC,"Y"]	;Position to row T4, column 0
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,40(T4)	;Get row position from T4
	IDPB	T1,TY
	MOVEI	T1,40		;Move to column zero
	IDPB	T1,TY
	POPJ	P,
SUBTTL	VIS200 -- Dispatch table (uses VT52 output routines)

	RELOC		;Put these in LOWSEG, TCH may be modified
DVIS20:	BYTE (7) ESC,"A"	;Cursor up
	BYTE (7) ESC,"B"	; down
	BYTE (7) ESC,"C"	; right
	BYTE (7) 10		; left
	BYTE (7) ESC,"H",CR	; home
	BYTE (7) ESC,"y"	;Clear page (foreground and background)
	BYTE (7) ESC,"x"	;Clear line (foreground and background)
	BYTE (7) 12		;Roll up and clear line
	BYTE (7) ESC,"I"	;Roll down and clear line
	BYTE (7) ESC,"4"	;Protect on (background)
	BYTE (7) ESC,"3"	;Protect off (foreground)
	BYTE (7) ESC,"L"	;Insert line
	BYTE (7) ESC,"i",SP,ESC,"j" ;Insert space
	BYTE (7) ESC,"M"	;Delete line
	BYTE (7) ESC,"O"	;Delete space
	BYTE (7) ESC,"Y","7",SP	;Move to bottom
	BYTE (7) ESC,"H",ESC,"y",CR ;Home and clear page
	CVIS20			;Routine to position to character
	LVIS20			;Routine to position to start of line
	^D24			;Lines per page
	^D80			;Characters per line
	TBS!NPG!LSD		;+TCH Flags (hard tabs, keep tty page, LF=down)
	EVIS20			;Routine to call on entry
	XVIS20			;Routine to call on exit
	0			;No nulls needed
	IVIS20			;Address of input character table
	RELOC		;Back to HISEG

;I - Input character table
IVIS20==IVT52		;Same as VT52

;E - Set up terminal on entry to SED
EVIS20==EVT52		;Same as VT52

;X - Exit from SED
XVIS20==XVT52		;Same as VT52

;C - Position to column
CVIS20==CVT52		;Same as VT52

;L - Position to line
LVIS20==LVT52		;Same as VT52
SUBTTL	HP2621 -- Dispatch t

	RELOC		;Go to LOWSEG
DH2621:	BYTE (7) ESC,"A"	;Cursor up
	BYTE (7) ESC,"B"	; down
	BYTE (7) ESC,"C"	; right
	BYTE (7) 10		; left
	BYTE (7) ESC,"H",CR	; home
	BYTE (7) ESC,"J"	;Clear page
	BYTE (7) ESC,"K"	;Clear line
	BYTE (7) 12		;Roll up and clear line
	BYTE (7) ESC,"T"	;Roll down and clear line
	BYTE (7) ESC,"&","d","A";Protect on (underline)
	BYTE (7) ESC,"&","d","@";Protect off (no underline)
	BYTE (7) ESC,"L"	;Insert line
	BYTE (7) ESC,"Q",SP,ESC,"R" ;Insert space
	BYTE (7) ESC,"M"	;Delete line
	BYTE (7) ESC,"P"	;Delete space
	BH2621			;Move to bottom
	BYTE (7) ESC,"H",ESC,"J",CR ;Home and clear page
	CH2621			;Routine to position to character
	LH2621			;Routine to position to start of line
	^D24			;Lines per page
	^D80			;Characters per line
	NPG!LSD			;+TCH Flags (keep tty page, LF=down)
	EH2621			;Routine to call on entry
	XH2621			;Routine to call on exit
	0			;No nulls needed
	IH2621			;Address of input character table
	RELOC		;Back to HISEG
SUBTTL	HP2621 -- Entry, Exit, and cursor positioning routines

;I - Input char table
	;IH2621 is defined above

;E - Set up terminal on entry to SED

EH2621:	MOVE	T1,[BYTE (7) ESC,"&","j","B"] ;Enable F1-F8 function keys
	PUSHJ	P,PUTSQ1		;Put single word string
	PJRST	PUTTYP			;Dump string to terminal now

;X - Exit from SED

XH2621:	MOVE	T1,[BYTE (7) ESC,"&","j","@"] ;Display primary screen labels
	PUSHJ	P,PUTSQ1
	PJRST	PUTTYP

;C - Position to column

CH2621:	MOVE	T1,[BYTE (7) ESC,"&","a"]	;Position to row RW, column CM
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,(RW)		;Get row position
	PUSHJ	P,PUTNUM	;Convert to ASCII
	MOVEI	T1,"y"		;Screen address
	IDPB	T1,TY		;To separate coords
	MOVEI	T1,(CM)		;Get column position
	PUSHJ	P,PUTNUM	;Put in string
	MOVEI	T1,"C"		;Code for positioning
	IDPB	T1,TY
	POPJ	P,		;Done

;L - Position to line

LH2621:	MOVE	T1,[BYTE (7) ESC,"&","a"]	;Position to row T4, column 0
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,(T4)		;Get row position from T4
	PUSHJ	P,PUTNUM	;Convert to ASCII
	MOVE	T1,[ASCII /y0C/];Move to column zero
	PJRST	PUTSQ1		;Store string and return

;B - Move to bottom (takes more than 5 chars)

BH2621:	MOVEI	T1,[BYTE (7) ESC,"&","2","3","y"	;Move to bottom
		    BYTE (7) "0","C"]
	PJRST	PUTSTG
SUBTTL	CONCEP -- Dispatch table

IFNDEF CON108,<CON108==1>	;Alternate escape sequences
	DQ=""""			;Double quote

	RELOC		;Put in LOWSEG
DCON08:
DCONCE:	BYTE (7) ESC,";"	;Cursor up
	BYTE (7) ESC,"<"	; down  >
	BYTE (7) ESC,"="	; right
	BYTE (7) 10		; left
	BYTE (7) ESC,"?",CR	; home
IFE CON108,<	;Use ESCape + Control-Char on older 100's
	BYTE (7) ESC,"C"-100	;Clear to end of page
	BYTE (7) ESC,"S"-100	;Clear to end of line
	BYTE (7) 12		;Roll up and clear line
	BYTE (7) ESC,"?",ESC,"R"-100;Roll down and clear line
	BYTE (7) ESC,"E"	;Protect on (background)
	BYTE (7) ESC,"e"	;Protect off (foreground)
	BYTE (7) ESC,"R"-100	;Insert line
	0	;BYTE (7) ESC,"P"-100,SP,ESC,"@"-100 ;Insert space
	BYTE (7) ESC,"B"-100	;Delete line
	BYTE (7) ESC,"Q"-100	;Delete space
	BYTE (7) ESC,"a","7",SP	;Move to bottom
	BYTE (7) ESC,"?",ESC,"C"-100,CR ;Home and clear page
	CCONCE			;Routine to position to character
	LCONCE			;Routine to position to start of line
	^D24			;Lines per page
	^D80			;Characters per line
>  ;End of IFE CON108
IFN CON108,<	;Use ESCape + Space + Normal-Char on new 108's
	BYTE (7) ESC,SP,"#"	;Clear to end of page
	BYTE (7) ESC,SP,"3"	;Clear to end of line
	BYTE (7) 12		;Roll up and clear line
	BYTE (7) ESC,"?",ESC,SP,"2" ;Roll down and clear line
	BYTE (7) ESC,"E"	;Protect on (background)
	BYTE (7) ESC,"e"	;Protect off (foreground)
	BYTE (7) ESC,SP,"2"	;Insert line
	SCONCE			;Insert space (requires more than 5 chars)
	BYTE (7) ESC,SP,DQ	;Delete line
	BYTE (7) ESC,SP,"1"	;Delete space
	BYTE (7) ESC,"a","7",SP	;Move to bottom
	BYTE (7) ESC,"?",ESC,SP,"#" ;Home and clear page
	CCONCE			;Routine to position to character
	LCONCE			;Routine to position to start of line
	^D24			;Lines per page
	^D132			;Characters per line
>  ;End of IFN CON108
	TBS!NPG!LSD		;+TCH Flags (hard tabs, keep tty page, LF=down)
	ECONCE			;Routine to call on entry
	XCONCE			;Routine to call on exit
	0			;No nulls needed
	ICONCE			;Address of input character table
	RELOC		;Back to HISEG
SUBTTL	CONCEP -- Entry, Exit, and cursor positioning routines

;I - Input char table
	;ICONCE defined above

;E - Entry to SED

ECONCE:	MOVEI	T1,[BYTE (7) 14,ESC,"U",ESC,"X"	;Clear, programmer, xmit keypad
		BYTE (7) ESC,SP,"w",ESC,"4"	;Autowrap off
		BYTE (7) DQ,"0",DQ,ESC,ESC	;Program INSRT key
		BYTE (7) ESC,"4","!","1",DQ	;Program DEL CHAR
		BYTE (7) "L"-100,ESC,"4","!","2";Program INS LINE
		BYTE (7) DQ,"D"-100,ESC,"4","!"
		BYTE (7) DQ,DQ,"F"-100,ESC,SP	;Program DEL LINE
		BYTE (7) "x","!",SP,"!",0]	;RESET/STAT to execute
	PUSHJ	P,PUTSTG
	PUSHJ	P,PUTTYP			;Output the string so far
	OUTSTR	[BYTE (7) ESC,"o","*",0]	;Change the EOM character
	IONEOU	[0]				;To NUL

IFN TOPS10,<	;Check terminal width setting
	MOVEI	T1,.TOWID	;Read terminal width
	SETO	T2,		;This terminal
	MOVE	T3,[2,,T1]	;Point to args
	TRMOP.	T3,		;Get terminal width
>  ;End of IFN TOPS10
	  MOVEI	T3,^D80		;Assume 80 column mode
	CAIE	T3,^D132	;Set to 133 columns?
	 MOVEI	T3,^D80		;Yes, that is for attached printer
	MOVEM	T3,CPL(TM)	;Store characters per line
	MOVEI	T1,[BYTE (7) ESC,"v",40,40,40+^D24,40+^D80,0];Window 24 by 80
	CAIE	T3,^D80		;Width 132 (not 80 or 133) ?
	 MOVEI	T1,[BYTE (7) ESC,DQ,ESC,"v",40		;Set 132 column mode
		BYTE (7) 40,40+^D24,1,^D<132-64>,0]	;Window 24 by 132
	PUSHJ	P,PUTSTG
	PJRST	PUTTYP

;X - Exit from SED

XCONCE:	MOVE	T3,CPL(TM)		;Get Characters per Line
	MOVE	T1,[BYTE (7) ESC,"!",0]	;Code to reset to 80 column mode
	CAIN	T3,^D132		;Wide mode?
	 PUSHJ	P,PUTSQ1		;Yes, cancel it
	MOVEI	T1,[BYTE (7) ESC,"4",SP,"0","!"	;Reset INSRT
		BYTE (7) ESC,"4",SP,"1","!"	;Reset DEL CHAR
		BYTE (7) ESC,"4",SP,"2","!"	;Reset INS LINE
		BYTE (7) ESC,"4",SP,DQ,"!"	;Reset DEL LINE
		BYTE (7) ESC,"v",40,40,1	;Set window to 96 lines
		BYTE (7) ^D<96-64>,40+^D80,ESC,"x",ESC ;Keypad to execute
		BYTE (7) SP,"W",ESC,"o","*"	;Wraparound on, End-of-Message
		BYTE (7) CR,ESC,"u",0]		; to <RET>, user mode
	PUSHJ	P,PUTSTG
	PJRST	PUTTYP

;C - Position to column

CCONCE:	MOVE	T1,[BYTE (7) ESC,"a"]	;Position to row RW, column CM
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,40(RW)	;Get row position
	IDPB	T1,TY
	MOVEI	T1,40(CM)	;Get column position
	IDPB	T1,TY
	POPJ	P,		;Done

;L - Position to line

LCONCE:	MOVE	T1,[BYTE (7) ESC,"a"]	;Position to row T4, column 0
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,40(T4)	;Get row position from T4
	IDPB	T1,TY
	MOVEI	T1,40		;Move to column zero
	IDPB	T1,TY
	POPJ	P,

;S - Insert a space

SCONCE:	MOVEI	T1,[BYTE (7) ESC,SP,"0",SP,ESC,SP,SP,0]
	PJRST	PUTSTG		;Insert mode on, SP, insert mode off
SUBTTL	VT100 -- Dispatch table

	RELOC		;Put in LOWSEG
DH19A:!				;Heathkit H19 in ANSI mode
DVT102:!DVT220:!		;VT102 and VT200 series have insert/delete
DVK100:!DGIGI:!			;GIGI terminal in ANSI mode
DVT100:	PHASE	0		;VT100,VT101,VT125,VT180
  CUP:	BYTE (7) ESC,"[","A"	;Cursor up
  CDN:	BYTE (7) ESC,"[","B"	; down
  CRG:	BYTE (7) ESC,"[","C"	; right
  CLF:	BYTE (7) 10		; left
  CHM:	BYTE (7) ESC,"[","H",CR	; home
  CPG:	BYTE (7) ESC,"[","J"	;Clear page
  CLN:	BYTE (7) ESC,"[","K"	;Clear line
  RUP:	BYTE (7) ESC,"D"	;Roll up and clear line
  RDN:	BYTE (7) ESC,"M"	;Roll down and clear line
  PON:	BYTE (7) ESC,"[","7","m"; Protect on
  POF:	BYTE (7) ESC,"[","0","m";Protect off
  ILN:	IDARGS,,ILVT10		;Insert line (args in T3 and T4)
  ISP:	0			;Insert space (VT102 only)
  DLN:	IDARGS,,DLVT10		;Delete line (args in T3 and T4)
  DSP:	0			;Delete space (VT102 only)
  MVB:	BVT100			;Move to bottom
  HCP:	HVT100			;Home and clear page
  PSC:	CVT100			;Routine to position to character
  PSL:	LVT100			;Routine to position to start of line
  LPP:	^D24			;+LPP Lines per page
  CPL:	^D80			;+CPL Characters per line
  TCH:	TBS!NPG!LSD		;+TCH Flags (hard tabs, keep tty page, LF=down)
  RTE:	EVT100			;Routine to call on entry
  RTX:	XVT100			;Routine to call on exit
  NUL:	0			;No nulls needed
  ITB:	IVT100			;Address of input character table
  MAR:	0			;Use PON to mark position
	DEPHASE
	RELOC		;Back to HISEG
SUBTTL	VT100 -- Entry, Exit, and cursor positioning routines

;I - Input character table

IVT100==IVT52			;Same as VT52

;E - Set up terminal on entry to SED

EVT100:	MOVEI	T1,[BYTE (7) ESC,"<",ESC,"[","H";> Force ANSI mode, HOME
		BYTE (7) ESC,"=",ESC,"[","?"	;Set keypad mode
		BYTE (7) "4","l",0 ]		;Disable smooth scrolling
	PUSHJ	P,PUTSTG	;Set up VT100

	MOVE	T1,TRMNAM	;Get terminal type
	CAME	T1,['VK100 ']	;GIGI?
	CAMN	T1,['GIGI  ']	; ...
	 JRST	[SETZM	ILN(TM)	  ;Yes, cannot use limited scrolling region
		 SETZM	DLN(TM)	  ; to insert/delete lines
		 MOVEI	T1,PGIGI  ;Get addr of "protect on" routine
		 MOVEM	T1,PON(TM); since string is more than 5 characters
		 PJRST	PUTTYP]
	MOVEI	T1,1		;Set up full scrolling region
	PUSHJ	P,SVT100
	MOVE	T1,TRMNAM
	HLRZ	T2,T1		;Get 1st 3 letters
	CAIE	T2,'VT2'	;VT200 series?
	CAMN	T1,['VT131 ']
	 JRST	EVT102		;Can do insert/delete characters
	CAME	T1,['VT132 ']
	CAMN	T1,['VT102 ']	;VT102?  (assumed to have printer port)
	 JRST	EVT102
	PJRST	PUTTYP		;Just a simple VT100

EVT102:	MOVSI	T1,[IDARGS,,ILV102	;Insert line
		    IDARGS,,ICV102	;Insert character
		    IDARGS,,DLV102	;Delete line
		    IDARGS,,DCV102]	;Delete character
	HRRI	T1,ILN(TM)	;Destination
	BLT	T1,DSP(TM)	;Set up for VT102/VT132 insert/delete
	PJRST	PUTTYP

;X - Exit from SED

				; <
XVT100:	MOVEI	T1,[BYTE (7) ESC,">",ESC,"[","?","4","h",ESC,"[","1"
		BYTE (7) ";","2","4","r",ESC,"[","H",CR,LF,0]
	PUSHJ	P,PUTSTG	;Disable keypad, enable smooth scrolling
	PJRST	PUTTYP
;C - Position to column

CVT100:	MOVE	T1,[BYTE (7) ESC,"["]	;Position to row RW, column CM
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,1(RW)	;Get row position
	PUSHJ	P,PUTNUM	;Convert to ASCII
	MOVEI	T1,";"		;Semicolon
	IDPB	T1,TY		;To separate coords
	MOVEI	T1,1(CM)	;Get column position
	PUSHJ	P,PUTNUM	;Put in string
	MOVEI	T1,"H"		;Code for positioning
	IDPB	T1,TY
	POPJ	P,		;Done

;L - Move to line

LVT100:	MOVE	T1,[BYTE (7) ESC,"["]	;Position to row T4, column 0
	PUSHJ	P,PUTSQ1	;Start it off
	MOVEI	T1,1(T4)	;Get row position
	PUSHJ	P,PUTNUM	;Convert to ASCII
	MOVE	T1,[ASCII /;1H/];Move to column zero
	PJRST	PUTSQ1		;Store string and return

;H - HOME and erase (takes more than 5 characters)

HVT100:	MOVEI	T1,[BYTE (7) ESC,"[","H",ESC,"["	;Home and clear page
		    BYTE (7) "J",CR,0]
	PUSHJ	P,PUTSTG
	PUSHJ	P,PUTTYP	;Output it now
	MOVEI	T1,^D0100	;Delay just a little
IFN TOPS10,<	HIBER	T1,
		  JFCL	>
IFE TOPS10,<	DISMS	>
	POPJ	P,

;B - Move to bottom

BVT100:	PUSH	P,T4
	MOVE	T4,LPP(TM)	;Lines per page
	SUBI	T4,1		;Set to what LVT100 expects
	PUSHJ	P,LVT100
	POP	P,T4
	POPJ	P,
;NOTE:	SED.MAC was modified to notice IDARGS bit set on dispatch

;IL - Insert line, T3 has the row, T4 has how many lines to insert
;Cursor is already at the row specified by T3, set scrolling region to be
;from there to bottom, roll the screen down using reverse index, reset.

ILVT10:	JUMPE	T3,ILVT00	;Optimize insert at top
	PUSH	P,T4
	MOVEI	T1,1(T3)	;First roll to scroll
	PUSHJ	P,SVT100	;Set scrolling region
	MOVE	T4,T3		;Bug in VT100 ROM demands that the cursor be
	PUSHJ	P,LVT100	; repositioned after setting scroll region
	POP	P,T4

	MOVE	T1,[BYTE (7) ESC,"M"]
	PUSHJ	P,PUTSQ1	;Roll screen down 1 line
	SOJG	T4,.-2		;Repeat
	JRST	RVT100		;Restore the initial state

ILVT00:	MOVE	T1,[BYTE (7) ESC,"M"]
	PUSHJ	P,PUTSQ1	;Roll down from top of screen
	SOJG	T4,.-2
	POPJ	P,

;DL - Delete line, T3 has the row, T4 has number of lines to delete
;Cursor is already at the row specified by T3, set scrolling region to be
;from there to bottom, roll the screen up using index, reset.

DLVT10:	MOVE	T1,LPP(TM)	;Get lines per page
	CAIL	T3,-1(T1)	;Deleting the bottom line?
	 JRST	[MOVE	T1,CLN(TM)	;Yes, just erase it
		 PJRST	PUTSQ1	]	; and let SED write new bottom line
	MOVEI	T1,1(T3)	;Get the current row
	PUSHJ	P,SVT100	;Make it the top of the region
	PUSHJ	P,BVT100	;Go to bottom of screen
	MOVE	T1,[BYTE (7) ESC,"D"]
	PUSHJ	P,PUTSQ1	;Roll screen up 1 line
	SOJG	T4,.-2		;Repeat
				;Fall into RVT100
;Reset scrolling region on VT100

RVT100:	MOVEI	T1,1		;Reset scrolling region
	PUSHJ	P,SVT100
	MOVE	T4,T3		;Reposition to proper row
	PJRST	LVT100

;Set scrolling region - call with top row in T1 (a number from 1 to 23)

SVT100:	PUSH	P,T1		;Save top row
	MOVE	T1,[BYTE (7) ESC,"[",0]
	PUSHJ	P,PUTSQ1
	POP	P,T1
	PUSHJ	P,PUTNUM	;Output digits
	MOVSI	T1,(ASCIZ /;/)
	PUSHJ	P,PUTSQ1
	MOVE	T1,LPP(TM)	;Lines per page
	PUSHJ	P,PUTNUM
	MOVSI	T1,(ASCIZ /r/)
	PJRST	PUTSQ1		;Store and return

;"Protected" field for a GIGI

PGIGI:	MOVEI	T1,[BYTE (7) ESC,"[","2",";","7","m",0]
	PJRST	PUTSTG		;Dim inverse video on GIGI


;Insert spaces for VT102, number in T4

ICV102:	MOVE	T1,[BYTE (7) ESC,"[","4","h"]
	PUSHJ	P,PUTSQ1	;Turn on INSERT mode
	MOVEI	T0,SP		;Get a space
	IDPB	T0,TY		;Store in string
	SOJG	T4,.-1		;T4 has number of spaces to insert
	MOVE	T1,[BYTE (7) ESC,"[","4","l"]
	PJRST	PUTSQ1		;Disable INSERT mode

;Delete spaces for VT102, number in T4

DCV102:	MOVEI	T3,"P"		;Last char of sequence
	PJRST	ESCNUM		;Output ESC, "[", number, and letter

IFN BUG102,<	;A VT100 with printer port has the following problems:
	;1) $[10L to insert 10 lines is much slower than split scroll
	;2) $[10M to delete 10 lines is much slower than split scroll
	;3) New lines added to the screen have the same character attributes
;as the preceeding line.  If you INSERT-LINE while positioned to a line with
;inverse video characters, the new "blank" line will have inverse video spaces.
;If you DELETE-LINE and line 24 has inverse video, the new "blank" line on
;line 24 will also have inverse video spaces.  (Common with /DTABS)
ILV102=ILVT10	;Use split scrolling region to insert line
DLV102=DLVT10	;Use split scrolling region to delete line
>  ;End of BUG102

IFE BUG102,<	;Use these at your own risk
;Insert lines for VT102, number in T4
ILV102:	MOVEI	T3,"L"		;Last char of sequence
	PJRST	ESCNUM		;Output ESC, "[", number, and letter

;Delete lines for VT102, number in T4
DLV102:	MOVEI	T3,"M"		;Last char of sequence
;*;	PJRST	ESCNUM		;Output ESC, "[", number, and letter
>  ;End of BUG102

ESCNUM:	MOVE	T1,[BYTE (7) ESC,"["]
	PUSHJ	P,PUTSQ1	;Start it off
	MOVE	T1,T4		;Number of repetitions
	PUSHJ	P,PUTNUM	;Output digits
	IDPB	T3,TY		;Output final uppercase letter
	POPJ	P,
SUBTTL	Unimplemented terminal types

DAPPLE:	DADM3A:	DADM5:	DTV910:	DTV925:	DTV950:
	PHASE	0
CUP:	00		;Up
CDN:	01		;Down
CRG:	02		;Right
CLF:	03		;Left
CHM:	04		;Home
CPG:	05		;Clear to end of page
CLN:	06		;Clear to end of line
RUP:	07		;Roll screen up and clear new line
RLD:	10		;Roll screen down, ditto
PON:	11		;Protect on
POF:	12		;Protect off
ILN:	13		;Insert a blank line
ISP:	14		;Insert a space
DLN:	15		;Delete a line
DSP:	16		;Delete a space
MVB:	17		;Move to bottom of page
HCP:	20		;Home and clear page
PSC:	21		;Position to a particular row, column
PSL:	22		;Position to start of line
LPP:	23		;Lines per page
CPL:	24		;Characters per line
TCH:	25		;Terminal characteristics flags
RTE:	EDUMMY		;Routine to call when editor is started
RTX:	27		;Routine to call when editor is exited
NUL:	30		;Number of nulls to output,,null character
ITB:	31		;Address of input character table
MAR:	32		;Mark to put up on enter (if no protected fields)
	DEPHASE
EDUMMY:	OUTSTR	[ASCIZ	/
Sorry, but this type of terminal is not supported yet/]
	EXIT

COMMENT ~
APPLE8
  ^K $F Clear to end of screen
  ^L $@ Home and erase
  ^N    Normal video
  ^O    Reverse video
  ^Y    Home
  ^Z    Clear all of current line
  ^\    Forward Space
  ^] $E Clear to end of line
  ^^    Goto column, row
  $A Up, $B Right, $C Left, $D Down




Data General DASHER D200        NEWLINE sends LF, does CR and LF functions

ERASE PAGE      CMD PRINT       ERASE EOL
Ctrl-L          Ctrl-^ Ctrl-Q   Ctrl-K

  C1            UP ARROW          C2                Direct Cursor Addressing
Ctrl-^ \        Ctrl-W          Ctrl-^ ]            is not known.

LEFT ARROW        HOME          RIGHT ARROW
Ctrl-Y          Ctrl-H          Ctrl-X

  C3            DOWN ARROW        C4
Ctrl-^ ^        Ctrl-Z          Ctrl-^ _

Control-circumflex is followed by a single character.  (S1 = Shift-F1)

C1  C2  C3  C4  S15 S1  S2  S3  S4  S5  S6  S7  S8  S9  S10 S11 S12 S13 S14
 \   ]   ^   _   `   a   b   c   d   e   f   g   h   i   j   k   l   m   n

CMD+PRINT       F15 F1  F2  F3  F4  F5  F6  F7  F8  F9  F10 F11 F12 F13 F14
 Ctrl-Q          p   q   r   s   t   u   v   w   x   y   z   {   |   }   ~

LITS:	END

    c@KÐ