        Title	SEDTTY - SED Terminal Definitions for STC/DCD,  4-MAR-82
	Subttl	Doug Demos,  from originals by DEC


; Search the universals we need

	SEARCH	SEDSYM,UUOSYM,MACTEN
	IFE TOPS10,<SEARCH MONSYM>

	EXTERN	CPOPJ,PUTSQ1,PUTNUM,PUTTYP,PUTSTG,TRMNAM,HOMPOS
	INTERN	TERMNL

; Here are the listing controls

	SALL			; Nice listings
	.DIRECTIVE FLBLST	; First line binary lists only



Comment ~

	This file is intended to  be  the  description  for  all
display  terminals  to which SED can converse.  Rather than have
the editor loaded for  one  and  only  one  terminal  type,  the
ability  to chose at run time is required.  Therefore, this file
contains the DECIDE routine and the  supporting  input,  output,
and other tables for all terminals.  The decide routine will get
the terminal type from the monitor if possible (7.??  for TOPS10
and   any   TOPS20);    if   this  is  not  possible,  an error
return stating terminal type cannot be determine is taken and
the editor takes an exit.     As  a  user   option,   a   switch
/TTYP:VALUE  has  been added.  It can be used at startup time to
avoid the error message.  This is  useful  for  compatible term-
inals that are not supported but must be used.All switches can be
put in SWITCH.INI for default application.

~;  End of Comment
	Subttl	Table of Contents

;		   Table of Contents for SEDTTY.MAC
;
;			     Section			      Page
;
;    1. Table of Contents  . . . . . . . . . . . . . . . . . .   2
;    2. Revision History . . . . . . . . . . . . . . . . . . .   3
;    3. Parameters and LOW Segment Storage . . . . . . . . . .   4
;    4. Main Decision Algorithm  . . . . . . . . . . . . . . .   5
;    5. Terminal Tables  . . . . . . . . . . . . . . . . . . .   6
;    6. Character Tables
;         6.1   ADM2 . . . . . . . . . . . . . . . . . . . . .   7
;         6.2   ADM3A  . . . . . . . . . . . . . . . . . . . .   8
;         6.3   IQ120  . . . . . . . . . . . . . . . . . . . .   9
;         6.4   Visual 200 . . . . . . . . . . . . . . . . . .  10
;         6.5   VT52 . . . . . . . . . . . . . . . . . . . . .  12
;         6.6   VT100  . . . . . . . . . . . . . . . . . . . .  13
;    7. Service Routines
;         7.1   ADM2, ADM3A, and IQ120 . . . . . . . . . . . .  19
;         7.2   Visual 200 . . . . . . . . . . . . . . . . . .  20
;         7.3   VT52 . . . . . . . . . . . . . . . . . . . . .  21
;         7.4   VT100  . . . . . . . . . . . . . . . . . . . .  22
;    8. IDPY
;         8.1   Read the Default Display Type  . . . . . . . .  24
;         8.2   SIXIN - Get a SIXBIT Word from DISPLA.INI  . .  30
;         8.3   VIDIN - Get a Byte from DISPLA.INI . . . . . .  31
;         8.4   SIXOCT - Convert Sixbit to Octal . . . . . . .  32
;         8.5   TSTABR - Test for Abbreviated Terminal Type  .  33
;    9. The End  . . . . . . . . . . . . . . . . . . . . . . .  34
	Subttl	Revision History

; Edit     Date   Who	Description
; ====     ====   ===	===========

;  1    11-Aug-80 TJW	Initial version from DEC originals
;
;  2    15-Aug-80 TJW	Use SEDSYM right.
;
;  3    24-Aug-80 TJW	Add Visual 200 flavor of a VT52
;
;  4	23-Sep-80 TJW	Correct register use bugs.
;
;  5	4-Mar-82  DBD   Rebuilt for new SED release.
;
;  6    4-Mar-82  DJS   Remove all DISPLA.INI code as we have for TECO.
;			Program will now only work on 7.?? monitors.
;

; End of Revision History
	Subttl	Parameters and LOW Segment Storage

; Definitions for ACs and parameters

	C==7				; For character
	M1==10				; Set of four
	M2==11				; ...
	M3==12				; ...
	M4==13				; ...

	F%TMP1==1B0			; Temporary flag
	TTY==4				; I/O channel for STD:DISPLA.INI

; Storage for permanent variables:

SAVEAC:	BLOCK	20			; Save all ACs
CRTTYP:	BLOCK	1			; Storage for crt type
MONCMT:	BLOCK	1			; Comment flag
VIDOPN:	BLOCK	3			; Open block for STD:DISPLA.INI
VIDLOK:	BLOCK	4			; Lookup block ...
NODNAM:	BLOCK	1			; Node name from file
LINNAM:	BLOCK	1			; Line number from file
VIDNAM:	BLOCK	1			; Display type from file
NODNUM:	BLOCK	1			; Node number of this TTY
LINNUM:	BLOCK	1			; Line number of this TTY
MONCMI:	BLOCK	1			; Comment character seen in STD:DISPLA.INI
VIDHED:	BLOCK	3			; Buffer header
DPYBUF:	BLOCK	203			; Hard coded space for buffer
	Subttl	Main Decision Algorithm

; Here to decide which terminal type the user has.  Save all  ACs.
; Call  the  IDPY  routine to actually decide the matter.  When it
; returns, set up accumulator TM with the index  into  TERMNL  and
; TRMNAM with the sixbit terminal name from CRTTAB.


DECIDE::MOVEM	F,SAVEAC+F		; Save AC F
	MOVE	F,[TM,,SAVEAC+TM] 	; Save ACs TM-CM (6-16)
	BLT	F,SAVEAC+CM		; Do it
	SKIPE	T3,TRMNAM##		; Has this been done
	  JRST	DECI01			; Yes, so continue

; Here to read STD:DISPLA.INI (or monitor) for terminal type

	PUSHJ	P,IDPY			; Get the terminal type
	  JRST	NOTKWN			; Cannot procede without this knowledge
	JRST	DECI02			; Continue

; Here when we have searched before

DECI01:	MOVE	T1,[-NUMCRT,,CRTTAB]	; Table pointer
	PUSHJ	P,TSTABR		; Do the table search
	  JRST	NOTKWN			; Terminal name is unknown to us

; Restore ACs and set up TM and TRMNAM, then return

DECI02:	MOVE	CM,[SAVEAC+F,,F]	; Restore ACs F-CM (5-16)
	BLT	CM,CM			; Do it
	MOVE	T2,CRTTAB(T1)		; Get terminal name
	MOVEM	T2,TRMNAM##		; Save for later
	HRR	TM,TERMNL(T1)		; And the terminal index to TM
	TRNN	T1,-1			; Since we are called from tty type 0 table,
	  POPJ	P,			; Do not call ourselves
	MOVE	T1,RTE(TM)		; Get enter routine address
	PJRST	(T1)			; Dispatch to it and return


; Here when termnial type is not known

NOTKWN:	OUTSTR	[ASCIZ/
?SEDTCD Your terminal type cannot be determined.  SED execution aborted./]
	EXIT
	Subttl	Terminal Tables

; Parallel tables of terminal output specs and sixbit names

TERMNL::OADM2				; Output table address
	OADM3A				; ...
	OIQ120				; ...
	OV200				; ...
	OVT52				; ...
	OVT100				; ...

CRTTAB:	SIXBIT	~ADM2~			; Sixbit terminal name
	SIXBIT	~ADM3A~			; ...
	SIXBIT	~IQ120~			; ...
	SIXBIT	~V200~			; ...
	SIXBIT	~VT52~			; ...
	SIXBIT	~VT100~			; ...
NUMCRT==.-CRTTAB

; These are the legal terminal types which TOPS20 can return

IFE TOPS10,<			; Only if TOPS-20
T20NAM:	SIXBIT	~TTY~			; 0 - Model 33 TTY
	SIXBIT	~TTY~			; 1 - Model 35 TTY
	SIXBIT	~TTY~			; 2 - Model 37 TTY
	SIXBIT	~TTY~			; 3 - TI/EXECUPORT
	SIXBIT	~DM1521~		; 4 - Customer defined - Datamedia DM1521
	SIXBIT	~CUSTOM~		; 5 - Customer defined
	SIXBIT	~CUSTOM~		; 6 - Customer defined
	SIXBIT	~CUSTOM~		; 7 - Customer defined
	SIXBIT	~CRT~			; 10 - Default
	SIXBIT	~IDEAL~			; 11 - Ideal
	SIXBIT	~VT05~			; 12 - VT05B
	SIXBIT	~VT50~			; 13 - VT50
	SIXBIT	~TTY~			; 14 - LA30
	SIXBIT	~GT40~			; 15 - GT40
	SIXBIT	~TTY~			; 16 - LA36
	SIXBIT	~VT52~			; 17 - VT52
	SIXBIT	~VT100~			; 20 - VT100
	SIXBIT	~TTY~			; 21 - LA38
	SIXBIT	~TTY~			; 22 - LA120
LENT20==.-T20NAM			; Length of the table >
	Subttl	Character Tables -- ADM2

; Terminal output table for ADM2


OADM2:	BYTE (7) 13		;CURSOR UP
	BYTE (7) 12		; DOWN
	BYTE (7) 14		; RIGHT
	BYTE (7) 10		; LEFT
	BYTE (7) 36		; HOME
	BYTE (7) 33,"Y" 	;CLEAR PAGE
	BYTE (7) 33,"T" 	;CLEAR LINE
	BYTE (7) 12		;ROLL UP AND CLEAR LINE
	BYTE (7) 33,"E" 	;ROLL DOWN AND CLEAR LINE
	BYTE (7) 33,")"		;PROTECT ON
	BYTE (7) 33,"("		;PROTECT OFF
	BYTE (7) 33,"E" 	;INSERT A BLANK LINE
	BYTE (7) 33,"Q" 	;INSERT A SPACE
	BYTE (7) 33,"R" 	;DELETE LINE
	BYTE (7) 33,"W" 	;DELETE SPACE
	BYTE (7) 36,13		;MOVE TO BOTTOM
	BYTE (7) 36,33,"Y"	;HOME AND CLEAR PAGE
	COMPSC			;ROUTINE TO POSITION TO CHARACTER
	COMPSL			;ROUTINE TO POSITION TO START OF LINE
	^D24			;LINES PER PAGE
	^D80			;CHARACTERS PER LINE
	WRP!LSD    		;FLAGS (WRAPAROUND, LF=DOWN, NO ROLL DOWN)
	DECIDE			;ROUTINE TO CALL ON ENTRY
	CPOPJ##			;ROUTINE TO CALL ON EXIT
	0			;NO NULLS NEEDED
	IADM2			;ADDRESS OF TERMINAL INPUT TABLE

; Terminal input table for ADM2
		 0			;COMMAND FOR RUBOUT (RESET)
IADM2:		40
	-14,,IAADM2	;^A NEEDS A SUBTABLE
	EXP	       2, 3, 4, 5, 6, 7
	EXP	10,11,50,36,35,15,16,17 ;^K AND ^L ALTERED
	EXP	20,21,22,23,24,25,26,27
	EXP	30,31,32,33,34,35,37,43 ;36==HOME; 37==REAL-TAB

IAADM2:	IADF16,,"O"	;^A O CR == 16 == ENTER
	IADF01,,"@"	;^A @ CR ==  1 == INSERT-SPACES
	IADF02,,"A"	;^A A CR ==  2 == DELETE-SPACES
	IADF03,,"B"	;^A B CR ==  3 == SLIDE-LEFT
	IADF04,,"C"	;^A C CR ==  4 == SLIDE-RIGHT
	IADF13,,"L"	;^A L CR == 13 == ERASE-CHARACTER
	IADF14,,"M"	;^A M CR == 14 == INSERT MODE
	IADF15,,"N"	;^A N CR == 15 == RECALL
	IADF08,,"G"	;^A G CR ==  8 == REAL TAB
	IADF12,,"K"	;^A K CR == 12 == MARK
	IADF11,,"J"	;^A J CR == 11 == ERASE-LINE
	IADF10,,"I"	;^A I CR == 10 == WINDOW

; Terminal input table routines

IADF01:	"A"-100,,0
IADF02:	"S"-100,,0
IADF03:	"K"-100,,0
IADF04:	"L"-100,,0
IADF08:	43,,0
IADF12:	44,,0
IADF13:	42,,0
IADF14:	41,,0
IADF15:	40,,0
IADF16:	33,,0
IADF11:	50,,0
IADF10:	47,,0


	Subttl	Character Tables -- ADM3A

; Terminal output table for ADM3A


;TERMINAL OUTPUT TABLE - SEQUENCES TO MAKE THE TERMINAL DO SPECIAL THINGS
;FILL IN WITH THE CODES FOR YOUR TERMINAL

OADM3A:	BYTE (7) 13		;CURSOR UP
	BYTE (7) 12		; DOWN
	BYTE (7) 14		; RIGHT
	BYTE (7) 10		; LEFT
	BYTE (7) 36		; HOME
	0			;CLEAR TO END OF PAGE
	0			;CLEAR TO END OF LINE
	BYTE (7) 12		;ROLL UP AND CLEAR LINE
	0			;ROLL DOWN AND CLEAR LINE
	0			;PROTECT ON  (NONE)
	0			;PROTECT OFF (NONE)
	0			;INSERT LINE (NONE)
	0			;INSERT SPACE (NONE)
	0			;DELETE LINE (NONE)
	0			;DELETE SPACE (NONE)
	BYTE (7) 36,13         	;MOVE TO BOTTOM
	BYTE (7) 36,32		;HOME AND CLEAR ENTIRE PAGE
	COMPSC			;ROUTINE TO POSITION TO CHARACTER
	COMPSL			;ROUTINE TO POSITION TO START OF LINE
	^D24			;LINES PER PAGE
	^D80			;CHARACTERS PER LINE
        MRK!SLW!LSD		;TERM-DEPENDANT FLAGS (MARK,SLOW,LF=DOWN)
	CPOPJ##			;ROUTINE TO CALL ON ENTRY
	CPOPJ##			;ROUTINE TO CALL ON EXIT
        0,,0                    ;NUMBER OF NULLS TO OUTPUT,,NULL CHARACTER
	IADM3A			;ADDRESS OF INPUT CHARACTER TABLE
	"~"			;MARK TO INSERT ON ENTER

;TERMINAL INPUT TABLE - TELLS WHAT CONTROL CHARACTERS OR SEQUENCES
;INVOKE WHICH COMMANDS

                0                       ;CODE FOR RUBOUT (0==RESET)
IADM3A:	EXP	33, 1, 2, 3, 4, 5, 6, 7 ;NOTHING SPECIAL
	EXP	10,11,34,36,35,15,16,17
	EXP	20,21,22,23,24,25,26,27
	EXP	30,31,32
	-A3A.IS,,A3ASTB			;ESCAPE NEEDS A SUBTABLE (FOR EXAMPLE)
	EXP		 34,35,37,37

;SUBTABLE. PUT THE MOST FREQUENTLY-USED SEQUENCES FIRST
;ENTER IS PROBABLY THE MOST COMMON OF ALL

A3ASTB:	42,,"0"				; ESC 0 == DEL CHAR
	33,,"1"				; ESC 1 == ENTER
	53,,"2"				; ESC 2 == DOWN-TAB
	41,,"3"				; ESC 3 == INSERT MODE
	40,,"4"				; ESC 4 == RECALL
	43,,"5"				; ESC 5 == REAL TAB
	14,,"6"				; ESC 6 == SLIDE RIGHT
	13,,"7"				; ESC 7 == SLIDE LEFT
	52,,"8"				; ESC 8 == UP-TAB
	44,,"9"				; ESC 9 == MARK
	50,,"K" 			;ESCAPE K == ERASE-LINE
	50,,"k"
	47,,"W"				;ESCAPE W == WINDOW
	47,,"w"
	63,,"S"				;ESCAPE S == SUBSITUTE AND REPLACE
	47,,"s"
	57,,"B"				; ESC B == BEGINNING OF LINE
	57,,"b"
	60,,"E"				; ESC E == END OF LINE
	60,,"e"
	54,,"R"				; ESC R == REWRITE THE SCREEN
	54,,"r"
	46,,"C"				; ESC C == CHANGE THE CASE
	46,,"c"
	42,,"O"				; ESC O == ERASE-WORD
	42,,"o"
	61,,"V"				; ESC V == DELETE-CHARACTER (LEFT)
	61,,"v"
	56,,"H"				; ESC H == ONLINE HELP
	56,,"h"
	55,,"."				; ESC . == SAVE-FILE
	55,,"."
A3A.IS=.-A3ASTB


	Subttl	Character Tables -- IQ120

; Terminal output table for IQ120



;TERMINAL OUTPUT TABLE - SEQUENCES TO MAKE THE TERMINAL DO SPECIAL THINGS
;FILL IN WITH THE CODES FOR YOUR TERMINAL

OIQ120:	BYTE (7) 13		;CURSOR UP
	BYTE (7) 12		; DOWN
	BYTE (7) 14		; RIGHT
	BYTE (7) 10		; LEFT
	BYTE (7) 36     	; HOME
	BYTE (7) 33,"Y"		;CLEAR TO END OF PAGE
	BYTE (7) 33,"T"		;CLEAR TO END OF LINE
	BYTE (7) 12		;ROLL UP AND CLEAR LINE
	CPOPJ##           	;ROLL DOWN AND CLEAR LINE
	BYTE (7) 33,")"		;PROTECT ON (CPOPJ IF NONE)
	BYTE (7) 33,"("		;PROTECT OFF (CPOPJ IF NONE)
        0	                ;INSERT LINE (0 IF NONE)
        0		        ;INSERT SPACE (0 IF NONE)
        0	 	        ;DELETE LINE (0 IF NONE)
        0	    	        ;DELETE SPACE (0 IF NONE)
	BYTE (7) 36,13         	;MOVE TO BOTTOM
	BYTE (7) 36,33,"Y"	;HOME AND CLEAR ENTIRE PAGE
	COMPSC			;ROUTINE TO POSITION TO CHARACTER
	COMPSL			;ROUTINE TO POSITION TO START OF LINE
	^D24			;LINES PER PAGE
	^D80			;CHARACTERS PER LINE
	LSD!WRP!SLW    		;TERMINAL-DEPENDENT FLAGS
	CPOPJ##			;ROUTINE TO CALL ON ENTRY
	CPOPJ##			;ROUTINE TO CALL ON EXIT
	0    			;NUMBER OF NULLS TO OUTPUT,,NULL CHARACTER
	IIQ120			;ADDRESS OF INPUT CHARACTER TABLE
                        	;MARK TO DISPLAY ON ENTER (OMIT IF MRK NOT SET)

;TERMINAL INPUT TABLE - TELLS WHAT CONTROL CHARACTERS OR SEQUENCES
;INVOKE WHICH COMMANDS
REPEAT 0,<

             	0			;CODE FOR RUBOUT (0==RESET)
IIQ120:	EXP	40, 21, 2, 3, 4, 5, 6, 7 ;
	EXP	10,11,50,1,23,15,16,17	 ; ^K and ~L are escape seq
	EXP	20,0,22,0,24,25,26,27
	EXP	30,31,32
	-IQ1.IS,,IQ1STB			;ESCAPE NEEDS A SUBTABLE (FOR EXAMPLE)
	EXP		 34,35,37,37
>  ;End repeat
		 0			; Command for rubout (RESET)
IIQ120:	EXP	40, 1, 2, 3, 4, 5, 6, 7	;
	EXP	10,11,34,36,35,15,16,17 ; ^K and ^L are escape sequences
	EXP	20,21,22,23,24,25,26,27	;
	EXP	30,31,32		;
	-IQ1.IS,,IQ1STB			;ESCAPE NEEDS A SUBTABLE (FOR EXAMPLE)
	EXP		    34,35,37,37 ; 36 is HOME
;SUBTABLE. PUT THE MOST FREQUENTLY-USED SEQUENCES FIRST
;ENTER IS PROBABLY THE MOST COMMON OF ALL

IQ1STB:	42,,"0"				; ESC 0 == DEL CHAR
	33,,"1"				; ESC 1 == ENTER
	53,,"2"				; ESC 2 == DOWN-TAB
	41,,"3"				; ESC 3 == INSERT MODE
	40,,"4"				; ESC 4 == RECALL
	43,,"5"				; ESC 5 == REAL TAB
	14,,"6"				; ESC 6 == SLIDE RIGHT
	13,,"7"				; ESC 7 == SLIDE LEFT
	52,,"8"				; ESC 8 == UP-TAB
	44,,"9"				; ESC 9 == MARK
	50,,"j" 			;ESCAPE J == ERASE-LINE
	50,,"J"
	47,,"W"				;ESCAPE W == WINDOW
	47,,"w"
	63,,"S"				;ESCAPE S == SUBSITUTE AND REPLACE
	47,,"s"
	57,,"B"				; ESC B == BEGINNING OF LINE
	57,,"b"
	60,,"E"				; ESC E == END OF LINE
	60,,"e"
	54,,"R"				; ESC R == REWRITE THE SCREEN
	54,,"r"
	46,,"C"				; ESC C == CHANGE THE CASE
	46,,"c"
	42,,"O"				; ESC O == ERASE-WORD
	42,,"o"
	61,,"V"				; ESC V == DELETE-CHARACTER (LEFT)
	61,,"v"
	56,,"H"				; ESC H == ONLINE HELP
	56,,"h"
	55,,"."				; ESC . == SAVE-FILE
	55,,"."
IQ1.IS=.-IQ1STB




	Subttl	Character Tables -- Visual 200

; Terminal output table for Visual 200

OV200:	BYTE (7) 33,"A"			; Cursor up
	BYTE (7) 33,"B"			;  Down
	BYTE (7) 33,"C"			;  Right
	BYTE (7) 33,"D"			;  Left
	BYTE (7) 33,"H"			;  Home
	BYTE (7) 33,"y"			; Clear page
	BYTE (7) 33,"x"			; Clear line
	BYTE (7) 12			; Roll up and Clear line
	BYTE (7) 33,"I"			; Roll down and Clear line
	BYTE (7) 33,"4"			; Protect on
	BYTE (7) 33,"3"			; Protect off
	BYTE (7) 33,"L"			; Insert line
	V20INS				; Insert space
	BYTE (7) 33,"M"			; Delete line
	BYTE (7) 33,"O"			; Delete space
	BYTE (7) 33,"Y","7"," " 	; Move to bottom
	BYTE (7) 33,"v"			; Home and Clear page
	V20PSC				; Routine to position to character
	V20PSL				; Routine to position to start of line
	^D24				; Lines per page
	^D80				; Characters per line
	TBS!NPG!SLW			; Flags
	V20ENT				; Routine to call on entry
	V20XIT				; Routine to call on exit
	0				; No nulls needed
	IV200				; Address of input character table
; Terminal input tables for Visual 200


;THIS TABLE SETS UP THE KEYBOARD ARRANGEMENT FOR WHEN YOU NEED TO KEEP
;TTY PAGE IN EFFECT BECAUSE THE TERMINAL SENDS OUT ITS OWN CONTROL S AND Q'S
;THOSE TWO CHARACTERS CANNOT BE COMMANDS SINCE SED DOES NOT KNOW WHETHER THE
;USER HAS TYPED THEM OR THEY WERE SENT BY THE TERMINAL.

;SO Q GOES TO A, A AND S GO TO K AND L, K TO ESCAPE-L, AND L TO ESCAPE-T

                 0                      ;COMMAND FOR RUBOUT (RESET)
IV200:	EXP	40,21, 2, 3, 4, 5, 6, 7 ;NOTHING SPECIAL
	EXP	42,11,50, 1,23,15,16,17
	EXP	20, 0,22, 0,24,25,26,27
	EXP	30,31,32
	-IV2.IS,,I.V200   		;ESCAPE NEEDS A SUBTABLE
	EXP		 34,35,36,37

;SUBTABLE FOR ESC LETTER. PUT THE MOST FREQUENTLY-USED SEQUENCES FIRST

I.V200:	33,,"P" 	;ESCAPE P == ENTER
	37,,"H" 	;ESCAPE R == HOME
	36,,"A" 	;ESCAPE A == UP
	34,,"B" 	;ESCAPE B == DOWN
	35,,"C" 	;ESCAPE C == RIGHT
	10,,"D" 	;ESCAPE D == LEFT
	40,,"Q" 	;ESCAPE Q == RECALL
	IAV200,,"?"	;ESCAPE ? == CHECK SOME MORE
	13,,"L" 	;ESCAPE L == SLIDE-LEFT
	14,,"T" 	;ESCAPE T == SLIDE-RIGHT
	13,,"l" 	;ESCAPE L == SLIDE-LEFT
	14,,"t" 	;ESCAPE T == SLIDE-RIGHT
        63,,"S"         ;ESCAPE S == SUBSTITUE
        63,,"s"         ;ESCAPE S == SUBSTITUE
IV2.IS=.-I.V200

;SUBTABLE FOR ESC ?  LETTER. Most of these are in the numeric keypad.

IAV200: 41,,"M"		;ESCAPE ? M == INSERT-MODE
	55,,"m"		;ESCAPE ? m == SAVE FILE
	57,,"t"		;ESCAPE ? t == START-OF-LINE
	60,,"v"		;ESCAPE ? v == END-OF-LINE
	52,,"x"		;ESCAPE ? x == UP-TAB
	53,,"r"		;ESCAPE ? r == DOWN-TAB
	61,,"n"		;ESCAPE ? n == ERASE-WORD
	43,,"p"		;ESCAPE ? p == REAL-TAB
	44,,"w"		;ESCAPE ? w == MARK
	46,,"y"		;ESCAPE ? y == CASE
	56,,"s"		;ESCAPE ? s == HELP
	54,,"u"		;ESCAPE ? u == REWRITE SCREEN
	47,,"q"		;ESCAPE ? q == WINDOW

	57,,"T"		;ESCAPE ? T == START-OF-LINE
	60,,"V"		;ESCAPE ? V == END-OF-LINE
	52,,"X"		;ESCAPE ? X == UP-TAB
	53,,"R"		;ESCAPE ? R == DOWN-TAB
	61,,"N"		;ESCAPE ? N == ERASE-WORD
	43,,"P"		;ESCAPE ? P == REAL-TAB
	44,,"W"		;ESCAPE ? W == MARK
	46,,"Y"		;ESCAPE ? Y == CASE
	54,,"U"		;ESCAPE ? U == REWRITE SCREEN
	47,,"Q"		;ESCAPE ? Q == WINDOW
	33,,0

;ROUTINE CALLED ON ENTRY: SET UP KEYPAD IN APPLICATION MODE

V20ENT:	MOVE	T1,[BYTE (7) 33,"="]
	PUSHJ	P,PUTSQ1##
	JRST	PUTTYP##

;ROUTINE CALLED ON EXIT: RESTORE KEYPAD TO NON-APPLICATION MODE

V20XIT:	MOVE	T1,[BYTE (7) 33,">"]
	PUSHJ	P,PUTSQ1##
	JRST	PUTTYP##

;POSITIONING ALGORITHMS

V20PSC:	MOVE	T1,[BYTE (7) 33,"Y"]
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,40(RW)	;GET ROW POSITION
	IDPB	T1,TY
	MOVEI	T1,40(CM)	;GET COLUMN POSITION
	IDPB	T1,TY
	POPJ	P,		;DONE

V20PSL:	MOVE	T1,[BYTE (7) 33,"Y"]
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,40(T4)	;GET ROW POSITION
	IDPB	T1,TY
	MOVEI	T1,40		;MOVE TO COLUMN ZERO
	IDPTY
	POPJ	P,

; Routine to set insert char mode, output a space, and set up rtn
; through the reset char mode routine, V20RNS

V20INS:	MOVE	T1,[BYTE (7) 33,"i",40] ; Sequence to do the job
	PUSHJ	P,PUTSQ1##  		; Do it
;	POP	P,T1			; Save caller return
	PUSH	P,[V20RNS]		; ...
;	PUSH	P,T1			; And the caller return
	PJRST	PUTTYP##		; Done

; Routine to reset char insert mode

V20RNS:	MOVE	T1,[BYTE (7) 33,"j"]	; Sequence to turn off insert mode
	PUSHJ	P,PUTSQ1##  		; Do it now
	PJRST	PUTTYP##		; Done

	Subttl	Character Tables -- VT52

; Terminal output table for VT52


OVT52:	BYTE (7) 33,"A" 	;CURSOR UP
	BYTE (7) 33,"B" 	; DOWN
	BYTE (7) 33,"C" 	; RIGHT
	BYTE (7) 10		; LEFT
	BYTE (7) 33,"H" 	; HOME
	BYTE (7) 33,"J" 	;CLEAR PAGE
	BYTE (7) 33,"K" 	;CLEAR LINE
	BYTE (7) 12		;ROLL UP AND CLEAR LINE
	BYTE (7) 33,"I" 	;ROLL DOWN AND CLEAR LINE
	CPOPJ##			;PROTECT ON (NONE FOR A VT52)
	CPOPJ##			;PROTECT OFF
	0			;INSERT LINE (NONE)
	0			;INSERT SPACE (NONE)
	0			;DELETE LINE (NONE)
	0			;DELETE SPACE (NONE)
	BYTE (7) 33,"Y","7"," "	;MOVE TO BOTTOM
	BYTE (7) 33,"H",33,"J"	;HOME AND CLEAR PAGE
	VT5PSC			;ROUTINE TO POSITION TO CHARACTER
	VT5PSL			;ROUTINE TO POSITION TO START OF LINE
	^D24			;LINES PER PAGE
	^D80			;CHARACTERS PER LINE
	MRK!TBS!NPG!SLW		;FLAGS (NO PROTECT, HARD TABS, KEEP TTY PAGE)
	TENTRY 			;ROUTINE TO CALL ON ENTRY
	TEXIT			;ROUTINE TO CALL ON EXIT
	0			;NO NULLS NEEDED
	IVT52			;ADDRESS OF INPUT CHARACTER TABLE
	"~"			;MARK TO DISPLAY ON ENTER


;THIS TABLE SETS UP THE KEYBOARD ARRANGEMENT FOR WHEN YOU NEED TO KEEP
;TTY PAGE IN EFFECT BECAUSE THE TERMINAL SENDS OUT ITS OWN CONTROL S AND Q'S
;THOSE TWO CHARACTERS CANNOT BE COMMANDS SINCE SED DOES NOT KNOW WHETHER THE
;USER HAS TYPED THEM OR THEY WERE SENT BY THE TERMINAL.

;SO Q GOES TO A, A AND S GO TO K AND L, K TO ESCAPE-L, AND L TO ESCAPE-T

                 0                      ;COMMAND FOR RUBOUT (RESET)
IVT52:	EXP	40,21, 2, 3, 4, 5, 6, 7 ;NOTHING SPECIAL
	EXP	42,11,50, 1,23,15,16,17
	EXP	20, 0,22, 0,24,25,26,27
	EXP	30,31,32
	-SIZ.IS,,I.VT52			;ESCAPE NEEDS A SUBTABLE
	EXP		 34,35,36,37

;SUBTABLE FOR ESC LETTER. PUT THE MOST FREQUENTLY-USED SEQUENCES FIRST

I.VT52:	33,,"P" 	;ESCAPE P == ENTER
	37,,"R" 	;ESCAPE R == HOME
	36,,"A" 	;ESCAPE A == UP
	34,,"B" 	;ESCAPE B == DOWN
	35,,"C" 	;ESCAPE C == RIGHT
	10,,"D" 	;ESCAPE D == LEFT
	40,,"Q" 	;ESCAPE Q == RECALL
        63,,"S"         ;ESCAPE S == SUBSTITUE
        63,,"s"         ;ESCAPE S == SUBSTITUE
	IAVT52,,"?"	;ESCAPE ? == CHECK SOME MORE
	13,,"l" 	;ESCAPE L == SLIDE-LEFT
	14,,"t" 	;ESCAPE T == SLIDE-RIGHT
	13,,"L" 	;ESCAPE L == SLIDE-LEFT
	14,,"T" 	;ESCAPE T == SLIDE-RIGHT
SIZ.IS=.-I.VT52

;SUBTABLE FOR ESC ? LETTER. PUT THE MOST FREQUENTLY-USED SEQUENCES FIRST

IAVT52: 41,,"M"		;ESCAPE ? M == INSERT-MODE
	55,,"m"		;ESCAPE ? m == SAVE FILE
	57,,"t"		;ESCAPE ? t == START-OF-LINE
	60,,"v"		;ESCAPE ? v == END-OF-LINE
	52,,"x"		;ESCAPE ? x == UP-TAB
	53,,"r"		;ESCAPE ? r == DOWN-TAB
	61,,"n"		;ESCAPE ? n == ERASE-WORD
	43,,"p"		;ESCAPE ? p == REAL-TAB
	44,,"w"		;ESCAPE ? w == MARK
	46,,"y"		;ESCAPE ? y == CASE
	54,,"u"		;ESCAPE ? u == REWRITE SCREEN
	47,,"q"		;ESCAPE ? q == WINDOW
	56,,"s"		;ESCAPE ? s == HELP
	57,,"T"		;ESCAPE ? T == START-OF-LINE
	60,,"V"		;ESCAPE ? V == END-OF-LINE
	52,,"X"		;ESCAPE ? X == UP-TAB
	53,,"R"		;ESCAPE ? R == DOWN-TAB
	61,,"N"		;ESCAPE ? N == ERASE-WORD
	43,,"P"		;ESCAPE ? P == REAL-TAB
	44,,"W"		;ESCAPE ? W == MARK
	46,,"Y"		;ESCAPE ? Y == CASE
	54,,"U"		;ESCAPE ? U == REWRITE SCREEN
	47,,"Q"		;ESCAPE ? Q == WINDOW
	0

;ROUTINE CALLED ON ENTRY: SET UP KEYPAD IN APPLICATION MODE

TENTRY:	MOVE	T1,[BYTE (7) 33,"="]
	PUSHJ	P,PUTSQ1##
	JRST	PUTTYP##

;ROUTINE CALLED ON EXIT: RESTORE KEYPAD TO NON-APPLICATION MODE

TEXIT:	MOVE	T1,[BYTE (7) 33,">"]
	PUSHJ	P,PUTSQ1##
	JRST	PUTTYP##

;POSITIONING ALGORITHMS

VT5PSC:	MOVE	T1,[BYTE (7) 33,"Y"]
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,40(RW)	;GET ROW POSITION
	IDPB	T1,TY
	MOVEI	T1,40(CM)	;GET COLUMN POSITION
	IDPB	T1,TY
	POPJ	P,		;DONE

VT5PSL:	MOVE	T1,[BYTE (7) 33,"Y"]
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,40(T4)	;GET ROW POSITION
	IDPB	T1,TY
	MOVEI	T1,40		;MOVE TO COLUMN ZERO
	IDPB	T1,TY
	POPJ	P,

	SUBTTL  TERMINAL TYPE -- VT100

; Terminal output table for VT100

VT1AST==1	;SET THIS IF TERMINAL SENDS OUT XON-XOFF'S


;VT100 TERMINAL OUTPUT TABLE
;NOTE: THE WRP FLAG SHOULD NOT BE SET WHETHER OR NOT THE VT100 HAS WRAPAROUND

OVT100:	BYTE (7) 33,"[","A"	;CURSOR UP
	BYTE (7) 33,"[","B"	; DOWN
	BYTE (7) 33,"[","C"	; RIGHT
	BYTE (7) 33,"[","D"	; LEFT
	BYTE (7) 33,"[","H"	; HOME
	BYTE (7) 33,"[","J"	;CLEAR TO END OF PAGE
	BYTE (7) 33,"[","K"	;CLEAR TO END OF LINE
	BYTE (7) 33,"D"		;ROLL UP AND CLEAR LINE
	BYTE (7) 33,"M"		;ROLL DOWN AND CLEAR LINE
	BYTE (7) 33,"[","7","m"	;PROTECT ON
	BYTE (7) 33,"[","0","m"	;PROTECT OFF
	ILVT10			;INSERT LINE
	0			;INSERT SPACE (NONE)
	DLVT10			;DELETE LINE
	0			;DELETE SPACE (NONE)
	3777,,[BYTE (7) 33,"[","2","4",";","1","H"] ;MOVE TO BOTTOM
	HVT100			;HOME AND CLEAR PAGE
	POSCUR			;ROUTINE TO POSITION TO CHARACTER
	POSLIN			;ROUTINE TO POSITION TO START OF LINE
	^D24			;LINES PER PAGE
	^D80			;CHARACTERS PER LINE
IFE VT1AST,<
	SLW!TBS			;FLAGS (SLOW TTY,TTY WRAPS,HARDWARE TABS)
>
IFN VT1AST,<
	NPG!SLW!TBS		;FLAGS (KEEP TTY PAGE AND ONES ABOVE)
>
	VENTRY			;ROUTINE TO CALL ON ENTRY
	VEXIT			;ROUTINE TO CALL ON EXIT
	0,,0			;NO NULLS NEEDED
	IVT100			;ADDRESS OF INPUT CHARACTER TABLE

HVT100:	MOVEI	T1,[BYTE (7) 33,"[","H",33,"[" ;HOME AND CLEAR PAGE
		    BYTE (7) "J"]
	PUSHJ	P,PUTSTG##
	PUSHJ	P,PUTTYP##	;OUTPUT IT NOW
	MOVEI	T1,^D0100	;DELAY JUST A LITTLE
IFN TOPS10,<
	HIBER	T1,
	  JFCL
>
IFE TOPS10,<
	DISMS
>
	POPJ	P,		;THEN RETURN

;INSERT LINE (USES LIMITED SCROLLING REGION)

ILVT10:	PUSH	P,T2		;SAVE T2
	PUSHJ	P,LVT100	;SET UP THE SCROLLING REGION
	MOVSI	T1,(BYTE (7) 33,"[")
	PUSHJ	P,PUTSQ1##
	PUSHJ	P,GETROW	;GET THE CURRENT ROW
	PUSHJ	P,PUTNUM##	;AND OUTPUT DECIMAL IN ASCII
	MOVE	T1,[BYTE (7) ";","0","H",33,"M"]
	PUSHJ	P,PUTSQ1##	;AND MAKE SURE WE ARE THERE
	JRST	RVT100		;NOW RESTORE THE INITIAL STATE

;DELETE LINE (USES LIMITED SCROLLING REGION)

DLVT10:	PUSH	P,T2		;SAVE T2
	PUSHJ	P,LVT100	;SET UP THE SCROLLING REGION
	MOVEI	T1,[BYTE (7) 33,"[","2","4",";"
		    BYTE (7) "0","H",33,"D"]
	PUSHJ	P,PUTSTG##

RVT100:	MOVEI	T1,[BYTE (7) 33,"[","0",";","2"
		    BYTE (7) "4","r",33,"8"]
	PUSHJ	P,PUTSTG##
	POP	P,T2		;RESTORE T2
	JRST	PUTTYP##	;OUTPUT AND RETURN

;SET UP SCROLLING REGION FOR INSERT OR DELETE LINES

LVT100:	MOVE	T1,[BYTE (7) 33,"7",33,"["]
	PUSHJ	P,PUTSQ1##
	PUSHJ	P,GETROW	;GET THE CURRENT ROW
	PUSHJ	P,PUTNUM##	;OUTPUT DECIMAL NUMBER IN ASCII
	MOVE	T1,[BYTE (7) ";","2","4","r"]
	JRST	PUTSQ1##

GETROW:	MOVEI	T1,1(RW)	;GET THE CURRENT ROW
	ADD	T1,HOMPOS##	;ADD WINDOW OFFSET, IF ANY
	JUMPE	CM,CPOPJ##	;IS THE CURSOR AT THE START OF THE ROW?
	AOJA	T1,CPOPJ##	;NO - START ONE ROW FARTHER DOWN AND RETURN

;ROUTINE CALLED ON VT100 ENTRY: SET UP KEYPAD IN APPLICATION MODE

VENTRY:	MOVE	T1,[BYTE (7) 33,"=",33,"<"]
	PUSHJ	P,PUTSQ1##	;ASSUME VT52, SET VT100 (+ ALTERNATE KEYPAD)
	JRST	PUTTYP##

;ROUTINE CALLED ON VT100 EXIT: RESTORE KEYPAD TO NON-APPLICATION MODE

VEXIT:
;	MOVE	T1,[BYTE (7) 33,"[","?","2","l"]
;	PUSHJ	P,PUTSQ1##	;RESET BACK TO A VT52
	MOVSI	T1,(BYTE (7) 33,">")
	PUSHJ	P,PUTSQ1##	;GET OUT OF ALTERNATE KEYPAD MODE
	JRST	PUTTYP##

;TERMINAL INPUT TABLE - TELLS WHICH CHARACTER SEQUENCES
;INVOKE WHICH COMMANDS

IFE VT1AST,<
		0			;CODE FOR RUBOUT (0==RESET)
IVT100:	EXP	40, 1, 2, 3, 4, 5, 6, 7 ;NOTHING SPECIAL
	EXP	42,11,50,13,14,15,16,17
	EXP	20,21,22,23,24,25,26,27
	EXP	30,31,32
	-SIZ.I1,,VT1STB			;ESCAPE NEEDS A SUBTABLE
	EXP		 62,35,36,37
>
IFN VT1AST,<
;THIS TABLE SETS UP THE KEYBOARD ARRANGEMENT FOR WHEN YOU NEED TO KEEP
;TTY PAGE IN EFFECT BECAUSE THE TERMINAL SENDS OUT ITS OWN CONTROL S AND Q'S
;THOSE TWO CHARACTERS CANNOT BE COMMANDS SINCE SED DOES NOT KNOW WHETHER THE
;USER HAS TYPED THEM OR THEY WERE SENT BY THE TERMINAL.

;SO Q GOES TO A, A AND S GO TO K AND L, K TO ESCAPE-L, AND L TO ESCAPE-T

		 0			;COMMAND FOR RUBOUT (RESET)
IVT100:	EXP	40,21, 2, 3, 4, 5, 6, 7 ;NOTHING SPECIAL
	EXP	42,11,50, 1,23,15,16,17
	EXP	20, 0,22, 0,24,25,26,27
	EXP	30,31,32
	-SIZ.I1,,VT1STB			;ESCAPE NEEDS A SUBTABLE
	EXP		 62,35,36,37
>
;SUBTABLE. PUT THE MOST FREQUENTLY-USED SEQUENCES FIRST
;ENTER IS PROBABLY THE MOST COMMON OF ALL

VT1STB:	XVT100,,"["	;ESCAPE [ == READ NEXT CHAR
	SVT100,,"O"	;ESCAPE O == READ NEXT CHAR
IFN VT1AST,<
	13,,"L"		;ESCAPE L == SLIDE-LEFT
	14,,"T"		;ESCAPE T == SLIDE-RIGHT
	13,,"l"		;ESCAPE l == SLIDE-LEFT
	14,,"t"		;ESCAPE t == SLIDE-RIGHT
>
SIZ.I1==.-VT1STB

XVT100:	36,,"A"		;ESCAPE [ A == UP
	34,,"B"		;ESCAPE [ B == DOWN
	35,,"C"		;ESCAPE [ C == RIGHT
	10,,"D"		;ESCAPE [ D == LEFT
	0

SVT100:	33,,"P"		;ESCAPE O P == ENTER
	37,,"R"		;ESCAPE O R == HOME
	40,,"Q"		;ESCAPE O Q == RECALL
	41,,"M"		;ESCAPE O M == INSERT-MODE
	55,,"S"		;ESCAPE O S == SAVE-FILE

	36,,"A"		;ESCAPE O A == UP
	34,,"B"		;ESCAPE O B == DOWN
	35,,"C"		;ESCAPE O C == RIGHT
	10,,"D"		;ESCAPE O D == LEFT

	57,,"t"		;ESCAPE O t == START-OF-LINE
	60,,"v"		;ESCAPE O v == END-OF-LINE
	52,,"x"		;ESCAPE O x == UP-TAB
	53,,"r"		;ESCAPE O r == DOWN-TAB
	61,,"n"		;ESCAPE O n == ERASE-WORD
	43,,"p"		;ESCAPE O p == REAL-TAB
	44,,"w"		;ESCAPE O w == MARK
	46,,"y"		;ESCAPE O y == CASE
	54,,"u"		;ESCAPE O u == REWRITE SCREEN
	47,,"q"		;ESCAPE O q == WINDOW
IFN VT1AST,<
	13,,"m"		;ESCAPE O m == SLIDE-LEFT
	14,,"l"		;ESCAPE O l == SLIDE-RIGHT
>
	56,,"s"		;ESCAPE O s == HELP
	0

;MOVE TO ROW (RW), COLUMN (CM)

POSCUR:	MOVSI	T1,(BYTE (7) 33,"[")
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,1(RW)	;GET ROW POSITION
	PUSHJ	P,PUTNUM##
	MOVEI	T1,";"		;THEN A SEMICOLON
	IDPB	T1,TY
	MOVEI	T1,1(CM)	;GET COLUMN POSITION
	PUSHJ	P,PUTNUM##
	MOVEI	T1,"H"		;THEN AN H
	IDPB	T1,TY
	POPJ	P,		;DONE

;MOVE TO START OF LINE (T4)

POSLIN:	MOVSI	T1,(BYTE (7) 33,"[")
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,1(T4)	;GET ROW POSITION
	PUSHJ	P,PUTNUM##
	MOVE	T1,[ASCII /;0H/]
	JRST	PUTSQ1##		;MOVE TO FIRST COLUMN; DONE


	Subttl	Service Routines -- ADM2, ADM3A, and IQ120


;POSITIONING ALGORITHMS   COMMON FOR ADM2, ADM3A, IQ120

COMPSC:	MOVE	T1,[BYTE (7) 33,"="]
   	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,40(RW)	;GET ROW POSITION
	IDPB	T1,TY
	MOVEI	T1,40(CM)	;GET COLUMN POSITION
	IDPB	T1,TY
	POPJ	P,		;DONE

COMPSL:	MOVE	T1,[BYTE (7) 33,"="]
	PUSHJ	P,PUTSQ1##	;START IT OFF
	MOVEI	T1,40(T4)	;GET ROW POSITION
	IDPB	T1,TY
	MOVEI	T1,40		;MOVE TO COLUMN ZERO
	IDPB	T1,TY
	POPJ	P,

	SUBTTL	IDPY -- Read the Default Display Type

;    This routine is to be called on program initialization  in
; order  to  read SYS:DISPLA.INI[1,4] for the terminal's default
; display type.  This display type is converted to an  index  to
; be  used  by  the  cursor  address  functions.
; [6] Remove all the DISPLA.INI stuff.
; [6] Make this version of SED require 7.00 or later. Get the
; [6] terminal type with a TRMOP. If that fails, we can't run
;
IDPY::
IFN TOPS10,<		; If TOPS-10:
	MOVE	T3,[2,,T1]		; Get the
	MOVX	T1,.TOTRM		; terminal
	SETO	T2,			; type from
	TRMOP.	T3,			; the monitor
	  POPJ  P,     			; None, non-skip return is error >

	MOVE	T1,[-NUMCRT,,CRTTAB]	; Check for a
	PUSHJ	P,TSTABR		; known type
	  POPJ  P,    			; Unknown, give error return
 	JRST	SETVID			; Found it, go use it

; Here when we found out about this TTY, set the type

SETVID:	TRC	T1,-1			; Complement right half
	TRCN	T1,-1			; Skip if not -1
	PJRST	.POPJ			; Here if not a match
	PJRST	.POPJ1			; Here if a match
	JRST	.POPJ1			; Yes, skip return
	SUBTTL	IDPY -- TSTABR - Test for Abbreviated Terminal Type

; Here to test for an abbreviated name

TSTABR:	SETO	T2,			; Reset the counter

TSTA01:	LSH	T2,-6			; Shift the mask
	TDNE	T3,T2			; Skip if out of characters
	JRST	TSTA01			; Loop until finished
	SETZ	T4,			; Clear the flag
	HRRZ	M2,T1			; Get the base addr

TSTA02:	MOVE	M3,(T1)			; Get a word to test
	XOR	M3,T3			; Try to clear some chars
	JUMPE	M3,TSTA04		; Jump if a match
	ANDCM	M3,T2			; Clear any extra chars
	JUMPN	M3,TSTA03		; Jump if not valid abbr
	TRON	T4,1			; Skip if not the first time
	TLOA	T4,(T1)			; Set the flag
	TLZ	T4,-1			; Reset the address

TSTA03:	AOBJN	T1,TSTA02		; Loop for whole table
	TLNN	T4,-1			; Any found?
	POPJ	P,			; No, give error
	HLRZ	T1,T4			; Yes, get the pointer

TSTA04:	SUB	T1,M2			; Make an index
	JRST	.POPJ1			; Skip return




; Here to give the general skip and non-skip returns

.POPJ1:	AOS	(P)			; Increment return address
 .POPJ:	POPJ	P,			; Return
	Subttl	The End

	END				; That's all folks
     ' ô