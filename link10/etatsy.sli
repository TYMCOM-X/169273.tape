	TITLE ETATSY
	SEARCH	UUOTYM(UUOTYM.UNV[26007,244321])

	LOC	.JBREN
	EXP	ETATS1		;REENTER ADRESSE
	RELOC
;AC'S
	T1=1
	T2=2
	T3=3
	T4=4
	P1=5
	P2=6
	P3=7
	P4=10
	P5=11
	N=12
	R=13
	P=17
;PILE
LPILE==40
PILE: BLOCK	LPILE
MAXJOB==^D100
MAXUNI==^D32
MAXACT==^D10		;MAXIMUN DE JOBS ACTIFS
MAXSWA==^D250		;MAXIMUN DE SWAPPING 
			;AVANT D'AVOIR UNE MACHINE CHARGEE
NBMXCH==^D10		;ON SONNE AU BOUT DE 10 FOIS CONSECUTIVES

;VALEURS DES BITS
;JBTSTS
%RUN==400000		;RUN
%CMWB==200000		;A DEMANDER DES CORES
%MRQ==100000		;IL MANQUE DES PAGES
%JNA==40000		;UTILISE
%JERR==20000		;ERREUR
%JRQ=2			;A REMETTRE EN QUEUE

;VALEURS DES BITS DE L'ETAT DU SYSTEME
STSHUT==400000		;SHUT
STSUPR==200000		;SUPER SHUT
STRLB==100000		;LOGIN SPECIAL

;SYSTEME D'INTERRUPTION
ADTRAP:	Z
	JRST	INTCHR	;INTERRUPTION SUR TOUS LES CARACTERES
MT1:	Z		;SAUVEGARDE DE T1
;MACROS
	DEFINE SYMSYS,<
	X	JOBN
	X	STSTBL
	X	SSCAN
	X	JBTQ
	X	SYSDRB
	X	DRBFNB
	X	DRBLNK
	X	FNBATB
	X	FNBLNK
	X	FNBDRB
	X	ATBDOR
	X	ATBUMC
	X	ATBSPT
	X	ATBLNK
	X	SPTLNK
	X	SPTNME
	X	SYSUNI
	X	UNISYS
	X	UNINAM
	X	UNIHCT
	X	UNIMCT
	X	UNITAL
	X	UNIICT
	X	UNIOCT
	X	NCTXPG
	X	SIPTOT
	X	MEMSIZ
	X	SYSSIZ
	X	STATES
>

	DEFINE	INCSYS,<
	X	JFYSEC
	X	UPTIME
	X	LSTWRD
	X	NULTIM
	X	PFLCNT
	X	SIJCNT
	X	OKPCNT
	X	SIFCNT
	X	EATCNT
	X	TSAVAL
	X	WSAVAL
	X	PARTOT
>

	DEFINE SYMJBT,<
	X	STS
	X	PGO
	X	MWS
	X	UPM
	X	UNM
	X	UN1
	X	NAM
>

	DEFINE	INCJBT,<
	X	RCT
	X	WCT
	X	MPC
	X	CIN
	X	COT
	X	CMP
	X	CP2
>

;GENERATION DES TABLEAUX
	DEFINE X(A),<SIXBIT	/'A/>
NAMSYM:
	SYMSYS
	INCSYS
	DEFINE	X(A),<SIXBIT	/JBT'A/>
	SYMJBT
	INCJBT
	DEFINE	X(A),<'A:0
>
VALSYM:
	SYMSYS
	INCSYS
	DEFINE	X(A),<JBT'A:0
>
	SYMJBT
	INCJBT
NBSYM==.-VALSYM

;POINTEUR
PTJMWS:	POINT	10,MJBMWS(P5),15	;MWS
PTJUWS:	POINT	10,MJBMWS(P5),35	;UWS
PTJPGO:	POINT	10,MJBPGO(P5),11	;PAGE ACTIVE
PT1WCD:	POINT	5,P1,14		;WAIT CODE
PTRETA:	POINT	14,MJBETA(P5),13	;RANGEMENT DU CODE DE L'ETAT(ASCII)
PTCODE:	POINT	6,T1,5	;2 PREMIERS CARACTERES DE STSTBL(LE 1ER)
	POINT	6,T1,17	;LES 2 SUIVANTS(LE 1ER)
	POINT	6,T1,29	;LES 2 DERNIERS(LE 1ER)
PTCOD1:	POINT	6,T1,11	;LES 2 PREMIERS(LE DERNIER)
	POINT	6,T1,23	;LES 2 SUIVANTS(LE DERNIER)
	POINT	6,T1,35	;LES 2 DERNIERS(LE DERNIER)
PTUNSA:	POINT	12,T1,11	;NB DE SATS FAILURES
PTUNRB:	POINT	12,T1,23	;NB DE RIBS ERRORS
PTUNCK:	POINT	12,T1,35	;NB DE CHECKSUM ERRORS

;TABLE POUR LES TOTAUX PAR QUEUE
CVTQUE:	1	;RUN
	14	;WS
	14	;TS
	14	;SS
	14	;SI
	14	;RI
	14	;PS
	14	;PR
	14	;M2
	14	;DA
	14	;CB
	14	;FC
	0	;MT
	14	;AX
	14	;AC
	11	;BP
	12	;IOW
	12	;MBW
	14	;CW
	11	;TIOW
	10	;SW
	13	;ML
	13	;IL
	13	;FL
	0	;SLP
	0	;NULL
	0	;STOP

;LISTE DES MOIS
MOIS:	ASCIZ	/JAN/
	ASCIZ	/FEV/
	ASCIZ	/MAR/
	ASCIZ	/AVR/
	ASCIZ	/MAI/
	ASCIZ	/JUN/
	ASCIZ	/JUL/
	ASCIZ	/AOU/
	ASCIZ	/SEP/
	ASCIZ	/OCT/
	ASCIZ	/NOV/
	ASCIZ	/DEC/

;NOMBRE DE JIFFIES PAR SECONDE
NBJIFF:	EXP	0		;

;TIMER
TIMER:	EXP	^D60	;1 MINUTE PAR DEFAUT

;FLAG
FLCPU:	0		;A -1 QUAND IL FAUT DEMANDER LE MIN DE MICROC
FLTIM:	0		;A -1 QUAND IL FAUT DEMANDER UN NOUVEAU TIMER
FLCLR:	0		;A -1 QUAND IL FAUT NETTOYER L'ECRAN
FLSTP:	0		;A -1 QUAND ARRET
FLHLP:	0		;A -1 QUAND IL FAUT EDITER HELP
FLACT:	-1		;A -1 QUAND IL FAUT EDITER LES JOBS ACTIFS
FLRUN:	-1		;A -1 QUAND IL FAUT EDITER LES JOBS QUI ON FAIT UN MINIMUN DE CPU
FLTOT:	0		;A -1 UN QUAND IL FAUT EDITER TOUS LES JOBS
FLIMP:	0		;A -1 QUAND IL FAUT EDITER TIMER ET MINCPU
FLBELL:	0		;A -1 QUAND LA SONETTE A DEJA ETE FAITE
MERROR:	0		;NOMBRE D'ERREURS DISQUES PLUS PARITE MEMOIRE
NBCHAR:	0		;NOMBRE DE CHARGES MACHINE CONSECUTIVES

OKJOB:	0		;JOB A EDITER
MINCPU:	^D50		;MINIMUN DE CPU

;VALEURS POUR VT100
AXCRD:	1
AXRBS:	1
AXWID:	^D160
AXFC1:	0
AXFC2:	0
AXRVX:	1
AXRMX:	1
AXBS:	1
AXBBP:	1
;DEFINITION DE L'IMPRESSION
	DEFINE DESCRIP,<
	Y(USE,INED,3,3,21,2,)	;USERS
	Y(ACT,INED,3,3,21,8,)	;RUN
	Y(Q0,INED,2,2,21,12,)	;Q0
	Y(Q1,INED,2,2,21,15,)	;Q1
	Y(Q2,INED,2,2,21,18,)	;Q2
	Y(Q3,INED,2,2,21,21,)	;Q3
	Y(MRQ,INED,3,3,21,24,)	;MRQ
	Y(MPQ,INED,4,4,21,28,)	;NOMBRE DE PAGES
	Y(SWP,IOPT,3,3,21,35,)	;SWAPPE
	Y(TIO,IOPT,3,3,21,40,)	;TTY IO
	Y(IO,IOPT,3,3,21,45,)	;IO
	Y(LQ,IOPT,3,3,21,50,)	;LOCKED
	Y(SPC,IOPT,3,3,21,55,)	;AUTRES
	Y(IDL,INED,3,2,21,63,%)	;IDLE
	Y(LOS,INED,3,2,21,70,%)	;LOST
	Y(MC,INED,7,7,21,76,)	;MICROCYCLE PAR SECOND
	Y(SWA,INED,3,3,21,87,)	;SWAPPING
	Y(SIN,INED,3,3,21,91,)	;IN
	Y(SOU,INED,3,3,21,95,)	;OUT
	Y(DKF,INED,7,7,21,108,)	;DISK FREE
	Y(MON,INED,5,4,21,120,p)	;MONITOR AREA
	Y(USA,INED,5,4,21,127,p)	;USERS AREA
	Y(FIL,INED,3,3,23,3,)	;FILES
	Y(MAP,INED,5,4,23,10,p)	;MAPPED
	Y(SHR,INED,5,4,23,19,p)	;SHARED
	Y(SIP,INED,5,4,23,30,p)	;SIPTOT
	Y(PFL,IOPT,7,4,23,38,p/s)	;PFLCNT
	Y(OKP,IOPT,7,4,23,48,p/s)	;OKPCNT
	Y(SIF,IOPT,7,4,23,58,p/s)	;SIFCNT
	Y(SIJ,IOPT,6,3,23,69,j/s)	;SIJCNT
	Y(EAT,IOPT,6,3,23,78,j/s)	;EATCNT
	Y(TSA,IOPT,6,3,23,89,j/s)	;TSAVAL
	Y(WSA,IOPT,6,3,23,98,j/s)	;WSAVAL
	Y(GWS,INED,3,2,23,107,%)	;% DU GAIN SUR LE WS DES JOBS "ACTIFS"
	Y(TMW,INED,6,5,23,112,p)	;TOTAL MONITEUR WS DES JOBS "ACTIFS"
>


	DEFINE	Y(A,B,C,D,E,F,H),<	EXP	DES'A
>
DESVAL:	DESCRIP


	DEFINE	Y(NOM,SUBR,LGTOT,LGVAL,LIG,COL,UNITES),<
DES'NOM:	EXP SUBR
	LGTOT,,LGVAL
ZZ=0
IRP UNITES,<ZZ=ZZ+1>
IFE ZZ,<EXP 0>
IFN ZZ,<ASCIZ	!'UNITES!>
	ASCIZ	!['LIG;'COL'H!
>
	DESCRIP

	DEFINE EDJB(NOM,LGTOT,DEPLA),<
DES'NOM:	EXP	'LGTOT
	EXP	'DEPLA
>
	EDJB(JOB,3,0)	;NUMERO DU JOB
	EDJB(SIZ,3,2)	;TAILLE
	EDJB(MWS,3,1)	;MWS
	EDJB(ACJ,3,1)	;TAILLE ACTIVE
	EDJB(MCJ,6,2)	;MICROCYCLE
	EDJB(DIO,5,2)	;DISQUE IO
	EDJB(JIO,5,2)	;CHARACTERE IO

;VALEURS DE L'INDICE DE LA DERNIERE VALEUR DE LA LIGNE
TBSTLG:	EXP	^D21		;USERS AREA
;ZONE DE TRAVAIL
ZONNUL:
;INCREMENTAL SYSTEME DIFFERENCE LUE
	DEFINE	X(A),<D'A: 0
>
	INCSYS

;LISTES DES JOBS A IMPRIMER
NBLIGN:	Z		;NOMBRE DE LIGNES IMPRIMEES
NBJOB:	Z		;NOMBRE DE JOB A IMPRIMER
JOBGAU:	Z		;JOB DE LA COLONNE DE GAUCHE
JOBDRO:	Z		;JOB DE LA COLONNE DE DROITE
LISJOB:	BLOCK	MAXJOB

	DEFINE	X(A),<MJB'A:	BLOCK	MAXJOB
>
	SYMJBT

MJBETA:	BLOCK	MAXJOB
MJBMCY:	BLOCK	MAXJOB	;NOMBRE MICROCYCLE PAR SECONDE

;TOTAUX DU SWAPPING
TOTICT:	Z		;IN
TOTOCT:	Z		;OUT

;ERREURS DISQUES
ERRSAT:	BLOCK	MAXUNI	;SAT FAILURES
ERRRIB:	BLOCK	MAXUNI	;RIB ERREUR
ERRDEV:	BLOCK	MAXUNI	;HARD DEVICE ERROR
NBUNI:	Z		;NOMBRE D'UNITES EN SERVICE

	DEFINE	Y(A,B,C,D,E,F),<
NEW'A:	Z
>
VALNEW:	DESCRIPT
LGVAL==.-VALNEW
DATNEW:	Z	;
NEWTUW:	Z	;TOTAL USER WORKING SET ACTIF
TIMNEW:	Z
FINNEW==.-1


;FIN DE LA ZONE NULLE A CHAQUE BOUCLE


;ZONE MEMOIRE DES VALEURS LUES LA FOIS D'AVANT
	INCJBT
	DEFINE	X(A),<MMJ'A:	BLOCK	MAXJOB
>
	INCJBT

;INCREMENTAL SYSTEME
	DEFINE	X(A),<M'A:0
>
	INCSYS

;TOTAUX DU SWAPPING
MEMICT:	Z		;IN
MEMOCT:	Z		;OUT


;MEMOIRE ERREUR DISQUE
MUNNAM:	BLOCK	MAXUNI	;NOM DE L'UNITES
MERCHK:	BLOCK	MAXUNI	;CHECKSUM ERRORS
MERDAT:	BLOCK	MAXUNI	;HARD DATA ERRORS
FINERR=.-1
ERRCHK:	BLOCK	MAXUNI	;CHECKSUM ERROR
ERRDAT:	BLOCK	MAXUNI	;HARD DATA ERRORS
FNERR=.-1
FINNUL=.-1

;VALEURS IMPRIMEES PRECEDENMENT
VALANC:	BLOCK	LGVAL
;DATE DU JOUR
DATANC:	Z		;
STSANC: Z		;STATUS DE LA MACHINE
FINANC==.-1
;VALEURS CONSTANTES
SIZSYS:	Z		;TAILLE DU MONITOR
SIZUSE:	Z		;TAILLE USERS
;DESCRIPTION DU CADRE FIXE

;LIGNE 2
;		!12345678901234567890!
LIFX02:	ASCII	!job  username     pr!	;COLONNE 1 A 20
	ASCII	!og   size mws act  m!	;COLONNE 21 A 40
	ASCII	!cyc/s  dio/s  tio/s !	;COLONNE 41 A 60
	ASCII	!etat    job  usernam!	;COLONNE 61 A 80
	ASCII	!e     prog   size mw!	;COLONNE 81 A 100
	ASCII	!s act  mcyc/s  dio/s!	;COLONNE 101 A 120
	ASCIZ	!  tio/s etat!		;COLONNE 121 A 132
;		!12345678901234567890!

;LIGNE 20
;		!12345678901234567890!
LIFX20:	ASCII	!users  act q0 q1 q2 !	;COLONNE 1 A 20
	ASCII	!q3 mrq(page)  swp  t!	;COLONNE 21 A 40
	ASCII	!io   io   lq  spc   !	;COLONNE 41 A 60
	ASCII	! idle   lost  mcycle!	;COLONNE 61 A 80
	ASCII	!/s   swap( in+out)bl!	;COLONNE 81 A 100
	ASCII	!/s   disk free   mon!	;COLONNE 101 A 120
	ASCIZ	!itor  users !		;COLONNE 120 A 132
;		!12345678901234567890!


;LIGNE 21
;		!12345678901234567890!
LIFX21:	ASCII	!                    !	;COLONNE 1 A 20
	ASCII	!      (    )        !	;COLONNE 21 A 40
	ASCII	!                    !	;COLONNE 41 A 60
	ASCII	!                    !	;COLONNE 61 A 80
	ASCII	!         (   +   )  !	;COLONNE 81 A 100
	ASCII	!                    !	;COLONNE 101 A 120
	ASCII	!            !		;COLONNE 121 A 132
;		!12345678901234567890!

;LIGNE 22
;		!12345678901234567890!
LIFX22:	ASCII	!files   mapped   sha!	;COLONNE 1 A 20
	ASCII	!red     siptot    pf!	;COLONNE 21 A 40
	ASCII	!lcnt    okpcnt     s!	;COLONNE 41 A 60
	ASCII	!ifcnt   sijcnt   eat!	;COLONNE 61 A 80
	ASCII	!cnt     tsaval   wsa!	;COLONNE 81 A 100
	ASCII	!val   gws     mws   !	;COLONNE 101 A 120
	ASCIZ	!            !		;COLONNE 121 A 132
;		!12345678901234567890!

;LECTURE D'UN SYMBOLE RETOUR T1/VALEUR DU SYMBOLE
	DEFINE	LECSYM(A),<
	MOVE	T1,'A
	HRLI	T1,400000
	VPEEK	T1,
	PUSHJ	P,ERRVPK
>


;LECTURE D'UN SYMBOLE INDICE PAR T2 RETOUR VALEUR DANS T1
	DEFINE	LSYMT2(A),<
	MOVE	T1,'A
	ADD	T1,T2
	HRLI	T1,400000
	VPEEK	T1,
	PUSHJ	P,ERRVPK
>
;DEBUT DU PROGRAMME
ETATSY:	RESET
	MOVE	P,[IOWD LPILE,PILE]
	MOVEI	T1,[LC.RC]	;LICENCE RC SETTABLE
	CHKLIC	T1,		;
	JUMPE	T1,ERRLIC	;NON
	HRROI	T1,.GTLIC	;VOIR SI SYS
	GETTAB	T1,		;
	SETZ	T1,		;ERREUR
	TDNN	T1,[LC.SY]	;
	JRST	ERRLI1		;
	MOVE	T1,['ETA',,NBSYM]	;LECTURE DE LA VALEUR
	MOVE	T2,[NAMSYM,,VALSYM]	;DES SYMBOLES DU MONITEUR
	PUSHJ	P,TAKSYM##
	MOVE	T1,[-NBSYM,,0]	;TEST SI ERREUR
	SKIPGE	VALSYM(T1)
	JRST	ERRSYM		;ERREUR
	AOBJN	T1,.-2
	MOVE	T1,[ZONNUL,,ZONNUL+1]
	SETZM	ZONNUL
	BLT	T1,FINNUL
	LECSYM(JFYSEC)		;NOMBRE DE JIFFIES PAR SECONDE
	MOVEM	T1,NBJIFF	;
	LECSYM(SYSSIZ)		;TAILLE DU MONITOR
	LSH	T1,-9		;DIVISION PAR 512
	MOVEM	T1,SIZSYS	;
	LECSYM(MEMSIZ)		;TAILLE MEMOIRE
	LSH	T1,-9		;DIVISION 512
	SUB	T1,SIZSYS	;
	MOVEM	T1,SIZUSE	;

;INITIALISATION DE LA VT100
ETATS1:	SETLCH	[LC.NFC+LC.NCM]	;NO CRLF
	MOVE	T1,[-1,,.AXCRD]	;DELAY AFTER CARRIAGE RETURN.(COMPUTE..)
	AUXCAL	T1,AXCRD		;
	MOVE	T1,[-1,,.AXRBS]	;REMOTE BACKSPACE ECHO
	AUXCAL	T1,AXCRD	
	MOVE	T1,[-1,,.AXWID]	;LINE WIDTH 1
	AUXCAL	1,AXWID
	MOVE	T1,[-1,,.AXFC1]	;VT,FF DELAY 0
	AUXCAL	T1,AXFC1	
	MOVE	T1,[-1,,.AXFC2]	;HT DELAY 0
	AUXCAL	1,AXFC2
	MOVE	T1,[-1,,.AXRVX]	;RESERVE XON
	AUXCAL	1,AXRVX	
	MOVE	T1,[-1,,.AXRMX]	;REMOTE XON
	AUXCAL	T1,AXRMX
	MOVE	T1,[-1,,.AXBS]	;BACK SPACE FOR CARACTERE DELETE
	AUXCAL	T1,AXBS
	MOVE	T1,[-1,,.AXBBP]	;BACK SPACE AS BREAK ON PUNCTUATION
	AUXCAL	T1,AXBBP
	MOVE	T1,[IO.NEE+IO.NEC]	;PAS D'ECHO
	SETMOD	T1,


;MISE EN PLACE DU SYSTEME D'INTERRUPTION
	MOVE	T1,[IA.REE,,ADTRAP]
	INTADR	T1,		;OUVERTURE DU SYSTEME
	JRST	ERRINT		;
	MOVEI	T2,.IACHR	;
	LSH	T2,9		;
	ADDI	T2,1		;CANAL 1
	SETO	T1,		;
	HRL	T1,T2		;
	TINASS	T1,		;
	JRST	ERRINT		;
	MOVE	T1,[1B1]	;NETTOYER LES INTERRUPTIONS EN ATTENTE
	INTACT	T1,		;
	JRST	ERRINT		;
	MOVE	T1,[3B1]	;PERMETTRE LE INTERRUPTIONS
	INTENB	T1,		;
	JRST	ERRINT		;
	SETZM	FLSTP
;BOUCLE POUR REFRESH DE L'ECRAN
;MISE A ZERO ET PASSAGE A 132 COLONNES ET IMPRESSION DU CADRE FIXE

BOUCLE:	SETZM	FLCLR			;
	SETZM	FLHLP			;
	SETZM	FLBELL			;SONETTE EN SERVICE
	OUTSTR	[ASCIZ	/[?3h/]	;PASSAGE EN 132 COLONNES
	OUTSTR	[ASCIZ /[?7l/]		;SUPPRESSION DU SAUT LIGNE AUTOMATIQUE
	OUTSTR	[ASCIZ /[3;19r/]	;SCROLLING REGION
	MOVE	T1,[VALANC,,VALANC+1]	;MISE A -1
	SETOM	VALANC			;
	BLT	T1,FINANC		;DE VALANC
	MOVE	T1,[ERRCHK,,MERCHK]	;POUR LES PETITS ERREURS DISQUES
	BLT	T1,FINERR		;
	MOVE	T1,[ERRCHK,,ERRCHK+1]	;
	SETZM	ERRCHK			;
	BLT	T1,FNERR		;
	OUTSTR	[ASCIZ /[2J/]		;EFFACEMENT DE L'ECRAN
;EDITION DE LA LIGNE 1
	OUTSTR	[ASCIZ /#6[1;0H/]	;DOUBLE LARGEUR ET LIGNE 1
	OUTSTR	[ASCIZ /SLIGOS /]	;
	HRRI	T1,.GTCNF		;LECTURE DE LA CONFIG
	HRLI	T1,.CNFG0		;
	MOVEI	T2,3			;5 MOTS
	SETZ	T4,			;POUR ARRETER L'IMPRESSION
BOUCL1:	MOVE	T3,T1			;
	GETTAB	T3,			;
	JRST	ERRGET			;
	OUTSTR	T3			;IMPRESSION
	ADD	T1,[1000000]		;
	SOJG	T2,BOUCL1		;
	OUTSTR	[ASCIZ / /]
	HRLI	T1,.CNDT0		;DATE DU MONITEUR
	MOVE	T3,T1			;
	GETTAB	T3,			;
	JRST	ERRGET			;
	OUTSTR	T3			;
	ADD	T1,[1000000]		;
	MOVE	T3,T1			;
	GETTAB	T3,			;
	JRST	ERRGET			;
	OUTSTR	T3			;
	OUTSTR	[ASCIZ /[1;33HUP/]
	OUTSTR	[ASCIZ /[1;57HA/]
	OUTSTR	[ASCIZ /[2;0H#5/]	;SIMPLE CARACTERE ET LIGNE 2
	OUTSTR	LIFX02			;LIGNE 2
	OUTSTR	[ASCIZ /[20;0H/]
	OUTSTR	LIFX20			;LIGNE 20
	OUTSTR	[ASCIZ	/
/]					;NEW LIGNE
	OUTSTR	LIFX21			;LIGNE 21
	OUTSTR	[ASCIZ	/
/]					;NEW LIGNE
	OUTSTR	LIFX22			;LIGNE 22
	OUTSTR	[ASCIZ	/
/]					;NEW LIGNE
;FIN DES IMPRESSIONS FIXES
;BOUCLE SUR LA LECTURE CALCUL IMPRESSION
ENCORE:	MOVE	T1,[ZONNUL,,ZONNUL+1]
	SETZM	ZONNUL
	BLT	T1,FINNEW
	MOVE	T1,SIZSYS		;TAILLE DU MONITEUR
	MOVEM	T1,NEWMON		;
	MOVE	T1,SIZUSE		;TAILLE USERS
	MOVEM	T1,NEWUSA		;
	LECSYM(SIPTOT)
	MOVEM	T1,NEWSIP	;VALEUR NON INCREMENTALE
;LECTURE DE LA DATE ET DE L'HEURE
	DATE	T1,			;
	MOVEM	T1,DATNEW		;
	MSTIME	T1,			;
	MOVEM	T1,TIMNEW		;
;LECTURE DES VALEURS DU SYSTEME
	PUSHJ	P,LECSYS
;LECTURE DES PARAMETRES DES JOBS
	PUSHJ	P,LECJOB	;
;ANALYSE DES QUEUES 0 1 2 3
	PUSHJ	P,ANARNQ
;LECTURE DES PARAMETRES DU DISQUE
	PUSHJ	P,LECDSK	;
;LECTURE DES PARAMETRES DES FICHIERS 
	PUSHJ	P,ANAFIC
;ANALYSE DES ETATS DES JOBS ET CUMUL DES QUEUES ET CALCUL DES VALEURS
	PUSHJ	P,ETAQUE
;CALCUL DES VALEURS GLOBALES
	PUSHJ	P,CAGLOB

	SUBTTLE IMPRESSION
;IMPRESSION LIGNE 1
	OUTSTR	[ASCIZ /[1;35H/]
	MOVE	T1,MUPTIM	;
	IDIV	T1,NBJIFF	;EN SECONDE
	IDIVI	T1,^D3600	;
	MOVEI	P1,4		;
	MOVE	T3,T1		;
	MOVE	T1,T2		;ON SAUVE LE RESTE
	PUSHJ	P,IMPDR		;
	IDIVI	T1,^D60		;
	MOVEI	N,":"		;
	OUTCHR	N		;
	MOVEI	T3,"0"		;
	CAIG	T1,^D9		;
	OUTCHR	T3		;
	MOVE	N,T1		;
	PUSHJ	P,NUMASC	;
	MOVEI	N,":"		;
	OUTCHR	N		;
	CAIG	T2,^D9		;
	OUTCHR	T3		;
	MOVE	N,T2		;
	PUSHJ	P,NUMASC	;
	MOVE	T1,DATNEW	;DATE DU JOUR
	CAMN	T1,DATANC	;IDENTIQUE
	JRST	EDSUI		;
	OUTSTR	[ASCIZ /[1;47H/]
	MOVEM	T1,DATANC	;NON ON LA SAUVE
	IDIVI	T1,^D31		;CONVERSION
	ADDI	T2,1		;LE JOUR
	MOVE	N,T2		;
	PUSHJ	P,NUMASC	;IMPRESSION
	IDIVI	T1,^D12		;
	MOVEI	N," "
	OUTCHR	N
	OUTSTR	MOIS(T2)	;
	ADDI	T1,^D64	;
	MOVEI	N," "		;
	OUTCHR	N
	MOVE	N,T1		;
	PUSHJ	P,NUMASC	;
EDSUI:	OUTSTR	[ASCIZ /[1;59H/]
	MOVE	T1,TIMNEW	;
	IDIVI	T1,^D1000	;EN SEC
	IDIVI	T1,^D3600		;
	MOVEI	T3," "		;
	CAIG	T1,^D9		;
	OUTCHR	T3		;IMPRESSION D'UN BLANC
	MOVE	N,T1		;
	PUSHJ	P,NUMASC	;
	MOVEI	N,":"		;
	OUTCHR	N
	MOVE	T1,T2		;
	IDIVI	T1,^D60		;
	MOVEI	T3,"0"		;
	CAIG	T1,^D9		;
	OUTCHR	T3		;
	MOVE	N,T1		;
	PUSHJ	P,NUMASC	;
	MOVEI	N,":"		;
	OUTCHR	N
	CAIG	T2,^D9		;
	OUTCHR	T3
	MOVE	N,T2		;
	PUSHJ	P,NUMASC	;
;EDITION DES VALEURS GLOBALES
	SETZ	P5,		;
	OUTSTR	[ASCIZ /[21;0H/]
	SETZ	T2,		;PAS DE DEPLACEMENT AU DEPART
EDIBOU:	PUSHJ	P,EDIVAL	;
	CAMN	P5,TBSTLG	;SAUT DE LIGNE
	OUTSTR	[ASCIZ /

/]
	AOS	P5		;
	CAIGE	P5,LGVAL
	JRST	EDIBOU		;

	LECSYM(STATES)		;LECTURE ETAT DU SYSTEME
	ANDI	T1,STSHUT+STSUPR+STRLB
	CAIN	T1,STSANC	;
	JRST	EDJOB		;AUCUN ETAT PARTICULIER
	OUTSTR	[ASCIZ /[23;120H[K/] ;CLEAR LA FIN DE LIGNE
	JUMPE	T1,EDSTF1	;
	OUTSTR	[ASCIZ /[1;5m/]
	TRNE	T1,STSUPR	;SUPER SHUT
	JRST	[	OUTSTR	[ASCIZ /Super shut/]
			JRST	EDSTF	]
	TRNE	T1,STSHUT	;SHUT
	JRST	[	OUTSTR	[ASCIZ /Shut/]
			JRST	EDSTF	]
	TRNE	T1,STRLB	;LOGIN SPECIAL
	OUTSTR	[ASCIZ /Restreint/]
EDSTF:	OUTSTR	[ASCIZ /[0m/]
EDSTF1:	MOVEM	T1,STSANC	;

;EDITIONS DES JOBS
EDJOB:	MOVE	T1,NBJOB	;NOMBRE DE JOB A IMPRIMER
	ADDI	T1,1		;RECHERCHE DU MILIEU
	LSH	T1,-1		;DIVISION PAR 2
	MOVEM	T1,JOBDRO	;PREMIER JOB DE LA COLONNE DE DROITE
	SETZM	JOBGAU		;PREMIER JOB DE LA COLONNE DE GAUCHE
	OUTSTR	[ASCIZ /[2;0H/]	;POSITIONNEMENT LIGNE 2
	SETZM	NBLIGN		;NOMBRE DE LIGNES IMPRIMEES
EDJOBS:	OUTSTR	[ASCIZ /
[K/]				;SAUT DE LIGNE ET ZERO
	MOVE	P5,JOBGAU	;COLONNE DE GAUCHE
	MOVE	P5,LISJOB(P5)	;NUMERO REEL DU JOB
	PUSHJ	P,EDIJOB	;EDITION
	AOS	NBLIGN		;
	SOSG	NBJOB		;ENCORE DES JOBS
	JRST	EDJOBF		;NON
	AOS	JOBGAU		;
	OUTSTR	[ASCIZ /[5C/]	;POSITIONNEMENT SUR LA COLONNE DE DROITE
	MOVE	P5,JOBDRO	;
	MOVE	P5,LISJOB(P5)	;NUMERO REEL
	PUSHJ	P,EDIJOB	;EDITION
	AOS	JOBDRO		;
	SOSLE	NBJOB		;ENCORE DES JOBS
	JRST	EDJOBS		;OUI
EDJOBF:	OUTSTR	[ASCIZ /[K/]	;NON EFFACEMENT DE LA LIGNE
	MOVE	T1,NBLIGN	;
EDJOBC:	CAIL	T1,^D17		;MOINS DE 17 LIGNES
	JRST	EDERR		;NON
	OUTSTR	[ASCIZ /
[2K/]				;SAUT DE LIGNE ET CLEAR
	AOS	T1		;
	JRST	EDJOBC		;
;TEST DE LA CHARGE EDITIONS DES ERREURS DISQUES ET ERREURS DE PARITES MEMOIRE
EDERR:	MOVE	T1,NEWACT	;NOMBRE DE JOBS ACTIFS
	MOVE	T2,NEWSWA	;VALEUR DU SWAPPING
	CAIGE	T1,MAXACT	;TROP DE JOBS ACTIFS
	CAIL	T2,MAXSWA	;OU TROP DE SWAPPING
	SKIPA			;
	SETOM	NBCHAR		;NON
	AOS	NBCHAR		;PLUS UN
	SETZ	P4,		;NOMBRE DE MESSAGE
	SETZ	P2,		;NOMBRE D'ERREURS
	OUTSTR	[ASCIZ /[24;0H[1;5m/]
	SOS	NBUNI		;A PARTIR DE 0
	SETZ	P5,		;D'ABORD LES SATS ERRORS
EDERR1:	SKIPN	P3,ERRSAT(P5)	;
	JRST	EDER1S		;PAS D'ERREUR
	ADD	P2,P3		;
	PUSHJ	P,EDISK		;
	JRST	EDFIN		;PLUS DE PLACE
	OUTSTR	[ASCIZ / Sat fail.  /]
EDER1S:	AOS	P5		;
	CAMG	P5,NBUNI	;
	JRST	EDERR1		;
	SETZ	P5,		;RIB ERRORS
EDERR2:	SKIPN	P3,ERRRIB(P5)	;
	JRST	EDER2S		;PAS D'ERREUR
	ADD	P2,P3		;
	PUSHJ	P,EDISK		;
	JRST	EDFIN		;PLUS DE PLACE
	OUTSTR	[ASCIZ	/ Rib error  /]
EDER2S:	AOS	P5		;
	CAMG	P5,NBUNI	;
	JRST	EDERR2		;
	SETZ	P5,		;HARD DEVICE ERRORS
EDERR3:	SKIPN	P3,ERRDEV(P5)	;
	JRST	EDER3S		;PAS D'ERREUR
	ADD	P2,P3		;
	PUSHJ	P,EDISK		;
	JRST	EDFIN		;PLUS DE PLACE
	OUTSTR	[ASCIZ	/ Device err  /]
EDER3S:	AOS	P5		;
	CAMG	P5,NBUNI	;
	JRST	EDERR3		;
	SETZ	P5,		;CHECKSUM ERRORS
EDERR4:	SKIPN	P3,ERRCHK(P5)	;
	JRST	EDER4S		;PAS D'ERREUR
	ADD	P2,P3		;
	PUSHJ	P,EDISK		;
	JRST	EDFIN		;PLUS DE PLACE
	OUTSTR	[ASCIZ	/ Checksum   /]
EDER4S:	AOS	P5		;
	CAMG	P5,NBUNI	;
	JRST	EDERR4		;
	SETZ	P5,		;HARD DATA ERRORS
EDERR5:	SKIPN	P3,ERRDAT(P5)	;
	JRST	EDER5S		;PAS D'ERREUR
	ADD	P2,P3		;
	PUSHJ	P,EDISK		;
	JRST	EDFIN		;PLUS DE PLACE
	OUTSTR	[ASCIZ	/ Data err   /]
EDER5S:	AOS	P5		;
	CAMG	P5,NBUNI	;
	JRST	EDERR5		;
	SKIPN	N,DPARTO	;PARITE MEMOIRE
	JRST	EDFIN
	AOS	P2		;
	CAIL	P4,6		;MOINS DE 6 MESSAGES
	JRST	EDFIN		;NON
	CAIN	P4,3		;PLUS DE 3 MESSAGES
	OUTSTR	[ASCIZ /#5/]
	AOS	P4		;
	OUTSTR	[ASCIZ /  Parite memo]
	ADD	N,MPARTO	;NOMBRE TOTAL
	PUSHJ	P,NUMASC	;

;FIN DES EDITIONS PAUSE
EDFIN:	MOVE	T1,NBCHAR	;DUREE DE LA CHARGE
	CAIGE	T1,NBMXCH	;TROP LONGUE
	JRST	EDFIN0		;NON
	ADDI	P2,1		;
	ADDI	P4,1		;
EDFIN0:	SKIPN	P4	;DES MESSAGES D'ERREURS
	JRST	EDFIN1	;NON
	CAMG	P2,MERROR	;TOUJOURS LE MEME NOMBRE
	SKIPN	FLBELL		;BELL FERMEE
	SKIPA			;NON
	JRST	EDFIN1		;PAS DE SONNETTE
	MOVEI	T1,^D1200	;
	MOVEI	N,7		;^G
	OUTCHR	N		;
	SOJGE	T1,.-1		;
EDFIN1:	MOVEM	P2,MERROR	;
	OUTSTR	[ASCIZ /[0K/]
	OUTSTR	[ASCIZ /[0m/]
	SKIPE	FLSTP
	JRST	EDFIF
	MOVE	T1,TIMER
	SKIPN	FLHLP		;HELP
	SLEEP	T1,
	SKIPN	FLHLP
	JRST	EDFI1
	OUTSTR	[ASCIZ	/[?3l[2j[6;10H
	taper a n'importe quel moment
	A	Liste des jobs actifs a un instant donne
	B	Pour arreter la sonnette (bell)
	C	Pour nettoyer l'ecran
	E	Liste de tous les jobs
	H	Pour obtenir ce message
	Q	Pour arreter
	M	Pour changer le minimun de microcycles (commande R)
	R	Liste des jobs actifs entre 2 occurrences du timer
	T	Pour changer le timer
	V	Pour connaitre les valeurs du timer et du minimun de microcycles
/]
	SETZM	FLHLP		;
	SETOM	FLCLR		;
	JRST	EDFIN		;ON DORT
EDFI1:	SKIPE	FLTIM		;CHANGER LE TIMER
	PUSHJ	P,VALTIM	;OUI
	SKIPE	FLCPU		;CHANGER LE MINIMUN DE MICROCYCLE
	PUSHJ	P,VALCPU	;
	SKIPE	FLIMP		;IMPRESSION DU TIMER ET MIN DE MICROC
	PUSHJ	P,EDIMP		;
	SKIPE	FLCLR		;CLEAR DE L'ECRAN
	JRST	BOUCLE		;
	SKIPN	FLSTP		;
	JRST	ENCORE

;ARRETER
EDFIF:	SETLCH	[LC.NCM+LC.HHT]	;
	MOVE	T1,[0]	;REMETTRE L'ECHO
	SETMOD	T1,
	OUTSTR	[ASCIZ /[?7h[1;24r[?3l[2J/]
	EXIT	1,	

;ERREURS
ERRGET:	OUTSTR	[ASCIZ /Erreur dans GETTAB/]
	EXIT	1,
ERRSYM:	OUTSTR	[ASCIZ/Erreur dans la recherche des symboles/]
	EXIT	1,
ERRVPK:	OUTSTR	[ASCIZ/Erreur dans la lecture du moniteur/]
	EXIT	1,
ERRINT:	OUTSTR	[ASCIZ/Erreur dans le systeme d'interruption/]
	EXIT	1,
ERRLIC:	OUTSTR	[ASCIZ /?? Process ayant perdu ses licences/]
	EXIT	1,
ERRLI1:	EXIT	1,
	SUBTTLE LECTURE DES VALEURS
;LECTURE DES PARAMETRES DES UNITES DES DISQUES
;APPEL	PUSHJ	P,LECDSK
;	RETOUR 
;USES	T1 T2 T3 ET T4

LECDSK:	SETZM	NEWDKF		;TOTAL LIBRE
	SETZM	TOTICT		;TOTAL UNIICT
	SETZM	TOTOCT		;TOTAL UNIOCT
	SETZ	T4,		;NOMBRE D'UNITES
	LECSYM(SYSUNI)		;1ERE UNITE
LEDSK1:	HLRZ	T2,T1		;LH
	JUMPE	T2,LEDSKF	;FIN
	LSYMT2(UNITAL)		;FREE
	ADDM	T1,NEWDKF	;
	LSYMT2(UNIICT)		;SWP IN
	ADDM	T1,TOTICT	;
	LSYMT2(UNIOCT)		;
	ADDM	T1,TOTOCT	;
	LSYMT2(UNINAM)		;LECTURE DU NOM DE L'UNITE
	MOVEM	T1,MUNNAM(T4)	;
	LSYMT2(UNIMCT)		;NOMBRE D'ERREURS
	LDB	T3,PTUNSA	;
	MOVEM	T3,ERRSAT(T4)	;SAT FAILURES
	LDB	T3,PTUNRB	;
	MOVEM	T3,ERRRIB(T4)	;RIB ERRORS
	LDB	T3,PTUNCK	;
	CAME	T3,MERCHK(T4)	;UNE NOUVELLE ERREUR
	MOVEM	T3,ERRCHK(T4)	;CHECKSUM ERRORS
	LSYMT2(UNIHCT)		;LECTURE HARD ERROR
	HLRZM	T1,ERRDEV(T4)	;HARD DEVICE ERRORS
	HRRZ	T3,T1		;
	CAME	T3,MERDAT(T4)	;UNE NOUVELLE ERREUR
	MOVEM	T3,ERRDAT(T4)	;HARD DATA ERRORS
	LSYMT2(UNISYS)		;UNITE SUIVANTE
	AOS	T4		;NOMBRE D'UNITES
	JRST	LEDSK1		;
LEDSKF:	MOVEM	T4,NBUNI	;
	POPJ	P,		;
;LECTURE DES PARAMETRES DES FICHIERS NOMBRE DE PAGES MAPPEES ET SEPARABLES
;APPEL	PUSHJ	P,ANAFIC
;	RETOUR
;USES	T1-T4
;P1=PT SUR DRB	P2=PT SUR FNB	P3=PT SUR ATB	P4=PT SUR SPT

ANAFIC:	LECSYM(SYSDRB)		;1ERE DRB
ANAFI1:	HLRZ	T2,T1		;ADRESSE 1ERE DRB
	JUMPE	T2,ANAFIF	;FIN PLUS DE DRB
	MOVE	P1,T2		;
	LSYMT2(DRBFNB)		;LECTURE DE LA 1ERE FNB DE CETTE DRB
	HLRZ	T2,T1		;EN LH
	JUMPE	T2,ANAFI8	;PLUS DE FNB
ANAFI2:	MOVE	P2,T2		;ON RANGE PT FNB
	LSYMT2(FNBATB)		;ON VA PASSER A L'ATB
	HLRZ	T2,T1		;LH
	JUMPE	T2,ANAFI7	;PAS D'ATB
ANAFI3:	MOVE	P3,T2		;RANGEMENT DU PT ATB
	LSYMT2(ATBDOR)		;ATB UTILSEE
	JUMPL	T1,ANAFI6	;PAS UTILISEE
;ANALYSE ET COMPTE DES PAGES
	LSYMT2(ATBUMC)		;NOMBRE DE PAGE MAPPEE NON PARTAGEABLE
	JUMPL	T1,[	OUTSTR	[ASCIZ /NON DORMANT ET DUMMY ??
/]
		JRST	ANAFI6	]
	HLRZ	T1,T1		;
	ADDM	T1,NEWMAP	;ON COMPTE LES PAGES MAPPEES
	AOS	NEWFIL		;LE NOMBRE DE FICHIER
	LSYMT2(ATBSPT)		;POINTEUR SUR SPT
	HLRZ	T2,T1		;EN LH
	JUMPE	T2,ANAFI6	;PAS DE PAGE PARTAGEE VOIR ATB SUIVANTE
	SOS	NEWMAP		;UNE PAGE DE PLUS QUAND SPT
ANAFI4:	MOVE	P4,T2		;RANGEMENT DE PT SPT
	MOVE	P5,SPTNME	;CALCUL DE LA FIN DE LA SPT
	ADDI	P5,2(P4)	;FIN +1
	ADDI	T2,2		;
ANAFI5:	MOVE	T1,T2		;
	HRLI	T1,400000	;VIRTUAL
	VPEEK	T1,		;
	JRST	ERRVPK		;
	HRRZ	T1,T1		;NOMBRE D'UTILISATEUR
	SKIPE	T1
	AOS	NEWMAP		;LES COMPTER COMME MAPPEES
	SKIPE	T1		;SAUF SI NUL
	SOS	T1		;
	ADDM	T1,NEWSHR	;NOMBRE DE PAGES SAUVEES PAR PARTAGE
	AOS	T2		;
	CAMGE	T2,P5		;FIN DE LA SPT
	JRST	ANAFI5		;NON
	MOVE	T2,P4		;VOIR SPT SUIVANTE
	LSYMT2(SPTLNK)		;
	HLRZ	T2,T1		;LH
	JUMPE	T2,ANAFI6	;PLUS DE SPT VOIR ATB SUIVANTE
	JRST	ANAFI4		;ON CONTINUE
;AUTRE ATB
ANAFI6:	MOVE	T2,P3		;ON REPREND LE PT ATB
	LSYMT2(ATBLNK)		;ATB SUIVANTE
	HLRZ	T2,T1		;T1
	JUMPN	T2,ANAFI3	;OK ENCORE UNE ATB
	HRRZ	T2,T1		;ON REMONTE A LA FNB
	CAME	T2,P2		;PAS CELUI EN COURS
	JRST	[	OUTSTR [ASCIZ /ATBFNB ET FNB DE DEPART DIFFERENT ??
/]
			JRST	.+1	]
;AUTRE FNB
ANAFI7:	MOVE	T2,P2		;ON PRENDRE LE POINTEUR DE LA FND COURANTE
	LSYMT2(FNBLNK)		;FNB SUIVANTE
	HLRZ	T2,T1		;LH
	JUMPN	T2,ANAFI2	;OK ENCORE UN FNB
;DRB SUIVANT
	MOVE	T2,P2		;POINTEUR DE LA FNB COURANTE
	LSYMT2(FNBDRB)		;ON REMONTE SUR DRB
	HRRZ	T2,T1		;LH
	CAME	T2,P1		;CELLE DU DEPART
	JRST	[OUTSTR [ASCIZ /BAB FNB SUR DRB??
/]
		JRST	.+2	]
ANAFI8:	MOVE	T2,P1		;
	LSYMT2(DRBLNK)		;
	JRST	ANAFI1		;
ANAFIF:	POPJ	P,		;FIN
;SUBROUTINE POUR LIRE TOUS LES PARAMETRES D'UN JOB
;APPEL	PUSHJ	P,LECJOB
;	RETOUR
;USES T1 P5

LECJOB:	SETZ	P5,

;LECTURE DE JBT??? P5/NUMERO DU JOB RETOUR RANGEMENT DANS MJB???
	DEFINE	X(A),<
	MOVE	T1,JBT'A
	ADD	T1,P5
	HRLI	T1,400000
	VPEEK	T1,
	PUSHJ	P,ERRVPK
	MOVEM	T1,MJB'A(P5)
>
LECJO1:	SYMJBT
	DEFINE X(A),<
	MOVE	T1,JBT'A
	ADD	T1,P5
	VPEEK	T1,
	PUSHJ	P,ERRVPK
	EXCH	T1,MJB'A(P5)
	MOVEM	T1,MMJ'A(P5)
>
	INCJBT
	AOS	P5
	CAMG	P5,JOBN
	JRST	LECJO1
	POPJ	P,
;LECTURE DES VALEURS INCREMENTALES DU SYSTEME
;APPEL	PUSHJ	P,LECSYS
;	RETOUR
;USES T1,T2

	DEFINE X(A),<
	MOVE	T1,'A
	HRLI	T1,400000
	VPEEK	T1,
	PUSHJ	P,ERRVPK
	SUBM	T1,M'A
	EXCH	T1,M'A
	MOVEM	T1,D'A
>

LECSYS:	INCSYS
	POPJ	P,
;ANALYSE DES QUEUES 0 ET 3, REMPLISSAGE DE MJBETA PAR "Q0"..."Q3"
;APPEL	PUSHJ	P,ANARNQ
;	RETOUR
;USES T1 A T4 P1 A P4


ANARNQ:	MOVE	T1,SSCAN	;
	SETZ	P1,		;NUMERO DE LA QUEUE
	MOVEI	P4,"Q0"		;
ANRNQ1:	MOVE	T2,T1		;
	HRLI	T2,400000	;
	VPEEK	T2,		;LECTURE
	JRST	ERRVPK
	JUMPE	T2,ANRNQF	;FIN
	SETZM	NEWQ0(P1)	;A ZERO
	HLRE	T2,T2		;PREMIER JOB POINTE
	MOVE	T4,JBTQ		;
ANRNQ2:	ADD	T2,T4		;
	HRLI	T2,400000	;
	VPEEK	T2,
	JRST	ERRVPK
	HRRZ	T2,T2		;
	TRNE	T2,400000	;FIN DE LA QUEUE
	JRST	ANRNQ3		;OUI
	AOS	NEWQ0(P1)	;
	MOVE	P5,T2		;
	DPB	P4,PTRETA	;
	JRST	ANRNQ2		;
ANRNQ3:	AOS	P1		;
	AOS	T1		;
	AOS	P4		;
	JRST	ANRNQ1		;
ANRNQF:	POPJ	P,
;DETERMINATION DE L'ETAT DU JOB ET CUMUL DES QUEUES MISE A ZERO PRELIMINAIRES
;APPEL	PUSHJ	P,ETAQUE
;RETOUR
;USES	T1,T2,P1,P2,P3,P4,P5

ETAQUE:	SETZ	P5,
	SETZ	T2,		;INDICE DE LA LISTE DES JOBS
ETAQU0:	SETZM	OKJOB		;JOB A LISTER
	PUSH	P,T2		;CONSERVER P2
	PUSHJ	P,CALCYC	;CALCUL DES MICROCYCLES PAR SECONDE
	MOVEM	T1,MJBMCY(P5)	;
	POP	P,T2		;RESTAURER T2
	MOVE	P1,MJBSTS(P5)	;ETAT DU JOB
	TLNN	P1,%JNA		;
	JRST	QUEFFN		;RIEN POUR CE JOB
	SKIPE	FLTOT		;TOUS LES JOBS
	SETOM	OKJOB		;OUI
	SKIPE	FLRUN		;RUN
	CAMGE	T1,MINCPU	;
	SKIPA	
	SETOM	OKJOB		;
	AOS	NEWUSE		;UN USER DE PLUS
	TLNN	P1,%CMWB	;
	JRST	ETAQU1		;
	MOVEI	P4,"CW"		;CORE WAIT
	TLNN	P4,%RUN		;ET RUN
	MOVEI	P4,"^W"		;NON
	JRST	QUEFIN		;
ETAQU1:	TLNN	P1,%JERR	;ERREUR
	JRST	ETAQU2		;NON
	MOVEI	P4,"OW"		;OUI
	JRST	QUEFIN		;
ETAQU2:	TLNE	P1,%RUN		;RUN
	JRST	ETAQU3		;OUI
	MOVEI	P4,"^C"		;
	JRST	QUEFIN		;
ETAQU3:	LDB	P3,PT1WCD	;WAIT CODE
	SKIPE	P4,CVTQUE(P3)	;COMPTEUR DE QUEUES
	SKIPE	MJBETA(P5)	;DEJA COMPTER DANS Q?
	SKIPA			;OUI
	AOS	NEWUSE(P4)	;
	IDIVI	P3,3		;
	MOVE	T1,STSTBL	;CODE 
	ADD	T1,P3		;
	HRLI	T1,400000	;
	VPEEK	T1,		;LECTURE
	JRST	ERRVPK
	LDB	P3,PTCODE(P4)	;
	ADDI	P3," "-' '	;CONVERSION EN ASCII
	LSH	P3,7		;
	LDB	P4,PTCOD1(P4)	;
	ADDI	P4," "-' '	;
	ADD	P4,P3		;
QUEFIN:	SKIPN	MJBETA(P5)	;DEJA REMPLI PAR Q?
	DPB	P4,PTRETA	;
	TLNN	P1,%RUN		;RUN?
	JRST	QUEFFN		;NON
	CAIN	P4,"TI"		;TIOW
	JRST	QUEFFN		;OUI
	CAIN	P4,"SL"		;SLEEP
	JRST	QUEFFN		;OUI
	SKIPE	FLACT		;
	SETOM	OKJOB		;ACTIF
	TLNN	P1,%MRQ		;MRQ
	JRST	QUEFFN		;NON
	AOS	NEWMRQ		;
	LDB	P3,PTJMWS	;
	ADDM	P3,NEWMPQ	;
	LDB	P3,PTJPGO	;MOINS LES ACTIVES
	SUB	P3,NCTXPG	;MOINS LES PAGES DE CONTEXTE
	SKIPG	P3
	SETZ	P3,
	MOVN	P3,P3		;
	ADDM	P3,NEWMPQ	;
QUEFFN:	SKIPN	OKJOB		;JOB A LISTER
	JRST	QUEFFF		;NON
	LDB	P1,PTJMWS	;
	ADDM	P1,NEWTMW	;
	LDB	P1,PTJUWS	;
	ADDM	P1,NEWTUW	;
	MOVEM	P5,LISJOB(T2)	;
	AOS	T2		;
QUEFFF:	AOS	P5
	CAMG	P5,JOBN		;
	JRST	ETAQU0		;
	MOVE	T1,NEWSWP	;SWP
	ADD	T1,NEWLQ	;+LQ
	ADD	T1,NEWSPC	;+SPC
	ADD	T1,NEWQ0	;+Q0
	ADD	T1,NEWQ1	;+Q1
	ADD	T1,NEWQ2	;+Q2
	ADD	T1,NEWQ3	;+Q3
	ADDM	T1,NEWACT	;+RN=ACT
	MOVEM	T2,NBJOB	;
	MOVE	T1,NEWTUW	;CALCUL DU GAIN DES WS DES JOBS ACTIFS
	SUB	T1,NEWTMW	;
	IMULI	T1,^D100	;
	IDIV	T1,NEWTUW	;
	MOVEM	T1,NEWGWS	;
	POPJ	P,
; CAGLOB - CALCUL DES VALEURS GLOBALES PAR RAPPORT A LA SECONDE
;APPEL	PUSHJ	P,CALGLOB
;	RETOUR	VALEUR DANS NEW???
;USES	T1,T2

CAGLOB:
;CALCUL IDLE
	MOVE	T1,DNULTI	;NULL TIME
	SUB	T1,DLSTWR	;MOINS LOST
	IMULI	T1,^D100	;%
	PUSHJ	P,CALJIF	;
	MOVEM	T1,NEWIDL	;
;CALCUL LOST
	MOVE	T1,DLSTWR	;LA DIFFERENCE
	IMULI	T1,^D100	;%
	PUSHJ	P,CALJIF	;
	MOVEM	T1,NEWLOS	;
;CALCUL MCYCLE
	MOVE	T1,MJBMCY	;
	MOVEM	T1,NEWMC	;
;CALCUL SWAPPING
	MOVE	T1,TOTICT	;
	ADD	T1,TOTOCT	;
	SUB	T1,MEMICT	;
	SUB	T1,MEMOCT	;DIFFERNCE DU TOTAL
	PUSHJ	P,CALSEC	;PAR SECONDE
	MOVEM	T1,NEWSWA	;
	MOVE	T1,TOTICT	;IN
	SUBM	T1,MEMICT	;
	EXCH	T1,MEMICT	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWSIN	;
	MOVE	T1,TOTOCT	;OUT
	SUBM	T1,MEMOCT	;
	EXCH	T1,MEMOCT	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWSOU	;
;CALCUL DE PFLCNT
	MOVE	T1,DPFLCN	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWPFL	;
;CALCUL OKPCNT
	MOVE	T1,DOKPCN	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWOKP	;
;CALCUL	SIFCNT
	MOVE	T1,DSIFCN	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWSIF	;
;CALCUL SIJCNT
	MOVE	T1,DSIJCN	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWSIJ	;
;CALCUL EATCNT
	MOVE	T1,DEATCN	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWEAT	;
;CALCUL TSAVAL
	MOVE	T1,DTSAVA	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWTSA	;
;CALCUL WSAVAL
	MOVE	T1,DWSAVA	;
	PUSHJ	P,CALSEC	;
	MOVEM	T1,NEWWSA	;
	POPJ	P,


;CALCYC - CALCUL DES MICRO CYCLES PAR SECONDE
;APPEL	MOVE	P5,NUMERO DU JOB
;	PUSHJ	P,CALCYC
;	RETOUR	VALEUR DANS T1
;USES T1 T2 T3 T4

CALCYC:	MOVE	T1,MJBCP2(P5)	;
	MOVE	T2,MJBCMP(P5)	;
	MOVE	T3,MMJCP2(P5)	;
	MOVE	T4,MMJCMP(P5)	;
	DSUB	T1,T3		;
	SKIPE	T1		;
	SETZ	T2,		;A ZERO SI NEGATIF
	MOVE	T1,T2		;
	JRST	CALSEC		;

; CALSEC - CALCUL PAR SECONDE
;APPEL	MOVE	T1,LA VALEUR
;	PUSHJ	P,CALSEC
;	RETOUR 	RESULTAT DANS T1
;USES T2

CALSEC:	IMUL	T1,NBJIFF	;NOMBRE DE JIFFIES PAR SECONDE
CALJIF:	MOVE	T2,DUPTIM	;+DUPTIM/2
	LSH	T2,-1		;
	ADD	T1,	T2	;POUR ARRONDI
	IDIV	T1,DUPTIM	;DIFFERENCE DE JIFFIES
	POPJ	P,
	SUBTTLE	SUBROUTINES D'AFFICHAGE


; EDIJOB - EDITION DES PARAMETRES D'UN JOB
;APPEL	MOVE	P5,NUMERO DU JOB ET CURSEUR POSITIONNE
;	PUSHJ	P,EDIJOB
;	RETOUR
;USES	

EDIJOB:	MOVEI	T1,DESJOB	;EDITION DU NUMERO DU JOB
	SETZ	T2,		;PAS DE DEPLACEMENT
	MOVE	T3,P5		;
	PUSHJ	P,IVALJB		;
	MOVEI	N," "		;DETACHED
	HRLZ	T1,P5		;
	HRRI	T1,.GTLIN	;
	GETTAB	T1,		;
	JFCL			;ON IGNORE
	TLNN	T1,-1		;
	MOVEI	N,"d"		;
	OUTCHR	N		;
	OUTSTR	[ASCIZ /[1C/]
	MOVE	T3,MJBUNM(P5)	;EDITION DE L'USERNAME
	PUSHJ	P,EDSIXB	;EDITION EN ASCII
	MOVE	T3,MJBUN1(P5)	;DEUXIEME PARTIE
	PUSHJ	P,EDSIXB	;
	OUTSTR	[ASCIZ /[1C/]	;SAUTER UN CARACTERES
	MOVE	T3,MJBNAM(P5)	;NOM DU PROGRAMME
	PUSHJ	P,EDSIXB	;
	MOVEI	T1,DESSIZ	;EDITION DE LA TAILLE
	SETZ	T2,		;
	HLRZ	T3,MJBUPM(P5)	;TAILLE
	PUSHJ	P,IVALJB		;
	MOVEI	T1,DESMWS	;EDITION DE MWS
	LDB	T3,PTJMWS	;
	PUSHJ	P,IVALJB		;
	MOVEI	T1,DESACJ	;EDITION DE TAILLE ACTIVE
	LDB	T3,PTJPGO	;
	SUB	T3,NCTXPG	;MOINS LES PAGES ACTIVES
	SKIPGE	T3		;SI MOINS ALORS 0
	SETZ	T3,		;
	PUSHJ	P,IVALJB	;
	MOVE	T3,MJBMCY(P5)	;MICROCYCLE
	MOVEI	T1,DESMCJ	;MICROCYCLE
	PUSHJ	P,IVALJB	;
	MOVE	T1,MJBRCT(P5)	;DIO/SEC
	ADD	T1,MJBWCT(P5)	;
	ADD	T1,MJBMPC(P5)	;
	SUB	T1,MMJRCT(P5)	;
	SUB	T1,MMJWCT(P5)	;
	SUB	T1,MMJMPC(P5)	;
	PUSHJ	P,CALSEC	;
	SKIPGE	T3,T1
	SETZ	T3,		;0 SI NEGATIF
	MOVEI	T1,DESDIO	;
	PUSHJ	P,IVALJB	;
	MOVE	T1,MJBCIN(P5)	;TIO/SEC
	ADD	T1,MJBCOT(P5)	;
	SUB	T1,MMJCIN(P5)	;
	SUB	T1,MMJCOT(P5)	;
	PUSHJ	P,CALSEC	;
	SKIPGE	T3,T1
	SETZ	T3,		;0 SI NEGATIF
	MOVEI	T1,DESJIO	;
	PUSHJ	P,IVALJB	;
	OUTSTR	[ASCIZ /[2C/]
	MOVE	T1,MJBETA(P5)	;EDITION DE L'ETAT
	OUTSTR	T1
	POPJ	P,
; EDIVAL - CONTROLE SI VALNUR A EDITER ET EDITION MISE A JOUR DES COMPTEUR
;APPEL	MOVE	P5,INDICE DS VALNEW ET VALANC
;	PUSHJ	P,EDIVAL
;USES T3,T1

EDIVAL:	MOVE	T1,DESVAL(P5)	;DESCRITIVE D'EDITION
	MOVE	T3,VALNEW(P5)	;NOUVELLE VALEUR
	CAMN	T3,VALANC(P5)	;= A L'ANCIENNE VALEUR
	JRST	EDIVAE		;OUI
	CAIE	P5,1		;NOMBRE DE JOBS ACTIFS
	JRST	EDIVA2		;NON
	CAIGE	T3,MAXACT	;TROP GRAND NON
	JRST	EDIVA3		;NON
EDIVA1:	OUTSTR	[ASCIZ /[1;5m/] ;BRILLANCE ET CLIGNOTEMENT
	PUSHJ	P,@(T1)		;EDITION
	OUTSTR	[ASCIZ/[0m/]	;SUPPRESSION DES ATTRIBUTS
	POPJ	P,		;
EDIVA2:	CAIE	P5,20		;VALEUR DU SWAPPING
	JRST	EDIVA3		;NON
	CAIL	T3,MAXSWA	;TROP IMPORTANT
	JRST	EDIVA1		;OUI
EDIVA3:	MOVEM	T3,VALANC(P5)	;ON RANGE COMME ANCIENNE
	PUSHJ	P,@(T1)		;EDITION
	POPJ	P,		;
EDIVAE:	HLRZ	T3,1(T1)	;LONGEUR TOTALE A SAUTER
	ADD	T2,T3		;
	ADD	T2,2(T1)	;
	POPJ	P,		;


; EDISK - EDITION DES ERREURS DISQUE
;APPEL	MOVE	P3,NOMBRE D'ERREUR DE CE TYPE
;	MOVE	P4,NOMBRE DE MESSAGE DEJA IMPRIMES
;	MOVE	P5,INDICE DE L'UNITE
;	PUSHJ	P,EDISK
;	RETOUR LIGNE PLEINE
;	RETOUR NORMAL
;USES T3,N,R

EDISK:	SKIPN	P4		;
	OUTSTR	[ASCIZ /#6/]
	AOS	P4		;ENCORE UN MESSAGE
	CAIN	P4,4		;LE 4EME MESSAGE
	OUTSTR	[ASCIZ /#5/]	;EN SIMPLE CARACTERE PLUS DE 3 MESSAGES
	CAILE	P4,6		;MOINS DE 6 MESSAGES
	POPJ	P,		;NON
	MOVE	T3,MUNNAM(P5)	;EDITION DE DPA??
	PUSHJ	P,EDSIXB	;
	MOVE	N,P3		;
	PUSHJ	P,NUMASC	;EDITION DU NOMBRE D'ERREURS
	AOS	(P)		;SKIP RETOUR
	POPJ	P,		;

; EDSIXB - EDITION D'UNE VALEUR EN SIXBIT TOUJOURS SI 6 CARACTERES
;APPEL	MOVE	T3,VALEUR
;	PUSHJ	P,EDSIXB
;	RETOUR
;USES	N,R

EDSIXB:	MOVE	N,[POINT 6,T3]
EDSIXS:	TLNN	N,(77B5)	;
	POPJ	P,		;FIN
	ILDB	R,N		;CARACTERE
	ADDI	R," "-' '	;CONVERSION EN ASCII
	OUTCHR	R
	JRST	EDSIXS		;




; IOPT - EDITION SI VALEUR # 0 SINON REMISE A BLANC DE LA ZONE
;APPEL	MOVE	T1,ADR DU DESCRIPTEUR
;	MOVE	T2,DEPLACEMENT ANTERIEUR
;	MOVE	T3,LA VALEUR
;	PUSHJ	P,IOPT
;	RETOUR T2 MIS ZERO T3 PERDU


IOPT:	SKIPE	T3	;VALEUR NULLE
	JRST	INED	;NON ALORS IMPRESSION
	OUTSTR	3(T1)		;POSITIONNEMENT
	HLRZ	P1,1(T1)		;LONGUEUR TOTALE
IOPT1:	SOJL	P1,IOPTF	;
	MOVEI	N," "		;
	OUTCHR	N		;
	JRST	IOPT1		;
IOPTF:	SETZ	T2,
	POPJ	P,
; INED - AFFICHAGE D'UNE VALEUR ENTIER CADREE A DROITE AVEC UNITE
;APPEL	MOVE	T1,ADR DU DESCRIPTEUR
;	MOVE	T3,LA VALEUR
;	RETOUR T3 PERDU
;USES	T4,P1,N

INED:	HRRZ	P1,1(1)	;LONGEUR DE LA VALEUR
	OUTSTR	3(T1)		;POSITIONNEMENT
	PUSHJ	P,IMPDR		;IMPRESSION
	SKIPE	T2,2(T1)	;UNITES
	OUTSTR	T2		;
	SETZ	T2,		;
	POPJ	P,


; IVALJB -EDITION D'UNE VALEUR NUMERIQUE LIEE A UN JOB
;APPEL	MOVE	T1,DESCRIPTIF
;	MOVE	T3,VALEUR A EDITER
;	PUSHJ	P,IVALJB
;USES T3

IVALJB:	HRRZ	P1,(T1)		;LONGUEUR DE LA ZONE A IMPRIMER
	MOVE	T2,1(T1)	;DEPLACEMENT
	PUSHJ	P,IMDPLC	;
	JRST	IMPDR		;IMPRESSION DE LA VALEUR


; IMPDR - EDITION D'UNE VALEUR CADREE A DROITE
;APPEL	MOVE	T3,VALEUR
;	MOVE	P1,LONGUEUR DE LA ZONE
;	PUSHJ	P,IMPDR
;	RETOUR
;USES T3,T4

IMPDR:	SETZ	T2,		;NOMBRE DE CARACTERE A IMPRIMER
IMPDR1:	IDIVI	T3,^D10		;
	PUSH	P,T4		;
	AOS	T2		;
	JUMPE	T3,IMPDR2	;
	CAMGE	T2,P1		;DEBORDEMENT
	JRST	IMPDR1		;NON ON CONTINUE
IMPDR2:	SUB	P1,T2		;PLACE RESTANTE
IMPDR3:	SOJL	P1,IMPDR4	;ECRIRE DES BLANCS
	MOVEI	N," "		;
	OUTCHR	N		;
	JRST	IMPDR3		;
IMPDR4:	POP	P,T4		;
	ADDI	T4,"0"		;CONVERSION EN ASCII
	OUTCHR	T4		;
	SOJG	T2,IMPDR4	;ENCORE
	POPJ	P,
; IMDPLC - DEPLACEMENT DU CURSEUR
;APPEL	MOVE	T2,DEPLACEMENT
;	PUSHJ	P,IMDPLC
;	RETOUR
;USES N

IMDPLC:	JUMPE	T2,IMDPLF	;SAUF SI PAR DE DEPLACEMENT
	MOVEI	N,33
	OUTCHR	N
	MOVEI	N,"["	
	OUTCHR	N
	MOVE	N,T2
	PUSHJ	P,NUMASC
	MOVEI	N,"C"
	OUTCHR	N
IMDPLF:	POPJ	P,


;IMPSLI - POSITIONNEMENT DU CURSEUR LIGNE N COLONNE M
;APPEL	MOVE	T2,[N,,M]
;	PUSHJ	P,IMPSLI
;	RETOUR	
;USES	T2, T3 ,T4, N

IMPSLI:	MOVEI	N,33
	OUTCHR	N
	MOVEI	N,"["
	OUTCHR	N
	HLRZ	N,T2
	PUSHJ	P,NUMASC
	MOVEI	N,";"
	OUTCHR	N
	HRRZ	N,T2
	PUSHJ	P,NUMASC
	MOVEI	N,"H"
	OUTCHR	N
	POPJ	P,


;NUMASC - CONVERSION ENTIER ASCII ET IMPRESSION
;APPEL:	MOVE	N,ENTIER
;	PUSHJ	P,NUMASC
;	RETOUR
;USES N, R T4 PERDU

NUMASC:	SETZ	T4,
NUMAS1:	IDIVI	N,^D10
	PUSH	P,R
	AOS	T4
	JUMPN	N,NUMAS1
NUMAS2:	POP	P,R
	ADDI	R,"0"
	OUTCHR	R
	SOJG	T4,NUMAS2
	POPJ	P,
; EDIMP - EDITION DU TIMER ET MINCPU SUR LA LIGNE 24
;APPEL	PUSHJ	P,EDIMP
;	RETOUR
;USES N ,R,T4


EDIMP:	SETZM	FLIMP
	OUTSTR	[ASCIZ /[24;0H#5 TIMER= /]
	MOVE	N,TIMER
	PUSHJ	P,NUMASC
	OUTSTR	[ASCIZ /   MINIMUN MICROCYCLES= /]
	MOVE	N,MINCPU
	PUSHJ	P,NUMASC
	POPJ	P,


; VALTIM - PRISE EN COMPTE DU CHANGEMENT DU TIMER
;APPEL	PUSHJ	P,VALTIM
;USES T1 T2

VALTIM:	SETZM	FLTIM	;
	MOVE	T1,[1B1];ARRETER LES INTERRUPTIONS
	INTENB	T1,
	JRST	ERRINT
	OUTSTR	[ASCIZ /[24;1H[0K#6 TIMER=/]
	PUSHJ	P,VALVAL	;
	CAIL	T2,^D10		;SUPERIEUR A 10 SEC
	CAIL	T2,^D300	;NI SUPERIEUR A 5MM
	SKIPA
	MOVEM	T2,TIMER	;
	POPJ	P,


; VALCPU - PRISE EN COMPTE DU CHANGEMENT DU MINIMUN DE MICROCYCLES
;APPEL	PUSHJ	P,VALCPU
;USES T1 T2

VALCPU:	SETZM	FLCPU	;
	OUTSTR	[ASCIZ /[24;1H[0K#6 MINIMUN MICROCYCLES=/]
	PUSHJ	P,VALVAL	;
	MOVEM	T2,MINCPU	;
	POPJ	P,
; VALVAL - LECTURE D'UNE VALEUR
;APPEL	PUSHJ	P,VALVAL
;	RETOUR 
;USES T1,T2

VALVAL:	MOVE	T1,[1B1];ARRETER LES INTERRUPTIONS
	INTENB	T1,
	JRST	ERRINT
	SETZ	T2,
	MOVE	T1,[0]	;REMETTRE L'ECHO
	SETMOD	T1,
VALVA0:	INCHWL	T1
	CAIN	T1,15	;ON IGNORE RC
	JRST	VALVA0	;
	CAIN	T1,12	;LF
	JRST	VALVAF	
	CAIN	T1,33	;ESC
	JRST VALVAF	;
	CAIN	T1,14	;FF
	JRST	VALVAF
	CAIN	T1,13	;VT
	JRST	VALVAF	;
	SUBI	T1,"0"	;CONVERSION
	JUMPL	T1,VALVA0
	CAILE	T1,^D9	;
	JRST	VALVA0
	IMULI	T2,^D10	;
	ADD	T2,T1	;
	JRST	VALVA0	;
VALVAF:	MOVE	T1,[IO.NEE+IO.NEC]	;PAS D'ECHO
	SETMOD	T1,
	MOVE	T1,[1B1]	;NETTOYER LE INTERRUPTIONS
	INTACT	T1,		;
	JRST	ERRINT		;
	MOVE	T1,[3B1]	;ON OUVRE LES INTERRUPTIONS
	INTENB	T1,
	JRST	ERRINT	;
	OUTSTR	[ASCIZ /[24;0H[2K/]
	POPJ	P,
; INTCHR - TRAITEMENT DE L'INTERRUPTION SUR UN CARACTERE


INTCHR:	MOVEM	T1,MT1		;SAUVER T1
	INCHRS	T1		;
	JRST	INTCHF		;PAS DE CARACTERES ALORS FIN
	CLRBFI			;ON CLEAR LE BUFFER
	CAILE	T1,140		;CONVERSION EN MAJUSCULE
	SUBI	T1,40
	CAIN	T1,"B"		;ARRET DE LA SONETTE
	SETOM	FLBELL		;
	CAIN	T1,"Q"		;ON ARRETE
	SETOM	FLSTP		;
	CAIN	T1,"C"		;CLEAR
	SETOM	FLCLR		;
	CAIN	T1,"M"		;CPU
	SETOM	FLCPU		;
	CAIN	T1,"T"		;TIMER
	SETOM	FLTIM		;
	CAIN	T1,"H"		;
	SETOM	FLHLP		;
	CAIN	T1,"V"		;IMPRESSION DE TIMER ET MINCPU
	SETOM	FLIMP		;
	CAIE	T1,"A"		;ACTIF
	JRST	INTCH1		;NON
	SETOM	FLACT		;
	SETZM	FLRUN		;
	SETZM	FLTOT		;
	JRST	INTCHF		;
INTCH1:	CAIE	T1,"R"		;RUN
	JRST	INTCH2		;NON
	SETOM	FLACT		;
	SETOM	FLRUN		;
	SETZM	FLTOT		;
	JRST	INTCHF		;
INTCH2:	CAIE	T1,"E"		;
	JRST	INTCHF		;
	SETZM	FLACT		;
	SETZM	FLRUN		;
	SETOM	FLTOT		;
INTCHF:	MOVE	T1,MT1		;
	DISMIS
	END	ETATSY
  5c