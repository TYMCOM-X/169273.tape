    SUBTTL	CONFIG.602 - DISPLAY CONFIGURATION DURING ASSEMBLY
: ******************************************************************
: **                  PROPRIETARY INFORMATION                     **
: **                                                              **
: **  This  source code listing constitutes the proprietary pro-  **
: **  perty of TYMNET, Incorporated. The recipient, by receiving  **
: **  this program listing, agrees that neither this listing nor  **
: **  the   information  disclosed herein nor any  part  thereof  **
: **  shall be  reproduced or transferred to other documents  or  **
: **  used  or  disclosed to others for manufacturing or for any  **
: **  other purpose except as specifically authorized in writing  **
: **  by  TYMNET, Incorporated.                                   **
: ******************************************************************
: **                   MODULE IDENTIFICATION                      **
: **                                                              **
: **  File Name          :  CONFIG.602      last change: 10/15/86 **
: **                                                 		  **
: **  File Description   :  Displaying CONSAT configuration during**
: **			    assembly time is based upon CONFIG    **
: **			    parameter which is defined in VERIFY  **
: **			    module. CONFIG = 0 - nothing display  **
: **			    1 - simple display, 2 - simple display**
: **			    with debugging options.               **
: **								  **
: **  File Abstract      :  Output an assembly configuration for  **
: **			    someone who wants to check if config- **
: **			    ration is correct.                    **
: **                                                              **
: ******************************************************************
: **                    CHANGE LOG                                **
: **                                                              **
: **  Ver   Date    By   PIR/NSR  Changes (most recent at top)    **
: ** ----- -------- ---  -------- ------------------------------- **
: ** 06.02 10/15/86 CAB		  Changed display to show PVC info**
: **				  for AID/ALO/TIDXX.		  **
: ** 06.02 10/10/86 CAB		  Changed host display to show both*
: **				  origination and destination host**
: **				  using OHSAY and DHSAY macros.   **
: ** 06.02 09/30/86 CAB		  TID speed display for 2400/4800 **
: ** 04.03 03/04/86 SDW  PIR 1653 show Outdial and no more VADIC  **
: ** 04.02 01/07/86 LH            Add proprietary banner.         **
: **                                                              **
: ******************************************************************
	SUBTTL	(CONFIG) OUTPUT ASSEMBLY CONFIGURATION


:    ***   ***  *   *  ****  ***  *     ***** ****    *   ***** ***** ****
:   *   * *   * **  * *     *   * *       *   *   *  * *    *   *     *   *
:   *     *   * * * *  ***  *   * *       *   *   * *****   *   ****  *   *
:   *   * *   * *  **     * *   * *       *   *   * *   *   *   *     *   *
:    ***   ***  *   * ****   ***  ***** ***** ****  *   *   *   ***** ****


:                     ***** *   * *   *  ****   *   *****
:                       *    * *  ** ** *      * *    *
:                       *     *   * * *  ***  *****   *
:                       *     *   *   *     * *   *   *
:                       *     *   *   * ****  *   *   *


:  ***   ***  *   * ***** *****  ***  *   * ****    *   ***** *****  ***  *   *
: *   * **  * **  * *       *   *   * *   * *   *  * *    *     *   *   * **  *
: *     *   * * * * ****    *   *     *   * ****  *****   *     *   *   * * * *
: *   * *   * *  ** *       *   *  ** *   * *  *  *   *   *     *   *   * *  **
:  ***   ***  *   * *     *****  ****  ***  *   * *   *   *   *****  ***  *   *

:	Display is based upon CONFIG parameter:
:	0 -	nothing displayed
:	1 -	simple display
:	2 -	simple display with debugging options


	IF	CONFIG
:	OUTPUT AN ASSEMBLY CONFIGURATION LIKE:

:	CONSAT CONFIGURATION - VERSION X.XX
:	----------------------------------------
:
:	Node:   303
:	Slot:   2
:	Default origination host:	16294
:
:	ASYNC ports:	64	(0-63)
:
:	   Debugging Options:
:		DCPSSW
:
:	--------------------------------------
:	Define Macros used...
	 KILL	PRANGE,PSET,SAY.P,BSAY,PVCSAY,OHSAY,DHSAY,OPTION


:	PRANGE Macro -		report number of ports N
:				L2:	range over R
PRANGE	 MACRO(N,R)[
	 NUMBER	$A R
	 IF	CONFIG-3		:LEVEL 4
	  REMARK 	(
	  NUMBER $A N
	  IF	R-1
	   REMARK -
	   NUMBER $A N+R-1
	  EI
	  REMARK )%
	 EI
]

:	PSET(N,BIT,R,SAY)		If N>0 and CONFIG>2, then say
:					"	N 'say' ports (i1, i2...)"
PSET	MACRO(N,B,R,SAY) [
	IF	(CONFIG-2)*N
	 REMARK %    -- 
	 NUMBER $A N
	 REMARK  SAY port
	 IF	N-1
	  REMARK s
	 EI
	 REMARK  (
Q	 EQ	0			:port counter
QQ	 EQ	0			:field counter
	 RE	R
	  IF	.BIT.(B,Q)
	   IF	QQ
	    IF	QQ&7
	     REMARK %              
	    ELSE
	     REMARK , 
	    EI	:QQ&7
	   EI	:QQ
QQ	   EQ	QQ+1
	   NUMBER $A Q
	  EI	:.BIT.(B,Q)
Q	  EQ	Q+1
	 ER
	 REMARK )%
	EI	:(CONFIG-2)*N
]


:	SAY.P(V) Macro		:output pretty port number V
SAY.P	MACRO(V) [
	 IF	$A 10-V
	  REMARK %  
	 ELSE	$A 100-V
	  REMARK % 
	 ELSE
	  REMARK %
	 EI
	 NUMBER $A V
	 REMARK : 
]

:	BSAY (C,A,T1,T2) Macro -	If C is .TRUE., then
:					if (bit(A,P) =1, say T1 else T2
BSAY	MACRO(C,A,T1,T2)[
BFLAG	EQ	0
	 IF	C
	  IF	.BIT.(A,P)
BFLAG	   EQ	1
	   REMARK T1
	  ELSE
	   REMARK T2
	  EI
	 EI
]
:	PVCSAY (P) Macro -		Identify PVC characteristics
:
PVCSAY	MACRO(P)[
QFLAG	EQ	0
	 IF	PVC
	  IF	.BIT.(PVCG,P)
	   IF	PVCX|P|&1
QFLAG      EQ	1
	    IF	PVCX|P|&2
	    REMARK PVC-AID-ALO
	    ELSE
	    REMARK PVC-AID    
	    EI
           ELSE
	    IF	.SPORT-P
	    REMARK PVC	
	    EI
	   EI
	  ELSE
	    IF	.SPORT-P
	    REMARK 		
	    EI
	  EI
	 ELSE
	    IF	.SPORT-P
	    REMARK 		
	    EI
	 EI
	 IF QFLAG
	   REMARK  TID
QTEMP	   EQ	(PVCX|P|&07F000000)/1000000
	   IF .SPORT-P
	   ELSE
QTEMP	   EQ	QTEMP-TIDMAX
	   EI
	   NUMBER $A QTEMP
	   REMARK 	
	 ELSE
	   IF	.SPORT-P
BFLAG      EQ 0
	   ELSE
	   REMARK MISSING SIO PVC-AID!!
BFLAG	   EQ 1
	   EI
	   IF 1-BFLAG
	   BSAY(1,.30T, TIDMSG@300)
	   EI
	   IF 1-BFLAG
	   BSAY(1,.120T, TIDMSG@1200)
	   EI
	   IF 1-BFLAG
	   BSAY(1,.240T, TIDMSG@2400)
	   EI
	   IF 1-BFLAG
	   BSAY(1,.480T, TIDMSG@4800)
	   EI
	   IF 1-BFLAG
	   REMARK 	 !!!AID NEEDED !!!
           EI
	 EI   
]

:	OHSAY(P) Macro - Prints out ":ORIG-HOST" 
:				(Level-3)
OHSAY	MACRO(P) [
	IF	CONFIG-2
	    REMARK 	:ORIG-
	   IF \.ACH|P|
Q2	EQ	.ACH|P|
	    NUMBER $A Q2
	   ELSE
	    NUMBER $A .AHOST
	   EI
	EI	:CONFIG
]
:	DHSAY(P) Macro - Prints out ":DEST-HOST.ADDR" for PA-ports
:			(Level-3)
DHSAY	MACRO(P) [
	IF	CONFIG-2
	 IF	NPAPRT
Q2	  EQ	0
	  RE	NPAPRT
	   IF	1-((PAC|Q2|&0FF)%P)
	    REMARK 	:DEST-
	    NUMBER PAC|Q2|/10000$A
	    REMARK .
	    NUMBER (PAC|Q2|/100)&0FF$A
	   EI
Q2	   EQ	Q2+1
	  ER	:NPAPRT
	 EI	:NPAPRT
	EI	:CONFIG
]

:	OPTION Macro -		Display parameter V if .TRUE.,
:				unless D is .TRUE. -- then DISPLAY Value V too.
OPTION	MACRO(D,V) [
	IF	D
	 REMARK %     V:	
	 NUMBER $A V
	ELSE
	 IF	V
	  REMARK %	V
	 EI	:V
	EI	:D
]
	SUBTTL	(CONFIG) Generate output

:	VERSION, NODE, SLOT, PVC and MPVC
	 REMARK %%%%  CONSAT configuration - version 
	 NUMBER $8 VERSION
	 REMARK .
	 IF	0A-REVLEV
	  REMARK 0
	 EI
	 NUMBER $A REVLEV
	 IF	FTLVL
	  REMARK %	FIELD TEST 
	  NUMBER $A FTLVL
	 EI	:FTLVL
	 REMARK %----------------------------------------%

	 IF	ISIS2
	  IF	\NODE*\SLOT
	   REMARK %Node:	
	   NUMBER $8 NODE
	   REMARK %Slot:	
	   NUMBER $A SLOT
	  ELSE	\NODE
	   REMARK %Node:	
	   NUMBER $8 NODE
	   REMARK %Slot determined at start-up
	  ELSE	\SLOT
	   REMARK %Node determined at start-up
	   REMARK %Slot:	
	   NUMBER $A SLOT
	  ELSE
	   REMARK %Node and slot determined at start-up
	  EI
	 IF	\.AHOST
	  REMARK %Default Origination host:	
	  NUMBER $A .AHOST
	 EI	:\.AHOST
	 EI	:ISIS2

       IF       OUTDIA
        REMARK%%%OUTDIAL host: 
        NUMBER $A OTHOST
        REMARK% with  
        NUMBER $A NOPORT
        REMARK%  logical command ports
       EI


	 IF	PVC
	  REMARK %%PVC ports:	
	  NUMBER $A PVCCNT

	  IF	NMPVC
	   REMARK % MPVC ports:	
	   PRANGE(.MPORT,NMPVC)
	  EI	:NMPVC
	 EI	:PVC

	 IF	NAPORT
:	  ASYNC ports (and printer ports)
	  IF	NUMPRN
	   REMARK %%Printer ports:	
	   PRANGE(0,NUMPRN)
	  EI	:NUMPRN

	  REMARK %%ASYNC ports:	
	  PRANGE(0,NAPORT)


	  IF	CONFIG-1		:LEVEL 2
Q	   EQ	0
	   RE	NAPORT
P	   EQ	Q
	   SAY.P(P)
	   PVCSAY(P)
	   OHSAY(P)
	   DHSAY(P)
	   BSAY(.2HD,.2HD,	202-S,)
Q	   EQ	Q+1
	    ER
	    REMARK %
	  EI	:CONFIG-1
	 EI	:NAPORT


	 IF	NSPORT
	  REMARK %%SIO ports:	
	  PRANGE(.SPORT,NSPORT)

	  IF	CONFIG-1		:LEVEL 2
Q	   EQ	0
	   RE	NSPORT
P	    EQ	.SPORT+Q
	    SAY.P(Q)
	    PVCSAY(P)
	    OHSAY(P)
	    DHSAY(P)
Q	    EQ	Q+1
	   ER
	    REMARK %
	  EI	:CONFIG-1
	  IF	1-BAUDY
	   REMARK %   all are expected to be ASCII%
	  EI	:BAUDY
	 EI	:NSPORT



	 IF	CONFIG-3		:LEVEL 4
:	  Debugging options DCPSSW, CPS
	  IF	CPS!DCPSSW
	   REMARK %%   Debugging Options:
	   OPTION(0,DCPSSW)
	   OPTION(0,CPS)
	  EI
	 EI	:CONFIG-3



	 IF	ISIS2
	  IF	CONFIG-4		:LEVEL 5
:	   ISIS debugging options CRSTOP, RHIST, AHIST,
:				  SHIST, ASYERR, FORRUN,
:				  ORTIME, IRTIME, FREEZE
Q	   EQ	RHIST!AHIST!ASYERR!CRSTOP!FORRUN
Q	   EQ	Q!SHIST!ORTIME!IRTIME!FREEZE
	   IF	Q&0FFFF
	    REMARK %%   ISIS Debugging Options:
	    OPTION(0,FREEZE)
	    OPTION(0,CRSTOP)
	    OPTION(0,RHIST)
	    OPTION(0,AHIST)
	    OPTION(0,SHIST)
	    OPTION(0,ASYERR)
	    OPTION(0,FORRUN)
	    OPTION(0,ORTIME)
	    OPTION(0,IRTIME)
	   EI
	  EI	:CONFIG-4
	 EI	:ISIS2

	 IF	CONFIG-2		:LEVEL 3
:	  Assembly options ISIS2, NHNGBK
	  IF	(ISIS2!NHNGBK)&0FFFF
	   REMARK %%   Assembly Options:
	   OPTION(1,CONFIG)
	   OPTION(0,ISIS2)
	   OPTION(0,NHNGBK)
	  EI
	 EI	:CONFIG-2

	 REMARK %%----------------------------------------%%%%

	 KILL	PRANGE,PSET,SAY.P,BSAY,PVCSAY,OHSAY,DHSAY,OPTION
	EI	:CONFIG

:	Finally (unconditionally) generate warning about warnings and errors...
	.ABORT

