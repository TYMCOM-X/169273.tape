define !  = "COMMENT", !! = "COMMENT";
!! title Module Copy ;
! *************************************************************************
*************************************+*************************************
*                                                                         *
*                                                                         *
*				   Copy					  *
*                                                                         *
*                                                                         *
*************************************+*************************************


	Warning! this program is a HACK! it does strange things if you
	have AC license when specifying copy to other than yourself.


! -------------------------------------------------------------------------
			       Edit History
! -------------------------------------------------------------------------


    V14.1 -	(21-Apr-83: kmd)
		Telecopy's intolerance of spaces in system numbers
		  provoked an eradication of whitespace in Sys.List.
		Irrelevant material from Util.Req was excised as well.

    V14.0 -	(27-Sep-82: kmd)
		Massive cosmetic surgery,
		Addition of file-selection switches like:

		    "/today"
		    "/yesterday"
		    "/before <date>"
		    "/after <date>"

		      (where <date> is of the form
			DD-MMM-YY.HHMMSS
			(eg, 1-Jul-82.012536, meaning 1-July-1982
			01:25:36)),

		and the "/gateway" switch.

    V13 -	Changed to send "ONLC" for 370,940 transfers and to
		only include the "." in names if there really is an
		extension

    V12 -	Changed to allow the following syntax for system lists:

		    *,number,number,number...,number

		which will copy to all systems and then to those explicitly
		named.  The idea is to allow copying to systems not in
		JOBS.DAT.

    V11 -	Fixed to read (PJ)JOBS.DAT for list of Tymcom-X's

    V10 -	Cosmetic changes

    V9 -	Can now specify switches in File(s) spec as well as <esc>

    V8 -	If user has AC license, creates frame logged in as 
		"copy to" user name. (beware of hack)

    V7 -	Runs telecopy in subordinate fork.

    V6 -	Reads HOST.DAT(PJ) for list of 10s.

    V5 -	Adds system 27 to the default list.

    V4 -	Fixes bug where command files over 2566 chars would get
		truncated.

    V3 -	Fixes bug where files with 2 or less character extensions
		didn't get copied.

    V2 -	Fixes bug where TMPCOR could run out and where lc was not
		accepted.


**************************************************************************;


begin "Copy"
  require "Sail.Hdr" source!file;
  require "Util.Req" source!file;
  require "Date.Da" source!file;
  require (crlf & "[License required: RF JL]" & crlf) message;

  !version(!ver('14, '1), !edit('050), !date(4-May-1983));


! ------------------------------------------------------------------------;


  redefine
    $F.ASCII         = '0,
    $F.ASCII.LINE    = '1,
    $F.KATAKANA      = '2,
    $F.IMAGE         = '10,
    $F.IMAGE.BINARY  = '13,
    $F.BINARY        = '14,
    $F.DUMP.RECORDS  = '16,
    $F.DUMP          = '17;

      ! Tymcom-X Data Modes - borrowed from Files.Da;


! ------------------------------------------------------------------------;

  external integer
    rpgsw;

  integer
    I,
    X1,
    X2,
    X3,
    Eof,
    Fd;

  string
    Dir.File,
    Copy.From.User,
    Copy.To.User,
    Sys.List,
    Home.Sys,
    Sysnum,
    Line,
    File.Name;

  define
    !Frmop = ('044 lsh 27);


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


  preload!with
    "?NOH: No handle in slot",
    "?BDI: Unmatching universal id number",
    "?NRT: No rights to do operation",
    "?BDF: Bad frame number",
    "?NLC: Not enough license",
    "?UND: Undefined frame descriptor type",
    "?NCH: Fd said child but gave non-child frame number.",
    "?CLP: Can't lock context pages (fd2upx)",
    "?BAR: Bad access rights code",
    "?NPR: Orphan asked for its parent",
    "?BFD: Bad family dispatch type",
    "?BDH: Bad handle index number in fd",
    "?BFN: Bad function code to frmop",
    "?ADB: Address found bad (replicate failed) in .fo?va",
    "?IOE: Page i/o error in frmrvm/frmwvm",
    "?ALR: Attempt to start a running frame",
    "?ALH: Attempt to halt non-running frame",
    "?CSJ: Attempt to halt frame with jacct on",
    "?FHE: Handle descriptor expected, not given",
    "?FHU: Handle (index already) used",
    "?NFH: No handle indices left.",
    "?BCN: Bad count",
    "?BTN: Bad table number",
    "?BHT: Bad handle type",
    "?CCH: Can't create handle (access failure)",
    "?BSV: Bad start vector offset",
    null
  ;

  string array
    Frmop.Err [1:64];


! ************************************************************************;
!! title Rtn Init.Brk.Tbls ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Init.Brk.Tbls				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  integer
    Dot.Tbl,
    Dash.Tbl,
    Slash.Tbl,
    White.Tbl;


  procedure Init.Brk.Tbls;
  begin "Init.Brk.Tbls"
    setbreak(Dot.Tbl _ getbreak, ".", null, "ISN");
    setbreak(Dash.Tbl _ getbreak, "-", null, "ISN");
    setbreak(Slash.Tbl _ getbreak, "/", null, "ISN");
    setbreak(White.Tbl _ getbreak, (sp & ht), null, "XNR");
  end "Init.Brk.Tbls";

  require Init.Brk.Tbls initialization[0];


! ===================================+=====================================
==========================================================================;
!! title Rtn Scan.RPG.Time ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Scan.RPG.Time				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  integer procedure Scan.RPG.Time (string Str);
  begin "Scan.RPG.Time"
    integer
      Hours,
      Minutes,
      Seconds;


    Minutes _ Hours _ Seconds _ 0;

    if (length(Str) >= 2) then
    begin
      Hours _ cvd(Str[1 for 2]);
      Str  _ Str[3 to inf];

      if (length(Str) >= 2) then
      begin
	Minutes _ cvd(Str[1 for 2]);
	Str     _ Str[3 to inf];
      end;

      if (length(Str) >= 2) then
      begin
	Seconds _ cvd(Str[1 for 2]);
      end;
    end;

    return(60 * ((Hours * 60) + Minutes) + Seconds);
  end "Scan.RPG.Time";


! ===================================+=====================================
==========================================================================;
!! title Rtn Scan.RPG.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Scan.RPG.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  procedure Scan.RPG.Date (string Str;
      reference string Day, Month, Year);

  begin "Scan.RPG.Date"
    boolean
      Error;
    integer
      Brk;
    string
      Day.Str,
      Month.Str,
      Year.Str;


    clear(Error);
    Month.Str _ Year.Str _ null;
    Day.Str   _ scan(Str, Dash.Tbl, Brk);

    if (length(Day.Str)) then
    begin
      if (not Is.Numeric(Day.Str)) then
	set(Error)
      else
      begin
	if (Brk = "-") then
	begin
	  if (not length(Str)) then
	    set(Error)
	  else
	  begin
	    Month.Str _ scan(Str, Dash.Tbl, Brk);

	    if (Brk = "-") then
	    begin
	      if (not length(Str)) then
		set(Error)
	      else
	      begin
		Year.Str _ Str;

		if (not Is.Numeric(Year.Str)) then
		  set(Error);
	      end;
	    end;
	  end;
	end;
      end;
    end;

    if (not Error) then
    begin
      integer
	Tmp;


      Tmp _ Get.Date;

      if (not length(Day.Str)) then
	Day.Str _ CvS.Date(Tmp, DF1, DC.DAY);

      if (not length(Month.Str)) then
	Month.Str _ CvS.Date(Tmp, DF1, DC.MONTH);

      if (not length(Year.Str)) then
	Year.Str _ CvS.Date(Tmp, DF1, DC.YEAR);
    end;

    Day   _ Day.Str;
    Month _ Month.Str;
    Year  _ Year.Str;
  end "Scan.RPG.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Process.Date.Switch ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			  Rtn Process.Date.Switch			  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Translate "Today", "Yesterday", "After" and "Before" switches
	to DIRIT-format, and return null for others.


==========================================================================;


  string procedure Process.Date.Switch (string Str);
  begin "Process.Date.Switch"
    boolean
      After.Flag,
      Error;
    integer
      Brk,
      Date,
      Date.Out,
      Time;
    string
      Day,
      Month,
      OutStr,
      Str1,
      Year;


    if (Str = "/") then
      lop(Str);

    clear(After.Flag);
    Str1 _ Scan.Token(Str);

    if (SubStr(Str1, "Today")) then
    begin
      Date.Out _ Get.Date;
      set(After.Flag);
    end
    else if (SubStr(Str1, "Yesterday")) then
    begin
      Date.Out _ Get.Date - 1;
      set(After.Flag);
    end
    else if (SubStr(Str1, "Before") or SubStr(Str1, "After")) then
    begin
      if (SubStr(Str1, "After")) then
	set(After.Flag)
      else
	clear(After.Flag);

      scan(Str, White.Tbl, Brk);

	! Pick off switch token;

      Str1 _ scan(Str, Dot.Tbl, Brk);

      Time _
	  if (Brk = ".") then
	    Scan.RPG.Time(Str)
	  else
	    0;

      Scan.RPG.Date(Str1, Day, Month, Year);
      Date     _ CvI.Date(Day, Month, Year);
      Date.Out _ (Date land !Mask(14)) lor ((Time land !Mask(17)) lsh 14);
    end
    else
      return(null);

    return("/" & cvs(Date.Out) & 
	(if (After.Flag) then
	  ">"
	else
	  "<")
    );
  end "Process.Date.Switch";


! ===================================+=====================================
==========================================================================;
!! title Rtn Parse.Input.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			    Rtn Parse.Input.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Discriminate information destined for DIRIT from that
	pertinent to COPY.

	Str should be of the form:

	  Str            ->  <files> (<switch.list>) | <switch.list>
	  <files>        ->  <any bunch of characters not containing
			     "/">
	  <switch.list>  ->  <switch.list>,<switch> | <switch>
	  <switch>       ->  /MUMBLE


==========================================================================;


  procedure Parse.Input.Str (string Str;
      reference string Files, Switches);

  begin "Parse.Input.Str"
    integer
      Brk;
    string
      Tmp,
      Tmp1;


    Switches _ null;
    Files    _ scan(Str, Slash.Tbl, Brk);

    while (length(Str)) do
    begin
      Tmp  _ scan(Str, Slash.Tbl, Brk);
      Tmp1 _ Process.Date.Switch(Tmp);

      if (length(Tmp1)) then
	Files _ Files & Tmp1
      else
	Switches _ Switches & "/" & Tmp;
    end;
  end "Parse.Input.Str";


! ===================================+=====================================
==========================================================================;
!! title Rtn Frmopr ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Frmopr				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple integer procedure Frmopr (
      integer Fd;
      reference integer Arg;
      integer Fn;
      boolean Silent (false));

  begin "Frmopr"
    own integer
      Err;


    start!code
      move	1, Fd;
      hrl	1, Fn;
      setom	!skip!;
      !Frmop	1, Arg;
       setzm	!skip!;
      movem	1, Err;
    end;

    if (not (!skip! or Silent)) then
      print (
	  "?FRMOP '",     cvos(Fn),
	  " for Fd = '",  cvos(Fd),
	  ", Arg = '",    cvos(Arg),
	  ", Err = '",    cvos(Err), crlf,
	  Frmop.Err[Err], crlf
      );

    return(Err);
  end "Frmopr";


! ===================================+=====================================
==========================================================================;
!! title Rtn FrmopI ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn FrmopI				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple integer procedure FrmopI (integer Fd, Arg, Fn, S(false));
  begin "FrmopI"
    return(Frmopr(Fd, memory[Arg], Fn, S));
  end "FrmopI";


! ===================================+=====================================
==========================================================================;
!! title Rtn Frmop ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				 Rtn Frmop				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple integer procedure Frmop (integer Fd, Arg, Fn, S(false));
  begin "Frmop"
    return(Frmopr(Fd, Arg, Fn, S));
  end "Frmop";


! ===================================+=====================================
==========================================================================;
!! title Rtn CvPpn ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				 Rtn CvPpn				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  integer procedure CvPpn (string User);
  begin "CvPpn"
    integer
      Chan,
      Err;
    integer array
      X [0:3],
      U [0:1];


    X[0] _ 3;
    X[1] _ xwd(1, 1);
    X[2] _ location(U[0]);
    X[3] _ cvsix("UFD");
    U[0] _ cvsix(User[1 for 6]);
    U[1] _ cvsix(User[7 for 6]);

    open(Chan _ getchan, "DSK", $F.ASCII, 0, 0,  0, 0, Err);

    if (Err) then
      usererr(0, 0, "?Can't init disk");

    chnior(Chan, X[0], 4);

    if (not !skip!) then
      usererr(0, 0, "?No UFD for user " & User);

    release(Chan);
    return(X[2]);
  evPpn";


! ===================================+=====================================
==========================================================================;
!! title Rtn CreFrm ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn CreFrm				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  integer procedure CreFrm (string User(null));
  begin "CreFrm"
    integer
      Fd,
      I;
    integer array
      A [0:4];


    A[0] _ GetTab(-1, 2);		! JBTPPN;
    A[1] _ GetTab(-1, 6) lor !bit(1);	! JBTPRV + JP!NOA;
    A[2] _ GetTab(-1, -'23); 		! AUN;
    A[3] _ GetTab(-1, -'22); 		! UN1;
    A[4] _ GetTab(-1, -'21); 		! UN2;

    if (length(User)) then 
    begin
      A[2] _ CvPpn(User);
      A[3] _ cvsix(User[1 for 6]);
      A[4] _ cvsix(User[7 for 6]);
    end;

    I  _ xwd(-5, location(A[0]));
    Fd _ Frmopr(0, I, 1);		! Create frame;
    return(Fd);
  end "CreFrm";


! ===================================+=====================================
==========================================================================;
!! title Rtn RunFrm ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn RunFrm				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  procedure RunFrm (integer Fd; string Arg; integer Start.Inc(0));
  begin "RunFrm"
    integer array
      Ac [0:'17];
    label
      Ac!data, 
      St;


    calli(Fd, -'126);			! SetOTF;
    calli(!hl('206001), -'67);		! Give it 1 page;

    if (false) then
    quick!code
      Ac!data:
	0;0;0;0;0;0;0;0;		! [0:7];
	0;				! [10];

      St:
	calli	'10, '35;		! RUN uuo;
	jrst	4,   '13;		! HALT;
    end;

    arrblt(Ac[0], memory[location(Ac!data)], '20);
    Ac[0] _ cvsix("DSK");
    Ac[1] _ cvfil(Arg, Ac[2], Ac[4]);

    if (Ac[4] = 0) then
      Ac[4] _ GetTab(-1, 2);

    if (lh(Ac[4]) = 0) then 
    begin
      Ac[6] _ memory[Ac[4]];
      Ac[7] _ memory[Ac[4]+1];
      Ac[4] _ '6;
    end;

    Ac['10] _ Ac['10] lor !hl(Start.Inc);

    for I _ 0 step 1 until '17 do 
    begin
      integer array A[0:1];


      A[0] _ I;
      A[1] _ Ac[I];
      Frmopr(Fd, A[0], 3);
    end;

    Frmop(Fd, !hl(-1) lor (location(St) - location(Ac!data)), 4);
    Frmop(Fd, 0, '24);			! Graft to frm 0 (disown);
  end "RunFrm";


! ===================================+=====================================
==========================================================================;
!! title Rtn Complain ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Complain				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple procedure Complain (string Text);
  begin "Complain"
    print(Text);
    exit;
  end "Complain";


! ===================================+=====================================
==========================================================================;
!! title Rtn System.Number ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn System.Number				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple string procedure System.Number;
  begin "System.Number"
    return(cvs(GetTab('33, '11)));
  end "System.Number";


! ===================================+=====================================
==========================================================================;
!! title Rtn Do.Telecopy ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Do.Telecopy				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple procedure Do.Telecopy;
  begin "Do.Telecopy"
    RunFrm(Fd, "(SYS)TELECO", 1);

    if (!skip!) then
      print("[Telecopy started in frame ", Fd land '777,  "]");
  end "Do.Telecopy";


! ===================================+=====================================
==========================================================================;
!! title Rtn XXX ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				  Rtn XXX				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple string procedure XXX;
  begin "XXX"
    own string
      Fn;
    own integer
      A,
      B;

    getformat(A, B);
    setformat(-3, 0);
    Fn _ cvs(Fd land '777);
    setformat(A, B);
    return(Fn);
  end "XXX";


! ===================================+=====================================
==========================================================================;
!! title Rtn My.Name ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn My.Name				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Returns the name of the program currently being run.


==========================================================================;


  simple string procedure My.Name;
  begin "My.Name"
    define
      !Gtprg = 3,
      !Gtfpn = -'25;


    return(cv6str(GetTab(-1, !Gtprg)) &
	"[" & cvos(!lh(GetTab(-1, !Gtfpn))) &
	"," & cvos(!rh(GetTab(-1, !Gtfpn))) & "]"
    );
  end "My.Name";


! ===================================+=====================================
==========================================================================;
!! title Rtn Call.Dirit ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Call.Dirit				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Str = "DSK:XXXTEL.DIR_<file list><crlf>
	       COPY[-,-]!<crlf>"

	    where "COPY[-,-]" is the value of My.Name().


==========================================================================;


  simple procedure Call.Dirit (string Com.Str);
  begin "Call.Dirit"
    own integer
      I,
      Err;
    own string
      Str;
    own integer array
      Run.Block [0:5];


    Str _ Com.Str & "DSK:" & My.Name & "!" & crlf;
    tmpout("DIR", Str, Err);

    if (Err) then 
    begin
      own integer
	Chan;

      open(Chan, "DSK", $F.ASCII, 0, 5, 0, 0, 0);
      enter(Chan, XXX & "DIR.TMP", Err);

      if (Err) then
	Complain("?Can't create temp file");

      cprint(Chan, Str);
      close(Chan);
      release(Chan);
    end;

    Run.Block[0] _ cvsix("SYS");
    Run.Block[1] _ cvsix("DIRIT");
    I _ location(Run.Block[0]) + '1000000;
    calli(I, '35);			! "RUN";
    Complain("?Unable to run SYS:DIRIT");
  end "Call.Dirit";


! ===================================+=====================================
==========================================================================;
!! title Rtn Foo ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				  Rtn Foo				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  string procedure Foo (string Fox);
  begin "Foo"
    own integer
      Brk.Tbl,
      Chan,
      Err,
      Eof,
      Brk;
    string
      S,
      X;


    if (Fox[1 for 1] neq "@") then
      return(Fox & crlf);

    open(Chan _ getchan, "DSK", $F.ASCII, 5, 0, 256, Brk, Eof);
    lookup(Chan, Fox[2 to inf] & ".CMD", Err);

    if (Err) then
      complain("?Can not read command file");

    setbreak(Brk.Tbl _ getbreak, cr, lf, "INS");
    S _ input(Chan, Brk.Tbl);

    while (X _ input(Chan, Brk.Tbl)) do
      S _ S & "," & X;

    relbreak(Brk.Tbl);
    release(Chan);
    return(S & crlf);
  end "Foo";


! ===================================+=====================================
==========================================================================;
!! title Rtn Def.Sys.List ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Def.Sys.List				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Search (PJ)JOBS.DAT for a list of Tymcom-X Systems.  If it is
	not there, return a canned list.


==========================================================================;


  define
    Tymcom.X = !hl(512);


! ------------------------------------------------------------------------;


  string procedure Def.Sys.List;
  begin "Def.Sys.List"
    integer
      Chan,
      Eof,
      I;
    string
      S;


    S _ null;
    open(Chan _ getchan, "DSK", $F.BINARY, 2, 0,  0, 0, Eof);
    lookup(Chan, "(pj)jobs.dat", Eof);

    if (Eof) then
      return(
	  "23, 25, 26, 27, 28, 29, " & 
	  "30, 31, 32, 33, 34, 35, 36, 37, 38, 39, " &
	  "54, 55, 56, 57, " &
	  "65, " &
	  "70, 72, 74, 79, " &
	  "83, " &
	  "90, " &
	  "930"
      );

    for I _ 12 step -1 until 0 do
      wordin(Chan);			! skip stuff at front;

    while (not Eof) do 
    begin
      I _ wordin(Chan);

      if (I = -1) then
	done;

      if (I land Tymcom.X) then
	S _ S & "," &  cvs(rh(I));
    end;

    if (I neq -1) then
      usererr(I, 2, "? -1 expected at end of (PJ)JOBS.DAT");

    release(Chan);
    return(S[2 to inf]);
  end "Def.Sys.List";


! ===================================+=====================================
==========================================================================;
!! title Mainline Code;
! ***********************************+*************************************
*                                                                         *
*                                                                         *
*			       Mainline Code				  *
*                                                                         *
*                                                                         *
*************************************+************************************;


  begin "Mainline Code"
    boolean
      DefaultAll,
      Super;
    integer
      Brk,
      Brk.Tbl1,
      Brk.Tbl2,
      Chan1,
      Chan2,
      Gtwy.Number,
      Gtwy.Host;
    string
      Gtwy.Password,
      Switches,
      FrameName,
      Gtwy.User;


    ttyup(true);
    setformat(0, 0);
    Fd _ calli(0, '30);		! "PJOB";


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    if (not Rpgsw) then 
    begin
      integer
	EscFlg,
	Brk;
      string
	Files,
	Switches,
	Tmp;


      do.banner("Copy - A Telecopy Slave Driver");
      print(crlf);

      Tmp _ Prompt.User("File(s): ");
      EscFlg _ (!skip! = #esc) or (!skip! = #ctrl("c"));

      Parse.Input.Str(Tmp, Files, Switches);

      if (EscFlg) then
	print(crlf);

      setbreak(Brk.Tbl1 _ getbreak, "()", " ", "KINR");
      Files _ scan(Files, Brk.Tbl1, Brk);

      if (Brk = "(" or Brk = ")") then
	usererr(0, 0, "?User name not allowed here", "S");

      open(Chan1 _ getchan, "DSK", $F.ASCII, 0, 1, 0, 0, Brk);
      enter(Chan1, XXX & "COP.TMP", Brk);

      cprint(Chan1,
	  if (EscFlg) then
	    "/ESC"
	  else
	    null,
	  Switches
      );

      relbreak(Brk.Tbl1);
      release(Chan1);

      Call.Dirit("DSK:" & XXX & "TEL.DIR" & "_" & Foo(Files));
      usererr(0, 0, "?Failure running SYS:DIRIT", "X");
    end;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Rpgsw _ DefaultAll _ Super _ false;
    Gtwy.Number _ 0;
    open(Chan1 _ getchan, "DSK", $F.ASCII, 1, 0,  256, Brk, Brk);
    lookup(Chan1, XXX & "COP.TMP", Brk);

    if (not Brk) then
      Switches _ input(Chan1, 0);

    release(Chan1);
    setbreak(Brk.Tbl1 _ getbreak, "/", " ", "SINK");

    while (length(Switches)) do 
    begin
      string Sw.Str;


      Sw.Str _ scan(Switches, Brk.Tbl1, Brk);

      if (length(Sw.Str)) then
      begin
	if (equ(Sw.Str, "ESC")) then
	  DefaultAll _ true
	else if (Starts.With("Supersede", Sw.Str) or
	    Starts.With("Supercede", Sw.Str)) then
	  Super _ true
	else if (Starts.With("Gateway", Sw.Str)) then
	begin
	  define
	    SILENTLY = true;


	  Gtwy.Number    _ cvd(Prompt.User("Gateway number: "));
	  Gtwy.Host      _ cvd(Prompt.User("Host Number:    "));
	  Gtwy.User      _ Prompt.User("Username:       ");
	  Gtwy.Password  _ Prompt.User("Password:       ", SILENTLY);
	  print(crlf);
	end
	else
	  usererr(0, 0, "?Illegal switch: /" & Sw.Str, "X");
      end;
    end;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Copy.From.User _ Gfd.User.Name;
    Copy.To.User   _ null;

    if (Gtwy.Number) then
      Copy.To.User _ Gtwy.User;

    if (not (length(Copy.To.User) or DefaultAll)) then
      Copy.To.User _ Prompt.User(
	  "Copy to user name [" & Copy.From.User & "]: ");

    if (not length(Copy.To.User)) then 
    begin
      Copy.To.User _ Copy.From.User;
      print("[Copying to ", Copy.To.User, "]", crlf);
    end;

    FrameName _ null;

    if (not equ(Copy.To.User, Logged.In.User.Name)) then
      if (GetTab(-1, -'20) land !bit(9)) then
	FrameName _ Copy.To.User
      else if (not Gtwy.Number) then
	 print(#bel & "[No AC license]", crlf);

    if (length(FrameName)) then
      print("[Logging in to ", FrameName, "]", crlf);


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Sys.List _ null;

    if (Gtwy.Number) then
      Sys.List _ cvs(Gtwy.Host)
    else
    begin
      string
	DefStr;
      integer
	BrkTbl1,
	BrkChr;

      setbreak(BrkTbl1 _ getbreak, "", (sp & ht), "I");
      DefStr _ Def.Sys.List;
      DefStr _ scan(DefStr, BrkTbl1, BrkChr);

      if (not DefaultAll) then 
      begin
	Sys.List _ Prompt.User("Copy to system(s) [all]: ");
	Sys.List _ scan(Sys.List, BrkTbl1, BrkChr);

	if (Sys.List = "*") then 
	begin
	  Sys.List _ DefStr & Sys.List[2 to inf];
	  print("[Copying to systems ", Sys.List, "]" & crlf);
	end;

	if (equ(Sys.List, "10S")) then
	  Sys.List _ null;
      end;

      if (length(Sys.List) = 0) then
	print("[Copying to systems ", (Sys.List _ DefStr), "]", crlf);

      relbreak(BrkTbl1);
    end;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    if (not Super) then 
    begin
      string
	Tmp;


      Tmp   _ Prompt.User("Supersede existing files? [N]");
      Super _ length(Tmp) and Starts.With(Tmp, "Y");
    end;

    if (Super) then
      print("[Superseding existing files]", crlf);


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Dir.File _ XXX & "TEL.DIR";
    Fd       _ CreFrm(FrameName);

    if (not !skip!) then
      usererr(0, 0, "?Can't create frame", "X");

    open(Chan1 _ getchan, "DSK", $F.ASCII, 4, 0, 256, X2, Eof);
    lookup(Chan1, Dir.File, X1);
    close(Chan1);

    if (X1) then
      complain("?Can not lookup directory file" & Dir.File);

    open(Chan2 _ getchan, "DSK", $F.ASCII, 0, 2, X1, X2, X3);
    enter(Chan2, XXX & "TEL.COM", X1);

    if (X1) then
      complain("?Can not enter file " & XXX & "TEL.COM");

    if (Super) then
      cprint(Chan2, "REPLACE", crlf)
    else
      cprint(Chan2, "KEEP", crlf);

    cprint(Chan2, "NO UPDATE", crlf);
    cprint(Chan2, "ONLC", crlf);

    if (Gtwy.Number) then
    begin
      if (not length(Gtwy.User)) then
	Gtwy.User _ Copy.To.User;

      cprint(Chan2, "PASSWORD", crlf, Gtwy.Password, crlf);
      cprint(Chan2, "GATEWAY ", cvs(Gtwy.Number), ";", Gtwy.User, ":",
	  cvs(Gtwy.Host), crlf);
    end;

    Home.Sys _ System.Number;
    setbreak(Brk.Tbl1 _ getbreak, lf, cr, "I");
    setbreak(Brk.Tbl2 _ getbreak, ",", ":", "I");

    while (Sysnum _ scan(Sys.List, Brk.Tbl2, X1)) do 
    begin "Next System"
      lookup(Chan1, Dir.File, X1);

      Eof _ false;

      for I _ 1 step 1 until 5 do
	input(Chan1, Brk.Tbl1);

      while (not Eof) do 
      begin
	Line _ input(Chan1, Brk.Tbl1);
	File.Name _ Clean(Line[9 for 3]);

	if (length(File.Name)) then
	  File.Name _ "." & File.Name;

	File.Name _ Clean(Line[1 for 6]) & File.Name;

	if (Line > 32) then
	  cprint(Chan2, "(", Copy.From.User, ":", Home.Sys, ")", File.Name,
	      ",(", Copy.To.User, ":", Sysnum, ")", File.Name, crlf);
      end;

      close(Chan1);
    end "Next System";

    cprint(Chan2, "QUIT", crlf);
    close(Chan2);

    lookup(Chan1, Dir.File, X1);
    rename(Chan1, null, null, X1);

    release(Chan1);
    release(Chan2);

    relbreak(Brk.Tbl1);
    relbreak(Brk.Tbl2);


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Do.Telecopy;
    exit;
  end "Mainline Code";
end "Copy";


! ***********************************+*************************************
**************************************************************************;
    ~Esÿ