begin require "(SAILIB)SAIL.DEF" source!file;
define LIM = 1;
forlc ATT _ (
    PRP.DEST, OOR.DEST, OUR.DEST,
    INTER.LATA, OFC.ORIG, OFC.USER, RPOA, INTER.NET
) doc {
	evaldefine ATT = {(MASK land }&cvms(LIM)&{)};
	redefine LIM = LIM lsh 1;
} endc
define DISALLOWED = {(
    ( OOR.DEST or OUR.DEST ) and not PRP.DEST or
    ( PRP.DEST or OOR.DEST or OUR.DEST ) and ( INTER.NET or RPOA ) or
    INTER.NET and INTER.LATA    )};
define SC = {(
    (if PRP.DEST then 1 else 0) +
    (if OOR.DEST then 2 else 0) +
    (if OUR.DEST then 4 else 0) )};
integer MASK, NSIM;
setprint("SIMGEN.OUT","B");
for MASK := 0 upto (LIM-1) do if not DISALLOWED then
begin NSIM := NSIM + 1; print(
	"(",
	if INTER.LATA then " Inter-LATA call" else
	if INTER.NET then " Inter-network call" else " Call",
	if RPOA then " with RPOA" else null,
	if OFC.USER then " by Official user" else null,
	if OFC.ORIG then " from Official host" else null,
	if INTER.NET then "." else
	if RPOA then " to LATA 1." else " to SC"& cvs(SC) &" host.",
	" )"& crlf,
	"sim ",
	if OFC.ORIG then "SC7." else "SC0.",
	if INTER.LATA then "2" else "1",
	" ",
	if OFC.USER then "OUSER1" else "USER1",
	":",
	if RPOA then "-3002" else null,
	if INTER.NET then "-39990123456789" else "-300020122"& cvs(SC) &"1234",
	crlf );
end;
setprint(null,"T");
print( NSIM, " simulations" );
end $
