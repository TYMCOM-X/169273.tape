	SUBTTL	STATUS.LIB . . . .DRIVER TO PROCESS ARBITRARY COMMANDS UNDER "?STAT"
:******************************************************************
:
:	This program is the sole property and confidential
:	information of Tymshare, Inc., and may not be copied
:	in whole or in part or disclosed to any third party
:	without the prior written consent of Tymshare.
:
:*******************************************************************

    IF	  1-\DB.STA		:DEFAULT DEBUG/STATUS TO ON
DB.STA	EQ	1
    EI

  IF	DB.STA				:ASSEMBLE ONLY WITH DEBUG STATUS ON

	SEG	A.XDDT
	MO	.,STATUS
	RA	$00
	GL	DB.NTC

  IF	1-\DB.NTC
DB.NTC	EQ	0		: Default Network Console protocol to off
  EI

	GL	DSTART,DS.ELL,DS.ILL,DS.ERR,DS.DLM
	GL	DS.TOK,DS.NUM,DS.EXI,DS.QUI,DS.GCH,DS.CUC,DS.DEL
  IF	DB.NTC
	GL	DS.NXT,DS.CHK
  EI

:	STATUS - User Command Handler
:
:	Version history:
:	13.3  07/11/84  The following changes were made by Romolo Raffo.
:		Network Console protocol was added. This allowed Network
:		Console to communicate with Extended DDT Operations Managers.
:		The new features are controled by the assemble switch DB.NTC.
:		The default for this switch is off, so any program can use
:		this version of STATUS.LIB without change. Network Console
:		protocol is invoked by a command composed of four unprintable
:		characters. This is so hopefully no one will inadvertently
:		enter Network Console mode. If Network Console mode, the prompt
:		is no longer output, and the request for a "\" to be typed
:		after an error is removed. All commands received in this mode
:		begin with hex 8080 followed by a halfword id. The command
:		following this header is unchanged. The response begins with
:		a hex 8081 followed by the commands halfword id. The response
:		following this header is also unchanged, consisting of multiple
:		lines, each ending with a carriage return and a linefeed. After
:		the response, a trailer of hex 8082, the command id and an
:		empty line is sent. All of this is handle by STATUS.LIB,
:		without any change to the program using this package.
:
:		To use Network Console protocol, the following changes must be
:		made.
:			1) The variable DB.NTC must be set to one. This will
:			   cause the code to handle Network Console protocol
:			   to be assembled.
:			2) All jumps to label DSTART should be replaced by
:			   jumps to DS.NXT . The label DSTART is where DDT
:			   enters from the ?STAT command. It is now used to
:			   initialize the code to not using Network Console
:			   protocol.
:			3) Commands which have indefinite and possibly unending
:			   response, and need a way to abort the command,
:			   must call DS.CHK to check if the response should
:			   should end. If Network Console protocol, this call
:			   checks for a command end message from Network
:			   Console. Otherwise, it checks if any character has
:			   been entered. In either case, it does a normal
:			   return if no termination was received, and a skip-4
:			   return if termination was found.
:
:		Note that with these changes, users will still be able to use
:		the ?STAT command directly from DDT without any change.
:	13.2  01/27/81  <BAB>Added RADIX=0 at start of mod.  Added jump
:		following 'QUIT' SVC to allow DDT "P" (proceede) command
:		to return the user to ":>" prompt.
:	13.1  11/17/81	<BAB>Added DNEXTK support, DS.DLM (last delimeter),
:		and fixed backspace for > 4 character entry, added the
:		command thought to be invalid to invalid command message.
:	13	08/81	<JK>Maintenance assumed by BAB from John Kopf
:
:		The Status package is intended to provide an extension to DDT,
:	invokable via the "?STAT" command in DDT.  Once invoked you may re-
:	activate it (provided you have not issued any DDT "G" (go) commands)
:	by typing "P ".
:
:		There is limited editing control built into the package:
:	ESC	aborts back to command level,
:	^W	discards partial 'token' already input (word or number),
:	^A	discards last char of token (backspace).
:	^H	discards last char of token (backspace).

:		Delimiters may occur in multiple (e.g. ", " is OK).  Valid
:	delimiters are:
:	C/R	L/F	H/T	","	and BLANK.

:		Entry Points:
:	DSTART	Initialize STATUS.LIB on new circuit.
:	DS.NXT	Place to exit on completion of command.
:	DNEXTK  Get next token with out prompt.
:	DS.ILL	Place to exit on illegal command -- types "Illegal command"
:	DS.ERR	Place to exit on command error -- types "type '\' "
:	DS.EXI	Place to exit machine.
:	DS.QUI	Place to exit back to DDT.
:	DS.GCH	Get a character...Link on R1, char (with high-order bit forced)
:			in R0...normal return if detected ^W, skip-4 if detect
:			~A, aborts back to DS.NXT if detects ESC.
:	DS.DEL	Check for delimiter...Link on R2, Character in R0.  Skip-4
:			return unless this is a delimiter, then normal return.
:	DS.CUC	Convert to upper case...Link on R1, Character in R0.
:	DS.TOK	Get a string token...Link on R4, TOKEN returned in R3...
:			inputs up to 4 characters, returns them (in uppercase)
:			left justified, 0 fill
:	DS.NUM	Get a number...Link on R5, expects Radix in R4...
:			number returned in R0.  May ABORT if any character is
:			input other than valid digit or delimiter.
:	DS.CHK	Check if command termination entered...Link on R4. Skip-4
:			if termination entered, otherwise takes normal return.
:	DS.DLM  Location of last delimeter entered (1 byte)

:		There are two built-in commands:
:	Quit	exit back to DDT.
:	EXIt	exit machine.

:		additional commands may be defined via the DS.CMD macro:
:	 DS.CMD(TEXT,ADDR)
:	Where:	TEXT	The (UPPERCASE) target command string.
:		ADDR	The virtual address to transfer to, to service that
:				command.
:	DS.CMD generates a 12-byte discriptor for this command, at the point
:	assembly where it is invoked.  This discriptor is on a WORD boundary.



OUT	EQ	0B			:OUTPUT SVC
IN	EQ	0A			:INPUT SVC

DS.ELL	EQ	0			:NULL POINTER FOR END OF LINKED LIST

	SEG	A.DATA
DS.DLM	BS	1			:LAST DELIMETER ENCOUNTERED
  IF	DB.NTC
NC.REF	BS	1			: Nonzero if end of response sent
NC.PRO	BS	1			: Nonzero if using NTCN protocol
:	Force NC.ID data (two bytes after length) to begin on a halfword
:	boundry. This is needed for compares and stores from HDR.ID
	ORG .-1; BND 1; ORG .+1
NC.ID	SC	/00/			: Current NTCN command id
HDR.ID	HS	1			: Last message header id
  EI
R4SAV	WS	1			:SAVE AREA FOR R4 IN DS.TOK
BADSTR	SC	/'xxxx'"8D"8A/		:PLACE TO PUT BAD COMMAND
	SEG	A.XDDT

:	DEFINE ELEMENTS FOR COMMAND BLOCK
C.NEXT	EQ	0			:POINTER TO NEXT ENTRY
C.ADDR	EQ	C.NEXT+4		:POINTER TO COMMAND HANDLER
C.STR	EQ	C.ADDR+4		:RELATIVE POSITION OF STRING

:	DEFINE MACRO TO GENERATE THIS COMMAND BLOCK
DS.CMD	MACRO(STR,ADDR)[
   IF	DS.ELL
Q	WS	0
	ORG	DS.ELL
	WC	Q
	ORG	Q
   EI
DS.ELL	WC	0,ADDR
	AC	/STR/
	BND	4
]


:	TABLE OF COMMANDS, AND TARGET ROUTINES.
CMDLST	WS	0			:FORCE TO WORD BOUNDARY
	DS.CMD(Q,DS.QUI)		:EXIT BACK TO DDT
	DS.CMD(QUIT,DS.QUI)		:EXIT BACK TO DDT
	DS.CMD(EXIT,DS.EXI)		:EXIT 
	DS.CMD(EXI,DS.EXI)		:EXIT 
  IF	DB.NTC
	DS.CMD("80"82"84"86,DS.NTC)	: Enter Network Console Protocol
  EI

  IF	DB.NTC
:	Network Console Messages
BEGRSP	SC	/"80"81/
ENDRSP	SC	/"80"82/
CRLF	SC	/"8D"8A/
ERRMES	SC	/"80"84"00/
  EI


:	DIAGNOSTIC MESSAGES
TERR	SC	/Illegal command /
TTERR	SC	/type '\'"8D"8A/
PROMPT	SC	/"8D"8A:>/		:"CR|LF:>"
BKSPAC	SC	/"88 "88/		:BACK SPACE
	SUBTTL	COMMAND SCANNER, VERIFIER, AND BITCHER


:	COMMAND INPUT HANDLER
DOIT	L	R0,C.ADDR,R1		:GO SERVICE THIS COMMAND
	JALR	R0,R0

:	NEXT COMMAND
DSTART
  IF	DB.NTC
	LIS	R0,0
	STB	R0,NC.PRO,,		: Turn off Network Console protocol
  EI
DS.NXT
  IF	DB.NTC
	LB	R1,NC.PRO,,		: Get Network Console protocol flag
	JE	DSNXT3			: If zero, use normal protocol
	JAL	R3,NC.END		: Send end of response message
DSNXT1	JAL	R1,NC.GCH		: Get first character of message header
	CLHI	R0,80			: Is this a message header?
	JNBS	DSNXT1			: No, ignore all chars before header
	JAL	R2,NC.HDR		: Get a message header
	CLHI	R3,85			: Is it a command termination?
	JN	DSNXT2			: No, go try command begin
	LH	R3,HDR.ID,,		: Get id in message header
	CLH	R3,NC.ID+1,,		: Is it the same as in last command?
	JN	NCPER0			: No, go send error message.
	JAL	R1,NC.GCH		: Get first character of command header
	JAL	R2,NC.HDR		: Get rest of command header
DSNXT2	CLHI	R3,80			: Is it a command header?
	JN	NCPER1			: No, go send error message.
	LH	R3,HDR.ID,,		: Get id from command header
	STH	R3,NC.ID+1,,		: And save for response
	SVC	OUT,BEGRSP		: Send response header
	SVC	OUT,NC.ID,,		: Including command id
	LIS	R0,0			: Get false for response end flag
	STB	R0,NC.REF,,		: and set to response end needed
	J	DNEXTK			: Go get command token
DSNXT3
  EI
	SVC	OUT,PROMPT		:ISSUE PROMPT
DNEXTK	JAL	R4,DS.TOK		:GET COMMAND TOKEN
	LA	R1,CMDLST		:SEARCH FOR STRING MATCH
DSTAR1	CL	R3,C.STR,R1
	JE	DOIT			:PROCEED IF FIND A MATCH
	L	R1,C.NEXT,R1		:ELSE CONTINUE SCAN
	JGBS	DSTAR1
DS.ILL	SVC	OUT,TERR		:"ILLEGAL COMMAND"
	ST	R3,BADSTR+2,,		:"XXX"<CR>
	SVC	OUT,BADSTR,,
DS.ERR
  IF	DB.NTC
	LB	R1,NC.PRO,,		: Is Network Console protocol inuse?
	JN	DS.NXT			: Yes, don't bitch
  EI
	SVC	OUT,TTERR		:TELL USER TO TYPE FLUSH CHAR
BITCH	JAL	R1,DS.GCH		:FLUSH INPUT
	JBS	BITCH;	 NOPR	0			:^W...IGNOR
	JBS	BITCH;	 NOPR	0			:^A...IGNOR
	CLHI	R0,0DC			:READ UNTIL "\" IS FOUND
	JNBS	BITCH
	J	DS.NXT			:THEN EXIT


:	BUILT-IN COMMANDS

:		Command EXIt
:		===========-
DS.EXI	SVC	KIO,0			:DISCONNECT


:		Command Quit (exit back to DDT)
:		=========---
DS.QUI	SVC	KIO,1			:RETURN TO DDT
	J	DS.NXT			:IF OPERATOR TYPED "P", GET NEXT COMMAND

  IF	DB.NTC
:		Command for entering Network Console protocol mode
:		==================================================
DS.NTC	LIS	R1,1			: Get nonzero value for protocol flag
	STB	R1,NC.PRO,,		: Store in flag
	STB	R1,NC.REF,,		: Also set response end seen flag
	J	DS.NXT			: Go start Network Console command
  EI
	SUBTTL	U T I L I T I E S

:	GET A COMMAND TOKEN...UP TO 4 CHARS LONG
:		SKIPS OVER EXTRA CHARACTERS TO NEXT DELIMITER
:	LINK ON R4
:	TOKEN RETURNED IN R3, LEFT JUSTIFIED
DS.TOK	HS	0
	ST	R4,R4SAV,,		:SAVE R4
TOKEN0	JAL	R1,DS.GCH		:GET NEXT CHAR
	JBS	TOKEN0;	 NOPR	0			:^W...IGNORE
	JBS	TOKEN0;	 NOPR	0			:^A...IGNORE
	JAL	R2,DS.DEL		:SKIP OVER DELIMITERS
	J	TOKEN0
	JAL	R1,DS.CUC		:CONVERT TO UPPER CASE
	CHI	R0,00DC			:= "\" ?
	JE	DS.NXT			:YES, PRINT PROMPT, GET NEXT COMMAND
	LBR	R3,R0			:SET UP STRING ACCUMULATOR (000000CC)
	LIS	R4,1			:1 CHAR. FOUND SO FAR
TOKEN	JAL	R1,DS.GCH		:GET ANOTHER CHAR
	J	TOKEN0			:^W...START OVER
	J	TOKEN3			:^A...TOSS LAST CHAR
	JAL	R1,DS.CUC		:CONVERT TO UPPER CASE
	JAL	R2,DS.DEL		:CHECK FOR DELIMITER
	J	TOKEN1			:GOT ONE...SKIP TO LEFT JUSTIFY IT
	CHI	R0,00DC			:= "\" ?
	JE	DS.ILL			:YES, TREAT AS ILLEGAL
	AIS	R4,1			:ONE MORE CHARACTER
	CHI	R4,4			:IF => 4 CHARS, DO NOT USE THIS ONE
	JG	TOKEN			:YES...SCAN FOR DELIMITER
	SLLS	R3,8			:NO...ACCUMULATE CHARS
	STBR	R0,R3
	J	TOKEN			:AND GET ANOTHER CHAR
TOKEN1	HS	0
	L	R4,R4SAV,,		:RESTORE R4
TOKEN2	TI	R3,0FF000000		:HAVE WE 4 CHARS ALREADY?
	JNR	R4			:YES...RETURN TOKEN IN R3
	SLLS	R3,8			:NO...JUSTIFY IT
	JBS	TOKEN2			:AND REPEAT

TOKEN3	HS	0			:HANDLE BACKSPACE IN TOKEN
	SIS	R4,1			:ONE LESS CHAR
	CHI	R4,4			:IF AFTER BACKSPACE TOKEN IS => 4
	JGE	TOKEN			: JUST DECRIMENT COUNTER
	SRLS	R3,8			:ELSE... DROP 1 CHARACTER
	J	TOKEN			:AND GET NEXT CHAR.

:	UTILITY ROUTINE TO READ A NUMBER
:	USES R0-R3
:	EXPECTS RADIX IN R4
:	LINK ON R5
:	ABORTS IF FIRST NON-DELIMITER IS NOT DIGIT
:	ELSE RETURNS NUMBER IN R0
DS.NUM	JAL	R1,DS.GCH		:GET NEXT CHAR
	JBS	DS.NUM;	 NOPR	0			:^W...IGNORE
	JBS	DS.NUM;	 NOPR	0			:^A...IGNORE
	JAL	R2,DS.DEL		:CHECK FOR DELIMITER CHAR
	J	DS.NUM
	JAL	R2,DS.DIG		:TEST IF CHAR IS DIGIT
	J	DS.ILL			:NOT A DIGIT - GIVE ERROR MESSAGE
	CLR	R0,R4
	JGE	DS.ILL			:DIGIT IS BIGGER THAN RADIX...ABORT
	LR	R3,R0			:PUT FIRST DIGIT INTO ACCUMULATOR
NUMBER	JAL	R1,DS.GCH		:READ NEXT CHAR
	J	DS.NUM			:^W...START OVER
	J	DIVIDE			:^A...TOSS LAST DIGIT
	JAL	R2,DS.DEL		:CHECK FOR DELIMITER
	LR	R0,R3;	 JR	R5	  :YES
	JAL	R2,DS.DIG		:SKIP IF DIGIT
	J	DS.ILL			:NO...ABORT
	CLR	R0,R4
	JGE	DS.ILL			:RADIX ERROR...BITCH
	MR	R2,R4			:BUILD NUMBER
	AR	R3,R0			:ADD NEW DIGIT
	J	NUMBER			:GET NEXT DIGIT
DIVIDE	LIS	R2,0			:GOT A ^A...MAKE DOUBLE-PRECISION ARG.
	DR	R2,R4			:QUOTENT INTO R3, WHERE WE WANT IT
	J	NUMBER			:NOW GO BACK FOR MORE DIGITS

:	DIGIT TEST
:	CHECK CHARACTER READ...SKIP RETURN IF DIGIT
:	THEN R0=BINARY VALUE OF HEX DIGIT
:	USES R1, LINK ON R2
DS.DIG	JAL	R1,DS.CUC		:CONVERT TO UPPER CASE
	LR	R1,R0			:COPY CHAR
	SHI	R1,0B0			:IS IT "0"?
	JLR	R2			:RETURN IF LESS THAN "0"
	SIS	R1,0B9-0B0		:IS IF "9"
	JLEFS	DIGIT			:LESS THAN "9" THEN A GOOD DIGIT
	SIS	R1,0C1-0B9		:IS IT "A"?
	JLR	R2			:RETURN IF LESS THAN "A"
	SIS	R1,0C6-0C1		:IS IF "F"?
	JGR	R2			:RETURN IF GREATER THAN "F"
	SIS	R0,7			:0A - "A" (PARTIAL CONVERSION)
DIGIT	SHI	R0,0B0			:"0"
	J	4,R2			:SKIP RETURN

:	CHECK FOR DELIMITER
:	LINK	R2
:	USES	R0,R1
:	SKIP-4 RETURN UNLESS (R0) IS A DELIMITER CHARACTER
DS.DEL	LHI	R1,DLEN-1		:NUMBER OF DELIMITER CHARS
	STB	R0,DS.DLM,,		:SAVE DELIMITER
DELIM	CLB	R0,DLIST,R1		:COMPARE TO A DELIMITER
	JER	R2			:RETURN IF DELIMITER
	SIS	R1,1			:CHECK NEXT DELIMITER
	JGEBS	DELIM			:LOOP IF ANY MORE DELIMITERS
	J	4,R2			:SKIP RETURN
:	LIST OF DELIMITER CHARS
DLIST	AC	' ,'			:BLANK,","
	BC	8D,8A,89		:CR,LF,HT
DLEN	EQ	.-DLIST			:NUMBER OF DELIMITER CHARS

:	INPUT A CHARACTER
:	LINK	R1
:	NORMAL RETURN:  ^W INPUT...FLUSH ENTRY
:	SKIP-4 RETURN:  ^A or ^H INPUT...FLUSH LAST CHAR
:	SKIP-8 RETURN: CHAR IN R0 (HIGH-BIT ON)
:	ABORTS TO COMMAND SCANNER ON "ESC"
DS.GCH	SVC	IN,0F			:GET THE CHAR
	J	DS.GCH
	OHI	R0,80			:SET HIGH-ORDER BIT
	CLHI	R0,9B			:CHECK FOR ESCAPE
	 JE	DS.NXT			:ABORT, PROCEED WITH PROMPT IF SO
	CLHI	R0,97			:CHECK FOR ^W
	 JER	R1			:NORMAL RETURN
	CLHI	R0,81			:CHECK FOR ^A
	 JE	4,R1			:SKIP-4 RETURN
	CLHI	R0,88			:CHECK FOR ^H
	 JN	8,R1			:NO, SKIP-8 RETURN, CHAR IN R0

	SVC	OUT,BKSPAC		:BACK SPACE
	J	4,R1			:SKIP-4 RETURN FOR ^H

:	CONVERT CHAR IN (R0) TO UPPER CASE
:	LINK ON R1
DS.CUC	CLHI	R0,0E1			:COMPARE WITH "a"
	JLR	R1			:EXIT IF IT CAN'T BE LOWERCASE
	CLHI	R0,0FA			:COMPARE WITH "z"
	JGR	R1			:NOT LOWER CASE...JUST RETURN
	SHI	R0,20			:LOWER CASE...CONVERT TO UPPER
	JR	R1			:AND RETURN

  IF	DB.NTC
:	Check for command termination
:	Link	R4
:	Uses	R0, R1, R2, R3
:	Normal return:	Termination not found
:	Skip-4 return:	Command termination received
DS.CHK
	LB	R0,NC.PRO,,		: Is Network Console mode active?
	JN	DSCHK1			: Yes, go check for message.
	SVC	IN,00			: No, just check for any character.
	JR	R4; NOPR		: Command termination not found
	J	4,R4			: Command termination found
DSCHK1	SVC	IN,00			: Get first character of header
	JR	R4; NOPR		: Command termination header not found
	JAL	R4,NC.HDR		: Get a message header
	CLHI	R3,85			: Is it a command termination?
	JN	NCPER2			: No, go send error message.
	LH	R3,HDR.ID,,		: Get header id
	CLH	R3,NC.ID+1,,		: Is it the correct id?
	JN	NCPER0			: No, go send error message.
	J	4,R4			: Yes, command termination found

:	Read a Network Console message header. First character passed in R0.
:	First byte must be a hex 80.
:	Link	R2
:	Uses	R0, R1, R3
:	Returns	second byte of header in R3 and message id in HDR.ID.
NC.HDR	CLHI	R0,80			: Does message header begin with 80?
	JN	NCPER3			: No, go send error message.
	JAL	R1,NC.GCH		: Get second character of header.
	LR	R3,R0			: Save in R3
	JAL	R1,NC.GCH		: Get first character of id
	STB	R0,HDR.ID,,		: and save
	JAL	R1,NC.GCH		: Get second byte of header
	STB	R0,HDR.ID+1,,		: and also save it
	JR	R2

:	Input a character. Wait until one is received. Return character in R0.
:	Link	R1
NC.GCH	SVC	IN,0F			: Get next character of header
	J	NC.GCH			: Loop until received
	JR	R1

:	Send an protocol error message to Network Console
:
:	Code	Meaning
:	----	-------
:	0	The id in a command end message (type 85) does not match the
:		id in the command message.
:	1	Expected a command message and did not receive one.
:	2	Received a message other than command end in DS.CHK call.
:		A call to DS.CHK should only be done for commands which
:		need termination, i.e. which have not sent end of response.
:		These commands can only receive command end.
:	3	Message header does not begin with a hex 80.
:
NCPER0	LIS	R0,0			: Error code = 0
	J	NC.ERR			: Go send error message
NCPER1	LIS	R0,1			: Error code = 1
	J	NC.ERR			: Go send error message
NCPER2	LIS	R0,2			: Error code = 2
	J	NC.ERR			: Go send error message
NCPER3	LIS	R0,3			: Error code = 3
	J	NC.ERR			: Go send error message

NC.ERR	JAL	R3,NC.END		: Send response end, if needed
	SVC	OUT,ERRMES		: Send error message header
	SVC	KIO,0E			: Output error code in R0
	J	DS.NXT			: Return to command loop

:	Send end of response message, if not already sent
:	and set flag that it has been sent.
:	Link	R3
:	Uses	R1
NC.END	LB	R1,NC.REF,,		: Get response end sent flag
	JNR	R3			: No need to send, already sent
	SVC	OUT,CRLF		: Insure response has at least one line
	SVC	OUT,ENDRSP		: End previous response
	SVC	OUT,NC.ID,,		: Include command id
	SVC	OUT,CRLF		: and blank line
	LIS	R1,1			: Get true for response end sent
	STB	R1,NC.REF,,		: and set flag
	JR	R3

  EI

	EM

  EI	:(DB.STA)
  