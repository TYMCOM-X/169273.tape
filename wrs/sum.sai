begin
require "(SAILIB)SAIL.DEF" source!file;

integer C,B,E;
integer TLt,NBt,ASt,NB,AS;

setbreak(1,#lf&#ff&"]",#cr,"IS");
open(C_getchan,"DSK",0,16,0,80,B,E);
lookup(C,"MLIST.RPT",E);
if E then usererr(0,0,"Lookup failure: MLIST.RPT","X");
setprint("MLIST.SUM","B");
AS := NB := TLt := ASt := NBt := 0;
print( "
	group		tot	as	nb

" );
while not E do begin
    string S;
    S := input(C,1);
    if B = "]" then begin
	if S = "[" then begin
	    string X;
	    X := S;
	    if equ(X[inf-5 to inf]," hits ") then
		while length(X) do
		    if lop(X)=":" then begin
			lop(X);
			print( #ht,
			    S_S[3 to inf-length(X)-2],
			    #ht&#ht[length(S)%8+1 to inf],
			    cvd(X),#ht,
			    AS,#ht,
			    NB,crlf );
			ASt := ASt + AS;
			NBt := NBt + NB;
			TLt := TLt + cvd(X);
		    end;
	    AS := NB := 0;
	end;
    end
    else begin
	while length(S) do begin
	    case lop(S) of begin
		["n"] if lop(S)="b" then NB := NB + 1;
		["a"] if lop(S)="s" then AS := AS + 1;
		[else]
	    end;
	end;
    end;
end;
print( crlf&
    #ht&"total"&#ht&#ht,
    TLt,#ht,
    ASt,#ht,
    NBt,crlf );

end $