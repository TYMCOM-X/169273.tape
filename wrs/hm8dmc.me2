procedure OUTSTANDING
begin

  field
    BALANCE same as END.BAL

  report to "*"
    top.margin 5
    bottom.margin 5
    page.top @ff
    heading.size 11
    page.heading
      print
	"Account:           ",trim(ACCT.NAME),
				" [",trim(ACCT.CODE),"]",
				@tab to 70,PAGE.NUM,@cr,
        "Account number:    ",trim(ACCT.NO),@cr,
        "Bank or creditor:  ",trim(BANK.NAME),@cr,
        "Branch:            ",trim(BANK.BRANCH),@cr,
	"Address:           ",trim(BANK.ADDR),@cr,@cr,
        "Transactions outstanding ",
        TODAY as date "WWW MMM DD, YYYY",@cr,@cr,@cr,
        " DATE",@tab to 13,
        "NUMBER",@tab to 23,
        "VENDOR",@tab to 51,
        "  AMOUNT",@tab to 61,
        " BALANCE",@cr,@cr
    group.heading on ACCT.CODE
      print page.top

  prompt "Account code (or <cr>): "
  accept ACCT.CODE
  move upper.case(ACCT.CODE) to ACCT.CODE
  if ACCT.CODE = "ALL" then move default to ACCT.CODE

  for each ACCOUNT where (
	ACCT.CODE of OUTSTANDING = default or ACCT.CODE)
  write report begin
    for each STATEMENT via ACCT.CODE
      move END.BAL to BALANCE
    print @tab to 61,BALANCE,@cr
    for each TRANS
     via ACCT.CODE,
     where STMT.DATE = default
     sorted on ACCT.CODE,TRAN.DATE,AMOUNT
     begin
      subtract AMOUNT from BALANCE
      print
        @tab to 01,TRAN.DATE,
	@tab to 13,NUMBER,
	@tab to 23,VENDOR,
	@tab to 51,AMOUNT,
	@tab to 61,BALANCE,
	@cr
      end
    end
  end
