;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	GTCHAR	- read character from circuit			 25-Oct-83 WRS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	INTEGER	port#,charvar,timelim
;	CALL	GTCHAR(port#,charvar,timelim)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	port# is an INTEGER containing the aux circuit port number, or
;	-1 for the job's command port.
;
;	charvar is an INTEGER variable in which to return the character or
;	an error code:
;		charvar .GE. 0		charvar is a received 8 bit character
;		charvar .EQ. -1		no character or time out
;		charvar .EQ. -2		no circuit (zapped)
;
;	timelim is an INTEGER constant or variable indicating the time in
;	seconds to wait for a character.  0 means to wait for ever.  This
;	argument may be omited in which case it will not wait at all (-1
;	returned unless character is already waiting in buffer).
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

        ENTRY GTCHAR

.AXI8S==1
.AXLDE==11
.GTLIN==-27
.GTJOB==-26

GTCHAR: 0
GTCHA0:	HRLZ	6,@0(16)	;get the port number
	HRRI	6,.AXI8S
	AUXCAL	6,5		;if chr rdy then put in 5 and skip
	 JRST	GTCHA2		;go decide why no character
GTCHA1:	MOVEM	5,@1(16)	;return character (or error code)
        JRA	16,2(16)
GTCHA2:	HRLO	6,@0(16)	;port # in left half
	JUMPL	6,GTCHA3	;check for command port reference
                             ;check for aux port zapped
	PJOB	5,		;job # in 5
	HRRI	6,.GTJOB	;get job who owns port
	GETTAB	6,
	 JRST	RETM2
	CAMN	5,6		;owned by this job?
	 JRST	GTCHA4
RETM2:	HRROI	5,-2		;return -2 (port zapped)
	JRST	GTCHA1
GTCHA3:                     ;check for TTY port zapped
	HRROI	6,.GTLIN
	GETTAB	6,		;get sixbit TTY name (lh = 0 if detached)
	 JRST	RETM2
	JUMPGE	6,RETM2		;(sixbit TTYxxx is negative)
GTCHA4:	                    ;simulate input wait - reflect yellow balls
	HRLZ	6,@0(16)	;get port number
	HRRI	6,.AXLDE	;leave deferred echo mode
	AUXCAL	6,
                            ;check for 3rd argument
	LDB	5,[point 9,2(16),8]
	CAIE	5,320		;opcode F4 uses for arguments (JUMP)
	 JRST	RETM1		;no time specified - return -1
                            ;do hiber for character ready
	HRLZI	6,11		;wait for character in seconds
	SKIPL	5,@0(16)	;get port number - skip if TTY:
	 TLO	6,100		;set aux circuit flag
	DPB	5,[point 9,6,8]	;setup port number field
	HRR	6,@2(16)	;setup time limit field
	HIBER	6,		;wait for character or such
	 JRST	GTCHA0
	JRST	GTCHA0		;start all over again
RETM1:	HRROI	5,-1		;return -1 to say time out - no char
	JRST	GTCHA1	 

        END
