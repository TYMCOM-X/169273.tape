comment

Name:		PASSWORD

Abstract:	Driver for NCUD1 master user directory server to allow
		Tymcom-X users to change their network passwords.

Summary:	1. builds circuit to NCUD1:1 server
		1a. checks user running program against MUD
		1b. checks user running program's password
		1c. submits audit trail remark containing user and job
		2. gets usernames from terminal or TMPCOR
		3. checks usernames in LUD for authorization
		4. gets password from terminal
		5. hash password
		6. send password change request to server
		7. clean up and exit

;
begin	"PASSWORD"

require 13&10&"
    required license: AC
    required protection: ALL RUN RUN
" message;

require "(SAILIB)SAIL.DEF" source!file;
require "(MPL)UUOSYM.SAI" source!file;

require '43 version;		! program version number;

define	TRYLIM = 4;		! number of retrys for CREAUX;
define	TIMLIM = 120;		! CUD input timeout in seconds;
redefine calli!FLEXIT = -'142;	! Fail Exit calli;

comment	Illegal characters are:
    <null> and <rubout>       -- ignored,
    <cr> and <semi-colon>     -- terminators,
    <'33-'37> and <'173-'176> -- escapes;

define	IllegalChars = {0&13&";"};
forc ILLCHR _ '33 stepc 1 untilc '37 doc {
    redefine IllegalChars = cvms(IllegalChars)&{&ILLCHR};
} endc
forc ILLCHR _ '173 stepc 1 untilc '177 doc {
    redefine IllegalChars = cvms(IllegalChars)&{&ILLCHR};
} endc

boolean BUBBNET;		! true if in Bubbnet;
boolean	CUDTRACE;		! print circuit traffic if true (debug);
integer	CUDport;		! port number for circuit to CUD slave;
integer	SuccessCnt;		! count of passwords successfully changed;
integer	FailureCnt;		! count of passwords not changed;
boolean	AS,JL;			! license flags;
string	MYNAME;			! user's name;
integer	MYGAN;			! user's global account;
record!class UN (		! descriptor for user name list item;
	string	UNAME;		! user name;
	r!p(UN)	LINK );		! link to next username;
record!pointer (UN) UNL;	! user name list;
integer	UNAMEN;			! number of target user names on list;
string	PASSWORD;		! the chosen password;
external boolean RPGSW;		! called at start+1 - look for TMPCOR;

preload!with 
	"TYMSHARE", "TYMNET", "PASSWORD", "WELCOME";
string array TRITE[1:4];	! list of trite passwords;

define	PWPAR# = { 1, "?request partially completed" };
define	PWCIR# = { 2, "?unable to build circuit to CUD - try later" };
define	PWBSY# = { 3, "?CUD file busy - try later" };
define	PWCUD# = { 4, "?unexpected CUD error" };
define	PWINV# = { 5, "?user name not valid " };
define	PWPRV# = { 6, "?Account Supervisor license required " };
define	PWGAN# = { 7, "?user not in your Global Account " };
define	PWTIM# = { 8, "?timeout waiting for CUD reply" };
define	PWZAP# = { 9, "?CUD circuit zapped" };
define	PWHND# = { 10,"?CUD handshake failure" };
define	PWSYN# = { 11,"?lost sync with CUD slave" };
define	PWLOC# = { 12,"?program not available to local users" };
define	PWVAL# = { 13,"?validation exception for user " };
define	PWNIL# = { 14,"?no user names specified " };
define	PWTMP# = { 15,"?unable to read DSK:nnnPAS.TMP" };
define	PWAUX# = { 16,"?program may not be run from aux circuit" };
define	PWPTY# = { 17,"?program may not be run from PTY" };
define	PWBAD# = { 18,"?password not correct for user " };
define	PWNTV# = { 19,"?command not implemented by NETVAL: " };

procedure EXIT;
comment
	Normal exit from program.
;
start!code
	uuo!EXIT;
end;

procedure CLEANUP;
comment
	Clean things up, zap circuit to slave, etc.
;
begin	"CLEANUP"
    calli( -1, calli!ZAPCIR );	! zap all aux circuits;
end	"CLEANUP";

procedure PrintStatistics;
comment
	Tell user how many passwords he changed.
;
if FailureCnt or SuccessCnt > 1 then begin	"PrintStatistics"
    case SuccessCnt of begin
	[0] print( "[no passwords changed]",crlf );
	[1] print( "[1 password changed]",crlf );
	else print( "[",SuccessCnt," passwords changed]",crlf )
    end;
    case FailureCnt of begin
	[0] ;
	[1] print( "%1 password not changed",crlf );
	else print( "%",FailureCnt," passwords not changed",crlf )
    end;
end	"PrintStatistics";

procedure FATAL(
	integer COD;		! code to return;
	string MSG );		! message to output;
comment
	Call upon encountering a fatal error.  Will clean up and stop the
	frame with appropriate message and return code.  The return code
	may be read by a parent frame with !FOHST.  Error codes are defined
	in the table above.  Note that if an error occurs when more than one
	username is being changed, and at least one username has been 
	successfully changed, the caller does not know which usernames were
	actually changed.  To indicate this condition, the error code
	returned to the caller is set to 1: "?partially completed request",
	and the original error code indicating the actual error is lost.
;
begin	"FATAL"
    procedure F1( integer I; string S );
    if SuccessCnt and FailureCnt and COD neq I then begin
	print( MSG, crlf );
	COD := I;
	MSG := S;
    end;
    PrintStatistics;		! print how many (if any) succeeded;
    CLEANUP;			! zap circuit, etc;
    F1( PWPAR# );		! check for partial request completed;
    print( MSG, crlf );		! print error message;
    calli( COD, calli!FLEXIT );	! return error code, and exit;
    EXIT;			! for pre-FEXIT monitors (or in case of CONT);
end	"FATAL";

procedure CHECK!ORIGIN;
comment
	Check origin of terminal - reject AUX and PTY.
;
begin
    integer L;
    L := calli( !xwd( -1,!GTLOG ), calli!GETTAB );
	! get supervisor data for command port;
    if 0 = L land ( LG!ND1 lor LG!ND2 ) then 
	FATAL( PWPTY# );	! node = 0, must be PTY;
    if 0 = L land LG!TID then
	FATAL( PWAUX# );	! TID = 0, must be AUX;
end;

string procedure TMP!READ!DEL(
	string	NAM );		! name of tempcore file;
comment
	Read and delete TMP:NAM or DSK:jjjNAM.TMP.
;
begin
    integer C,B,E;
    string S,F;
    integer array T[0:1];
    S := tmpin( NAM, E );	! SAIL function;
    if E then begin "DISK"
	F := cvs(1000+calli(0,calli!PJOB))[2 to 4]& NAM& ".TMP";
	open( C_getchan, "DSK",1,4,0, 256,B,E );
	lookup( C,F,E );
	if E then FATAL( PWTMP# );
	S _ null;
	do S _ S & input(C,0) until E;	! read whole file into string;
	rename( C,null,0,E );		! delete disk file;
	release( C );
    end "DISK"
    else begin "CORE"
	T[0] _ Cvsix( NAM );		! setup to delete core file;
	T[1] _ 0;
	calli( !xwd( !TCRDF, location(T[0]) ), calli!TMPCOR );
    end "CORE";
    return( S );
end;

procedure CREAUX;
comment
	Build an aux circuit to the CUD system.  Retry up to TRYLIM
	times on error.  Print error message if appropriate.
;
begin	"CREAUX"
    integer I,TRY;
    preload!with cvasc("NCUD1"),cvasc(":1;");
    own integer array X[0:1];
    for TRY := 1 upto TRYLIM do begin "TRY"
	CUDport := I := calli( location(X[0]), calli!CREAUX );
	if !SKIP! then return;
	print( "%unable to build circuit to CUD system: " );
	case !lh(I) of begin
	    [CXSUP#] case !rh(I) of begin
		[CXSFE#] print( "format error" );
		[CXSBU#] print( "user not in MUD" );
		[CXSBM#] print( "bad MUD" );
		[CXSHU#] begin print( "host unavailable"&crlf ); done end;
		[CXSDF#] print( "downline request failure" );
		[CXSTO#] print( "timeout" );
		[CXSAB#] print( "access barred" );
		[CXSIS#] print( "error in ISIS" );
		[CXSLQ#] print( "long queue in supervisor" );
		else print( "'",cvos(I) )
	    end;
	    [CX2AX#] begin print( "AC lic required (cnt)"&crlf ); done end;
	    [CXLOG#] begin print( "AC lic required (name)"&crlf ); done end;
	    [CXDCB#] print( "no room in monitor" );
	    [CXNRR#] print( "sup did not respond (req)" );
	    [CXNRL#] print( "sup did not respond (login)" );
	    [CXNCH#] print( "sup did not supply circuit" );
	    [CXERR#] print( "sup error from request" );
	    else print( "'",cvos(I) )
	end;
	print( crlf );
	if TRY < TRYLIM then begin
	    print( "%retrying...please wait...",crlf );
	    calli( TRY*10, calli!SLEEP );
	end;
    end "TRY";    
    FATAL( PWCIR# );
end	"CREAUX";

integer procedure CUD!IN;
comment
	Read a single 8 bit byte from the CUD slave.  FATAL
	if no traffic for TIMLIM seconds.
;
begin	"CUD!IN"
    integer C;
    calli( (CUDport lsh 27) + !hl('111) + TIMLIM, calli!HIBER );
    if !SKIP! then begin
	C := auxclv( CUDport,0,!AXI8S );
	if CUDTRACE then print( ">",cvos(C),crlf );
    	if !SKIP! then return( C );
	FATAL( PWTIM# );	! timeout;
    end;
    FATAL( PWZAP# );		! circuit zapped;
end	"CUD!IN";

procedure CUD!OUT(
	integer CHAR );		! character to send;
comment
	Send a single 8 bit byte to the CUD slave.
;
begin	"CUD!OUT"
    auxclv( CUDport, CHAR, !AXO8 );
    if CUDTRACE then print( "<",cvos(CHAR),crlf );
end	"CUD!OUT";

integer procedure GET!WORD( 
	integer BYTE!CNT );	! word size in bytes;
comment
	Read BYTE!CNT 8 bit bytes from the CUD slave and return
	the resulting word.  As per CUD protocol, the most signifigant
	byte is received first.
;
begin
    integer I,J;
    I := 0;
    for J := 1 upto BYTE!CNT do
	I := (I lsh 8) lor CUD!IN;
    return( I );
end;

procedure SEND!WORD(
	integer BYTE!CNT;	! number of bytes;
	integer WORD );		! word;
comment
	Send BYTE!CNT 8 bit bytes from WORD to CUD slave via circuit.
	As per CUD protocol, most signifigant bytes are sent first.
;
begin
    integer I;
    for I := 1-BYTE!CNT upto 0 do
	CUD!OUT( WORD lsh (I*8) land '377 );
end;

integer procedure CIPHER(
	string PASSWORD );	! password to be ciphered;
comment
	Hashes a password passed as its argument into a 32 bit
	value as per supervisor password hash algorithm.
;
begin "CIPHER"
    integer A,B,I,CH;
    define mask!32 = '37777777777;
    A := B := 0;
    while CH := lop(PASSWORD) do begin "NEXT-CHARACTER"
	for I := 1 step 1 until 7 do begin "NEXT-PASS"
	    B := A + B + CH;
	    A := A + B;
	    if A land '100 then A := A + CH;
	    A := ((A lsh 3) land mask!32) + ((A lsh -29) land 7);
	end "NEXT-PASS";
    end "NEXT-CHARACTER";
    A := A xor B;
    return( A land mask!32 );
end "CIPHER";

procedure ADD!USER( 
	string	UNAME );	! user name to add to user name list;
comment
	Add the given user name to the user name list, checking first
	to make sure it isn't already there.
;
begin
    record!pointer (UN) U1,U2;
    U1 := UNL;
    while U1 do begin
	if equ( UN:UNAME[u1], UNAME ) then begin
	    print( "%user ",UNAME," already on list",crlf );
	    return;
	end;
	U2 := U1;
	U1 := UN:LINK[u1];
    end;
    U1 := new!record(UN);
    if U2
	then UN:LINK[u2] := U1
	else UNL := U1;
    UN:UNAME[u1] := UNAME;
    UNAMEN := UNAMEN +1;
end;	

procedure HAND!SHAKE;
begin
    integer I;
    for I:=1 step 1 until 4 do CUD!OUT( I );
    while CUD!IN neq 1 do;
    if CUD!IN neq 2 or CUD!IN neq 3 or CUD!IN neq 4 then
	FATAL( PWHND# );
end;

procedure TEST!SYNC;
begin
    if CUD!IN neq '40 then
	FATAL( PWSYN# );
end;

procedure SEND!UserName( string UNAME );
begin
    while length(UNAME) do begin
	if length(UNAME) > 4
	    then CUD!OUT( lop(UNAME) )
	    else CUD!OUT( lop(UNAME)+'200 );
	CUD!OUT( lop(UNAME) );
	CUD!OUT( lop(UNAME) );
	CUD!OUT( lop(UNAME) );
    end;
end;

integer procedure GET!AUN(
	string UNAME );		! user name;
comment
	Lookup the specified user name in the MUD and return its AUN.
	If the user is not valid, return -1.
;
begin
    integer I,J,GAN,UUN;
    CUD!OUT( 10 );		! read MUD data command;
    SEND!UserName( UNAME );	! user name;
    CUD!OUT( '40 );		! end of command;
    case I := CUD!IN of begin	! check completion code;
	[1] begin		! function complete;
	    GAN := GET!WORD(4) land '777777;	! GAN;
	    UUN := GET!WORD(4) land '777777;	! UUN;
	    GET!WORD(2);	! CPARW;
	    GET!WORD(1);	! options;
	    GET!WORD(2);	! district;
	    if not BUBBNET then GET!WORD(1);	! IRC;
	    while( '377 neq I := GET!WORD(1) ) do begin "classes"
		GET!WORD(2);	! class code;
		for J := 1 upto I do
		    GET!WORD(2);! group or host code;
	    end "classes";
	    GET!WORD(3);	! password change date;
	end;
	[2] GAN := UUN := -1;	! not valid;
	[4][10] FATAL( PWBSY# );
	[6] FATAL( PWNTV# & "GET_MUD_DATA (10)" );
	[else] begin
	    print( "?code '",cvos(I)," from CUD slave",crlf );
	    FATAL( PWCUD# );
	end
    end;
    TEST!SYNC;
    return( !xwd( GAN, UUN ) );
end;

procedure CHANGE!PASSWORD (
	string UNAME;		! user name;
	string PASSWORD );	! password;
begin
    integer I;
    CUD!OUT( 30 );		! change password command;
    SEND!UserName( UNAME );
    SEND!WORD( 4, CIPHER( PASSWORD ));
    CUD!OUT( '40 );		! end of command;
    case I := CUD!IN of begin	! check completion code;
	[1] begin
	    print( "[password changed for user ",UNAME,"]",crlf );
	    SuccessCnt := SuccessCnt +1;
	    FailureCnt := FailureCnt -1;
	end;
	[4][10] FATAL( PWBSY# );
	[6] FATAL( PWNTV# & "CHANGE_PASSWORD (30)" );
	[else] begin
	    print( "?code '",cvos(I)," from CUD slave",crlf );
	    FATAL( PWCUD# );
	end
    end;
    TEST!SYNC;			! are we in sync?;
end;

procedure CHANGE!PASSWORDS;
comment
	Change the password for all users in the user name list.
;
begin
    record!pointer(UN) U;
    FailureCnt := UNAMEN;	! length of list;
    U := UNL;			! head of list;
    while U do begin
	CHANGE!PASSWORD( UN:UNAME[u], PASSWORD );
	U := UN:LINK[u];
    end;
end;

procedure GET!MYDATA;
comment
	Check to see that current user is properly validated by
	comparing his login AUN / username combination with the MUD.
;
begin
    integer LAUN,MAUN,LPRV,LLIC;
    MYNAME := cv6str( calli( !xwd( -1,!GTUNM ), calli!GETTAB ) ) &
	cv6str( calli( !xwd( -1,!GTUN1 ), calli!GETTAB ) );
    LAUN := calli( !xwd( -1,!GTAUN ), calli!GETTAB );
    LPRV := calli( !xwd( -1,!GTPRV ), calli!GETTAB );
    LLIC := calli( !xwd( -1,!GTLIC ), calli!GETTAB );
    MAUN := GET!AUN( MYNAME );
    if MAUN < 0 then
	FATAL( PWLOC# );	! not available to local users;
    if MAUN neq LAUN then
	if BUBBNET then begin
	    print( "WARNING: user ", MYNAME, " is improperly validated:"& crlf&
		"   MUD AUN = ", cvos(!lh(MAUN)),",",cvos(!rh(MAUN)), crlf&
		"   LUD AUN = ", cvos(!lh(LAUN)),",",cvos(!rh(LAUN)), crlf&
		"Please contact BUBBNET validations to have this corrected."
		&crlf& "[using AUN from MUD]"& crlf& crlf );
	    LAUN := MAUN;
	end
	else
	    FATAL( PWVAL# & MYNAME );! validation exception for user ;
    MYGAN := !lh(LAUN);		! global account number;
    AS := LPRV land JP!ASV;	! Account Supervisor license;
    JL := LLIC land LC!JL;	! Jacct-Login license;
end;

procedure SEND!REMARK;
comment
	Send audit trail information to slave.
;
begin
    integer I;
    string Remark;
    Remark := "User: "& MYNAME& " job: "& cvs(calli(0,calli!PJOB));
    CUD!OUT( 13 );		! remark command;
    CUD!OUT( length(Remark) );
    while length(Remark) do CUD!OUT( lop(Remark) );
    CUD!OUT( '40 );		! end of command;
    case I := CUD!IN of begin	! check completion code;
	[1] ;
	[6] FATAL( PWNTV# & "SET_OPLOG_REMARK (13)" );
	[else] begin
	    print( "?code '",cvos(I)," from CUD slave",crlf );
	    FATAL( PWCUD# );
	end
    end;
    TEST!SYNC;			! are we in sync?;
end;

procedure GET!NAMES;
comment
	Input names from terminal, check as they go.
;
begin
    string UNAME;
    integer GAN,AUN;
    if not ( AS or JL ) then begin "my-name-only"
	print( "[changing password for user ",MYNAME,"]"&crlf );
	UNL := new!record(UN);
	UNAMEN := 1;
	UN:UNAME[unl] := MYNAME;
	return;
    end "my-name-only";
    print( "Enter user names, one per line, extra return at end:",crlf );
    do begin "NextUser"
	print( "User name ",UNAMEN+1,": " );
	UNAME := inchwl;
	if length(UNAME)= 0 then done;
	if UNAME = "?" then begin "help"
	    print( "Enter user name to change password of.
You may change the password of " );
	    if JL then print( "any valid user [JL]." )
	    else if AS then print( "any user in your global account [AS]." )
	    else print( "user ",MYNAME," only." );
	    print( "
After all names have been entered, enter an extra RETURN.
" );	    continue;
	end;
	AUN := GET!AUN( UNAME );
	GAN := !lh(AUN);
	if AUN < 0 then begin
	    print( "?user ",UNAME," not in MUD",crlf );
	    continue;
	end;
	if GAN neq MYGAN and not JL then begin
	    print( "?user ",UNAME," not in your global account",crlf);
	    continue;
	end;
	ADD!USER( UNAME );
    end until false;
    if UNAMEN = 0 then FATAL( PWNIL# );	! no names specified;
    print( crlf );
end;

integer UNIbrk;			! break table for token scans;
boolean CCLSW;			! true if rescan CCL available;
string CCLARG;			! CCL argument string;
procedure CCLini;
begin
    setbreak( UNIbrk_getbreak," ,"&#ht&#lf," "&#ht&#cr&#lf,"TINK" );
    begin!code
	SETO 1,;
	uuo!GETLCH 1;
	AND 1,[ LC!ECS ];	! see if rescan available;
	MOVEM 1,CCLSW;
    end;
    if CCLSW then begin
	integer B;
	string S;
	backup;			! backup to command line;
	CCLARG := inchwl;	! read in command line;
	CCLSW :=		! set true if arguments specified;
	    equ( scan( CCLARG, UNIbrk, B )[1 to 6], "PASSWO" ) 
	    and length( scan( S_CCLARG, UNIbrk, B ) );
    end;	
end;
require CCLini initialization;

procedure SCAN!NAMES( string S );
comment
	Scan names from list in string.
;
begin
    string UNAME;
    integer GAN,AUN,B;
    record!pointer(UN) U;
    while length(S) do begin "NextUser"
	UNAME := scan( S,UNIbrk,B );
	if length(UNAME)= 0 then continue;
	AUN := GET!AUN( UNAME );
	GAN := !lh(AUN);
	if AUN < 0 then
	    FATAL( PWINV# & UNAME );	! not in MUD;
	if not JL and GAN neq MYGAN then
	    FATAL( PWGAN# & UNAME );	! not in GAN;
	if not JL and not AS and not equ( UNAME, MYNAME ) then
	    FATAL( PWPRV# & UNAME );	! AS license required;
	ADD!USER( UNAME );
    end;
    U := UNL;			! head of user name list;
    case UNAMEN of begin
	[0] FATAL( PWNIL# );	! no names specified;
	[1] print( "[changing password for user ",UN:UNAME[u],"]" );
	[else] begin
	    string S;	
	    S := "[changing password for users ";
	    while U do begin
		if length(S) > 50 then begin
		    print( S,crlf );
		    S := null;
		end;
		S := S & UN:UNAME[u];
		U := UN:LINK[u];
		if U then S := S & ", ";
	    end;
	    print( S, "]", crlf	end
    end;
    print( crlf );
end;

integer OKAYbrk,NONSPACEbrk,ILLEGALbrk;
procedure OKAYbrkINI;
begin
    setbreak( OKAYbrk_getbreak, "ABCDEFGHIJKLMNOPQRSTUVWXYZ", null, "XS" );
    setbreak( NONSPACEbrk_getbreak, " ", null, "XS" );
    setbreak( ILLEGALbrk_getbreak, IllegalChars, null, "IS" );
end;
require OKAYbrkINI initialization;

boolean procedure OKAY( 
	string	PASSWORD );	! password to check;
comment
	Return true if given password meets security restrictions.
;
begin
    boolean OK;			! set if problem detected;
    OK := true;			! innocent until proven guilty;
!
	RULE 1: must not resemble user name
;
    begin "RULE1"
	record!pointer(UN) U;	! temp;
	integer B;
	string S1,S2;
	S1 := null;
	S2 := PASSWORD;
	do			! S1 = alpha portion of password;
	    S1 := S1 & scan( S2,OKAYbrk,B )
	until length( S2 ) = 0;
	U := UNL;		! head of user name list to follow;
	while U do begin
	    if equ( UN:UNAME[u],S1 ) or equ( UN:UNAME[u],PASSWORD ) then begin
		print( "?password closely resembles user name (",
		    UN:UNAME[u], ")"& crlf );
		OK := false;
		done;
	    end;
	    U := UN:LINK[u];
	end;
    end "RULE1";
!
	RULE 2: must be at least 7 characters
;
    begin "RULE2"
	if length( PASSWORD ) < 7 then begin
	    print( "?password less than 7 characters"& crlf );
	    OK := false;
	end;
    end "RULE2";
!
	RULE 3: must contain embeded non-alphabetic
;
    begin "RULE3"
	integer B;
	string S1,S2;
	S2 := PASSWORD;
	lop( S2 );		! don't count the first character;
	S1 := scan( S2,OKAYbrk,B );
	if B=0 or length(S2)=0 then begin
	    print( "?password lacks an embedded non-alphabetic"& crlf );
	    OK := false;
	end;
    end "RULE3";
!
	RULE 4: must not be trite or obvious
;
    begin "RULE4"
	integer I,B;
	string S1,S2;
	S2 := PASSWORD;
	scan( S2,NONSPACEbrk,B );
	if not B then begin
	    print( "?password is trivial (blanks)"&crlf );
	    OK := false;
	end;
	S1 := null;
	S2 := PASSWORD;
	do
	    S1 := S1 & scan( S2,OKAYbrk,B )
	until length( S2 ) = 0;
	for I := arrinfo( TRITE, 1 ) upto arrinfo( TRITE, 2 ) do
	    if equ( S1, TRITE[i] ) then begin
		print( "?password is trite (",S1,")"&crlf );
		OK := false;
	    end;
    end "RULE4";
!
	RULE 5: password must not contain illegal characters.
;
    begin "RULE5"
	integer B;
	string S;
	S := PASSWORD;
	scan( S,IllegalBrk,B );
	if B then begin
	    print( "?Illegal character in password (",
		if B < 32 then "^"&(B+64) else """"&B&"""",
		")"&crlf );
	    OK := false;
	end;
    end "RULE5";
    return( OK );
end;
boolean HDX;
simple procedure HDXini;
begin!code
	SETO	1,;
	uuo!GETLCH 1;
	AND	1,[ LC!HDX+LC!LCP ];
	MOVEM	1,HDX;		! true if TTY is half duplex or local copy;
end;
require HDXini initialization;

string procedure ReadPassword;
begin
    string S;
    if HDX then print( crlf&
	"XXXXXXXXXXXXXXX"&#cr&
	"WWWWWWWWWWWWWWW"&#cr&
	"OOOOOOOOOOOOOOO"&#cr&
	"QMEUXTDHEGSOKGR"&#cr );
    S := inchwl;
    while !SKIP! neq #cr do
	S := S & !SKIP! & inchwl;
    if equ( S,"?" ) then print( "?" );
    print( crlf );
    return( S );
end;

procedure CHECK!OLD!PASSWORD;
comment
	Get old password from user, hash it and check with CUD to make
	sure it matches.  Prevents unauthorized user from walking up to
	a terminal and changing the user's password.
;
begin
    integer I;
    string OldPassword;
    auxcli( -1,IO!NEC,!AXCFS );		! turn off echo;
    print( "Enter current password for user ", MyName, ": " );
    OldPassword := ReadPassword;
    auxcli( -1,0,!AXCFS );		! turn on echo;
    CUD!OUT( 31 );			! check password command;
    SEND!UserName( MyName );
    SEND!WORD( 4, CIPHER( OldPassword ));
    CUD!OUT( '40 );			! end of command;
    case I := CUD!IN of begin		! check completion code;
	[1] ;
	[4][10] FATAL( PWBSY# );
	[6] FATAL( PWNTV# & "CHECK_OLD_PASSWORD (31)" );
	[18] FATAL( PWBAD# & MyName );
	[else] begin
	    print( "?code '",cvos(I)," from CUD slave",crlf );
	    FATAL( PWCUD# );
	end
    end;
    TEST!SYNC;				! are we in sync?;
end;

procedure GET!PASSWORD;
comment
	Obtain password from terminal, and verify its legality.
;
begin
    label AGAIN;
    auxcli( -1,IO!NEC,!AXCFS );		! turn off echo;
AGAIN:
    print( "Enter password: " );
    PASSWORD := ReadPassword;
    if equ( PASSWORD, "?" ) then begin
	print( "Enter new password (it will not echo).
In order to provide a minimal level of security, the password:
  [1] must not resemble the user name
  [2] must be at least 7 characters in length
  [3] must contain at least 1 embedded number or punctuation character
  [4] must not be trite or obvious
"& crlf );
	goto AGAIN;
    end;
    if not OKAY( PASSWORD ) then begin
	print( "Type a ? for password rules."& crlf&crlf );
	goto AGAIN;
    end;
    print( "Enter it again: " );
    if not equ( PASSWORD, ReadPassword ) then begin
	print( "?passwords do not agree"& crlf );
	goto AGAIN;
    end;
    auxcli( -1,0,!AXCFS );	! turn on echo;
end;

begin "MAIN"

    ttyup( true );		! convert lower case input to upper;
    ifcr compiler!switch("B") thenc usererr(0,0,0,"B"); endc ! bail?;

    BUBBNET := cvstr(calli(!xwd(!CNFG0,!GTCNF),calli!GETTAB)) = "B";
    if CCLARG = "?" or not CCLSW then print (
	"PASSWORD version ", cvos(memory['137]), crlf&
	"change ",
	if BUBBNET then "BUBBNET" else "TYMNET",
	" network password"& crlf& crlf );
    if CCLSW and CCLARG = "?" then begin "help"
	print( "    PASSWORD <usernames>"&crlf );
	calli( 0,calli!EXIT );
    end "help";
    CHECK!ORIGIN;		! must not be AUX or PTY;
    CREAUX;			! connect with CUD slave;
    CUD!OUT( #cr );		! satisfy project code prompt;
    HAND!SHAKE;			! enter protocol with CUD slave;
    GET!MYDATA;			! get information about our user;
    CHECK!OLD!PASSWORD;		! verify old password;
    SEND!REMARK;		! send username and job number;
    if CCLSW then SCAN!NAMES( CCLARG )
    else if RPGSW then SCAN!NAMES( TMP!READ!DEL( "PAS" ))
    else GET!NAMES;		! prompt from terminal;
    GET!PASSWORD;
    CHANGE!PASSWORDS;
    if FailureCnt then		! return error code if any failures;
	FATAL( PWPAR# );
    PrintStatistics;		! indicate number of successes;
    CLEANUP;			! shutdown slave, etc;
    EXIT;			! terminate execution;

end "MAIN";
end "PASSWORD";
$
    @Ì