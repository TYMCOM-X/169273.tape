title	Cphr1
entry	Cphr1
intern	Cphr1

W1=	0
W2=	1
W3=	2
W4=	3
W5=	4

SI=	5

A=	6
B=	7
C=	10
D=	11

PTR=	13
CNT=	14

P=	17
SP=	16


seed0:	27657537
seed1:	55555555
magicM:	6065
magicD:	37777761

Cphr1:	pop 	SP,ptr		; get string byte pointer
	pop 	SP,cnt		; get string length
	hrrzs 	cnt		; clear bits in lh of count
	dmove 	W1,seed0	; initialize working buffer
	dmove 	W3,W1
	move 	W5,W4
	setz 	SI,		; initialize working index

smush:	movei 	C,^D21		; right from SUP code at label CPHR1
rn:	move 	A,W1(SI)
	lsh 	A,^D36-^D24	; 24 bit -> 36 bit sign extend
	ash 	A,^D24-^D36
	mul 	A,magicM
	div 	A,magicD
	add 	B,W2(SI)
	ldb 	D,[point 2,B,35]
	exch 	B,W1(SI)
	movem 	B,W2(SI)
	exch 	W1,W5
	move 	SI,D
	sojg 	C,rn		; loop 21 times per character
	sojl 	cnt,done	; any more chars?
	ildb 	A,ptr		; get next char
	subi 	A,40		; subtract '40 (940 internal code?)
	andi 	A,177		; strip extra bits
	addm 	A,W1		; add in to workng buffer
	jrst 	smush		; go back with next character
done:	add 	W1,W2		; out of characters
	move 	1,W1		; SAIL gets value in AC1
	tlz	1,740000	; return only low order 32 bits
	popj 	p,		; be off

end
