procedure PERTS
begin
  field P as "6C"
  source INPERT from "TERMINAL" free form
    input P,conditional on upper.case(P) begin
      "Q" or "QUIT": @cr
      "E" or "ENTER": PERT.ID,STARTING,PERIOD,
        ACCT.CODE,NUMBER,DESC,VENDOR,AMOUNT,BUDGET.ID
      "L" or "LIST": @cr
      "D" or "DELETE": PERT.ID
      end
  type "Enter, List, Delete, Quit",@cr
  INPERT.LOOP: for each INPERT begin
    move upper.case(P) to P
    move upper.case(PERT.ID) to PERT.ID
    move upper.case(ACCT.CODE) to ACCT.CODE
    move upper.case(NUMBER) to NUMBER
    move upper.case(DESC) to DESC
    move upper.case(VENDOR) to VENDOR
    move upper.case(BUDGET.ID) to BUDGET.ID
    if P="Q" or "QUIT" then finish INPERT.LOOP
    conditional on P begin
      "E" or "ENTER": select PERT via key
        then alter PERT
        else insert into PERT
      "L" or "LIST": for each PERT
        type trim(PERT.ID),",",STARTING,",",PERIOD,",",
          trim(ACCT.CODE),",",trim(NUMBER),",",trim(DESC),",",
          trim(VENDOR),",",AMOUNT,",",trim(BUDGET.ID),@cr
      "D" or "DELETE": for each PERT
        where (PERT.ID of INPERT = "*" or PERT.ID)
        delete from PERT via key
      end
    end
  end
