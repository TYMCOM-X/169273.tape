begin "SECURE"

comment			SECURE 1.0  4/5/87  Bill Soley

This program is setup as an INIT program for a user name that is to run
only one dedicated application program and is not to be allowed to access
the monitor command level.  It also sets up some desirable terminal
characteristics such as ^S/^Q enable and such.  Lower case input is not
enabled for the benefit of FORTRAN progams.  The program enforces that
the two bits
	JP.LOS - logout on stop
and	JP.RCM - restricted command mode
are set in the LUD and outputs a panic message to the CTY: if they are
not.  These two bits cause the job to be logged out immediately if the
program that is running terminates for any reason what-so-ever.  Their
function in that respect overlaps to some degree.

To setup a username for this function requires special privilege and
must be performed by a member of the Tymcom-X operating systems group.
The SECURE program must be stored in the same directory as the application
to be run.  The application program name must be the first 6 characters
of the user name used to access the application.  For example, the user
name INFORMATION may be setup to run the program (MKTGINFO)INFORM and so
the SECURE program must be stored in (MKTGINFO) as well.  The SECURE program
must be installed with the following license:
	OP	Operator
This allows it to report probems to the operator console terminal.

;
require "(SAILIB)SAIL.DEF" source!file;
require 1 version;

define	Rev!XON 	= {auxclv(-1,1,'55)},
	No!Rev!XON 	= {auxclv(-1,0,'55)},
	BackSpace	= {auxclv(-1,1,'57)},
	No!BackSpace	= {auxclv(-1,0,'57)},
	BSD		= {auxclv(-1,1,'77)},
	no!BSD		= {auxclv(-1,0,'77)},
	Erase		= {auxclv(-1,1,'62)},
	No!Erase	= {auxclv(-1,0,'62)},
 	Tab 		= {setlch(-1,1,14)},
	No!Tab		= {setlch(-1,0,14)},
	Form		= {setlch(-1,1,17)},
	No!Form		= {setlch(-1,0,17)},
	Width (x) 	= {auxclv(-1,x,'27)},
	Line!Width (x) 	= {auxclv(-1,x,'27)},
	Wrap 		= {setlch(-1,0,10)},
	No!Wrap 	= {setlch(-1,1,10)};

define gettab(a,b) = {calli(!xwd(a,b),'41)};
define calli!SETUUO = '75;

simple string procedure user;
return(cv6str(gettab(-1,-'22))&cv6str(gettab(-1,-'21)));

simple procedure SetLch (integer Port,Value,BitNo(-1));
begin "SetLch"
    own integer T,PTR;
    if BitNo < 0 
	then PTR _ point(36,T,35)
	else PTR _ point(1,T,BitNo);
    start!code
    	define GETLCH = '051300000000;
	define SETLCH = '051340000000;
    	MOVE 1,port;
    	MOVEM 1,T;
    	GETLCH T;
    	MOVE 1,value;
    	DPB 1,PTR;
    	SETLCH T;
    end;
end "SetLch";

procedure Run (
	string 	File; 
	integer Start(0) );
begin "run"
    integer array RNB[0:5];
    RNB[1] := cvfil(File, RNB[2], RNB[4]);
    RNB[0] := cvsix("DSK");
    RNB[3] := RNB[5] := 0;
    if RNB[4] = 0 then RNB[4] := gettab(-1,-'25);
    calli ((Start lsh 18) + location(RNB[0]), '35);
end "run";

define jp!LOS = !bit(0);
define jp!RCM = !bit(26);
define !gtPRV = 6;
define calli!SETPRV = -'11;

integer PRV;

calli( 
    jp!LOS lor
    jp!RCM lor
    (PRV := gettab(-1,!gtPRV)), calli!SETPRV );
if PRV land jp!LOS = 0 or PRV land jp!RCM = 0 then begin
	string MSG;
	integer I;
	MSG := "


-------------------------------------------------------------------------
ATTENTION!  Call Tymcom-X hotline to report security problem.  ATTENTION!
-------------------------------------------------------------------------
        The username "& USER& " requires the following bits to be
        set in the LUD privilege word:
                bit 0    JP.LOS    logout on stop
                bit 26   JP.RCM    restricted command mode
        One or both of these bits are not set and this results in
        a security risk to this system.
-------------------------------------------------------------------------
ATTENTION!  Call Tymcom-X hotline to report security problem.  ATTENTION!
-------------------------------------------------------------------------


";
		calli( location(I:=lop(MSG) lsh 29), calli!SETUUO );
end;

Rev!XON;				! enable ^S/^Q from terminal;
backspace;				! backspace on erase character;
erase;					! erase with bs-sp-bs;
bsd;					! backspace as delete character;
form;					! don't simulate form feeds;
width(80);				! length of line=80;
wrap;					! wrap long lines;

Run( USER );		! default directory is the one this file is in;
print( "
The TYMNET ", USER, " Service is not available at this time.
" );
Run( "(SYS)LOGOUT" );

end $
