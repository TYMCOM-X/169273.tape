entry
    Pos;
begin	"TAB"

require "(SAILIB)SAIL.DEF" source!file;
!get!definitions(GOGTAB);
external integer array GOGTAB[0:ENDREN];

! .dcl begin;
define MaxChan = 48;
define Tab(X) = "char!w(X,35)";
! .dcl end;

internal integer array Pos[-1:MaxChan];
integer OldPROU;

define Tabs = "(#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht&#ht)";

simple procedure MyPrint( integer Chan; string S );
begin
    GOGTAB[$$PROU] := OldPROU;
    if -1 leq Chan leq MaxChan
	then if width(S)=35 and length(S)=1
	    then begin
		integer I;
		if ( I := S%8 - Pos[Chan]%8 ) > 0 then begin
		    cprint( Chan, Tabs[1 to I] );
		    Pos[Chan] := S land -8;
		end;
		S := "        "[1 to S-Pos[Chan]];
	    end
	    else Pos[Chan] := if S < " " then 0 else Pos[Chan]+length(S);
    cprint( Chan, S );
    GOGTAB[$$PROU] := location(MyPrint);
end;

simple procedure MyPrintIni;
begin
    OldPROU := GOGTAB[$$PROU];
    GOGTAB[$$PROU] := location(MyPrint);
end;
require MyPrintIni initialization[0];

end	"TAB" $
