! TIOIO -- TIO IO PACKAGE

GLOBAL %AUXCAL, %SETLCH, %GETLCH, %LCHSET, %TABLCHSET, %TABTYPESET

FIND %DOUUO

! PROCEDURE: AUXCAL
! ARG:1 - FUNCTION # TO AUXCAL (SEE MONITOR CALLS BOOK)
! ARG:2 - ARGUMENT TO AUXCAL
! RETURNS THE VALUE FROMT THE AUXCAL
%AUXCAL(FN,ARG)

        DOUUO(OCT 042, 1, @ARG, 0, 0, FN BOR OCT 777777.000000)
        RETURN ARG

END AUXCAL

! PROCEDURE: SETLCH
! ARG:1 - BITS TO USE
! ARG:2 - TRUE TO SET (OR) ARG:1 BITS FROM GETLCH,
!       FALSE TO UNSET (AND -ARG1) BITS FROM SETLCH
%SETLCH(BITS,SW)

        ^14 _ -1
        EXU(OCT 51, OCT 6, 14, 0) ! GETLCH INTO 14
        IF SW THEN ^14 BOR _ BITS ELSE ^14 BAND _ BNOT BITS
        EXU(OCT 51, OCT 7, 14, 0) ! SETLCH FROM 14
        RETURN TRUE

END SETLCH

! PROCEDURE: GETLCH
! NO ARGUMENTS
! RETURNS LINE CHARACTERISTICS WORD
%GETLCH

        ^14 _ -1 ! THIS JOB
        EXU(OCT 51, OCT 6, 14, 0) ! GETLCH
        RETURN ^14

END GETLCH

! PROCEDURE: LCHSET
! ARG:1 LCH BITS TO SET
! ARG:2 MASK FOR OLD LCH BITS
! WILL TAKE OLD LCH BITS, MASK THEM WITH ARG:2, THEN OR IN ARG:1
! AND DO A SETLCH
%LCHSET(BITS,MASK)

        ^14 _ -1 ! THIS JOB
        EXU(OCT 51, OCT 6, 14, 0) ! GETLCH 14,
        ^14 _ (^14 BAND MASK) BOR BITS
        EXU(OCT 51, OCT 7, 14, 0) !SETLCH 14,
        RETURN TRUE

END LCHSET

! PROCEDURE: TABTYPESET
! ARG:1 = ADR OF A TABLE IN FORM
!       AUXCAL NO,,FLAG VALUE
!       = 0 WHEN DONE
! ARG:2 = TRUE IF INPUT STUFF SHOULD BE SET, FALSE IF NOT
! ARG:3 = TRUE IF OUTPUT STUFF SHOULD BE SET, FALSE IF NOT
! WILL SET TERMINAL CHARACTERISTICS AND RETURN
%TABTYPESET(THE.TABLE(),INFLAG,OUTFLAG)

TABLE INTYPE(0:30)[0,0,0,0,0,0,0,0,0,0,0,0,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0]
TABLE OUTYPE(0:30)[0,0,0,0,0,0,0,0,0,0,0,0,1,0,1,1,1,1,1,1,1,1,0,1,1,1,0,0]

LOCAL NDX

        NDX _ -1
        WHILE THE.TABLE(INC NDX) # 0 THEN DO
          EIF INTYPE(LH(@THE.TABLE(NDX))) AND INFLAG THEN AUXCAL(LH(@THE.TABLE(NDX)),RH(@THE.TABLE(NDX)))
          FIF OUTYPE(LH(@THE.TABLE(NDX))) AND OUTFLAG THEN AUXCAL(LH(@THE.TABLE(NDX)),RH(@THE.TABLE(NDX)))
          END

        RETURN TRUE

END TABTYPESET

! PROCEDURE: TABLCHSET
! ARG:1 - ADR OF LCH TABLE
!       SAME FORMAT AS TO TABTYPESET
!       BUT: LCHBIT,,FLAG
! ARG:2 - TRUE IF INPUT STUFF OK ELSE FALSE
! ARG:3 - TRUE IF OUTPUT STUFF OK ELSE FALSE
%TABLCHSET(THE.TABLE(),INFLAG,OUTFLAG)

LOCAL NDX

        NDX _ -1
        WHILE THE.TABLE(INC NDX) THEN DO
          IF OUTFLAG THEN SETLCH(LH(@THE.TABLE(NDX)),RH(@THE.TABLE(NDX)))
          END

        RETURN TRUE

END TABLCHSET
