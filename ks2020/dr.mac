TITLE	 DR11C diagnostic to run on KS-2020 (stand alone)
comment |

Copyright (C) 1979 by Tymshare, Inc.
		      20705 Valley Green Drive
		      Cupertino, CA   95014


This software may be used or copied only as follows:
1) For use on a system which is owned by Tymshare; or
2) with specific written permission from Tymshare.

	|
comment |
	Note: DDT should be assembled with EDDT=1 and DDTLOC=200000.
	This forces the start address of DDT to be out of the way of
	DR and makes it easy to remember for restarting from the CTY.
	|

T1=1	; Local use prefered
T2=2
T3=3
B=4	; Base address of current DR
I=5	; Local to trap routines
I1=6	; Local to trap routines
TST=7	; The current test (al la .TST)
P=10	; Stack Pointer
A=11	; Used by main routines only
DR=12	; Logical # of DR11C under test
C=13	; Local to IO routines
D=14	; Local to IO routines
M1=15	; Local to MSG routines
M2=16	; Local to MSG routines

;	DR11C register offsets

DRCSR==0			; Control Status Register
DROB==2				; Output Buffer
DRIB==4				; Input Buffer


SUBTTL KS-10 INSTRUCTION DEFINITIONS
        XLIST

;OPDEFS FOR KS10 INSTRUCTIONS

	OPDEF	APRID	[700000000000]	;READ SERIAL NO, ETC.
	OPDEF	WRPI	[700600000000]	;CONO PI.
	OPDEF	RDPI	[700640000000]	;CONI PI.
	OPDEF	WRAPR	[700200000000]	;CONO APR.
	OPDEF	RDAPR	[700240000000]	;CONI APR.
	OPDEF	WRADB	[255000000000]	;DATAO APR.(NOT AVAIL ON KS)
	OPDEF	CLRPT	[701100000000]	;BLKO PAG.
	OPDEF	WREBR	[701200000000]	;CONO PAG.
	OPDEF	RDEBR	[701240000000]	;CONI	PAG.
	OPDEF	WRUBR	[701140000000]	;DATAO PAG.
	OPDEF	RDUBR	[701040000000]	;DATAI	PAG.
	OPDEF	WRTIME	[702600000000]	;WRITE 2 WORD TIME BASE.
	OPDEF	RDTIME	[702200000000]	;READ 2 WORD TIME BASE.
	OPDEF	WRINT	[702640000000]	;WRITE INTERVAL TIMER PERIOD
	OPDEF	RDINT	[702240000000]	;READ CURRENT VALUE OF INTERVAL TIMER
	OPDEF	PXCT	[XCT]		;PREVIOUS CONTEXT XCT.
	OPDEF	UMOVE	[704000000000]	;MOVE FROM USER
	OPDEF	UMOVEM	[705000000000]	;MOVE TO USER
	OPDEF	TIOE	[710000000000]	;I/O REGISTER INSTRUCTIONS
	OPDEF	TION	[711000000000]
	OPDEF	RDIO	[712000000000]
	OPDEF	WRIO	[713000000000]
	OPDEF	BSIO	[714000000000]
	OPDEF	BCIO	[715000000000]
	OPDEF	TIOEB	[720000000000]
	OPDEF	TIONB	[721000000000]
	OPDEF	RDIOB	[722000000000]
	OPDEF	WRIOB	[723000000000]
	OPDEF	BSIOB	[724000000000]
	OPDEF	BCIOB	[725000000000]
;APRID BITS.
APRSER==77777		;PROCESSOR SERIAL NUMBER.

;WRAPR (CONO APR) BITS.
LP.IOR==1B19		;IO RESET
LP.ESF==1B20		;ENABLE SELECTED FLAGS (BITS 26-31)
LP.DSF==1B21		;DISABLE SELECTED FLAGS (BITS 26-31)
LP.CSF==1B22		;CLEAR SELECTED FLAGS (BITS 26-31)
LP.SSF==1B23		;SET SELECTED FLAGS (BITS 26-31)
LP.I8C==1B25		;INTERRUPT 8080 CONSOLE
LP.PWF==1B26		;POWER FAIL
LP.NXM==1B27		;NXM
LP.HPE==1B28		;HARD MEMORY ERROR
LP.SPE==1B29		;SOFT MEMORY ERROR
LP.TIM==1B30		;INTERVAL TIMER
LP.80C==1B31		;8080 CONSOLE
LP.INT==1B32		;GENERATE INTERUPT REQUEST
LP.PIA==7B35		;PIA

;LEFT HALF OF RDAPR(CONI APR) BITS
LP.PFE==1B8		;POWER FAIL ENABLED
LP.NEE==1B9		;NON-EXISTENT MEMORY ERROR ENABLED
LP.HEE==1B10		;HARD MEMORY ERROR INTERUPT ENABLED
LP.SEE==1B11		;SOFT MEMORY ERROR INTERRUPT ENABLED
LP.ITE==1B12		;INTERVAL TIMER ENABLED
LP.80E==1B13		;8080 CONSOLE INTERRUPT ENABLED

;RIGHT HALF OF RDAPR(CONI APR) BITS
LP.PWF==1B26		;POWER FAIL ERROR
LP.NXM==1B27		;NON-EXISTENT MEMORY ERROR
LP.HPE==1B28		;HARD MEMORY ERROR
LP.SPE==1B29		;SOFT MEMORY ERROR
LP.ITD==1B30		;INTERVAL TIMER DONE
LP.80I==1B31		;8080 CONSOLE INTERUPT
LP.INT==1B32		;INTERRUPT REQUEST
LP.PIA==7B35		;PIA

;BITS IN WRPI (CONO PI.).

LI.CPP==1B22		;CLEAR PROGRAM REQUESTS ON SELECTED CHANNELS
LI.CPS==1B23		;CLEAR PI SYSTEM
LI.REQ==1B24		;REQUEST INTERRUPTS ON SELECTED CHANNELS
LI.CON==1B25		;TURN SELECTED CHANNEL ON.
LI.COF==1B26		;TURN SELECTED CHANNEL OFF.
LI.PIF==1B27		;TURN SYSTEM OFF
LI.PIN==1B28		;TURN SYSTEM ON.
LI.CO1==1B29
LI.CO2==1B30
LI.CO3==1B31
LI.CO4==1B32
LI.CO5==1B33
LI.CO6==1B34
LI.CO7==1B35
LI.ACO==LI.CO1+LI.CO2+LI.CO3+LI.CO4+LI.CO5+LI.CO6+LI.CO7

;LEFT HALF CONI PI BITS
LI.PR1==1B11		;PROGRAM P1 REQUEST FOR CHANNEL 1
LI.PR2==1B12		;PROGRAM PI REQUEST FOR CHANNEL 2
LI.PR3==1B13		;PROGRAM PI REQUEST FOR CHANNEL 3
LI.PR4==1B14		;PROGRAM PI REQUEST FOR CHANNEL 4
LI.PR5==1B15		;PROGRAM PI REQUEST FOR CHANNEL 5
LI.PR6==1B16		;PROGRAM PI REQUEST FOR CHANNEL 6
LI.PR7==1B17		;PROGRAM PI REQUEST FOR CHANNEL 7

;RIGHT HALF PI CONI BITS.

LI.IP1==1B21			;INTERRUPT IN PROGRESS ON PI CHANNEL 1
LI.IP2==1B22			;INTERRUPT IN PROGRESS ON PI CHANNEL 2
LI.IP3==1B23			;INTERRUPT IN PROGRESS ON PI CHANNEL 3
LI.IP4==1B24			;INTERRUPT IN PROGRESS ON PI CHANNEL 4
LI.IP5==1B25			;INTERRUPT IN PROGRESS ON PI CHANNEL 5
LI.IP6==1B26			;INTERRUPT IN PROGRESS ON PI CHANNEL 6
LI.IP7==1B27			;INTERRUPT IN PROGRESS ON PI CHANNEL 7
LI.IPA==LI.IP1+LI.IP2+LI.IP3+LI.IP4+LI.IP5+LI.IP6+LI.IP7
				;INTERRUPT IN PROGRESS ON ANY OR ALL PI CHANNELS
LI.PIA==1B28			;PI ACTIVE
LI.CO1==1B29			;PI CHANNEL 1 ON
LI.CO2==1B30			;PI CHANNEL 2 ON
LI.CO3==1B31			;PI CHANNEL 3 ON
LI.CO4==1B32			;PI CHANNEL 4 ON
LI.CO5==1B33			;PI CHANNEL 5 ON
LI.CO6==1B34			;PI CHANNEL 6 ON
LI.CO7==1B35			;PI CH7NNEL 7 ON
LI.ACO==LI.CO1+LI.CO2+LI.CO3+LI.CO4+LI.CO5+LI.CO6+LI.CO7


;BITS IN WREBR (CONO PAG).
LG.KLP==1B21		;KL PAGING IN EFFECT.
LG.TEN==1B22		;TRAP ENABLE
LG.EPT==3777		;ADDRESS OF EPT

;BITS IN WRUBR (DATAO PAG).
LG.LAB==1B0		;LOAD AC BLOCKS
LG.LUB==1B2		;LOAD USER BASE REGISTER
LG.CAC==7B8		;CURRENT AC BLOCK #
LG.PAC==7B11		;PREV. AC BLOCK #
LG.UPT==3777		;ADDRESS OF UBR
; HERE ARE THE DEFINITION OF THE CTY(8080) INTERFACE WORDS
RLWORD==31 ; THE RELOAD WORD FOR COMMUNICATING BETWEEN THE KS AND THE 8080
	RLBOOT==1B4		;REQUEST FOR BOOTS (BT)
	RLACT==1B5		;KEEP ALIVE ACTIVE
	RLALIV==377B27		;KEEP ALIVE WORD
		KPALVE==^D27	;END OF KEEP ALIVE BYTE
		KPALVL==8	;LENGTH OF KEEP ALIVE BYTE

CTYIWD==32 ; THE INPUT FROM CTY WORD
	CTYICH==377B35		;CTY INPUT CHARACTER
	CTYIVL==1B27		;INPUT VALID FLAG

CTYOWD==33 ; THE OUTPUT TO CTY WORD
	CTYOCH==377B35		;CTY OUTPUT CHARACTER
	CTYOVL==1B27		;OUTPUT VALID FLAG

	LIST
subttl	Locations of location depend. data

EPT==1000
TRP==EPT+1000
ORG==TRP+1000

subttl	Macros to do messages

define	MSG (A)	<
		MOVE M1,[POINT 7,[ASCIZ \A\]]
		PUSHJ P,CTYSO
>
define	MSGNL <
		MOVE M1,[POINT 7,[BYTE (7)15,12]]
		PUSHJ P,CTYSO
>
define	MSGBIT (A) <
		MOVE M1,A
		PUSHJ P,CTYBO
>
define	MSGOCT (A) <
		MOVE M1,A
		PUSHJ P,CTYOO
>
define	MSGDEC (A) <
		MOVE M1,A
		PUSHJ P,CTYDO; 
>

; setup ept and upt and interrupt vectors and stuff

LOC EPT+103			; UBA 3 trap vector
EXP TRP

LOC EPT+422			; PDL overflow
JSR PDLOV

LOC EPT+502			; Page Fail new PC
EXP PAGFAL

LOC EPT+600			; Page map
	; AccessAllowed+Writable+Cacheable 
	XWD 500000,520001	; page 0 not cached
	XLIST
REPEAT 157,<			; Make map slot entries
	; AccessAllowed+Writable+Cacheable
	XWD 520000+<.-EPT-600>*2,520000+<.-EPT-600>*2+1
>
	LIST


; setup interrupt vectors for UBA 3

TRPCNT=	200			; number of vectors

LOC	TRP
REPEAT	TRPCNT,<
	TRPADR==.-TRP
	JSR [	Z
		MOVE I1,@.
		MOVEI I,TRPADR
		JRST TRPSER]
	XLIST
>
	LIST

subttl	MAIN	- begin executable code and location indep. data here 

LOC	ORG			; begin code and data

TRPTAB:	BLOCK TRPCNT		; Bits for each trap vector...
				; 0=trap not expected, cause error
				; if we trap through this vector;
				; >0=set given bits in INTFLG for
				; polling later.

; called at interrupt level
; called with return address in I1 and vector/4 in I
; will check for TRPTAB[I] zero and cause "bad trap error" if so,
; if TRPTAB[I] is non-zero (set by NxtDr) then this interrupt may
; be expected and the value of TRPTAB[I] is OR'd in to INTFLG

TRPOPC:	Z			; Save OPC here
TRPSER:	MOVEM I1,TRPOPC
	MOVE I1,TRPTAB(I)
	JUMPE I1,BADTRP		; Trap not expected
	IORM I1,INTFLGG		; Set flags in INTFLG

	JEN @TRPOPC		; Dismiss
BADTRP:	IMULI I,4		; Convert to vector number
	SKIPE .QUIET		; Complain if enabled
	  JRST BADTR1
	PUSH P,M1
	PUSH P,C
	msgnl
	msg <TRP: unexpected; vec=>
	msgoct I
	msgnl
	POP P,C
	POP P,M1
BADTR1:	AOS TRPERR
	JEN @TRPOPC

SUBTTL	Routines to handle MSG to CTY

CTYSO:	;	CTY String Output routine
	ILDB C,M1	
	JUMPE C,[POPJ P,]
	PUSHJ P,CTYOUT
	JRST CTYSO

CTYBO:	;	CTY Bit pattern Output routine
	TRZE M1,200000		; Int A bit on?
	  JRST [PUSH P,M1
		msg <IntA+>
		POP P,M1
		JRST .+1]
	TRZE M1,400000		; Int B bit on?
	  JRST [PUSH P,M1
		msg <IntB+>
		POP P,M1
		JRST .+1]

CTYOO:	;	CTY Octal Output routine
	PUSH P,M1
	HLRZ M1,(P)		; get left half
	JUMPE M1,CTYOO1
	PUSHJ P,CTYHO
	msg <,,>		; do ,, if necessary
CTYOO1:	HRRZ M1,(P)		; get right half
	PUSHJ P,CTYHO
	POP P,M1
	POPJ P,

CTYHO:	;	CTY Halfword octal Output routine
	IDIVI M1,10
	PUSH P,M2
	SKIPE M1
	  PUSHJ P,CTYHO
	POP P,M2
	MOVEI C,"0"(M2)
	PUSHJ P,CTYOUT
	POPJ P,

CTYDO: 	;	CTY Decimal Output routine
	IDIVI M1,12	
	PUSH P,M2
	SKIPE M1
	  PUSHJ P,CTYDO
	POP P,M2
	MOVEI C,"0"(M2)
	PUSHJ P,CTYOUT
	POPJ P,


subttl	Trap routines

PDLOV:	Z			; Should NEVER happen!!!
	msg <PDL OV; PC = >
	msgoct PDLOV
	JRST START

PFW==	EPT+500			; hard address of page fail word
PAGOPC==EPT+501			; old PC of word causing PF

OPDEF	JUMPT [JUMP]		; if this is found following an 
				; instruction that caused a page fail 
				; then JRST to its effective address

PAGFAL:	;	Page Fail trap handler
	PUSH P,T1
	MOVE T1,PAGOPC		; get PF causing instruction
	LDB T1,[point 9,1(T1),8]; get op code of instruction after trap
	CAIN T1,<JUMPT>_<^D9-^D36>	; is it a JUMP?
	  JRST PAGFA1
	PUSH P,M1
	PUSH P,C
	msgnl
	msg <Page Fail; PFW = >	; otherwise, describe the problem
	msgoct PFW
	msg <; PC = >
	msgoct PAGOPC
	msgnl
	POP P,C
	POP P,M1
	SKIPA T1,[1]		; return to instruction after
PAGFA1:	  MOVE T1,[Z @1]	; return to E of inst after trap
	ADDM T1,PAGOPC
	POP P,T1
	JRSTF @PAGOPC		; return somewhere

SUBTTL	IO SUBROUTINES

CTYSIC: SKIPE CTYIWD		; char test
	  AOS (P)
	POPJ P,

CTYIN:	SKIPN C,CTYIWD		; read char
	  JRST .-1
	SETZM CTYIWD
	ANDI C,177
	POPJ P,

COL1:	1			; indicated carriage at COL 1

CTYOUT: 			; write a char
	CAIN C,12		; is it a LF?
	  JRST [SKIPE COL1	; are we already at a new line (COL 1)?
		  POPJ P,	;   yes - just return
		SETOM COL1	; no - set flag and do LF
		JRST .+1]
	CAILE C,40		; printing character?
	  SETZM COL1		;   yes - not at COL1 any more
	PUSH P,C		; save C
	SKIPE CTYOWD		; exec mode write char
	  JRST .-1
	TRO C,CTYOVL
	MOVEM C,CTYOWD
	WRAPR 12000		; TICKLE 8080
	SKIPE CTYOWD		; (hack for EDDT)
	  JRST .-1
	POP P,C			; get back original character
	POPJ P,

SUBTTL INITIALIZE

EXTERN DDT
PDL:	BLOCK 40		; la Stack!
RUNFLG:	Z			; flag to indicate running
EXPECT:	Z			; data expected
ERRCNT:	Z			; bad data counter

START:	MOVE P,[IOWD 40,PDL]	; init la Stack!

	WRAPR 61760		; CLEAR AND DISABLE ALL
	WRPI 11577		; CLEAR AND DISABLE ALL

	MOVE T1,116		; Patch up symbol table for EDDT
	MOVEM T1,36

	SETZM CTYOWD		; CLEAR CTY LOCATIONS
	SETZM CTYIWD

	WREBR 1B22+EPT/1000	; EPT at page EPT/1000, trap enable
	WRUBR [5B2+EPT/1000]	; UPT at same page!
	MOVEI T1,44		; PIA 4 for high and low
	WRIO T1,@[3,,763100]	; Setup UBA 3 Status Register
	WRPI 2377		; Enable All, Enable System

	MOVEI T1,DR.CNT-1	; Get DR count
	SETZ T2,
ST1:	MOVE B,DR.ADR(T1)	; Get DR base address
	WRIO T2,DRCSR(B)	; Reset DR
	JUMPT .+1		; Ignore UB timeout trap
	SOJGE T1,ST1

	msgnl
	msg <DR11C diagnostic - Version 1>; Say HELLO
	msgnl

	SETOM DR		; Set defaults
	SETOM TST
	SETOM .DR
	SETOM .TST
	SETZM PATERR		; Clear counters, etc.
	SETZM CSRERR
	SETZM TRPERR
	SETZM PATMSK
	SETZM CSRMSK
	SETZM RUNFLG		; Not running

	JSR CP			; Process command(s)

SUBTTL  MAIN TEST LOOP - do all test as appropriate

DRfail:	SKIPGE .DR		; Can we get another?
	  JRST NxtDR		;    yes - go do it
	msgnl
	msg <?DR>		; Here if would loop looking for good DR
	msgoct .DR
	msg <: not responding>
	msgnl
	msg <?diagnostic halted>
	msgnl
	SETZM RUNFLG
	JSR CP

NxtDR:	SETO TST,		; Clear test start at beginning
	SKIPGE T1,.DR		; If spcific DR selected
	  MOVEI T1,1(DR)	;   Select next one
	IDIVI T1,DR.CNT		; T2 _ T1 mod DR.CNT
	MOVEM T2,DR		; Save new logical DR number
	MOVE B,DR.ADR(DR)	; Get DR base address
	RDIO DRCSR(B)		; See if DR exists
	JUMPT DRfail		;   If trap, go to next DR
	EXCH T2,DR		; Use new DR but keep around old one
	SKIPGE .TST		; Are all tests enabled?
	 SKIPE .QUIET		;  Should we be quiet?
	  JRST NDR1
	msgnl
	msg <Testing DR>	; Print DR number
     	msgoct DR
     	msg <:>
     	msgnl
NDR1:	JSR CP			; Process command if waiting
	SETZ T1,		; Clear CSR
	WRIO T1,DRCSR(B)
	SETZM TRPTAB		; Clear expected vector int flags
	MOVE T1,[TRPTAB,TRPTAB+1]
	BLT T1,TRPTAB+TRPCNT-1
	MOVE T1,DR.VEC(DR)
	LSH T1,-2		; T1 _ T1/4
	MOVEI T2,INTA
	MOVEM T2,TRPTAB+0(T1)	; INT A HANDLER
	MOVEI T2,INTB
	MOVEM T2,TRPTAB+1(T1)	; INT B HANDLER
	SETOM TST		; Next test is pattern 0
	
NxtTst:	JSR CP			; Process command if one waiting
	SKIPGE T1,.TST		; If specific test selected
	  MOVEI T1,1(TST)	;   Select next one
	MOVEM T1,TST		; Use new test 
DoTest:	CAIGE TST,200000	; See if a pattern test
	  JRST RWtest		;   Yes - dispatch to pattern tester
	CAIGE TST,200020	; See if CSR test
	  JRST CStest		;   Yes - dispatch to CSR tester
	JRST NxtDR		; None of these - done with tests


SUBTTL	PAUSE for PDP-11 to loop data back to us

Pause:	PUSH P,T1		; Save T1
	MOVE T1,.TIME
	SOJG T1,.		; Wait for '11 to respond for loopback
	POP P,T1
	POPJ P,


SUBTTL	R/W TEST - tries to read/write all possible data through link

; Call here with TST/ data pattern
	; expects B/ base address of DR
	; uses A,T1,T2,T3; calls MSGxxx who use C,D,M1
	; also uses ERRCNT,EXPECT
; This routine is called once for each data value to be tested for each
; DR by the routine NxtDR. RWTEST will return when the
; data read is correct or if "continue on error" is set or if "Halt
; on error" is set and the operator does a continue in KS10 console
; mode.  

RWtest:	SETZM ERRCNT		; Zero error count
	HRRM TST,T3		; Good data
	HRLM TST,T3
	MOVEM T3,EXPECT		; Initially expect good data
RWTRY:	JSR CP			; Do a command if one waiting
	WRIO TST,DROB(B)	; Write data
	RDIO T2,DROB(B)		; See if it stuck
	HRL A,T2		; OB in LH
	PUSHJ P,PAUSE		; Wait for slow loopback if necessary
	RDIO T2,DRIB(B)		; Read looped back data
	HRR A,T2		; IB in RH
	MOVE T1,A		; compute mask of error bits
	XOR T1,T3
	IORM T1,PATMSK		; save mask
	CAMN A,EXPECT		; Is it expected?
	  JRST RW1		;   expected
				; not expected
	SKIPN ERRCNT		; Print count if any
	  JRST RW2		;   no count
				; count
	SKIPE .QUIET
	  JRST RW2
	msg <; >
	msgdec ERRCNT
	msg < times>
	msgnl
	SETZM ERRCNT		; clear count for this error

RW2:	; -- data not expected --
	JUMPE T1,RWOKAY		; Is it good data?
				; data is bad
	MOVEM A,EXPECT		; expect same data again
	SKIPE .QUIET
	  JRST RW1
	msg <PAT: >
	msgoct TST
	msg <; OUTBUF=>
	HLRZ T2,A
	msgoct T2
	msg <; INBUF=>
	HRRZ T2,A
	msgoct T2
	msg <; mask=>
	msgoct T1
	SKIPN .SW		; If any but loop on error, need CRLF
	  JRST RW1		;   0=loop on error
	msgnl

RW1:	; -- data was expected --
	JUMPE T1,RWOKAY		; Is it good data?
				; data is bad
	AOS ERRCNT		; Count failures
	AOS PATERR		; Count pattern failures
	MOVE T1,.SW		; What to do on error
	JUMPE T1,RWTRY		; 0=loop on error, go try again
	CAIN T1,1		; 1=halt on error, die!
%H1:	  HALT .+1
				; 2=continue on error, return
RWOKAY: JRST NxtTst		; End of this test


SUBTTL	CSR bit R/W test

; Call here with B/ DR11C base address
	; TST/ 200000+N where N is a test number in [0:15]
	; uses T1, T2, T3, EXPECT and ERRCNT
	; calls MSGxxx who use C,D,M1
; This routine is called once for each CSR value to be tested for each
; DR by the routine NxtDR.  CSTEST will return when the
; data read is correct or if "continue on error" is set or if "Halt
; on error" is set and the operator does a continue in KS10 console
; mode.  Note that interrupts are tested at the same time.

IntA==1B19			; set in INTFLG by TRPSER
IntB==1B18			; set in INTFLG by TRPSER
ReqA==1B<35-7>			; read only
ReqB==1B<35-15>			; read only
EnbA==1B<35-6>			; r/w
EnbB==1B<35-5>			; r/w
BitA==1B<35-0>			; r/w
BitB==1B<35-1>			; r/w

;	 write	  	        expect
CS.TAB:	0                    ,,0
	0               +BitA,,0               +BitA               +ReqA
	0          +BitB     ,,0          +BitB               +ReqB
	0          +BitB+BitA,,0          +BitB+BitA          +ReqB+ReqA
	0     +EnbA          ,,0     +EnbA
	0     +EnbA     +BitA,,0     +EnbA     +BitA     +IntA     +ReqA
	0     +EnbA+BitB     ,,0     +EnbA+BitB               +ReqB
	0     +EnbA+BitB+BitA,,0     +EnbA+BitB+BitA     +IntA+ReqB+ReqA
	0+EnbB               ,,0+EnbB               
	0+EnbB          +BitA,,0+EnbB          +BitA               +ReqA
	0+EnbB     +BitB     ,,0+EnbB     +BitB     +IntB     +ReqB
	0+EnbB     +BitB+BitA,,0+EnbB     +BitB+BitA+IntB     +ReqB+ReqA
	0+EnbB+EnbA          ,,0+EnbB+EnbA
	0+EnbB+EnbA     +BitA,,0+EnbB+EnbA     +BitA     +IntA     +ReqA
	0+EnbB+EnbA+BitB     ,,0+EnbB+EnbA+BitB     +IntB     +ReqB
	0+EnbB+EnbA+BitB+BitA,,0+EnbB+EnbA+BitB+BitA+IntB+IntA+ReqB+ReqA

CStest:	SETZM ERRCNT		; Zero error count
	MOVEI T1,-200000(TST)	; Get CSR test #
	HLRZ T2,CS.TAB(T1)	; What to write
	HRRZ T3,CS.TAB(T1)	; What to hope for
	MOVEM T3,EXPECT		; What to expect
CStry:	JSR CP			; Test for command
	SETZ T1,		; Reset the CSR
	WRIO T1,DRCSR(B)
	SETZM INTFLG		; Clear interrupt flags
	WRIO T2,DRCSR(B)	; Write it
	PUSHJ P,PAUSE		; Pause if necessary for softloop
	RDIO A,DRCSR(B)		; What do we have?
	IOR A,INTFLG		; Merge in interrupts (bits 16-17)
	MOVE T1,A		; Mask bits in error
	XOR T1,T3
	IORM T1,CSRMSK		; Save mask
	CAMN A,EXPECT		; Did we expect that?
	  JRST CS1		;   expected
	  
	SKIPN ERRCNT		; Print error count if any
	  JRST CS2
	
	SKIPE .QUIET
	  JRST CS2
	msg <; >
	msgdec ERRCNT
	msg < times>
	msgnl
	SETZM ERRCNT

CS2:	; -- Data not expected --
	JUMPE T1,CSOKAY		; Is data good?
	Data is incorrect
	MOVEM A,EXPECT
	SKIPE .QUIET
	  JRST CS1
	msg <BIT: >
	msgoct T2
	msg <; expected=>
	msgbit T3
	msg <; CSR=>
	msgbit A
	msg <; mask=>
	msgbit T1
	SKIPN .SW		; If any but loop on error, need CRLF
	  JRST CS1		;   0=loop on error
	msgnl

CS1:	; -- Data was expected --
	JUMPE T1,CSOKAY		; Good data?
				; Data is bad
	AOS ERRCNT		; Another error
	AOS CSRERR
	MOVE T1,.SW		; Get what to do on error
	JUMPE T1,CStry		; 0=loop on error
	CAIN T1,1
%H2:	  HALT .+1		; 1=halt on error
				; 2=continue on error
CSokay:	JRST NxtTst		; End of this test


SUBTTL	Command processor - command definition

; call here to get a command...
; returns immediately if there is not character waiting
; processes command if char is waiting
; may never return to caller (ie. may JRST somewhere)
; will not return if RUNFLG = 0 (ie. diagnostic not running)

CP:	Z
	PUSHJ P,CTYSIC		; character there?
	  JRST CP1		; no, do nothing
	msgnl			; insure new line
	PUSHJ P,CTYIN		; get character
	CAIL C,140		; convert to upper case
	  SUBI C,40
DISP:	XCT CHRTAB(C)		; do it
CP1:	SKIPN RUNFLG		; running?
	  JRST CP+1		;   no - loop
	JRSTF @CP		; yes, return

OCTER:	START			; Place to go to in case of error
OCTIN:	SETZ T1,
OCTIN1:	PUSHJ P,CTYIN
	CAIN C,33		; "escape" means -1
	  JRST [msg <[all]>
		msgnl
		SETO T1,
		AOS (P)		; skip return
		POPJ P,]
	CAIN C,32		; ^Z means go to DDT
	  JSR %Z.CMD
	CAIN C,15		; "return" means we got it
	  JRST [msgnl
		AOS (P)		; skip return
		POPJ P,]
	CAIN C,25		; ^U ignore this command
	  JRST [msg < ^U>
		msgnl
		POPJ P,]	; return to caller (error)
	CAIN C,177		; RUBOUT - delete one
	  JRST [JUMPN T1,QQ	; Don't back up too far
		  msg <>	; Bell
		  JRST OCTIN1	; go back
	QQ:	msg < >	; <BS><SP><BS>
		LSH T1,-3	; delete the char
		JRST OCTIN1]
	PUSHJ P,CTYOUT		; echo it
	CAIL C,"0"		; is it a valid digit
	 CAILE C,"7"
	  JRST [MOVEI C,7
		PUSHJ P,CTYOUT
		msg <? >
		JRST OCTIN]
	LSH T1,3
	ADDI T1,-"0"(C)		; add in this digit
	JRST OCTIN1

CHRCNT=140
	XLIST
CHRTAB:	REPEAT CHRCNT,<PUSHJ P,.BEEP>
	LIST

.BEEP:	MOVEI C,7		; Ring bell to signify illegal command
	PUSHJ P,CTYOUT
	POPJ P,

define	BEEP (ADR) <		;; ring bell, complain and jump
JRST [	PUSHJ P,.BEEP
	JRSTF ADR
]>

define	CHRDEF (CH,ADR) <	;; call this to map a subroutine to
	LOC CHRTAB+ch		;; a particular command character
	JSR adr
	LOC >


subttl	? Command - print help message

chrdef	"?",HELP
HELP:	Z
	MOVE M1,[point 7,HLPMSG]; inform user of his fate
	PUSHJ P,CTYSO		; type it all out
	JRSTF @HELP
HLPMSG:	ASCIZ \
One of following...
  All (tests enable)
  Bits (in DR CSR) #
  Continue on error
  Device (logical no) #
  Error status is...
  Halt on error
  Loop on error
  Map devices
  Next test
  Pattern (16 bits) #
  Run (diagnostic)
  Stop (diagnostic)
  Quiet (no messages)
  Verbose (messages)
  What is going on?
  ^C boot ten [confirm]
  ^Z ddt
  ?
\


subttl	^C Command - boot ten

chrdef	"C"-100,%C.CMD
%C.CMD:	Z
	msg <^C boot ten [confirm]>
	PUSHJ P,CTYIN
	CAIE C,15
	  JRST [msg < XXX>
		msgnl
		JRSTF @%C.CMD]
	msgnl
	MOVSI (RLBOOT)		; bit to say "please reboot"
	IORM RLWORD		; send to the 8080
	WRAPR 12000		; knock on the door
	HALT .


subttl	^Z Command - enter DDT at breakpoint or cold-start

chrdef	"Z"-100,%Z.CMD
%Z.CMD:	Z
	MOVE T1,[JRSTF @%Z.CMD]
	CAME T1,%Z		; is a breakpoint set?
%Z:	  JRSTF @%Z.CMD		; should set a breakpoint here!
	msg <^Z DDT (%Z$G to return)>; no breakpoint - jrst to DDT
	JRST DDT


subttl	A Command - enable all tests

chrdef	"A",A.CMD
A.CMD:	Z
	msg <All (tests enable)>
	msgnl
	SETOM .TST		; enable all tests
	JRSTF @A.CMD


subttl 	B Command - set test to DR CSR bit N

chrdef	"B",B.CMD
B.CMD:	Z
	msg <Bits (16 bits) >
B.CMD1:	PUSHJ P,OCTIN		; get bit pattern in T1
	  JRSTF @B.CMD
	MOVEI T2,17
B.C1:	HLRZ T3,CS.TAB(T2)	; get bits
	CAMN T1,T3		; does it match?
	  JRST B.C2		;   yes!
	SOJGE T2,B.C1		; no - try next one
	msg <?invalid; bit patterns are:>
	msgnl
	msg <  >
	MOVEI T2,17
B.C3:	HLRZ T1,CS.TAB(T2)
	msgoct T1
	msg <,>
	SOJGE T2,B.C3
	msgnl
	JRSTF @B.CMD
B.C2:	MOVEM T2,.TST		; save test index
	JRSTF @B.CMD


subttl	C Command - continue on errors

chrdef	"C",C.CMD
C.CMD:	Z
	msg <Continue on errors>
	msgnl
	MOVEI T1,2		; .SW code for CONTINUE on error
	MOVEM T1,.SW		; save what to do for later
	JRSTF @C.CMD


subttl	D Command - set device to test

chrdef	"D",D.CMD
D.CMD:	Z
	msg <Device (logical no) >
D.CMD1:	PUSHJ P,OCTIN		; get octal argument
	  JRSTF @D.CMD		; ^U escape out of command
	CAILE T1,DR.CNT-1	; test for in range
	  JRST BADDEV		; out of range - beep
	JUMPL T1,D.CMD2		; okay if [all]
	MOVE T2,DR.ADR(T1)	; get base address
	RDIO T2,(T2)		; does it exist?
	JUMPT BADDEV		; does not exist
D.CMD2:	MOVEM T1,.DR		; save index of DR number
	JRST NxtDR
BADDEV:	msg <?non-existant device>
	msgnl
	JRSTF @D.CMD


subttl	E Command - display error status

chrdef	"E",E.CMD
E.CMD:	Z			; display all error stuff for fun
	msg <Error status is...>
	msgnl
	msg <  PAT: count=>
	msgdec PATERR
	SETZM PATERR		; zero it
	msg <; mask=>
	msgoct PATMSK
	SETZM PATMSK
	msgnl
	msg <  BIT: count=>
	msgdec CSRERR
	SETZM CSRERR		; zero them all!
	msg <; mask=>
	msgbit CSRMSK
	SETZM CSRMSK
	msgnl
	msg <  TRP: count=>
	msgdec TRPERR
	SETZM TRPERR		; zero them!
	msgnl
	JRSTF @E.CMD


subttl	F Command - set Fast loop (hardware maint cable)

chrdef	"F",F.CMD		; Not available
F.CMD:	Z
	msg <Fast (hard loop)>
	msgnl
	SETZM .TIME		; # time loop for good data
	JRSTF @F.CMD


subttl	H Command - set Halt on Error

chrdef	"H",H.CMD
H.CMD:	Z
	msg <Halt on error>
	msgnl
	MOVEI T1,1		; .SW code for HALT
	MOVEM T1,.SW		; save flag for later
	JRSTF @H.CMD


subttl	L Command - set Loop on Error

chrdef	"L",L.CMD
L.CMD:	Z
	msg <Loop on errors>
	msgnl
	MOVEI T1,0		; .SW code for LOOP on errors
	MOVEM T1,.SW
	JRSTF @L.CMD


subttl	M Command - display map of existing DRs

chrdef	"M",M.CMD
M.CMD:	Z
	msg <Map DR11C's:>
	msgnl
	MOVEI T1,DR.CNT-1	; DR counter
M.CMD1:	MOVE T2,DR.ADR(T1)	; get DR base address
	RDIO T2,DRCSR(T2)	; try to read CSR
	JUMPT M.CMD2		; did it time out?
	msg <  DR>
	msgoct T1		; DR number
	msg <: adr=>
	msgoct DR.ADR(T1)	; type UBA address
	msg < vec=>
	msgoct DR.VEC(T1)	; type vector address
	msgnl
M.CMD2:	SOJGE T1,M.CMD1		; try next DR
	JRSTF @M.CMD


subttl	N Command - go to next test pattern

chrdef	"N",N.CMD
N.CMD:	Z
	SKIPN RUNFLG		; legal only if running
	  beep @N.CMD
	msg <Next test>
	msgnl
	SKIPGE T1,.TST
	  JRST NxtTst
	AOS T1
	IDIVI T1,200020
	MOVEM T2,.TST
	JRST NxtTst


subttl	P Command - set pattern for test

chrdef	"P",P.CMD
P.CMD:	Z
	msg <Pattern (16 bits) >
	PUSHJ P,OCTIN		; get octal argument
	  JRSTF @P.CMD		; abort with ^U
	MOVEM T1,.TST		; don't bother checking bounds
	JRSTF @P.CMD


subttl	Q Command - set quiet - no messages

chrdef	"Q",Q.CMD
Q.CMD:	Z
	msg <Quiet (no messages)>
	msgnl
	SETOM .QUIET		; make it quiet
	JRSTF @Q.CMD

subttl	R Command - Run

chrdef	"R",R.CMD
R.CMD:	Z
	SKIPE RUNFLG
	  beep @R.CMD
	msg <Run (diagnostic)>
	msgnl
	SETOM RUNFLG
	JRSTF @R.CMD


subttl	S Command - Stop

chrdef	"S",S.CMD
S.CMD:	Z
	SKIPN RUNFLG
	  beep @S.CMD
	msg <Stop (diagnostic)>
	msgnl
	SETZM RUNFLG
	JRSTF @S.CMD


subttl	U Command - set unfast loopback (PDP-11 echo)

chrdef	"U",U.CMD
U.CMD:	Z
	msg <Unfast (soft loopback)>
	msgnl
	MOVE T1,U.TAB		; get count for soft loopback
	MOVEM T1,.TIME
	JRSTF @U.CMD
U.TAB:	1000			; #loops to do waiting for data


subttl	V Command - set verbose - print messages

chrdef	"V",V.CMD
V.CMD:	Z
	msg <Verbose (messages)>
	msgnl
	SETZM .QUIET		; make it noisy
	JRSTF @V.CMD


subttl	W Command - explain what is going on

chrdef	"W",W.CMD
W.CMD:	Z
	msg <What (is going on?)>
	msgnl

define	J.MSG (adr,txt) <
	JRST   [msg <txt>
		msgnl
		JRST adr]
	W.ADR==adr
>
define	W.MSG (txt) <
	JRST   [msg <txt>
		msgnl
		JRST W.ADR]
>
define	w.dsp (var,msg) <
	W.ADR==E'var
	MOVE T1,var
	ANDI T1,77777		; only right 15 bits
	CAIL T1,E'var-B'var
	  w.msg <msg>
	JRST B'var(T1)
>

	w.dsp RUNFLG,<  Run (diagnostic)>
BRUNFL:	w.msg <  Stop (diagnostic)>
ERUNFL:
	w.dsp .SW,<?? illegal .SW ??>
B.SW:	w.msg <  Loop on error>
	w.msg <  Halt on error>
	w.msg <  Continue on error>
E.SW:	
	w.dsp .QUIET,<  Quiet (no messages)>
B.QUIE:	w.msg <  Verbose (messages)>
E.QUIE:
	SKIPE .TIME
	  j.msg E.TIME,<  Unfast (soft loopback)>
E.TIME:	
	SKIPGE .DR		; all enabled?
	  JRST .DR1		;   yes
	msg <  Device (logical no) >
	msgoct .DR
	msgnl
	JRST E.DR
.DR1:	msg <  Device (logical no) [all]>
	msgnl
	SKIPGE DR		; is it a valid DR number?
	  JRST E.DR		;   no
	msg <  Current device is DR>
	msgoct DR
	msg <:>
	msgnl
E.DR:
	SKIPGE T1,.TST		; are all tests enabled?
	  JRST .TST1		;   yes
	TRZE T1,200000		; is it a pattern?
	  JRST .TST2		;   no - its bits
	msg <  Pattern (16 bits) >
	msgoct T1
	msgnl
	JRST E.TST
.TST2:	msg <  Bits (in CSR) >
	HLRZ T1,CS.TAB(T1)	; lookup bit pattern from tst #
	msgoct T1
	msgnl
	JRST E.TST
.TST1:	msg <  All (tests enable)>
	msgnl
	SKIPGE T1,TST		; is current test defined?
	  JRST E.TST		;   no - don't mention it
	TRZE T1,200000		; is it a pattern?
	  JRST .TST3		;   no - its bits
	msg <  Current test is PAT: >
	msgoct T1
	msgnl
	JRST E.TST
.TST3:	msg <  Current test is BIT: >
	HLRZ T1,CS.TAB(T1)	; lookup bit pattern from tst #
	msgoct T1
	msgnl
E.TST:	

	JRSTF @W.CMD		; return


SUBTTL	miscellaneous storage locations

%DRA==	767770			; address of first DR
%DRV==	300			; vector of first DR
DR.CNT==^D16			; maximum number of DR's
DR.ADR:	repeat DR.CNT,<		;; UB addresses of DR's
	XWD 3,%DRA
	%DRA==%DRA-10
	XLIST>
	LIST
DR.VEC:	repeat DR.CNT,<		;; vector addresses of DR's
	XWD 0,%DRV
	%DRV==%DRV+10
	XLIST>
	LIST

ALL==	-1

INTFLG:	Z			; gets bits set when interrupts occur
PATERR:	Z			; count of pattern errors
CSRERR:	Z			; count of bit R/W errors
TRPERR:	Z			; count of unexpected trap errors
PATMSK:	Z			; mask of pattern errors
CSRMSK:	Z			; mask of bit R/W errors

.DR:	ALL			; DR index number (or ALL)
.TST:	ALL			; test number (or ALL)
	; 0..177777		; R/W pattern test
	; 200000..200017	; CSR bit R/W test
	; 200020		; test after last
.SW:	0			; switches
	; 0 = loop on error
	; 1 = halt on error
	; 2 = continue on error
.QUIET:	0			; 0=loud,#0=quiet
.TIME:	0			; # time to loop waiting for data



END	START

    @}ƒ