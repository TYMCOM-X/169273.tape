TITLE LPTVFU	-- RELOAD LP20 DAVFU
	FS==^D66	;FORM SIZE
	LPP==^D65	;LINES PER PAGE

               
DEFINE	XPP(A,B)		;SYSTEM PARAMETER
<A=:B>
XPP S,0	       	;*I/O DEVICE STATUS WORD 
XPP P,1		;*PUSH DOWN POINTER
XPP J,2		;BUFFER ITEM COUNT, OR JOB NUMBER
XPP PG,3	;THIS DOES REPLACE R. IN GENERAL A PAGE NUMBER
XPP F,4		;*LH=UUOS DONE SO FAR FOR THIS DEVICE (SEE BELOW)
		; RH=ADDRESS OF DEVICE DATA BLOCK - ONE PER FILE
XPP U,5		;TTY OUTPUT BUFFER POINTER FOR COMMANDS, ERROR
		; MESSAGES, ETC.	;OR TEMPORARY
XPP T1,6	;TEMPORARY (SOMETIMES PRESERVED ACROSS SUBRTNS)
XPP T2,T1+1	;TEMPORARY (SOMETIMES PRESERVED ACROSS SUBRTNS)
XPP T3,T2+1	;TEMPORARY
XPP T4,T3+1	;*ADDRESS OF DEVICE SERVICE ROUTINE'S DISPATCH TABLE
XPP W,12	;CONTENTS OF FIRST WORD OF 3 WORD USER BUFFER HEADER
XPP M,13	;*CURRENT UUO IN PROGRESS
XPP P1,14	;PERMANENT AC'S
XPP P2,P1+1	;
XPP P3,P2+1	;
XPP P4,P3+1	;CONTENTS OF 2ND WORD OF USER BUFFER

DEFINE	LPTCMD (OP,MASK<0>,FIRST<1>,LAST<1>)<
	EXP <OP_^D33>!<<MASK>_^D18>!<<FIRST>_^D9>!<LAST>
	>;END DEFINE LPTCMD

DEFINE	CLRVFU (MASK,FIRST,LAST<0>)<
	IFN LAST,<LPTCMD 0,<MASK>,<FIRST>,<LAST>>
	IFE LAST,<LPTCMD 0,<MASK>,<FIRST>,<FIRST>>	>;END CLRVFU
DEFINE	SETVFU (MASK,FIRST,LAST<0>)<
	IFN LAST,<LPTCMD 1,<MASK>,<FIRST>,<LAST>>
	IFE LAST,<LPTCMD 1,<MASK>,<FIRST>,<FIRST>>	>;END SETVFU
DEFINE	LODVFU (MASK,FIRST,LAST<0>)<
	IFN LAST,<LPTCMD 3,0,<FIRST>,<LAST>>
	IFE LAST,<LPTCMD 3,0,<MASK>,<FIRST>>	>;END LODVFU
DEFINE	LPTTOF <LPTCMD 2,0,1,1			>;END LPTTOF

DEFINE MOD(A,B),<A-<<A/B>*B>>

DEFINE VFUBIT(LINE,LPTLPP),<		;;LINE #, LINES PER PAGE
..BT==0
..LLL==LINE-1
IFE MOD(..LLL,36),<..BT==..BT!1B16>	;;EVERY 30 LINES GET CH. 2
IFE MOD(..LLL,4),<..BT==..BT!1B15>	;;EVERY 4TH LINE GETS CH. 3
IFE MOD(..LLL,3),<..BT==..BT!1B14>	;;EVERY 3RD LINE GETS CH. 4
IFE MOD(..LLL,12),<..BT==..BT!1B12>	;;EVERY 10TH LINE GETS CH. 6
IFE MOD(..LLL,24),<..BT==..BT!1B11>	;;EVERY 20TH LINE GETS CH. 7
IFLE ..LLL-<LPTLPP-1>,<..BT==..BT!1B13>	;;LINES.LE.LINES/PAGE GET CH. 5
IFLE ..LLL-<LPTLPP-1>,<..BT==..BT!1B10>	;;LINES 0-LINES/PAGE GET CH. 8
IFE ..LLL-<LPTLPP-1>,<..BT==..BT!1B6>	;;LAST LINE ON PAGE GETS CH. 12
IFGE ..LLL-LPTLPP,<..BT==1B13>	;;ABOVE LINES/PAGE CH.5 ONLY
IFE ..LLL,<..BT==1B17>		;;LINE 0 GETS CH. 1
..BT==..BT!1B2!<<LINE>_<^D9>>!<LINE>
EXP ..BT
>;VFUBIT MACRO DEFINITION

	OPDEF	ADJBP	[XWD 133000,0]
	OPDEF	PXCT	[XCT]
	OPDEF	RET	[POPJ P,0]
	OPDEF	CALL	[PUSHJ P,0]

CPOPJ1:	AOS	0,(P)
	RET

	CH1==1
	CH2==2
	CH3==4
	CH4==10
	CH12==4000




START:
	MOVE	P,PDL
	JRST	OPENLP
QMSFLG:	0
RESTART:
	MOVE	P,PDL
	SKIPN	QMSFLG
	  OUTSTR QUIKMS		;A QUICK MESSAGE BEFORE YOU START
	OUTSTR	RPTMSG
	SETOM	QMSFLG
	INCHWL	T1
LFLUP:	INCHWL	T2
	CAIE	T2,12	;EAT THE REST OF THE LINE
	  JRST	LFLUP
	CAIN	T1,"?"
	  JRST	[OUTSTR LONGMS	;TYPE OUT THE LONG TEXT
		JRST START]
	IORI	T1,"y"-"Y"
	CAIN	T1,"y"
	  JRST	OPENLP		;OPEN THE LINE PRINTER ON CH 9
	CAIN	T1,"x"
	  JRST TEST
OPENLP:	OPEN 9,[
	0
	SIXBIT /LPT/ 
	0]
	  JRST OERR		;OPEN ERROR
	CALL UINPUT		;GET FORM SIZE AND LPP FROM USER
	  JRST RESTART
	MTAPE 9,TTYLST		;Command list FormSize,LinesPerPage
	  JRST BADCMD		;ERROR IN COMMAND LIST
	MOVEI	M,TTYLST	;COMMAND LIST FormSize,LinesPerPage
	CALL	INIVFU	;simulate buffer build to look at in DDT
	  JRST BADCMD		;ERROR IN COMMAND LIST
	EXIT
BADCMD:	OUTSTR [ASCIZ /
	ERROR IN COMMAND LIST.  ABORTING... /]
	EXIT
OERR:	OUTSTR	[ASCIZ /
	The LP20 isn't available to you now.

/]
	JRST RESTART

TEST:	MOVE	P,PDL
	OUTSTR [ASCIZ /
This is only a trial run, it won't affect the LP20.
/]
	CALL UINPUT
	  JRST RESTART
	MOVEI	M,TTYLST	;COMMAND LIST FormSize,LinesPerPage
	CALL	INIVFU
	  JRST	[OUTSTR [ASCIZ /
	  ERROR IN COMMAND LIST./]
	  JRST TEST]
	OUTSTR	[ASCIZ /
Good!  That should work fine.
/]
	JRST TEST

UINPUT:	OUTSTR [ASCIZ /
Enter form length  >/]
	CALL GETFS
	  SKIPA
	JRST CPOPJ1
	OUTSTR	[ASCIZ /
Form Length must be at least 8 but no more than 88.

/]
	RET

GETFS:
	MOVEI	T4,TINARG
	PUSHJ	P,DECBIN
	RET			;ERROR RETURN. ASK QUESTION AGAIN.
	MOVE	T1,K#		;HERE ON SUCCESSFUL RETURN
	ANDI	T1,377
	CAILE	T1,^D88
	  RET
	CAIGE	T1,^D8		;MUST BE GREATER THAN 8
	  RET
	DPB	T1,TTYFS
	DPB	T1,TTYLPP	;make FS & LPP the same
	JRST CPOPJ1

GETLPP:	
	MOVEI	T4,TINARG
	PUSHJ	P,DECBIN
	RET			;ERROR RETURN. ASK QUESTION AGAIN.
	MOVE	T1,K		;HERE ON SUCCESSFUL RETURN
	ANDI	T1,377
	LDB	T2,TTYFS
	CAIL	T1,(T2)
	  RET
	DPB	T1,TTYLPP
	JRST CPOPJ1


PDL:	XWD -PDLLEN,PDL+1
	BLOCK 30
	PDLLEN==.-PDL-1
TINARG:	Z	2,K

TTYLST:	CLRVFU	7777,1,^D88	;VARIABLE CMD LIST
	SETVFU	3776,1,^D88
TTYCMD:	LODVFU	^D87,^D88	;MAY BE CHANGED FROM TTY
TTYLPP:	POINT 8,TTYCMD,26	;BITS 19-26
TTYFS:	POINT 8,TTYCMD,35	;BITS 28-35

CFSLPP:	CLRVFU	7777,1,FS	;FIXED CMD LIST
	SETVFU	3776,1,FS
	LODVFU	LPP,FS

FANCY:	CLRVFU	7777,1,FS	;FANCY CMD LIST
	..L==1
	REPEAT FS,<
	VFUBIT (..L,LPP)
	..L==..L+1
	>;END REPEAT
	LODVFU	LPP,FS

				;MISC COMMAND EXAMPLES
	SETVFU	3776,FS-6,FS	;SET ALL STOPS BELOW PAGE LEN
	SETVFU	CH3!CH4,1,FS		;SET ALL STOPS ON CH3 & CH4
	SETVFU	1740,FS/2		;SET MID FORM STOPS CH6-CH10
	CLRVFU	CH2,FS-6,FS	;CLEAR CH2 BELOW PAGE LEN
	CLRVFU	CH1!CH12,1,FS	;MAKE SURE CH1 & CH12 ARE CLEAR
	LODVFU	FS-6,FS		;END OF RELOAD DAVFU CMD LIST!
LODV20:	LODVFU	^D15,^D20
LODV10:	LODVFU	^D8,^D10	;THAT'S ABOUT AS SMALL AS WE CAN GO
LODV30:	LODVFU	^D25,^D30
LODV40:	LODVFU	^D35,^D40
LODV50:	LODVFU	^D45,^D50
	LPTTOF
	LODVFU	0,FS-6,FS		;SHOULD WORK THIS WAY TOO!

	OPDEF	ADJBP	[XWD 133000,0]
;;;;;;;;	VFUBUF==LPVLPT	;our symbol for the COMMOD buffer
	MAXC==^D143	;allow no more commands than max DAVFU lines
	MAXLN==^D88	;only allow 88 lines for now
INIVFU::;;;;;;;;CALL WAIT1	;Wait for IOACT to go off
	HRRZI M,(M)	;M points to current(the first for now) command
			;FIRST TEST FOR A SINGLE RESET TOF COMMAND
	HLRZ	T3,(M)	;T3/cmd op and channel mask
	ANDI	T3,(3B2);T3/cmd op
	CAIE	T3,(1B1);skip if RESET TOF cmd
	  JRST	CMDLST	;  else go process the reload command list
	MOVE T1,NEWTOF	;set up for a one byte (357) LODVFU
 RET ;;;;;;;;	JRST	LPTFLV	;go into LODVFU routine
			;----------------------------------------------
CMDLST:	CALL ERRSCN	;scan command list for errors
	  POPJ	P,	;NON-SKIP RETURN! (bad command list)
CMDLUP:	CALL GETCMD	;T3/op + chan mask; T2/last line; T1/first line
	TLZE	T3,(1B1);test 'this is the final command' bit
	  JRST	RLDVFU	;  if set go and RELOAD VFU
VFULUP:	CALL FETCH	;else fetch the channel stops for current line
	TLNE	T3,(1B2);command bit tells wether to set or reset the
	  IORM	T3,T1	;	masked channels.  IORM sets them.
	TLNN	T3,(1B2);skip if bit 2 is set
	  ANDCAM T3,T1	;	reset masked bits.
	CALL STORE	;store bits 6-17 of T1 as channel stops 12-1
	ADDI	T1,1	;increment current line
	CAIG 	T1,(T2)	;if current line > last line skip out of loop
	  JRST	VFULUP	;else process the new current line
	AOJA	M,CMDLUP;increment command pointer and loop back

RLDVFU:	HRLZI	T3,4000	;channel 12 mask bit
	CALL FETCH	;fetch channel stops for bottom line of page
	IORM	T3,T1	;set channel 12 "bottom of form" stop
	CALL STORE
	HRLZI	T3,1	;channel 1 mask bit
	MOVEI	T1,0	;set current line pointer to zero
	CALL FETCH
	IORM	T3,T1	;set channel 1 "top of form" stop
	CALL STORE
	MOVEI	T3,356
	MOVE	T4,[POINT 18,VFUBUF+1]	;skip 2 bits
	IDPB T3,T4	;put start byte (356) in first byte position.
	LSH	T2,1	;T2/<form length -1>*2
	ADDI	T2,2	;T2/number of bytes to be sent to printer davfu
	MOVEI	T1,(T2)	;T1/T2
	ADJBP T1,VFUPTR	;T1/byte pointer to last byte to be sent
	MOVEI	T3,357
	PUSH	P,P1
	MOVE	P1,T1
	CALL FWADJ
	IDPB	T3,P1	;put stop byte (357) in last byte position.
	MOVEI	T3,0	;zero T3
	IDPB	T3,P1
	IDPB	T3,P1
	IDPB	T3,P1	;once more just to be sure
	MOVE	P1,T1
	CALL HWADJ
	POP	P,P1
	MOVEI	T1,4(T2);T1/number of bytes to be sent plus 3 (+1)
	LSH	T1,^D16	;T1/number of words to be sent,,?
	HRRI	T1,(T2)	;T1/# of words,,# of bytes
	HLLM T1,VFUBUF	;put number of words in left half of vfubuf
	AOS	0,(P)
 RET ;;;;;;;;	JRST LODVFU	;LOAD THE VFU AND RETURN!


GETCMD:	PXCT 4,[HLLZ T3,(M)]	;T3/cmd op and channel mask,,0
	PXCT 4,[HRRZ T2,(M)]	;T2 will hold "last line"
	MOVE	T1,T2
	LSH	T1,-9	;T1 will hold "current line"
	SUBI	T2,1
	SUBI	T1,1
	ANDI	T2,377	;T2/last line
	ANDI	T1,377	;T1/current line
	RET

ERRSCN:	PUSH	P,M	;save command pointer
	HRLI	M,-MAXC	;M/-max command count,,command pointer
ERRLUP:	CALL GETCMD	;T3/cmd & mask; T1/current line; T2/last line
	CAILE	T2,MAXLN
	  JRST	VFUERR	;last line must be =< max lines in VFUBUF
	CAILE	T1,(T2)
	  JRST	VFUERR	;starting line must be =< last line
	TLNN	T3,(1B1);test 'this is the final command' bit
	  JRST	TSTMAX	;if bit is 0 go see if we should loop again
	TLNN	T3,(1B2);bit 2 must be set for a RELOAD DAVFU cmd
	  JRST	VFUERR	;RESET TOF is an illegal list terminator
	POP	P,M	;restore initial command pointer
	JRST	CPOPJ1	;SKIP RETURN! all commands are good.
TSTMAX:	AOBJN M,ERRLUP	;go check next cmd (unless we've exceeded MAXC)
VFUERR:	POP	P,M	;restore the stack
	RET		;NON-SKIP RETURN! (bad or too many commands)

FETCH:	PUSH	P,P1
	MOVEI	P1,(T1)	;P1/current line
	LSH	P1,1	;P1/byte offset into vfubuf 
	ADDI	P1,2	;skip two for the start byte
	ADJBP P1,VFUPTR	;P1=pointer to byte just before this line's
	PUSH	P,P1
	CALL FWADJ	;pack bytes of 2 words at (P1) left justified
	ILDB	T4,P1	;get channel stops 6-1 of current line
	DPB T4,C6TO1	;deposit them in bits 12-17 of T1.
	ILDB	T4,P1	;get channel stops 12-7 of current line
	DPB T4,C12TO7	;deposit them in bits 6-11 of T1.
	POP	P,P1	;P1/ as it was before the ILDB's
	CALL HWADJ	;re-adjust the bytes into 16 bit half words
	POP	P,P1
	RET

STORE:	PUSH	P,P1
	MOVEI	P1,(T1)
	LSH	P1,1
	ADDI	P1,2	;skip two for the start byte
	ADJBP P1,VFUPTR	;P1=pointer to byte just before this line's
	PUSH	P,P1
	CALL FWADJ	;pack bytes of 2 words at (P1) left justified
	LDB T4,C6TO1	;T4/six bits from T1 positions 12-17
	IDPB	T4,P1	;put chan stops 6-1 for current line in vfubuf
	LDB T4,C12TO7	;T4/six bits from T1 positions 6-11
	IDPB	T4,P1	;put chan stops 12-7 for current line in vfubuf
	POP	P,P1	;P1/ as it was before the ILDB's
	CALL HWADJ	;re-adjust the bytes into 16 bit half words
	ANDI	T1,377	;T1/0,,current line
	POP	P,P1
	RET

FWADJ:	PUSH	P,P1
	CALL FW
	CALL FW		;pack the next word
	POP	P,P1
	RET

HWADJ:	PUSH	P,P1
	CALL HW
	CALL HW		;pack the next word
	POP	P,P1
	RET

FW:	MOVE	T4,(P1)	;T4/ x x 16 bits,, x x 16 bits
	LSH	T4,2
	HLL	T4,(P1)	;T4/ x x 16 bits,,16 bits 0 0
	LSH	T4,2	;T4/ four 8 bit bytes then four bits of 0
	MOVEM	T4,(P1)
	ADDI	P1,1	;increment P1
	RET

HW:	MOVE	T4,(P1)	;T4/ four left justified 8 bit bytes
	LSH	T4,-2
	HLLM	T4,(P1)	;(P1)/ 0 0 16 bits,,xxxxxxxxxxxxxxxxxx
	HRRZ	T4,T4	;zero left half of T4
	LSH	T4,-2	;T4/ 0,,0 0 two bytes
	HRRM	T4,(P1) ;(P1)/ 0 0 two bytes,,0 0 two bytes
	ADDI	P1,1	;increment P1
	RET


VFUPTR:	POINT 8,VFUBUF+1
C6TO1:	POINT 6,T1,17
C12TO7:	POINT 6,T1,11
NEWTOF:	XWD	1,<<.&777>_2>+4	;MOVE TO LPTBUF PAGE IN COMMOD
	BYTE (8) 377,0


VFUBUF:
LPVLPT::VLDATL,,<<.&777>*4>+4	;output pointer to load DAVFUQ
	XWD	356,37437	;start byte,,channels 1-11
	REPEAT <<FS-4>/2>,<	;this gets most of them
	XWD 37037,37037		;generate DAVFU line, channels 2-11
	>;REPEAT
	IFE MOD(FS,2),<XWD 37037,37077	;for even form sizes
			XWD 167400,0>	;for even form sizes
	IFN MOD(FS,2),<XWD 37077,16740>	;for odd form sizes
VLDATL==.-LPVLPT-1
	BLOCK ^D46-VLDATL	;leave room for form sizes up to ~88/GL
OOPS:	EXP -1,-1,-1	;DEB CHECK

	COMMENT %
	This subroutine takes a decimal number from the TTY,
converts it to binary,
and stores the binary number where directed by the first argument of
the argument list.
%
;  REGISTER ASSIGNMENTS

	FLAGS=T1			;Register for flags
	ASCICH=T2		;Accumulator for ASCII character from TTY
	BINARY=T3		;Accumulator for binary number
	CX=T4			;Pointer to argument list

;   Bit assignments

	PRECH=1B35		;Previous character flag
	MINUS=1B34		;Flag for legal minus sign

DECBIN:	TRZ	FLAGS,PRECH!MINUS	;Clear flags to  be used
	MOVEI	BINARY,0	;Clear binary accumulator
NEXTDG:	INCHWL	ASCICH		;Fetch character from TTY
	CAIN	ASCICH,12	;<LF>?
	JRST	[TRNE	FLAGS,MINUS	;IF SO, DONE: TEST FOR MINUS
		MOVNS	BINARY		;IF MINUS, NEGATE BINARY NUMBER
		TRNE	FLAGS,MINUS	;TEST FOR MINUS AGAIN
		JUMPE	BINARY,INPERR	;If -0, probably an error
		MOVEM	BINARY,@(CX)	;STORE @ ARG. LIST 
		AOS	(P)		;SET SKIP RETURN
		POPJ	P,]		;AND DO IT
	CAIN	ASCICH,15	;<CR>?
	JRST	NEXTDG		;IF SO, GET <LF>
	TROE	FLAGS,PRECH	;ELSE SET FLAG TO "previous character"
	JRST	NTFRST		;AND JUMP IF IT WAS ALREADY SET
	CAIN	ASCICH,"-"	;TEST FIRST CHAR. FOR MINUS SIGN
	JRST	[TRO	FLAGS,MINUS	;IF SO, SET MINUS BIT
		JRST	NEXTDG]		;AND GET NEXT CHAR
NTFRST:	CAIL	ASCICH,"0"	;LEGAL DECIMAL DIGIT??
	CAILE	ASCICH,"9"
INPERR:	JRST	[CLRBFI		;If not, clear the rest of the input ...
		OUTSTR	[ASCIZ	"
Sorry, I can't accept that as a number."]
		OUTSTR	[BYTE	(7)15,12,0,0,0]
		POPJ	P,]	;... and take the error return (no skip)
	IMULI	BINARY,^D10	;MULTIPLY BINARY NUMBER BY RADIX
	ADDI	BINARY,-"0"(ASCICH)	;STRIP ASCII BIAS AND ADD DIGIT
	JRST	NEXTDG		;Get next digit


	BINARY=T1
	DIGIT=T2

OCTYPE:
	PUSHJ	P,SPEW
	SKIPE	@2(CX)		;IF ARG3 = 0 SKIP THE CRLF
	  OUTSTR	[BYTE	(7)15,12,0,0,0]
	POPJ	P,


SPEW:	MOVE	BINARY,@(CX)	;Number from argument list
	IDIVI	BINARY,^D8	;Divide by radix 
	HRLM	DIGIT,(P)	;Keep remainder as digit
	SKIPE	BINARY		;If quotient zero, done ...
	PUSHJ	P,SPEW+1	;...else do it again
	HLRZ	BINARY,(P)	;Recover digit
	ADDI	BINARY,"0"	;Add ASCII bias
	OUTCHR	BINARY		;Send to TTY
	POPJ	P,		;Do next until return

QUIKMS:	ASCIZ /
This program allows you to change the FORM LENGTH by reloading the LP20's
Direct Access Vertical Format Unit (DAVFU).  Typeing a ? at this point will
get you a VERY long text describing the details of DAVFU loading.  Typeing
an X allows you to make a trial run without affecting the LP20.
/
RPTMSG:	ASCIZ /
[^C to exit], [? for Documentation], [X for trial run], [RETURN to continue]/

LONGMS:	ASCIZ \

FIRST some notes about this program.  Channels 2-11 of the DAVFU will
have stops set on every line.  A copy of the source of this program
may be useful as an example in setting up more complex formats.  Below
is a document describing how the DAVFU can be reloaded by a program.
This together with a copy of LPTVFU.MAC will allow you to develop and
test a command list to load the DAVFU for any format you want.  This
command list combined with the short program below (and GD licence)
is all you need.

  |	NOTE: The bit in channel 12 (Lines Per Page) seems to have no
  |	effect on the number of lines printed on a page.  However some
  |	DEC documentation insists that it is necessary.  Set Lines Per
  |	Page equal to Formlength unless you really want to experiment.


START:
	MOVE	P,PDL
	OPEN 9,[
	0
	SIXBIT /LPT/ 
	0]
	  JRST OERR		;OPEN ERROR
;;;	CALL UINPUT		;COMMENT OUT FOR FIXED COMMAND LISTS
	MTAPE 9,CLIST		;Command list FormSize,LinesPerPage
	  JRST BADCMD		;ERROR IN COMMAND LIST
	OUTSTR [ASCIZ /
	DAVFU HAS BEEN RELOADED!/]
	EXIT
BADCMD:	OUTSTR [ASCIZ /
	ERROR IN COMMAND LIST.  ABORTING... /]
	EXIT
OERR:	OUTSTR	[ASCIZ /
	Sorry, the LP20 isn't available to you now./]
	EXIT
CLIST:	;INSERT YOUR COMMAND LIST HERE!

PDL:	XWD -PDLLEN,PDL+1
	BLOCK 30
	PDLLEN==.-PDL-1
	
	END START


			USER CONTROL OF THE LP20 DAVFU

	To provide user control of form size and other features of the
	LP20 line printer's Direct Access Vertical Format Unit (DAVFU)
	the MTAPE UUO is being added to the 2020 version of LPTSER.

	The MTAPE UUO's function field will point to a command list
	which allows the user to set or reset stops on any of the 12
	control channels and in the final command to specify form size
	and page length and initiate the actual reloading of the DAVFU.

	There are four command types; Each command comprises one PDP-10
	word.  Two of the commands allow stops to be set or reset in
	any combination of channels over any range of consecutive lines
	or on a single line.  The final command of a command list must
	be the RELOAD DAVFU command.  This command specifies the form
	size and page length; It's execution sets the top of form stop
	in channel 1 and the bottom of form stop in channel 12 and then
	starts the actual loading of the DAVFU from the buffer that has
	been created (or just modified) by the command list.  The fourth
	command type is RESET TOF.  It must be the first and only word
	in a command list to reset the Top of Form to the current paper
	position.  RESET TOF is provided for completeness only.  It's
	use is not reccomended.

	Under certain conditions the Line Printer driver can decide to
	refresh the LP20's DAVFU.  This makes it necessary to always
	have a buffer available containing good DAVFU data.  There is
	no way that the DAVFU might be refreshed in the middle of
	performing the MTAPE UUO; However, an error in the command list
	causing an immediate error return could leave a half baked DAVFU
	buffer laying around somewhere.  Rather than using two buffers
	an initial pass is made thru the command list checking for
	errors before actually processing any commands which alter the
	buffer.

	Of course the user is responsible for creating a good command
	list.  The main precaution a user should take is to clear all
	stops in channels 1 and 12 just before giving the RELOAD DAVFU
	command.  This could be automatic but someone will surely want
	to get fancy.  Clearing channels 1 and 12 can be accomplished
	in one command [004001,,001104].

	A detailed description of the commands follows:



  OPCODE 			COMMAND
  -----	  -------------------------------------------------------------
   0 0	  CLEAR STOPS ON MASKED CHANNELS from first line to last line.
   0 1	  SET STOPS ON MASKED CHANNELS from first line to last line.
   1 0	  RESET TOP OF FORM
   1 1	  RELOAD DAVFU and terminate command list


		     C O M M A N D     B I T S
____________________________________.____________________________________
|      		     1 1 1 1 1 1 1 1 1 1 2 2 2 2 2 2 2 2 2 2 3 3 3 3 3 3|
|0 1 2 3 4 5 6 7  1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5|
|_.___._____._______________________|_._______________._._______________|
|x|0 0|x x x|     channel mask      |x|  frist line   |x|   last line   |
|_|___|_____|_______________________|_|_______________|_|_______________|
|x|0 1|x x x|     channel mask      |x|  first line   |x|   last line   |
|_|___|_____|_______________________|_|_______________|_|_______________|
|x|1 0|x x x|x x x x x x x x x x x x|x|0 0 0 0 0 0 0 1|x|0 0 0 0 0 0 0 1|
|_|___|_____|_______________________|_|_______________|_|_______________|
|x|1 1|x x x|x x x x x x x x x x x x|x|  page length  |x|   form size   |
|_|___|_____|_______________________|_|_______________|_|_______________|
  |CMD|	    |1 1 1                  |
  +---+	    |2 1 0 9 8 7 6 5 4 3 2 1|
            |   CHANNEL  NUMBERS    |
	    +-----------------------+

  NOTE:	CHANNEL NUMBERS INCREASE FROM RIGHT TO LEFT.
	LAST LINE AND FORM SIZE MUST BE .LE. 143 DECIMAL
	FIRST LINE MUST BE .GT. 0 BUT .LE. LAST LINE
	PAGE LENGTH MUST BE .GT. 8 BUT .LE. FORM SIZE
	IF RESET TOF COMMAND IS USED IT MUST BE THE ONLY CMD IN THE LIST
	FIRST LINE & LAST LINE FIELDS OF RESET TOF SHOULD BE SET TO ONE


\
	END	START
   t@1™