SUBTTL	Initialization  Joe Smith @ CSM /JMS

	SEARCH	SYSUNV
	SALL

$BEGIN	(<REM - Program for the GIGI>,REM,1(4),<TOPS10,SCAN,TYPER,VERSION>)

;Definitions

	%0==0		;I/O channel used by WILD
	TTY==1		;I/O channel to terminal
	DSKI==2		;Input channel to disk
	DSKO==3		;Output channel to disk

	LF=.CHLFD	;Linefeed
	CR=.CHCRT	;Carriage return
	ESC=.CHESC	;ESCape character

	HOUR=1B17/^D24	;One hour in UDT format
	MINUTE=HOUR/^D60
	SECOND==MINUTE/^D60


;Flag bits in F

      $FLAGS	F.
	$FLAG	2H2W	;Print double height double width (GIGI only)
	$FLAG	LF	;Previous character was a linefeed
	$FLAG	PLOT	;Output is going to the DMP4R plotter
	$FLAG	GO	;Print screen image when done or run program when done
	$FLAG	SEQ	;Add seqence number bits
	$FLAG	WAIT	;OK for DOPRMT to wait for GIGI to say "REM SAVE"
SUBTTL	Revision history, contents

; 1	 8-Jun-83  JMS	Created
; 2	10-Jul-83  JMS	Finished OLD and SAVE commands.
; 3	26-Aug-83  JMS	Add TYPE command to activate VT102 printer port.
; 4	16-Dec-83  JMS	Look in GGL: if file not found on DSK:.
; 5	16-Dec-83  JMS	Add RUN command to run load and run GGB program.
;
;End of REVISION HISTORY


;      Table of Contents for REM - REMote computer interface
;
;
;			   Section			      Page
;
;    1. Revision history, contents . . . . . . . . . . . . . .   2
;    2. SCAN blocks, list of commands  . . . . . . . . . . . .   3
;    3. Main program . . . . . . . . . . . . . . . . . . . . .   4
;    4. Main command subroutines . . . . . . . . . . . . . . .   6
;    5. File name routines
;         5.1   Do LOOKUP and/or ENTER . . . . . . . . . . . .   7
;         5.2   Get name from SCAN . . . . . . . . . . . . . .   8
;    6. Data area  . . . . . . . . . . . . . . . . . . . . . .   9
SUBTTL	SCAN blocks, list of commands

	$HIGH

ISCNBK:	IOWD	CMDLEN,CMDTAB		;Monitor level commands
	XWD	OFFSET,TP.PFX		;Starting offset, CCL name
	XWD	0,0			;Output,,input routines
	XWD	0,0			;No indirect
	XWD	DOPRMT,0		;Prompt routine, MONRT.
ISCSIZ==.-ISCNBK

VSCNBK:	IOWD	VERBSL,VERBSN		;Length, addr of names of verbs
	XWD	VERBSD,VERBSM		;Defaults, routines
	XWD	HLPBLK,VERBSP		;Help text, pointers
	EXP	-1			;Standard HLP:REM.HLP
VSCSIZ==.-VSCNBK

ISCNPT:	XWD	ISCSIZ,ISCNBK		;ISCAN pointer for monitor commands
VSCNPT:	XWD	VSCSIZ,VSCNBK		;VSCAN pointer for verbs
HLPBLK:	XWD	-HLPSIZ,HLPMSG		;Pointer to help texts

CMDTAB:					;Table of monitor level commands
GIGCMD:	SIXBIT	/GIGI/		;User commands, such as "GIGI SHOW FOOBAR.PIC"
OKCMD:	SIXBIT	/OK/		;When GIGI is in Remote-Basic mode
REMCMD:	SIXBIT	/REM/		;From GIGI, such as "REM HOST" or "REM OLD FOO"
RDYCMD:	SIXBIT	/READY/		;From other versions of BASIC
CMDLEN==.-CMDTAB

	FILRQ==FS.VRQ		;File name is required
	FILRQ==0	;*HACK*	;Until VSCAN/PSCAN is fixed

DEFINE	SWTCHS,<
SP (BASIC ,,$BASIC,,     )	;Set terminal to BASIC mode
SP (DOUBLE,,$DOUBL,,FILRQ)	;Print double size on LA34
SP (GIGI  ,,$GIGI ,,FILRQ)	;Read next word for actual command
SP (HOST  ,,$HOST ,,     )	;"Now connected to TOPS-10"
SP (LOAD  ,,$LOAD ,,FILRQ)	;Loads BASIC program into the GIGI
SP (OK    ,,$OK   ,,     )	;Response from GIGI, null command
SP (OLD   ,,$OLD  ,,FILRQ)	;Request from GIGI to load BASIC program
SP (PRINT ,,$PRIN ,,FILRQ)	;Display PIC file and send to Graphics Printer
SP (PLOT  ,,$PLOT ,,FILRQ)	;Send PIC file to DMP4R plotter
SP (READY ,,$OK   ,,     )	;Another null command from BASIC
SP (REM   ,,$REM  ,,FILRQ)	;Read next word for actual command
SP (RUN   ,,$RUNIT,,FILRQ)	;Loads BASIC program into the GIGI and run it
SP (SAVE  ,,$SAVE ,,FILRQ)	;Save BASIC program on the DEC-10
SP (SEQUEN,,$SEQ  ,,FILRQ)	;Set LSN bits so SOS can edit the BASIC program
SP (SHOW  ,,$SHOWX,,FILRQ)	;Display PIC file on GIGI or VT125
SP (STORE ,,$STORE,,FILRQ)	;Accept a data file from a Personal Computer
SP (TYPE  ,,$TYPEX,,FILRQ)	;Send file to LA50 on PC350 or VT102
SP (UNSEQU,,$UNSEQ,,FILRQ)	;Clear LSN bits so SED can edit BASIC programs
>
PAGE
	DOSCAN	(VERBS)			;Create the SCAN tables



HLPMSG:	$BASIC,,[ASCIZ	~GIGI-BASIC commands~]
	$DOUBL,,[ASCIZ	~PIC file name~]
	 $GIGI,,[ASCIZ	~DOUBLE, PLOT, PRINT, SHOW~]
	 $HOST,,[ASCIZ	~~]
	 $LOAD,,[ASCIZ	~GIGI-BASIC file name~]
	   $OK,,[ASCIZ	~~]		;READY is same as OK
	  $OLD,,[ASCIZ	~GIGI-BASIC file name~]
	 $PLOT,,[ASCIZ	~PIC file name~]
	 $PRIN,,[ASCIZ	~PIC file name~]
	  $REM,,[ASCIZ	~HOST, LOAD, OLD, SAVE, SEQ, STORE, UNSEQ~]
	$RUNIT,,[ASCIZ	~GIGI-BASIC file name~]
	 $SAVE,,[ASCIZ	~GIGI-BASIC file name~]
	  $SEQ,,[ASCIZ	~File without LSN bits~]
	$SHOWX,,[ASCIZ	~PIC file name~]
	$TYPEX,,[ASCIZ	~Listing file name~]
	$STORE,,[ASCIZ	~Data file name~]
	$UNSEQ,,[ASCIZ	~File with LSN bits~]
HLPSIZ==.-HLPMSG

SUBTTL	Main program


START:	TDZA	T1,T1			;Normal entry
	 MOVEI	T1,1			;CCL entry
	MOVEM	T1,OFFSET
	RESET
	MOVE	P,[IOWD PDLEN,PDL]
	SKIPE	T1,.JBHGH+.JBHNM	;Get HISEG name
	 SETNAM	T1,			;Clear JACCT for now

	MOVSI	T1,TP.PFX		;Get the prefix
	$TYPES	(INI)			;Initialize TYPER
	PUSHJ	P,GETTTY		;INIT TTY with no echo
	SETZ	F,			;Clear all flag bits

;Check monitor command

	MOVE	T1,ISCNPT		;Point to ISCAN's data
	PUSHJ	P,.ISCAN##		;Initialize SCANner
	MOVEM	T1,COMDSP		;-1 if RUN command
	SKIPGE	T1			;If it was a RUN command,
	 TXO	F,F.WAIT		; then it is OK to wait

	CAIE	T1,RDYCMD-CMDTAB	;Did some other computer say "Ready"?
	CAIN	T1,OKCMD-CMDTAB		;Did GIGI-BASIC send us an "OK"?
	 JRST	EATCMD			;Yes, ignore it.

MAINLP:	MOVE	T1,VSCNPT		;Get VSCAN/PSCAN pointer
	PUSHJ	P,.PSCAN##		;Set up partial scanner
	  PUSHJ	P,DOPRMT		;Must have keyword
	JRST	MAIN00			;Skip call to QSCAN

;Here when expecting the GIGI to say "REM SAVE FOOBAR" after a REM LOAD

MAIN:	MOVX	F,F.WAIT		;Clear all flags but OK-to-wait
	SETZ	T1,			;Use the previous command table
	PUSHJ	P,.QSCAN##		;Get another partial command
	  PUSHJ	P,DOPRMT		;No input, call the prompt routine

MAIN00:	PUSHJ	P,.KEYWD##		;Process a command
	  SKIPA				;Non-alphameric, may be Control-Z
	JRST	MAIN			;Loop
	JUMPLE	C,MAIN			;Ignore blank lines

DOEXIT:	PUSHJ	P,.MONRT##		;End of command, exit to monitor
	JRST	MAINLP			;In case of .CONTINUE
SUBTTL	Prompt routine

GETTTY:	OPEN	TTY,[IO.SUP!.IOASC	;Suppress echo
		     SIXBIT /TTY/	; on terminal
		     TOBUF,,TIBUF]	;Buffer headers
	  $TYPE	(F,CIT,$NON,<Cannot init TTY>,,A)
	POPJ	P,


DOPRMT:	TXNN	F,F.WAIT		;OK to wait for "REM SAVE"?
NOCMD:	 $TYPE (F,M2H,$NON,<Missing 2nd half of command>,,DOEXIT)
	MOVE	T2,[%CNDTM]		;Get date/time
	GETTAB	T2,
	  JFCL

;Prevent CSMCPD(JOBMON) from killing this job while waiting for "REM SAVE"

DOPRM1:	SKPINL				;Has a command come in yet?
	  SKIPA				;No
	 POPJ	P,			;Yes
	MOVE	T1,[%CNDTM]		;Get date/time
	GETTAB	T1,
	  JFCL
	SUB	T1,T2			;Find elapsed time
	CAXL	T1,2*HOUR		;Been waiting 2 hours?
	 JRST	GOODBY			;Yes, KJOB
	MOVE	T1,[HB.RTL+^D10000]	;Wait for 10 secs
	HIBER	T1,
	  JFCL
	JRST	DOPRM1			;Wait for REM SAVE after REM LOAD

GOODBY:	MOVEI	T1,[SIXBIT /SYS/
		    SIXBIT /LOGOUT/
		    EXP 0,0,0,0]
	RUN	T1,
	  HALT	GOODBY
SUBTTL	GIGI and REM commands


$GIGI:	SKIPA	T1,[GIGCMD-CMDTAB]	;GIGI command
$REM:	MOVEI	T1,REMCMD-CMDTAB	;REM command
	MOVEM	T1,COMDSP
	TXO	F,F.WAIT		;OK to wait for command

;There is a slight problem here, PSCAN versus top-level VSCAN

REM1:	PUSHJ	P,.KEYWD##		;Check next keyword
	 $TYPE	(F,ILC,$FUN,<Illegal character |>,,,,P4)
	JUMPLE	C,REM1			;Loop on blank lines
	JRST	.POPJ1##


SUBTTL	HOST, OK, and READY commands


$HOST:	MOVEI	T1,5-1			;Do all 5 words of the CONFIG name
HOST1:	MOVEI	T2,.GTCNF		;Config table
	HRL	T2,T1			;Index
	GETTAB	T2,			;Get word
	  MOVX	T2,<ASCII/??/>
	MOVEM	T2,CONFIG(T1)		;Store
	SOJGE	T1,HOST1		;Get all 5
	MOVEI	T1,CONFIG		;Point to ASCIZ
	$TYPE	(N,,$ASC,<Now connected to |>)
$OK:	JRST	EATCMD			;Ignore "READY" or "OK" messages
SUBTTL	Commands -- Display PICture file

$DOUBL:	TXO	F,F.2H2W		;Print double size
$PRIN:	TXOA	F,F.GO			;Print screen image afterwards
$PLOT:	 TXO	F,F.PLOT		;Use DMP-4R commands
$SHOWX:	AOS	(P)			;Make for skip return
	MOVSI	T1,'PIC'
	PUSHJ	P,GTIFIL		;Open PICture file for input

	MOVEI	T1,INTBLK		;Set Control-C intercept
	MOVEM	T1,.JBINT
	MOVEI	T1,GRESET		;Routine to go to on Control-C
	MOVEM	T1,ABORTR

	TXNE	F,F.PLOT		;Going to DMP4R plotter?
	 OUTSTR	DMPON			;Yes, turn it on
	TXNN	F,F.PLOT		;Going to GIGI or VT125?
	 OUTSTR	GON$			;Yes, turn it on
	OUTSTR	RGSINI			;Initialize ReGIS

	PUSHJ	P,TYPFIL		;Send the file to the graphics terminal

	TXZE	F,F.2H2W		;Double size copy?
	 JRST	DOUBL			;Yes, requires special handling
	TXZE	F,F.GO			;Print the screen image?
	 OUTSTR	PRISCN			;Yes, activate the Graphics Printer
	PUSHJ	P,GRESET		;Reset GIGI to ASCII mode

ENDCMD:	SKIPL	COMDSP		;*HACK*	;Started by monitor level command?
	 JRST	DOEXIT		;*HACK*	;Yes
	JRST	.POPJ1##


GRESET:	OUTSTR	GOFF$			;Turn off ReGIS
	TXZE	F,F.PLOT		;Was the DMP4R turned on?
	 OUTSTR	DMPOFF			;Yes, turn if off
	OUTSTR	LINE23			;Position text cursor on line 23
	POPJ	P,			;Return to VSCAN

DOUBL:	OUTSTR	HC2X2			;Load 8085 program into font #2
	OUTSTR	HOME			;Make sure cursor is reasonable
	OUTSTR	BASIC2			;Turn on BASIC to host
	PUSHJ	P,CLRIB			;Clear out possible "OK"
	OUTSTR	USR1			;Start the program
	OUTSTR	BASIC0			;Turn off BASIC
	TXZ	F,F.GO			;Screen has been printed

DOUBL1:	INCHWL	T1			;Wait for response
	CAIE	T1,"1"			;Got the response to PRINT USR(1)?
	 JRST	DOUBL1			;No, get it
					;Fall into CLRIB

CLRIB:	MOVEI	T1,1			;Wait 1 second
	SLEEP	T1,
	CLRBFI				;Clear input buffer of "OK"
	POPJ	P,
SUBTTL	Type a file on the printer port

AUTOON:	BYTE (7) ESC,"[","?","5","i",0	;Auto print, a line at a time
AUTOOF:	BYTE (7) ESC,"[","?","4","i",0	;Auto print off
PRTCUR:	BYTE (7) ESC,"[","?","1","i",0	;Print line cursor is on
PRTSCN:	BYTE (7) ESC,"[","i",0		;Print screen image
PORTON:	BYTE (7) ESC,"[","5","i",0	;Send to printer port, not screen
PORTOF:	BYTE (7) ESC,"[","4","i",0	;Send to screen, not printer port

$TYPEX:	AOS	(P)			;Make for skip return
	MOVSI	T1,'LST'
	PUSHJ	P,GTIFIL		;Open listing file for input

	MOVEI	T1,INTBLK		;Set Control-C intercept
	MOVEM	T1,.JBINT
	MOVEI	T1,PRESET		;Routine to go to on Control-c
	MOVEM	T1,ABORTR

	PUSHJ	P,SETFRM		;SET TTY FORM NO CRLF
	OUTSTR	PORTON			;Enable printer port

	PUSHJ	P,TYPFIL		;Send the file to the graphics terminal

	PUSHJ	P,PRESET		;Reset printer port
	JRST	ENDCMD			;Finish up

PRESET:	OUTSTR	PORTOF			;Turn off printer port
;*;	PUSHJ	P,RESFRM		;Reset TTY settings
	POPJ	P,

SETFRM:	MOVX	T1,.TOFRM+.TOSET	;Change formfeed setting
	SETO	T2,			;This TTY
	MOVEI	T3,1			;Enable output of formfeeds
	MOVE	T4,[3,,T1]
	TRMOP.	T4,			;.SET TTY FORMS
	  JFCL
	MOVX	T1,.TONFC+.TOSET	;Change free CRLF setting
	SETO	T2,			;This TTY
	MOVEI	T3,1			;No free ones
	MOVE	T4,[3,,T1]
	TRMOP.	T4,			;SET TTY NO CRLF
	  JFCL
	POPJ	P,

;TYPFIL - Copy DSK file to the TTY

TYPFIL:	PUSHJ	P,DSKIBT		;Get a byte
	  PJRST	TTYOBF			;EOF
	PUSHJ	P,TTYOBT		;Output it to the terminal
	JRST	TYPFIL

ABORT:	PUSHJ	P,@ABORTR		;Reset the terminal to normal mode
	MONRT.				;Abort on Control-C
	EXIT
SUBTTL	Commands -- Get/put GIGI-BASIC files

;GIGI BASIC		!User command to set GIGI to BASIC mode
;REM LOAD PROG.GGB	!User command to load a GIGI-BASIC program
;REM RUN PROG.GGB	!User command to load and run a GIGI-BASIC program
;REM OLD PROG.GGB	!Command from GIGI-BASIC to load program
;REM SAVE PROG.GGB	!Command from GIGI-BASIC to store program


$RUNIT:	TXOA	F,F.GO			;Run program when done
$LOAD:	 TXZ	F,F.GO			;Load program but do not run it
	MOVSI	T1,'GGB'
	PUSHJ	P,GTIFIL		;Open BASIC file for input

	OUTSTR	BASIC2			;Turn on BASIC to host
	OUTSTR	[ASCIZ /NEW
/]					;Clear any existing program

;It would be nice to send something to the screen during the load process
	PUSHJ	P,TYPFIL		;Send it to the GIGI

	MOVEI	T1,FLPBLK		;Point to FILOP block
	$TYPE	(N,,$LKP,<PRINT #1,"Loaded |">)
	TXZN	F,F.GO			;Run program?
	 JRST	$BASIC			;No, just start BASIC
	OUTSTR	BASICR			;Yes, activate local BASIC and RUN
	JRST	.POPJ1##

$BASIC:	OUTSTR	BASIC1			;Connect BASIC to the keyboard
	JRST	.POPJ1##


;When the command 'OLD "NONAME"' is given to MICROSOFT BASIC, it does an implied
;'NEW' to clear memory, sends 'REM OLD NONAME' to the host, and goes into
;receive mode.  The first statement without a line number terminates receive
;mode.  The line 'PRINT "Loaded file"' does this.

$OLD:	MOVSI	T1,'GGB'
	PUSHJ	P,GTIFIL		;Open BASIC file for input

	PUSHJ	P,TYPFIL		;Send it to the GIGI

	MOVEI	T1,FLPBLK		;Point to FILOP block
	$TYPE	(N,,$LKP,<PRINT "Loaded |">,,P1)



$SAVE:	PUSHJ	P,LOGINP		;Must be logged in to write files
	MOVSI	T1,'GGB'
	PUSHJ	P,GTOFIL		;Open BASIC file for output
	TXO	F,F.LF			;Start of new line

	OPEN	TTY,[.IOPIM		;Packed Image Mode to input ^C
		SIXBIT /TTY/
		0,,TIBUF]		;Input only
	  $TYPE	(N,CIT)

;Tell Monitor to give full buffers, not just a single character

	MOVEI	T1,.TOPBS+.TOSET	;Set the PIM break set
	SETO	T2,			;This terminal
	MOVE	T3,[BYTE(9)LF,.CHCNC,.CHCNZ,LF+200]
	MOVE	T4,[3,,T1]		;Point to args
;*;	TRMOP.	T4,			;Break on LF, ^C, or ^Z
;*;	  $TYPE	(N,CSB)

SAVE1:	PUSHJ	P,TTYIBT		;Get byte from terminal
	CAIE	T1,.CHCNC		;Control-C?
	CAIN	T1,.CHCNZ		; or Control-Z?
	 JRST	SAVE2			;Yes, premature end of file
	TXNE	F,F.LF			;At start of line?
	CAIE	T1,"R"			;And "R" from "REM"?
	 SKIPA				;No
	  JRST	SAVE2			;Yes, end of save
	CAIN	T1,LF			;Linefeed?
	 TXOA	F,F.LF			;Yes
	  TXZ	F,F.LF			;No
	PUSHJ	P,DSKOBT		;Send byte to the disk file
	JRST	SAVE1

SAVE2:	CLRBFI				;Clear rest of input buffer
	SKIPE	T1,FIVEWD		;If part of a word left over,
	 PUSHJ	P,DSKOWD		;Output it

	PUSHJ	P,DSKOBF		;Output last buffer
	CLOSE	DSKO,			;Close the file

;Reset terminal and output message

	OPEN	TTY,[IO.SUP!.IOASC	;Suppress echo
		     SIXBIT /TTY/	; on terminal
		     TOBUF,,0]		;Output buffer only
	  $TYPE	(N,CIT)
	MOVEI	T1,SUPBLK		;Point to FILOP block
	$TYPE	(N,,$ENT,<PRINT "Saved |">,,P1)
SUBTTL	Commands -- Add/remove PDP-10 sequence bits

$SEQ:	TXO	F,F.SEQ			;Add bits
$UNSEQ:	PUSHJ	P,LOGINP		;Must be logged in to write files
	MOVSI	T1,'GGB'
	PUSHJ	P,GTXFIL		;Open BASIC file for I/O
	OUTSTR	[ASCIZ	/?REM - SEQ and UNSEQ not implemented/]
	EXIT
SUBTTL	Commands -- Store data file on the DECsystem-10

	EOM=.CHCNY			;^Y = End of Message
	EOF=.CHCNZ			;^Z = End of File

$STORE:	PUSHJ	P,LOGINP		;Must be logged in to write files
	PUSHJ	P,.SAVE1##		;Preserve P1
	MOVSI	T1,'DAT'
	PUSHJ	P,GTOFIL		;Open DATa file for output

;Use PIM to input Control-C and such

	OPEN	TTY,[.IOPIM		;Packed Image Mode to input ^C
		SIXBIT /TTY/
		0,,TIBUF]		;Input only
	  $TYPE	(N,CIT)

;Tell Monitor to give full buffers, not just a single character

	MOVEI	T1,.TOPBS+.TOSET	;Set the PIM break set
	SETO	T2,			;This terminal
	MOVE	T3,[BYTE(9)EOM,EOF,EOM+200,EOF+200]
	MOVE	T4,[3,,T1]		;Point to args
	TRMOP.	T4,			;Break on ^Y or ^Z
	  $TYPE	(W,CSB,$NON,<Cannot set PIM break-set on TTY>)

;Enable receipt of Control-S, Control-Q

	MOVEI	T1,.TOPAG		;Read the PAGE bit
	SETO	T2,			;This terminal
	MOVE	P1,[2,,T1]		;Point to args
	TRMOP.	P1,
	  MOVEI	P1,1			;Assume it is on
	MOVEI	T1,.TOPAG+.TOSET	;Now change the bit
	MOVEI	T3,0			; to zero
	MOVE	T4,[3,,T1]
	TRMOP.	T4,
	  JFCL

STORE1:	PUSHJ	P,TTYIBT		;Get a byte from the terminal
	CAIE	T1,EOF			;End Of File?
	CAIN	T1,EOM			;End Of Message?
	 JRST	STORE2			;Yes, close file
	PUSHJ	P,DSKOBT		;Send byte to the disk file
	JRST	STORE1

STORE2:	SKIPE	T1,FIVEWD		;If part of a word left over,
	 PUSHJ	P,DSKOWD		;Output it
	PUSHJ	P,DSKOBF		;Output last buffer
	CLOSE	DSKO,			;Close the file

;Restore XON/XOFF

	MOVEI	T1,.TOPAG+.TOSET	;Change the bit
	SETO	T2,			;This terminal
	MOVE	T3,P1			;To its old value
	MOVE	T4,[3,,T1]
	TRMOP.	T4,
	  JFCL

;Reset terminal and output message

	OPEN	TTY,[IO.SUP!.IOASC	;Suppress echo
		     SIXBIT /TTY/	; on terminal
		     TOBUF,,0]		;Output buffer only
	  $TYPE	(N,CIT)
	MOVEI	T1,SUPBLK		;Point to FILOP block
	$TYPE	(N,,$ENT,<Stored in file |>)
	PJRST	ENDCMD
SUBTTL	File name routines -- Do LOOKUP and/or ENTER

;Routine to get an input file spec and LOOKUP file

GTIFIL:	MOVE	T2,[FLPINI,,FLPBLK]	;Init FILOP block
	BLT	T2,FLPBLK+FLPINL

	HRRI	T1,OPENI		;Point to OPEN block
	PUSHJ	P,GETFIL		;Get file name

	MOVEI	T1,FLPBLK		;For $TYPE
	MOVE	T2,[.FOLEN,,FLPBLK]	;Point to args
	FILOP.	T2,			;LOOKUP input file
	 SKIPA
	  POPJ	P,			;Found it
	JUMPN	T2,GTIERR		;Abort if anything but FILE NOT FOUND
	HLRZ	T2,FLPBLK+.FODEV	;Not found, get device name
	CAIE	T2,'DSK'		;Default?
	 JRST	GTIERR			;No, not found with explicit device
	MOVSI	T2,'GGL'		;Yes, try the GIGI Library device
	MOVEM	T2,FLPBLK+.FODEV
	MOVEI	T1,FLPBLK		;For $TYPE
	MOVE	T2,[.FOLEN,,FLPBLK]	;Point to args
	FILOP.	T2,			;LOOKUP input file
	 SKIPA				;Not there either
	  POPJ	P,
	MOVSI	T2,'DSK'		;Reset to original device name
	MOVEM	T2,FLPBLK+.FODEV
GTIERR:	$TYPE	(F,LKP,$ERR,<LOOKUP error, |>,,DOEXIT)

FLPINI:	DSKI,,.FORED			;Read file on DSK channel
	.IOASC				;Normal ASCII mode
	SIXBIT /DSK/			;This gets updated by .STOPB
	0,,DIBUF			;Input buffer header
	0,,2				;2 input buffers
	0,,LKPBLK			;Pointer to LOOKUP block
	.PTMAX,,ACTPTH			;Actual PATH after LOOKUP succeeds
	0,,0				;No PPN
FLPINL==.-FLPINI


;Routine to get an output file spec and ENTER file

GTOFIL:	MOVE	T2,[SUPINI,,SUPBLK]	;Init FILOP block
	BLT	T2,SUPBLK+SUPINL

	HRRI	T1,OPENO		;Point to OPEN block
	PUSHJ	P,GETFIL		;Get file name

	MOVEI	T1,SUPBLK		;For $TYPE
	MOVE	T2,[.FOLEN,,SUPBLK]	;Point to args
	FILOP.	T2,			;ENTER output file
	  $TYPE	(F,ENT,$ERR,<ENTER error, |>,,DOEXIT)
	MOVE	T1,[POINT 7,FIVEWD]	;Reset byte pointer
	MOVEM	T1,FIVEBP
	SETZM	FIVEWD
	POPJ	P,

SUPINI:	DSKO,,.FOWRT			;Supersede file on SUP channel
	.IOIBN				;Binary mode (LSN bits are set)
	SIXBIT /DSK/			;This gets updated by .STOPB
	DOBUF,,0			;Output buffer header
	2,,0				;2 output buffers
	0,,LKPBLK			;Pointer to ENTER block
	0,,0				;Actual PATH to file
	0,,0				;No PPN
SUPINL==.NI
SUBTTL	File name routines -- Get name from SCAN

GETFIL:	MOVEM	T1,DEFEXT		;Set default extension and address
	JUMPLE	C,[$TYPE (F,FMS,$NON,<File name must be specified>,,EATCMD)]
	PUSHJ	P,.FILIN##		;Input a file name
	MOVEI	T1,LKPLEN		;Set extended LOOKUP block
	MOVEM	T1,LKPBLK

	MOVEI	T1,FILBLK		;Addr of block
	MOVEI	T2,.FXLEN		;Length
	PUSHJ	P,.GTSPC##		;Get file spec

	MOVE	T1,DEFEXT		;Get default extension
	SKIPN	FILBLK+.FXEXT		;If no extension supplied,
	 HLLOM	T1,FILBLK+.FXEXT	; use the default

	MOVE	T1,[.FXLEN,,FILBLK]	;SCAN block
	HRRZ	T2,DEFEXT		;OPEN block (in FLPBLK or SUPBLK)
	MOVE	T3,[LKPLEN,,LKPBLK]	;LOOKUP/ENTER block
	MOVEI	T4,PTHBLK		;PATH block
	PUSHJ	P,.STOPB##		;Set up open block
	  $TYPE	(F,WCI,$SCN,<Wildcards illegal in |>,,P,,[FILBLK])
	POPJ	P,


;Routine to open a file for input and output

GTXFIL:	MOVE	T2,[FLPINI,,FLPBLK]	;Init FILOP block
	BLT	T2,FLPBLK+FLPINL
	MOVE	T2,[SUPINI,,SUPBLK]	;Init FILOP block
	BLT	T2,SUPBLK+SUPINL

	HRRI	T1,OPENI		;Point to LOOKUP block
	PUSHJ	P,GETFIL		;Get file name

	MOVEI	T1,FLPBLK		;For $TYPE
	MOVE	T2,[.FOLEN,,FLPBLK]	;Point to args
	FILOP.	T2,			;LOOKUP input file
	  $TYPE	(N,LKP)

	MOVE	T1,ACTPTH+.PTFCN	;Get actual device name
	MOVEM	T1,SUPBLK+.FODEV
	MOVEI	T1,ACTPTH		;Use actual directory
	MOVEM	T1,LKPBLK+.RBPPN	; in ENTER block

	MOVEI	T1,SUPBLK		;For $TYPE
	MOVE	T2,[.FOLEN,,SUPBLK]	;Point to args
	FILOP.	T2,			;Supersede output file
	  $TYPE	(N,ENT)
	POPJ	P,
SUBTTL	Misc - LOGINP and EATCMD

;Routine to check if job is logged in

LOGINP:	PUSHJ	P,.ISLGI##		;Check if logged in
	  $TYPE	(F,MLI,$NON,<Must be logged in to write on DSK:>,,DOEXIT)
	POPJ	P,			;OK


;Eat the linefeed that comes after the CR, then exit to Monitor level

EATCMD:	MOVX	T1,^D250		;Wait 1/4 second
	HIBER	T1,
	  JFCL

EATCM1:	SKPINL				;Let monitor know we are expecting more
	  JFCL				; and allow deferred echo
	INCHRS	T1			;Get a char
	 JRST	EATCM2			;No more
	JRST	EATCM1			;Loop till end of command from GIGI

EATCM2:	PUSHJ	P,.ISLGI##		;Is job logged in?
	  OUTCHR ["."]			;No, type just a dot (not "KJOB")
	MONRT.				;Quiet exit
	JRST	MAIN
SUBTTL	Byte I/O routines
;The byte is transferred to/from T1

;Disk input byte, error return on EOF

DSKIBT:	SOSGE	DIBUF+.BFCTR		;Any more bytes?
	 JRST	DSKIBF			;Get another disk input buffer
	ILDB	T1,DIBUF+.BFPTR		;Yes, get one
	JRST	.POPJ1##

;Disk input buffer

DSKIBF:	IN	DSKI,			;Get another buffer
	  JRST	DSKIBT			;OK
	GETSTS	DSKI,T1			;Get error status
	TXNN	T1,IO.ERR		;Any real errors?
	 POPJ	P,			;No, just EOF
	$TYPE	(F,DIE,$OCT,<Disk input error |>,,P)

;Disk output byte

DSKOBT:	PUSH	P,T1			;Save char
	IDPB	T1,FIVEBP		;Pack in word
	MOVE	T1,FIVEBP		;Get byte pointer
	TLNE	T1,760000		;Word full?
	 JRST	DSOBT1			;No
	MOVE	T1,FIVEWD		;Yes, get the packed word
	PUSHJ	P,DSKOWD		;Output it
	MOVE	T1,[POINT 7,FIVEWD]	;Reset byte pointer
	MOVEM	T1,FIVEBP
	SETZM	FIVEWD

DSOBT1:	POP	P,T1			;Restore char
	POPJ	P,

;Disk output word

DSKOWD:	SOSG	DOBUF+.BFCTR		;Any room?
	  PUSHJ	P,DSKOBF		;No, make some
	IDPB	T1,DOBUF+.BFPTR		;Store byte
	POPJ	P,

;Disk output buffer, aborts on error

DSKOBF:	OUT	DSKO,			;Output this buffer
	  POPJ	P,
	GETSTS	DSKO,T1
	$TYPE	(F,DOE,$OCT,<Disk output error |>,,A)

;TTY input byte

TTYIBT:	SOSG	TIBUF+.BFCTR		;Any more bytes?
	 INPUT	TTY,			;Get another line from the TTY
	ILDB	T1,TIBUF+.BFPTR		;Yes, get one
	ANDI	T1,177			;Ignore parity
	POPJ	P,

;TTY output byte

TTYOBT:	SOSG	TOBUF+.BFCTR		;Any room?
	 OUTPUT	TTY,			;No, make some
	IDPB	T1,TOBUF+.BFPTR		;Store byte
	POPJ	P,

;TTY output buffer

TTYOBF:	OUTPUT	TTY,			;Dump last output buffer
	POPJ	P,
SUBTTL	Data area -- Constants

	.DIRECTIVE	FLBLST

;ESCape sequences to position the text cursor

CRLF:	BYTE (7) CR,LF,0
HOME:	BYTE (7) ESC,"[","H",CR,0
LINE23:	BYTE (7) ESC,"[","2","3",";","1","H",CR,0

;ESCape sequences to enable/disable ReGIS and the DMP-4R plotter

GON$:	BYTE (7) ESC,"P","p",0
GOFF$:	BYTE (7) "P","[","0",",","6","9","0","]",ESC,"\",0
DMPON:	BYTE (7) ESC,"[","?","9","i",ESC,"P","3","p",0
DMPOFF:	BYTE (7) ESC,"[","?","8","i",0

;ESCape sequences to initialize ReGIS and print the screen image

RGSINI:	ASCIZ /;S(I0,N0,A[0,0][767,479],E) W(V,I7,A0,S0,M1,N0,P1(M2))
T(I0,A0,D0,S1) /
PRISCN:	BYTE (7) ESC,"#","7",0

;Commands to activate the HC2X2 program via the USR function of BASIC

BASICR:	ASCIZ /PRINT #1,ESC$+"PrBA1"+GOFF$ : RUN
/
BASIC2:	BYTE (7) ESC,"P","r","B","A","2",ESC,"\",CR,LF,0       ;BASIC on (HOST)
BASIC1:	BYTE (7) ESC,"P","r","B","A","1",ESC,"\",CR,LF,0       ;BASIC on (LOCAL)
USR1:	ASCIZ /POKE -25196,33 : POKE -25195,134 : PRINT USR(1)
/
BASIC0:	BYTE (7) CR,LF,ESC,"P","r","B","A","0",ESC,"\",0       ;BASIC off

;Program in 8085 machine language, loaded into character font #2 in hexadecimal.
;This machine code is activated via the USR function after Microsoft BASIC
;has been POKEd.  It outputs the screen image to the LA34, using pixel           width double height
;replication to make the image twice as wide and twice as tall.

HC2X2:	ASCIZ	~L(A2'HC2X2.GGM')
L" "CDE30B2A4F80228C803E; L"!"52C34186228C80EBD7CD; L'"'A00E110B00193E0CCD4E;
L"#"047D3DF8328B80210000; L"$"39228680CD620E214B87; L"%"CD10873E02D3603E06D3;
L"&"622A8C80221880215387; L"'"CD108721000022898022; L"("1680CD4A0E0E1F3A1680;
L")"E603C6FB47DB400704C2; L"*"8286F579D3641F4FF179; L"+"1F4FDA78860F0FC63F21;
L","8880BEC2AA862A898023; L"-"23228980C3D78656772A; L"."89807C0707B5FE03DAC5;
L"/"86E53E21CD2A87E1CD1C; L"0"87C3CD863DFAD1867AC4; L"1"2A877ACD2A8721020022;
L"2"89802A1680232216807C; L"3"FE03C273862DCAF1863A; L"4"888057FE3FC2AC862158;
L"5"87CD1087110600CD9C0E; L"6"221880228C80218B8035; L"7"F26486CD3287C345877E;
L"8"B7F8E5CD2A87E123C310; L"9"873E0ACD4E04F57DB4C4; L":"1C87F1C630F5CD3287F1;
L";"C32305CD3C87CD0E05D2; L"<"3287C93A8B803CF02A86; L"="80F9CD1305C3220E1B5C;
L">"0D20202020FF1B503171; L"?"FF1B5C1B4BFF218B807E; L"@"36003DF036FEC9;
~	;End of HC2X2


	$LOW

OFFSET:	BLOCK	1			;CCL entry flag
COMDSP:	BLOCK	1			;Which Monitor-level command was used

	$ND PDLEN,200
PDL:	BLOCK	PDLEN			;Stack

DEFEXT:	BLOCK	1			;Default extension
FILBLK:	BLOCK	.FXLEN			;SCAN block for file
PTHBLK:	BLOCK	.PTMAX			;PATH block set up by SCAN

	$ND LKPLEN,40
LKPBLK:	BLOCK	LKPLEN			;LOOKUP/ENTER block
	$ND .FOLEN,.FOPPN+1
FLPBLK:	BLOCK	.FOLEN			;FILOP block for input file
	OPENI=FLPBLK+.FOIOS
SUPBLK:	BLOCK	.FOLEN			;FILOP block for output file
	OPENO=FLPBLK+.FOIOS
ACTPTH:	BLOCK	.PTMAX			;Actual path to file, set by FILOP

DIBUF:	BLOCK	3			;Disk input buffer header
DOBUF:	BLOCK	3			;Disk output buffer header
TIBUF:	BLOCK	3			;TTY input buffer header
TOBUF:	BLOCK	3			;TTY output buffer header

FIVEWD:	BLOCK	1			;Word for packing 5 bytes per
FIVEBP:	BLOCK	1			;Byte pointer into FIVEBP

CONFIG:	BLOCK	5			;"CSM DECsystem-1091" in ASCIZ

;Control-C intercept block

INTBLK:	4,,ABORT			;New PC
	ER.ICC				;Intercept Control-C
	BLOCK	2			;Old PC and actual reason
ABORTR:	POPJ	P,			;Routine to PUSHJ to on Control-C

	$HIGH
LITS:	$END	START
    -|D3Û