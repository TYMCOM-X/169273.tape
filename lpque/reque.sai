
begin

require "(SAILIB)SAIL.DEF" source!file;
require "(SAILIB)UUOSYM.DEF" source!file;
require "(SAILIB)MFDSCN.REQ" source!file;
require "(SAILIB)CVPPN.REQ" source!file;


Own Integer  File, Ext, UserPPN, Quest, Brk;
Own String   FileSpec, CopySpec;


boolean procedure WantFile( Reference Integer Filename, Extension );
begin
    Return( Extension neq CvSix("TMP") );
end;

procedure FileProc( Reference Integer Chan, Count, Brk, FileEof;
		    Integer Array LKB );
If ( 0 = ( LKB[!RBLIC] land '001000000000 ) )
 then Print(	Cv6str( LKB[!RBNAM] ), ".",
		Cv6str( LKB[!RBEXT] land '777777000000 ), ",",
		FData( Chan, LKB ), Crlf );

setbreak(Quest_ getbreak,"?",Null,"INS");
MFDset( '17, 0,0, 0 );

while ( true )
 do begin
    Print( "File> " );
    FileSpec_ Inchwl;
    If Length( FileSpec )
     then begin
	CopySpec_ Null;
	Brk_ 0;
	While Length( FileSpec ) or (Brk neq 0)
	 do CopySpec_ CopySpec & (If Brk = "?" then "#" else Null)
		    & Scan( FileSpec, Quest, Brk );
	File_ cvfil( CopySpec, Ext, UserPpn );
	If ( 0 = (File lor Ext lor UserPpn) )
	 then calli( 0, Calli!EXIT );

	If UserPpn = 0
	 then UserPpn_ Calli( !Xwd(-1,!GtPPN), Calli!GETTAB );
	If !lh( UserPpn ) = 0
	 then UserPpn_ CvPPN( Cv6str( memory[ UserPpn   ] ) &
			      Cv6str( memory[ UserPpn+1 ] ) );

	If ( 0 = (File lor Ext) )
	 then File_ Ext_ CvSix("*");

	UfdScn( UserPpn, File, Ext, -1, WantFile, FileProc )
     end
     else calli( 0, Calli!EXIT )
 end;

end;
