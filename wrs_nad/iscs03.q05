	IF	SOLOCS
PATCH1	EQ	PA1PTR
PATCH0  EQ      PA0PTR
	EI	:SOLOCS

	IF	BTELEX

    REMARK %%Patch to remove linefeeds from telex login ports (TID34)
    REMARK %

	LO	CONSAT

	ORG	VBTLX+0E
	HC	0,0,0
	ORG	VBTLX+0E
	J	PATCH1,,

	ORG	PATCH1
	HC	0,0,0,0,0,0,0,0,0,0,0,0,0  : PATCH1 to PATCH1+20
	ORG	PATCH1
	NHI	R1,1F		: strip to 5 bits
	CHI	R1,02		: is it BAUDOT line feed?
	JER	RLINK		: yes, discard character return
	CHI	R1,1B		: replace logic removed by patch
	JN	VBTLX+1E,,
	J	VBTLX+18,,
PATCH1	EQ	.


    REMARK %%Patch changes default echo handlers to correct echo
    REMARK %echo problems between TID34 ports and some interfaces.
    REMARK %This will also affect the echo on ASYNC BAUDOT terminals.
    REMARK %

	LO	SIGNAL

	ORG	SIGDIS+60
	HC	0,0
	ORG	SIGDIS+60
	HC	SECHO-BASE,QECHO-BASE
	EI	:BTELEX


	IF	TELEX


    REMARK %%Patch to correct TID table to force half duplex on
    REMARK %TID33 ports.
    REMARK %

	LO	DATA

	ORG	TID.1F+VID.FL
	HC	0
	ORG	TID.1F+VID.FL
	HC	0040


    REMARK %%Patch to correct TID table to force half duplex on
    REMARK %TID34 ports
    REMARK %

	ORG	TID.28+VID.FL
	HC	0
	ORG	TID.28+VID.FL
	HC	0040
	EI	:TELEX


	IF	1-SOLOCS
	IF	NAPORT


    REMARK %%Patch to initialize OUTMSK in ASYNC initial table
    REMARK %
	LO	CONSAT
	LO	SIGNAL
	LO	DATA

	ORG	IZASN1
	HC	0,0,0
	ORG	IZASN1
	J	PATCH1,,

	ORG	PATCH1
	RE	5
	WC	0
	ER
	ORG	PATCH1
	RBT	RP,TAMMER
	SBT	RP,TAM
	RBT	RP,OUTMSK
	J	IZASN1+08,,
PATCH1	EQ	.


    REMARK %%Initialize OUTMSK in the case of port hang when:
    REMARK %YONENA is set (reverse XON enable) and 
    REMARK %RXON is set (output stopped)
    REMARK %

	ORG	A.CLRO
	HC	0,0,0
	ORG	A.CLRO
	J	PATCH1,,

	ORG	PATCH1
	RE	5
	WC	0
	ER
	ORG	PATCH1
	SBT	RP,TAM
	RBT	RP,TAMMER
	RBT	RP,OUTMSK
	J	A.CLRO+08,,
PATCH1	EQ	.
	EI 	:NAPORT
	EI	:SOLOCS, SOLO CONSAT




	IF	NSPORT


    REMARK %%Patch to solve SIO PVC's BAUDOT problem
    REMARK %

	LO	CONSAT
	LO	DATA

	ORG	SIOBSY+06
	HC	0,0,0,0,0,0,0,0
	ORG	SIOBSY+06
	LHL	R0,OCMSIO,R9
	JEFS	SIOBS1
	SIS	R0,4
	JER	RCH
	LIS	R0,0
	STH	R0,OUTSIO,R9
	EI	:NSPORT

        IF      1-SOLOCS        :crashed solo nodes
        IF      PVC
        IF      NAPORT
        REMARK %Patch to set async pvc AID ports to answered on restart
	REMARK %
        LO      DATA
        LO      PVC

        ORG     PVCI1+4
        HC      0,0,0
        ORG     PVCI1+4
        J       PATCH1,,

        ORG     PATCH1
        RE      26
        HC      0
        ER
        ORG     PATCH1
        THI     R3,POAID
        JE      PAT001
        SRLS    R1,1
        RBT     R1,HANGWT
        SBT     R1,AH.TO
        SBT     R1,AH.TO1
        SBT     R1,AH.TO2
        SLLS    R1,1
PAT001  SIS     R1,2
        JGE     PVCI0,,
        J       PVCI1+0A,,
PATCH1  EQ      .
        EI
        EI
        EI

        IF      NUMPRN
        LO      CONSAT
        REMARK%%Patch to fix 0641 crash problem with printer port.
	REMARK %
        ORG     PRINT7+8
        HC      0,0
        ORG     PRINT7+8
        J       XDEF
        FO      CONSAT
        EI

        IF      SOLOCS
        IF      NAPORT
        REMARK%%Patch to fix SOLOCS 0641 crash problem with 2741 terminal.
	REMARK %
        LO      CONSAT
        LO      DATA
        ORG     X27B+10
        HC      0,0,0
        ORG     X27B+10
        J       PATCH1,,
        ORG     PATCH1
        HC      0,0,0,0,0,0,0
        ORG     PATCH1
PATCH1  LR      R9,RBC
        LB      RBC,T27XC,RBC,R4
        J       X27B+16
PATCH1  EQ      .
        ORG     X27D+8
        RE      8
        HC      0
        ER
        ORG     X27D+8
        EXBR    RBC,R0
        OHI     RBC,80
	LR	R5,R2
        J       PATCH1,,
        ORG     PATCH1
PATCH1  JAL     RCH,APUTCH,,
	LR	R2,R5
	CBT	RP,UCF
        LR      RBC,R9
	GL	WCD
        JAL     R8,WCD,,
        JR      RLINK
PATCH1  EQ      .

  REMARK%%Patch to restore register on return from ADELAY for 2741 receiver.
  REMARK%
        ORG     VCRC+0C
        HC      0,0,0
        ORG     VCRC+0C
        J       PATCH1,,

        ORG     PATCH1
        LHI     RBC,8D
        L       RBP,.FTBUF,RPI
        J       V27D,,
PATCH1	EQ	.
        EI
        EI


        IF      NMPVC
   REMARK%%Patch to keep accurate count of active MPVC subports for accounting%
      LO        MPVC
        ORG     M.D.G1+1C
        HC      0,0,0,0
        ORG     M.D.G1+1C
        J       PATCH1,,
        ORG     PATCH1
        LH      R7,MPV.CO,R9
        JL      M.D.GC,,
        TBT     R7,MPV.AP,R9
        JE      M.D.GC,,
        J       M.D.G1+24,,
PATCH1  EQ      .

:FOLLOWING IS PROBABLY UNNEEDED AND IS FOR DEBBUGGING AND A DOUBLE CHECK
:THE ABOVE SHOULD SOLVE THE PROBLEM

        SEG     0
        ORG     PATCH0
MPVERR  WS      1
PATCH0  EQ      .
        SEG     1

        ORG     MPVZ5+4
        HC      0,0,0
        ORG     MPVZ5+4
        J       PATCH1,,
        ORG     PATCH1
        LH      R0,MPV.PA,R9
        JEFS    .+10
        SIS     R0,1
        STH     R0,MPV.PA,R9
        LCS     R0,1
        J       MPVZ5+0A,,
        LIS     R0,1
        AHM     R0,MPVERR,,
        JBS     .-10

PATCH1  EQ      .
        ORG     M.DME0
        HC      0,0,0,0
        ORG     M.DME0
        J       PATCH1,,
        ORG     PATCH1
        LHL     R4,MPV.PA,R9
        JEFS    .+14
        SIS     R4,1
        STH     R4,MPV.PA,R9
        LCS     R4,1
        L       RBP,.FTBUF,R8
        J       M.DME0+8,,
        LIS     R4,1
        AHM     R4,MPVERR,,
        JBS     .-14
PATCH1  EQ      .
        ORG     M.MUXB+4
        HC      0,0,0
        ORG     M.MUXB+4
        J       PATCH1,,
        ORG     PATCH1
        LH      R0,MPV.PA,R9
        JEFS    .+10
        SIS     R0,1
        STH     R0,MPV.PA,R9
        LCS     R0,1
        J       M.MUXB+0A,,
        LIS     R0,1
        AHM     R0,MPVERR,,
        JBS     .-10

PATCH1  EQ      .
        EI      :MPVC PATCH

        IF      NSPORT
        IF      1-NAPORT        :only sync ports
        REMARK %Patch to define ARSUM%
        REMARK%IGNORE undefined ARSUM errors. That has been patched%%
ARSUM   EQ     SBPSIG           :equate ARSUM s t no assembly error
        ORG     SBPSIG+22
        HC      0,0,0,0
        ORG     SBPSIG+22
        J       .+4             :ARSUM only needed for async
        LCS     R4,2            :NAD zeros out 8 bytes
        NR      R4,R5
        EI
        EI

        IF      1-SOLOCS
        IF      NAPORT
   REMARK%%Patch to check for async ring wrap.  If wrapped then a blackball%
        REMARK%is sent and the ring is cleared%
        LO      CONSAT
        LO      DATA
        ORG     AGCI1+4
        HC      0,0,0
        ORG     AGCI1+4
        J       PATCH1,,
        ORG     PATCH1
        IF      AHIST
        JAL     R0,AHISTY,,
        EI
        LH      R0,AR.BCT,R4            :check wrap
        CHI     R0,AR.RS-AR.IC          :more chars than space
        JGEFS   A.WRAP
        LCS     R0,1
        AHM     R0,AR.BCT,R4            :decrement BCT
	JGER	RBF			:return if not empty
	CBT	RP,DQDAT		:Flip flag
	JR	RBF

A.WRAP  
        RBT     RP,DQDAT                :no data waiting
        LCS     R1,1                    :and clear input ring
        STH     R1,AR.BCT,R4            :no chars
        LIS     R1,AR.IC                :init cursors
        STB     R1,AR.FC,R4
        STB     R1,AR.EC,R4
        LO SIGNAL
        LHI     R4,BLBSIG               :black ball em
        FO SIGNAL
        JAL     RCH,PUTSIG,,
        J       VDEF,,                  :read all data for this port
PATCH1  EQ      .
        EI   (NAPORT)
        EI      (ISIS)

        IF      SOLOCS
        IF      PVC
        LO      PVC
   REMARK%%Patch so SOLO pvc ports display logon failure messages%
        ORG     PSTA03+8
        HC      0,0
        ORG     PSTA03+8
        JGE     TEXT

  REMARK%%Patch so SOLO PVC ports will not get lost when the supervisor
  REMARK% goes away.%
        ORG     PSTA83+6
        HC      0
        ORG     PSTA83+6
        JFS     PSTA85
        FO      PVC
        EI
        EI

        IF      SOLOCS
        REMARK%%Patch so LGGO does only one group of ports per call%
        REMARK%which caused an async ring wrap.%
        LO LOGGER
        ORG LGGO
        HC      0,0,0,0,0,0,0,0,0,0,0,0,0
        ORG     LGGO
	JFS	LGRET
	JFS	LGRET
        LHL     R0,TOLOG,RPOL
        NH      R0,LOGER,RPOL
        NH      R0,LOGING,RPOL
        JFFOH   R0,LG1
        J       SNDLOG,,
        EI

        IF     \HARDANSWER
        IF     HARDANSWER
        IF      NAGRP
        IF      1-SOLOCS
        REMARK %Patch to answer hardwire ports on slot initialization%
        ORG     IZASYN
        HC      0,0,0
        ORG     IZASYN
        J       PATCH1,,

        ORG     PATCH1
QQQ     EQ      0
        RE      NAGRP
        LCS     R1,1
        IF      \MODMG|QQQ|      :ports on this group that are modems
        XHI     R1,MODMG|QQQ|    :are not answered
        EI
        STH     R1,HANGWT+2*QQQ :save 
        XHI     R1,0FFFF
        STH     R1,AH.TO+2*QQQ
        STH     R1,AH.TO1+2*QQQ
        STH     R1,AH.TO2+2*QQQ
QQQ     EQ      QQQ+1
        ER
        LM      R14,SPED.A,,
        J       IZASYN+6,,
PATCH1  EQ      .

        ORG     IZASN1+10
        HC      0,0
        ORG     IZASN1+10
        JFS     .+4
        JFS     .+2             :the hangwt has been setup above
        EI (NAGRP)
        EI (SOLOCS)
        EI (HARDANSWER)
        EI (\HARDANSWER)

        IF      NMPVC
        IF      PVC
        LO      PVC
        LO      CONSAT
        REMARK%%Patch to prevent MPVC ports getting hung in login state.%
        ORG     PSTATE
        HC      0,0,0
        ORG     PSTATE
        J       PATCH1,,
        ORG     PATCH1
        TBT     RP,PVCARY,,
        JE      TEXT,,
        CHI     RP,.MPORT
        JL      PSTATE+08,,
        CI      R9,2
        JGEFS   .+0E
        CI      R9,-2
        JGE     PSTATE+08,,
        L       RBP,.TTBUF,RPI
        JAL     RCH,EMPTY,,
       IF       1-SOLOCS   (ISIS consat needs cirdis)
        ST      RLINK,PVCRET
        JAL     RLINK,CIRDIS
        L       RLINK,PVCRET
        L       RBP,.TTBUF,RPI
       EI     :isis consat
        LO      SIGNAL
        LHI     R4,ZAPSIG
        FO      SIGNAL
        JAL     RCH,PUTSIG,,
        JR      RLINK
PATCH1  EQ      .
        EI
        EI



        IF      1-SOLOCS
        IF      PVC
        REMARK%%Patch to make NOHNG disable TID timer%
        LO      PVC
        ORG     AIDPVC+0E
        HC      0,0,0
        ORG     AIDPVC+0E
        J       PATCH1,,

        ORG     PATCH1
        L       R7,.PVCX,RPI
        THI     R7,POHNG
        JE      AIDPV1,,
        RBT     RP,TATBIN
        SBT     RP,AH.TO
        SBT     RP,AH.TO1
        SBT     RP,AH.TO2
        J       AIDPV1,,
PATCH1	EQ	.
        EI :PVC
        EI :SOLOCS


        IF NMPVC
        IF SOLOCS
 REMARK%%Patch to return hung MPVC pseudo ports back to normal%
        ORG     MPVCSA+0E
        HC      0,0,0
        ORG     MPVCSA+0E
        J       PATCH1,,

        ORG     PATCH1
        SBT     RP,RLA     :patch sees if rla is set without a circuit
        JE      MPVCSA+16,,
        LHI     R5,ASYZFL,RP
        LH      R0,IOTAB,R5,R5
        JG      LGRET,,
        J       MPVCSA+16,,    :no circuit so build one
PATCH1  EQ      .

        EI  :SOLOCS
        EI  :NMPVC

        IF      NSPORT          :SIO PORTS COULD SEND THE DISCONNECT SVC

 REMARK%%Patch to prevent SIO ports from hanging due to disconnect SVC%

        ORG     QHAN26+3C       :WHILE STILL OUTPUTTING AND THE PORT WOULD
        HC      0,0,0           :ALWAYS BE BUSY SINCE MICROCODE QUIT THE OUTPUT
        ORG     QHAN26+3C       :SVC WITHOUT TELLING IT WAS DONE.
        J       PATCH1,,

        ORG     PATCH1
        LIS     R7,0            :0 IS COMPLETED STATUS
        LHI     RLINK,-.SPORT,RP :PORT OF SIO
        SLLS    RLINK,5         :MAKE AN 8 WORD INDEX
        STH     R7,OCMSIO,RLINK :SAY OUTPUT DONE
        JAL     RLINK,DT.PRT,,  :OVERWRITTEN INSTRUCTIONS
        J       QHANS1,,

PATCH1  EQ      .

  REMARK%%Patch to prevent register from getting clobberred in SIOBSY%
        ORG     QHAN16
        HC      0,0,0
        ORG     QHAN16
        J       PATCH1,,


        ORG     PATCH1
        ST      R0,PATCH0,,
        JAL     RCH,SIOBSY,,
        J       .+10
        L       R0,PATCH0,,
        J       QHAN26,,
        J       QHANS1,,
PATCH1  EQ      .

       SEG      A.DATA
        ORG     PATCH0
        WS      1
PATCH0  EQ      .
        SEG     A.CODE

   REMARK%%Patch to change SIO hang program%
        ORG     S.HNG
        HC      0,0,0,0
        ORG     S.HNG
        HC      3,1500,0,0

  REMARK%%Patch to stop outputting when signals drop towards consat for SIO ports%
        ORG     QHANS6
        HC      0,0,0
        ORG     QHANS6
        J       PATCH1,,

        ORG     PATCH1
        THI     R0,28   :CARRIER OR DSR
        JE      QHAN26,, :IF NOT PRESENT HANG IMMED
        TBT     RP,YONENA
        JE      QHAN16
        J       QHANS6+6
PATCH1  EQ      .

   EI   :NSPORT

        IF      1-SOLOCS
        IF      NAPORT
    REMARK%%Patch to prevent ports hanging while being answered%
        ORG     A.VID
        HC      0,0
        ORG     A.VID
        J       PATCH1
        ORG     PATCH1
        SBT     RP,TAM
        JAL     RBF,A.DAT
        J       A.VID+4
PATCH   EQ      .
        EI
        EI

        IF 1-SOLOCS

: THIS GIVES US MORE PATCH AREA AND SHOULD BE THE LAST PATCH FILE
: (ISIS PATCH)
Q       EQ      PATCH1-SEG1
S1SIZE  EQ      Q+80            :SOME EXTRA FOR SPECIALIZED PATCHES
        SEG     0E
        ORG AREAS+4
        WC      0
        ORG     AREAS+4
        WC      S1SIZE
        EI :SOLOCS
PA1PTR  EQ      PATCH1
    