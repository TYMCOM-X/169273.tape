     SUBTTL	PVC.602 - HANDLES PERMANENT VIRTUAL CIRCUITS
: ******************************************************************
: **                  PROPRIETARY INFORMATION                     **
: **                                                              **
: **  This  source code listing constitutes the proprietary pro-  **
: **  perty of TYMNET, Incorporated. The recipient, by receiving  **
: **  this program listing, agrees that neither this listing nor  **
: **  the   information  disclosed herein nor any  part  thereof  **
: **  shall be  reproduced or transferred to other documents  or  **
: **  used  or  disclosed to others for manufacturing or for any  **
: **  other purpose except as specifically authorized in writing  **
: **  by  TYMNET, Incorporated.                                   **
: ******************************************************************
: **                   MODULE IDENTIFICATION                      **
: **                                                              **
: **  File Name          :  PVC.602         last change: 09/26/86 **
: **                                                              **
: **  File Description   :  PVC capability supports various chara-**
: **			    cteristics and functions with parti-  **
: **			    cular physical ports which are defined**
: **			    in slot tymfile. Those main functions **
: **			    include: AID,ALO,PCO,SLO,DSC.         ** 
: **			    AID- automatically identifies terminal**
: **			    types without user typing terminal ID.**
: **			    ALO- automatically checks logon string**
: **			    specified in slot tymfile for certain **
: **			    ports and builds circuits from them.  **
: **			    PCO- automatically rebuilds circuits  **
: **			    after specified timer elapsed if lost.**
: **			    SLO- allows user rebuild circuits or  **
: **			    alter new one if not successful.      **
: **			    DSC- automatically disconnects ports  **
: **			    when the circuit is zapped.           **
: **                                                              **
: **  File Abstract      :  PVC - permanent virtual circuit imply **
: **			    there is alway a circuit existing as  **
: **			    long as logon string correctly speci- **
: **			    fied and ports are left active.       **
: **                                                              **
: ******************************************************************
: **                    CHANGE LOG                                **
: **                                                              **
: **  Ver   Date    By   PIR/NSR  Changes (most recent at top)    **
: ** ----- -------- ---  -------- ------------------------------- **
: ** 06.02 12/03/86 sdw		  Have AIDPVC call IXPROF just    **
: **				  like AVID does                  **	
: ** 06.02 09/26/86 LH	 NSR  957 Changes made around AIDPV1 for  **
: **				  mapping incorrect PVC TID entry.**
: ** 06.01 09/11/86 dlg		  Changed instruc to RX3 at PSTA10**
: **				  , PSTA50+6, and PVCSOP+4, to 	  ** 
: **				  prevent assembly errors	  **
: ** 06.01 09/10/86 LH		  Change RX1 to RX3 around PSTA0F,**
: **				  errors at referring to EMPTY.   **
: ** 06.01 09/03/86 LH	 NSR  901 Removed "vanilla" set for TELEX **
: **				  ports. Instead set "vanilla" for**
: **				  SIO and TELEX ports in ESTPRT.  **
: ** 06.00 05/29/86 LH	 PIR 1824 Make IIX capable in AIDPVC	  **
: ** 06.00 05/15/86 LH	 PIR 1824 Make IIX capable after ckt zapped*
: ** 05.00 04/22/86 LH   PIR 1869 Zap circuit on PCOWAT timer     **
: **				  expired at location PVC400.     **
: ** 05.00 04/08/86 LH            Globe PVCSWT			  **
: ** 05.00 04/02/86 sdw           Have PVCSOP use R6 so it uses a **
: **                              non smashed register .          **
: ** 05.00 03/28/86 sdw           added PCOWAT to PSTA80+ logic   **
: **                              and cleaned up that logic       **
: ** 04.02 03/04/86 SDW           Make sure both SEVBIT and PARITY**
: **                              both aren't set.                **
: ** 04.02 02/28/86 sdw           Made sure zapper goes to MPVC   **
: **                              to terminal buffer.             **
: ** 04.02 02/20/86 SDW  NSR xxxx AIDPVC disables TID timer       **
: ** 04.02 02/13/86 SDW           PSTATE converts a logon failure **
: **                              to a zapper for MPVC psuedo prt **
: ** 04.03 01/28/86 SDW  n/a      Moved PVCIZ to INITIZ.403       **
: ** 04.03 01/23/86 SDW  n/a      RX3ed NOCIRM                    **
: ** 04.02 01/07/86 LH   PIR 1869 Add new parameters PORCV,POPRB. **
: **  ''      ''    ''      ''    New retry timer para. PCOWAT and**
: ** 				  associated routine PVCSWT,PVC400**
: **				  are added.			  **
: **  ''      ''    ''            Add proprietary banner.         **
: **								  **
: ******************************************************************
	SUBTTL	(PVC) PVC CONTROL ROUTINES

	 IF	PVC

:				****  *   *  ***
:				*   * *   * *   *
:				****  *   * *
:				*      * *  *   *
:				*       *    ***

	GL	RELOG,LOG5		:references
	GL	PVCLOG,PVCOUT,AIDPVC	:definitions
	GL	PSTATE
	GL	CRLF,POPRB	:remove this some day
	GL	POALO,PORCV,POTXT,PVCSWT

	SEG	A.CODE
	MO	.,PVC

	GL	CIRDIS,PVCIZ

C.PVC	EQ	88			:crash-code for PVC

	LO	DATA

:	Set up equates for handy reference to PVC parameters
POAID	EQ	1			:auto Terminal-ID option
POALO	EQ	1^1			:auto LOGON option
PONPI	EQ	1^2			:Print NODE/PORT IDentifier option
POSLO	EQ	1^3			:Subsequent LOGON to alternate host option
POPAR	EQ	1^4			:Even-Parity option
POPCO	EQ	1^5			:Permanent-Circuit option
POHNG	EQ	1^6			:Ignore Network-hang option
POHDX	EQ	1^7			:Half-duplex option
POCRLF	EQ	1^8			:Echo-LF-with-CR option
POLFCR	EQ	1^9			:echo-CR-with-LF option
:  was POTRS until 5.00.  Removed since no reason not to have it. *5.00.sdw
PORBP	EQ	1^0B			:Reverse-backpressure option
POTXT	EQ	1^0C			:Text-Output not permitted
PODSC	EQ	1^0D			:Disconnect on circuit Zapped
POMPC	EQ	1^0E			:MPVC Candidate
POTLX	EQ	1^0F			:TELEX port

POSPAR	EQ	1^12			:force space parity  4.01U
POKAT   EQ      1^13                    :force katakana  4.01s
POUPC   EQ      1^14                    :force uppercase only  4.01s
PORCV	EQ	1^15			:port as receive only ** 4.02 LH
POPRB	EQ	1^16			:PROBE message required ** 4.02 LH

:	Entry-point for PVC control routines
:	Execute PVC control-routine frequently to see if any work to do
	GL	PVCCTL
PVCCTL	LHI	RPOL,(NGRP-2)*2		:for all ports...

PVCC10	L	R1,PVCSTD,RPOL		:Get something-to-do array
	JEFS	PVCC08			:skip if nothing to do here
	ST	R1,P.DONE		:else save value
PVCC04	L	R1,P.DONE		:get bits
	JFFO	R1,PVCC20		:Skip if we've got one
PVCC08	SIS	RPOL,4			:examine next port group
	JGEBS	PVCC10			:continue looking for work to do
	J	CSDISM,,		:no more work...exit

:	Got something to do.  Get port number, find state/time vector,
:		and dispatch state processor to proper handler.
PVCC20	RBT	R2,P.DONE		:R2 specifies relative port
	LR	RP,RPOL			:...within this group
	SLHLS	RP,3			:port group times $A 16
	AR	RP,R2			:add to relative port within this group
	GETPTP(RPI,RP,RP)		:set up pointer to port-table
	LH	R1,.PVCTO,RPI		:get ticker
	JEFS	PVCC30			:Clock ticked to zero.  Time to do something
	SIS	R1,1
	STH	R1,.PVCTO,RPI		:Tick another 1/4 second
	JGE	PVCC04
:	Clock went negative.  ERROR!
	JAL	R11,PVCRSH		:Better diagnose this one

:	Clock went to zero.  Whatever was scheduled is about to happen...
PVCC30	RBT	RP,PVCSTD		:disable control...may not need it again
	TS	.PVCTO,RPI		:...and turn off timeout
	LB	R3,.PVCDO,RPI		:Get dispatch value (*2)
	LHL	R2,PVCRTN,R3		:Index into service routine table
	J	BASE,R2,,			:go service PVC port


:	Routine table.  Relative entry corresponds to routine dispatch
:	value in .PVCDO.
PVCRTN	HC	EA(PVCERR)		: 0 -	Invalid Value
	HC	EA(PVC100)		: 1 -	first LOGON attempt timed-out
	HC	EA(PVC200)		: 2 -	subsequent-LOGON with PCO timed-out
	HC	EA(PVC300)		: 3 -	Re-try PCO circuit
	HC	EA(PVC400)		: 4 -	delay timer before subsequent-
					:       LOGON with PCO  **4.02 LH

:	ERROR in dispatch value.  Crash!
PVCERR	JAL	R11,PVCRSH		:fatal table error.  CRASH and diagnose

:	PVC CODE SUFFERED FATAL ERROR
PVCRSH	ABORT(R11,C.PVC)		:R11 might be interesting

:	Routine dispatch value 1.  Timed-out on original LOGIN attempt.
:	LOGIN should have completed by now.  Test BF to see if we are
:	still in LOGIN mode and retry if true
PVC100	TBT	RP,RLA			:See if port still active
	JE	PVCC04			:no - don't care about hung port
	TBT	RP,LOGING		:Test for still-in-LOGIN-mode
	JE	PVCC04			:no - last LOGIN must have succeeded
	JAL	RLINK,CIRDIS,,		:Go disconnect circuit
	LHL	R1,.TERMT,RPI		:get terminal type
	LHL	R1,VID.IZ,R1,		:get initialization routine
	LHI	R5,SPECFE		:turn IIX capable on **5.00 LH
	JAL	RLINK,BASE,R1,		:go establish port
	JAL	R9,PVCSOP		:set up options
	J	PVCC04			:return to PVCCTL loop

:	Routine dispatch value 2.  Timed-out on subsequent LOGIN attempt.
:	LOGON-timer expired.  See if last LOGIN succeeded.
PVC200	TBT	RP,RLA			:see if port still active
	JE	PVCC04			:no - forget this guy
	TBT	RP,LOGING		:test for still-in-login-mode
	JE	PVCC04			:no - forget this guy
	L	RBP,.LGBUF,RPI		:logon-buffer buffer-pointer
	JAL	RCH,EMPTY,,		:clear logon buffer
	JAL	RLINK,CIRDIS,,		:go disconnect circuit
	LHL	R1,.TERMT,RPI		:get terminal type
	LHL	R1,VID.IZ,R1,		:get initialization routine
	LHI	R5,SPECFE		:turn IIX capable on **5.00 LH
	JAL	RLINK,BASE,R1,		:go establish port
:	JAL	RLINK,P.NEXT		:now rebuild the circuit
:	JAL	RLINK,PVCSCF		:set up retry timer
	J	PVCC04			:and exit back to scanner




:	Routine dispatch value 3. PCO circuit has been delayed because
:	of short-lived circuits. It is now time to try again.
PVC300	TBT	RP,RLA			:See if port still active
	JE	PVCC04			:no - don't care about hung port
	L	RBP,.FTBUF,RPI		:empty out any left over characters
	JAL	RCH,EMPTY,,		:that the user may have typed
	LHL	R1,.TERMT,RPI		:get terminal type
	LHL	R1,VID.IZ,R1,		:get initialization routine
	LHI	R5,SPECFE		:turn IIX capable on **5.00 LH
	JAL	RLINK,BASE,R1,		:go establish port
	J	PVCC04			:return to PVCCTL loop

:	Routine dispatch value 4. PCO circuit has been delayed before
:	rebuilding circuit which is zapped at the very first time. ** 4.02 LH
	
PVC400	IF	TTELEX
	RBT	RP,INTIMR		:check if initial timer over and reset it
	JN	PVCC04			:not yet, don't issue P-needle
	EI	:TTELEX
	JAL	RLINK,CIRDIS,,	:**5.00 LH, zap ckt before another P-needle sent
	LHL	R1,.TERMT,RPI		:get terminal type
	LHL	R1,VID.IZ,R1,		:get initialization routine
	LHI	R5,SPECFE		:set IIX capable on **5.00 LH
	JAL	RLINK,BASE,R1,		:go establish port - request P-needle
	J	PVCC04			:return to PVCCTL loop





PSTATE	TBT	RP,PVCARY,,		:is this a PVC port?
	JE	TEXT			:no - then ignore

	IF	NMPVC
	CHI	RP,.MPORT		:MPVC pseudo ports convert
	JL	PSTA00			:logon failures into zappers
	CI	R9,2			:error conditions
	JGEFS	PSTA0F
	CI	R9,-2			:no supe failures
	JG	PSTA00
PSTA0F	L	RBP,.TTBUF,RPI		:zap psuedo port (it can't work)
	JAL	RCH,EMPTY,,		:**6.01 LH
	ST	RLINK,PVCRET
	JAL	RLINK,CIRDIS,,
	L	RLINK,PVCRET
	LO	SIGNAL
	LHI	R4,ZAPSIG
	FO	SIGNAL
	L	RBP,.TTBUF,RPI		:cirdis can overwrite RBP
	JAL	RCH,PUTSIG
	JR	RLINK
	EI :MPVC

PSTA00	TBT	RP,NEWLOG		:first time for this session?
	JEFS	PSTA01			:no...just skip
	STH	R9,PVCSAV		:need r9 (sender)--so save it
	JAL	R9,PVCSOP		:else set options
	L	RBP,.TTBUF,RPI		:restore to-terminal buffer-pointer
	LH	R9,PVCSAV		:restore R9

PSTA01	L	R6,.PVCX,RPI		:get PVC parameters
	THI	R6,POSLO		:is this an SLO user?
	JEFS	PSTA03			: no - leave it as it is
	RBT	RP,PVCSTD		:reset for SLO users
PSTA03	CLHI	R9,1			: if this is 1, probably
	JL	PSTA04			:this range needs further checking
	JG	TEXT			: else pass to TEXT if greater than 1
         IF 1-SOLOCS                    :SOLO's CIRDIS does not make PLI
        IF      NPAPRT                  :but not for port addressing
        TBT     RP,AD.PRT,,             :don't remove on PLI
        JNFS    PSTA3A			:addressable port don't have extra PLI
        EI	:NPAPRT
        LIS     R9,0                    :but ISIS CIRDIS makes another PLI
        STH     R9,PVCSAV               :and make it work      *SDW*
	 EI	:SOLOCS
PSTA3A	ST	RLINK,PVCRET		:preserve reg. 0A
	JAL	RLINK,CIRDIS,,		:disconnect circuit
	LHL	R1,.TERMT,RPI		:get terminal type
	LHL	R1,VID.IZ,R1,		:get initialization routine
	LIS	R5,1			:from PSTATE for Tymnet II
	JAL	RLINK,BASE,R1,		:go establish port
	J	PSTA40			:get rlink back, exit thru TEXT
PSTA04	CLHI	R9,-1			:must check PONPI if came from CIRBLT
	 JG	TEXT			: if greater, R9 must be 0!
	 JN	PSTA05			: if not -1, check for re-build
	THI	R6,PONPI		: empty .TTBUF if NPI not allowed
	 JNFS	PSTA10			: not allowed
PSTA05	THI	R6,POTXT		:is any text allowed?
	 JEFS	PSTA20			:yes - then leave the buffer alone
PSTA10	JAL	RCH,EMPTY,,		:empty to-terminal buffer
PSTA20	ST	RLINK,PVCRET		:preserve return register
	STH	R9,PVCSAV		:preserve R9 for TEXT
	RBT	RP,NEWLOG		:FIRST LOGIN ATTEMPT?
	JE	 PSTA50			:no-PSTA50 for second attempts
	THI	R6,POALO		:is this a CKT building port?
	JE	TEXT			:no...go finish up text processing
	AIS	R9,1			:yes..is this a pseudo-needle response?
	JLFS	PSTA40			:no - continue
	JAL	RLINK,PVCALO		:go build circuit
	L	RBP,.TTBUF,RPI		:point to to-terminal buffer
	L	R6,.PVCX,RPI		:don't set PVCSTD for SLO ports
	JAL	RLINK,PVCSCO		:set up state 1
PSTA40	L	RLINK,PVCRET		:get return address
	 IF	TTELEX			:** 4.02 LH
	TBT	RP,TTLXP		:if transparent TELEX port
	JNR	RLINK			:yes, no text string out to TELEX dev.
	 EI	:TTELEX			:** 4.02 LH
	LH	R9,PVCSAV		:return TEXT parameter
	J	TEXT			:send out appropriate messages

PSTA50	THI	R6,PODSC		:should we hang up?
	JEFS	 PSTA60			:no
	JAL	RCH,EMPTY,,		:yes - empty out buffer
	SBT	RP,HANGWT		:hang port
	JR	RLINK			:and exit

:       Removed test for POTRS since not having it caused domplaints which
:       would be fixed by including TRS option.  500.sdw
PSTA60
:	THI	R6,POTRS		:terminal-options to be reset?
:	JEFS	PSTA70			:no
	LHL	R4,.TERMT,RPI		:yes...get terminal-type cursor
	JAL	R5,IZPORT		:reset TID options and start LOGIN
	JAL	R9,PVCSOP		:set PVC options
	L	RBP,.TTBUF,RPI		:restore to-terminal buffer-pointer

PSTA70	THI	R6,POALO!POPCO		:AUTO-LOGON or Permanent Circuit?
	JE	PSTA75
	THI	R6,POSLO		:subsequent logon option?
	JE	PSTA80			:no - handle subsequent PCO ckts

	LIS	R0,0			:SLO option
	STB	R0,.PVCDO,RPI		:set :L state 0
	ST	R0,.PVCLH,RPI		:enable :L and fall through for "PLI"
	THI	R6,POHNG		:some conditions will cause
	JE	PSTA75			:'mirroring' if NOHNG is enabled.
	LH	R9,PVCSAV		:need to check for these, and
	AIS	R9,1			:delay if necessary.
	JE	PSTA75			: coming from CIRBLT is okay
	JAL	RLINK,CIRDIS,,		:disconnect circuit
	JAL	RLINK,PVCSCF		: other conditions, defer
	L	RBP,.TTBUF,RPI		:send it in the right direction
	LA	R6,CRLF			:send CR/LF
	JAL	R9,PVCOUT		:to user before sending msg
	LA	R6,NOCIRM,,		:announce the problem to user
	J	PSTA87			: and exit

:	Neither ALO nor PCO...set up LGECHO as required
PSTA75	TBT	RP,ASCII		:ASCII terminal?
	JE	PSTA40			:no
	TBT	RP,HAFDUX		:Half-duplex?
	JN	PSTA40			:yes
	SBT	RP,LGECHO		:normal...turn on logon-echo
	J	PSTA40			:finished with SLO port, exit

:  the following was slightly rewritten by sdw.500 to include PCOWAT and
:  to just be a bit more understancable
PSTA80	LB	R7,.PVCDO,RPI		:get PVC state
	SIS	R7,2			:initial build failed
	JE	PSTA92			:wait PCOWAT for rebuild
	SIS	R7,2			:PCOTRY timer last to run?
	JE	PSTA90		:not yet ready to rebuild
PSTA83	LH	R9,PVCSAV		:is this from CIRBLT?
	AIS	R9,1			:/
	JEFS	PSTA85			: yes
	JAL	RLINK,CIRDIS,,		: no-then disconnect
	LHL	R1,.TERMT,RPI		:get terminal type
	LHL	R1,VID.IZ,R1,		:get initialization routine
	LIS	R5,1			:from PSTATE for Tymnet II
	JAL	RLINK,BASE,R1,		:go establish port
PSTA85	JAL	RLINK,PVCSLS		:now send LOGING string
	LA	R6,PVCMS1,,		:point "CIRCUIT BUILDING IN PROGRESS"
					:	message or equivalent
	LB	R7,.PVCDO,RPI		:pick up state again
	SIS	R7,6			:PCORTY expire?
	JEFS	PSTA86			: yes - appropriate msg for terminal
	LA	R6,PVCMS2,,		:send "CIRCUIT LOST - REBUILDING"
PSTA86	JAL	RLINK,PVCSCS		:set up retry timer
PSTA87	L	RBP,.TTBUF,RPI		:assure pointing toward terminal
	JAL	R9,PVCOUT		:let user know what's happening
					:later and keep trying if necessary
	J	PSTA40			:exit

PSTA90	LH	R7,.PVCTO,RPI		:has state 2 timer expired?
	JL	PSTA92			:yes-set PCOWAT if nonzero
	JAL	RLINK,PVCSCF		: set up timer to try again later
	JAL	RLINK,CIRDIS,,		:wait on rebuild
	LA	R6,PVCMS6,,		:'CIRCUIT LOST - WILL TRY LATER' msg
	J	PSTA87			

:  PCOWAT logic added to PSTATE by lliu.402 and sdw.500
        IF      PCOWAT          :only delay if a delay exists
PSTA92  JAL     RLINK,PVCSWT            :delay for PCOWAT seconds
        J       PSTA40                  :they get only the build notice
        ELSE                    :if no delay
PSTA92  EQ      PSTA83                  :build again and save a jmp
        EI :PCOWAT




:	Set Terminal-options as specified by PVC parameters
:	Places ^R, ^H, and ^P into from-terminal buffer if specified.
:	R9 -	Link register
:	returns R6 -	PVC parameters
PVCSOP	L	RBP,.FTBUF,RPI		:Set from-terminal buffer-pointer
	JAL	RCH,EMPTY,,		:start out by emptying buffer
	L	R6,.PVCX,RPI		:Get PVC parameters for this port
	THI	R6,PORBP		:Test for Reverse-backpressure
	JEFS	PVCSO1			:no
	LHI	RBC,12			:yes
	JAL	RCH,PUTCHR		:Send ^R to Sup
PVCSO1	THI	R6,POLFCR		:Test for Echo-LF-on-CR
	JEFS	PVCSO2			:Yes
	RBT	RP,ECR.LF		:Disable Echo-LF-on-CR
PVCSO2	THI	R6,POCRLF		:Test for Echo-CR|RUB-on-LF
	JEFS	PVCSO3			:yes
	RBT	RP,ELF.CR		:Disable Echo-CR|RUB-on-LF
PVCSO3	THI	R6,POHDX		:Test for Half-duplex
	JEFS	PVCSO4			:no
	LIS	RBC,8			:yes
	JAL	RCH,PUTCHR		:Send ^H to Sup
	SBT	RP,HAFDUX		:set specifically
PVCSO4	THI	R6,POPAR		:Test for Even-parity
	JEFS	PVCSO5			:no
	LHI	RBC,10			:yes
	JAL	RCH,PUTCHR		:Send ^P to Sup
	SBT	RP,PARITY		:set specifically
	RBT	RP,SEVBIT		:not bo02sdw
PVCSO5	THI	R6,POMPC		:MPVC candidate?
	JEFS	PVCSO6			:No	4.01U
	SBT	RP,MPVC.C		:yes...flag it
PVCSO6	TI	R6,POSPAR		:Test for space parity 4.01U
        JEFS    PVCSO7                  :No,  4.01s
	SBT	RP,SEVBIT		:set if so 4.01U
	RBT	RP,PARITY		:not both*402sdw
PVCSO7  TI      R6,POKAT                :Test for katakana port
        JEFS    PVCS08                  :all options checked
        SBT     RP,KATERM               :actually set at succesful login
PVCS08  TI      R6,POUPC                :and uppercase only? 4.01s
        JER     R9                      :no more options 4.01s
        SBT     RP,KATUPC,,             :set if so 4.01s
        JR      R9                      :done


:	ALO Specified.  Output meaningful text fo user.  Send LOGIN
:	string to Supervisor.
:	RLINK -	Link
PVCALO	L	RBP,.TTBUF,RPI		:Set to-terminal buffer-pointer
	LA	R6,PVCMS1,,		:set "CIRCUIT BUILDING IN PROGRESS" or
					: equivalent message
	JAL	R9,PVCOUT		:send message to terminal if appropriate
					:...and fall through to PVCSLS


:	Send LOGIN string to supervisor.
:	RLINK -	Link register
:	RP -	port number
PVCSLS	L	RBP,.FTBUF,RPI		:Set buffer index toward supervisor
	RBT	RP,LGECHO		:LOGGER shouldn't echo for PVC
	 IF	NUTXT
	LH	R6,IUTXTS,RP,RP		:Check userstring to use
	JLFS	PVCSL2			:none...use default
	LA	R6,BASE,R6,		:special...set it up
	J	PVCSL8			:and skip to send it
PVCSL2	 EI	:NUTXT

:	Default username, password to be used
:	Insert (octal) port-number in default username before shipping it on.
	LR	R0,RP			:Port number
	OH	R0,PVCBAS		:add bias
	LA	R3,PVCUN+6		:place to put port number
	LIS	R4,4			:4 bytes
	JAL	R6,OOS			:Output-Octal-String
	LA	R6,PVCUN		:set up pointer to username
	JAL	R9,MSG

:	If here because of :L option, might be host-number specified
	L	R6,.PVCLH,RPI		:Get LOGIN history string entry
	JLE	PVCSL6			:nothing to do here
:	Host specified.  Send ":"|digit|... before Password.
:	May be several digits.
	LHI	RBC,3A			:Get a COLON
PVCSL4	JAL	RCH,PUTCHR		:Stick Character in Sup-bound buffer
	SRLS	R6,4			:Slide next host-number into position
	LIS	RBC,0F			:set mask
	NR	RBC,R6			:Isolate host-number digit
	SIS	RBC,1			:Subtract bias
	JLFS	PVCSL6			:Nothing there - done with host number
	OHI	RBC,30			:make number ASCII
	JBS	PVCSL4			:Continue

:	Insert (octal) port-number in default password before shipping it on.
PVCSL6	LR	R0,RP			:port number
	AH	R0,PVCBAS		:bias it
	LA	R3,PVCPW+2		:place to put port number
	LIS	R4,4			:4 bytes
	JAL	R6,OOS			:Output-Octal-String
	LA	R6,PVCPW		:pointer to password

PVCSL8	JAL	R9,MSG			:Send User-specified LOGIN string
	RBT	RP,DEM			:No Green-balls
	TS	.PVCLH,RPI		:Disable :L scanner
	JR	RLINK			:return


:	PVCOUT Utility Routine
:	This function checks the NOTXT option, and either
:	outputs the string, or just returns, as optioned.
:	RBP -	pointer to buffer
:	R6 -	pointer to text-string
:	R9 -	link address
:	RPI -	port-table pointer
PVCOUT	L	R0,.PVCX,RPI		:get exception word
	THI	R0,POTXT		:text-output permitted?
	JE	MSG			:yes...output it (exit via MSG)
	JR	R9			:no...just return


:	Set timer for original LOGIN attempt
:	RLINK -	link register
PVCSCO	LHI	R0,$A 90*4		:come back in 90 seconds if necessary
	LIS	R1,1*2			:set dispatch value...
PVCSET	STH	R0,.PVCTO,RPI		:store in time vector
	STB	R1,.PVCDO,RPI		:	so we know why we came back
	SBT	RP,PVCSTD		:set something-to-do
	JR	RLINK			:return

:	Set timer for (failed) subsequent LOGIN attempt for PCO port
:	RLINK -	Link register
PVCSCF	LHI	R0,PCORTY*4		:user defines how often we retry
	LIS	R1,3*2			:set up to delay login retry (state 3)
	JBS	PVCSET			:and skip

:	Set timer for subsequent LOGIN attempt for PCO port
:	RLINK -	Link register
PVCSCS	LHI	R0,PCOTRY*4		:user defines how often we retry
PVCSC1	LIS	R1,2*2			:set dispatch value to see if LOGON succeeds
	JBS	PVCSET			:go set up

:	Set timer for rebuilding circuit attempt for PCO port ** 4.02 LH
PVCSWT	LHI	R0,PCOWAT*4		:user defines how long to rebuild ckt
	LIS	R1,4*2			:set dispatch value ...4, state=0
	J	PVCSET			:jump back to PVCSET

:	Define the PVC exception table
PVCX	WS	0
Q	EQ	0
	RE	NPORT			:for all ports...
	 IF	\PVCX|Q|
	  WC	0FFFFFFFF&PVCX|Q|	:Exception for port Q
	 ELSE	:\PVCX|Q|
	  WC	0			:null for non-PVC ports
	 EI	:\PVCX|Q|
Q	 EQ	Q+1
	ER	:NPORT

	 IF	NUTXT

:	Initialization for User-string texts.
:	0 -	not PVC
:	<0 -	Default (message not specified)
IUTXTS	HS	0
Q	EQ	0
	RE	NPORT
	 IF	.BIT.(PVCG,Q)
	  IF	\UTAP|Q|
QQ	EQ	UTAP|Q|
	HC	EA(UTXA|QQ|)		:assigned string
	  ELSE	:not \UTAP|Q|
	HC	-1			:default (computed) string
	  EI	:\UTAP|Q|
	 ELSE	:.BIT.(PVCG,Q)
	HC	0			:not PVC
	 EI	:.BIT.(PVCG,Q)
Q	EQ	Q+1
	ER	:NPORT
	 EI	:NUTXT
	SUBTTL	(PVC) ":L" PVC PROCESSING

:	The Port-table entry .PVCLH contains a half-word entry for each
:	port.  This entry is initialized to -1.  When P.NEXT is setting up
:	a port for LOGIN, it sets that port's .PVCLH entry to 0.  If the
:	first LOGIN character entered by the user is ":", the entry will
:	incremented at each new character from that port, and dispatched
:	to the appropriate code.  Otherwise, .PVCLH is set to -1.

:	Subsequent character from port which started LOGON with a colon.
:	Handle character based on number previously entered.  If invalid
:	character detected, send escape to supervisor.
:	RLINK -	link register (not used often to return).
PVCLOG	LB	R3,.PVCDO,RPI		:get state
	AIS	R3,1			:advance to next state
	STB	R3,.PVCDO,RPI
	LHL	R4,PVCLGD,R3,R3		:get proper entry from dispatch table
	J	BASE,R4,

:	Dispatch table for :L login.  The routine to get control is based
:	upon the number of characters already received.
PVCLGD	HC	EA(PVCESC)		:0th Character -  shouldn't happen!
	HC	EA(PVCLG1)		:1st Character -- should be ':'.
	HC	EA(PVCLG2)		:2nd Character -- should be 'L'.
	HC	EA(PVCLG3)		:3rd character -- CR or ':'
	HC	EA(PVCLG4)		:4th character -- host number
	HC	EA(PVCLG4)		:5th character -- host number or CR
	HC	EA(PVCLG4)		:6th character -- host number or CR
	HC	EA(PVCLG4)		:7th character -- host number or CR
	HC	EA(PVCLG4)		:8th character -- host number or CR
	HC	EA(PVCLG4)		:9th character -- should be CR
	HC	EA(PVCESC)		:10th character -- error


:	See if port has entered a COLON as first character of LOGIN.
:	Proceed with :L if so, else abort.
PVCLG1	CLHI	R9,3A			:":"?
	JE	RELOG			:yes...just proceed
	TS	.PVCLH,RPI		:no...abort process
	JR	RLINK			:and logon with this character

:	Second character in string.  Should be an 'L'.
PVCLG2	CLHI	R9,4C			:is it an "L"?
	JN	PVCESC			:no...error in :L LOGIN string...
					:	Send escape to supervisor.
	J	RELOG			:proceed with next char

:	Third character in string.  Maybe CR, or might be another colon
:	to specify host number.
PVCLG3	CLHI	R9,0D			:test for carriage return
	JE	PVCSCL			:yes - string terminated.  Tap PVC
					:	control process to generate
					:	login string.
	 IF	NUTXT
	LH	R0,IUTXTS,RP,RP		:check user-string in use for this port
	JGE	PVCESC			:not default user-name...entry error
	 EI	:NUTXT
	CLHI	R9,3A			:got another colon?
	JE	RELOG			:yes - proceed to wait for some numbers
	J	PVCESC			:no - entry error

:	4th - 9th character.  Number or carriage return valid.
PVCLG4	CLHI	R9,0D			:test for CR
	JE	PVCSCL			:yes - string terminated
:	Character must be between 30-39, inclusive
	LHI	R0,-30,R9
	JL	PVCESC			:too low
	CLHI	R0,9
	JG	PVCESC			:too high
	AIS	R0,1			:bias host number digit by 1 so we may
					:	tell difference between 0 and
					:	non-entered character.
	SIS	R3,4
	SLHLS	R3,2			:set up shift index of 4,8,0C
	SLL	R0,0,R3			:align 4-bit number to combine with
					:	what we already may have
	AM	R0,.PVCLH,RPI		:place new digit and proceed
	J	RELOG

:	Abort :L LOGON attempt.  Send ZAPPER to SUPERVISOR.
PVCESC	TS	.PVCLH,RPI		:disable :L scan function
	LHI	RBC,1B			:Escape character
	L	RBP,.LGBUF,RPI
	JAL	RCH,PUTCHR		:place into buffer...
	J	LOG5			:...and kill LOGIN

:	End of :L string.  Prevent further scanning and notify PVC control
:	mechanism that it has work to do for this port.
PVCSCL	L	RBP,.FTBUF,RPI		: ":L" received from port
	JAL	RCH,EMPTY		:empty buffer
	JAL	RLINK,PVCSLS		:fire-up login attempt
	J	RELOG			:now proceed to service it
	SUBTTL	(PVC) PVC UTILITY ROUTINES

:	The routine that follows is called from various places in the
:	code to detect conditions which may require PVC service.

:	Test for AUTO-Terminal-ID port.  Called from HANG/ANSWER when a
:	new port becomes connected.
:	RLINK -	Link register
:	RPI -	port-table pointer
:	RP -	port number
:	Skip-4 return -	succeeded in setting up TID
:	Normal return -	failed, proceed with normal setup
AIDPVC	TS	.PVCLH,RPI		:Disable :L Scanner
	RBT	RP,PVCSTD		:turn off something-to-do
	TBT	RP,PVCARY,,		:test for PVC port
	JER	RLINK			:no
	L	R7,.PVCX,RPI		:Get PVC parameter word
	THI	R7,POHNG		:Allow HANGS?
	JEFS	AIDPV1			:yes
	RBT	RP,TATBIN		:if TID not received in over 16 seconds
        SBT     RP,AH.TO                :Dont hang if tid not recieved*sdw402
        SBT     RP,AH.TO1               :TATBIN also changes a hang signal
        SBT     RP,AH.TO2               :into a ZAPPER. that is unchanged*402
					:	won't get hung
AIDPV1	THI	R7,POAID		:test for AUTO-ID for this port
	JER	RLINK			:no...just exit
	SRL	R7,$A 23		:yes...extract AID index...
	NHI	R7,0FE			:Isolate 2*TID value **6.02 LH
	LH	R4,PVCTID,R7,		:map PVC TID value into useful VIDTT index
	JAL	R5,IZPORT		:initialize consat port parameters
	 IF	X3ON
	JAL	RBF,IXPROF,,		:initialize PAD port parameters
	 EI	:X3ON
	 IF	.2HD
	 GL	ZHDUX
	TBT	RP,HD202,,		:is this a 202 port?
	JEFS	AIDPV2			:no...proceed
	SBT	RP,HAFDUX		:yes...flag as half-duplex
	LA	R0,ZHDUX		:set alternate input
	ST	R0,.VSTAT,RPI
AIDPV2	 EI	:.2HD
	SBT	RP,C.ACP		:set port active
	 IF	BTELEX
	SBT	RP,VB.ACP		:set virtual telex port in active state
	 EI	:BTELEX
	 IF	TTELEX			:** 4.02 LH
	SBT	RP,TT.ACP		:set transparent tlx port in active state
	 EI	:TTELEX
	SBT	RP,AH.TO		:cancel timeouts
	SBT	RP,AH.TO1
	SBT	RP,AH.TO2
	LHL	R4,.TERMT,RPI		:look up terminal-type
	LHL	R9,VID.IZ,R4,		:locate initialization routine
	LHI	R5,SPECFE		:set IIX capable **6.01 LH
	AIS	RLINK,4			:set skip return
	J	BASE,R9,		:exit via initialization process...
					:	(nominally port-establishment
					:	for TID)

	FO	DATA

	EM	:PVC
	 EI	:PVC
]4è