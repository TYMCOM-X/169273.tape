begin "MNSCAN" comment SCANS stream accounting and prints counts of records;
  require "(MPL)SAIL.DEF" source!file;
  require "(SAILIB)EXTEND.REQ" source!file;

require '17 version;

integer TYPE!PTR,LEN!PTR,GRTH43,TOTAL!CNT;
integer YEAR,MONTH,DAY,CNT,FLG,EOF;
integer LEN,TYPE,JOB,SAF,BRK,I,LASTDATE;
string FILE,S,F,T;
boolean ANY;
integer array REC[0:64],REC!CNT[0:45];
define blank = {('40&null)};

preset!with null,"RUN","EXIT","NEXIT","SETNAM","START","REENTER",
  "DDT","PJC","XCHARGE","INIT",null,"PUTLSA","SETLIC","SETE",
  "RUNLIC","TRANSACTION","TATTLE",null,"CREAUX",[13]null,"LOGIN",
  "CHKPNT","LOGOUT","UPTIME";
  string array TYPE!NAME[-1:'43];
preload!with false,[10]true,false,[6]true,[15]false,true,
  false,true,false;
  boolean array TYPE!FLAG[-1:'43];

simple procedure INIT; begin "INIT"
  ttyup(true);
  arrclr(REC!CNT);
  TOTAL!CNT_GRTH43_0;
  TYPE!PTR := point(9,REC[0],8);
  LEN!PTR  := point(6,REC[0],35);
  setbreak(1,")","(","KINS");
  setbreak(2,",-/"," ","KINS");
  open(SAF_getchan,"DSK",'14,5,0,0,0,EOF);
  end "INIT";
simple procedure read!record;
  begin
  integer I;
  REC[0]:=wordin(SAF);
  TYPE:=ldb(TYPE!PTR);
  LEN:=ldb(LEN!PTR);
  TOTAL!CNT:= TOTAL!CNT + 1;
  if TYPE>'43 then begin "SKIP"
    TYPE:= -1;
    GRTH43:= GRTH43 + 1;
    for I:=2 upto LEN
      do wordin(SAF);
  end "SKIP"
  else if TYPE='16 then begin "LICENSE"
    REC[1]:=wordin(SAF);
    REC[7]:=wordin(SAF);
    for I:=4 upto LEN
      do wordin(SAF);
    REC!CNT[TYPE]:= REC!CNT[TYPE] + 1;
  end "LICENSE"
  else begin "OTHER"
    if TYPE='17 and 
      REC[6]='1000004 and 
      (REC[5]=cvsix("OPER") or REC[5]=cvsix("SETE"))
	then TYPE_'20;		! fake record type '20 - TATTLE TALE;
    for I:=1 upto LEN-1
      do if I<33
        then REC[I]:=wordin(SAF)
        else REC[32]:=wordin(SAF);
    REC!CNT[TYPE]:= REC!CNT[TYPE] + 1;
  end "OTHER";
  end;
begin "MAIN"
    ifc compiler!switch("B") thenc usererr(0,0,0,"B"); endc
  INIT;
  outstr("Start date (mon,day,yr): ");
  S:=inchwl;
  MONTH:=cvd(scan(S,2,BRK));
  DAY:=cvd(scan(S,2,BRK));
  YEAR:=cvd(S)mod 100;
  outstr("Number of days [all]: ");
  CNT:=cvd(inchwl);
  if CNT=0 then CNT:='377777777777;
  ANY _ 5;
  while (CNT>0) and (ANY>0) do begin "READ"
    setformat(-2,1);
    F:=cvs(MONTH)&cvs(DAY)&cvs(YEAR)&".SAT";
    lookup(SAF,"(BILLING10)"&F,FLG);
    if FLG then lookup(SAF,"(UN1)"&F,FLG);
    if FLG then lookup(SAF,F,FLG);
    if FLG 
	then ANY _ ANY -1 
	else ANY _ 5;
    DAY:=DAY+1;
    if DAY>31 then begin
      DAY:=1;
      MONTH:=MONTH+1;
      end;
    if MONTH>12 then begin
      MONTH:=1;
      YEAR:=YEAR+1;
      end;
    if FLG then continue "READ";
    CNT:=CNT-1;
    setformat(0,1);
    outstr(crlf&"Reading file "&F);
    do
    READ!RECORD
    until EOF;
    close(SAF);
    end "READ";
  release(SAF);
  print(crlf,crlf,"Record count:",crlf);
  for I_0 step 1 until '43 do
    if REC!CNT[I] neq 0 then
       print("    '",cvos(I)," ",TYPE!NAME[I],blank,REC!CNT[I],crlf);
  print("Record type > '43 = ",GRTH43,crlf,crlf,"Total record count = ",
    TOTAL!CNT,crlf);
  end "MAIN";
end "MNSCAN" .
