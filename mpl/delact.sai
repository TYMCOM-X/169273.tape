begin "delete accounting"

 require "settrm.sai" source!file;

 string file!name, user!name, buf, act!mon, un1, billing10, sys,
        actrty;

 integer break, chan, act!yr, act!month!num, i, error, jobs;

 preload!with
  "JAN", "FEB", "MAR", "APR", "MAY", "JUN", "JUL", "AUG",
  "SEP", "OCT", "NOV", "DEC";
 string array months[1:12];

 preload!with
  31,28,31,30,31,30,31,31,30,31,30,31;
 integer array days!month[1:12];

 procedure delete!file( integer chan; string file!name);
  begin
    print(file!name);

    lookup(chan, file!name, error);

    if error = 0
     then
      begin
       rename (chan,null,null,error);
       if error = 0
        then
         print (" Deleted", nl)
        else
         print (" not DELETED .. unable to rename", nl);
       end
      else
       print(" not DELETED .. File does not exist", nl);

    close(chan);
  end;

simple integer procedure number!jobs;
 return( gettab('15,'11) -1 );

 print( nl, "Delete accounting files", nl, " Version 1.0", nl,nl);

 ttyup(true);

 do
  begin

   error _ 1;
   outstr("Enter month(mmm), year(yy): ");

  buf _ inchwl;

  if length(buf) = 0
   then
    exit;

  setbreak(6," ,", null, "kios");

  act!mon _ scan(buf, 6, break);
  act!month!num _ 0;

 while true do
  begin "check"
  if break = 0
   then
    begin
    print ( nl, "Please enter the MONTH and YEAR seperated by a space",
            " or a comma", nl,nl);
    done "check";
   end;

    for i _ 1 upto 12
      do
       if equ(act!mon,months[i])
        then
         begin
          act!month!num _ i;
          done;
         end;

 if act!month!num = 0
  then
   begin
    print( nl, act!mon, " is not a valid MONTH", nl,nl);
    done "check";
   end;

 act!yr _ intscan(buf,break);

 if break neq 0
  then
   begin
     print( nl, "Year must be numeric", nl,nl);
     done "check";
    end;

   error _ 0;
   done;
 end "check"
 end
  until not error;



 chan _ getchan;

 open(chan,"dsk",0,0,0,0,0,error);

 setformat(-2,0);

 print(nl,nl);

 for i _ 1 upto days!month[act!month!num]
  do
   begin
    file!name _ "(un1)" & cvs(act!month!num) & cvs(i) & cvs(act!yr) & ".sat";
    delete!file(chan, file!name);
   end;

 print(nl,nl);

 for i _ 1 upto days!month[act!month!num]
  do
   begin
    file!name _ "(sys)" & cvs(act!month!num) & cvs(i) & cvs(act!yr) & ".dat";
    delete!file(chan, file!name);
   end;


 setformat(-3,0);

 print(nl,nl);

 for i _ 1 upto number!jobs
  do
   begin
    file!name _ "(actrty)mga" & cvs(i) & "." & act!mon;
    delete!file(chan, file!name);
   end;

 print(nl, nl);
 for i _ 1 upto number!jobs
  do
   begin
    file!name _ "(actrty)rta" & cvs(i) & "." & act!mon;
    delete!file( chan, file!name);
   end;

 release(chan);

 print(nl, "All done", nl,nl);

 exit;
end; "delete accounting"
