MODULE TC(STACK,CCL,SREG=#17,FREG=#16,VREG=#15,BREG=#14)=

! VERSION 1.0 - MICHAEL GEARY
! Version 2.0 - W.R.Soley 9/8/78 - added type 7 for RPG file

BEGIN

%
THIS PROGRAM IS A DEBUGGING AID WHICH IS INTENDED FOR USE WITH RPG.
THE PROGRAM MAY BE LOADED AND RUN BY ITSELF OR IT MAY BE LOADED IN
CORE WITH RPG.  IF THIS PROGRAM IS LOADED WITH RPG, RPG WILL CALL
THE ROUTINE 'RPGDB' BEFORE RUNNING ANY PROGRAM.  RPGDB WILL TYPE THE
NAME OF THE PROGRAM WHICH RPG WOULD HAVE RUN AND THEN WILL CALL TMPLIST.
IF THIS PROGRAM IS RUN BY ITSELF, ONLY TMPLIST IS CALLED.  THE ROUTINE
TMPLIST TYPES A DIRECTORY OF ALL TMPCOR:XXX AND DSK:###XXX.TMP FILES
AND THEN WAITS FOR A FILE NAME FROM THE TTY.  THE SPECIFIED FILE
IS TYPED AND THEN ANOTHER NAME IS REQUESTED.  ALL FILES ARE TYPED IN
TEXT FORMAT EXCEPT 'RPG', WHICH IS INTERPRETED AND TYPED IN A FAIRLY
READABLE FORMAT.
%

OWN BUFH[3], BUF[#406], JOBNAME, DOT;

EXTERNAL JOBFF;

BIND RINGPTR=0, PTR=1, CNT=2, INCH=0;

MACRO SKIP(OP)=(VREG_1; OP; VREG_0; .VREG)$,
      MSG(MES)=TTCALL(3,PLIT ASCIZ MES)$;

MACHOP CALLI=#47, OPEN=#50, TTCALL=#51, IN=#56, STATO=#61, STATZ=#63,
       INBUF=#64, RELEASE=#71, LOOKUP=#76;

MACHOP JRST=#254; MACRO HALT=JRST(4)$;

ROUTINE TYPE(CHAR)=
  !TYPES .CHAR AND DOES & STUFF FOR CONTROL CHARACTERS
  IF .CHAR NEQ 0 THEN
    (IF .CHAR LSS " " AND .CHAR NEQ #15 AND .CHAR NEQ #12 THEN
      (TTCALL(1,PLIT"&"); CHAR_.CHAR+"A"-1);
    TTCALL(1,CHAR));

ROUTINE TYPSIX(TXT)=
  !TYPES THE SIXBIT MESSAGE IN .TXT
  BEGIN REGISTER P,T;
    P_TXT<36,6>;
    DECR I FROM 6 TO 1 DO IF (T_SCANI(P)) NEQ 0 THEN TYPE(.T+" ");
  END;

ROUTINE TYPNUM(NUM,BASE)=
  !TYPES THE NUMBER .NUM IN BASE .BASE
  BEGIN REGISTER R;
    R_.NUM MOD .BASE; NUM_.NUM/.BASE;
    IF .NUM GTR 0 THEN TYPNUM(.NUM,.BASE);
    TYPE(.R+"0");
  END;

ROUTINE TYPPPN(PPN)=
 BEGIN IF .PPN EQL 0 THEN RETURN;
 TYPE ("[");TYPNUM(.PPN<18,18>,8);TYPE(",");
 TYPNUM(.PPN<0,18>,8); TYPE("]");
END;

ROUTINE RDSIX(MAX)=
  !READS A MAXIMUM OF .MAX SIXBIT CHARACTERS FROM THE TTY.  IF A DOT
  !APPEARS ANYWHERE, SETS DOT_1.  IF CONTROL-S APPEARS ANYWHERE,
  !RESTARTS FROM @JOBSA.  IF CONTROL-D APPEARS ANYWHERE, JUMPS TO @JOBDDT.
  BEGIN REGISTER TXT,T,P; EXTERNAL JOBSA,JOBDDT;
    TXT_0; P_TXT<36,6>; DOT_0;
    WHILE (TTCALL(4,T); .T) NEQ #12 DO IF .T EQL "." THEN DOT_1 ELSE
      IF .T EQL "?S" THEN (MSG('?M?J'); (@JOBSA)()) ELSE
      IF .T EQL "?D" THEN IF .JOBDDT EQL 0 THEN TTCALL(#16,7) ELSE
        (MSG('?M?JDDT'); (@JOBDDT)()) ELSE
      IF (IF .T GEQ "A"+" " THEN T_.T-" "; T_.T-" ") GTR 0 THEN
        IF (MAX_.MAX-1) GEQ 0 THEN REPLACEI(P,.T);
    .TXT
  END;

ROUTINE TYPZOT(NAME,EXT,PPN,DEV)=
BEGIN
  IF .DEV NEQ 0 THEN (TYPSIX(.DEV);TYPE (":"));
  TYPSIX(.NAME);IF .EXT NEQ 0 THEN (TYPE ("."); TYPSIX(.EXT));
  TYPPPN(.PPN);
END;

ROUTINE RDBLK=
  !READS A NEW DISK BLOCK.  1 IF SUCCESSFUL, 0 IF EOF OR TMPCOR.
  IF .BUFH[RINGPTR] EQL 0 THEN 0 ELSE
    IF NOT SKIP(IN(INCH)) THEN 1 ELSE
      IF SKIP(STATZ(INCH,#740000)) THEN 0 ELSE
        (MSG('?M?JINPUT ERROR'); CALLI(0,#12); 0);

ROUTINE READ=
  !READS ONE WORD OR CHARACTER FROM DSK OR TMPCOR INPUT BUFFER
  BEGIN
    WHILE (BUFH[CNT]_.BUFH[CNT]-1) LSS 0 DO IF RDBLK() EQL 0 THEN RETURN -1;
    SCANI(BUFH[PTR])
  END;

ROUTINE TYPTCDIR=
  !TYPE THE NAMES OF ALL FILES IN TMPCOR:
  BEGIN REGISTER T; LOCAL TCBLK[2];
    TCBLK[0]_0; TCBLK[1]<18,18>_-#406; TCBLK[1]<0,18>_BUF<0,0>-1;
    T_4^18+TCBLK<0,0>; SKIP(CALLI(T,#44));
    DECR I FROM .T-1 TO 0 DO (TYPSIX(.BUF[.I]<18,18>); TYPE(" "));
  END;

ROUTINE TYPDSKDIR=
  !TYPE THE NAMES OF ALL DSK:###NAM.TMP FILES
  BEGIN REGISTER LOOK[4],TT; BIND T=LOOK[0];
    IF NOT SKIP(OPEN(INCH,PLIT(#16,SIXBIT'DSK',BUFH<0,0>))) THEN HALT;
    CALLI(LOOK[0],#24); LOOK[1]_SIXBIT'UFD'; LOOK[2]_0; LOOK[3]_1^18+1;
    IF NOT SKIP(LOOKUP(INCH,LOOK)) THEN HALT;
    BUF[0]<18,18>_-(#406-8); BUF[0]<0,18>_BUF<0,0>+8;
    BUF[1]_.JOBNAME+SIXBIT'   ###'; BUF[2]_SIXBIT'TMP';
    BUF[3]_BUF[4]_BUF[5]_0;
    WHILE MONIO(#33,INCH,BUF[0]) DO
     BEGIN TT_8; WHILE .BUF[7] NEQ 0 DO
      BEGIN TYPSIX(.(BUF[.TT])<0,18>); MSG('. '); TT_.TT+2;
       BUF[7]_.BUF[7]-1; END; END;
    RELEASE(INCH);
  END;

ROUTINE TYPDIR=
  !TYPES BOTH TMPCOR AND DSK DIRECTORIES
  (TYPTCDIR(); TYPDSKDIR(); MSG('?M?J'));

ROUTINE OPENTC(NAME,MODE)=
  !READS THE FILE TMPCOR:.NAME INTO BUF AND SETS UP A FAKE BUFFER HEADER
  BEGIN LOCAL TCBLK[2]; REGISTER T;
    BUFH[RINGPTR]_0;
    TCBLK[0]_.NAME; TCBLK[1]<18,18>_-#406; TCBLK[1]<0,18>_BUF<0,0>-1;
    T_1^18+TCBLK<0,0>;
    IF NOT SKIP(CALLI(T,#44)) THEN RETURN 0;
    BUFH[CNT]_.T*(IF .MODE EQL 0 THEN 5 ELSE 1);
    BUFH[PTR]_BUF<36,(IF .MODE EQL 0 THEN 7 ELSE 36)>;
    1
  END;

ROUTINE OPENDSK(NAME,MODE)=
  !LOOKS UP THE FILE DSK:###(.NAME).TMP
  BEGIN REGISTER LOOK[4];
    LOOK[0]_.MODE; LOOK[1]_SIXBIT'DSK'; LOOK[2]_BUFH<0,0>;
    IF NOT SKIP(OPEN(INCH,LOOK)) THEN HALT;
    JOBFF_BUF<0,0>; INBUF(INCH,2);
    LOOK[0]_.JOBNAME+.NAME<18,18>; LOOK[1]_SIXBIT'TMP'; LOOK[2]_LOOK[3]_0;
    SKIP(LOOKUP(INCH,LOOK))
  END;

ROUTINE TYPFNM(DEV,NAME,EXT,PPN)=
  !TYPES THE FILE NAME GIVEN BY THE ARGUMENTS.
  BEGIN
    TYPSIX(.DEV); TYPE(":");
    TYPSIX(.NAME);
    IF .EXT<18,18> NEQ 0 THEN (TYPE("."); TYPSIX(.EXT<18,18>));
    IF .PPN NEQ 0 THEN (TYPE("["); TYPNUM(.PPN<18,18>,8); TYPE(",");
      TYPNUM(.PPN<0,18>,8); TYPE("]"));
  END;

ROUTINE TYPRPG=
  !TYPES THE 'RPG' FILE, GIVING EACH ENTRY IN A READABLE FORMAT
  BEGIN REGISTER HEAD,T,NM,TT; BIND MAXTYP=8;
    WHILE (HEAD_READ()) GTR 0 DO
      BEGIN
        MSG('TYPE '); TYPNUM(.HEAD<18,18>,8); TYPE(" ");
        IF .HEAD<18,18> GTR MAXTYP THEN (MSG('ILLEGAL'); RETURN);
        CASE .HEAD<18,18>-1 OF
          SET
%1%         BEGIN
              MSG('(SPECIAL RPG): ');
              IF .HEAD<0,18> NEQ 6 THEN (MSG('WRONG LENGTH'); RETURN);
              TYPFNM(READ(),READ(),READ(),(READ();READ()));
              READ();
            END;
%2%         BEGIN
              MSG('(SPECIAL PROCESSORS): ');
              T_.HEAD<0,18>/7;
              IF .HEAD<0,18> MOD 7 NEQ 0 THEN (MSG('WRONG LENGTH'); RETURN);
              DECR I FROM .T TO 1 DO
                BEGIN
                  IF .I NEQ .T THEN TTCALL(3,PLIT ASCIZ', ');
                  TYPSIX(READ()); TYPE("=");
                  TYPFNM(READ(),READ(),READ(),(READ();READ()));
                  READ();
                END;
            END;
%3%             BEGIN
                  MSG ('(SPECIAL DDT): ');
                  IF .HEAD<0,18> NEQ 4 THEN (MSG ('WRONG LENGTH'); RETURN);
          T_READ(); TYPZOT(READ(),READ(),READ(),.T);
          END;
%4%
BEGIN
        MSG ('(NONSTANDARDS-OBSOLETE): ');
        T_.HEAD<0,18>/4;
        IF .HEAD<0,18> MOD 4 NEQ 0 THEN (MSG ('WRONG LENGTH'); RETURN);
        DECR I FROM .T TO 1 DO
          BEGIN
          IF .I NEQ .T THEN TTCALL(3,PLIT ASCIZ ', ');
          TYPZOT (READ(),READ(),READ(),READ());
          END;
END;
%5%
BEGIN
        MSG ('(DEFAULT PROCESSOR): ');
        TYPSIX (READ());
END;
%6%
BEGIN
        MSG ('(NONSTANDARDS):?M?J?I');
        T_.HEAD<0,18>/8;
        IF .HEAD<0,18> MOD 8 NEQ 0 THEN (MSG ('WRONG LENGTH'); RETURN);
        DECR I FROM .T TO 1 DO
          BEGIN
          IF .I NEQ .T THEN MSG('?M?J?I');
          TYPZOT (NM_READ(),READ(),READ(),READ());
          IF (TT_READ()) NEQ .NM THEN (MSG (' SWITCH='); TYPSIX(.TT));
          TT_READ();
          IF .NM<18,18> NEQ .TT<18,18> THEN
                (MSG (' EXTENSION='); TYPSIX(.TT<18,18>));
          IF (TT_READ()) NEQ 0 THEN (MSG (' NEXT='); TYPSIX(.TT<18,18>));
          IF (TT_READ()) NEQ 0 THEN (MSG(' OUTPUT='); TYPSIX(.TT<18,18>));
          END;
END;
%7%
BEGIN
        MSG ('(MAILBOX): ');
        IF .HEAD<0,18> NEQ 6 THEN (MSG ('WRONG LENGTH'); RETURN);
        IF READ() NEQ 5 THEN (MSG ('WRONG FORMAT'); RETURN);
        T_READ();
        TYPZOT(READ(),READ(),.T,0);
        READ();READ();
END;
%8%
BEGIN
        MSG ('(DO LIST): ');
        T_.HEAD<0,18>/3;
        IF .HEAD<0,18> MOD 3 NEQ 0 THEN (MSG ('WRONG LENGTH'); RETURN);
        DECR I FROM .T TO 1 DO
        BEGIN
                TT_READ(); NM_READ();
                IF .TT EQL 1 THEN (MSG ('LOG'); READ())
                ELSE (IF .TT EQL 0 THEN (MSG ('DEFAULT'); READ())
                ELSE (IF .NM EQL 0 THEN (TYPPPN(.TT); READ())
                ELSE (msg('('); TYPSIX(.NM); TYPSIX(READ()); msg(')'))
                ));
                IF .I NEQ 1 THEN MSG (', ');
        END;
END;
          TES;
        MSG('?M?J');
      END;
  END;

ROUTINE TMPLIST=
  !THIS ROUTINE DRIVES ALMOST EVERYTHING ELSE
  BEGIN REGISTER NAME,MODE,T;
    CALLI(0,0); CALLI(T,#30); JOBNAME_0;
    INCR I FROM 18 TO 30 BY 6 DO (JOBNAME<.I,6>_.T MOD 10 +#20; T_.T/10);
    TYPDIR();
    WHILE (TYPE(":"); NAME_RDSIX(3)) NEQ 0 DO
      BEGIN
        MODE_(IF .NAME EQL SIXBIT'RPG' THEN #14 ELSE 0);
        IF (IF (IF .DOT EQL 0 THEN OPENTC(.NAME,.MODE)) NEQ 0 THEN 1 ELSE
          OPENDSK(.NAME,.MODE)) EQL 0 THEN TYPE("??") ELSE
            IF .NAME EQL SIXBIT'RPG' THEN TYPRPG() ELSE
              WHILE (T_READ()) GEQ 0 DO TYPE(.T);
        MSG('?M?J'); RELEASE(INCH);
      END;
  END;

GLOBAL ROUTINE RPGDB(BLOCK)=
  !CALLED BY RPG - TYPES PROGRAM NAME THEN GOES TO TMPLIST
  BEGIN
    STRUCTURE ARY[I]=(..ARY+.I)<0,36>;  MAP ARY BLOCK[6];
    TTCALL(3,PLIT ASCIZ'-> ');
    IF .BLOCK[0] NEQ 0 THEN TYPFNM(.BLOCK[0],.BLOCK[1],.BLOCK[2],.BLOCK[4]);
    MSG('?M?J');
    TMPLIST();
  END;

!THE FOLLOWING CODE IS EXECUTED IF THIS IS RUN AS A STAND-ALONE PROGRAM

TMPLIST();
CALLI(1,#12);

END
ELUDOM
 