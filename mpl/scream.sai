begin "stream scanner shell"

  require '30 version;
  require "(sailib)sail.def" source!file;
  require "(mpl)stream.req" source!file;

!			*  S C R E A M  *
!
! Simple shell for the stream accouting scanning module.  Based on
! (MPL)SCAN, with a liberal amount of plagiarism.
!
! v 20  Dave W. Smith,  December 3, 1981
!
! Edit:
! v 21  7-Jan-82 DWS	Daemon now has the record array as a paramater.
! v 22 11-Jan-82 DWS	"User Friendly" hacks & help messages.
! v 23 23-Feb-82 DWS	Command files.
! v 24  3-Mar-82 DWS	Changed version to reflect change in STREAM.
! v 25 22-Apr-82 DWS	Ditto. 
! v 26 21-Jul-82 LFL    Ditto.
! v 27 ?????????????
! v 30 12-Jun-86 WRS	No source change - load with new library (NODES.MAP);


!   N O T E  to the reader:
!
!  If you're looking to get more information out of the stream, the
!  routine to play with is 'RecordDaemon'.  He gets called by the
!  STREAM package when good records are found, and gets passed the
!  entire stream record.  At the moment most of the info in the
!  record is ignored,  but 'RecordDaemon' is the place to put any
!  code to print it.
!
!  [End of Note] ;

  own boolean Give!Help;

  own string Line, Token1, Token2;
  own boolean ok, wildcards, firstpass;

  own integer B!Comma, B!Paren, B!C;
  own boolean Include!Flag;

  own integer Month, Day, Year, I;

  own boolean OutputToTerm, OutputToFile, Detach;
  own integer FileChan, FileFlag;
  own string FileName;

  preload!with
    cvsix( "SYS" ), cvsix( "LOGOUT" ), 0,0,0,0,0,0;
  own integer array RunBlock[ 0 : 7 ];

  own integer CMD!File, CMD!EOF, CMD!BC, CMD!BT;
! Check for a command file ;

simple procedure CheckForCMDFile;
  begin
    own integer Rescan, Flag;  own string  RescanLine;

    CMD!File := -1;  CMD!EOF := false;
    CMD!BT := getbreak;
    setbreak( CMD!BT, '15, '12, "INS" );

    start!code
      setom Rescan;	! assume that we can rescan ;
      ttcall '10,;	! rescan ;
      ttcall '14,;	! skpinl ;
       setzm Rescan;	!  no line rescanned ;
    end;

    if ( not Rescan ) then return;
    RescanLine := inchwl;
    while ( length( RescanLine ) and ( RescanLine neq ";" )) do
      lop( RescanLine );
    if ( length( RescanLine ) = 0 ) then return;
    lop( RescanLine );
    open( CMD!File := getchan, "DSK", 0,2,0,200,CMD!BC,CMD!EOF );
    lookup( CMD!File, RescanLine, Flag );
    if ( Flag ) then
      usererr( 0,0,"can't find cmd file: " & RescanLine, "x" );
  end;
! Routine to get a "line" of input.  If the current input string ends
! with a comma, concatonate another line to it (unless the line is null) ;

simple string procedure Read!Line;
  begin
    own string Line, S;
    own integer Last!Length;

    Line := null;  Last!Length := 0;
    while true do
      begin
	if ( CMD!File >= 0 ) then
	  begin
	    if ( not CMD!EOF ) then
	      begin
		S := input( CMD!File, CMD!BT );
		print( S, crlf );
		Line := Line & S;
	      end
	    else
	      usererr( 0,0,"EOF in cmd file!", "x" );
	  end
	else
	  Line := Line & inchwl;
	if ( Last!Length = length( Line ) ) then return( Line[ 1 to inf-1 ] );
	if ( Line[ inf for 1 ] neq "," ) then return( Line );
	Last!Length := length( Line );
      end;
  end;
! This is the guy that gets called when a good record is found.
!
! Note that it gets passed the stream record, but ignores it.  If
! you want to extract more information from the record than is
! provided by S!Str, add your own code where indicated. ;

simple procedure RecordDaemon( integer array StreamRec );
  begin
    own string s;

    ! Add any custom code to examine the record here ;

    s := S!Str;

    if ( OutputToTerm ) then
      print( s );
    if ( OutputToFile ) then
      cprint( FileChan, s );

  end;
! --------------------------- Main Program -------------------------- ;


  i := call( -'20, "GETTAB" );
  if (( i land '000033000000 ) = 0 ) then	! rf,ra, wf,wa ;
    begin
      print( "? Requires RF", crlf );
      start!code calli 1,'12; end;
    end;

  setbreak( B!Comma := getbreak, ",", null, "KINS" );
  setbreak( B!Paren := getbreak, ")", null, "KINS" );

  print( crlf );
  print( "Stream Accounting Scanner v",cvos( memory[ '137 ] ), "." );
  print( "  Hello, User.", crlf );
  print( crlf );

  CheckForCMDFIle;

  S!Init;
  ! Find out where the output is to go. ;

  do begin
    ok := false;
    OutputToFile := OutputToTerm := false;

    while ( true ) do
      begin
	print( "Output to (File, Term, Both,?)  [Term]: " );
	Line := Read!Line;
	if ( Line neq "?" ) then done;
	print( crlf );
	print( "Tell me where the results of scanning are to go.",crlf );
	print( "  File  -  Only to a file.",crlf );
	print( "  Term  -  Only to the terminal.",crlf );
	print( "  Both  -  Both to a file and to the terminal.",crlf );
	print( "A filename will be prompted for if needed.",crlf );
	print( crlf );
      end;

    if (( Line = null ) or ( kequ( Line, "Term"[1 for length( Line )] ))) then
      begin
	ok := true;
	Line := "Term";
	OutputToTerm := true;
      end;
    if ( kequ( Line, "Both"[1 for length( Line )] )) then
      begin
	ok := true;
	OutputToTerm := true;
	Line := "File";
      end;
    if ( kequ( Line, "File"[1 for length( Line )] )) then
      begin
	ok := true;
	OutputToFile := true;
	open( FileChan := getchan, "DSK", 0,0,2,FileFlag,FileFlag,FileFlag:=0 );
	if ( FileFlag ) then usererr( 0,0,"Can't open DSK: for file" );
	print( "Output File  [SCAN.LOG]: " );
	Line := Read!Line;
	if ( Line = null ) then Line := "SCAN.LOG";
	enter( FileChan, Line, FileFlag );
	if ( FileFlag ) then usererr( 0,0,"Can't enter file" );
      end;
  end until ( ok );

  print( crlf );
  ! Include user(s) by name or GAN.  If anything "wild" is seen then
  ! proceed on to ask for possible exlusions. ;

  wildcards := false; firstpass := true;

  while ( true ) do
    begin
      print( "User(s) to watch  [All]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "Tell me who to watch out for.  To wit, a list of", crlf );
      print( "one or more of the following, seperated by commas:", crlf );
      print( "  (username)         - a username, parens optional", crlf );
      print( "  GAN OF (username)  - all people in the GAN", crlf );
      print( "  GAN n              - all people in GAN n", crlf );
      print( "To use more than one line, end the line with a comma.",crlf );
      print( crlf );
    end;
  if ( kequ( Line, "ALL" )) then Line := NULL;

  do begin
    ok := true;
    if (( Line = null ) and firstpass ) then
      wildcards := true;
    while ( Line ) do
      begin "line"
	while ( Line = " " ) do lop( Line );
	Token1 := scan( Line, B!Comma, B!C );
	if ( Token1 = null ) then continue;
	if ( kequ( Token1[ 1 to 7 ], "GAN OF " )) then
	  begin
	    Token2 := Token1[ 8 to inf ];
	    I := CVPPN( Token2 );
	    if ( !lh( I ) = 0 ) then
	      begin
		print( "% (", Token2, ") is unknown", crlf );
		ok := false;
	      end
	    else
	      begin
		S!IPPN( I land '777777000000 );
		wildcards := true;
	      end;
	  end
	else if ( kequ( Token1[ 1 to 4 ], "GAN " )) then
	  begin
	    I := cvo( Token1[ 5 to inf ] );
	    if ( I = 0 ) then
	      begin
		print( "% Bad GAN - ", Token2, crlf );
		ok := false;
	      end
	    else
	      begin
		S!IPPN( I lsh 18 );
		wildcards := true;
	      end;
	  end
	else if ( not S!IUser( Token1 )) then
	  begin
	    print( "% Bad user name - ", token1, crlf );
	    ok := false;
	  end;
      end "line";
    if ( not ok ) then
      begin
	print( "User(s) to watch : " );
	Line := Read!Line;
      end;
    firstpass := false;
  end until ( ok );
  ! Exlude user(s) by name or GAN, but only if there were wildcards in
  ! the inclusion specification. (It's pointless to ask who to exlude
  ! if the only inclusions where specific). ;

  if ( wildcards ) then
    begin "wildcards"
      while ( true ) do
	begin
	  print( "User(s) to ignore  [None]: " );
	  Line := Read!Line;
	  if ( Line neq "?" ) then done;
	  print( crlf );
	  print( "Who, if anyone, to ignore.  A list consisting of", crlf );
	  print( "one or more of the following, seperated by commas:", crlf );
	  print( "  (username)         - a username, parens optional", crlf );
	  print( "  GAN OF (username)  - all people in the GAN", crlf );
	  print( "  GAN n              - all people in GAN n", crlf );
	  print( "To use more than one line, end the line with a comma.",crlf );
	  print( crlf );
	end;

    if ( kequ( Line, "None" )) then Line := NULL;

      do begin
	ok := true;
	while ( Line ) do
	  begin "line"
	    while ( Line = " " ) do lop( Line );
	    Token1 := scan( Line, B!Comma, B!C );
	    if ( Token1 = null ) then continue;
	    if ( kequ( Token1[ 1 to 7 ], "GAN OF " )) then
	      begin
		Token2 := Token1[ 8 to inf ];
		I := CVPPN( Token2 );
		if ( !lh( I ) = 0 ) then
		  begin
		    print( "% (", Token2, ") is unknown", crlf );
		    ok := false;
		  end
		else
		  S!XPPN( I land '777777000000 );
	      end
	    else if ( kequ( Token1[ 1 to 4 ], "GAN " )) then
	      begin
		I := cvo( Token1[ 5 to inf ] );
		if ( I = 0 ) then
		  begin
		    print( "% Bad GAN - ", Token2, crlf );
		    ok := false;
		  end
		else
		  S!XPPN( I lsh 18 );
	      end
	    else if ( not S!XUser( Token1 )) then
	      begin
		print( "% bad user name - ", Token1, crlf );
		ok := false;
	      end;
	  end "line";
	if ( not ok ) then
	  begin
	    print( "User(s) to ignore : " );
	    Line := Read!Line;
	  end;
      end until ( ok );
    end "wildcards";

  print( crlf );
  ! Ask what programs are to be included, and remember if any wildcards
  ! where used. ;

  wildcards := false; firstpass := true;

  while ( true ) do
    begin
      print( "Program(s) to watch  [All]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "A list of programs to watch for.  One or more of:", crlf );
      print( "  (user)program  - a specific program", crlf );
      print( "  program        - a program name from any directory", crlf );
      print( "  (user)         - anything program belonging to user", crlf );
      print( "If you need more than one line, end the line with a comma.", crlf );
      print( crlf );
    end;

  if ( kequ( Line, "All" )) then Line := NULL;

  do begin
    ok := true;
    if (( Line = null ) and firstpass ) then
      wildcards := true;
    while ( Line ) do
      begin "line"
	while ( Line = " " ) do lop( Line );
	Token1 := scan( Line, B!Comma, B!C );
	if ( Token1 = null ) then continue;
	if ( not S!IProg( Token1 )) then
	  begin
	    print( "% bad program name - ", Token1, crlf );
	    ok := false;
	  end
	else if ( Token1 = "(" ) then
	  begin
	    Token2 := scan( Token1, B!Paren, B!C );
	    if ( Token1 = null ) then
	      wildcards := true;
	  end;
      end "line";
    if ( not ok ) then
      begin
	print( "Program(s) to watch : " );
	Line := Read!Line;
      end;
    firstpass := false;
  end until ( ok );
  ! If wildcards were used on program inclusion, ask what programs are
  ! to be excluded. ;

  if ( wildcards ) then
    begin "wildcards"
      while ( true ) do
	begin
	  print( "Program(s) to ignore [None]: " );
	  Line := Read!Line;
	  if ( Line neq "?" ) then done;
	  print( crlf );
	  print( "A list of programs to ignore.  One or more of:", crlf );
	  print( "  (user)program  - a specific program", crlf );
	  print( "  program        - a program name from any directory", crlf );
	  print( "  (user)         - anything program belonging to user", crlf );
	  print( "If you need more than one line, end the line with a comma.", crlf );
	  print( crlf );
	end;

    if ( kequ( Line, "None" )) then Line := NULL;

      do begin
	ok := true;
	while ( Line ) do
	  begin "line"
	    while ( Line = " " ) do lop( Line );
	    Token1 := scan( Line, B!Comma, B!C );
	    if ( Token1 = null ) then continue;
	    if ( not S!XProg( Token1 )) then
	      begin
		print( "% bad program name - ", Token1, crlf );
		ok := false;
	      end;
	  end "line";
	if ( not ok ) then
	  begin
	    print( "Program(s) to ignore : " );
	    Line := Read!Line;
	  end;
      end until ( ok );
    end "wildcards";
! Owner exclusion. ;

  while ( true ) do
    begin
      print( "Exclude owner(s)?  [No]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "Answer ""Y"" to cause the owners of programs to be exluded.",crlf );
      print( "E.g. exclude user FOO from run records of (FOO)BAR.", crlf );
      print( crlf );
    end;

  if (( Line = "Y" ) or ( Line = "y" )) then
    S!XOwners;

  print( crlf );
  ! Inclusion/exclusion by node. ;

  wildcards := false;  firstpass := true;

  while ( true ) do
    begin
      print( "Node(s) to watch  [All]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "What nodes, if any are to be watched.  Give me a list of", crlf );
      print( "node names, seperated by commas.  If you need more than one", crlf );
      print( "line, end the line with a comma.", crlf );
      print( crlf );
    end;

  if ( kequ( Line, "all" )) then Line := NULL;

  do begin
    ok := true;
    if (( Line = null ) and firstpass ) then
      wildcards := true;
    while ( Line ) do
      begin "line"
	while ( Line = " " ) do lop( Line );
	Token1 := scan( Line, B!Comma, B!C );
	if ( Token1 = null ) then continue;
	if ( not S!INode( Token1 )) then
	  begin
	    print( "% Bad node name - ", Token1, crlf );
	    ok := false;
	  end;
      end "line";
    if ( not ok ) then
      begin
	print( "Node(s) to watch : " );
	Line := Read!Line;
      end;
    firstpass := false;
  end until ( ok );

  if ( wildcards ) then
    begin "wildcards"

      while ( true ) do
	begin
	  print( "Node(s) to ignore [None]: " );
	  Line := Read!Line;
	  if ( Line neq "?" ) then done;
	  print( crlf );
	  print( "What nodes, if any are to be ignored.  Give me a list of", crlf );
	  print( "node names, seperated by commas.  If you need more than one", crlf );
	  print( "line, end the line with a comma.", crlf );
	  print( crlf );
	end;

      if ( kequ( Line, "None" )) then Line := NULL;

      do begin
	ok := true;
	while ( Line ) do
	  begin "line"
	    while ( Line = " " ) do lop( Line );
	    Token1 := scan( Line, B!Comma, B!C );
	    if ( Token1 = null ) then continue;
	    if ( not S!XNode( Token1 )) then
	      begin
		print( "% Bad node name - ", Token1, crlf );
		ok := false;
	      end;
	  end "line";
	if ( not ok ) then
	  begin
	    print( "Node(s) to ignore : " );
	    Line := Read!Line;
	  end;
	firstpass := false;
      end until ( ok );
    end "wildcards";
  ! Inclusion/exclusion by port. ;

  wildcards := false;

  while ( true ) do
    begin
      print( "Port(s) to watch  [All]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "What ports are to be watched?  Give me a list, seperated", crlf );
      print( "by commas.  If you need more than one line, end the line", crlf );
      print( "with a comma.  Hit return to watch all ports.", crlf );
      print( crlf );
    end;

  if ( kequ( Line, "All" )) then Line := NULL;

  do begin
    ok := true;
    if ( Line = null ) then
      wildcards := true;
    while ( Line ) do
      begin
	while ( Line = " " ) do lop( Line );
	Token1 := scan( Line, B!Comma, B!C );
	if ( Token1 = null ) then continue;
	if ( not S!IPort( cvd( Token1 ))) then
	  begin
	    print( "% Bad port number - ", Token1, crlf );
	    ok := false;
	  end;
      end;
    if ( not ok ) then
      begin
	print( "Port(s) to watch : ", crlf );
	Line := Read!Line;
      end;
  end until ( ok );

  ! ports to exclude ;

  if ( wildcards ) then
    begin "wildcards"

      while ( true ) do
	begin
	  print( "Port(s) to ignore  [None]: " );
	  Line := Read!Line;
	  if ( Line neq "?" ) then done;
	  print( crlf );
	  print( "What ports are to be ignored?  Give me a list, seperated", crlf );
	  print( "by commas.  If you need more than one line, end the line", crlf );
	  print( "with a comma.  Hit return to ignore no ports.", crlf );
	  print( crlf );
	end;

    if ( kequ( Line, "None" )) then Line := NULL;

      do begin
	ok := true;
	while ( Line ) do
	  begin "line"
	    while ( Line = " " ) do lop( Line );
	    Token1 := scan( Line, B!Comma, B!C );
	    if ( Token1 = null ) then continue;
	    if ( not S!XPort( cvd( Token1 ))) then
	      begin
		print( "% Bad port number - ", Token1, crlf );
		ok := false;
	      end;
	  end "line";
	if ( not ok ) then
	  begin
	    print( "Port(s) to ignore : ", crlf );
	    Line := Read!Line;
	  end;
      end until ( ok );
    end "wildcards";

  print( crlf );
  ! more criteria ;

  wildcards := false;  firstpass := true;

  while ( true ) do
    begin
      print( "Record Type(s) to include  [All]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "What types of records do you want to look out for?", crlf );
      print( "Give me a list, seperated by commas.  If you need", crlf );
      print( "more than one line, end the line with a comma.", crlf );
      print( "(Refer to chapter 20 of the YAMM for record types.)", crlf );
      print( crlf );
    end;

  if ( kequ( Line, "All" )) then Line := NULL;

  do begin
    ok := true;
    if (( Line = null ) and firstpass ) then
      wildcards := true;
    while ( Line ) do
      begin "line"
	while ( Line = " " ) do lop( Line );
	Token1 := scan( Line, B!Comma, B!C );
	if ( Token1 = null ) then continue;
	if ( not S!IType( Token1 )) then
	  begin
	    print( "% Unknown type name - ", Token1, crlf );
	    ok := false;
	  end;
      end "line";
    if ( not ok ) then
      begin
	print( "Record(s) to include : " );
	Line := Read!Line;
      end;
    firstpass := false;
  end until ( ok );

  if ( wildcards ) then
    begin "wildcards"

      while ( true ) do
	begin
	  print( "Record Type(s) to exclude  [None]: " );
	  Line := Read!Line;
	  if ( Line neq "?" ) then done;
	  print( crlf );
	  print( "What record types, if any, do you wish to ignore?", crlf );
	  print( "Give me a list, seperated by commas.  If you need", crlf );
	  print( "more than one line, end the line with a comma.", crlf );
	  print( "(See YAMM, chapter 20, for record types)", crlf );
	  print( crlf );
	end;

      if ( kequ( Line, "None" )) then Line := NULL;

      do begin
	ok := true;
	while ( Line ) do
	  begin
	    Token1 := scan( Line, B!Comma, B!C );
	    if ( not S!XType( Token1 )) then
	      begin
		print( "% Bad type name - ", Token1, crlf );
		ok := false;
	      end;
	  end;
	if ( not ok ) then
	  begin
	    print( "Record Type(s) to exclude : " );
	    Line := Read!Line;
	  end;
      end until( ok );
    end "wildcards";

  print( crlf );
  ! Ask for starting date and days to scan, then do it. ;

  while ( true ) do
    begin
      print( "Starting date (month,day,year): " );
      Line := Read!Line;
      if ( Line = NULL ) then continue;
      if ( Line = "?" ) then
	begin
	  print( crlf );
	  print( "What date should I start scanning at?", crlf );
	  print( "E.g. 1,18,57  for January 18, 1957.", crlf );
	  print( crlf );
	  continue;
	end;

      Month := cvd( scan( Line, B!Comma, B!C ));
      Day := cvd( scan( Line, B!Comma, B!C ));
      Year := cvd( scan( Line, B!Comma, B!C ));
      if (( Month = 0 ) or ( Day = 0 ) or ( Year = 0 )) then
        print( "% Bad date - try again", crlf )
      else
	done;
    end;

  S!BDate( Month, Day, Year );

  while ( true ) do
    begin
      print( "Number of days  [All]: " );
      Line := Read!Line;
      if ( Line neq "?" ) then done;
      print( crlf );
      print( "Aw.... You can guess this one!", crlf );
      print( crlf );
    end;

  if ( not (( Line = null ) or kequ( Line, "All" ))) then
    S!NDays( cvd( Line ) );

  print( crlf );
! run detached, if need be ;

  if ( OutputToFile and not OutputToTerm ) then
    begin
      while ( true ) do
	begin
	  print( "Detach?  [Yes]: " );
	  Line := inchwl;
	  if ( Line neq "?" ) then done;
	  print( crlf );
	  print( "Answer ""N"" to keep the job attached to the", crlf );
	  print( "terminal.  Otherwise it will detach itself.", crlf );
	  print( crlf );
	end;
      Detach := not (( Line = "N" ) or ( Line = "n" ));
    end;

  if ( Detach ) then
    begin
      print( "Job ", cvs( call( 0, "PJOB" )), " detaching ..." );
      start!code
	hrroi 1,6;
	calli 1,'41;
	  jfcl;
	tlo 1,'200000;
	calli 1,-'11;
	seto 0,;
	calli 0,-6;
      end;
    end;
  
  S!Scan( RecordDaemon, false );

  if ( OutputToFile ) then
    begin
      close( FileChan );
      release( FileChan );
    end;

  if ( Detach ) then
    calli( '1000000 + location( RunBlock[ 0 ] ), '35 );

  call( '1, "EXIT" );

end "stream scanner shell" ;
   =o6¹