begin "SCAN" comment SCANS stream accounting;
  require "(MPL)SAIL.DEF" source!file;
  require "(SAILIB)EXTEND.REQ" source!file;

require '21 version;

COMMENT 
    Special version for COEES that will allow only COEES records
    to be checked/ L Lindley, 9-8-82

    Additional information printed out in logout record, and
    Special excess TRU report added/ L Lindley, 8-6-82
;

integer JOB!PTR,TYPE!PTR,LEN!PTR,NOD1!PTR,NOD2!PTR,PORT!PTR,TID!PTR;
integer OUT!SW,LOCAL,PPN,FPPN,YEAR,MONTH,DAY,CNT,FNAM,NODE,FLG,EOF;
integer LEN,TYPE,JOB,SAF,BRK,PORT,I,MAP!SIZE,MAP,LASTDATE,PPNMASK;
integer OLDWIDTH,OLDDIGIT,TRU!LIMIT;
string OUT!FILE,USER,FILE,F,S,T;
boolean X!OWN,ANY,DETACHED,X!RPG,I!TRU,S!TRU,S!LIC,C!TIME,SPEC!RPT,SELECTED;
integer array REC[0:64],NOD[0:128],PRT[0:128],NODE!MAP[1:2048];
long real array TRUs[0:128];
string array NODE!NAME[1:2048];
define GMT = ('400000 + '20);
define blank = {('40&null)};
define RESTRICT!GAN = (1056440320);

preset!with null,"RUN","EXIT","NEXIT","SETNAM","START","REENTER",
  "DDT","PJC","XCHARGE","INIT",null,"PUTLSA","SETLIC","SETE",
  "RUNLIC","TRANSACTION","TATTLE",null,"CREAUX",[13]null,"LOGIN",
  null,"LOGOUT",null;
  string array TYPE!NAME[-1:'43];
preload!with false,[10]true,false,[6]true,[15]false,true,
  false,true,false;
  boolean array TYPE!FLAG[-1:'43];

simple procedure INIT; begin "INIT"
  ttyup(true);
  open(MAP_getchan,"DSK",'14,5,0,0,0,EOF);
  lookup(MAP,"(TYMNET)NODES.MAP",FLG);
  LastDate _ -1;
  I:=1;
  if FLG 
    then
      outstr("?Can not read (TYMNET)NODES.MAP"&crlf)
    else do begin
      if (NODE!MAP[I]:=wordin(MAP))=0 then continue;
      NODE!NAME[I]:=cvastr(wordin(MAP))&cvastr(wordin(MAP));
      I:=I+1;
      end until EOF;
  MAP!SIZE:=I-1;
  close(MAP); release(MAP);
  TYPE!PTR := point(9,REC[0],8);
  LEN!PTR  := point(6,REC[0],35);
  NOD1!PTR := point(6,REC[6],19);
  NOD2!PTR := point(6,REC[6],27);
  PORT!PTR := point(7,REC[6],35);
  TID!PTR  := point(4,REC[6],11);
  JOB!PTR  := point(9,REC[0],17);
  LOCAL:=call('777777000006,"GETTAB") lsh -28 land '77;
  setbreak(1,")","(","KINS");
  setbreak(2,",-/"," ","KINS");
  open(SAF_getchan,"DSK",'14,5,0,0,0,EOF);
  end "INIT";

define SET!NO!ATTACH = {start!code
  HRROI 6;
  CALLI '41;
    JFCL;
  TLO '200000;
  CALLI -'11;
  end};


define XA (A) = {
  redefine XM = A-PP;
  ifc XM 
    thenc redefine XX = cvms(XX) & {[} & cvms(XM) & {]"$",};
    elsec endc
  redefine PP = A};

define X1 (A,B) = {
  XA (B);
  redefine XX = cvms(XX) & {A,};
  redefine PP = B+1};

define X2 (A,B,C) = {
  XA (C);
  forc PP _ A stepc 1 untilc B doc {
    redefine XX = cvms(XX) & {PP,};
    } endc
  redefine PP = 1+C+B-A};

define PP = 0;
define XX = {};

x1('40,'0);
x2('101,'132,'1);
x2('54,'72,'33);
x1('52,'52);
x1('47,'53);
x1('44,'54);
xa(45);
redefine XX = cvms(XX)[1 to inf-1];

preset!with XX;
integer array ASCOTB[0:44];

integer XM1,XM2;
string procedure cvR50(integer NM1,NM2);
begin
    string S;
    recursive procedure R50;
    begin "R50" 
      integer P; 
      start!code
	label NAMPR;
	MOVE 3,XM1;
	MOVE 4,XM2;
NAMPR:	MOVE 1,3;
	IDIVI 1,45;
	MOVEM 2,3;
	DIVI 3,45;
	EXCH 3,4;
	EXCH 1,3;
	JUMPE 1,NAMPR;
	MOVEM 3,XM1;
	MOVEM 4,XM2;
	MOVEM 1,P;
      end;
      if XM1 or XM2 then R50;
      S _ S & AscOtb[p];
    end "R50";
    S _ null; XM1 _ NM1; XM2 _ NM2;
    if XM1 or XM2 then R50;
    return (S);
end;



simple integer procedure cvdirppn( string dirname; integer chan(-1) );
! returns the ppn associated with dirname, or 0 on some failure.
  chan is either the number of a channel, or -1 to indicate this
  is to obtain (and release) its own channel. (chan closed after
  call if it is specified).
;
begin	own integer array fildat[0:5];
	integer result, dskchan;
if chan<0
 then begin
	open(dskchan_getchan, "DSK", '17,0,0,
		result,result,result_0);
	if result then begin release(dskchan); return(0); end;
      end
 else dskchan_chan;
lookup(dskchan, if length(dirname)
		 then "("&dirname&")"&"******.***"
		 else "******.***", result );
fileinfo(fildat); result_fildat[3];
if chan < 0 then release(dskchan) else close(dskchan);
return( result );
end;



simple string procedure CVTIME(integer X); begin "CVTIME"
  own integer DATE,TIME,A,B;
  own string S;
  DATE:=lh(X);
  TIME:=rh(X);
  if C!TIME then CNVDTIM(DATE,TIME,GMT,LOCAL);
  if Date neq LastDate 
	then begin
	LastDate_Date;
	if C!TIME then S _ "[ "& stdate(LastDate,'33) &" ]"& crlf
        else S _ "[ "& F[1 to 2]&"/"&F[3 to 4]&"/"&F[5 to 6]&" ]"& crlf
        end
     else S _ null;
        
  getformat(A,B);
  setformat(2,1);
  S:=S& cvs(rh(TIME) div 3600);
  setformat(-2,1);
  S:=S& ":"& cvs(rh(TIME) div 60 mod 60)
      & ":"& cvs(rh(TIME) mod 60);
  setformat(A,B);
  return(S);
  end "CVTIME";

simple procedure read!record(reference integer TYPE,JOB); begin
  integer I;
  REC[0]:=wordin(SAF);
  TYPE:=ldb(TYPE!PTR);
  LEN:=ldb(LEN!PTR);
  JOB:=ldb(JOB!PTR);
  if TYPE>'43 then begin "SKIP"
    TYPE:= -1;
    for I:=2 upto LEN
      do wordin(SAF);
  end "SKIP"
  else if TYPE='16 then begin "LICENSE"
    REC[1]:=wordin(SAF);
    REC[7]:=wordin(SAF);
    for I:=4 upto LEN
      do wordin(SAF);
  end "LICENSE"
  else begin "OTHER"
    if TYPE='17 and 
      REC[6]='1000004 and 
      (REC[5]=cvsix("OPER") or REC[5]=cvsix("SETE"))
	then TYPE_'20;		! fake record type '20 - TATTLE TALE;
    for I:=1 upto LEN-1
      do if I<33
        then REC[I]:=wordin(SAF)
        else REC[33]:=wordin(SAF)
  end "OTHER";
  if Type='40 then "LOGIN" TRUs[Job]_0.0;
  end;

simple string procedure CVLIC(integer LIC); begin
  own integer A,B;
  return(cvos(lh(LIC))&",,"&cvos(rh(LIC)));
  end;

simple string procedure CVNODE(integer NODE); begin
  own integer I;
  for I:=1 upto arrinfo(NODE!MAP,2) do
    if NODE<NODE!MAP[I] then return(cvos(Node))
    else if NODE=NODE!MAP[I] then return(Node!Name[i]);
  return(cvos(Node));
  end;

simple integer procedure CVNAME(string NAME); begin
  own integer I,NODE;
  NODE:=cvo(NAME);
  for I:=1 upto arrinfo(NODE!MAP,2) do
    if equ(NODE!NAME[I],NAME) then NODE:=NODE!MAP[I]
    else nothing;
  return(NODE);
  end "CVNAME";

simple long real procedure CvTRU (reference integer R);
begin
    long real T;
    if R land '777000000000 then
	return (0.0);
    start!code
	DMOVE 1,R;
	DMOVEM 1,T;
	MOVSI 1,'276000;
	IORM 1,T;
	SETZB 1,2;
	DFAD 1,T;
	DMOVEM 1,T;
	end;
    return (T/10000.0);
    end;

simple real procedure TruInc (reference integer R);
begin
    long real T;
    T _ CvTRU(R);
    TRUs[Job] swap T;
    return (TRUs[Job]-T);
    end;

integer LSbrk;
simple procedure LSbrkIni;
setbreak(LSbrk_getbreak,'15&";"&0,null,"SINKZ");
require LSbrkIni initialization;

simple long real procedure CvInfo (reference integer R);
begin
    long real T;
    if R land '777000000000 then
	return (0.0);
    start!code
	DMOVE 1,R;
	DMOVEM 1,T;
	MOVSI 1,'276000;
	IORM 1,T;
	SETZB 1,2;
	DFAD 1,T;
	DMOVEM 1,T;
	end;
    return (T);
    end;

simple string procedure LoginString;
begin
    string S; integer I;
    S _ null;
    for I _ 6 upto LEN do S _ S & cvastr(REC[i]);
    return( scan(S,LSbrk,!SKIP!) );
end;

begin "MAIN"	
    ifc compiler!switch("B") thenc usererr(0,0,0,"B"); endc
  INIT;
  outstr("Version "&cvos(memory['137])&crlf);
  outstr("Default values shown in []"&crlf);
  outstr("Output to (File,Term or Both) [Term]: ");
  OUT!SW:=inchwl;
  if OUT!SW=null then OUT!SW:="Term";
  if OUT!SW neq "T" then begin
    outstr("Output file: ");
    OUT!FILE:=inchwl;
    end;
  SETPRINT(OUT!FILE,OUT!SW);
  outstr("Special excess TRU report? [No]: ");
  SPEC!RPT _ (inchwl="Y");
  if SPEC!RPT then begin
    outstr("TRU/hour threshold?: ");
    TRU!LIMIT_cvo(inchwl);
    end;
  SELECTED_false;
  while SELECTED=false do begin "SelectUsers"
  outstr("User(s) to watch (user, [ppn])  [all]: ");
  USER:=inchwl;
  if equ(User,"ALL") then begin
    PPN _ RESTRICT!GAN; 
    PPNmask _ '777777000000;
    SELECTED_true;
  end
  else if User = "[" then begin "PPN"
    integer S,B;
    setbreak(S _ getbreak,",]"," 	","SINK");
    PPN _ lop(User);  comment discard the "[";
    PPN _ cvo(scan(User,S,B));
    if PPN neq 7676 then
      begin
      print("Not gan 7676",crlf);
      SELECTED_false;
      end
    else SELECTED_true;
    if B neq "," then usererr(B,2,"Ill formed PPN, ',' expected: ","S");
    PPN _ hl(PPN) lor hr(cvo(scan(User,S,B)));
    if B neq "]" then usererr(B,2,"Ill formed PPN, ']' expected: ","S");
    PPNmask _ (((PPN lsh -18)neq 0)lsh 18)lor(((PPN lsh 18)neq 0)lsh -18);
  end "PPN"
  else if length(USER) then begin "USER"
    PPN_cvdirppn(USER);
    if lh(PPN)=0 then usererr(0,1,"User not found","S");
    if PPN land '777777000000 neq RESTRICT!GAN then
      begin
      print("Not in gan 7676",crlf);
      SELECTED_false;
      end
    else SELECTED_true;
    PPNmask _ -1;
  end "USER"
  else
    begin
    PPN _ RESTRICT!GAN; 
    PPNmask _ '777777000000;
    SELECTED_true;
  end ;                                comment default is all of selected gan;
  end "SelectUsers";

  if SPEC!RPT then
    begin
    FPPN_0;
    X!OWN_X!RPG_I!TRU_S!LIC_FALSE;
    S!TRU_TRUE;
    end
  else begin
  outstr("Program to watch ([(user)][file]): ");
  FILE:=inchwl;
  if FILE="("
    then FPPN:=cvdirppn(scan(FILE,1,BRK))
    else FPPN:=0;
  FNAM:=cvsix(FILE);
  outstr("Exclude owner? [No]: ");
  X!OWN:=(inchwl="Y");
  outstr("Exclude SYS:RPG? [No]: ");
  X!RPG:=(inchwl="Y");
  outstr("Node to watch [all]: ");
  do begin
    NODE:=cvname(S:=inchwl);
    if NODE=0 and equ(S,"ALL") then S_null;
    if (length(S)>0) and (NODE=0)
      then outstr("Error, reenter node name or number: ");
    end until (length(S)=0) or NODE;
  outstr("Port to watch [all]: ");
  PORT:=cvo(inchwl);
  end;
  outstr("Start date (mon,day,yr): ");
  S:=inchwl;
  MONTH:=cvd(scan(S,2,BRK));
  DAY:=cvd(scan(S,2,BRK));
  YEAR:=cvd(S)mod 100;
  outstr("Number of days [all]: ");
  CNT:=cvd(inchwl);
  if CNT=0 then CNT:='377777777777;
  if not SPEC!RPT then begin
  outstr("Include record types (type list, ""?"" or <cr>) [all]: ");
  S:=inchwl;
  if equ(S,"ALL") then S_null;
  if length(S) then begin "PROCESS-TYPES"
    if S="?" then begin "PROMPT-TYPES"
      for I:=0 upto arrinfo(TYPE!NAME,2) do
	if length(TYPE!NAME[i]) then begin
	  outstr("  "&TYPE!NAME[i] &"? ");
	  TYPE!FLAG[i]:=(inchwl="Yes");
	  end;
      end "PROMPT-TYPES"
    else begin "SCAN-TYPES"
      arrclr(TYPE!FLAG,false);
      while length(S) do begin
        T:=scan(S,2,BRK);
	for I:=0 upto arrinfo(TYPE!NAME,2) do
	  if equ(TYPE!NAME[i][1 to length(T)],T) then begin
	    TYPE!FLAG[i]:=true;
	    done;
	    end;
	end;
      end "SCAN-TYPES";
  end "PROCESS-TYPES";
  outstr("Show incremental TRUs? [No]: ");
  I!TRU _ (inchwl="Y");
  outstr("Show session TRUs? [No]: ");
  S!TRU _ (inchwl="Y");
                          comment   outstr("Show job license? [No]: ");
  S!LIC _ FALSE;
  end;
  outstr("Convert time? [Yes]: ");
  C!TIME _ (inchwl neq "N");

  if OUT!SW="File"
    then begin 
      outstr("Detach? [Yes]: ");
      DETACHED:=(inchwl neq "N");
      end
    else
      DETACHED:=false;
  if DETACHED then begin
    outstr("Job "&cvs(call(0,"PJOB"))&" detaching...");
    SET!NO!ATTACH;
    calli(0,-'6);
    end;

  ANY _ 5;
  while (CNT>0) and (ANY>0) do begin "READ"
    setformat(-2,1);
    F:=cvs(MONTH)&cvs(DAY)&cvs(YEAR)&".SAT";
    lookup(SAF,"(BILLING10)"&F,FLG);
    if FLG then lookup(SAF,"(UN1)"&F,FLG);
    if FLG then lookup(SAF,F,FLG);
    if FLG 
	then ANY _ ANY -1 
	else ANY _ 5;
    DAY:=DAY+1;
    if DAY>31 then begin
      DAY:=1;
      MONTH:=MONTH+1;
      end;
    if MONTH>12 then begin
      MONTH:=1;
      YEAR:=YEAR+1;
      end;
    if FLG then continue "READ";
    CNT:=CNT-1;
    setformat(0,1);
    if not DETACHED then outstr(crlf&"Reading file "&F);
    do begin "EACH-REC"
    READ!RECORD(TYPE,JOB);
    if TYPE='40 then begin
        NOD[JOB]:=ldb(NOD1!PTR)lsh 6 +ldb(NOD2!PTR);
        PRT[JOB]:=ldb(PORT!PTR);
	end;
    if SPEC!RPT then begin
      if TYPE = '42 and (cvTru(REC[3]) > 100.0) and
         ((((cvTru(REC[3]))*3600)/REC[16]) > TRU!LIMIT) 
        and (PPN=(PPNmask land REC[2])) then
        begin
          print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  "LOGOUT",
	  ", Total TRUs="&cvf(cvTru(REC[3])));

          getformat(OLDWIDTH,OLDDIGIT);
          setformat(12,0);
          print (crlf,
          "  Lookups              ",cvs(REC[8]),
          "    Useti, usetos        ", cvs(REC[21]),crlf,
          "  Blocks read          ", cvs(REC[18]), 
          "    Blocks written       ", cvs(REC[19]),crlf,
          "  Chars in             ",cvs(REC[13]),
          "    Chars out            ",cvs(REC[14]),crlf,
          "  Attach time          ",cvs(REC[16]),
          "    Mapped page count    ",cvs(REC[29]),crlf,
          "  Microcycles          "&cvs(CvInfo(REC[27])),
          "    Prem charg           ", cvs(REC[24]/10000),crlf,
          "  Seconds block port io", cvs(REC[25]),
          "    Block port chars     ", cvs(REC[26]),crlf);

          print (
          "  Lookups*size         ",cvs(REC[9]),
          "    Enter + Renames*size ", cvs(REC[10]),crlf,
          "  Blocks read*size     ", cvs(REC[11]),
          "    Blocks written*size  ",cvs(REC[12]),crlf,
          "  Seconds elapsed*size ",cvs(REC[15]),
          "    Break chars*size     ", cvs(REC[17]), crlf,
          "  Kilo core ticks      ",cvs(CvInfo(REC[6])),crlf);

        setformat(OLDWIDTH,OLDDIGIT);
        end;
       continue "EACH-REC";
       end;

    if (JOB geq 0) and (TYPE geq 0) and TYPE!FLAG[TYPE]
      and((NODE=0)or(NODE=NOD[JOB]))
      and((PORT=0)or(PORT=PRT[JOB]))
      and(PPN=(PPNmask land REC[2]))
      and((FNAM=0)or(FNAM=REC[5]))
      and((FPPN=0)or(FPPN=REC[6]))
      and((not X!OWN)or(REC[2] neq REC[6]))
      and((not X!RPG)or(REC[5] neq cvsix("RPG"))or(REC[6] neq '1000004))
      then case TYPE of begin
	[0][1][2][3][4][5][6]['10]['13]['14]['15] print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  TYPE!NAME[TYPE],blank,
	  (if REC[6] then lokdul(REC[6]) else null),
	  cv6str(REC[5]),
  	  (if TYPE>'10 then " LIC="&CVLIC(REC[7]) else null),
	  (if Type<7 and I!TRU 
		then " TRUs="&cvf(TruInc(REC[3])) else null));
	['7] print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  "PJC=",cvastr(REC[5]),cvastr(REC[6]),cvastr(REC[7]),
	  ",VFY=",(REC[7] land 1));
        ['11]['12] print(crlf,
	  cvtime(REC[1]),blank,
	  TYPE!NAME[TYPE]);
	['16] if S!LIC then print (
	  " LIC="&CVLIC(REC[7]));
	['17] print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  TYPE!NAME[TYPE],blank,
	  (if REC[6] then lokdul(REC[6]) else null),
	  cv6str(REC[5]),
	  (if I!TRU then " TRUs="&cvf(TruInc(REC[3])) else null));
	['20] begin
	    print (crlf,
		cvtime(REC[1]),blank,
	    	"#",cvs(JOB),lokdul(REC[2]),blank,
	    	TYPE!NAME[TYPE],blank,
	    	cv6str(REC[5]));
	    if REC[5]=cvsix("SETE") then print(blank,
	      cvos(REC[11]),",",cvos(REC[10]),blank,
	      (if REC[12] 
		then "failed:"&cvos(REC[8])&",,"&cvos(REC[9]) 
		else "okay"))
	    else if REC[5]=cvsix("OPER") then print(blank,
	      cvR50(REC[8],REC[9]),blank,
	      cvos(rh(REC[11])),
	      (if rh(REC[11]) neq rh(REC[12])
		then "("&cvos(rh(REC[12]))&")"
		else null),blank,
	      case REC[13] of (
		"okay",
		"bad name",
		"bad pswd:"&cvos(REC[10]),
		"not auth"))
	    else print(" ???");
	    if I!TRU then print(" TRUs="&cvf(TruInc(REC[3])));
	  end;
	['22] print (crlf,
	  cvtime(REC[1]),
	  " #",cvs(JOB),lokdul(REC[2]),
	  " CREAUX ",
	  LoginString,
	  " port=",cvos(rh(REC[5])),
	  (if lh(REC[5]) 
	     then (if REC[5]<0
		then " monerr="
		else " superr="
		)& cvos(lh(REC[5]) land '377777)
	     else null )
	  );
	['40] print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  "LOGIN ",
	  cvnode(NOD[JOB]),":",cvos(PRT[JOB]),blank,
	  "TID=",case ldb(TID!PTR) of ("AUX","A","C","E","G","B",
	    "F","J","10","<CR>","D","2780","1200","15","16","17"));
	['42] begin
          print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  "LOGOUT",
	  (if S!TRU then ", Total TRUs="&cvf(cvTru(REC[3])) else null));

          getformat(OLDWIDTH,OLDDIGIT);
          setformat(12,0);
          print (crlf,
          "  Lookups              ",cvs(REC[8]),
          "    Useti, usetos        ", cvs(REC[21]),crlf,
          "  Blocks read          ", cvs(REC[18]), 
          "    Blocks written       ", cvs(REC[19]),crlf,
          "  Chars in             ",cvs(REC[13]),
          "    Chars out            ",cvs(REC[14]),crlf,
          "  Attach time          ",cvs(REC[16]),
          "    Mapped page count    ",cvs(REC[29]),crlf,
          "  Microcycles          "&cvs(CvInfo(REC[27])),
          "    Prem charg           ", cvs(REC[24]/10000),crlf,
          "  Seconds block port io", cvs(REC[25]),
          "    Block port chars     ", cvs(REC[26]),crlf);

          print (
          "  Lookups*size         ",cvs(REC[9]),
          "    Enter + Renames*size ", cvs(REC[10]),crlf,
          "  Blocks read*size     ", cvs(REC[11]),
          "    Blocks written*size  ",cvs(REC[12]),crlf,
          "  Seconds elapsed*size ",cvs(REC[15]),
          "    Break chars*size     ", cvs(REC[17]), crlf,
          "  Kilo core ticks      ",cvs(CvInfo(REC[6])),crlf);

!          "  Pages put in u wk set", cvs(REC[30]),
PP          "    Page working set*size",cvs(CvInfo(REC[31])),crlf,;

          setformat(OLDWIDTH,OLDDIGIT);
          end;
        
	['41] print (crlf,
	  cvtime(REC[1]),blank,
	  "#",cvs(JOB),lokdul(REC[2]),blank,
	  "CHKPNT",
	  (if S!TRU then ", Total TRUs="&cvf(cvTru(REC[3])) else null));
        ['400][['402] nothing;
	else print(crlf,"?Record type "&cvos(TYPE)) end;
      end "EACH-REC"
    until EOF;
    close(SAF);
    end "READ";
  release(SAF);
  setprint(null,"N");
  if DETACHED then begin "KJOB"
    integer array RB[0:7];
    RB[0]:=cvsix("SYS");
    RB[1]:=cvsix("LOGOUT");
    calli(hl(1)+location(RB[0]),'35);
    end "KJOB";
  end "MAIN";
end "SCAN" .
    ? „