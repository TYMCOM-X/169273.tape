begin "UNDER"
  integer BRK,BREAK,EOF,I,FLAG,x1,x2,x3,STRIKE!COUNT,NORMAL;
  integer StartPage,Line,Page,Pos,Display,Overscore;
  string Text,IN!FILE,OUT!FILE,SWITCH;

require "(SAILIB)SAIL.DEF" source!file;
require 1 version;

define HELP "{}" = {"
[<outfile>=]<infile>[$<pageno>]

If outfile is ommited, output will be typed on TTY:.
""_"" may replace ""="".
If pageno is given, text pages less than pageno will be suppressed.
"};

simple procedure xout(integer chan;string data);
    if OUT!FILE
	then out(Chan,Data)
	else while length(data) do auxclv(-1,lop(data),3);

simple procedure PARSE(string Text);
begin "PARSE"
  own string TMPSTR;
  breakset(10,"!""#$%&'-=^~\|@`[{_]}+;*:/,<>"&13,"I");
  do begin
    TMPSTR_scan(Text,10,BREAK);
    if BREAK="=" or BREAK="_" then begin
      OUT!FILE_TMPSTR;
      end
    else if BREAK="/" then begin
      IN!FILE_TMPSTR;
      end
    else if BREAK=0 then begin
      if IN!FILE=null then
        IN!FILE_TMPSTR
      else
        SWITCH_TMPSTR;
      end
    else begin
      outstr("?Illegal character in input line: """&BREAK&"""");
      call(0,"EXIT");
      end;
    end
  until BREAK < 32;
  Normal _ false;
  case SWITCH of begin
    ["0"]["1"]["2"]["3"]["4"]["5"]["6"]["7"]["8"]["9"] begin
	Strike!Count _ SWITCH-"0";
	Normal _ false;
	Overscore _ false;
	Display _ false;
	end;
    ["N"][0] begin
	Strike!Count _ 1;
	Normal _ true;
	Overscore _ false;
	Display _ false;
	end;
    ["D"] begin
	Strike!Count _ 0;
	Normal _ false;
	Overscore _ false;
	Display _ true;
	end;
    ["O"] begin
	Strike!Count _ 1;
	Normal _ true;
	Overscore _ true;
	Display _ false;
	end;
    else usererr(SWITCH,2,"?illegal switch: ")
    end;
  if IN!FILE="?"
    or equ(IN!FILE,"HELP") 
    then begin
      outstr(HELP);
      call(0,"EXIT");
      end;
  end "PARSE";


begin "MAIN"
  ttyup(true);
  outstr("* ");
  PARSE(inchwl);
  if !SKIP!='33
	then StartPage _ cvd(inchwl)
	else StartPage _ 1;
  open(1,"DSK",0,2,0,512,BREAK,EOF);
  lookup(1,IN!FILE,FLAG);
  if FLAG then usererr(0,0,"?Can not lookup file","X");
  if OUT!FILE then begin
    open(2,"DSK",0,0,2,x1,x2,x3);
    enter(2,OUT!FILE,FLAG);
    if FLAG then usererr(0,0,"?Can not enter file","X");
    end;
  setbreak(1,#lf,'177,"FINS");

  Line_0; Pos_0;
  if StartPage leq 1 then xout(2,12); comment form feed at start!;
  while not EOF do begin
    integer C;
    Text_input(1,1);
    if EOF then DONE;
    Page _ Line % 66 +1;
    if length(text) and Page geq StartPage then begin
	if (Pos % 66) < (Line % 66) then begin
	    xout(2,#ff); 
	    Pos _ Line % 66 * 66;
	end;
	while POS < LINE do begin
	    xout(2,#lf);
	    Pos _ Pos +1;
	end;
	xout(2,text);
    end;
    if BREAK then Line _ Line + 1;
  end;
  xout(2,12);	comment form feed at end! ;

  close(1);
  close(2);
  release(1);
  release(2);
  call(0,"EXIT");
  end "MAIN";
end "UNDER";
