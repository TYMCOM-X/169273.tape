begin "TALL"

require "(HELGEM)AUX" load!module;
require "(SAILIB)sail.def" source!file;

define  nhosts = 50;
boolean File,
	det,
	BlankLine;
integer RecFilChan,
	Eof,
	BK,
	CmdFileChan,
	DSKIN,
	foo,
	h,
	PortNumber,
	lasthost;
string  UserName,
	line,
	commands;

external integer procedure auxcre(string username);
external procedure auxzap(integer port);
external procedure auxout(integer port; string text);
external integer procedure auxinc(integer port);
integer array hosts[0:nhosts-1];

string procedure outstr(string s);
begin
	integer c;
if File then
  out (RecFilChan,S);
while (c _ lop(s)) neq null do
  auxclv(-1,c,3);
end;

string procedure prompt(string msg);
begin
	string S;
	integer SKIP; ! receives character on which input broke (open) ;
while true do
  begin 
    if DSKIN then ! DSKIN will may be set below. ;
    ! Get a char from the command file ;
      begin 
        S _ input(CmdFileChan,1);
        SKIP _ !SKIP!; ! activation char from input ;
	if EOF then
	  begin
	    DSKIN _ false;
	    continue;
          end;
      end
    else 
      begin 
        outstr (MSG);
        S _ inchwl;  ! get line from tty ;
        SKIP _ !SKIP!; ! activation char from input ;
        if !SKIP! = #esc  then ! line preceded by esc will not echo ;
				! won't show in log file either. ;
          begin 
            auxclr(-1,memory['200],'64); ! tty no echo ;
	    S _ S & inchwl; ! & #esc removed ;
	    SKIP _ !SKIP!; ! activation char from input ;
	    outstr(crlf); ! tty cr will not be echo'd so force it ;
	    auxclr(-1,memory['000],'64); ! tty echo ;
	  end ;
      end ;

    if equ(S[1 to 2],"@@") then
    ! If command file specified with "@filename" contains the name of ;
    !    another command file which is proceeded by "@". ;
      begin
        !SKIP! _ SKIP; ! activation char from input ;
        return(S[2 to inf]); ! return filename found in file ;
      end;

    if S="@" then  
    ! Find the specified command file. ;
      begin 
	open(CmdFileChan_getchan,"DSK",1,1,0, 256,!SKIP!,EOF);
        ! Character on which input breaks wil be saved in !SKIP! ;
	lookup(CmdFileChan,S[2 to inf],EOF_0);

	if EOF then ! file as specified could not be found ;
		    ! so try it with "MHX" extension. ;
	  lookup(CmdFileChan,S[2 to inf]&".MHX",EOF_0);

	if EOF then ! Still not found so complain. ;
	  begin
	    outstr("?file "&S[2 to INF]&" not found"&crlf);
	    DSKIN _ false;
	    continue;
	  end;
	DSKIN _ true;
	continue;
      end ;

    !SKIP! _ SKIP; 
    return(S);
  end; ! While true loop ;
end;

procedure gethosts;
begin
	integer h,
		OneHost,
                HostFilChan,
		eof,
		HostFilLnBrk;
	string  line, 
		savline;
while true do
  begin "get hosts"
    outstr("hosts: ");
    if EQU(Line_inchwl,null) then
      continue; ! try again if nothing but cr was typed. ;
    if line[1 for 1] = "?" then 
      begin "help"
	outstr("enter either: "&crlf);
        outstr("- host numbers, separated by commas"&crlf);
        outstr("- 10s (to get all PDP10s)"&crlf);
      end "help"
    else
      if equ(line,"10s") or equ(line,"10S") then 
        begin "HostsFromFile"
  	  setbreak(HostFilLnBrk_GetBreak,#lf,#cr,"I");
	  open(HostFilChan_getchan,"DSK",0,2,0,500,0,Eof);
	  lookup(HostFilChan,"(helgem)TENS.lst",Eof); 
          if Eof then
            usererr(0,0,"Can't read (helgem)Tens.lst","X")
          else
            begin ! Get hosts from file;
              Line_input(HostFilChan,HostFilLnBrk);
              for h _ 0 step 1 until nhosts-1 do
                begin
                  lasthost _ h-1;
                  if (savline_line) = null then
                    done;
                  OneHost _ intscan(line,foo); 
		  ! foo set to -1 if intscan cannot find an integer ;
                  if foo = -1 then
                    begin
                      print(savline&"? -- "
                            &"type ? for help"&crlf);
		      continue "get hosts";
                    end;
                  hosts[h] _ OneHost;
                end;
	      return;
	    end
        end "HostsFromFile"
      else 
        begin "HostsFromTerm"
        for h _ 0 step 1 until nhosts-1 do
          begin
            lasthost _ h-1;
            if (savline_line) = null then
              done;
            OneHost _ intscan(line,foo);
            ! foo set to -1 if intscan cannot find an integer ;
            if foo = -1 then
               begin
                 print(savline&"? -- "
                       &"type ? for help"&crlf);
                 continue "get hosts";
               end;
             hosts[h] _ OneHost;
           end;
             return;
	end "HostsFromTerm" ;

  end "get hosts" ;
end;

string procedure doesc(string s); 
! any char preceded by "$" is converted to control char ;
! ex: $v becomes '26. ;
! if a literal dollar sign is needed in the string then send "$$" . ;
begin
	string  res;
	integer c;
res _ null;
while c _ lop(s) do
  if c = "$" then
    begin
      if "$" = (c _ lop(s)) then
        ! consecutive dollar signs so leave one in string ;
        res_res&"$"
      else
        ! char following first "$" becomes control char ;
        res_res&(c land '37);
    end  ! if c = "$" ;
  else
    res_res&c;
return(res);
end;

simple procedure PrintRules;
begin
	string c;
  print("rules?[n]");
  if EQU(c _ inchwl,null) then return;
  if EQU(c ,"n") then return;
  print("tall COMMAND RULES:",crlf);
  print("'@filename' will cause tall to get commands from command file.",crlf);
  print("  The command file may contain the name of another file if ",crlf);
  print("  the other file name is preceded by '@'.",crlf);
  print("'$char' will cause tall to insert CTRL'char' into the command.",crlf);
  print("  Literal dollar sign is inserted by typing '$$'.",crlf);
  print("Anything typed on a given line following esc is not echoed ",crlf);
  print("  nor will such type be included in log file (good for ");
  print("passwords).",crlf,crlf);
end;

simple procedure getcommands;
begin
	string cl,fs,temp;
	external integer !skip!; ! INCHWL places activation char in !SKIP! ;
PrintRules;
commands _ null;
fs _ "commands (end with ctrl-D):"&13&10;
while true do
  begin
    temp _ prompt(fs); ! get from term or file. ;
    cl _ doesc(temp);  ! look for control char markers. ;
    commands _ commands & cl; ! add onto command string. ;
    fs _ null; ! get more lines of commands with no prompt. ;
    ! evaluates terminator for each line of typing at command prompt ;
    case !SKIP! of               ! !SKIP! = terminating char ;
      begin
        [4] begin ! found ^d at end of command string ;
	      if length(cl) then commands_commands & #cr;
              done; ! so done ;
            end;
	[#lf] commands_commands&#cr;
	else commands_commands& !SKIP!
      end;
  end;
end;

procedure getfilestuff;
begin
ttyup(true);
setbreak(1,#lf&#eot,#cr,"SINK");
File _ (prompt("Output to file? ") = "Y"); ! File set if response = "Y" ;
if File then
  begin
    open(RecFilChan_getchan,"DSK",0,0,4, 0,0,Eof);
    enter(RecFilChan,prompt("output file: "),Eof);
    if Eof then ! could not create this file. ;
      usererr(Eof,2,"Enter failure: ","X");
    det _ (prompt("detach? ") = "Y"); ! det set if response = "Y" ;
  end
else
  det _ false; ! No use detaching without record file. ;
end;



Procedure GoTohosts;
begin 
	integer ch;
for h _ 0 step 1 until lasthost do 
  begin "hostloop"
    outstr(crlf&cvs(hosts[h])&":");
    PortNumber _ auxcre(UserName&":"&cvs(hosts[h]));
    if PortNumber = -1 then 
      begin
        outstr(" cannot build circuit"&crlf);
        continue;
      end;
    outstr(crlf);
    auxout(PortNumber,#cr&#cr); ! proj code/attach msgs;
    auxout(PortNumber,";("&#cr); ! begin marker;
    auxout(PortNumber,commands);
    auxout(PortNumber,";)"&#cr); ! end marker;
    auxout(PortNumber,"exit"&crlf);
  
    ! search for begin marker;
    while true do 
      begin
        ch _ auxinc(PortNumber); ! catches echo;
        if ch = -1 then 
          begin
            outstr("Not valid on this host."&crlf);
            continue "hostloop";
          end;
        if ch = ";" then
          begin "BeginMarkFound"
            ch _ auxinc(PortNumber);
            if ch = -1 then
              begin
                outstr("*** fail B "&crlf);
                continue "hostloop";
              end;
            if ch = "(" then
              begin
                ch_auxinc(PortNumber);
                if ch = -1 then
                  begin
                    outstr("*** fail C "&crlf);
                    continue "hostloop";
                  end;
                done;
              end;
          end "BeginMarkFound" ;
      end;
  
    ! copy command output until end marker;
    BlankLine _ true;
    while true do
      begin "Respond"
             ! loops in one char at a time as they ;
             ! come in through the pipeline        ;
        ch _ auxinc(PortNumber);
        if ch = -1 then
          begin
            outstr("*** fail D "&crlf);
            continue "hostloop";
          end;
        if ch > '40 then
          BlankLine _ false;
        if ch = ";" then
          begin "EndMarkFound"
            ch _ auxinc(PortNumber);
            if ch = -1 then
              begin
                outstr("*** fail E "&crlf);
                continue "hostloop";
              end;
            if ch = ")" then
              done;
            outstr(";"&ch);
          end "EndMarkFound"
        else
          if (ch neq #lf) or not BlankLine then
            ! Print whatever comes back ;
            outstr(ch);  
        if ch = #lf then
          BlankLine _ true;
      end; "Respond"
    ! copy characters until zapped;
    if (ch _ auxinc(PortNumber)) neq -1 then
      outstr(crlf);
    while (ch _ auxinc(PortNumber)) neq -1 do ;
    auxzap(PortNumber); ! kill the circuit ;
  end;  "hostloop"
end;

!  *** main *** ;
print("welcome to ");

UserName _ cv6str(calli(-1 lsh 18 lor -'22, '41))
	 & cv6str(calli(-1 lsh 18 lor -'21, '41));
print("TALL, ",UserName,".",crlf);
setbreak(1,#lf&#eot,#cr,"SINF");

gethosts;

getcommands;
getfilestuff;

if det then
  begin
    print("Job ",call(0,"PJOB")," detaching...");
    calli(0,-6); ! attach command port to frame ;
  end;

Gotohosts;

close(RecFilChan);
release(RecFilChan);
 if det then
  calli(0,-'130);	      ! LOGOFF;
 call(0,"EXIT");               ! Supress "End of SAIL execution". ;

end "TALL"
   