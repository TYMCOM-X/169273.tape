$TERMINALS

  <identifier> = ident
  <intconst> = intconst
  <realconst> = realconst
  <stringconst> = stringconst
  NIL = nilsy
  NOT = notsy
  '**' = powerop
  <mulop> = mulop
  <addop> = addop
  <relop> = relop
  '(' = lparent
  ')' = rparent
  '[' = lbracket
  ']' = rbracket
  ',' = comma
  '.' = period
  '^' = arrow
  ':' = colon
  ':=' = becomes
  ';' = semicolon
  '..' = elipsis
  LABEL = labelsy
  CONST = constsy
  TYPE = typesy
  VAR = varsy
  FUNCTION = functionsy
  PROCEDURE = proceduresy
  EXCEPTION = exceptionsy
  EXTERNAL = externalsy
  PUBLIC = publicsy
  STATIC = staticsy
  FORWARD = forwardsy
  OPTIONS = optionssy
  PACKED = packedsy
  SET = setsy
  ARRAY = arraysy
  RECORD = recordsy
  STRING = stringsy
  FILE = filesy
  PREC = precsy
  PROGRAM = programsy
  MODULE = modulesy
  DATAMODULE = datamodsy
  ENVMODULE = envmodsy
  BEGIN = beginsy
  IF = ifsy
  CASE = casesy
  REPEAT = repeatsy
  WHILE = whilesy
  FOR = forsy
  WITH = withsy
  LOOP = loopsy
  GOTO = gotosy
  RETURN = returnsy
  STOP = stopsy
  <io function> = iosy
  EXIT = exitsy
  END = endsy
  THEN = thensy
  ELSE = elsesy
  UNTIL = untilsy
  OF = ofsy
  DO = dosy
  TO = tosy
  DOWNTO = downtosy
  OTHERS = otherssy
  ALLCONDITIONS = allcondsy
  <eof> = eofsy


$NONTERMINALS

<program id> = program_id
<module id> = module_id
<datamodule id> = datamod_id
<envmodule id> = envmod_id
<subr options> = subr_options
<statement> = null_stmt				(* PLACE HOLDING TYPE FOR TREE *)
<simple statement> = simple_stmt
<goto statement> = goto_stmt
<io statement> = io_stmt
<return statement> = return_stmt
<stop statement> = stop_stmt
<if head> = if_stmt
<for head> = for_stmt
<while head> = while_stmt
<case head> = case_stmt
<with head> = with_stmt
<exit head> = exit_clause
<until clause> = until_clause
<expression> = exprtree				(* PLACE HOLDING NODE TYPE FOR TREE *)
<not op> = not_op
<sign op> = sign_op
<paren expr> = paren_expr
<set expression> = set_expr
<array qualifier> = array_qualifier
<field qualifier> = field_qualifier
<ptr qualifier> = ptr_qualifier
<function qualifier> = func_qualifier
<type decl> = type_decl				(* PLACE HOLDING NODE TYPE FOR TREE *)
<packed type> = packed_type
<set type> = set_type
<string type> = string_type
<pointer type> = pointer_type
<subr type> = subr_type
<array type> = array_type
<packed array type> = pk_array_type
<record type> = record_type
<file type> = file_type
<parameter list> = parm_list
<var parm decl> = var_parm_decl
<value parm decl> = value_parm_decl
<const id decl> = const_id_decl
<var id decl> = var_id_decl
<type id decl> = type_id_decl
<identifier list> = id_list
<field id decl> = field_id_decl
<variant part> = variant_part
<tag field> = tag_field
<variant case> = variant_case
<range list> = range_list
<declaration> = declaration			(* PLACE HOLDING NODE TYPE FOR TREE *)
<label declaration> = label_declaration
<const declaration> = const_declaration
<var declaration> = var_declaration
<type declaration> = type_declaration
<condition declaration> = cond_declaration
<subr decl> = subr_decl
<star> = starsy
<io arg> = io_arg


$CODES

  (* Tree building actions *)

  attach attachnt attachqual infixop returnnt returnterm returnsubr
  returnqual attachopt

  (* Syntactic error recovery actions *)

  extratext progidexp semiexp progexp dotexp declexp labelidexp nodecl
  noending typeexp initvalexp colonexp  subrnameexp subrbodyexp parmdeclexp idexp
  chkeq chkstar eqexp lbracketexp idxtypeexp ofexp
  nofieldsemi novariant nocaselab lparenexp nostatement nosemi norhs noifpred
  thenexp chksemielse foridxexp becomesexp forinitexp forwayexp forfinexp doexp
  untilexp untilpredexp whilepredexp nocase stmtincaseexp nocasedelim nowithref
  labelexp endstmtexp ifexp operandexp expressionexp rparenexp rbracketexp rangeexp
  upbexp fieldexp eqinstead chksign declrequired nohandler

$GRAMMAR

<program> ::=		<dat or env module>				|
			<program or module>				;

<program or module> ::=	[ <program id>		$s returnnt		|
			  <module id>		$s returnnt
						$f returnnt		]
			[ <declarations>				]
			[ <compound statement>				]
			'.'			$f dotexp
			<eof>			$f extratext		;

<dat or env module> ::=	( <datamodule id>	$s returnnt		|
			  <envmodule id>	$s returnnt		)
			[ <declarations>				]
			END			$f endstmtexp
			'.'			$f dotexp
			<eof>			$f extratext		;

<program id> ::=	PROGRAM
			<identifier>		$s attach
						$f progidexp
			[ <subr options>	$s attachnt		]
			';'			$f semiexp		;

<module id> ::=		MODULE
			<identifier>		$s attach
						$f progidexp
			[ <subr options>	$s attachnt		]
			';'			$f semiexp		;

<datamodule id> ::=	DATAMODULE
			<identifier>		$s attach
						$f progidexp
			[ <subr options>	$s attachnt		]
			';'			$f semiexp		;

<envmodule id> ::=	ENVMODULE
			<identifier>		$s attach
						$f progidexp
			[ <subr options>	$s attachnt		]
			';'			$f semiexp		;

<declarations> ::=	<declaration>		$f nodecl
			[ <declaration>		$f nodecl		]* ;

<declaration> ::=	( <public declaration>				|
			  <external declaration>   			|
			  <static declaration>				|
			  <simple declaration>				) ;

<public declaration> ::=
			PUBLIC			$s attach
			( <const declaration>	$s returnqual		|
			  <subr declaration>				|
			  <var declaration>	$s returnqual		|
			  <condition declaration>	$s returnqual	|
			  <label declaration>	$s returnqual		|
			  <type declaration>	$s returnqual
						$f declrequired		);

<external declaration> ::=
			EXTERNAL		$s attach
			( <const declaration>	$s returnqual		|
			  <var declaration>	$s returnqual		|
			  <condition declaration>	$s returnqual	|
			  <subr decl>		$s returnqual		|
			  <label declaration>	$s returnqual		|
			  <type declaration>	$s returnqual
						$f declrequired		);

<static declaration> ::=
			STATIC			$s attach
			( <var declaration>	$s returnqual		|
			  <const declaration>	$s returnqual		|
			  <subr declaration>				|
			  <label declaration>	$s returnqual		|
			  <condition declaration>	$s returnqual	|
			  <type declaration>	$s returnqual
						$f declrequired		);

<simple declaration> ::=
			( <label declaration>	$s returnnt		|
			  <const declaration>	$s returnnt		|
			  <type declaration>	$s returnnt		|
			  <var declaration>	$s returnnt		|
			  <condition declaration>	$s returnqual	|
			  <subr declaration>				) ;

<label declaration> ::=	LABEL
			<intconst>		$s attach
						$f labelidexp
			[ ','
			  <intconst>		$s attach
						$f labelidexp		]*
			[ ';'			$f noending		]  ;

<const declaration> ::=	CONST
			<const id decl>		$s attachnt
						$f declexp
			[ <const id decl>	$s attachnt		]* ;

<const id decl> ::=	<identifier>		$s attach
			( <typed const>					|
			  ( ':='				    |
			    <equals>		$f becomesexp	    )
			  <expression>		$f initvalexp		)
			[ ';'			$f noending		] ;

<typed const> ::=	':'			$s attach
			<type decl>		$f typeexp
			[ ( ':='				    |
			    <equals>				    )
			  <expression>		$f initvalexp		] ;

<var declaration> ::=	VAR
			<var id decl>		$s attachnt
						$f declexp
			[ <var id decl>		$s attachnt		]* ;

<var id decl> ::=	<identifier list>	$s attachnt
			':'			$f colonexp
			<type decl>		$f typeexp
			[ <becomes>
			  <expression>		$f initvalexp		]
			[ ';'			$f noending		] ;

<type declaration> ::=	TYPE
			<type id decl>		$s attachnt
						$f declexp
			[ <type id decl>	$s attachnt		]* ;

<type id decl> ::=	<identifier>		$s attach
			<equals>		$f eqexp
			<type decl>		$f typeexp
			[ ';'			$f noending		] ;

<identifier list> ::=	<identifier>		$s attach
			[ ','
			  <identifier>		$s attach
						$f idexp		]* ;

<condition declaration> ::=
			EXCEPTION
			<identifier list>	$f idexp
			[ ';'			$f noending		] ;

<subr declaration> ::=	<subr decl>
			[ FORWARD		$s returnsubr
						$f returnsubr		|
			  <subr body>					]
			[ ';'			$f noending		] ;

<subr decl> ::=		( PROCEDURE		$s attach		|
			  FUNCTION		$s attach		)
			<identifier>		$s attach
						$f subrnameexp
			[ <parameter list>	$s attachnt		]
			[ <parm type>					]
			[ <subr options>	$s attachnt		]
			[ ';'			$f noending		] ;

<parameter list> ::=	'('
			[ <parm decl>
			  [ ';'
			    <parm decl>		$f parmdeclexp		]* ]
			')'			$f rparenexp		;

<parm decl> ::=		<var parm decl>		$s attachnt		|
			<value parm decl>	$s attachnt		;

<var parm decl> ::=	VAR
			<value parm decl>	$f parmdeclexp		;

	(* The contorted BNF below exists in order to have generalized
	   <type decl>s in parameter lists.  The problem exists because it is
	   impossible to tell between a parameter declaration containing only
	   a <type decl> and one listing the parameter names, without examining
	   the following token to see if it is a comma, colon, elipsis or
	   expression symbol.  *)

<value parm decl> ::=	<vpd id left>					|
			<type decl>					;

<vpd id left> ::=	<identifier>		$s attach
			[ <parm id list>				|
			  <parm type>					|
			  [ <array qualifier>	$s attachqual		|
			    <field qualifier>	$s attachqual		|
			    <ptr qualifier>	$s attachqual		|
			    <function qualifier>   $s attachqual	]*
			  [ <mulop>		$s attach
			    <unary expr>	$s infixop
						$f operandexp		]*
			  [ <addop>		$s attach
			    <factor>		$s infixop
						$f operandexp		]*
			  [ '..'		$s attach
			    <term>		$s infixop
						$f upbexp		]  ] ;

<parm id list> ::=	','
			<identifier>		$s attach
						$f idexp
			[ ','
			  <identifier>		$s attach
						$f idexp		]*
			<parm type>		$f typeexp		;

<parm type> ::=		':'
			<type decl>		$f typeexp		;

<subr body> ::=		[ <declaration>		$f nodecl		]*
			<compound statement>	$f subrbodyexp		;

<type decl> ::=		<packed type>		$s attachnt		|
			<unpacked type>					;

<packed type> ::=	PACKED
			( <packed array type>	$s attachnt		|
			  [ '['
			    <expression>	$f expressionexp
			    ']'			$f rbracketexp		]
			  <unpacked type>	$f typeexp		) ;

<unpacked type> ::=	<range type>					|
			<string type>		$s attachnt		|
			<pointer type>		$s attachnt		|
			<set type>		$s attachnt		|
			<aggregate type>				|
			<subr type>		$s attachnt		;

    (* Note below we use <term> instead of <expression> because: relational
       expressions are ambiguous in a constant declaration because of the
       use of '=' following a type, and because subranges of boolean are kind
       of silly anyway. *)

<range type> ::=	<term>	
			[ '..'			$s attach
			  <term>		$f upbexp
			  [ <prec clause>	$s infixop
						$f infixop		] ] ;

<prec clause> ::=	PREC			$s attach
			<term>			$f expressionexp	;

<pointer type> ::=	'^'
			<type decl>		$f typeexp		;

<string type> ::=	STRING
			[ '['
			  <upb expr>		$f expressionexp
			  ']'			$f rbracketexp		] ;

<upb expr> ::=		<term>						|
			<star>			$s attachnt		;

<set type> ::=		SET
			OF			$f ofexp
			<range type>		$f typeexp		;

<aggregate type> ::=	<array type>		$s attachnt		|
			<record type>		$s attachnt		|
			<file type>		$s attachnt		;

<array type> ::=	ARRAY
			'['			$f lbracketexp
			<index type>		$f idxtypeexp
			[ ','
			  <index type>		$f idxtypeexp		]*
			']'			$f rbracketexp
			OF			$f ofexp
			<type decl>		$f typeexp		;

<packed array type> ::=	<array type>					;

<index type> ::=	<star>			$s attachnt		|
			<term>
			[ '..'			$s attach
			  <upb expr>		$s infixop
						$f upbexp		] ;

<record type> ::=	RECORD
			<field list>
			END			$f endstmtexp		;

<field list> ::=	[ <field id decl>	$s attachnt		]*
			[ <variant part>	$s attachnt		]  ;

<field id decl> ::=	<identifier list>	$s attachnt
			':'			$f colonexp
			<type decl>		$f typeexp
			[ ';'			$f nofieldsemi		] ;

<variant part> ::=	CASE
			<tag field>		$s attachnt
						$f idexp
			OF			$f ofexp
			<variant case>		$s attachnt
						$f novariant
			[ <variant case>	$s attachnt		]* ;

<tag field> ::=		<tag decl> |
			<type decl>					;

<tag decl> ::=		<identifier>		$s attach
			[ ':'
			  <type decl>		$f typeexp		] ;

<variant case> ::=	( <range list>		$s attachnt		|
			  OTHERS		$s attach		)
			':'			$f colonexp
			'('			$f lparenexp
			<field list>
			')'			$f rparenexp
			[ ';'			$f nofieldsemi		] ;

<file type> ::=		FILE
			OF			$f ofexp
			[ <star>		$s attachnt		|
			  <type decl>		$f typeexp		] ;

<subr type> ::=		( PROCEDURE		$s attach		|
			  FUNCTION		$s attach		)
			[ <parameter list>	$s attachnt		]
			[ <parm type>					] ;

<subr options> ::=	OPTIONS
			<option>		$s attachopt
						$f idexp
			[ ','
			  <option>		$s attachopt
						$f idexp		]* ;

<option> ::=		[ <star>		$s attachnt		]
			<identifier>		$s attach
			[ '('
			  <sub option>		$f idexp
			  [ ','
			    <sub option>	$f idexp		]*
			  ')'			$f rparenexp		]  ;

<sub option> ::=	<identifier>		$s attach		|
			<intconst>		$s attach		;

<statement list> ::=	[ <stmt in list>				|
			  ';'						]* ;

<stmt in list> ::=	( <labeled statement>				|
			  <statement>		$f nostatement		)
			[ ';'			$f nosemi		] ;

<labeled statement> ::=	<intconst>		$s returnterm
			':'			$f colonexp
			[ <statement>		$f nostatement		] ;

<statement> ::=		( <simple statement>	$s returnnt		|
			  <goto statement>	$s returnnt		|
			  <io statement>	$s returnnt		|
			  <return statement>	$s returnnt		|
			  <stop statement>	$s returnnt		|
			  <compound statement>				|
			  <if statement>				|
			  <for statement>				|
			  <repeat statement>				|
			  <while statement>				|
			  <loop statement>				|
			  <case statement>				|
			  <with statement>				|
			  <exit clause>					) ;

<simple statement> ::=	<reference>
			[ <becomes>
			  <expression>		$f norhs		] ;

<goto statement> ::=	GOTO
			<intconst>		$s attach
						$f labelexp		;

<io statement> ::=	<io function>			$s attach
			[ '('
			  [ <io arg>		$s attachnt
			    [ ','
			      <io arg>		$s attachnt
						$f expressionexp	]* ]
			  ')'			$f rparenexp		] ;

<io arg> ::=		<expression>
			[ ':'
			  <expression>		$f expressionexp	]* ;

<return statement> ::=	RETURN						;

<stop statement> ::=	STOP						;

<compound statement> ::=
			BEGIN			$s returnterm
			<statement list>
			[ <handler clause>				]
			END			$s returnterm
						$f endstmtexp		;

<handler clause> ::=	EXCEPTION		$s returnterm
			<handler>		$f nohandler
			[ <handler>					]* ;

<handler> ::=		( <identifier list>	$s returnnt		|
			  OTHERS		$s returnterm		|
			  ALLCONDITIONS		$s returnterm		)
			':'			$f colonexp
			[ <statement>		$f stmtincaseexp	]
			[ ';'			$f nocasedelim		] ;

<if statement> ::=	<if head>		$s returnnt
			[ <statement>		$f returnnt		]
			[ ELSE			$s returnterm
						$f chksemielse
			  [ <statement>		$f returnnt		] ] ;

<if head> ::=		IF
			<expression>		$f noifpred
			THEN			$f thenexp		;

<for statement> ::=	<for head>		$s returnnt
			[ <statement>		$f returnnt		] ;

<for head> ::=		FOR
			<reference>		$f foridxexp
			<becomes>		$f becomesexp
			<expression>		$f forinitexp
			<for way>		$f forwayexp
			<expression>		$f forfinexp
			DO			$f doexp		;

<for way> ::=		TO			$s attach		|
			DOWNTO			$s attach		;

<repeat statement> ::=	REPEAT			$s returnterm
			<statement list>
			<until clause>		$s returnnt		;

<until clause> ::=	UNTIL			$f untilexp
			<expression>		$f untilpredexp		;

<while statement> ::=	<while head>		$s returnnt
			[ <statement>		$f returnnt		] ;

<while head> ::=	WHILE
			<expression>		$f whilepredexp
			DO			$f doexp		;

<loop statement> ::=	LOOP			$s returnterm
			<statement list>
			END			$s returnterm
						$f endstmtexp		;

<case statement> ::=	<case head>		$s returnnt
			<case member>		$f nocase
			[ <case member>					]*
			END			$s returnterm
						$f endstmtexp		;

<case head> ::=		CASE
			<expression>		$f expressionexp
			OF			$f ofexp		;

<case member> ::=	( <range list>		$s returnnt		|
			  OTHERS		$s returnterm		)
			':'			$f colonexp
			[ <statement>		$f stmtincaseexp	]
			[ ';'			$f nocasedelim		] ;

<with statement> ::=	<with head>		$s returnnt
			[ <statement>		$f returnnt		] ;

<with head> ::=		WITH
			<reference>		$f nowithref
			[ ','
			  <reference>		$f nowithref		]*
			DO			$f doexp		;

<exit clause> ::=	<exit head>		$s returnnt
			[ DO			$s returnterm
			  [ <statement>		$f returnnt		] ] ;

<exit head> ::=		EXIT
			IF			$f ifexp
			<expression>		$f noifpred		;

<expression> ::=	<term>
			[ <relop>		$s attach
			  <term>		$s infixop
						$f operandexp		]* ;

<term> ::=		<factor>
			[ <addop>		$s attach
			  <factor>		$s infixop
						$f operandexp		]* ;

<factor> ::=		<power expr>
			[ <mulop>		$s attach
			  <power expr>		$s infixop
						$f operandexp		]* ;

<power expr> ::=	<unary expr>
			[ '**'			$s attach
			  <unary expr>		$s infixop
						$f operandexp		] ;

<unary expr> ::=	( <not op>		$s attachnt		|
			  <sign op>		$s attachnt		|
			  <simple expression>				) ;

<not op> ::=		NOT
			<unary expr>		$f operandexp		;

<sign op> ::=		<sign>
			<unary expr>		$f operandexp		;

<simple expression> ::=	<intconst>		$s attach		|
			<realconst>		$s attach		|
			<stringconst>		$s attach		|
			NIL			$s attach		|
			<reference>					|
			<paren expr>		$s attachnt		|
			<set expression>	$s attachnt		;

<paren expr> ::=	'('
			[ <paren term>
			  [ ','
			    <paren term>	$f expressionexp	]* ]
			')'			$f rparenexp		;

<paren term> ::=	<expression>
			[ OF			$s attach
			  <expression>		$s infixop
						$f expressionexp	] ;

<set expression> ::=	'['
			[ <range list>					]
			']'			$f rbracketexp		;

<range list> ::=	<range>
			[ ','
			  <range>		$f rangeexp		]* ;

<range> ::=		<expression>
			[ '..'			$s attach
			  <expression>		$s infixop
						$f expressionexp	];

<reference> ::=		<identifier>		$s attach
			[ <array qualifier>	$s attachqual		|
			  <field qualifier>	$s attachqual		|
			  <ptr qualifier>	$s attachqual		|
			  <function qualifier>	$s attachqual		]* ;

<array qualifier> ::=	'['
			<subscript>		$f expressionexp
			[ ','
			  <subscript>		$f expressionexp	]*
			']' rbracketexp		;

<subscript> ::=		<expression>
			[ ':'			$s attach
			  <expression>		$s infixop
						$f expressionexp	] ;

<field qualifier> ::=	'.'
			<identifier>		$s attach
						$f fieldexp		;

<ptr qualifier> ::=	'^'						;

<function qualifier> ::=
			'('
			[ <expression>
			  [ ','
			    <expression>	$f expressionexp	]* ]
			')'			$f rparenexp		;

<becomes> ::=		':='						|
			<equals>		$s eqinstead		;

<equals> ::=		<relop>			$s chkeq		;

<star> ::=		<mulop>			$s chkstar		;

<sign> ::=		<addop>			$s chksign		;

$END
@Rˆ