:      SIO MICROCODE VERSION D
:      PROGRAM STARTS HERE ON POWER UP OR CARD RESTART COMMAND
:      FIRST 8 BITS OF FIRST FOUR WORDS MUST HAVE EVEN PARITY
       A1+COUNTD,CONQ,1        START COUNTER AT PORT 1
       A1,ZQ+OR+RAMF,BA        ASYNC CODE USES REG A FOR TIMOUTS
ILOOP  A1+SIOD,CONST,18FF      ISSUE RESET CHANNEL, SEE IF CARD IS THERE
       A1,MUX,460 CLEAR MUX, RESET CHANNEL, FORCE DATA LINES HIGH, READ STATUS
       A1+CJS,,TRUE+LIDLE      WILL READ FF IF NO SIO CHIP AT THIS ADDRESS
       ,CONQ,RIIDLE
       RAMD,OUTQ,RINTPC
       ,CONQ,TIIDLE
       RAMD,OUTQ,TINTPC
       RAMD,ALU+DZ+AND+QREG,RELOC
       RAMD,OUTQ,LWLIM
       RAMD,ALU+ZQ+EXNOR,STAT
       ,CONQ,8                 THIS BIT TELLS SOFTWARE ABOUT COMPLETION
       RAMD,OUTQ,ABORT         OF STATUS REQUEST
       ,CONQ,1000
       RAMD,OUTQ,WINDOW
       RAMD,ALU+DZ+AND,RCHECK
       RAMD,ALU+DZ+AND,RICNT
       RAMD,OUTQ,RBSIZE
       ,CONQ,100
       RAMD,OUTQ,RRSIZE
       ,CONQ,0FF
ILOOP0 CJP,,SIOBY+ILOOP0       460 SEQUENCE TAKES A WHILE
       ,SIO+DQ+OR+QREG
       ,ZQ+EXNOR
       CJP,,FE0+ILOOP2
       AA,ZA+ADD+RAMF,CARRY+BA REG A HAS 1+NUMBER OF LINES FOR ASYNC TIMEOUT
ILOOP1 COUNT,COUNT+DZ+OR
       CJP,,FE0+MLOOP1
       JMAP,,ILOOP
ILOOP2 ,CONQ,RSTART            NONEXISTENT PORT, DO NOT WASTE TIME ON IT
       RAMD,OUTQ,TINTPC
       JMAP,,ILOOP1
RSTART ,,0     SHUT OFF INADVERTENTLY STARTED SIO SEQUENCE
       JMAP+COUNTD,ALU+DZ+AND,MLOOP1   GO TO CHANNEL 0
:
:      MAIN LOOP
:      DISPATCH TO THE SIX PROGRAM COUNTERS TO SERVICE INTERLEAVED PROGRAMS
:      TRY NEVER TO SPEND MORE THAN 15 MICROSECONDS ON ANY ONE SHOT
MLOOP  CJP,,FE0+MSERVE         PROCESS MUX INTERFACE AFTER CHANNEL ZERO
MLOOP1 REGD,RAM,TINTPC
       JRP+SIOD,CONST,11
TIIDLE ,,440   OUTPUT RIGHT COMMAND, READ R1,R0
       REGD,RAM,TBPC
       JRP+REGD,RAM,RINTPC
TBIDLE JRP+REGD,RAM,RBPC
RIIDLE JRP+REGD,RAM,TSPC
RBIDLE CJP,,SIOBY+RBIDLE
       JRP+RAMD,SIO+DZ+OR+RAMF,STAT+B1
TSIDLE REGD,RAM,RSPC
       JRP+A1,ALU+ZA+OR
RSIDLE JMAP+COUNT,COUNT+DZ+OR,MLOOP  DONE WITH THIS PORT, GO TO NEXT ONE
:
:      QUAD WORD QUALIFIER
:      CALLED WITH VIRTUAL QUAD ADDRESS IN R2
:      RETURNS WITH NEGATIVE R5 (AND F3 SET) IF OUT OF RANGE
:      ELSE R5 HAS NUMBER OF QUAD WORDS LESS ONE REMAINING
:      R3 AND Q HAVE HIGH ORDER 4 BITS OF RELOCATED ADDRESS
:      R4 HAS REMAINING 16 (LOW ORDER 4 BITS WILL BE ZERO)
QWQ    ,RAM+DZ+OR+QREG,LWLIM
       A2,AQ+SUBS+RAMF,CARRY+B5
       CRTN+A5,ZA+OR,F3
       ,RAM+LOADQ,WINDOW
       A5,AQ+SUBR+RAMF,B5
       CRTN+A5,ZA+OR,F3
       ,RAM+LOADQ,RELOC
       A2,AQ+ADD+RAMF,B4
       ,CONQ,0
       A4,ZA+OR+RAMQU,B4
       A4,ZA+OR+RAMQU,B4
       A4,ZA+OR+RAMQU,B4
       A4,ZA+OR+RAMQU,B4
       CRTN,ZQ+OR+RAMF,TRUE+B3
:
:      INTERRUPT PROCESSING
TINTER ,,440
       CJP,CONQ,IPEND+TIIDLE
       RAMD,OUTQ,TINTPC
       ,RAM+LOADQ,TINTFG
       ,COUNT+DQ+OR+QREG
       JMAP+MUXD,ALU+ZQ+OR,TIIDLE
RINTER CJP,CONQ,IPEND+RIIDLE
       RAMD,OUTQ,RINTPC
       ,RAM+LOADQ,RINTFG
       ,COUNT+DQ+OR+QREG
       JMAP+MUXD,OUTQ,RIIDLE
:
:      MUX BUS SERVICE
:      PROCESSED AFTER CHANNEL ZERO
MSERVE SIOD                    ACTIVATE STAT AND W/D LINES FOR ONE CYCLE
       ,,428                   TO RESET OPTO ISOLATED BOARDS
       CJP,MUXLN,MUXR+MSET
       JMAP,,MSEXIT            ALLOW TIME FOR SIO TO COMPLETE
MSET   CJP+COUNTD,MUXLN+LOADQ,BUS8+MSERR
       CJP,MUXLN+RAMQU,BUS9+MSE47
       CJP,OUTQ,BUS10+MSE23
       CJP,,BUS10+MSE1
:      CODE 0  START INPUT
       ,MUX+DZ+OR+RAMF,B2
       CJS,,TRUE+QWQ
       CJP,,F3+MSERR
       A3+RAMD,ALU+ZA+OR,RCADDH
       A4+RAMD,ALU+ZA+OR,RCADDL
       ,CONQ,RBCOM
       RAMD,OUTQ,RBPC
       ,CONQ,RSIDLE
       RAMD,OUTQ,RSPC
MSEXIT COUNTD,CONST,1
       JMAP,,MLOOP1
:      CODE 1  START OUTPUT
MSE1   ,MUX+DZ+OR+RAMF,B2
       CJS,,TRUE+QWQ
       CJP,,F3+MSERR
       A3+RAMD,ALU+ZA+OR,TCADDH
       A4+RAMD,ALU+ZA+OR,TCADDL
       ,CONQ,TBCOM
       RAMD,OUTQ,TBPC
       ,CONQ,TSIDLE
       RAMD,OUTQ,TSPC
       JMAP,,MSEXIT
MSERR  ,CONQ,80
       RAMD,OUTQ,TINTFG
       ,CONQ,TINTER
       RAMD,OUTQ,TINTPC
       JMAP,MUX,MSEXIT
MSE23  CJP,,BUS10+MSE3
:      CODE 2  RELOCATION
       RAMD,MUX,RELOC
       JMAP,,MSEXIT
:      CODE 3  LOWER LIMIT
MSE3   RAMD,MUX,LWLIM
       JMAP,,MSEXIT
MSE47  CJP,OUTQ,BUS10+MSE67
       CJP,,BUS10+MSE5
:      CODE 4  DMA WINDOW SIZE
       RAMD,MUX+LOADQ,WINDOW
       ,CONST+DQ+NOTRS,1FFF
       CJP,,FE0+MSEXIT
       JMAP,,MSERR
:      CODE 5  STATUS
MSE5   ,MUX+DZ+OR+RAMF,B2
       CJS,,TRUE+QWQ
       CJP,,F3+MSERR
       A3+DMAD,ALU+ZA+OR
       DMAH,RAM+DZ+OR+RAMF,RCHECK+BF
       A4+DMAD,ALU+ZA+OR
       A4+DMAL,ZA+ADD+RAMF,CARRY+B4
       DMAD,RAM,STAT
       SIOD,CONQ,0F
       ,,42C                   READ CUSTOM CHIP STATUS
       AF,CONST+DA+AND+RAMF,0FF
       AF,RAM+DA+OR+RAMF,RICNT+BF
MSE5A  CJP,,DMABY+MSE5A
       A4+DMAD,ALU+ZA+ADD+RAMF,CARRY+B4
       DMAL,RAM+DZ+OR+RAMF,ABORT+B5
       AF+DMAD,ALU+ZA+OR
       A4,ZA+ADD+RAMF,CARRY+B4
MSE5B  CJP,,DMABY+MSE5B
       A4+DMAD,ALU+ZA+ADD,CARRY
       DMAL,SIO+DQ+AND+QREG
       A5+DMAD,ALU+AQ+OR
MSE5C  CJP,,DMABY+MSE5C
       JMAP,,MSEXIT
MSE67  CJP,,BUS10+MSE7
:      CODE 6  RESET CARD
       JMAP,,0
:      CODE 7, RESET LINE  (JUST STOP ANY CURRENT CHANNEL PROGRAMS)
MSE7   CJS,MUX,TRUE+LIDLE
       JMAP,,MSEXIT
:
:      SIO TRANSMIT SERVICE FOR HDLC
:      FIRST CHARACTER
TSF    CJP,,BUS5+TSF1         CHECK OUTPUT BUFFER EMPTY
       JMAP,,TSIDLE
TSF1   ,RAM+LOADQ,TDBUF        GET LEFT CHARACTER
       ,CONST+DQ+NOTRS+QREG,0FF  DISCARD RIGHT CHARACTER
       ,CONST+DQ+OR+QREG,0C0   MERGE IN RESET UNDERRUN COMMAND
       SIOD,OUTQ
       ,,450                   OUTPUT LEFT CHARACTER, THEN RIGHT COMMAND
       ,RAM+LOADQ,TDCNT        GET BYTE COUNT
       ,ZQ+SUBR+QREG
       RAMD,ALU+ZQ+SUBR,TDCNT
TSF2   ,CONQ,TSR
TSF3   RAMD,OUTQ,TSPC
       REGD,RAM,RSPC
TSWAIT JRP+A1,ALU+ZA+OR+QREG,SIOBY+TSWAIT
:      LEFT CHARACTER
TSL    CJP,,BUS5+TSL1
       JMAP,,TSIDLE
TSL1   SIOD,RAM,TDBUF
       A1,ALU+ZA+OR,410        SEND LEFT CHARACTER
       CJP,,BUS1+TSOVER
       ,RAM+LOADQ,TDCNT        DECREMENT TRANSMIT RECORD COUNT
       RAMD,ALU+ZQ+SUBR+QREG,TDCNT
       CJP,,F3+TSL2
       JMAP,,TSF2
TSL2   ,CONQ,TSEND
       JMAP,,TSF3
TSOVER ,CONQ,60                SIO CHIP OVERRUN
TSERR1 RAMD,OUTQ,TINTFG
       ,CONQ,TINTER
       RAMD,OUTQ,TINTPC
       CJS,,TRUE+LIDLE
       JMAP,,TSIDLE
TSERR  ,CONQ,20
       JMAP,,TSERR1
LIDLE  ,CONQ,TBIDLE
       RAMD,OUTQ,TBPC
       ,CONQ,TSIDLE
       RAMD,OUTQ,TSPC
       ,CONQ,RBIDLE
       RAMD,OUTQ,RBPC
       RAMD,ALU+DZ+AND+QREG,TSTOP
       RAMD,ALU+ZQ+EXNOR,XOFF
       ,CONQ,RSIDLE
       CRTN+RAMD,OUTQ,TRUE+RSPC
:
:      TRANSMIT RIGHT CHARACTER
TSR    CJP,,BUS5+TSR1
       JMAP,,TSIDLE
TSR1   SIOD,RAM,TDBUF
       A1,ALU+ZA+OR,43A        SEND RIGHT CHARACTER
       CJP,,BUS1+TSOVER
       ,RAM+LOADQ,TDCNT
       RAMD,ALU+ZQ+SUBR,TDCNT
       CJP,,F3+TSL2
       ,CONQ,TDREAD
       RAMD,OUTQ,TBPC
       ,CONQ,TSL
       JMAP,,TSF3
:
:      DATA SENT, WAIT FOR CHECKSUM
TSEND  CJP,,BUS1+TSEND1        WAIT FOR UNDERRUN
       JMAP,,TSIDLE
TSEND1 DMAD,RAM,TCADDH
       DMAH,RAM+DZ+OR+RAMF,TCADDL+B2
       A2,CONST+DA+ADD+RAMF,4
       A4,CONST+DA+AND,0FF
       CJP+A4+DMAD,ALU+ZA+ADD,CARRY+FE0+TSERR
       DMAL
TSEND2 A4+RAMD,ALU+ZA+OR,TCADNL
       ,CONQ,TSIDLE
       RAMD,OUTQ,TSPC
       REGD,RAM,RSPC
       ,CONQ,TBCOMC
TSEND3 CJP,DMA+DZ+OR,DMABY+TSEND3
       CJP,,FE0+TSEND4
       RAMD,DMA,TCOM
       RAMD,OUTQ,TBPC
       JRP+A1,ALU+ZA+OR+QREG
TSEND4 ,CONQ,TBCOMS
       RAMD,OUTQ,TBPC
       JRP+A1,ALU+ZA+OR+QREG
:
:      SIO RECEIVE SERVICE FOR HDLC
:      CLEAN OUT BUFFER, RESET OVERRUN FLAG
RSCLN  CJP,,BUS7+RSCLN2
       SIOD,CONST,30           ERROR RESET CODE
       ,,435                   SEND OUT ONE COMMAND
       ,CONQ,RSF
       RAMD,OUTQ,RSPC
RSCLN1 CJP,,SIOBY+RSCLN1
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSCLN2 SIOD                    FLUSH A CHARACTER
       ,,420
       JMAP,,RSCLN1
:      FIRST CHARACTER
RSF    CJP+SIOD,ALU+DZ+AND,BUS7+RSF1
       ,,42C                   GET SPECIAL STATUS
       ,CONQ,RSILN
       ,RAM+DZ+OR+RAMF,RICNT+B4
       A4,CONST+DA+ADD+RAMF,100
       ,CONST+DZ+OR+RAMF,2
       A2,SIO+DA+AND
       CJP,,FE0+RSIDLE
       RAMD,OUTQ,RSPC          IDLE LINE DETECTED
       A0+RAMD,ALU+ZA+OR,RICNT
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSF1   ,,418                   GET LEFT CHARACTER, ZERO RIGHT
       ,RAM+LOADQ,RRSIZE
       RAMD,ALU+ZQ+SUBR,RECNT  INITIALIZE RECORD COUNT IN HALFWORDS
       ,RAM+DZ+OR+RAMF,RCDADH+B2
       ,RAM+LOADQ,RCDADL
       A1,CONST+DQ+ADD+QREG,2
       RAMD,OUTQ,RCDTL
       CJP,,FE0+RSF2
       A2,ZA+SUBR+RAMF,B2
RSF2   A2+RAMD,ALU+ZA+ADD,CARRY+RCDTH
       ,CONQ,RSR
       RAMD,OUTQ,RSPC
       RAMD,SIO,RDBUF
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
:      IDLE LINE
RSILN  CJP+SIOD,ALU+DZ+AND,BUS7+RSF1
       ,,0     SHUT OFF SIO
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
:      RIGHT CHARACTER
RSR    CJP+A1,ALU+ZA+OR,BUS7+RSR1
       CJP,,BUS10+TSOVER
       A1,CONST+DA+AND,1000
       CJP,,FE0+RSRX
       JMAP,,RSR3
RSR1   SIOD,CONST,0
       A1,ZA+OR,420            READ CHARACTER RIGHT, ZERO LEFT
       CJP+A1,ALU+ZA+OR,F3+RSR2
       CJP+A1,ALU+ZA+OR,BUS8+RSR4
       ,CONQ,RSL
       RAMD,OUTQ,RSPC
       ,CONQ,RDRITE
       RAMD,OUTQ,RBPC
       ,RAM+LOADQ,RDBUF
RSR1W  CJP,,SIOBY+RSR1W
       ,SIO+DQ+OR+QREG
       RAMD,OUTQ,RDBUF
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSR2   ,RAM+LOADQ,ABORT
       ,CONST+DZ+OR+RAMF,10
       A0+RAMD,ALU+AQ+ADD,ABORT
RSR3   ,CONQ,RSF
       RAMD,OUTQ,RSPC
RSR3A  CJP,,SIOBY+RSR3A
RSRX   JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSR4   CJP,,BUS9+RSL3
       ,CONQ,RDEND
       RAMD,OUTQ,RBPC
       ,CONQ,RSIDLE
       RAMD,OUTQ,RSPC
       ,RAM+DZ+OR+RAMF,RECNT+B2
       ,RAM+LOADQ,RRSIZE
       A2,AQ+SUBR+RAMQU,B2
       RAMD+A2,ALU+ZA+OR,RECNT
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
:      LEFT CHARACTER
RSL    CJP,,BUS7+RSL1
       A1,CONST+DA+AND,1000
       CJP,,FE0+RSRX
       JMAP,,RSR3
RSL1   SIOD,CONST,0
       A1,ZA+OR,418            READ CHARACTER LEFT, ZERO RIGHT
       CJP+A1,ALU+ZA+OR,F3+RSR2
       CJP+A1,ALU+ZA+OR,BUS8+RSL2
       ,RAM+LOADQ,RECNT
       RAMD,ALU+ZQ+SUBR,RECNT
       CJP,,F3+RSL4
       ,CONQ,RSR
       RAMD,OUTQ,RSPC
RSL1W  CJP,,SIOBY+RSL1W
       RAMD,SIO,RDBUF
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSL2   CJP,,BUS9+RSL3
       ,RAM+DZ+OR+RAMF,RECNT+B2
       ,RAM+LOADQ,RRSIZE
       A2,AQ+SUBR+RAMQU,CARRY+B2
       A2+RAMD,ALU+ZA+SUBR,RECNT
       ,CONQ,RDEND
       RAMD,OUTQ,RBPC
       ,CONQ,RSIDLE
       RAMD,OUTQ,RSPC
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSL3   ,RAM+LOADQ,RCHECK
       RAMD,ALU+ZQ+ADD,CARRY+RCHECK
       JMAP,,RSR3
RSL4   ,CONQ,RSBIG
       RAMD,OUTQ,RSPC
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSBIG  CJP+SIOD,,BUS7+RSBIG1
       ,,0
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSBIG1 ,,420                   DISCARD CHARACTER QUICKLY
       ,                       NOP
RSBIG2 CJP+A1,ZA+OR,SIOBY+RSBIG2
       CJP+A1,ALU+ZA+OR,F3+RSR2
       CJP,,BUS8+RSR3
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
:
:      TRANSMIT BACKGROUND PROCESSING
TBCOM  DMAD,RAM,TCADDH         READ FIRST HALF OF COMMAND
       DMAH,RAM+LOADQ,TCADDL
       DMAD,ALU+ZQ+ADD,CARRY
       DMAL,CONQ,TBCOM2
       RAMD,OUTQ,TBPC
TBCOM1 CJP,,DMABY+TBCOM1
       RAMD,DMA,TCOM
       JRP+REGD,RAM,RBPC
TBCOM2 DMAD,RAM,TCADDH
       DMAH,RAM+LOADQ,TCADDL   READ SECOND HALF OF COMMAND
       ,CONST+DQ+OR+QREG,3
       DMAD,OUTQ
       DMAL,RAM+LOADQ,TCOM
       ,CONST+DQ+SUBR+QREG,CARRY+6
       CJP,ZQ+ADD+QREG,CARRY+F3+TBCD5
TBCERR CJP,,DMABY+TBCERR
       JMAP,,TSERR
:      CODE 0  STOP
TBCD0  ,CONQ,TBIDLE
       RAMD,OUTQ,TBPC
TBCOM3 CJP,,DMABY+TBCOM3
       JRP+REGD,RAM,RBPC
:      CODE 2  JUMP
TBCD2  CJP,ZQ+ADD,CARRY+F3+TBCD1
       ,CONQ,TBCOM6
       RAMD,OUTQ,TBPC
TBCOM5 CJP,,DMABY+TBCOM5
       RAMD,DMA,TCADDL
       JRP+REGD,RAM,RBPC
TBCOM6 ,RAM+DZ+OR+RAMF,TCADDL+B2
       CJS,,TRUE+QWQ
       CJP,,F3+TSERR
       A3+RAMD,ALU+ZA+OR,TCADDH
       A4+RAMD,ALU+ZA+OR,TCADDL
       ,CONQ,TBCOM
       RAMD,OUTQ,TBPC
       JRP+REGD,RAM,RBPC
:      CODE 1  OUTPUT BLOCK
TBCD1  CJP,,F3+TBCD0
TBCD1A ,CONQ,TBCOM9
       RAMD,OUTQ,TBPC
TBCOM8 CJP,,DMABY+TBCOM8
       RAMD,DMA,TDADDL
       JRP+REGD,RAM,RBPC
TBCOM9 ,RAM+DZ+OR+RAMF,TDADDL+B2
       CJS,,TRUE+QWQ
       CJP+A3+DMAD,ALU+ZA+OR,F3+TSERR
       DMAH
       A4+DMAD,ALU+ZA+ADD,CARRY
       DMAL
       A3+RAMD,ALU+ZA+OR,TDADDH
       A4+RAMD,ALU+ZA+OR,TDADDL
       A5+RAMD,ALU+ZA+OR,TDBUF
       ,CONQ,TBCM11
       RAMD,OUTQ,TBPC
TBCM10 CJP,,DMABY+TBCM10
       RAMD,DMA,TDCNT
       JRP+REGD,RAM,RBPC
TBCM11 DMAD,RAM,TDADDH
       DMAH,RAM+LOADQ,TDADDL
       ,CONST+DQ+OR+QREG,3
       DMAD,OUTQ
       DMAL
       RAMD,ALU+ZQ+ADD,CARRY+TDADDL
       ,RAM+DZ+OR+RAMF,TDCNT+B1
       A1,ZA+ADD+RAMD,CARRY+B1
       A1,ZA+OR+RAMD,B1
       A1,ZA+OR+RAMD,B1
       A1,ZA+OR+RAMD,B1
       ,RAM+LOADQ,TDBUF
       A1,AQ+SUBR,CARRY
       CJP,,F3+TBCERR
       ,CONQ,TBCM13
       RAMD,OUTQ,TBPC
TBCM12 CJP,,DMABY+TBCM12
       RAMD,DMA,TDBUF
       JRP+REGD,RAM,RBPC
TBCM13 ,CONQ,TBIDLE
       RAMD,OUTQ,TBPC
       ,CONQ,TSF
       ,RAM+DZ+OR+RAMF,TCOM+B3
       A3,ZA+SUBR
       CJP,,FE0+TBCM14
       ,CONQ,TSAL
TBCM14 RAMD,OUTQ,TSPC
       JRP+REGD,RAM,RBPC
TDREAD DMAD,RAM+LOADQ,TDADDH
       DMAH,RAM+DZ+OR+RAMF,TDADDL+B2
       A2+DMAD,ALU+ZA+ADD+RAMF,CARRY+B2
       DMAL
       A2+RAMD,ALU+ZA+ADD,CARRY+TDADDL
       CJP,,FE0+TDRED3
TDRED1 ,CONQ,TBIDLE
       RAMD,OUTQ,TBPC
TDRED2 CJP,,DMABY+TDRED2
       RAMD,DMA,TDBUF
       JRP+REGD,RAM,RBPC
TDRED3 RAMD,ALU+ZQ+ADD,CARRY+TDADDH
       JMAP,,TDRED1
TBCOMC DMAD,RAM,TCADDH
       DMAH,RAM+LOADQ,TCADNL
       DMAD,RAM,TCADDL
       DMAL
       DMAD,CONST,100
       RAMD,OUTQ,TCADDL
       ,CONQ,TBCOM2
TBCMC1 RAMD,OUTQ,TBPC
TBCMC2 CJP,,DMABY+TBCMC2
       JRP+REGD,RAM,RBPC
TBCOMS DMAD,RAM,TCADDH
       DMAH,CONQ,TBIDLE
       DMAD,RAM,TCADDL
       DMAL,CONST+DZ+OR+RAMF,200
       JMAP+A0+DMAD,ALU+ZA+OR,TBCMC1
:      CODE 3  COMMAND PAIR
TBCD3  CJP,ZQ+ADD+QREG,CARRY+F3+TBCD2
       ,RAM+LOADQ,TCADDL
       A1,CONST+DQ+ADD+QREG,4
       RAMD,OUTQ,TCADDL
       ,CONST+DQ+AND,0FF
       CJP,,FE0+TBCERR
TBCCP1 CJP,,SIOBY+TBCCP1
TBCCP2 CJP,,DMABY+TBCCP2
       SIOD,DMA
       ,,430                   OUTPUT LEFT AND RIGHT COMMAND
       ,CONQ,TBCOM
       RAMD,OUTQ,TBPC
TBCCP3 CJP,,SIOBY+TBCCP3
       JMAP+COUNT,COUNT+DZ+OR,MLOOP  NOT ENOUGH TIME FOR ANYTHING ELSE
:      CODE 4  ASYNC OUTPUT
TBCD4  CJP,ZQ+ADD+QREG,CARRY+F3+TBCD3
       JMAP,,TBCD1A
:
:      RECEIVE BACKGROUND PROCESSING
RBCOM  DMAD,RAM,RCADDH         READ FIRST HALF OF COMMAND
       DMAH,RAM+LOADQ,RCADDL
       DMAD,ALU+ZQ+ADD,CARRY
       DMAL,CONQ,RBCOM2
       RAMD,OUTQ,RBPC
RBCOM1 CJP,,DMABY+RBCOM1
       RAMD,DMA,RCOM
       JMAP,,RBIDLE
RBCOM2 DMAD,RAM,RCADDH         READ SECOND HALF OF COMMAND
       DMAH,RAM+LOADQ,RCADDL
       ,CONST+DQ+OR+QREG,3
       DMAD,OUTQ
       DMAL,RAM+LOADQ,RCOM
       ,CONST+DQ+SUBR+QREG,CARRY+5
       CJP,ZQ+ADD+QREG,CARRY+F3+RBCD4
       JMAP,,TBCERR
:      CODE 0  STOP
RBCD0  ,CONQ,RBIDLE
       RAMD,OUTQ,RBPC
RBCOM3 CJP,,DMABY+RBCOM3
       JMAP,,RBIDLE
:      CODE 2  JUMP
RBCD2  CJP,ZQ+ADD,CARRY+F3+RBCD1
       ,CONQ,RBCOM6
       RAMD,OUTQ,RBPC
RBCOM5 CJP,,DMABY+RBCOM5
       RAMD,DMA,RCADDL
       JMAP,,RBIDLE
RBCOM6 ,RAM+DZ+OR+RAMF,RCADDL+B2
       CJS,,TRUE+QWQ
       CJP,,F3+TSERR
       A3+RAMD,ALU+ZA+OR,RCADDH
       A4+RAMD,ALU+ZA+OR,RCADDL
       ,CONQ,RBCOM
       RAMD,OUTQ,RBPC
       JMAP,,RBIDLE
#200
:      CODE 1  HDLC INPUT
RBCD1  CJP,,F3+RBCD0
       ,CONQ,RBCOM9
RBCD1A RAMD,OUTQ,RBPC
RBCOM8 CJP,,DMABY+RBCOM8
       RAMD,DMA,RDADDL
       JMAP,,RBIDLE
RBCOM9 ,RAM+DZ+OR+RAMF,RDADDL+B2
       CJS,,TRUE+QWQ
       CJP,,F3+TSERR
       A3+RAMD,ALU+ZA+OR,RDADDH
       A4+RAMD,ALU+ZA+OR,RDADDL
       A3+RAMD,ALU+ZA+OR,RCDADH
       A4+RAMD,ALU+ZA+OR,RCDADL
       ,RAM+DZ+OR+RAMF,RBSIZE+B2
       ,DZ+AND+QREG
       A2,ZA+SUBR+RAMD,B2
       A2,ZA+OR+RAMD,B2
       A2,ZA+OR+RAMD,B2
       A2,ZA+OR+RAMD,B2
       A2,ZA+OR+QREG
       A5,AQ+SUBS,CARRY
       CJP,,F3+TSERR
       ,CONQ,RBCM11
       RAMD,OUTQ,RBPC
       JMAP,,RBIDLE
RBCM11 ,CONQ,RSCLN
RBCM1A RAMD,OUTQ,RSPC
       ,CONQ,RBIDLE
       RAMD,OUTQ,RBPC
       JMAP,,RBIDLE
RDRITE DMAD,RAM+DZ+OR+RAMF,RCDTH+B2
       DMAH
       DMAD,RAM+LOADQ,RCDTL
       DMAL,ZQ+ADD+QREG,CARRY
       DMAD,RAM+ZQ+ADD+QREG,CARRY+RDBUF
       CJP,,FE0+RDRIT3
RDRIT1 RAMD,OUTQ,RCDTL
RDRTE  ,CONQ,RBIDLE
       RAMD,OUTQ,RBPC
RDRIT2 CJP,,DMABY+RDRIT2
       CJP,,SIOBY+RBIDLE
       JRP+RAMD,SIO+DZ+OR+RAMF,STAT+B1
RDRIT3 A2+RAMD,ALU+ZA+ADD,CARRY+RCDTH
       JMAP,,RDRIT1
RDEND  DMAD,RAM,RCDTH          PUT MINUS 1 AT END OF RECORD
       DMAH
       DMAD,RAM,RCDTL
       DMAL,DZ+AND+QREG
       DMAD,ALU+ZQ+EXNOR
       ,CONQ,RDEND2
       RAMD,OUTQ,RBPC
RDEND1 CJP,,DMABY+RDEND1
       JMAP,,RBIDLE
RDEND2 DMAD,RAM,RCDADH         PUT CHARACTER COUNT AT BEGINING OF RECORD
       DMAH,RAM+LOADQ,RDADDL
       DMAD,RAM,RCDADL
       DMAL,RAM+DZ+OR+RAMF,RCDTL+B2
       DMAD,RAM,RECNT
       A2,AQ+SUBS+QREG,CARRY
       ,RAM+DZ+OR+RAMF,RRSIZE+B3
       A3,ZA+ADD+RAMU,CARRY+B3
       A3,AQ+ADD+RAMF,B4
       ,RAM+LOADQ,RBSIZE
       A4,AQ+SUBR
       CJP,,F3+RDEND3
       ,RAM+LOADQ,RCDTH
       JMAP,,RDEND4
RDEND3 ,RAM+LOADQ,RDADDH
       ,RAM+DZ+OR+RAMF,RDADDL+B2
RDEND4 RAMD,OUTQ,RCDADH
       A2+RAMD,ALU+ZA+OR,RCDADL
       ,CONQ,RBIDLE
       RAMD,OUTQ,RBPC
       ,CONQ,RSF
       RAMD,OUTQ,RSPC
RDEND5 CJP,,DMABY+RDEND5
       JMAP,,RBIDLE
:      CODE 3  BUFFER SIZE
RBCD3  CJP,ZQ+ADD+QREG,CARRY+F3+RBCD2
RBCBS  CJP,DMA+LOADQ,DMABY+RBCBS
       ,CONST+DQ+NOTRS,1FFF
       CJP,,FE0+RBCBS1
       JMAP,,TSERR
RBCBS1 ,CONST+DQ+NOTRS,1F
  CJP,,FE0+TSERR
       ,ZQ+OR+RAMU,B1
       A1+RAMD,ALU+ZA+OR,RBSIZE
       ,DZ+AND+RAMQD,B0
       ,DZ+AND+RAMQD,B0
       ,DZ+AND+RAMQD,B0
       RAMD,OUTQ,RRSIZE
RBCONT ,CONQ,RBCOM
       RAMD,OUTQ,RBPC
       ,RAM+LOADQ,RCADDL
       A1,CONST+DQ+ADD+QREG,4
       RAMD,OUTQ,RCADDL
       ,CONST+DQ+AND,0FF
       CJP,,FE0+TSERR
       JMAP,,RBIDLE
:
:      ASYNC SUPPORT FOLLOWS
:      USES REG A OF 2901 FOR TIMEOUT CONSTANT   SEE INITIALIZATION
:      OUTPUT
TSAL   CJP,,BUS5+TSAL1
       ,RAM+LOADQ,TSTOP
       CJP,,FE0+TSIDLE
       ,CONQ,TSP2
TSP1   RAMD,OUTQ,TSPC
       JMAP,,TSIDLE
TSP2   ,CONQ,TSAL
       ,RAM+DZ+OR,TSTOP
       CJP,,FE0+TSP1
       JMAP,,TSIDLE
TSAL1  SIOD,RAM,TDBUF
       ,,410
       ,RAM+LOADQ,TDCNT
       RAMD,ALU+ZQ+SUBR,TDCNT
       CJP,,FE0+TSAEND
       ,CONQ,TSAR
       JMAP,,TSF3
TSAEND ,CONQ,TSEND1
       JMAP,,TSF3
TSAR   CJP,,BUS5+TSAR1
       JMAP,,TSIDLE
TSAR1  SIOD,RAM,TDBUF
       ,,43A
       ,RAM+LOADQ,TDCNT
       RAMD,ALU+ZQ+SUBR,TDCNT
       CJP,,FE0+TSAEND
       ,CONQ,TDREAD
       RAMD,OUTQ,TBPC
       ,CONQ,TSAL
       JMAP,,TSF3
:      ASYNC INPUT
RSAF   CJP+A1,ZA+OR,BUS7+RSAF1 COME HERE FOR FIRST CHARACTER OF COMMAND
       CJP,,F3+BREAK
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSAC   CJP+A1,ZA+OR,BUS7+RSAC1 COME HERE FOR REMAINING 7 CHARACTERS
       CJP,,F3+BREAK
       ,RAM+LOADQ,RRSIZE       20 MILLISECOND TIMEOUT RUNNING
       AA+RAMD,ALU+AQ+SUBR,CARRY+RRSIZE
       CJP,,F3+RSADON
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSADON ,CONQ,RSIDLE
       RAMD,OUTQ,RSPC
       ,CONQ,RBCONT
       RAMD,OUTQ,RBPC
       ,CONQ,10
       RAMD,OUTQ,RINTFG
       ,CONQ,RINTER
       RAMD,OUTQ,RINTPC
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RSAF1  ,CONQ,RSAC
       RAMD,OUTQ,RSPC
RSAC1  SIOD,CONST,0
       ,,420
RSAC1B ,CONQ,RDARIT
       RAMD,OUTQ,RBPC
       ,CONQ,800
       RAMD,OUTQ,RRSIZE
       ,RAM+DZ+OR+RAMF,XOFF+B0
       ,CONQ,7F
RSAC2  CJP,,SIOBY+RSAC2
       RAMD,SIO+DQ+AND+QREG,RDBUF
       ,RAM+DQ+EXOR,XON
       CJP+A0,AQ+EXOR,FE0+RXON
       CJP,,FE0+RXOFF
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RXON   RAMD,ALU+DZ+AND,TSTOP
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
RXOFF  RAMD,ALU+ZQ+ADD,CARRY+TSTOP
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
BREAK  ,CONQ,BREAK1            WAIT FOR END OF BREAK
       RAMD,OUTQ,RSPC          THEN INPUT WITH BREAK FLAG
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
BREAK1 CJP,,BUS7+BREAK2
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
BREAK2 SIOD,CONST,101          GET NULL CHARACTER PLUS BREAK FLAG
       ,,420                   SIO INTERFACE WILL ECHO BREAK FLAG
       ,CONQ,RSAC
       RAMD,OUTQ,RSPC
       JMAP,,RSAC1B            CONTINUE AS FOR NORMAL CHARACTER
:      ASYNC INPUT BACKGROUND
RDARIT DMAD,RAM,RCDTH
       DMAH
       DMAD,RAM+LOADQ,RCDTL
       DMAL,ZQ+ADD+QREG,CARRY
       DMAD,RAM+ZQ+ADD+QREG,CARRY+RDBUF
       RAMD,OUTQ,RCDTL
       ,CONST+DQ+AND,0F
       CJP,,FE0+RDART1
       JMAP,,RDRTE
RDART1 ,CONQ,RSADON
       RAMD,OUTQ,RSPC
       JMAP,,RDRTE
:      ASYNC INPUT COMMAND
RBCD4  CJP,ZQ+ADD+QREG,CARRY+F3+RBCD3
       ,CONQ,RBCA1
       JMAP,,RBCD1A
RBCA1  ,RAM+DZ+OR+RAMF,RDADDL+B2
       CJS,,TRUE+QWQ
       CJP,,F3+TSERR
       A3+RAMD,ALU+ZA+OR,RCDTH
       A4+RAMD,ALU+ZA+OR,RCDTL
       ,CONQ,RSAF
       JMAP,,RBCM1A
:      CODE 5  XON, XOFF SPECIFY
TBCD5  CJP,ZQ+ADD+QREG,CARRY+F3+TBCD4
       ,RAM+LOADQ,TCADDL
       A1,CONST+DQ+ADD+QREG,4
       RAMD,OUTQ,TCADDL
       ,CONST+DQ+AND,0FF
       CJP,,FE0+TBCERR
       RAMD,ALU+DZ+AND,TSTOP
       ,CONQ,TBCOM
       RAMD,OUTQ,TBPC
       ,CONQ,0FF
TBCBP  CJP,,DMABY+TBCBP
       ,DMA+DQ+AND+QREG
       RAMD,OUTQ,XOFF
       ,DMA+LOADQ
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       ,ZA+AND+RAMQD,B0
       RAMD,OUTQ,XON
       JMAP+COUNT,COUNT+DZ+OR,MLOOP
    t@K 