	TITLE	SLAVE - SLAVE FOR PDP - 10 TELECOPY

OPDEF	RESET	[CALLI	0]	; Force a definition.
ENTRY	GD,RESET
ENTRY	CKTIM,ABT,COPYST,ZAPINT,ESCRTN,PANIC,RBLK,DBLK,CLRCIR
	ENTRY	ADIAG,BDIAG
;EXTERNAL ROUTINES REFERENCED
EXTERN	RF,TF,GC,GCOD,BIOMOD,BIOEND,SBYTE,SCOD,SNDYEL,CPOPJ1
EXTERN	CRASH,TRAP,BAD,OPF,FPF,UIO
;EXTERNAL STORAGE USED
EXTERN	CHCNT,NONAME,SYSTYP,SW10,SW940,SW370
EXTERN	AUXPRT,BMOD,MASTER,DONOR,TIMFLG
EXTERN	BPARAM,RPARAM,TPARAM,RMODE,TMODE,COPT
EXTERN	FIX,VRECL
EXTERN	RBCNT,RBNAM,RBEXT,RBPPN,RBLIC,FDLIC,OFDLIC
LOC	137
1,,1
RELOC
PDL:	BLOCK	40
UNAM:	BLOCK	^D16
FNAM==UNAM+2
UNAMEE:	0
START:	RESET
	SETZ	R0,
	MOVEI	P,R1
	BLT	P,F
	MOVE	P,[IOWD 40,PDL]
	SETZM	BMOD		;NOT IN BLOCK I/O MODE
	SETOM	AUXPRT
	SETZM	MASTER
	IFG	0,<HRROI R1,SLVPRT
	AUXCAL	R1,R1
	HALT
	MOVEM	R1,AUXPRT>
;	HRL	R1,AUXPRT
;	HRRI	R1,SDVIOS
;	AUXCAL	R1,IOMOD+NOECHO
;	PUSHJ	P,BIOMOD		;ENTER BLOCK I/O MODE
; INITIALIZE INTERRUPTS
;	MOVEI	R1,INTTAB
;	HRLI	R1,6		;ENABLE AND CLEAR INTERRUPT SYSTEM
;	INTADR	R1,
;	JFCL
;	HRRZ	R1,AUXPRT
;	HRLI	R1,7001		;CIR ZAP (7) ON CHANNEL 1
;	TINASS	R1,
;	JFCL
;	HRLI	R1,4
;	HRRI	R1,2		;TIMER	(4) AN CHANNEL 2
;	INTASS	R1,
;	JFCL
;	HRRZ	R1,AUXPRT
;	HRRI	R1,5003		;CHARS	LOST (5) ON CHANNEL 3
;	TINASS	R1,
;	JFCL
;	MOVSI	R1,740000
;	INTENB	R1,		;ENABLE CHANNELS 1-3
;	JFCL
;	SETZ	R1,
;	SETTIM	R1,
;	JFCL
DBG1:
;	MOVE	R1,[XWD 1,^D300]	;5 MINUTES
;	SETTIM	R1,
;	JFCL
;	SETZM	TIMFLG		;DON'T IGNORE TIMEOUTS NOW.
; HANDSHAKE CODE
	MOVEI	C,1
	PUSHJ	P,SBYTE
	MOVEI	C,2
	PUSHJ	P,SBYTE
	MOVEI	C,3
	PUSHJ	P,SBYTE
	MOVEI	C,4
	PUSHJ	P,SBYTE
	MOVEI	C,3+ACRCAP+KATCAP+BINCAP+UPDCAP
	PUSHJ	P,SBYTE
	MOVEI	C,VERNUM
	PUSHJ	P,SBYTE
START1:	PUSHJ	P,GC
	CAIE	C,1
	JRST	START1
	PUSHJ	P,GC
	CAIE	C,2
	JRST	START1
	PUSHJ	P,GC
	CAIE	C,3
	JRST	START1
	PUSHJ	P,GC
	CAIE	C,4
	JRST	START1
	PUSHJ	P,GSYSTYP
	JRST	GD

GSYSTYP:	PUSHJ	P,GC
	ANDI	C,177
	MOVEM	C,SYSTYP
	TRNN	C,170
	JSR	TRAP
	SETZM	SW10
	SETZM	SW940
	SETZM	SW370
	PUSHJ	P,GC
	CAIE	VERNUM
RESET:	JSR	TRAP
; SET DEFAULT PARAMETERS FOR THIS TYPE OF CONTROLLER
	MOVE	R1,SYSTYP
	ANDI	R1,7
	JRST	.+1(R1)
	JSR	TRAP		;0
	JRST	SET940		;1
	JSR	TRAP		;2
	JRST	SET10		;3
	JRST	SET370		;4
	JSR	TRAP		;5
	JSR	TRAP		;6
	JSR	TRAP		;7
SET370:	SETOM	SW370
	MOVEI	R2,AUTO
	JRST	SET9A
SET10:	SETOM	SW10
	MOVEI	R1,XMODE
	MOVEM	R1,TPARAM
	MOVEM	R1,RPARAM
	MOVEI	R1,3		;BINARY MODE
	MOVEI	R2,AUTO+ASCII
	JRST	SET9B
SET940:	SETOM	SW940
	MOVEI	R2,AUTO+ASCII
SET9A:	MOVEI	R1,1
	MOVEM	R1,TMODE
	MOVEI	R1,AMODE
	MOVEM	R1,TPARAM
	MOVEM	R1,RPARAM
	SKIPA	R1,TMODE
SET9B:	MOVEM	R1,TMODE
	MOVEM	R1,RMODE
	MOVEM	R2,COPT
	POPJ	P,

; GENERAL DISPATCHER
GD:	CLOSE	FIL,40		;KEEP FILE OUT OF UFD
	RELEASE	FIL,
	SETOM	TIMFLG		;IGNORE TIMEOUTS
	MOVE	P,[IOWD 40,PDL]
	PUSHJ	P,GCOD
	DI	<<DNRCOD,SEND>,<RCVCOD,REC>,<LOGCOD,LOG>,<QUICOD,QUIT>,<WHYCOD,WHY>>

SETVAR:	REPEAT	4,<PUSHJ P,GC>
	HRLOI	C,377777
	MOVEM	C,VRECL
	SETZM	FIX
SETV1:	LNKMSG	SUCCOD
	JSR	RETURN

SETFIX:	REPEAT	4,<PUSHJ P,GC>
SETF1:	LNKMSG	NICOD
	JSR	RETURN

TRIM:
TRUNCATE:	PUSHJ	P,GC
	CAIG	C,0
	JRST	SETV1
	JRST	SETF1

ONLC:	PUSHJ	P,GC
	TRZ	F,LCO		;ASSUME LOWER CASE OUTPUT PROHIBITTED
	TRNE	C,200
	TRO	F,LCO		;PERMIT
	JRST	SETV1

CPAR:	PUSHJ	P,GC
	CAIG	C,ASCII+AUTO
	JRST	.+2
	JRST	SETF1
	MOVEM	C,COPT
	JRST	SETV1

SFATT:	MOVEI	C,SUCCOD
	PUSHJ	P,SCOD
	MOVEI	C,6
	PUSHJ	P,SBYTE
	MOVEI	R1,6
	MOVE	R2,[POINT	6,RBLIC]
	ILDB	C,R2
	PUSHJ	P,SBYTE
	SOJG	R1,.-2
	PUSHJ	P,SNDYEL
	JSR	RETURN

RFATT:	SKIPN	SW10
	JSR	BAD
	PUSHJ	P,GC
	ANDI	C,177
	CAIE	C,6
	JSR	TRAP
	MOVEI	R1,6
	MOVE	R2,[POINT 6,FDLIC]
	PUSHJ	P,GC
	IDPB	C,R2
	SOJG	R1,.-2
	JSR	RETURN

RETURN:	0
	SKIPN	DONOR
	JRST	RMOD
	JRST	SMOD

QUIT:	HRLOI	R1,377777
;	INTENB	R1,
	JFCL
	PUSHJ	P,BIOEND
	RESET
	EXIT	1,

;	TIMER	INTERRUPT
TIMADR:	SKIPN	TIMFLG
	JRST	TIMA1
	SKIPL	TIMFLG
	SETZM	TIMFLG
;	PUSH	P,R1
;	MOVE	R1,[XWD	1,^D300]
;	SETTIM	R1,
;	JFCL
;	POP	P,R1
;	DISMIS
TIMA1:	IFG	TESTNG,<JSR CRASH>
;	MOVEI	R1,ENDIT
;	MOVEM	R1,TIMRTN-1
;	DISMIS

; BUFFER ZAP
BZADR:	IFG TESTNG,<JSR CRASH>
	MOVE	R1,ENDIT
	MOVEM	R1,BZRTN-1
;	DISMIS

;	CIRCUIT ZAP
ZAPADR:	MOVE	R1,ENDIT
	MOVEM	R1,ZAPRTN-1
;	DISMIS

INTTAB:	0
ZAPRTN:	ZAPADR			;CIRCUIT ZAP ON CHANNEL 1
	0
TIMRTN:	TIMADR			;TIMER ON CHANNEL 2
	0
BZRTN:	BZADR			;BUFFER ZAP ON CHANNEL 3
	BLOCK	^D70-6

ZAPINT:	0		;JSR HERE TO FAKE CIRCUIT ZAP INTERRUPT
	IFG TESTNG,<JSR CRASH>
LOG:
ENDIT:	HRLOI	R1,377777
;	INTENB	R1,		;DISABLE ALL CHANNELS
	JFCL
	PUSHJ	P,BIOEND	;LEAVE BLOCK I/O MODE
	RESET
	MOVSI	R1,1
	HRRI	R1,LOGNAM
	RUN	R1,
	HALT
LOGNAM:	SIXBIT	/SYS/
	SIXBIT	/LOGOUT/
	EXP	0,0,0,0

;	PRINT LATEST ERROR INFORMATION
WHY:	MOVEI	C,RSPCOD
	PUSHJ	P,SCOD
;	HRL	R1,AUXPRT
;;	HRRI	R1,ASCSTG	;OUTPUT ASCII STRING
;	AUXCAL	R1,WHYNOT
	MOVEI	R1,101		;DO PRIMARY OUTPUT
	HRROI	R2,WHYNOT	;POINT TO STRING
	SETZ	R3,
	SOUT
;	HRRI	R1,OICHI	;OUTPUT	IMAGE CHAR IMMEDIATE
;	AUXCAL	R1,0
	MOVE	R2,R0		;GET THE CHARACTER
	BOUT			;OUTPUT THE CHAR
	JRST	GD
WHYNOT:	ASCIZ	"WHY NOT?"

SEND:	SETOM	DONOR
	SETZM	TIMFLG		;STOP IGNORING TIMEOUTS
SMOD:	PUSHJ	P,GCOD
	DI	<<FLKCOD,GFNAM>,<FENQCD,SFATT>,<OPNCOD,TFBEG>,<CPACOD,CPAR>,
	<ONLCOD,ONLC>,<TRUCOD,TRUNCATE>,<TRICOD,TRIM>,<VARCOD,SETVAR>,<FIXCOD,SETFIX>,<OPFCOD,OPF>,<FPFCOD,FPF>>

REC:	SETZM	DONOR
	SETZM	TIMFLG		;STOP IGNORING TIMEOUTS
RMOD:	PUSHJ	P,GCOD
	DI	<<FLKCOD,GFNAM>,<FATTCD,RFATT>,<OPNCOD,RFBEG>,<CPACOD,CPAR>,
	<ONLCOD,ONLC>,<TRUCOD,TRUNCATE>,<TRICOD,TRIM>,<VARCOD,SETVAR>,<FIXCOD,SETFIX>,<OPFCOD,OPF>,<FPFCOD,FPF>>

; BEGIN TRANSMISSION
TFBEG:	MOVEI	R2,TPARAM
	PUSHJ	P,MODES
	PUSHJ	P,TF
	JRST	GD

; BEGIN RECEIVE
RFBEG:	MOVEI	R2,RPARAM
	PUSHJ	P,MODES
	PUSHJ	P,RF
	JRST	GD

; READ AND DECIPHER OPNCOD RECORD.	R2 POINTS TO APPROPRIATE TABLE
MODES:	PUSHJ	P,GC
	MOVEM	C,TMODE
	MOVEM	C,RMODE
; MODE 2 (EBCDIC) NOT SUPPORTED
	CAIN	C,2
	JSR	UIO
	CAIG	C,6
	CAIG	C,0
	JSR	UIO
	CAIG	C,4
	CAIG	C,2
	POPJ	P,
; READ BINARY PARAMETERS
	PUSHJ	P,GC
	MOVEM	C,(R2)
	MOVEM	C,BPARAM
	MOVE	C,1(R2)		;TMODE
	CAIG	C,3
	POPJ	P,
; READ BLOCKING
	PUSHJ	P,GC
	LSH	C,10
	MOVEM	C,2(R2)		;BLOCKING
	PUSHJ	P,GC
	ADDM	C,2(R2)
	POPJ	P,

; DUMMY ROUTINES NEEDED BY TRANSMISSION LOGIC
COPYST:
ADIAG:
BDIAG:
CKTIM:	POPJ	P,
; DUMMY CELLS REFERENCED FROM TRANSMISSION LOGIC
RBLK:
DBLK:
CLRCIR:
ESCRTN:
PANIC:	0
; SEND ABTCOD TO CONTROLLER
ABT:	LNKMSG	ABTCOD
	POPJ	P,

;	READ (USERNAME)FILENAME

GFNAM:	SETZM	UNAM
	MOVE	R2,[UNAM,,UNAM+1]
	BLT	R2,UNAMEE
	MOVE	R2,[POINT	6,UNAM]
	SETZM	RBPPN
	SETOM	NONAME		;SET NO-NAME ENTERED SWITCH
	PUSHJ	P,GC		;GET COUNT FOR STRING
	MOVEM	C,CHCNT		;SAVE
;SKIP OVER LEADING BLANKS
GFNAM5:	SOSGE	CHCNT
	JRST	GFD2
	PUSHJ	P,GC
	CAIN	C,40
	JRST	GFNAM5
	CAIE	C,LPAREN
	JRST	[ MOVEM C,NONAME	;NO USER NAME DEFAULTS TO LOGGED IN NAME
		JRST GFD4]
GFNAM6:	SOSGE	CHCNT
	JRST	GFD2
	PUSHJ	P,GC
	CAIN	C,RPAREN
	JRST	GFD2
	SUBI	C,40		;CONVERT TO SIXBIT
	IDPB	C,R2
	JRST	GFNAM6
; LOOK UP USER NAME
GFD2:	DMOVE	UNAM
	PUSHJ	P,USRPPN
	JRST	[ MOVEI C,ULKFCD	;USER NOT IN LUD
		JRST GFD3]
	MOVEM	RBPPN		;SAVE PPN IN LOOKUP BLOCK
	MOVEI	C,ULKSCD
GFD3:	PUSHJ	P,SCOD
	PUSHJ	P,SNDYEL
GFD4:	MOVE	R2,[POINT 6,FNAM]
	SKIPL	C,NONAME
	JRST	G10FN3
G10FN2:	SOSGE	CHCNT
	JRST	G10FN4
	PUSHJ	P,GC
G10FN3:	SUBI	C,40
	IDPB	C,R2		;DEPOSIT BYTE IN FNAM
	JRST	G10FN2
G10FN4:	TRZ	F,ILLCHR	;SAY NO ERROR
	PUSHJ	P,GET10		;MOVE FNAM TO RBNAM
	TRNN	F,ILLCHR	;TEST FOR SYNTAX ERROR
	JRST	G10FN6		;NO
G10FN5:	LNKMSG	FLKRCD
	JSR	RETURN
G10FN6:	INIT	FIL,17
	SIXBIT	/DSK/
	0
	JRST	G10FN5
	MOVEI	14
	MOVEM	RBCNT
	LOOKUP	FIL,RBCNT
	JRST	[ MOVEI C,FLKFCD	;FILE NOT FOUND
		PUSHJ P,SCOD
		JRST G10FN7]
	MOVEI	C,FLKSCD
	PUSHJ	P,SCOD
	MOVE	RBLIC
	MOVEI	R1,FDLIC
	SKIPN	DONOR
	MOVEI	R1,OFDLIC
	MOVEM	(R1)
; SEND FD ATTRIBUTES TO PDP-10 CONTROLLER
	SKIPN	SW10
	JRST	G10FN7
	MOVEI	R1,6
	MOVE	R2,[POINT 6,RBLIC]
	ILDB	C,R2
	IORI	C,200
	PUSHJ	P,SBYTE
	SOJG	R1,.-3
G10FN7:	PUSHJ	P,SNDYEL
	JSR	RETURN

;ROUTINE TO BREAK DOWN A STRING IN FNAM INTO PDP-10
;FORMAT OF FILENAME AND EXTENSION IN RBNAM
GET10:	MOVE	R3,[POINT 6,RBNAM]
	MOVE	R2,[POINT 6,FNAM]
	SETZM	RBNAM
	SETZM	RBEXT
	MOVEI	R1,6
GETN:	ILDB	R0,R2
	CAIN	R0,"."-40
	JRST	GETEXT
	JUMPE	R0,[POPJ P,0]
	PUSHJ	P,CHKCHR
	IDPB	R0,R3
	SOJG	R1,GETN
GET1:	CAIN	R0,0
	POPJ	P,0
	PUSHJ	P,CHKCHR
	ILDB	R0,R2
	CAIN	R0,"."-40
	JRST	GETEXT
	JRST	GET1
GETEXT:	SETZ	R0,
	SOJL	R1,.+3
	IDPB	R0,R3
	JRST	.-2
	MOVEI	R1,3
	ILDB	R0,R2
	JUMPE	R0,[POPJ P,0]
	PUSHJ	P,CHKCHR
	IDPB	R0,R3
	SOJG	R1,.-4
	POPJ	P,0


CHKCHR:	CAIGE	R0,"0"-40	;ERROR IF LESS THAN ZERO
	JRST	BADCHR
	CAIG	R0,"9"-40	;OK IF LESS THAN 9
	POPJ	P,0
	CAIL	R0,"A"-40
	CAILE	R0,"Z"-40
BADCHR:	TRO	F,ILLCHR
	POPJ	P,0

SUBTTL	USRPPN - LUD ROUTINES

HA==	5		;USED ONLY BY HASHER - REGISTER PRESERVED BY HASHER
HB==	HA+1		;USED ONLY BY HASHER - REGISTER PRESERVED BY HASHER
HC==	HA+2		;USED ONLY BY HASHER - REGISTER PRESERVED BY HASHER
HD==	HA+3		;USED ONLY BY HASHER - REGISTER PRESERVED BY HASHER
HE==	HA+4		;USED ONLY BY HASHER - REGISTER PRESERVED BY HASHER
;USRPPN CONVERTS A USER NAME TO A PPN
;  CALL WITH USER NAME IN R0 AND R1
;  SKIP RETURN WITH PPN IN R0 IF SUCCESSFUL
;  NON-SKIP RETURN IN UNSUCCESSFUL
;  R1, R2, AND R3 ARE NOT PRESERVED. R0 NOT PRESERVED ON FAILURE
USRPPN::TLNN	F,LUDFLG	;SKIP IF THE LUD HAS BEEN OPENED
	PUSHJ	P,GETLUD	;TRY TO OPEN THE LUD
	TLNN	F,LUDFLG	;SKIP IF THE LUD HAS BEEN OPENED
	POPJ	P,		;FAIL RETURN FROM USRPPN
	PUSHJ	P,HASHER	;HASH THE USER NAME
	PUSHJ	P,FINNAM	;FIND NAME IN LUD
	POPJ	P,		;FAIL RETURN
	MOVE	R0,LUDBLK(R2)	;GET PPN
	JRST	CPOPJ1		;RETURN PPN IN R0

;RETURN IF NOT FOUND
;RETURN+1 IF NOT FOUND BUT USER WAS THERE(DELETED IN LUD)
;RETURN+2 IF FOUND
TYP1:	SKIPN	R1,LUDBLK(R2)
	POPJ	P,		;FAIL RETURN FROM USRPPN
FINNAM:	CAMN	R1,LUDL
	JRST	TYP2
	USETI	LUD,@R1
	INPUT	LUD,LUDLST
	STATZ	LUD,760000
	POPJ	P,		;*** ERROR IN LUD
	MOVEM	R1,LUDL		;SAVE LUD LOCATOR
TYP2:	SETZ	R2,
TYP3:	JUMPLE	R1,TYP1
	CAMN	R0,LUDBLK+4(R2)
	JRST	CPOPJ1		;SUCCESS RETURN FROM USRPPN
	LDB	R1,[POINT 7,LUDBLK+2(R2),35]
	ADD	R2,R1
	JRST	TYP3
HASHER:	PUSH	P,HA
	PUSH	P,HB
	PUSH	P,HC
	PUSH	P,HD
	PUSH	P,HE
	MOVEI	HB,0
	MOVE	HC,[555555555555]
	MOVE	HD,[361275431652]
	MOVE	HE,[612754316523]
	PUSHJ	P,RND
	PUSHJ	P,RND
	PUSHJ	P,RND
	PUSHJ	P,RND
	XOR	HE,HD
	MOVE	R0,HE
	TLZ	R0,400000
	IDIVI	R0,^D887
	MOVE	R0,HE
	XOR	R0,HC
	ADDI	R1,1
	POP	P,HE
	POP	P,HD
	POP	P,HC
	POP	P,HB
	POP	P,HA
	POPJ	P,
RND:	ADD	HD,R0
	ROTC	R0,-22
	MOVEI	HA,5
RND1:	MOVE	R2,HC+1(HB)
	MUL	R2,[5*5*5*5*5*5*5*5*5*5*5*5*5*5*5]
	ADDM	R3,HD+1(HB)
	AOJE	HB,RND2
	MOVNI	HB,1
	TRNE	HD,1
	SKIPL	HE
	MOVEI	HB,0
	EXCH	HC,HE
RND2:	SOJG	HA,RND1
	POPJ	P,

GETLUD:	OPEN	LUD,LUDTAB	;INIT THE DEVICE SYS
	POPJ	P,		;*** CANNOT ACCESS SYS
	LOOKUP	LUD,LOKLUD	;LOOKUP THE LUD
	POPJ	P,		;FAIL RETURN FROM GETLUD
	SETOM	LUDL		;NOTHING IN BUFFER YET
	TLO	F,LUDFLG	;MARK LUD AS OPENED
	POPJ	P,		;SUCCESS RETURN FROM GETLUD
LUDTAB:	XWD	0,17		;DUMP MODE
	SIXBIT	/SYS/		;DEVICE SYS
	0			;NO BUFFERS

LOKLUD:	SIXBIT	/LUD/		;FILE NAME
	SIXBIT	/SYS/		;EXTENSION
	0
	0

	0
LUDLST:	IOWD	200,LUDBLK	;BUFFER
	0
LUDBLK:	BLOCK	200
LUDL::	-1			;BLOCK NO. IN LUD (SEE USRPPN)
	END	START
    