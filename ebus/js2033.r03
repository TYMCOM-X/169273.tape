	SUBTTL	Definitions
:------------------------------------------------------------------: 
:  Class TTE 442 - ISIS Slot Programming - 31-Oct-88 to 11-Nov-88  : 
:  Student: Joe Smith, MDFSCO, mail stop D21, phone 922-6220       :
:  Node 2033, Slot 03, Host 30, File JS2033.#03 in (TECHTRAIN:31)  :
:------------------------------------------------------------------: 
	RA	0		: In case DATIME.LIB left radix at $A

	SEG	A.DATA

MAXNAM	EQ	$A 1+12+1	: "!" + 12 chars name + CR or semi
MAXPHW	EQ	(MAXPRT/10)+1	: Number of bits (3 halfwords if MAXPRT=$A 32)
INUSE	HS	MAXPHW		: Bit array for in-use ports
OBWAIT	HS	MAXPHW		: Bit array for ports waiting for orange ball
ACTIVE	HS	MAXPHW		: Bit array for ports actively outputting
BPRESS	HS	MAXPHW		: Bit array for ports backpressured
USECNT	HC	00		: Count of ports in use

Q	EQ	.
	ORG	0		: Ports database
P.READ	WS	01		: 4 bytes address of input read routine
P.WRIT	WS	01		: 4 bytes address of output write routine
P.R10	WS	01		: 4 bytes to save R10 for P.WRIT to use
P.R11	WS	01		: 4 bytes to save R11 for P.WRIT to use
P.INVN	WS	01		: 4 bytes invoice number (accounting)
P.PORT	HS	01		: 2 bytes port number (from Dispatcher)
P.DHST	HS	01		: 2 bytes destination host (this slot)
P.ONOD	HS	01		: 2 bytes origination node (CONSAT)
P.OHST	HS	01		: 2 bytes origination host (CONSAT)
P.OPRT	HS	01		: 2 bytes origination port (obsolete)
P.LOG2	HS	01		: 2 bytes for LOG-II invoice extension
P.XTPC	BS	02		: 1 byte unused, 1 byte thru-put class
P.DNOD	HS	01		: 2 bytes destination node (this node)
P.CCT	BS	01		: 1 byte circuit characteristic type
P.NAMS	BS	01		: 1 count of bytes in name
P.NAME	BS	MAXNAM		:12+2 bytes user name
P.SLIM	BS	01		: 1 byte of send limit
P.SCNT	BS	01		: 1 byte of send count
P.XXXX	BS	0		: End of structure
	ORG	Q		: End of structure
P.WS	EQ	(P.XXXX+3)/4	: Number of words required
P.BASE	WS	(MAXPRT+1)*P.WS	: Array of structures (word aligned)
P.LAST	EQ	.-(P.WS*4)	: Beginning of MAXPRT's structure
P.SIZE	HC	P.WS*4		: This is for the multiply instruction
: Host up message

HSTUP	HC	$A 14		: Size of message (including 2 filler bytes)
	BC	10		: RPORT=0, message type 10 = Host UP
	BC	0		: Dispatcher's key
	HC	HST0		: Host number (from TYM file)
	HC	HPRT0		: Number of ports on this host (from TYM file)
	HC	0		: Relative host number (Host 0)
HSTDWN	BC	0		: 0=up, 1=down, 2=shut, 3=gone
	BC	0		: Host key, not used
	BC	0		: IIX file is off
	BC	PRODID		: Product ID, 99x=TEST
	HC	0		: 2 bytes filler

: All of these messages have filler (to make ORING easier to read)
DETMSG	XC	029E00		: ISIS message to DETACH circuit
ZAPMSG	XC	029F00		: ISIS message to ZAP circuit
GREMSG	XC	02AA00		: ISIS message for green ball
REDMSG	XC	02AB00		: ISIS message for red ball
YBLMSG	XC	02AC00		: ISIS message for yellow ball
ORAMSG	XC	02AD00		: ISIS message for orange ball
EC1MSG	XC	06B10001000000	: ISIS message to turn echo on

CRLF	SC	/"8D"8A/
SPACE	SC	/ /
BELSTR	SC	/"87 /		: BEEP, SPACE
BCOUNT	WC	0

: Global variables
	GL	SCOUNT
	SEG	A.CODE
	SUBTTL	Start up foreground and background
: Slot's execution starts at label START (called from CRASH.LIB)

START	LIS	R2,0		: RPORT=0 message, to node code
	LA	R3,HSTUP+1,,	: Point to byte count at start of message
	JAL	R5,OCM,,	: Output canned message saying we are up
	LIS	R0,0
	LHI	R1,MAXPHW-1	: Number of HW's to clear, starting at 0
START1	SH	R0,INUSE,R1	: Clear out the "in use" bits
	SH	R0,OBWAIT,R1	: Orange ball wait
	SH	R0,ACTIVE,R1	: No port using P.WRIT yet
	SH	R0,BPRESS,R1	: No backpressure
	SIS	R1,1
	JGE	START1
	SH	R0,USECNT	: Clear count
: Put host and slot number in connect message
	LHI	R2,HST0		: Our host number
	LI	R0,0220+0A	: 2 bytes, blank fill, decimal
	LA	R1,HOSTXX	: Address of XX in CONSTR
	JAL	R6,OUTNUM,,
	LH	R2,SLOTNO,,	: Our slot number 
	LI	R0,0140+10	: 1 byte, zero fill, hex
	LA	R1,SLOTXX	: Address of X in CONSTR
	JAL	R6,OUTNUM,,
	LH	R2,NODEX,,	: Our node number
	LI	R0,0440+08	: 4 bytes, zero fill, octal
	LA	R1,NODEXX	: Address of XXXXX in CONSTR
	JAL	R6,OUTNUM,,
	LHI	R2,MAXPRT	: Max port number
	LI	R0,0220+0A	: 2 bytes, blank fill, decimal
	LA	R1,MAXPXX	: Address of XX in CONSTR
	JAL	R6,OUTNUM,,

	GL	FSTART,FCHECK	: Defined in JS2033.F03
: Here is where the foreground task is started
	LA	R0,FSTART,,	: Foreground starting address
	SVC	SYS,CFGND.	: Connect foreground
	LIS	R0,0
	ST	R0,BCOUNT	: Zero the background counter
	ST	R0,ICOUNT	: Input bytes counter
	ST	R0,ECOUNT	: Echoed bytes counter
	J	BMAIN		: Main loop starts on next page
	SUBTTL	Main processing loop (for background)

BMAIN	SVC	DISMIS,0	: Give other slots a chance to run
	LIS	R0,1
	AM	R0,BCOUNT	: Count # of times thru here
	JAL	R13,FCHECK,,	: Check if foreground wants anything done
	JAL	R4,LOOK,,	: Look to see if there is anything in IRING
	  J	BWRITE		: Nothing there, check for output, then dismiss
	JE	B0FLSH		: If port zero message, flush it
: R1 has port, R2 has message type
	LR	R13,R1		: RPORT number
	MH	R13,P.SIZE	: Point to port data structure
	LA	R13,P.BASE,R13,	:  "		"
	CHI	R2,0		: Message type of 0 is a needle, the
	JE	NEEDL		:  only thing allowed on an unused port
	TBT	R1,INUSE	: Is this port in use?
	JN	BPROC		: Yes, process the command
				: No, flush this message
: Message type must still be in R2
BFLUSH	LB	R0,LENGTH,R2,	: Get size from table in SEG F
	JAL	R4,FLUSH,,	: Skip to end of message
	J	BMAIN		: Go check next message

: Here to flush a port 0 message

B0FLSH	LH	R6,LP0LST,,	: Get addr of port 0 length list (from SEG F)
				: "NRATE,R6" now points to start of table
	LB	R0,NRATE,R6,R2	: Pickup size based on message type in R2
	JAL	R4,FLUSH,,	: Skip to end of this input message
	J	BMAIN		: See if next message is a needle
: BWNEXT = Set up to run a routine when port is not backpressured.
:	Link on R12, address of routine in R1, initial values in R10 and R11.
: BWDONE = Cancel the routine set by BWNEXT.  Use "J BWDONE" to call it.
: BPCHEK = Check back pressure.  Link on R1, preserves R10 and R11 only.
: Note: BPCHEK can only be called from routines started by BWNEXT.

BWDONE	LIS	R1,0		: Zero when done outputting
BWNEXT	ST	R10,P.R10,R13	: Store initial values of preserved registers
	ST	R11,P.R11,R13	: These will be restore by BWRITE
	LH	R2,P.PORT,R13
	SBT	R2,ACTIVE	: Mark port as active
BPCHEK	ST	R1,P.WRIT,R13	: Save return address
	JR	R12		: Return to BWRITE, which checks BPRESS

: Here to check for deferred output

BWRITE	LHI	R14,MAXPRT	: OK to use R14 (done with IRING)
BWLOOP	TBT	R14,ACTIVE	: Check for active writer
	JE	BWEND		: Zero means not active
	TBT	R14,BPRESS	: If port is back pressured,
	JN	BWEND		:  ignore until BP is turned off
	LR	R13,R14		: Port number
	MH	R13,P.SIZE	: Point to data structure
	LA	R13,P.BASE,R13,	:  "	   "
	L	R1,P.WRIT,R13	: Get output routine
	JN	BWRIT1		: Continue if there is a routine
	RBT	R14,ACTIVE	: Else clear the active bit
	JFS	BWEND
BWRIT1	L	R10,P.R10,R13	: Get saved loop counter
	L	R11,P.R11,R13	: Saved link
	JALR	R12,R1		: Go to output routine (R13 is set up)
	ST	R10,P.R10,R13
	ST	R11,P.R11,R13
BWEND	SIS	R14,1
	JN	BWLOOP		: Check all ports
	J	BMAIN		: Go check IRING now
	SUBTTL	Process login request from needle

NEEDL	STH	R1,P.PORT,R13	: Store port # in easy to access place
	SBT	R1,INUSE	: Mark this port as in use
	SBT	R1,HSECHO	: Host must do echo
	RBT	R1,BPRESS	: Port is not backpressured
	JAL	R4,GETH,,	: Put msg type & length in R0
	JAL	R4,GETW,,	: Get invoice number
	ST	R0,P.INVN,R13
	JAL	R4,GETH,,	: Get dest host
	STH	R0,P.DHST,R13
	JAL	R4,GETH,,	: Get orig node
	STH	R0,P.ONOD,R13
	JAL	R4,GETH,,	: Get orig host
	STH	R0,P.OHST,R13
	JAL	R4,GETH,,	: Get orig port
	STH	R0,P.OPRT,R13
	JAL	R4,GETH,,	: Get invoice number, third half
	STH	R0,P.LOG2,R13
	JAL	R4,GETH,,	: Get unused byte and TPC
	STH	R0,P.XTPC,R13
	JAL	R4,GETH,,	: Get dest node number
	STH	R0,P.DNOD,R13
	JAL	R4,GETCH,,	: Get circuit characteristics type
	STB	R0,P.CCT,R13
	JAL	R4,GETCH,,	: Get size of username
	STB	R0,P.NAMS,R13
	LR	R6,R0		: Save count
	LIS	R7,0
NEEDL1	JAL	R4,GETCH,,	: Get next byte of username
	CHI	R7,MAXNAM
	JGE	NEEDL2		: Don't store if name is too long
	STB	R0,P.NAME,R13,R7
NEEDL2	AIS	R7,1
	SIS	R6,1
	JG	NEEDL1		: Loop for all of username
	JAL	R4,ELIR		: End of needle, go tell FRING

	LH	R2,P.PORT,R13
	LA	R3,EC1MSG	: Send message to turn on echo
	JAL	R5,OCM,,
	LIS	R2,1		: One more port in use
	AHM	R2,USECNT
	LA	R0,CREAD	: Connect port to command reader
	ST	R0,P.READ,R13
	JAL	R7,PORTIS	: Output "port xx"
	LA	R1,WELSTR	: Message to be output after systat
	JAL	R4,SYSTAT	: Set up to start a system status report
	J	BMAIN
WELSTR	SC	/"8D"8AWelcome to Joe's slot.  Type ? for help."8D"8A"8A/
	SUBTTL	Command scanner
: Here to process ISIS messages

BPROC	CHI	R2,009E		: Check for control message
	JGE	BCNTRL		: Go do OB, YB, GB, RB, EC type stuff
	JAL	R4,GETCH,,	: Officially get the count
	STH	R0,NCOUNT	: Store for MYGETC
	AM	R0,ICOUNT	: Increment number of characters input
	CHI	R0,1		: Check for multi-byte messages
	JE	NEXTC
	LIS	R0,1
	AM	R0,MCOUNT	: Increment count of multi's

:Loop back here if it is a multi-byte string

NEXTC	JAL	R6,MYGETC	: Get a character, echo if needed
	  J	BMAIN		: No more input (ELIR called already)
	L	R4,P.READ,R13	: Get address of input routine
	JR	R4		: Go there (usually goes to CREAD)

: Read a command (default input handler)

CREAD	JAL	R4,UPCASE	: Convert to uppercase, strip parity
	LHI	R4,CMDLEN	: Size of table to check
CMDCHK	SIS	R4,4
	JL	NOTCMD		: Continue if count hasn't run out
	LB	R2,CMDTAB,R4	: Get char
	NHI	R2,7F		: Strip parity bit that SC left on
	CR	R0,R2		: Match?
	JN	CMDCHK		: Loop if not

: Found a matching command.  Now go to it.

	L	R6,CMDTAB,R4	: Get dispatch address
	NI	R6,00FFFFFF	: Mask off char, leave only 24-bit address
	JR	R6		: Go to routine

: Here if character is not a command

NOTCMD	LA	R3,BELSTR	: No match, output a bell
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Go beep
	J	NEXTC		: Get new command

XX	MACRO(CH,ADDR)	[WC CH*01000000+ADDR]	: Combine CH and address

XCMDX	MACRO	[
	XX(03F,CMD.)		:"?" = list of commands
	XX(044,CMDD)		:"D" = dummy
	XX(048,CMDH)		:"H" = help
	XX(04D,CMDM)		:"M" = message
	XX(050,CMDP)		:"P" = print time
	XX(051,CMDQ)		:"Q" = quit
	XX(053,CMDS)		:"S" = status
	XX(057,CMDW)		:"W" = who
	]
HLPSTR	SC	/elp: commands are Dummy, Present-time, Message, Quit, Status, Who."8D"8A/

CMDTAB	WS	0		: Must start on a word boundary
	XCMDX			: Create command table
CMDLEN	EQ	.-CMDTAB	: Number of bytes in command table
	KILL	XCMDX,XX

: Routine to convert byte in R0 to uppercase.  Link on R4

UPCASE	NI	R0,7F		: Strip parity bit before doing compares
	CHI	R0,61		: Test for lowercase "a"
	JL	ISUPR
	CHI	R0,7A		: Test for lowercase "z"
	JG	ISUPR
	SHI	R0,20		: Convert lower to upper case
ISUPR	JR	R4
: The user typed "H" or "h"

CMDH	LA	R3,HLPSTR	: Output the "H" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: "Help: Commands are:..."
	J	NEXTC

: The user typed "?"

HSTR	SC	/ Legal command characters are:/
CMD.	LA	R3,HSTR		: Output the "?" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: "Legal commands are: "
	LHI	R0,(CMDLEN/4)*2	: Number of bytes = 2 * number of commands
	LH	R1,P.PORT,R13
	LR	R2,R0		: Message type
	JAL	R4,SLOR,,	: Start Logical Output Record
	LIS	R6,0		: Start from beginning of commmand table
CMDH1	LB	R0,SPACE+1	: Seperate letters
	JAL	R4,PUTCH,,
	LB	R0,CMDTAB,R6	: Get command code
	JAL	R4,PUTCH,,	: Output the character
	AIS	R6,4		: Each entry in command table is 4 bytes long
	CHI	R6,CMDLEN	: At end of table?
	JL	CMDH1		: No, do more
	JAL	R4,ELOR		: End of Logical Output Record
	LA	R3,CRLF
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Finish with CR LF
	J	NEXTC
: The user typed "D" or "p"

DSTR	SC	/ummy command goes here"8D"8A/
CMDD	LA	R3,DSTR		: Output the "D" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Output "Date is:" (after being patched)
	J	NEXTC

: The user typed "M" or "m"

MSTR	SC	/essage from George King:"8D"8A/
CMDM	LA	R3,MSTR		: Output the "M" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Output header to the message
	LA	R1,SHOMSG	: Address of routine to continue output
	LHI	R10,$A 20	: Do 20 lines at a time
	JAL	R12,BWNEXT	: Store info on what to do next
	J	NEXTC

: Routine to output GK messages.  Link on R12, count in R10
	GL	GETGKM		: Routine in SIO section
SHOMSG	JAL	R4,GETGKM,,	: Get address of next GK message into R3
	  J	0,R12		: Try again later if no message yet
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Output next line of message
	SIS	R10,1
	JGR	R12		: Come back later for 20 times
	JAL	R1,BPCHEK	: One last check for backpressure
	LA	R3,MORMSG	: "Type M for more"
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,
	J	BWDONE		: Stop sending messages
MORMSG	SC	/(type 'M' for more)"8D"8A/
: The user typed "P" or "p"

PSTR	SC	/resent time is: /
CMDP	LA	R3,PSTR		: Output the "P" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Output "The present time is: "
	JAL	R7,PTIME
	J	NEXTC

	SEG	A.DATA
TIMSTR	SC	/00:00:00"8D"8A/
	SEG	A.CODE

: Routine to output time of day and CRLF.  Link on R7

PTIME	L	R3,GMT,,	: Get current time of day
	LIS	R4,8		: Want to do 8 bytes
	LA	R5,TIMSTR+1	: Put it right after the byte count
	JAL	R6,TMSTMP,,	: Convert GMT to timestamp
	LA	R3,TIMSTR
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Output HH:MM:SS CR LF
	JR	R7

: User typed "Q" or "q"

QSTR	SC	/uit"8D"8A/
CMDQ	LA	R3,QSTR		: Output the "Q" message
	LH	R2,P.PORT,R13
	RBT	R2,INUSE	: Port is not in use any more
	JAL	R5,OCS,,	: "You typed Q to QUIT"
	LA	R3,DETMSG	: Output a DETACH command
	LH	R2,P.PORT,R13
	JAL	R5,OCM,,	: Output 1 byte message (no byte count)
	LCS	R0,1		: One less port in use
	AHM	R0,USECNT
	J	NEXTC		: Go back looking for more commands
: User typed "S" or "s"

SSTR	SC	/tatus of slot at /
CMDS	LA	R3,SSTR		: Output "S" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,
	JAL	R7,PTIME	: Output time of day and CRLF
	L	R2,ICOUNT	: Number of data bytes (excludes control msg)
	LI	R0,0920+0A	: 9 bytes, blank fill, decimal
	LA	R1,BYTEXX	: Where to store them
	JAL	R6,OUTNUM,,
	L	R2,ECOUNT	: Number of echoed bytes
	LI	R0,0920+0A	: 9 bytes, blank fill, decimal
	LA	R1,ECHOXX
	JAL	R6,OUTNUM,,
	L	R2,MCOUNT	: Number of multi-byte messages
	LI	R0,0620+0A	: 6 bytes, blank fill, decimal
	LA	R1,MULTXX	: Where to store them
	JAL	R6,OUTNUM,,
	LH	R2,P.PORT,R13
	LA	R3,MBSTR	: Output statistics
	JAL	R5,OCS,,
	L	R2,BCOUNT,,	: Number of background executions
	LI	R0,0920+0A	: 9 bytes, blank fill, decimal
	LA	R1,BCNTXX
	JAL	R6,OUTNUM,,
	L	R2,SCOUNT,,	: Number of sync bytes sent
	LI	R0,0840+10	: 8 bytes, zero fill, hex
	LA	R1,SCNTXX
	JAL	R6,OUTNUM,,
	LH	R2,P.PORT,R13
	LA	R3,BCSTR	: Output background count
	JAL	R5,OCS,,
	J	NEXTC

	SEG	A.DATA
MBSTR	SC	/000000000 bytes input 000000000 bytes echoed 000000 multi-byte messages"8D"8A/
BYTEXX	EQ	$A MBSTR+1
ECHOXX	EQ	$A BYTEXX+22
MULTXX	EQ	$A ECHOXX+23
BCSTR	SC	/000000000 background executions, sync count is 00000000"8D"8A/
BCNTXX	EQ	$A BCSTR+1
SCNTXX	EQ	$A BCNTXX+47
	SEG	A.CODE

: User typed "W" or "w"

WSTR	SC	/ho - you are on/
CMDW	LA	R3,WSTR		: Output the "W" message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,
	JAL	R7,PORTIS	: Output " port XX"
	LA	R1,CRLF		: Message to output after SYSTAT
	JAL	R4,SYSTAT	: Set up to do a systat
	J	NEXTC


: Routine to output port number and CRLF.  Link on R7

PORTIS	LI	R0,0240+10	: 2 digits, zero fill, hex
	LA	R1,THISXX	: Address of XX in "This is port XX"
	LH	R2,P.PORT,R13
	JAL	R6,OUTNUM
	LA	R3,GRTSTR	: Output greeting message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,
	JR	R7

	SEG	A.DATA
GRTSTR	SC	/ port XX"8D"8A/
THISXX	EQ	$A GRTSTR+7
CONSTR	SC	/This is host XX in node XXXX slot X. XX ports in use out of XX"8D"8A/
HOSTXX	EQ	$A CONSTR+14
NODEXX	EQ	$A HOSTXX+11
SLOTXX	EQ	$A NODEXX+10
USEDXX	EQ	$A SLOTXX+3
MAXPXX	EQ	$A USEDXX+23
LINSTR	SC	/Port XX came from node XXXX, user /
PORTXX	EQ	$A LINSTR+6
ONODXX	EQ	$A PORTXX+18
	SEG	A.CODE

: Show status (who's logged in).  Link on R4, message in R1

SYSTAT	LR	R11,R1		: Copy message pointer to preserved reg
	LA	R1,SYST0	: Address of routine
	JAL	R12,BWNEXT	: Continue at SYST0 next time
	JR	R4

: Show status, called from BWRITE, link on R12

SYST0	LH	R2,USECNT	: Number of ports in use
	LI	R0,0220+0A	: 2 bytes, blank fill, decimal
	LA	R1,USEDXX	: Address of XX in CONSTR
	JAL	R6,OUTNUM,,
	LH	R2,P.PORT,R13
	LA	R3,CONSTR	: Point to the configuration message
	JAL	R5,OCS,,	: Send string
	LA	R10,P.LAST	: Point to last port's data

: Show status of all lines

SYST1	JAL	R1,BPCHEK	: Check on backpressure (preserves R10,R11)
	LH	R2,P.PORT,R10,	: Get port number
	TBT	R2,INUSE	: Check if port is in use
	JE	SYST2
	LI	R0,0220+10	: 2 digits, blank fill, hex
	LA	R1,PORTXX	: Address in message
	JAL	R6,OUTNUM,,	: Put port # in LINSTR
	LI	R0,0440+08	: 4 digits, zero fill, octal
	LA	R1,ONODXX
	LH	R2,P.ONOD,R10,	: Origination node
	JAL	R6,OUTNUM,,	: Put port # on LINSTR
	LH	R2,P.PORT,R13
	LA	R3,LINSTR
	JAL	R5,OCS,,	: Output port number and node number
	LA	R3,P.NAMS,R10,	: Point to user name count field
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,
	LA	R3,CRLF
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Finish with CR LF
SYST2	SH	R10,P.SIZE	: Point to previous port's data
	CI	R10,P.BASE	: Check to see if at beginning already
	JG	SYST1		: Output info for all lines
	JAL	R1,BPCHEK	: One last check for backpressure
	LR	R3,R11		: Done them all, now do trailing message
	LH	R2,P.PORT,R13
	JAL	R5,OCS,,	: Send CRLF or "Welcome" message
	J	BWDONE		: End of SYSTAT
	SUBTTL	Input and echo routines

	SEG	A.DATA
HSECHO	HS	MAXPHW		: Bit map for ports needing echo
NCOUNT	HC	0		: Count of characters in current input message
ICOUNT	WC	0		: Count of data bytes input from IRING
ECOUNT	WC	0		: Count of input bytes that were echoed back
MCOUNT	WC	0		: Number of multi-byte messages
ECHO1C	SC	/X/		: ISIS message to output one character
ECHOR0	EQ	ECHO1C+1	: The character to echo is stored here
ECHO2C	SC	/^X/		: ISIS message to output two characters
ECHO2X	EQ	ECHO2C+2	: Converted character here
ECHORB	SC	/^?/		: Echo RUBOUT as up-arrow ionmark
	SEG	A.CODE

: Routine to get a character, echoing it if necessary.  Link on R6.

MYGETC	LH	R0,NCOUNT	: Number of bytes left to process
	SIS	R0,1
	JL	MYGET0		: End of this input string
	STH	R0,NCOUNT
	AIS	R6,4		: Set up for skip return
	JAL	R4,GETCH,,	: Get next byte
	LH	R2,P.PORT,R13
	TBT	R2,HSECHO	: Should the host (me) do echoing?
	JER	R6		: No, return with character in R0
	LIS	R1,1		: Yes, count number of times we echoed
	AM	R1,ECOUNT
	JFS	MYECHO		: Echo character before returning

MYGET0	JAL	R4,ELIR,,	: End of logical Input record
	JR	R6		: Nonskip return from MYGETC

: Routine to do host echo.  Character to be echoed in R0, link on R6

MYECHO	STB	R0,ECHOR0	: Save character to be echoed
	NHI	R0,7F		: Clear parity bit before doing compares
	LA	R3,ECHORB
	CHI	R0,7F		: Check for RUBOUT/DELETE
	JE	MYECH1		: It is echoed as ^?
	CHI	R0,07		: Check for ^@ ^A ^B ^C ^D ^E ^F
	JL	UPECHO		: They are echoed in up-arrow form
	CHI	R0,0D		: Check for BL, BS, HT, LF, VT, FF
	JLFS	ECHOIT		: They are echoed as themselves
	LA	R3,CRLF
	CHI	R0,0D		: Check for CR
	JE	MYECH1		: It is echoed as CR+LF
	CHI	R0,1F		: Check if it looks like a printing char
	JLE	UPECHO		: Echo control chars in up-arrow mode
ECHOIT	LA	R3,ECHO1C	: Echo the one character (in ECHOR0)
	JFS	MYECH1
UPECHO	LB	R0,ECHOR0	: Get the control character
	AHI	R0,40		: Make it a printing letter
	STB	R0,ECHO2X
	LA	R3,ECHO2C	: Echo two characters
MYECH1	LH	R2,P.PORT,R13
	JAL	R5,OCS,,
	LB	R0,ECHOR0	: Return character in R0
	JR	R6		: Return from MYECHO/MYGETC
	SUBTTL	Ball logic
: Echo processing:  When a new circuit arrives, we send a B10001 message to
: turn on echo mode.  The CONSAT will send us a green ball, which we return.
: It then sends use a LDEM when it takes over echoing.  When an unechoable
: character is typed, the CONSAT preceeds it with a EDEM, telling us to enter
: deferred echo mode (where we do echoing).  We return any red balls that
: the CONSAT may send us.

: Enter and Leave Deferred Echo Mode

CTLEDE	SBT	R1,HSECHO	: Enter DEM - host must echo
	J	BFLUSH		: (message type still in R2)

CTLLDE	RBT	R1,HSECHO	: Leave DEM - CONSAT now doing echo
	J	BFLUSH		: (message type still in R2)

: Red and Green Balls (CONSAT uses these as part of echo processing)

CTLRED	LB	R0,LENGTH,R2,	: Get size from table in SEG F
	JAL	R4,FLUSH	: Skip past message (in R2)
	LH	R2,P.PORT,R13
	LA	R3,REDMSG
	JAL	R5,OCM,,	: Reflect red ball as red ball
	J	BMAIN

CTLGRE	LB	R0,LENGTH,R2,	: Get size from table in SEG F
	JAL	R4,FLUSH	: Skip past message (in R2)
	LH	R2,P.PORT,R13
	LA	R3,GREMSG
	JAL	R5,OCM,,	: Reflect green ball as green ball
	J	BMAIN

: Yellow and Orange Balls:  Whenever we get a yellow ball, we send back
: an orange ball so the CONSAT knows that all input has been received.
: When we send a yellow ball, we set a bit in OBWAIT and wait for an
: orange ball to come back from the CONSAT, indicating that it has
: received all our output data.

CTLYEL	LB	R0,LENGTH,R2,	: Get size from table in SEG F
	JAL	R4,FLUSH	: Skip past message (in R2)
	LH	R2,P.PORT,R13
	LA	R3,ORAMSG
	JAL	R5,OCM,,	: Reflect yellow ball as orange ball
	J	BMAIN

CTLORA	RBT	R1,OBWAIT	: No longer waiting for an orange ball
	J	BFLUSH		: (message type still in R2)
	SUBTTL	Zaps and backpressure
: The bit 0002 in CNTRL says that ports are new reusable until they emit
: disconnect, the Dispatcher wants us to return zappers.

CTLZAP	RBT	R1,INUSE	: Turn off the in-use bit
	LCS	R0,1		: One less port in use
	AHM	R0,USECNT
	LB	R0,LENGTH,R2,
	JAL	R4,FLUSH,,	: Remove zapper from IRING
	LH	R2,P.PORT,R13
	LA	R3,ZAPMSG	: The dispatcher wants to see a zap back
	JAL	R5,OCM,,
	J	BMAIN

: Stop sending when backpressure is turned on

CTLBPN	SBT	R1,BPRESS	: Stop BWRITE from sending anything more
	J	BFLUSH

: Resume when backpressure is turned off

CTLBPF	RBT	R1,BPRESS	: Turn off bit so that BWRITE will continue
	J	BFLUSH

: Set new transmit limit

CTLLIM	JAL	R4,GETH,,	: Pick up the B9 and the limit byte
	STB	R0,P.SLIM,R13	: This is the new send limit
	JAL	R4,ELIR,,	: Tell FRING that we got the whole message
	J	BMAIN
: Process control messages from the IRING.  Port in R1, type in R2.  R13 set up

BCNTRL	LHI	R4,CTLLEN	: Size of table to check
CTLCHK	SIS	R4,4		: Stop at end of table
	JL	BFLUSH		: Ignore messages not in our table
	LB	R0,CTLTAB,R4	: Get code
	CR	R0,R2		: Match message type?
	JN	CTLCHK		: Loop if not
	L	R6,CTLTAB,R4	: Get dispatch address
	NI	R6,00FFFFFF	: Mask off code, leave only 24-bit address
	JR	R6		: Go to routine

XX	MACRO(CH,ADDR)	[WC CH*01000000+ADDR]	: Combine CH and address

XCTLX	MACRO	[	: Control messages we are expecting
	XX(09F,CTLZAP)		: Zapper
	XX(0A0,CTLBPN)		: Backpressure oN
	XX(0A1,CTLBPF)		: Backpressure oFf
	XX(0A6,CTLEDE)		: Enter Deferred Echo Mode
	XX(0A7,CTLLDE)		: Leave Deferred Echo Mode
	XX(0AA,CTLGRE)		: Green Ball
	XX(0AB,CTLRED)		: Red Ball
	XX(0AC,CTLYEL)		: Yellow Ball
	XX(0AD,CTLORA)		: Orange Ball
:*:	XX(0B1,CTLTTY)		: Terminal parameters
	XX(0B9,CTLLIM)		: Set transmit Limit
	]

CTLTAB	WS	0		: Must start on a word boundary
	XCTLX			: Create command table
CTLLEN	EQ	.-CTLTAB	: Number of bytes in command table
	KILL	XCTLX,XX



::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::									    ::
::  End of JS2033.R03(..MAIN), the foreground code is in JS2033.F03(FMAIN)  ::
::									    ::
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
   [m[X‘