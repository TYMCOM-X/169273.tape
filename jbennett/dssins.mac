TITLE DSSINS - DISTRIBUTED SOFTWARE INSTALLATION PROGRAM
       
P=17
C=16
T1=1
T2=2
T3=3
T4==4 
N=11
E=5
BYP==12
CNT==13
RTINX==14
DISK==4
D==5
A==6
B==7
WD==10
CH==11

LOC 137
3,,1
RELOC

INTERNAL P,C,T1,T2,T3

EXTERNAL JOBFF


FIL==1	;CHANNEL FOR FILE
DSK==2  ;CHANNEL FOR DATA FILE
TTY==3  ;CHANNEL FOR TTY 

;DEFINITONS FOR SOME WORDS IN THE LOOKUP BLOCKS

RPPN==1
RNAM==2 ;NAME OF FILE IN EXTENDEC LOOKUP BLOCK
REXT==3 ;NAME OF EXT IN EXTENDED LOOKUP BLOCK
RPROT==4 ;PROTECTION WORD IN LOOKUP AND RENAME BLOCK
RLIC==14;WORD CONTAINING LICENSE (RH)

DATAWC==0,,400000

;MISC

SIZUSR==^D12            ;12 CHARACTER USER NAME
SIZFIL==6
SIZEXT==3
WFLIC==10


;GETTAB DEFINITIONS
LICENS==20

;CHARACTER DEFINITIONS

LNFEED==12
CARRET==15
SPACE==40
LPAREN==50
RPAREN==51
STAR==52
COMMA==54
DOT==56
SLASH==57
ZEROCH==60
ATSIGN==100
ALPHA==101
ZETA==132


ARRAY PDL[40],IBUF,OBUF,INBF[3],FILNM[11],USRN[2]
OPDEF NOP[JFCL]


STPT:	RESET
        MOVE P,[IOWD 40,PDL]
        INIT FIL,14
        SIXBIT /DSK/
        OBUF,,IBUF
        JRST ERRINT     ;ERROR ON FILE INITIALIZATION
        INIT DSK,14
        SIXBIT /DSK/
        0,,INBF
        JRST ERRINT     ;AGAIN ERROR ON FILE INIT.
MAIN:   PUSHJ P,CKRUN
MAIN1:  SKIPN CMDFLG
        OUTSTR [ASCIZ/
>/]
        SKIPN CMDFLG
        PUSHJ P,GETC
        SKIPE CMDFLG
        PUSHJ P,GCMDIN
        CAIN CH,"I"
        JRST INSTAL
        CAIN CH,"B"
       ;JRST BACKUP
        CAIE CH,"Q"
        JRST ILLINP
        JRST QUIT


INSTAL: PUSHJ P,PREP    ;INITAILIZE BEFORE DOING ANY FILE MANIPULATION
        PUSHJ P,SUBCOM  
INSTL1: PUSHJ P,RECPPN          ;NOW GET THE PPN WE WILL USE HERE
INSTL2: MOVE T1,%SSPPN          ;PPN FOR THE SSINSTALL DIRECTORY
        MOVEM T1,INSLOK+RPPN    ;PUT PPN IN LOOKUP BLOCK
        MOVE T1,FILNAM          ;GET FILE NAME FROM MEMORY
        MOVEM T1,INSLOK+RNAM    ;PUT IN LOOKUP BLOCK
        MOVE T1,FILNAM+1
        MOVEM T1,INSLOK+REXT    ;PUT EXTEN IN LOOKUP BLOCK
        LOOKUP FIL,INSLOK
         JRST NOTINS            ;NOT IN SYSINST ;QUIT AND GO HOME
        MOVE T1,[INSLOK,,INSSAV]
        BLT T1,INSSAV+RLIC      ;SAVE LOOKUP BLOCK
CHKBAK: MOVE T1,%BAPPN          ;PPN FOR SYSBACKUP DIRECTORY
        MOVEM T1,INSLOK+RPPN    ;PUT IN LOOKUP BLOCK
        LOOKUP FIL,INSLOK       ;IS THE FILE IN SYSBACKUP?
         JRST CHKSYS            ;NO; THIS SHOULD BE NORMAL CONDITON
        JRST INBAKP             ;YES 
CHKSYS: MOVE T1,%SYSPN          ;PPN FOR SYS DIRECTORY
        MOVEM T1,INSLOK+RPPN    ;PREPARE TO CHECK SYS
        MOVE T1,FILNM+1
        HLLZM   T1,INSLOK+REXT  ;PUT EXTENTION IN AND REMOVE ERROR CODE
        LOOKUP FIL,INSLOK
         SETOM %NOSYS           ;FILE NOT IN SYS
        MOVE T1,[INSLOK,,SYSSAV]
        BLT T1,SYSSAV+RLIC     ;SAVE IN SYSSAV LOCATION FOR LATER USE
         PUSHJ P,LICHK   ;GO CHECK DATA FILE AND SYS FILE
        MOVE T1,[SYSSAV,,INSLOK]        ;RECOVER SYSSAV BLOCK NOW
        BLT T1,INSLOK+RLIC
        MOVE T1,%SYSPN          ;GET PPN FOR SYS
        MOVEM T1,INSLOK+1
        LOOKUP FIL,INSLOK       ;SET-UP FOR RENAME FROM SYS 
         JRST SETSYS
        MOVE T1,%BAPPN          ;TO SYSBACKUP DIRECTORY
        MOVEM T1,INSLOK+RPPN
        RENAME FIL,INSLOK
         JRST RPTERR            ;TELL MASER WE'S GOT TROUBLES
SETSYS: MOVE T1,[SYSSAV,,SYSLOK]
        BLT T1,SYSLOK+RLIC      ;GET THAT LOOKUP BLOCK WE SAVED
        MOVE T1,%SSPPN          ; PPN FOR SYSINST
        MOVEM T1,SYSLOK+RPPN    ;PUT IN LOOKUP BLOCK
        LOOKUP FIL,SYSLOK       ;READY FOR THE RENAME
         NOP                    ;NOTING TO DO HERE
        MOVE T1,%SYSPN          ;GET SYSPPN
        MOVEM T1,SYSLOK+1       ;PUT IN BLOCK
        LDB T1,[POINT 9,SYSSAV+RPROT,8] ;GET PROTECTION BITS
        DPB T1,[POINT 9,SYSLOK+RPROT,8] ;PLACE IN LOOKUP&RENAMEBLOCK
        HRRM T3,SYSLOK+RLIC    ;SET LICENSE BITS IN PROPER PLACE
        PUSHJ P,CNGDAT          ;UPDATE CREATION DATE
        RENAME FIL,SYSLOK       ;OUT OF SSINSTALL INTO SYS
         JRST NOINS             ;BAD SITUATION TELL MASTER
DONE:   PUSHJ P,PNTFIL
        PUSHJ P,PNTUSR
        LDB T1,[POINT 9,SYSLOK+RPROT,8] ;GET PROTECTION BITS
        LSH T1,^D27                     ;PUT IN 0-8 POSTIONS
        HRR T1,SYSLOK+RLIC              ;LIC IN RH 18-35
        PUSHJ P,PNTLIC
        PUSHJ P,CRLF
        JRST MAIN1

GCMDIN: MOVE CH,WD
        ROT CH,6
        ADDI CH,40
        POPJ P,

;GNC WILL RETURN NEXT CHARACTER TO CALLING ROUTINE
;WHETHER IT BE TERMINAL OR FILE INPUT THE CALL IS THE SAME
;PUSHJ P,GNC THE CHARACTER IS RETURNED IN CH AC.

GNC:    SKIPE CMDFLG    ;ARE WE COMMING FROM CMD FILE
        JRST GNCD       ;YES,START READING
        INCHRW CH       ;NO
        CAIG CH,172     ;IS CHAR IN LOWER CASE?
        CAIGE CH,141
        SKIPA
        SUBI CH,40      ;IF SO CONVERT TO UPPER CASE
        POPJ P,         ;LET USER PROGRAM FIDDLE WITH CH

GNCD:   SOSL IBUF+2     ;ANYTHING LEFT IN BUFFER?
        JRST GNCD1      ;YES,PUT IN CH AND RETURN
        IN DISK,        ;RELOAD BUFFERS
        JRST GNC        ;GET NEXT CHAR
        RELEASE DISK,
        SETZ CMDFLG
        JRST MAIN1      ;READY FOR MORE ACTION

GNCD1:  ILDB CH,IBUF+1  ;GET CHAR IN CH
        CAIG CH,172     ;CHECK FOR LOWER CASE
        CAIG CH,141
        SKIPA
        SUBI CH,40      ;IF SO CONVERT TO UPPER CASE
        JUMPE CH,GNC    ;IGNORE NULLS
        POPJ P,         ;SEND CH BACK FOR PROCESSING

;SUBCOM WILL READ THE COMMAND STRING TO GET USERNAME AND
;FILE NAME AND STORE EACH IN MEMORY USRN AND FILNAM
;RESPECTIVELY. AFTER A SUCCESSFUL COMPLETION RETURNS TO CALLING
;MAIN SUB ROUTINE FOR NEXT STEP.

SUBCOM: SKIPN CMDFLG    ;NO PROMPT NECESSARY IF CMDS FILE
        OUTSTR [ASCIZ/
*/]
        SETZM USRN      ;SET TO ZERO NAME FOR KNOWN VALUE
        MOVE T1,[XWD USRN,USRN+1]
        BLT T1,USRN+3   ;MAKE SURE OF WHAT WE START WITH
        PUSHJ P,RDFIL           ;GET FILE NAME
        POPJ P,                 ;RETURN TO CALLER

RDUSR:  MOVE BYP,[POINT 6,USRN]
        MOVEI CNT,SIZUSR        ;LIMIT OF CHARACTERS
RDUSR1: PUSHJ P,GNC
        CAIN CH,RPAREN
        POPJ P,         ;RETURN ON RIGHT PAREN
        CAIN CH,CARRET
        JRST RDUSR1
        CAIN CH,LNFEED
        JRST [SETOM %FILPT
              POP P,E
              JRST RDFIL]       ;LINEFEED NOW REQUIRES A PROMT FOR FILE
        SUBI CH,SPACE           ;MAKE SIXBIT
        SOJL CNT,RDUSR1         ;DON'T EXCEED LIMIT
        IDPB CH,BYP
        JRST RDUSR1

RDFIL:  SKIPE %FILPT            ;DO WE PROMPT FOR A FILE NAME
        OUTSTR [ASCIZ/FILE NAME: /]       ;YES
        MOVE BYP,[POINT 6,FILNAM]
        MOVEI CNT,SIZFIL
RDFIL1: PUSHJ P,GNC
        CAIN CH,SPACE
        JRST RDFIL1
        CAIN CH,LPAREN          ;LEFT PAREN MEANS USER NAME TO BE READ
        PUSHJ P,RDUSR           ;GO READ THEN GET BACK
        CAIN CH,RPAREN          ;END OF USER NAME START READING FILE
        JRST RDFIL              ;;POINTERS ALREADY SET-UP
        CAIN CH,DOT             ;TIME TO GET EXTENTION
        JRST RDEXT              ;GO
        CAIN CH,CARRET
        JRST RDFIL1
        CAIN CH,LNFEED
        JRST FILCHK             ;CHECK FOR ANY CHAR IN FILNAM
        CAIL CH,ZEROCH
        CAILE CH,ZETA
        JRST ILLCHF             ;SAY NO-NO BUT ALLOW A RETRY
        SUBI CH,SPACE
        SOJL CNT,RDFIL1
        IDPB CH,BYP
        JRST RDFIL1
        
RDEXT:  MOVE BYP,[POINT 6,FILNAM+1]
        MOVEI CNT,SIZEXT
RDEXT1: PUSHJ P,GNC
        CAIN CH,"["             ;TEST FOR PROTECTION CODE
        JRST RDPROT
        CAIN CH,CARRET
        JRST RDEXT1
        CAIN CH,LNFEED
        POPJ P,                 ;THIS SHOILD BE THE NORMAL EXIT
        CAIL CH,ZEROCH          ;OK TO HAVE ANY EXTENSION USER WANTS
        CAILE CH,132            ;EXTENSION SHOULD HAVE ONLY ALPHA CHARS
        JRST ILLCHF
        SUBI CH,SPACE
        SOJL CNT,RDEXT1
        IDPB CH,BYP
        JRST RDEXT1


RDPROT: CAIE RTINX,7            ;COMMING FROM DECLARE?
        JRST ILLCHF             ;ERROR IF NOT
        MOVE BYP,[POINT 3,PROTSV,26] ;INIT BYTE PTR TO PROTSV
        MOVEI CNT,3             ;INIT CNTR TO # OF FIELDS
        MOVE T1,[POINT 7,T2]     ;INIT BYTE PTR TO SAVE REG
        MOVEI T2,0              ;CLEAR SAVE REG
RDPRO1: PUSHJ P,GNC             ;READ NEXT CHAR
        CAIN CH,"]"             ;DISCARD BRACKET
        JRST RDPRO1
        CAIN CH,CARRET          ;DISCARD CR
        JRST RDPRO1
        CAIN CH,LNFEED          ;ENTER SEARCH ON LF
        JRST SRHLP-1
        CAIN CH,SPACE           ;ENTER SEARCH ON SP
        JRST SRHLP-1
        CAIG CH,"Z"             ;IS CHAR ALPHA?
        CAIGE CH,"A"
        JRST ILLCHP             ;IF NOT,REENTER
        IDPB CH,T1              ;SAVE CHAR FOR COMPARISON
        JRST RDPRO1             ;AND CONTINUE READING
        MOVEI T1,0              ;INIT PROTBL INDEX
SRHLP:  CAMN T2,PROTBL(T1)      ;IS THERE A MATCH?
        JRST PRTFND             ;IF SO, UPDATE PROTECTION
        ADDI T1,1               ;ELSE, PROCEED TO END
        CAIL T1,PROEND          ;IF NONE FOUND, REENTER
        JRST ILLCHP
        JRST SRHLP              ;ELSE, CONTINUE LOOKING
PRTFND: IDPB T1,BYP             ;SAVE PROTECTION CODE
        SOJG CNT,RDPRO1-2       ;GET REMAINING FIELDS
        POPJ P,                 ;RETURN TO CALLER
 
RDCFIL: SETOM SUPFLG#  ; FOR USE IN RDUSER FUNCTION
        SETZM USRN      ;NO LEFTOVERS FROM PREVIOUS USE
        MOVE E,[XWD USRN,USRN+1]        ;START FRESH
        BLT E,USRN+3    ;CLEAR USERNAME AND FILENAME
        INIT DISK,0      ;ASCIZ MODE FOR COMMANDS FILE
        SIXBIT/DSK/
        0,,IBUF         ;ONLY INPUT FOR THIS GUY
        JRST BDEV       ;SAY BAD DEVICE AND SEND TO PROMPT
        MOVEI E,DBUF    ;GIVE E THE ADDRESS OF BUFFER HDR
        EXCH E,JOBFF    ;CHANGE JOBFF TO NEW LIMITS
        INBUF DISK,2    ;TWO BUFFERS FOR COMMANDS FILE
        MOVEM E,JOBFF   ;GIVE BACK JOBFF
GETFIL: PUSHJ P,RDFIL   ;NOW GET FILENAM AND EXT
        PUSHJ P,RECPPN  ;GO GET PPN FOR COMMAND FILE
        MOVE T1,FILNAM  ;GET FILE NAME
        MOVEM T1,CMDFIL+2       ;SET IN LOOKUP BLOCK
        MOVE T1,FILNAM+1        ;GET EXT
        MOVEM T1,CMDFIL+3       ;SET IN LOOKUP BLOCK
        LOOKUP DISK,CMDFIL      ;COMMAND FILENAME
         JRST CMDERR
        SETOM CMDFLG            ;LET WORLD KNOW WERE USING CMD FIL
        SETZM USRN      ;ZERO USRENAME FOR LATER SECURITY
        MOVE E,[XWD USRN,USRN+1]        ;SETUP TO ZERO USERN FILNM
        BLT E,USRN+3    ;ONLY 4 WDS BUT ADD 20 AND CAN 0 SYSERT
        JRST GETC


;GET CHAR ROUTINE WILL RETURN TO USER AND PROCESS COMMAND OR GO
;TO COMMAND FIL PROCESSING ROUTINE

GETC:   SETZ WD,
        HRRZI T2,6
        MOVE BYP,[POINT 6,WD]
GETC1:  PUSHJ P,GNC
        CAIN CH,SPACE   ;IGNORE BLANKS
        JRST GETC1
        CAIN CH,ATSIGN  ;MEANS COMMANDS FILE
        JRST RDCFIL
        CAIN CH,LNFEED  ;TERMINATING LNFEED?
        JRST [JUMPE WD,.+2
             SETOM %SYSPT
             POPJ P,]
        CAIN CH,CARRET
        JRST [JUMPE WD,.+2
              JRST GETC1
              POPJ P,]
        SUBI CH,40      ;MAKE SIXBIT
        SOJE T2,GETC1   ;ONLY LET SIX CHAR INTO WORD
        IDPB CH,BYP
        JRST GETC1      ;GET BALANCE OF WORD

;PROTECTION TABLE USED BY COMMAND SCANNER

PROTBL: ASCIZ /ALL/
        ASCIZ /CP/
        ASCIZ /UPD/
        ASCIZ /AP/
        ASCIZ /RD/
        ASCIZ /RUN/
        ASCIZ /LK/
        ASCIZ /NO/
PROEND==.-PROTBL

;PROTECTION WORDS THE ACTUAL CODE OR NUMBER IN THE 3 BIT FIELD
;IS USED AS THE INDEX IN THIS TABLE.

WORD:   ASCIZ/ ALL/     ;ALL PRIVILEGES
        ASCIZ/ CP/      ;CHANGE PROTECTION
        ASCIZ/ UPD/     ;UPDATE FILE
        ASCIZ/ AP/      ;APPEND TO FILE
        ASCIZ/ RD/      ;READING OF FILE
        ASCIZ/ RUN/     ;RUNNING OF FILE
        ASCIZ/ LK/      ;DIRECTORY OF FILE
        ASCIZ/ NO/      ;NOTHING

;END OF PROTECTION TABLE THE SPACE IN FRONT OF THE ABBRV IS
;TO PROVIDE A SPACE IN THE TTY LINE.LOOKS NICE

;LICENSE BIT TABLE FOR USE IN REPORTING LICENSE ON FILE
;F0-F4 ARE UNUSED MONITOR LICENSE BITS

RFTB:    0
        ASCIZ/ RP/
        ASCIZ/ RF/
        ASCIZ/ RA/

WFTB:   0
        ASCIZ/ WP/
        ASCIZ/ WF/
        ASCIZ/ WA/

LICTB:  ASCIZ/ WC/      ;WRITE CORE
        ASCIZ/ RC/      ;READ CORE
        ASCIZ/ OP/      ;OPERATOR
        ASCIZ/ SY/      ;SYSTAT
        ASCIZ/ GD/      ;GET DEVICES(PERIPERALS)
        ASCIZ/ TD/      ;TRANSFER THOS DEVICES
        ASCIZ/ ST/      ;STRUCTURES
        ASCIZ/ HF/      ;HOME FILES
        ASCIZ/ JL/      ;JAL-JACCT(ACCOUNTING)
        ASCIZ/ XC/      ;TRANSFER ACCOUNTING CHARGES
        ASCIZ/ F1/      ;STRART OF UNUSED LICENSE BITS
        ASCIZ/ F2/
        ASCIZ/ F3/

ILLINP: OUTSTR [ASCIZ/ILLEGAL COMMAND VALID COMMANDS ARE:

        I       (INSTALL)

        B       (BACKUP)

        C       (CHKSUM)

/]
        JRST MAIN1

ILLCHU: OUTSTR [ASCIZ/ILLEGAL CHHARATER IN USERNAME - REENTER:/]
        PUSHJ P,CRLF
        SETZM USRN
        SETZM USRN+1
        PUSHJ P,WTLF
        JRST RDUSR

ILLCHF: OUTSTR [ASCIZ/ILLEGAL CHARATER IN FILE NAME - REENTER:/]
        PUSHJ P,CRLF
        SETZM FILNAM
        SETZM FILNAM+1
        PUSHJ P,WTLF
        JRST RDFIL

ILLCHP: OUTSTR [ASCIZ /ILLEGAL PROTECTION CODE - REENTER: /]
        PUSHJ P,CRLF
        SETZM PROTSV
        PUSHJ P,WTLF
        JRST RDPROT



WTLF:   PUSHJ P,GNC
        CAIN CH,LNFEED
        POPJ P,
        JRST WTLF
FILCHK: SKIPE FILNAM
        POPJ P,         ;HAVE NAME WILL TRAVEL
        SETZM FILNAM
        SETZM FILNAM+1
        SETOM %FILPT
        SKPINC
        JRST RDFIL
        PUSHJ P,WTLF
        JRST RDFIL

OUTNUM: MOVE WD,[POINT 7,T1]    ;HERE TO PRINT SYSTEM NUMBERS
        HRRZI N,5               ;PRINT UP TO THE 5 CHAR LIMIT
OUTNM1: ILDB CH,WD      ;GET A CHAR
        JUMPE CH,RET            ;POPJ BACK IF NULL
        OUTCHR CH       ;PRINT THE CHARATER
        SOJG N,OUTNM1   ;ONLY PRINT TWO DIGITS
        POPJ P,         ;RETURN
PNTUSR: MOVE T1,USRN            ;GET FIRST WORD OF USER NAME
        OUTCHI LPAREN           ;SURROUND USER NAME
        PUSHJ P,OUTASC          ;PRINT SAME
        MOVE T1,USRN+1          ;SECOND WORD
        PUSHJ P,OUTASC          ;PRINT SECOND WORD
        OUTCHI RPAREN           ;CLOSE IT UP
        POPJ P,                 ;HOME AGAIN



;PNTLIC ROUTINE WILL PRINT THE PROT AND LIC
;ON THE SPECIFIED FILE IN SUB COM.THE PROT & LIC BITS
;WILL BE PROVIDED BY THE SLAVE AFTER COMPLETION OF REQUESTED
;OPERATION

PNTLIC: LDB E,[POINT 3,T1,2]    ;GET FIRST 3 BITS OF PROT
        OUTSTR WORD(E)          ;PRINT WORD
        LDB E,[POINT 3,T1,5]    ;SECOND 3 BITS
        OUTSTR WORD(E)          ;AGAIN PRINT WORD
        LDB E,[POINT 3,T1,8]    ;LAST 3 BITS
        OUTSTR WORD(E)          ;PRINT LAST WORD OF PROTECTION STRING
        OUTCHI COMMA            ;SEPERATE PROTECTION AND LICENSE
        HRRZM T1,E              ;PUT LICENSE BITS IN RT HALF OF E
        LDB N,[POINT 2,E,33]    ;GET READ LIC BITS
        OUTSTR RFTB(N)          ;PRINT APPROPRIATE ABBRV
        LDB N,[POINT 2,E,35]    ;GET WRITE LICENSE BITS
        OUTSTR WFTB(N)          ;PRINT WRITE LIC BITS
        TRZ T1,17               ;ZERO READ AND WRITE BITS;ALREADY RPTD
        MOVEI N,0               ;SET INDEX TO START OF TABLE
LICLP:  TRZE T1,400000          ;START WITH WRITE CORE AND WORK DOWN TBL
        OUTSTR LICTB(N)         ;PRINT LIC ABBRV
        LSH T1,1                ;TRY NEXT LIC BIT IF ON PRNT ABBRV FROM TBL
        ADDI N,1                ;NEXT ABBRV IN TABLE
        JUMPN T1,LICLP          ;LOOP UNTIL THE LSH HAS MOVED OUT ALL BTS
        POPJ P,                 ;RETURN

OUTASC: MOVE WD,[POINT 6,T1]
        MOVEI N,SIZFIL
OUTAS1: ILDB CH,WD
        JUMPE CH,RET
        ADDI CH,SPACE
        OUTCHR CH
        SOJG N,OUTAS1
RET:    POPJ P,


OUTDEC: SKIPA N,[^D10]
OUTOCT: MOVEI N,10
        IDIV T1,N
        HRLM T2,(P)
        SKIPE T1
        PUSHJ P,OUTOCT+1
        HLRZ T1,(P)
        OUTCHI "0"(T1)
        POPJ P,
PNTFIL: MOVE T1,FILNAM  ;FILE NAME TO BE PRINTED
        PUSHJ P,OUTASC
        OUTCHI DOT
        MOVE T1,FILNAM+1        ;GET EXTENTION
        PUSHJ P,OUTASC
        POPJ P,         ;HOME AGAIN

NOTINS: OUTSTR [ASCIZ/FILE NOT IN SYSINST/]
        PUSHJ P,CRLF
        JRST MAIN1               ;MOST LIKELY TO QUIT;BUT WAIT AND SEE
RPTERR: OUTSTR [ASCIZ/RENAME ERROR IN SYSINST OR SYSBACKUP/]
        JRST MAIN1       ;SEE WAT MASER WANTS


INBAKP: SETZM INSLOK+RNAM       ;IF THERE ZERO FILE NAME
        SETZM INSLOK+REXT       ;AND EXTENTION
        RENAME FIL,INSLOK       ;DELETE FILE.OLD AND MAKE NEW ONE
         JRST RPTERR
INBAK2: MOVE T1,[INSSAV,,INSLOK]        ;SET UP LOOKUP BLOCK AGAIN
        BLT T1,INSLOK+RLIC      ;GET BALANCE FOR WORDS
        JRST CHKBAK     ;ROUND AND ROUND UNTIL LOOKUP FAILS(CHKBAK+2)

NOINS:  OUTSTR [ASCIZ/RENAME ERROR (SYSINST TO SYS)/]
        PUSHJ P,CRLF
        JRST MAIN1       ;WAIT

CNGDAT: MOVE T1,[11,,11]        ;GET CURRENT DATE
        GETTAB T1,
         JFCL
        LDB T2,[POINT 12,T1,35] ;GET LOW ORDER BITS
        DPB T2,[POINT 12,SYSLOK+4,35] ;SAVE IN LOOKUP BLOCK
        LDB T2,[POINT 2,T1,23]  ;GET HIGH ORDER BITS
        DPB T2,[POINT 2,SYSLOK+3,21] ;SAVE IN LOOKUP BLOCK
        MOVE T1,[10,,11]        ;GET CURRENT TIME
        GETTAB T1,
         JFCL
        IDIVI T1,^D3600         ;CONVERT TO MINUTES
        DPB T1,[POINT 11,SYSLOK+4,23] ;SAVE IN LOOKUP BLOCK
        POPJ P,                 ;RETURN TO CALLER

LICHK:  LOOKUP DSK,DATNAM       ;DATA FILE FOR COMPARISON
         JRST NODAT             ;NO DATA FILE ; NO PROCEED
        MOVE T1,DATNAM+RLIC     ;GET LIC BITS
        TRNN T1,DATAWC          ;MUST HAVE THIS BIT TO PROCEED
         JRST NODAT             ;BAD DATA FILE
        JRST    USRSRH
GET:    SOSGE   INBF+2          ;IS INPUT BUFFERY?
        JRST    GETBUF          ;IF SO, GET ANOTHER
        ILDB    T1,INBF+1       ;GET WORD FROM BUFFER
        POPJ    P,              ;RETURN TO DATSRH
GETBUF: INPUT DSK,              ;GET BUFFER FROM MONITOR
        STATZ   DSK,740000      ;ERROR WHILE READING FILE?
        JRST    B0              ;IF SO, GO TO B0
        STATZ   DSK,20000       ;END OF FILE?
        JRST    B0              ;IF SO, GO TO B0
        JRST    GET             ;GO TO GET
USRSRH: PUSHJ   P,GET           ;GET A WORD FROM BUFFER
        CAME    T1,[-1]         ;IS WORD A FLAG?
        JRST    [PUSHJ P,GET
                 PUSHJ P,GET
                 JRST  USRSRH]  ;IF NOT, KEEP LOOKING
        PUSHJ   P,GET           ;ELSE, GET PPN
        CAME    T1,%SYSPN       ;CORRECT PPN?
        JRST    [PUSHJ P,GET
                 JRST  USRSRH]  ;IF NOT, KEEP LOOKING
        PUSHJ   P,GET           ;SKIP REST OF RECORD
FILSRH: PUSHJ   P,GET           ;READ A WORD
        CAMN    T1,[-1]         ;IS WORD A FLAG?
        JRST    B0              ;FILE NOT FOUND
        CAME    T1,FILNAM       ;DOES FILE NAME MATCH?
        JRST    [PUSHJ P,GET
                 PUSHJ P,GET
                 JRST  FILSRH]  ;IF NOT, KEEP LOOKING
        PUSHJ   P,GET           ;ELSE, READ NEXT WORD
        LDB T3,[POINT 18,T1,17] ;PUT LEFT HALF IN T3
        HLRZ T4,FILNAM+1        ;GET EXTENTION FROM MEM
        CAME T3,T4              ;DO THEY MATCH?
        JRST    [PUSHJ P,GET
                 JRST  FILSRH]  ;IF NOT, KEEP LOOKING
        LDB T3,[POINT 9,T1,26]  ;GET PROTECTION BITS
        MOVEM T3,%PROT
        PUSHJ   P,GET           ;GET LAST WORD OF DATA
        HLRZM T1,%LICSV         ;SAVE LIC BITS
        HLRZS T1                ;SWITCH AND ZERO
        HRRZ T3,SYSSAV+RLIC     ;GET LOOKUP LIC
        XOR T1,T3       ;T3 TO HAVE THE EXCLUSIV OR OF THE ACS
        MOVEM T1,%DIFLC         ;SAVE THE DIFFERENCES
        MOVE T3,%PROT
        LDB T1,[POINT 9,SYSSAV+RPROT,8] ;GET JUST PROT BITS
        XOR T1,T3       ;T3 IS THE XOR OF THE TWO ACS
        MOVEM T1,%DIFPT         ;SAVE DIFFERENCE OF PROT BITS
        LSH T1,^D27             ;MOVE PROT BITS TO 0-8 POS
        HRR T1,%DIFLC           ;PUT LIC DIFF BIT IN RH
        SKIPE BAKFLG            ;ARE WE COMING FROM BACKUP ROUTINE
        JRST SKPRPT             ;YES SKIP UNNECESSARY REPORTS
        SKIPE T1                ;ANY DISCREPENCIES?
        PUSHJ P,SNDBIT          ;YES SEND TO MASTER
        SKIPE %NOSYS            ;DID WE HAVE A LOOKUP FAILURE?
        PUSHJ P,SNDMSG          ;TELL MASTER
SKPRPT: SKIPN %DIFPT            ;PROTECTION BITS MATCH DON'T MESS
         JRST DOLIC             ;GO CHECK LIC NOW
DOPROT: LDB T1,[POINT 9,%PROT,35]
        DPB T1,[POINT 9,SYSSAV+RPROT,8]
DOLIC:  SKIPN %DIFLC            ;LICENSE OK?
        JRST DOLIC+4            ;YES
        MOVE T3,%LICSV          ;GET LICENSE SAVE IN CORE
        HRRM T3,SYSSAV+RLIC     ;PUT IN RH OF 14TH WORD
        MOVE T3,SYSSAV+RLIC     ;ON EITHER ROUTE PUT LIC IN T3
        POPJ P,                 ;SYSSAV BLOCK NOW SET FOR RENAME

B0:     SKIPN %NOSYS
        JRST B1
        OUTSTR [ASCIZ/FILE NOT IN SYS DIRECTORY OR DATA FILE/]
        PUSHJ P,CRLF
        CLOSE DSK,
        JRST MAIN1               ;CAN'T RUN LIKE THAT
B1:     OUTSTR [ASCIZ/FILE IN SYS/]
        PUSHJ P,CRLF
        RELEASE DSK,
        JRST MAIN1               ;QUIT 

NODAT:  OUTSTR [ASCIZ/DATA FILE ERROR/]
        PUSHJ P,CRLF
        JRST MAIN1


VOWT:   101
        105
        111
        117
        125

;HERE ON INITIAL ENTRY DO NOT DO ANYTHING UNTIL QUALIFIED USER HAS
;BEEN DETERMINED

CKRUN:  INIT TTY,0
        SIXBIT /DSK/
        Z
        HALT CKRUN
        MOVNI T1,LICENSE        ;GETTAB FUNCTION TO RETREIVE USER LICENSE
        GETTAB T1,
         SETZ T1,      ;REFUSED
        TLNN T1,WFLIC   ;NOW LOOKING FOR WRITE FILES LICENSE
        JRST NORUN      ;NOT A QUALIFIED USER
        POPJ P,         ;OK GO AHEAD


PREP:   SETZM %NOSYS            ;START AT 
        MOVE T4,[%NOSYS,,%PROT] ;AND CLEAR THRU
        SETZM FILNM             ;CLEAR FILE NAME
        SETZM FILNM+1           ;AND EXTENTION DON'T WANT SURPRIZES
        SETZM USRN              ;ALSO, USERNAME
        SETZM USRN+1
        SETZM INSSAV            ;FIRST WORD OF SAVE AREA
        MOVE T4,[XWD INSSAV,INSSAV+1]   ;SET START DR OF BLT
        BLT T4,INSSAV+31                ;ZERO SYSSAV AND INSSAV
        SETZM INSLOK+1          ;DON'T ZERO COUNT WORD
        MOVE T4,[XWD INSLOK+1,INSLOK+2]
        BLT T4,INSLOK+RLIC      ;ZERO 13 WORDS
        SETZM SYSLOK+1
        MOVE T4,[XWD SYSLOK+1,SYSLOK+2]
        BLT T4,SYSLOK+RLIC
        POPJ P,                 ;ENOUGH CLEARING NOW GO TO WORK

;RECPPN WILL CONVERT THE USER NAME SENT BY THE MASTER
;TO A PPN AND SAVE IT IN %SYSPN MEMORY LOCATION FOR USE
;THROUGH THE BALANCE OF THE CALLING ROUTINE.

RECPPN: MOVE T1,USRN            ;GET FIRST WORD OF USER NAME
        MOVEM T1,LOKNAM         ;PUT IN LOOKUP BLOCK
        MOVE T1,USRN+1          ;SECOND WORD  OF USER NAME
        MOVEM T1,LOKNAM+1       ;STOW IN SECOND CELL
        MOVEI T1,LOKNAM         ;GET ADDRESS OF LOOKUP BLOCK
        MOVEM T1,NAMDEF+1       ;PUT WHERE WE WILL GET PPN BACK
        LOOKUP FIL,NAMDEF       ;
         NOP                    ;FILE NEVER THERE WILL ALWAYS FAIL
        MOVE T1,NAMDEF+1        ;RECOVER PPN 
        MOVEM T1,%SYSPN         ;MAKE BELEIVE PPN IS THAT OF SYS
        POPJ P,                 ;BACK TO CALLING ROUTINE

NORUN:  OUTSTR [ASCIZ/INCORRECT LICENSE/]
        PUSHJ P,CRLF
        RELEASE FIL,
        RELEASE DSK,
        JRST QUIT

CRLF:   OUTCHI CARRET
        OUTCHI LNFEED
        POPJ P,

QUIT: EXIT
SNDMSG: OUTSTR [ASCIZ/UNABLE TO CLEAR SYSBACKUP DIRECTORY/]
        PUSHJ P,CRLF
        POPJ P,


SNDBIT: OUTSTR [ASCIZ/LICENSE OR PROTECTION DISCREPENCIES/]
        PUSHJ P,CRLF
        LDB T1,[POINT 9,SYSSAV+RPROT,8] ;GET GET PROTECTION BITS FROM LOOKUP
        LSH T1,^D27     ;MOVE TO 0-8 POSITION
        HRR T1,SYSLOK+RLIC  ;GET LICENSE BITS AND SEND TO MASTER
        PUSHJ P,SNDWRD
        POPJ P,

SNDWRD: MOVE T2,[POINT 6,T1]   ;WORD IN T1 TO BE SENT
        MOVEI T3,6
        ILDB C,T2
        OUTSTR C
        SOJG T3,.-2
        POPJ P,



CMDERR: OUTSTR [ASCIZ/ERROR ON LOOKUP OF COMMANDS FILE/]
        PUSHJ P,CRLF
        SETZM %SYSPT
        JRST MAIN1

DATERR: OUTSTR [ASCIZ/UNABLE TO CLEAR SYSBACKUP DIRECTORY/]
        PUSHJ P,CRLF
        JRST MAIN1


SNDERR: OUTSTR [ASCIZ/ERROR RESPONSE FILE NOT IN SYSINST/]
        PUSHJ P,CRLF
        JRST MAIN1

BDEV:   OUTSTR [ASCIZ/UNABLE TO INIT DEVICE/]
        PUSHJ P,CRLF
        JRST MAIN1

ENDFAL: OUTSTR [ASCIZ/ERROR RESPONSE FILE NOT IN SYSINST/]
        PUSHJ P,CRLF
        JRST MAIN1


ERRINT: OUTSTR [ASCIZ/ERROR DURING FILE INITIALIZATION/]
        PUSHJ P,CRLF
        EXIT

DBUF:   BLOCK 203*2     ;TWO DSK BUFFERS FOR COMMANDS FILES
        0
LOKNAM: 0
        0
NAMDEF: 3
        0
        SIXBIT/K/
        0
%SSPPN: 11447,,260005   ;PPN FOR SYSINST
%SYSPN: Z               ;SYS PPN
%BAPPN: 11447,,260002   ;PPN FOR SYSBACKUP
%SYSPT: Z       ;FLAG FOR SYSTEM PROMPT
%NOSYS: Z               ;FLAG FOR NO FILE IN SYS BY NAME GIVEN
%PROT:  Z       ;SAVE LOCATION FOR PROTECTION BITS
OLDLIC: Z       ;PLACE TO SAVE OLD LIC BITS FROM LOOKUP
%DPTBT: Z       ;FLAG TO MARK DIFFERING PROT IN LIC ROUTINE
%LICSV: Z       ;SAVE LOCATION FOR LICENSE BITS
%DIFPT: Z       ;SAVE LOCATION AND FLAG FOR PROTECTION DISCREPINCIES
%DIFLC: Z       ;SAVE LOCATION AND FLAG FOR LIC DISCREPENCIES
%FILPT: Z               ;FLAG FOR FILE PROMT
CMDFLG: Z               ;FLAG FOR COMMAND FILE
REMFLG: Z       ;FLAG TO MARK REMOVE ROUTINE IN SNDMSG ROUTINE
BAKFLG: Z       ;FLAG TO MARK BACKUP AS CALLING ROUTINE
CKSHLD: Z
        Z
USRN:   BLOCK 2         ;TWO WORDS TO USER NAME
FILNAM: BLOCK 2         ;TWO WORDS FOR FILE NAME AND EXT
PROTSV: BLOCK 1         ;FILE PROTECTION DESIRED
DIB:    BLOCK 3         ;HEADER FOR CHECKSUM ROUTINE
DATBLK: BLOCK 3         ;DATE CONVERSION BLOCK


CMDFIL: 3
        0       ;PPN FOR LOOKUP BLOCK
        0       ;FILENAME OF LOOKUP BLOCK
        0       ;EXTENSION FOR LOOKUP BLOCK
        0

INSLOK: 14              ;LOOKUP BLOK FOR CHECKING AND REMOVEL
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
SYSLOK: 14              ;LOOKUP BLOCK FOR INSTALLING INSYS
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0

INSSAV: BLOCK 14
        0
SYSSAV: BLOCK 14
        0
DATNAM: 14      ;LOOKUP BLOCK FOR DATA FILE(TENCRT.DAT)
        11447,,260004   ;SYSADM21
        SIXBIT/TENCRT/
        SIXBIT/DAT/
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0
        0




        END STPT
  9F©