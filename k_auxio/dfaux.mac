title	DFAUX	PTY transparent link program
debug==	0			;=1 to trace special interrupts
minver==12			;version rh
majver==1			;version lh

twoseg
loc	137
xwd	majver,minver
reloc	400000

X==	0			;be careful - for null job
P==	1			;stack pointer
A==	2			;for interrupt routines
B==	3
C==	4
D==	5
TC==	6			;TTY character
PC==	7			;PTY character
T1==	10			;for TTY character interrupts
T2==	11
T3==	12
T4==	13
P1==	14			;for PTY character interrupts
P2==	15
P3==	16
P4==	17

if2 <ifn DEBUG,<printx [debug switch enabled]>>
;;	CALLI's
opdef	CREPTY	[CALLI -141]	;create PTY
    CPNPT%==1			;PTY capacity exceeded
opdef	RETACH	[CALLI -135]	;reattach a port to another frame
opdef	VPEEK	[CALLI -136]	;peek at virtual address

;;	interrupt causes
.IACHR==1			;character received
.IAORG==6			;orange ball received
.IAZAP==7			;circuit zapper received
.IAYEL==11			;yellow ball received
.IAGOB==12			;character gobbler received
.IATCC==13			;terminal characteristic changed in PTY
.IANTQ==13			;notice to quit

;;	auxcal function codes
.AXI8C==0			;input image character and wait
.AXI8S==1			;input image character and skip
.AXO8==	3			;output image character
.AXCBI==6			;clear input buffer
.AXCBO==7			;clear output buffer (send gobbler)
.AXLDE==11			;leave deffered echo
.AXSYB==12			;send yellow ball
.AXCFS==64			;change file status bits for port
.AXSOB==70			;send orange ball
.AXTCN==73			;read/set term char by number
.AXTCP==74			;read changed term char from PTY
.AXOSF==76			;output string as will fit

;;	frmop function codes
.FOCFM==1			;create frame
    CF.FCE==1			;frame capacity exceeded

;;	gettab table numbers
.GTLOG==-24			;LDBLOG(U)
.GTLIC==-20			;JBTLIC(J)
.GTPRV==6			;JBTPRV(J)
.GTTTY==10			;TTYTAB(J)
.GTSLF==23			;NUMTAB(I)

;;	fatal error message macro
define	fatal (M)	<
	jrst	[reset
		outstr [asciz "M"]
		exit
		]	>

;;	debug error message macro
ifn	debug,<define	bug (M)	<outstr [asciz "[M]"]>>
ife	debug,<define	bug (M)	<;>>

subttl	create frame and PTYs
;	* setup translations (?RPGSW)
;	* setup initial escape char
;	* initialize the stack
;	* create PTY
;	* create frame
;	* set frame license
;	* initialize PTY terminal characteristics
;	* set mode on our end of PTY
;	* attach PTY to frame
;	* set mode on command port

array	PDL[40]			;stack

START:	TDZA	P,P		;remember entry point
	SETO	P,		; 0=normal, -1=rpg
	SETCA	P,		;  invert for testing!!!
	MOVEM	P,CHRTRN	;save it.

	SETOM	CHRVRB#		;use verbose messages
	MOVEI	P,"X"-100	;use ^X as initial break character
	MOVEM	P,CHRESC	; for user commands

	MOVE	P,[iowd 40,PDL]	;init stack pointer

	SETZ	T1,		;create PTY pair
	CREPTY	T1,
	 JRST	[CAIN	T1,CPNPT%
		  fatal	<?PTY capacity exceeded>
		fatal	<?error creating PTYs>
		]
	MOVEM	T1,PP#		;PTY port #
	SETOM	TT#		;TTY port #

	MOVSI	T1,.FOCFM	;create frame with default parameters
	FRMOP	T1,[0]
	 JRST	[CAIN	T1,CF.FCE
		  fatal	<?frame capacity exceeded>
		fatal	<?error creating frame>
		]
	MOVEM	T1,FD#
	
	HRROI	T1,.GTLIC	;copy frame license
	GETTAB	T1,		;get our license
	 JRST	LICDON
	MOVE	T2,[.GTLIC,,.GTSLF]
	GETTAB	T2,		;get address of JBTLIC
	 JRST	LICDON
	MOVE	T3,FD		;get child frame number
	ANDI	T3,7777		;mask off abs frame number
	ADD	T2,T3		;T2/ JBTLIC+(frame#)
	TLO	T2,(1B0)	;virtual memory reference
	MOVEI	T3,T1		;arg block at T1
	POKE	T3,
	 JRST	LICDON
	bug <LIC>
	SKIPA
LICDON:	 OUTSTR [asciz "[JBTLIC not patched]
"]
	;; here set LDBLOG on PTY's LDB
	MOVE	A,[.GTLOG,,.GTSLF]
	GETTAB	A,
	 JRST	NOLOG
	HRRZM	A,B		;(B)=GETLOG temporarily
	HRLI	A,(1b0)
	VPEEK	A,
	 JRST	NOLOG
	HLRZ	T1,A
	CAIE	T1,(PUSHJ 1,)
	 fatal	<?GETLOG not a PUSHJ P,>
	ADDI	A,4		;offset into LINCHK of LINTAB
	HRLI	A,(1b0)
	VPEEK	A,
	 JRST	NOLOG
	HLRZ	T1,A
	CAIE	T1,(<SKIPA 5,(5)>)
	 fatal	<?CHKLIN+4 not a SKIPA U,(U)>
	HRRZM	A,C		;(C)=LINTAB
	ADDI	B,2		;offset from GETLOG of LDBLOG
	HRLI	B,(1b0)
	VPEEK	B,
	 JRST	NOLOG
	HLRZ	T1,B
	CAIE	T1,(<SKIPA 6,(5)>)
	 fatal	<?GETLOG+2 not a SKIPA T1,(U)>
	HRRZM	B,B		;(B)=LDBLOG
	HRRZ	T1,PP		;port number
	ADD	T1,C		;+LINTAB
	HRLI	T1,(1b0)
	VPEEK	T1,
	 JRST	NOLOG
	TLNE	T1,-1
	 fatal	<?left half of LINTAB(port) non-zero>
	ADD	T1,B		;(T1)=LDBLOG(U)
	HRLI	T1,(1b0)
	MOVE	T2,[17b11!1b16!77b19!77b27!177b35]
	HRROI	T3,.GTLOG
	HRL	T3,TT
	GETTAB	T3,
	 JRST	NOLOG
	AND	T3,T2
	MOVE	A,T1
	VPEEK	A,
	 JRST	NOLOG
	HRROI	B,.GTLOG
	HRL	B,PP
	GETTAB	B,
	 JRST	NOLOG
	CAME	A,B
	 fatal	<?.GTLOG(p) neq LDBLOG(u)>
	MOVEI	A,T1
	BITPOK	A,
	 JRST	NOLOG
	bug <LOG>
	SKIPA
NOLOG:	 OUTSTR	[asciz "[LDBLOG not patched]
"]
	HRROI	T1,.AXTCN	;copy terminal characteristics
	HRL	T1,TT
	HRROI	T2,.AXTCN	
	HRL	T2,PP
	SETZ	T3,		;start with characteristic 0
TCINI:	HLLOS	T3		;read characteristics
	AUXCAL	T1,T3		;get characteristic from real TTY
	 JRST	TCDON		;no more characteristics
	AUXCAL	T2,T3		;set it on other end of PTY
	 fatal	<?cant set terminal characteristic>
	ADD	T3,[1,,0]	;incriment terminal char #
	JRST	TCINI
TCDON:

	HLL	T1,PP		;set mode on our end of PTY
	HRRI	T1,.AXCFS	;change file status for port
	AUXCAL	T1,214		;image binary no echo

	HRRO	T1,FD		;move PTY to child
	HRLI	T2,(2B2)	;attach in command mode
	HRR	T2,PP		;his port number
	MOVEI	T3,T1		;address of arg block
	RETACH	T3,
	 fatal	<?cant retach PTY to child frame>
	
	HRROI	T1,.AXCFS	;change file status for port
	HRL	T1,TT
	AUXCAL	T1,214		;image binary no echo

	HRROI	A,.AXTCP	;get next characteristic
	HLL	A,PP
TCCCLR:	AUXCAL	A,B
	 fatal	<?cant read terminal characteristics from PTY>
	JUMPGE	B,TCCCLR	;more?

subttl	initialize interrupt system
;	* reset and enable INTADR system
;	* assign causes to channels
;	* enable all channels

	MOVE	T1,[VECDAT,,VECTOR]	;load the vector
	BLT	T1,VECTOR+VECLEN-1

	MOVE	T1,[6,,VECTOR]		;clear and enable system
	INTADR	T1,
	 fatal	<?cant initialize INTADR system>
	
	MOVE	T1,[.IANTQ,,NTQ..C]	;NTQ 
	INTASS	T1,
	 fatal	<?cant assign NTQ>

	HRLOI	T1,<.IAZAP>B26+ZAP..C	;PTY ZAP
	HLR	T1,PP
	TINASS	T1,
	 fatal	<?cant assign PTY ZAP>

	HRLOI	T1,<.IAORG>B26+ORG.TC	;ORANGE BALL from TTY 
	HRR	T1,TT
	TINASS	T1,
	 fatal	<?cant assign ORANGE BALL for TTY>

	HRLOI	T1,<.IAORG>B26+ORG.PC	;ORANGE BALL from PTY 
	HLR	T1,PP
	TINASS	T1,
	 fatal	<?cant assign ORANGE BALL for PTY>

	HRLOI	T1,<.IAYEL>B26+YEL.TC	;YELLOW BALL from TTY 
	HRR	T1,TT
	TINASS	T1,
	 fatal	<?cant assign YELLOW BALL for TTY>

	HRLOI	T1,<.IAYEL>B26+YEL.PC	;YELLOW BALL from PTY 
	HLR	T1,PP
	TINASS	T1,
	 fatal	<?cant assign YELLOW BALL for PTY>

	HRLOI	T1,<.IAGOB>B26+GOB.TC	;GOBBLER from TTY
	HRR	T1,TT
	TINASS	T1,
	 fatal	<?cant assign GOBBLER for TTY>

	HRLOI	T1,<.IAGOB>B26+GOB.PC	;GOBBLER from PTY
	HLR	T1,PP
	TINASS	T1,
	 fatal	<?cant assign GOBBLER for PTY>

	HRLOI	T1,<.IATCC>B26+TCC.PC	;TERM CHAR from PTY
	HLR	T1,PP
	TINASS	T1,
	 fatal	<?cant assign TERM CHAR for PTY>

	HRLOI	T1,<.IACHR>B26+CHR.TC	;CHAR from TTY
	HRR	T1,TT
	TINASS	T1,
	 fatal	<?cant assign CHAR for TTY>

	HRLOI	T1,<.IACHR>B26+CHR.PC	;CHAR from PTY
	HLR	T1,PP
	TINASS	T1,
	 fatal	<?cant assign CHAR for PTY>

	OUTSTR	[asciz "[display passthrough created]"]
	OUTSTR	[byte (7)15,12]

	SETO	X,			;enable all interrupt channels
	INTENB	X,
	 fatal	<?cant enable interrupt channels>

WAIT:	SETZ	X,		;don't wake up for anything
	HIBER	X,
	 JRST	WAIT
	JRST	WAIT

subttl	interrupt routines for special balls

NTQ..:	bug <NTQ..>
	SETOM	CHRNTQ#		; set flag - handled elsewhere
	DISMIS

ZAP..:	bug <ZAP..>
	MOVEI	2
	SLEEP
	OUTSTR	[asciz "
[display passthrough deleted]"]
	EXIT	1,
	JRST	.-1

ORG.T:	PUSH	P,A
	bug <ORG.T>
	HRROI	A,.AXSOB	;send orange ball to PTY
	HLL	A,PP
	AUXCAL	A,
	POP	P,A
	DISMIS

ORG.P:	PUSH	P,A
	bug <ORG.P>
	HRROI	A,.AXSOB	;send orange ball to TTY
	HRL	A,TT
	AUXCAL	A,
	POP	P,A
	DISMIS

YEL.T:	PUSH	P,A
	bug <YEL.T>
	HRROI	A,.AXSYB	;send yellow ball to PTY
	HLL	A,PP
	AUXCAL	A,
	POP	P,A
	DISMIS

YEL.P:	PUSH	P,A
	bug <YEL.P>
	HRROI	A,.AXSYB	;send yellow ball to TTY
	HRL	A,TT
	AUXCAL	A,
	POP	P,A
	DISMIS

GOB.T:	PUSH	P,A
	HRROI	A,.AXCBO	;send gobbler to PTY
	HLL	A,PP
	AUXCAL	A,
	POP	P,A
	bug <GOB.T>
	DISMIS

GOB.P:	PUSH	P,A
	HRROI	A,.AXCBO	;send gobbler to TTY
	HRL	A,TT
	AUXCAL	A,
	POP	P,A
	bug <GOB.P>
	DISMIS

subttl	interrupt routines for terminal characteristics
;	no TCC.T because can't do anything with them

TCC.P:	PUSH	P,A
	PUSH	P,B
TCC.P0:	HRROI	A,.AXTCP	;get next characteristic
	HLL	A,PP
	AUXCAL	A,B
	 fatal	<?cant read terminal characteristics from PTY>
	JUMPL	B,TCC.P1	;none left?
	bug <TCC.P>
	HRROI	A,.AXTCN
	HRL	A,TT
	AUXCAL	A,B		;go set it on real TTY:
	 fatal	<?cant set terminal characteristic on TTY>
	JRST	TCC.P0
TCC.P1:	POP	P,B
	POP	P,A
	DISMIS

subttl	interrupt routines for terminal originated traffic
;	T1-T4 are reserved for these routines

CHR.T:	HRROI	T1,.AXI8S	;get next character
	HRL	T1,TT
	AUXCAL	T1,TC
	 DISMIS			;no more characters
	ANDI	TC,177		;strip off parity
	SKIPE	CHWAIT#		;waiting?
	 JRST	CHR.TX		;yes, handle it
	CAMN	TC,CHRESC	;escape character?
	 JRST	[SETOM CHWAIT	;yes, handle it
		 SKIPE CHRVRB	;  verbose messages?
		  OUTSTR [ASCIZ\
<eXc>: \]
		 JRST  CHR.T]	;   and get more
CHR.T0:	HRROI	T1,.AXO8	;no, output to PTY 
	HLL	T1,PP
	AUXCAL	T1,TC
	SKIPE	CHRNTQ		; ntq?
	 OUTSTR	[ASCIZ\
[display passthrough received notice to quit - please log off]
\]
	JRST	CHR.T

CHR.TX:	CAMN	TC,CHRESC	;escape character?
	 JRST	[SETZM CHWAIT	;yes, clear flag
		 SKIPE	CHRVRB		;verbose?
		 OUTSTR [ASCIZ\<eXc>\]	;print the character
		 JRST  CHR.T0]	;  and send it
	SKIPE	CHRVRB		;verbose?
	 OUTCHR	TC		;print the character
	CAIE	TC,"D"		;disable command?
	CAIN	TC,"d"		; ...
	 JRST	[SETZM CHRTRN	;  disable translate flag
		 JRST  CHR.T9]	;  and return
	CAIE	TC,"E"		;enable command?
	CAIN	TC,"e"		; ...
	 JRST	[SETOM CHRTRN	;  enable translate flag
		 JRST  CHR.T9]	;  and return
	CAIN	TC,"?"		;help command?
	 MOVEI	TC,"H"		;  sort of...
	CAIE	TC,"H"		;help command?
	CAIN	TC,"h"		; ...
	 JRST	[OUTSTR CHRHLP	;  print message
		 JRST   CHR.TD]	;  and return
	CAIE	TC,"Q"		;quit command?
	CAIN	TC,"q"		; ...
	 JRST	ZAP..		;  so act like a circuit zap
	CAIE	TC,"T"		;toggle command?
	CAIN	TC,"t"		; ...
	 JRST	[SETCMM CHRTRN	;  toggle translate flag
		 JRST	CHR.T9]	;  and return
	CAIE	TC,"V"		;toggle verbosity?
	CAIN	TC,"v"		; ...
	 JRST	[SETCMM CHRVRB	;  toggle verbosity flag
		 SKIPN	CHRVRB	; turned on?
		 OUTSTR	[ASCIZ \
[verbose messages disabled]
\]				; added crlf for "off"
		 SKIPE	CHRVRB	; turned off?
		 OUTSTR	[ASCIZ \
[verbose messages enabled]\]
		 JRST	CHR.TD]	;  and return
	CAIE	TC,"X"		;eXcape command?
	CAIN	TC,"x"		; ...
	 JRST	[SKIPE CHRVRB	;  verbose messages?
		 OUTSTR [ASCIZ\
 eXcape character: \]
		 HRROI T1,.AXI8C
		 AUXCAL T1,TC
		  MOVEI	TC,"X"-100 ; if detached???
		 ANDI  TC,177	; remove parity
		 MOVEM TC,CHRESC
		 SKIPE CHRVRB	;  verbose messages?
		 OUTSTR [ASCIZ \ [ok]\]
		 JRST	CHR.TD]	;  and return
	SKIPN	CHRVRB		; verbose?
	 OUTCHI	7		;  no - beep
	SKIPE	CHRVRB		; verbose?
	 OUTSTR [ASCIZ\ illegal command - sequence aborted.\]

CHR.TD:	SETZM	CHWAIT		;clear waiting flag
	SKIPE	CHRVRB		;verbose?
	 OUTSTR	[ASCIZ\
\]
	JRST	CHR.T		; and look for more

CHR.T9:	SKIPN	CHRVRB		; verbose?
	  JRST	CHR.TD		; no -- just return
	SKIPN	CHRTRN		; turned on?
	 OUTSTR	[ASCIZ \
[display functions disabled]\]
	SKIPE	CHRTRN		; turned on?
	 OUTSTR	[ASCIZ \
[display functions enabled]\]
	JRST	CHR.TD
	 
CHRHLP:	ASCIZ \

[Passthrough commands]:

 <eXc>H - Type this message (also ?)
 <eXc>E - Enable display functions
 <eXc>D - Disable display functions
 <eXc>T - Toggle display functions
 <eXc>V - Verbosity flag toggle
 <eXc>X - Xchange eXcape character
 <eXc>Q - Quit leaving PTY job detached
 <eXc><eXc>  Pass an <eXc> to the PTY

\

subttl	interrupt routines for PTY originated traffic
;	P1-P4 are reserved for these routines

CHR.P:	HRROI	P1,.AXI8S	;get next character
	HLL	P1,PP
	AUXCAL	P1,PC
	 DISMIS			;no more characters
	HRROI	P1,.AXO8	;output character
	HRL	P1,TT
	CAIL	PC," "		;control character
	 JRST	CHR.P0		;no, continue
	SKIPN	CHRTRN		;do we want translations
	 JRST	CHR.P0		;no, continue
	CAIN	PC,12		;known format character
	 AUXCAL	P1,PC		;  lf -- printed as <lf>^J
	CAIN	PC,15		;  cr -- printed as ^M<cr>
	 SETOM	CHRCRF#		; yes, set flag
	MOVEI	PC,"@"(PC)	; make it ascii
	AUXCAL	P1,["^"]	; with a ^ in front
CHR.P0:	AUXCAL	P1,PC
	SKIPN	CHRTRN		;translations?
	 JRST	CHR.P		; no, continue
	SKIPE	CHRCRF		; was it a <cr>?
	 AUXCAL	P1,[15]		;  yes -- print as ^M<cr>
	SETZM	CHRCRF		;  only once per customer
	JRST	CHR.P		;try again

subttl	define interrupt vector and channel numbers

VECDAT:	;; interrupt vectors
define	INTDEF (x) < irp x <
EXP 0,x				;;load vector table
x'C==<.-VECDAT>/2		;;define interrupt channel number
>>
intdef	<GOB.T,GOB.P>
intdef	<CHR.T,ORG.T,YEL.T>
intdef	<TCC.P>
intdef	<CHR.P,ORG.P,YEL.P>
intdef	<ZAP..>
intdef	<NTQ..>
VECLEN==.-VECDAT

lit
reloc	0
var
CHRTRN:	block 1			; flag for translation "on"
CHRESC:	block 1			; character to use for escape
VECTOR:	block ^D72		; interrupt vector space

end	START
   