TITLE RTYMAC - VERSION 05/27/75 - - ROYALTY ACCOUNTING ROUTINE

;PROGRAM MUST BE SAVED WITH HOME FILES LICENSE.

        ENTRY  RTYUUO, RTYDAT, RTYTRU, RTYJOB
        ENTRY  RTYNOD, RTYPPN, RTYFPN, RTYLIC
        ENTRY   RTYINT, RTYLKP, RTYENT, RTYCNM, RTYINP, RTYOUT
        ENTRY   RTYUSI, RTYUSO, RTYSIO, RTYRLS, RTYMSG, RTYMTH
        ENTRY   RTYDSK, RTYCR
        EXTERN  JOBFF, JOBREL, RTYSIM, RTYERR, DYNDV.
        INTERN RTYDYN
RTYDYN=DYNDV.

RTYUUO: TYMCHG  1,
        JRST    [ MOVEI 1,3
                  JSP   17,RTYERR ]
        JRST    @17

RTYDAT: MOVE    1, [ XWD 11,11 ]
        GETTAB  1,                      ;GET GMT DATE
        JRST    [ MOVEI 1,4
                  JSP   17,RTYERR ]
        MOVE    2, [ XWD 10,11 ]
        GETTAB  2,                      ;GET GMT JIFFIES
        JRST    [ MOVEI 1,5
                  JSP   17,RTYERR ]
        JRST    @17

RTYTRU: SETZ    1,              ;SET TO CURRENT JOB
        CALLI   1,27                    ;GET TRU
        JRST    @17

RTYJOB: SETZ    1,              ;SET TO CURRENT JOB
        CALLI   1,30                    ;GET JOB NUMBER
        JRST    @17


RTYNOD: MOVNI   3,24    ;GETTAB FOR NODE/PORT INFO
        GETTAB  3,
        JRST    [ SETZ  1,
                  SETZ  2,
                  JRST  @17 ]
        LDB     1, [ POINT 6,3,27 ]
        LDB     2, [ POINT 6,3,19 ]
        DPB     2, [ POINT 6,1,29 ]
        LDB     2, [ POINT 7,3,35 ]
        JRST    @17

RTYPPN: MOVNI   1,23
        GETTAB  1,              ;GET THIS JOB'S PPN
        JRST    [ MOVEI 1,^D8
                  JSP   17,RTYERR ]
        JRST    @17             ;RETURN

RTYFPN: MOVNI   1,25
        GETTAB  1,              ;GET PPN OF WHERE PROGRAM CAME FROM
        MOVEI   1,0             ;ZERO ON ERROR RETURN
        JRST    @17             ;RETURN

RTYLIC: MOVNI   1,20
        GETTAB  1,              ;READ LICENSE
        MOVEI   1,0             ;ZERO ON ERROR RETURN
        JRST    @17             ;RETURN

RTYINT: XOR     1, [ OPEN 0,SPEC ]        ;(1)=CHANNEL
        XCT     1
        JRST    [ MOVEI 1,7
                  JSP   17,RTYERR ]
        JRST    @17

SPEC:   EXP     17              ;DUMP MODE
SPEC1:  SIXBIT  /DSK/
        XWD     0,0

RTYLKP: XOR     1, [ LOOKUP 0,0 ] ;(1)=CHANNEL
        XOR     1,2     ;(2)=ADDRESS
        XCT     1
        JRST    [ MOVNI 1,1
                  JRST  @17 ]
        MOVEI   1,0
        JRST    @17

RTYENT: XOR     1, [ ENTER 0,0 ]
        XOR     1,2
        XCT     1
        JRST    [ MOVNI 1,1
                  JSP   @17 ]
        MOVEI   1,0
        JRST    @17

RTYCNM: SIXBIT  /RTYCTL/
        SIXBIT  /DAT/
        0
        XWD     4,125552        ;PPN FOR (UAS)

RTYSIO: SETZM   0,E+1
        MOVNS   1,1
        HRLM    1,E
        SUBI    2,1
        HRRM    2,E
        JRST    @17

E:      0
        0

RTYINP: MOVE    2,1
        XOR     2, [ INPUT 0,E ]
        XCT     2
        XOR     1, [ STATZ 0,340000 ]
        XCT     1
        JRST    [ MOVEI 1,^D11
                  JSP   17,RTYERR ]
        JRST    @17

RTYOUT: MOVE    2,1
        XOR     2, [ OUTPUT 0,E ]
        XCT     2
        XOR     1, [ STATZ 0,740000 ]
        XCT     1
        JRST    [ MOVEI 1,^D12
                  JSP   17,RTYERR ]
        JRST    @17

RTYUSI: XOR     1, [ USETI 0,0 ]
        XOR     1,2
        XCT     1
        JRST    @17

RTYUSO: XOR     1, [ USETO 0,0 ]
        XOR     1,2
        XCT     1
        JRST    @17

RTYRLS: MOVE    2,1
        XOR     2, [ CLOSE 0,0 ]
        XCT     2
        XOR     1, [ RELEASE 0,0 ]
        XCT     1
        JRST    @17

RTYMSG: TTCALL  3,(1)
        JRST    @17

MTH:    SIXBIT  /JANFEB/
        SIXBIT  /MARAPR/
        SIXBIT  /MAYJUN/
        SIXBIT  /JULAUG/
        SIXBIT  /SEPOCT/
        SIXBIT  /NOVDEC/

RTYMTH: MOVE    2,1
        SUBI    2,1
        IDIVI   2,2
        HLRZ    1,MTH(2)
        JUMPE   3,.+2
        HRRZ    1,MTH(2)
        JRST    @17

RTYDSK: MOVE    1,SPEC1
        JRST    @17

RTYCR:  TTCALL  3, [ ASCIZ /









/]
        JRST    @17

        END
