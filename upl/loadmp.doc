














                            MAGNUM LOAD/DUMP UTILITY
                            ========================


                                       by

                                Dennis L. Allen






          One of the more tedious tasks  of  a  schema  revision  that
          changes  the storage requirements of a populated relation is
          reconciliation.  To facilitate the construction of DUMP  and
          LOAD  procedures a LOAD/DUMP Generator has been added to the
          UPL.  Three files are involved:

               (UPL) LOADMP.CMD - MAGNUM Inport commands file
               (UPL) LOADMP.TXT - MAGNUM Procedure source
               (UPL) LOADMP.DOC - This document


          Due to the Command syntax used in the  Inport  file,  MAGNUM
          version  11.00  or  higher  is  required.   The  Inport file
          creates  three  .TMP  files  containing  output   from   the
          following MAGNUM DIRECTORY commands:

               DIR ALL (to obtain hash names)
               DIR RELATIONS SIZE (detect unpopulated relation)
               DIR RELATIONS WITH FIELDS FULL (relation structure)


          The GENERATOR then loads data  from  these  .TMP  files  and
          compiles  a  list  of populated relations.  The USER is then
          asked if all populated relations are to be included  in  the
          generated  code.  A reply of 'Y' will start generation using
          all populated relations.  A reply of  'N'  will  initiate  a


                             - - - - - - - - - - -
               Comments and suggestions are welcome.   I  can  be
               reached   by   IMS  to  MEDMAIL  on  H8  attention
               Dennis L. Allen or by phone at (201) 825-1100 x241
               (9 to 5 Eastern time).
                                                                          Page 2


          conversational exchange listing the populated relations  for
          a  'Y'  or  'N' response from the USER.  This exchange works
          the same way as that used by the MAGNUM INSTALL command.


          The text files that are generated should be modified by  the
          USER before use as required by the type of reconciliation to
          be  performed.   For  example;   If  the  contents  of   two
          different  relations  are  to be combined into one relation,
          then the LOAD routine for the first relation  should  remain
          as  generated  while the code for the second relation should
          be changed from a simple INSERT into  a  SELECT  THEN  ALTER
          combination.   The  purpose  of the GENERATOR is to automate
          the non creative portion  of  the  resultant  procedure  (ie
          constructing  the  FORMAT, REPORT, and SOURCE constructs for
          many relations).


          The output from the DUMP procedure goes to files whose  EXEC
          names  are  built from the MAGNUM hash name for the relation
          with an extension of '.DTA'.  This  method  allows  multiple
          copies  of  the  DUMP/LOAD  texts  to  be  generated without
          concern about duplicate file names that  would  occur  if  a
          numbering scheme had been used.  The GENERATOR is also ideal
          for  creating  LOAD  and   DUMP   procedures   for   offline
          maintenance of relational data using the capabilities of the
          EDITOR.


          The following pages contain a sample session  and  the  text
          files  produced during that session.  The schema 'CAMERA' of
          MAGNUM Primer fame was used for this sample.  The problem to
          be  solved in this sample was the need to change the picture
          of the field 'CUST.NO'  from  '5N'  to  '5C'  to  allow  for
          expansion  of  the  number  of  customers  on  file  without
          re-designing report layouts.
                                                                          Page 3


                            SAMPLE TERMINAL SESSION
                         (Terminal Input is Underlined)

          -FTMAGNUM CAMERA           _______________
          4-NOV-1979  21:16:20

          :DO FILE (UPL)LOADMP.CMD           _______________________
          RELFLD.TMP File Created.
          XYXY1234 Deleted.

          BEGIN DATA LOAD

          DO YOU WISH TO INCLUDE ALL POPULATED RELATIONS ? N                                                           _
          RESPOND TO EACH RELATION NAME WITH Y TO INCLUDE OR N TO BYPASS
          CUSTOMER ? Y                     _
          OFFICE ? N                   _
          PAYMENT.HISTORY ? Y                            _
          PRODUCT ? N                    _
          REORDER ? N                    _
          SALES.ORDER ? Y                        _
          SALES.SUMMARY ? N                          _
          SALESMAN ? N                     _
          SHIPMENT ? N                     _
          SHIPPING.HISTORY ? N                             _
          SO.LINE.ITEM ? N                         _
          SUPPLIER ? N                     _
          SUPPLIER.PRODUCT ? N                             _
          GENERATING TEXT FOR DUMP ROUTINE TO FILE DUMP.TXT
          GENERATING TEXT FOR LOAD ROUTINE TO FILE LOAD.TXT

          :RUN DUMP.RELATION.DATA FROM DUMP.TXT NO SAVE           ____________________________________________
          COMPILING DUMP.RELATION.DATA
          SYNTAX DONE
          SEMANTICS DONE
          EXECUTABLE DUMP.RELATION.DATA CREATED
          LOADING DUMP.RELATION.DATA
          EXECUTION

          DUMPING CUSTOMER RELATION
          DUMPING PAYMENT.HISTORY RELATION
          DUMPING SALES.ORDER RELATION

          EXECUTION COMPLETED

          :MOD CAMERA           __________
          6075 Chars
          *'CUST.NO'E           __________
                    CUST.NO               AS '5N',
                    CUST.NO               AS '5C',                                               _
          *EX           __
          CAMERA
          6075 Chars
          4-NOV-1979  21:22:03

          :DELETE RELATION CUSTOMER CURRENT           ________________________________
          CUSTOMER Deleted
          :DELETE RELATION PAYMENT.HISTORY CURRENT           _______________________________________
          PAYMENT.HISTORY Deleted
                                                                          Page 4


          :DELETE RELATION SALES.ORDER CURRENT           ___________________________________
          SALES.ORDER Deleted

          :COM SCHEMA CAMERA           _________________
          COMPILING
          SYNTAX DONE
          SEMANTICS DONE
          SCHEMA CAMERA COMPILES

          The following fields are declared differently:

              CUST.NO
                Picture has changed from "5NV" to "5C".


          The following relations are declared differently:

              SALES.ORDER
                CUST.NO is declared differently.

          Storage requirements for SALES.ORDER are different.

              CUSTOMER
                CUST.NO is declared differently.

          Storage requirements for CUSTOMER are different.

              PAYMENT.HISTORY
                CUST.NO is declared differently.

          Storage requirements for PAYMENT.HISTORY are different.

          The following compiled procedures refer to fields or
          relations that are no longer declared or are declared
          differently:

              SALES.RPT
                Compiled form of SALES.RPT will be deleted.
              PROCESS.SALES.ORDER
                Compiled form of PROCESS.SALES.ORDER will be deleted.
              NEW.SALES
                Compiled form of NEW.SALES will be deleted.

          Ready to perform following actions for new schema creation:
              Outdated compiled procedures will be deleted.
          CONTINUE?   Y                      _

              PROCEDURE SALES.RPT deleted

              PROCEDURE PROCESS.SALES.ORDER deleted

              PROCEDURE NEW.SALES deleted

          SCHEMA CAMERA CREATED

          :MOD FILE LOAD.TXT           _________________
          1430 Chars
          *FIND'CUST.NO'S           ______________
          "5C" for "5NV"           __       ___
                                                                          Page 5


          Wait? N                _
          3
          *EX           __
          LOAD.TXT
          1425 Chars
          4-NOV-1979  21:24:39

          :RUN LOAD.RELATION.DATA FROM LOAD.TXT NO SAVE           ____________________________________________
          COMPILING LOAD.RELATION.DATA
          SYNTAX DONE
          SEMANTICS DONE
          EXECUTABLE LOAD.RELATION.DATA CREATED
          LOADING LOAD.RELATION.DATA
          EXECUTION

          LOADING CUSTOMER RELATION
          LOADING PAYMENT.HISTORY RELATION
          LOADING SALES.ORDER RELATION

          EXECUTION COMPLETED

          :Q           _
          4-NOV-1979  21:28:26

          EXIT
                                                                          Page 6


          PROCEDURE DUMP.RELATION.DATA
          BEGIN

          %  CUSTOMER  %
          REPORT VMBAKD.RPT TO "VMBAKD.DTA-A" PAGE.SIZE 0 LINE.LENGTH 500
          FORMAT VMBAKD.FIELDS
              CUST.NO,",",
              SLSMAN.NO,",",
              PREV.BAL,",",
              '"',TRIM(CUST.NAME),'"',",",
              '"',TRIM(CUST.ADDR),'"',",",
              '"',TRIM(CUST.CITY),'"',",",
              '"',TRIM(CUST.STATE),'"',",",
              CUST.ZIP,",",
              '"',TRIM(SHIP.NAME),'"',",",
              '"',TRIM(SHIP.ADDR),'"',",",
              '"',TRIM(SHIP.CITY),'"',",",
              '"',TRIM(SHIP.STATE),'"',",",
              SHIP.ZIP,",",
              @CR

          %  PAYMENT.HISTORY  %
          REPORT KP92OO.RPT TO "KP92OO.DTA-A" PAGE.SIZE 0 LINE.LENGTH 500
          FORMAT KP92OO.FIELDS
              CUST.NO,",",
              PAYMENT.DATE,",",
              PAYMENT.AMT,",",
              '"',TRIM(CUST.CHECK.NO),'"',",",
              PAYMENTS.POSTED.FLAG,",",
              @CR

          %  SALES.ORDER  %
          REPORT J51608.RPT TO "J51608.DTA-A" PAGE.SIZE 0 LINE.LENGTH 500
          FORMAT J51608.FIELDS
              SALES.ORD.NO,",",
              CUST.NO,",",
              SO.ORD.DATE,",",
              TOTAL.PRICE,",",
              @CR

          TYPE TO TERMINAL "DUMPING CUSTOMER RELATION",@CR
          FOR EACH CUSTOMER WRITE REPORT VMBAKD.RPT
           PRINT TO VMBAKD.RPT VMBAKD.FIELDS

          TYPE TO TERMINAL "DUMPING PAYMENT.HISTORY RELATION",@CR
          FOR EACH PAYMENT.HISTORY WRITE REPORT KP92OO.RPT
           PRINT TO KP92OO.RPT KP92OO.FIELDS

          TYPE TO TERMINAL "DUMPING SALES.ORDER RELATION",@CR
          FOR EACH SALES.ORDER WRITE REPORT J51608.RPT
           PRINT TO J51608.RPT J51608.FIELDS

          END
                                                                          Page 7


          PROCEDURE LOAD.RELATION.DATA
          BEGIN

          %  CUSTOMER  %
          SOURCE VMBAKD.SRC FROM "VMBAKD.DTA" FREE FORM LINE.LENGTH 500
            INPUT
              CUST.NO as "5NV",
              SLSMAN.NO as "5NV",
              PREV.BAL as "5ZN.2ZCR",
              CUST.NAME as "40C",
              CUST.ADDR as "30C",
              CUST.CITY as "30C",
              CUST.STATE as "15C",
              CUST.ZIP as "5NV",
              SHIP.NAME as "40C",
              SHIP.ADDR as "30C",
              SHIP.CITY as "30C",
              SHIP.STATE as "15C",
              SHIP.ZIP as "5NV",
              @CR

          %  PAYMENT.HISTORY  %
          SOURCE KP92OO.SRC FROM "KP92OO.DTA" FREE FORM LINE.LENGTH 500
            INPUT
              CUST.NO as "5NV",
              PAYMENT.DATE as date "YYMMDD",
              PAYMENT.AMT as "5ZN.2Z",
              CUST.CHECK.NO as "10C",
              PAYMENTS.POSTED.FLAG as "NV",
              @CR

          %  SALES.ORDER  %
          SOURCE J51608.SRC FROM "J51608.DTA" FREE FORM LINE.LENGTH 500
            INPUT
              SALES.ORD.NO as "6NV",
              CUST.NO as "5NV",
              SO.ORD.DATE as date "YYMMDD",
              TOTAL.PRICE as "5ZN.2Z",
              @CR

          TYPE TO TERMINAL "LOADING CUSTOMER RELATION",@CR
          FOR EACH VMBAKD.SRC
           BEGIN
            INSERT INTO CUSTOMER
           END

          TYPE TO TERMINAL "LOADING PAYMENT.HISTORY RELATION",@CR
          FOR EACH KP92OO.SRC
           BEGIN
            INSERT INTO PAYMENT.HISTORY
           END

          TYPE TO TERMINAL "LOADING SALES.ORDER RELATION",@CR
          FOR EACH J51608.SRC
           BEGIN
            INSERT INTO SALES.ORDER
           END

          END
    