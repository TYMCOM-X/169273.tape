	SUBTTL	MA355/JDS/DRH/MP/RCM/VB		15-JUN-76

; COPYRIGHTS 1972,1973,1976,1977 STEVENS INSTITUTE OF TECHNOLOGY
; HOBOKEN, NEW JERSEY, 07030
; ALL RIGHTS RESERVED.

  ;UNIVERSALS

	SEARCH	MACTEN		;DEC-10 COMMON MACROS
	SEARCH	SNOPRM		;SITBOL PARAMETER FILE

  ;DIRECTIVES

	.DIRECT	.XTABM		;EXCLUDE TABS FROM MACRO CALLS
	SALL			;SUPPRESS MACRO EXPANSIONS
	TWOSEG	400K		;TWOSEGMENT CODE

  ;VERSION NUMBER

	SYNVER==5		;MAJOR VERSION NUMBER
	SYNMIN==0		;MINOR VERSION NUMBER
	SYNEDT==246		;EDIT LEVEL
	SYNWHO==0		;CUSTOMER ARGUMENT

  ;TITLE

	TITLE.	SYN,SNOSYN,<Syntactical Analyzer>

  ;SHOW UNIVERSAL VERSION NUMBERS

	SHOW.	%%MACT		;VERSION NUMBER OF MACTEN
	SHOW.	%%SNOP		;VERSION NUMBER OF SNOPRM

	SYNTTL			; Title
	SYNPTX			; PASS2 assembly message
	SUBTTL	Table of Contents

;		Table of Contents for Syntactical Analyzer
;
;
;			   Section			      Page
;   1. Table of Contents. . . . . . . . . . . . . . . . . . .    2
;   2. REVISION HISTORY . . . . . . . . . . . . . . . . . . .    3
;   3. CODE ENTRY POINT . . . . . . . . . . . . . . . . . . .    4
;   4. CVTEXP . . . . . . . . . . . . . . . . . . . . . . . .    5
;   5. SPECIAL TOKEN INITIALIZATION . . . . . . . . . . . . .    6
;   6. STATEMENT LOOP (STLOOP). . . . . . . . . . . . . . . .    7
;   7. TOKEN PROCESSING . . . . . . . . . . . . . . . . . . .    9
;   8. GOTO'S . . . . . . . . . . . . . . . . . . . . . . . .   14
;   9. UTILITIES. . . . . . . . . . . . . . . . . . . . . . .   16
;  10. COMPILER ERROR HANDLING. . . . . . . . . . . . . . . .   22
;  11. MISC. DATA . . . . . . . . . . . . . . . . . . . . . .   24
	SUBTTL	REVISION HISTORY

COMMENT	\

START OF VERSION 5.
122	RENAME MODULES & CLEAN UP.
164	Don't compile unevaluated expressions inline any longer.
246	Remove the definition of CSTACK to the parameter file.
\

	SUBTTL	CODE ENTRY POINT

; CODE IS ONE OF TWO ENTRY POINTS TO THIS MODULE. (THE
; OTHER IS CVTEXP.) CODE CAN RECEIVE CONTROL EITHER AT
; EXECUTION TIME (THE CODE BUILT-IN FUNCTION) OR AT 
; PRE-EXECUTION TIME. IN THE LATTER CASE, CODE IS 
; CALLED BY THE INTERPRETER TO COMPILE THE USERS PROGRAM.
; THE WORD PREX INDICATES WHETHER THE PRE-EXECUTION 
; STAGE IS IN EFFECT.

CODE::	MOVEM	PLINK,IGNOP##	; SAVE LINKING REGISTER
	CLEARM	ENDFLG		; SET WHEN EOP IS ENCOUNTERED
	CLEARM	EXFLAG		; DISTINGUISH ENTRY POINT
	CLEARM	LAST		; OFFSET OF LAST STATEMENT

	JSP	SLINK,TKINIT##	; LET TOKEN INITIALIZE
	JRST	CODE1		; ERROR RETURN

	JSP	TLINK,CBINIT	; INITIALIZE A CBLOK
	JRST	VB.ALL		; GO ALLOCATE SOME VARIABLE BLOCKS

	 ; HERE IF ERROR RETURN FROM TKINIT

CODE1:	SKIPE	PREX##		; IS IT COMPILE TIME?
	ERROR	12,4		; YES - INCORRECTIBLE READ ERROR
	MOVE	P2,-1(STACK)	; GET DESCRIPTOR TOP
	CAME	P2,NULLST##	; WAS ARG NULL?
	ERROR.	1,56		; NO - INVALID DT
	ERROR	13,55		; YES - INVALID VALUE


	SUBTTL	CVTEXP	ENTRY POINT

; THIS IS THE OTHER ENTRY POINT. CVTEXP ESSENTIALLY COMPILES A 
; STRING INTO AN EXPRESSION. IT DIFFERS FROM CODE IN THAT 
; NO STATEMENT HEADER IS PRODUCED AND THAT SPECIAL OPERATORS AT
; LEVEL 0 ARE NOT SEARCHED FOR AND GOTO'S ARE UNALLOWED.
; WHERE A DISTINCTION MUST BE MADE BETWEEN CVTEXP AND CODE THE 
; EXFLAG IS EXAMINED.

CVTEXP::MOVEM	PLINK,IGNOP##	; SAVE LINKING REGISTER
	SETOM	EXFLAG		; INDICATE ENTRY POINT

	HLRZ	S1,-1(STACK)	; PICK UP DATATYPE
	CAIN	S1,EDT		; IS IT EXPRESSION ALREADY
	JRST	1(PLINK)	; SUCCEED IF SO

	JSP	TLINK,CVTSTR##	; IT HAD BETTER BE STRING
	JRST	0(PLINK)	; NO - FAIL

	JSP	SLINK,EXINIT##	; INITIALIZE TOKEN FOR EXPRESSIONS
	JRST	0(PLINK)	; MUST HAVE BEEN NULL - FAIL

	CLEARM	GTZONE		; CLEAR GTZONE FLAG AND ...
	CLEARM	CURSOE		; AN CURRENT EXPRESSION START OFFSET
	MOVEI	Z,1		; INITIALIZE LEVEL TO ...
	MOVEM	Z,LEVEL		; 1 TO AVOID COMPLICATIONS

	JSP	TLINK,CBINIT	; INITIALIZE CBLOK
	JRST	STLP1		; JOIN NORMAL PROCESSING


	SUBTTL	SPECIAL TOKEN INITIALIZATION

	NVBS==5		; NUMBER OF VARIABLE BLOCKS

VB.TBL:	XWD	"S",VB.S##	; FOR S GOTO'S
	XWD	"F",VB.F##	; FOR F GOTO'S
	XWD	"*",VB.ST##	; TO DETECT EXPRESSIONS
	XWD	" ",VB.BL##	; TO DETECT IMPLICIT PATTERN MATCHING
	XWD	"=",VB.EQ##	; TO IGNORE IMPLICIT PATTERN MATCHING

	 ; BRANCH HERE FROM OPENING PAGE

VB.ALL:	SKIPN	PREX##		; PRE-EXECUTION?
	JRST	STLOOP		; NO - IGNORE THIS
	MOVEI	S1,NVBS		; LOAD NUMBER OF BLOCKS

	 ; LOOP TO ALLOCATE VARIABLE BLOCKS
	 ; 
VB.AL1:	HLL	S2,VB.TBL-1(S1)	; CHAR. TO S2 LEFT
	HRRI	S2,1		; LENGTH IS 1
	PUSH	STACK,$ALPHA##	; PART OF &ALPHABET
	PUSH	STACK,S2	; PUSH PREPARED WORD
	JSP	TLINK,INSTAL##	; INSTALL INTO SYMBOL TABLE
	ERROR	15		; THIS CAN'T FAIL
	MOVE	S2,VB.TBL-1(S1)	; LOAD THE ASSOCIATED ADDRESS
	MOVEM	Z,0(S2)		; STORE THE RESULT
	SOJG	S1,VB.AL1	; LOOP UNTIL S1 IS EXHAUSTED


	SUBTTL	STATEMENT LOOP (STLOOP)

; IMPURE LOCATIONS USED EXCLUSIVELY BY SYN ZEROED AT THE 
; START OF EACH STATEMENT.

	 ; LOOP FOR PROCESSING STATEMENTS

STLOOP:	CLEARM	LEVEL##		; ZERO OUT ...
	MOVE	Z,[LEVEL,,LEVEL+1] ; ABOVE ...
	BLT	Z,ENDLBL	; LOCATIONS


	 ; MERGE HERE FROM CVTEXP ENTRY POINT

STLP1:	MOVE	CSTACK,HSTACK##	; USE HISTORY STACK FOR COMPILING
	PUSH	CSTACK,[0]	; PUSH ON HIGH FENCE
	PUSH	CSTACK,[0]	; FENCES HAVE 0 PRECEDENCE
	MOVEM	P2,FLAP##+1	; STATEMENT BEGINNING
	SKIPE	EXFLAG##		; ARE WE DOING AN EXPRESSION?
	JRST	TKLOOP		; YES - IGNORE THE REST

	MOVE	T1,FLAP		; LOAD ADDRESS ...
	ADD	T1,FLAP+1	; OF STATEMENT

	MOVS	Z,PAGENO##	; GET CURRENT PAGE NO.
	AND	Z,[CDARGS,,0]	; MASK HIGH ORDER BITS
	IOR	Z,[CDIF,,BGST##] ; COMPLETE RESTOF HEADER
	MOVEM	Z,CHEAD(T1)	; FILL HEAD

	MOVS	Z,CMPNO##	; GET STATEMENT #
	MOVEM	Z,CSTNO(T1)	; INSERT INTO CBLOK
	AOS	CMPNO		; BUMP FOR NEXT TIME

	ADDI	P2,CRULE	; MOVE PAST HEADER

	 ; PROCESS LABEL

	JSP	SLINK,TOKEN##	; CALL FOR A TOKEN
	JRST	CERR		; REPORT ERROR
	JRST	TKEND		; END OF PROGRAM

	SKIPN	S1,Z		; IS RETURNED VALUE 0?
	JRST	TKLOOP		; YES - IGNORE

	DMOVE	Q1,FLAP##	; GET ADDRESS OF CBLOK
				; GET OFFSET OF CURRENT STATEMENT

	SKIPE	PREX		; IS IT PRE-EXECUTION?
	JRST	LBL2		; YES - BRANCH

	MOVE	T1,VFLAGS(S1)	; LOAD FLAGS OF VARIABLE BLOCK
	TLNN	T1,LTFLAG	; IS LABEL BEING TRACED?
	JRST	LBL1		; BRANCH IF NOT

	MOVE	T2,VLABEL(S1)	; PICK UP ADDRESS OF TRBLOK
	DMOVEM	Q1,TRVAL(T2)	; PLUG TRVAL ...
				; FIELD
	JRST	TKLOOP		; AND GO GET FIRST TOKEN
 
	 ; BRANCH HERE FOR COMPILE-TIME INSERTION

LBL2:	HRRZ	T2,VLABEL(S1)	; LOAD PREVIOUS LABEL
	JUMPN	T2,RCE.PL	; BRANCH IF PREV. DEFINED

	 ; MERGE HERE FOR NORMAL LABEL INSERTION

LBL1:	HRL	Q1,Q2		; CONDENSCE TO ONE WORD
	MOVEM	Q1,VLABEL(S1)	; PLUG LABEL FIELD


	 ; HERE TO GET FIRST NON-LABEL TOKEN AND ALL SUBSEQUENT TOKENS

TKLOOP:	JSP	SLINK,TOKEN	; CALL TO GET A TOKEN
	JRST	CERR		; REPORT ERROR
	JRST	TKEOS		; END OF STATEMENT
	 ; 
	 ; NORMAL RETURN FROM TOKEN. TRANSFER TO PROPER ROUTINE

	HLRZ	S1,Z		; ROUTINE ADDRESS IN LEFT HALF OF TOKEN
	JRST	0(S1)		; DO THE BRANCH

	 ; END OF PROGRAM DETECTED BY TOKEN

TKEND:	MOVE	PLINK,IGNOP	; RESTORE LINK (STACK NO LONGER NEEDED)

	SETOM	ENDFLG##		; INDICATE END ENCOUNTERED
	MOVEM	Z,ENDLBL##	; SAVE RETURNED VALUE

	MOVE	Z,[CDIF,,EOP##]	; FINAL TOKEN
	JSP	QLINK,PUT	; INSERTED INTO CBLOK

	JSP	QLINK,CBSNIP	; SNIP OFF REST OF CBLOK

	CLEAR	P2,		; 0 MEANS NO NEXT STATEMENT
	JRST	TKEOS1		; GO FILL IN HEADER

	 ; END OF STATEMENT RETURN FROM TOKEN

TKEOS:	JSP	TLINK,FLUSH	; FLUSH OUT THE STACK
	JRST	TKEOS1		; HIGH FENCE - GOOD
	MOVEI	Z,CE.BL##	; TOO MANY LEFT'S
	JRST	CERR		; GIVE ERROR AND NO FLUSH

	 ; HERE TO FILL IN STATEMENT HEADER

TKEOS1:	SKIPE	EXFLAG		; TEST FOR EXPRESSION
	JRST	ENDEXP		; YES - GOTO SPECIAL END

	MOVE	S1,FLAP		; GET ADDRESS OF ...
	ADD	S1,FLAP+1	; STATEMENT HEADER

	 ; FILL IN  CFGOTO FIELD

	SKIPN	S2,RGT##		; SKIP IF A RECOVERY GOTO WAS DETECTED
	MOVE	S2,P2		; NO - USE NEXT STATEMENT
	HRRM	S2,CFGOTO(S1)	; FILL FIELD

	 ; FILL IN ANY IMMEDIATE GOTO

	SKIPN	S2,FGT##		; TEST FOR ANY FAIL GOTO'S
	JRST	TKEOS2		; NONE - THEN NO IMMEDIATES
	MOVE	S3,P2		; S3 TO HOLD DESTINATION OF IMMEDIATE
	CAMG	S2,SGT##		; IF SGT IS THERE AND GR THAN FGT ...
	MOVE	S3,SGT		; THEN MAKE IT THE DESTINATION.
	ADD	S2,FLAP		; POINT TO THE FAIL GOTO
	MOVEM	S3,-1(S2)	; AN IMMEDIATE GOTO OCCURS JUST BEFORE
	 ;			; THE FAIL GOTO.

	 ; FILL IN THE CLAST & CNEXT FIELDS

TKEOS2:	MOVE	S3,P2		; P2 IS NEXT OFFSET
	MOVE	S2,CBLENG##	; LOAD LENGTH OF CBLOK
	LSH	S2,-1		; DIVIDE BY 2
	CAML	P2,S2		; IS P2 LESS THAN THIS AMOUNT
	MOVEI	S3,CCODE	; NO - NEXT STATEMENT TO BEGIN NEW CBLOK
	HRL	S3,LAST##		; GET OFFSET OF LAST STATEMENT
	MOVEM	S3,CLAST&CNEXT(S1)	; PLUG BOTH FIELDS

	MOVE	S3,FLAP+1	; MAKE THE CURRENT STATEMENT ...
	MOVEM	S3,LAST		; THE LAST.

	 ; NEW CBLOK REQUIRED?

	SKIPE	ENDFLG		; TEST FOR END OF PROGRAM
	JRST	TKEOS3		; YES - GO WRAP-UP
	CAML	P2,S2		; AGAIN TEST FOR P2 TOO BIG
	JSP	TLINK,CBMORE	; IT IS - ALLOCATE A NEW CBLOK
	JRST	STLOOP		; GET NEXT STATEMENT

	 ; END OF PROGRAM

TKEOS3:	SKIPN	S2,ENDLBL	; GET BEGIN LABEL THAT APPEARED ...
				; ON END CARD
	JRST	1(PLINK)	; NO SUCH LABEL APPEARED

	SKIPN	S3,VLABEL(S2)	; PICK UP OFFSET/ADDRESS
	JRST	BADLBL		; IT ISN'T EVEN A LABEL

	HRRM	S3,-1(STACK)	; INSERT CBLOK ADDRESS
	HLRM	S3,0(STACK)	; AND OFFSET
	JRST	1(PLINK)	; AND RETURN

	 ; THE LABEL ON THE END CARD WAS UNDEFINED

BADLBL:	MOVEI	Z,CE.UT##	; BAD TRANSFER ADDRESS
	JSP	SLINK,CERROR	; PUT OUT MESSAGE
	JRST	1(PLINK)	; AND RETURN

	 ; END OF EXPRESSION

ENDEXP:	MOVE	Z,[CONST,,NULLST] ; BEWARE ...
	CAIN	P2,CCODE	; OF ...
	JSP	QLINK,PUT	; NULLITY

	MOVE	Z,[CDIF,,EOE##]	; PREPARE TRAP FOR ...
	JSP	QLINK,PUT	; INTERPRETER

	MOVEI	Z,0		; PUT A 0 WORD ...
	JSP	QLINK,PUT	; BEHIND IT

	JSP	QLINK,CBSNIP	; SNIP OFF REST OF CBLOK

	MOVEI	Z,EDT		; MAKE THE RETURNED ...
	HRLM	Z,-1(STACK)	; VALUE OF TYPE EXPRESSION

	MOVE	PLINK,IGNOP	; RESTORE LINKING
	JRST	1(PLINK)	; NORMAL RETURN


	SUBTTL	TOKEN PROCESSING

	INTERN	SY.ID,SY.CON,SY.FUN,SY.ARY,SY.UNY,SY.BIN
	INTERN	SY$COL,SY$COM,SY$LB,SY$LP,SY$RP,SY$RB


	 ; PROCESSING IDENTIFIERS

SY.ID:	HRLI	Z,VAR		; COMPILES INTO A VARIABLE

	 ; MERGE HERE FROM MANY PLACES

PZLOOP:	JSP	QLINK,PUT	; PUT TOKEN INTO CBLOK ...
	JRST	TKLOOP		; AND LOOP BACK TO GET A NEW TOKEN


	 ; CONSTANTS

SY.CON:	HRLI	Z,CONST		; INDICATE CONSTANT
	JRST	PZLOOP		; PUT Z AND LOOP


	 ; FUNCTON REFERENCE - Z RIGHT CONTAINS IDENT. ADDR.

SY.FUN:	MOVEI	S1,GOTO##	; LOAD GOTO INDICATOR IN CASE
	JSP	QLINK,GTTEST	; IS GOTO?
	JRST	CONDGT		; YES - CONDITIONAL GOTO

	HRLI	Z,BK.FUN	; INSERT BACK-UP ADDRESS

	 ; HERE TO PUSH A LOW FENCE (FROM MANY PLACES)

FNLOOP:	PUSH	CSTACK,Z	; Z LEFT CONTAINS BACK-UP ADDRESS
	PUSH	CSTACK,[0]	; 0 PRECEDENCE MEANS FENCE
	JRST	TKLOOP		; LOOP FOR NEXT TOKEN

	 ; BACK-UP PROCESSING FOR FUNCTIONS (S3 CONTAINS # OF ARGS)

BK.FUN:	HRRZ	Z,1(CSTACK)	; GET IDENTIFIER ADDRESS
	ADDI	Z,VFUNC		; POINT TO VFUNC FIELD
	HRLI	Z,CDF(S3)	; INSERT FUNC + ARG COUNT

	JRST	PZLOOP		; PUT Z AND LOOP


	 ; ARRAY REFERENCE - Z RIGHT CONTAINS ID ADDRESS

SY.ARY:	MOVEI	S1,DGOTO##	; IF GOTO ITS A DIRECT GOTO
	JSP	QLINK,GTTEST	; IS GOTO?
	JRST	CONDGT		; YES - PROCESS CONDITIONAL GOTO'S

	HRLI	Z,VAR		; ID IS ACTUALLY THE FIRST ARGUMENT
	JSP	QLINK,PUT	; INSERT INTO CBLOK

	HRLI	Z,BK.ARY	; INSERT BACK-UP ADDRESS
	JRST	FNLOOP		; PUSH FENCE AND LOOP

	 ; BACK-UP PROCESSING FOR ARRAYS (S3 CONTAINS # OF ARGS)

BK.ARY:	HRLZI	Z,1(S3)		; NO OF ARGS + 1 TO Z LEFT
	ADD	Z,[CDF,,ITEM$$]	; IMPLIED FUNCTION IS ITEM
	JRST	PZLOOP		; PUT Z AND LOOP


	 ; UNARY OPERATORS

SY.UNY:	HRRZ	Z,Z		; CLEAR OUT LEFT HALF OF TOKEN
	CAMN	Z,VB.ST##		; CHECK FOR UNARY *
	JRST	SY.STR		; YES, GO HANDLE IT AS SPECIAL CASE

	ADDI	Z,VUNARY	; POINT TO VUNARY FIELD
	HRLI	Z,CDF+1		; INSERT UNARY FUNCTION CODE
	PUSH	CSTACK,Z	; PUSH OPERATOR ONTO STACK
	PUSH	CSTACK,[1000]	; INFINITE PRECEDENCE
	JRST	TKLOOP		; LOOK FOR ANOTHER TOKEN

	 ; HERE TO PROCESS UNARY *

SY.STR:	MOVE	Z,CURSOE##	; SAVE CURRENT EXPRESSION START...
	JSP	QLINK,PUT	; IN THE CURRENT CBLOK

	MOVEM	P2,CURSOE	; P2 IS NEW CURSOE

	MOVE	Z,[CDIF,,EOE##]	; INDICATE END OF EXPRESSION
	PUSH	CSTACK,Z	; PUSH ONTO STACK
	PUSH	CSTACK,[1000]	; LIKE AN OPERATOR
	JRST	TKLOOP		; LOOK FOR ANOTHER TOKEN


	 ; BINARY OPERATORS

SY.BIN:	HRRZ	S1,Z		; GET ADDRESS OF VARIABLE BLOCK
	MOVEI	S3,VBINRY(S1)	; POINT TO BINARY FIELD OF VB

	SKIPE	LEVEL		; TEST LEVEL
	JRST	SY.BN1		; NONZERO - BYPASS CHECKS

	SKIPE	GTZONE##		; ARE WE IN THE GOTO ZONE
	JRST	SY.BN4		; YES - SPECIAL PROCESSING NEEDED

	SKIPE	PMFLAG##		; ANY PREVIOUS BIN. OPS AT LEVEL 0?
	JRST	SY.BN1		; YES, SKIP IMPLICIT PATT. MATCHING
	SETOM	PMFLAG		; FLAG BIN OP AT LEVEL 0

	CAME	S1,VB.BL##	; IF OP IS BLANK
	JRST	SY.BN1		; DON'T BRANCH HERE, BUT

	 ; IMPLICIT PATTERN MATCHING

	MOVEI	S3,MTCH$$	; POINT TO PATTERN MATCHING
	SETOM	PMFLAG		; AND SET THE FLAG.
	MOVEI	S1,6		; IMPLIED RIGHT PRECEDENCE
	MOVEI	S2,7		; IMPLIED LEFT PRECEDENCE
	JRST	SY.BN5		; JOIN NORMAL PROCESSING

	 ; JUMP HERE IF NOT IMPLICIT PATTERN MATCH

SY.BN1:	HLRZ	S2,LPREC(S1)	; GET LEFT PRECEDENCE TO S2
	HLRZ	S1,RPREC(S1)	; RIGHT PREC. TO S1
	 ; 
	 ; MERGE HERE FROM IMPLICIT PATTERN MATCHING

SY.BN5:	HRLI	S3,CDF+2	; MOVE IN 2-ARG INDICATOR

	 ; LOOP HERE TO TEST PRECEDENCE

SY.BN2:	CAMG	S2,0(CSTACK)	; COMPARE LEFT PR. WITH PR. ON STACK
	JRST	SY.BN3		; LOWER; DISGORGE ITEM
	PUSH	CSTACK,S3	; HIGHER, PUSH IT ONTO STACK.
	PUSH	CSTACK,S1	; ALSO PUSH RIGHT PRECEDENCE
	JRST	TKLOOP		; AND LOOP BACK

	 ; HIGH PRECEDENCE OPERATOR ON STACK

SY.BN3:	POP	CSTACK,Z	; REMOVE PRECEDENCE
	POP	CSTACK,Z	; REMOVE OPERATOR
	JSP	QLINK,PUT	; PUT OPERATOR OUT
	JRST	SY.BN2		; RESUME COMPARISONS

	 ; HERE FOR LEVEL 0 OPERATOR IN GOTO ZONE

SY.BN4:	CAMN	S1,VB.BL	; ONLY BLANK IS ALLOWED HERE.
	JRST	TKLOOP		; OK - BUT DON'T INSERT IN CBLOK
	JRST	RCE.GT		; BAD OPERATOR


	 ; THE COLON

SY$COL:	SKIPE	EXFLAG		; MAKE SURE WE'RE NOT DOING EXPRESSIONS
	JRST	@IGNOP		; FAIL IF WE ARE

	SETOM	GTZONE		; SET FLAG TO INDICATE GOTO ZONE

	JSP	TLINK,FLUSH	; FLUSH ALL OPERATORS FROM STACK
	JRST	TKLOOP		; HIGH FENCE - GOOD
	JRST	RCE.BL		; REPORT IMBALANCE - TOO MANY LEFTS


	 ; THE COMMA

SY$COM:	JSP	TLINK,FLUSH	; REMOVE LATEST OPERATORS
	JRST	RCE.BR		; HIGH FENCE -	NOT ENOUGH LEFTS

	MOVEI	Z,CE.IC##	; POTENTIAL ERROR IS 'ILL CHAR'
	HLRZ	S1,-1(CSTACK)	; GET LAST 'BACK-UP' ADDR
	CAIN	S1,TKLOOP	; WAS IS AN OPEN PAREN?
	JRST	CERR1		; YUP - GIVE DIAGNOSTIC

	HRLI	Z,BK$COM	; BACK-UP ROUTINE FOR COMMA
	JRST	FNLOOP		; PUSH FENCE AND LOOP

	 ; BACK-UP FOR COMMA

BK$COM:	AOJ	S3,		; ADD ONE TO ARG COUNT
	SUB	CSTACK,[2,,2]	; BUMP STACK FOR NEXT FENCE
	HLRZ	S2,1(CSTACK)	; PICK UP NEXT BACK-UP ADDRESS
	JRST	0(S2)		; AND BRANCH TO IT


	 ; LEFT BRACKET

SY$LB:	MOVEI	S1,DGOTO##	; IF GOTO, ITS A DIRECT GOTO
	JSP	QLINK,GTTEST	; CHECK FOR GOTO
	JRST	UNCOND		; PROCESS UNCONDITIONAL GOTO'S

	HRLI	Z,BK.ARY	; MUST BE COMPILER ARRAY REF.
	JRST	FNLOOP		; PUSH FENCE AND LOOP


	 ; LEFT PAREN

SY$LP:	MOVEI	S1,GOTO##	; IF GOTO, ITS A PLAIN GOTO
	JSP	QLINK,GTTEST	; CHECK FOR GOTO
	JRST	UNCOND		; YES - UNCOND. GOTO

	HRLI	Z,TKLOOP	; BACK-UP PROCEDURE IS THE NO-OP
	JRST	FNLOOP		; PUSH FENCE AND LOOP


	 ; RIGHT PAREN AND RIGHT BRACKET
SY$RP:	 ;
SY$RB:	SOSGE	LEVEL		; DECREMENT AND TEST LEVEL
	JRST	RCE.BR		; IF IT GOES NEG. - TOO MANY RIGHTS

	JSP	TLINK,FLUSH	; FLUSH OUT
	JRST	RCE.BR		; HIGH FENCE - TOO MANY RIGHTS

	CLEAR	S3,		; ZERO OUT ARG. COUNT
	JRST	BK$COM		; MAKE LIKE A COMMA


	SUBTTL	GOTO'S

; UTILITY GTTEST - TESTS WHETHER A FUNCTION OR PARENTHESIZED
; EXPRESSION IS TO BE TREATED AS A GOTO.
;
; CALLING SEQUENCE:
;	JSP	QLINK,GTTEST
;	IS GOTO
;	IS NOT GOTO

GTTEST:	AOS	Q2,LEVEL	; BUMP AND LOAD LEVEL
	SKIPN	GTZONE		; IN THE GOTO ZONE?
	JRST	1(QLINK)	; NO - GIVE NO GOTO

	CAIE	Q2,1		; WAS LEVEL 0?
	JRST	1(QLINK)	; NO - GIVE NO GOTO
	JRST	0(QLINK)	; YES - GIVE GOTO


	 ; CONDITIONAL GOTO'S

CONDGT:	HRRZ	S2,Z		; GET ASSOCIATED IDENTIFIER
	CAMN	S2,VB.S##		; IS IT 'S'?
	JRST	SUCCGT		; YES

	CAMN	S2,VB.F##		; IS IT 'F'?
	JRST	FAILGT		; YES

	JRST	RCE.GT		; BAD FUNCTION


	 ; SUCCESS GOTO

SUCCGT:	SKIPE	RGT		; OK IF NO RECOVERY GOTO EXISTS
	SKIPE	FGT		; OK IF RGT WAS FGT
	SKIPE	SGT		; 'OK' MEANS TEST FOR PREVIOUS SGT
	JRST	RCE.GT		; OTHERWISE REPORT ERROR
	MOVEM	P2,SGT		; FILL IN SUCCESS GOTO

	 ; MERGE HERE FROM OTHER GOTO'S BEFORE LOOPING
	 ; S1 CONTAINS TYPE OF GOTO

GTLOOP:	HRLI	S1,BK.GT	; COMMON BACK-UP PROCEDURE
	MOVE	Z,S1		; PLACE FENCE INTO Z
	JRST	FNLOOP		; PUSH FENCE AND LOOP


	 ; FAIL GOTO

FAILGT:	SKIPE	RGT		; TEST FOR PREVIOUS RECOVERY GOTO
	JRST	RCE.GT		; REPORT GOTO ERROR IF SO

	MOVE	Z,[CDIF,,IGOTO##]	; INSERT IMMEDIATE GOTO ...
	JSP	QLINK,PUT	; TO BRANCH AROUND FGOTO
	JSP	QLINK,PUT	; THIS TO BE PLUGGED LATER

	MOVEM	P2,RGT		; SET RECOVERY GOTO
	MOVEM	P2,FGT		; SET FAIL GOTO

	JRST	GTLOOP		; LOOP BACK VIA COMMON CODE


	 ; UNCONDITIONAL GOTO

UNCOND:	SKIPN	RGT		; IF RECOVERY ALREADY EXISTS REPORT ERR
	SKIPE	SGT		; SKIP IF SUCCESS GOTO NOT THERE
	JRST	RCE.GT		; REPORT GOTO ERROR

	MOVEM	P2,RGT		; SET RECOVERY GOTO
	JRST	GTLOOP		; LOOP BACK VIA GOTO'S


	 ; COMMON BACK-UP FOR GOTO'S

BK.GT:	HRR	Z,1(CSTACK)	; GET DGOTO/GOTO ADDRESS
	HRLI	Z,CDIF		; MAKE IT AN INTERPRETER FUNCTION
	JSP	QLINK,PUT	; INSERT TOKEN INTO CBLOK
	JRST	TKLOOP		; AND LOOP BACK


	SUBTTL	UTILITIES

; PUT - PUT A TOKEN INTO THE CBLOK POINTED TO BY FLAP
; AT AN OFFSET INDICATED BY P2.
; CALLING SEQUENCE:
;	LOAD Z WITH TOKEN
;	JSP	QLINK,PUT
;	RETURN

PUT:	AOS	Q3,P2		; INCREMENT OFFSET AND LEVEL INTO Q3
	ADD	Q3,FLAP		; FORM ADDRESS+1 OF NEW ENTRY
	MOVEM	Z,-1(Q3)	; INSERT TOKEN

	CAMN	Z,[CDIF,,EOE]	; DID WE JUST STORE EOE ?
	SKIPN	CURSOE		; YES, BUT WAS IT UNARY * ?
	JRST	PUT1		; NO, BYPASS EXPRESSION HANDLING

	 ; HERE TO ALLOCATE AN EBLOK FOR OUT-OF-LINE EXPRESSION

	PUSHR	TQ		; ELEVATE OUR STATUS...
	PUSHSA	T		; TO BE A TYPE T ROUTINE

	MOVE	T3,P2		; CURRENT POINTER INTO T3
	SUB	T3,CURSOE	; MINUS CURSOE = LENGTH OF EBLOK
	HRLI	Z,EBLOK		; WE WANT AN EBLOK
	HRRI	Z,LHDR(T3)	; OF LENGTH = HEADER + P2 - CURSOE
	JSP	QLINK,BLOK##	; ALLOCATE IT
	ERROR	15		; CAN'T FAIL

	MOVELAP		; GET THE CURRENT CBLOK ADDRESS
	ADD	Q1,CURSOE	; PLUS CURSOE = 'FROM' ADDRESS
	HRLS	Q1		; IN THE LEFT HALF
	HRR	Q2,-1(STACK)	; ADDRESS OF EBLOK...
	HRRI	Q1,LHDR(Q2)	; PLUS HEADER LENGTH IS 'TO' ADDRESS
	ADDI	Q2,LHDR(T3)	; EBLOK ADDRESS PLUS LENGTH
	BLT	Q1,-1(Q2)	; MOVE EXPRESSION CODE INTO EBLOK

	MOVEI	Z,LHDR		; LENGTH OF BLOK HEADER TO Z
	MOVEM	Z,0(STACK)	; IS OFFSET TO START OF EXPRESSION
	JSP	TLINK,UBENTR##	; PUT DESCRIPTOR IN CURRENT UTBLOK
	HRLI	Z,CONST		; THIS IS JUST LIKE A CONSTANT

	MOVE	P2,CURSOE	; RESET P2 TO MOST RECENT SOE
	MOVE	Q3,FLAP		; GET ADDRESS OF CURRENT CBLOK
	ADDI	Q3,-1(P2)	; POINT TO PREVIOUS CURSOE VALUE
	EXCH	Z,0(Q3)		; STORE TOKEN AND FETCH OLD CURSOE
	MOVEM	Z,CURSOE	; RESTORE PREVIOUS VALUE OF CURSOE

	POPSA	T		; POP SAVE AREA AND COME BACK...
	POPR	TQ		; DOWN TO TYPE Q
	JRST	0(QLINK)	; RETURN TO CALLER

	 ; HERE TO CHECK CBLOK SIZE

PUT1:	CAMGE	P2,CBLENG	; ARE WE AT THE END OF THE CBLOK ?
	JRST	0(QLINK)	; NO - RETURN

	MOVEI	Z,CE.LS##	; GIVE LONG-STATEMENT ...
	JRST	CERR1		; ERROR MESSAGE.


; FLUSH - WILL FLUSH OPERATORS OUT OF THE CSTACK INTO THE
; CBLOK, STOPPING AT THE FIRST FENCE (0 PRECEDENCE).
;
; CALLING SEQUENCE:
;	JSP	TLINK,FLUSH
;	HIGH FENCE (0 ELEMENT)
;	LOW FENCE (NON ZERO ELEMENT)

	 ; TEST FOR FENCE

FLUSH:	SKIPE	0(CSTACK)	; ARE WE AT A FENCE ALREADY?
	JRST	FLUSH1		; NO - TAKE BRANCH

	 ; TAKE PROPER RETURN

	SKIPN	-1(CSTACK)	; CHECK FOR HIGH FENCE
	JRST	0(TLINK)	; YES - HIGH
	JRST	1(TLINK)	; NO - LOW

	 ; FLUSH ONE ELEMENT FROM THE STACK

FLUSH1:	POP	CSTACK,Z	; REMOVE PRECEDENCE
	POP	CSTACK,Z	; POP OPERATOR
	JSP	QLINK,PUT	; INSERT OPERATOR INTO CBLOK
	JRST	FLUSH		; LOOP FOR MORE


; CBALL (CBLOK ALLOCATE) IS USED BY BOTH CBINIT AND
; CBMORE TO ALLOCATE A CBLOK.
;
; CALLING SEQUENCE:
;	JSP	T4,CBALL

	 ; ESTABLISH ROOM

CBALL:	MOVE	Z,CBLENG	; GET LENGTH OF CBLOK
	JSP	QLINK,GCOLF##	; ENSURE THIS MUCH SPACE

	HRLI	Z,CBLOK		; INSERT BLOK TYPE
	JSP	QLINK,BLOK##	; ALLOCATE THE CBLOK
	ERROR	15		; CAN'T FAIL

	MOVEI	P2,CCODE	; INITIALIZE P2
	JRST	0(T4)		; AND RETURN


; CBMORE - CALLED WHENEVER THE CURRENT CBLOK IS RUNNING LOW
;
; CALLING SEQUENCE
;	JSP	TLINK,CBMORE

CBMORE:	MOVE	Z,[CDIF,,LRGOTO##]	; INSERT LAST ...
	JSP	QLINK,PUT	; TOKEN INTO CBLOK

	JSP	QLINK,CBSNIP	; SNIP OFF THE UNUSED PORTION

	JSP	T4,CBALL	; GET THE NEXT CBLOK
	POP	STACK,Z		; JUNK
	POP	STACK,Q2	; GET POINTER TO NEW

	MOVE	Q1,FLAP		; GET POINTER TO OLD
	MOVEM	Q2,FLAP		; REESTABLISH CURRENT

	MOVEM	Q2,NCLINK(Q1)	; INTERCONNECT ...
	MOVEM	Q1,LCLINK(Q2)	; THE PAIR

	JRST	0(TLINK)	; AND RETURN


; CBINIT - INITIALIZE A CBLOK - CALLED AT THE BEGINNING
; OF COMPILATION.
;
; CALLING SEQUENCE:
;	JSP	TLINK,CBINIT

CBINIT:	JSP	T4,CBALL	; ALLOCATE THE CBLOK

	MOVE	T3,-1(STACK)	; PICK UP DESCRIPTOR TOP ...
	MOVEM	T3,FLAP		; AND STORE INTO FLAP.

	MOVEM	P2,0(STACK)	; PLACE INITIAL OFFSET INTO STACK
	JRST	0(TLINK)	; AND RETURN


; CBSNIP - WILL REMOVE THE UNUSED PORTION OF THE CURRENT
; CBLOK RETURNING IT TO THE AVAILABLE STORAGE POOL.
; P2 INDICATES THE CURRENT LENGTH OF THE CBLOK
; CALLING SEQUENCE:
;	JSP	QLINK,CBSNIP

CBSNIP:	HRRZ	Q2,FLAP		; LOAD ADDRESS OF CBLOK
	HRRZ	Z,BLENG(Q2)	; ORIGINAL LENGTH OF CBLOK
	SUBI	Z,0(P2)		; AMOUNT OF REMAINING STORAGE
	CAIGE	Z,2		; NO SENSE SNIPPING UNLESS WE ...
	JRST	0(QLINK)	; GET AT LEAST 2 WORDS
	HRRM	P2,BLENG(Q2)	; INSERT NEW LENGTH

	ADDI	Q2,0(P2)	; POINT PAST BOTH ...
	ADD	Q2,Z		; REGIONS AND SEE IF WE'RE ...
	CAME	Q2,SURF		; AT THE SURFACE
	JRST	CBSNP1		; AND JUMP IF NOT

	SUB	SURF,Z		; OTHERWISE BRING SURFACE
	JRST	0(QLINK)	; DOWN AND RETURN

	 ; CREATE A JUNK BLOK TO FILL THE VOID

CBSNP1:	MOVE	Q2,FLAP		; GET ORIGINAL ADDRESS
	ADDI	Q2,0(P2)	; POINT JUST AFTER CBLOK
	ADD	Z,[JKBLOK,,0]	; PREPARE TYPE LENGTH
	MOVEM	Z,BTCODE&BLENG(Q2)	; PLUG HEADER
	CLEARM	GCFLD(Q2)	; MAKE GC HAPPY
	JRST	0(QLINK)	; AND RETURN


	SUBTTL	COMPILER ERROR HANDLING

;	CERR IS THE MAIN ENTRY POINT. Z CONTAINS SOME
; ERROR INDICATION. THE RETURN POINT IS TKEOS1 (DURING
; PRE-EXECUTION ONLY).

CERR:	CLEARM	IGNOP+1		; FLAG TO INDICATE ENTRY POINT

	SKIPE	PREX		; ARE WE IN EXECUTION
	JRST	CERR2		; NO - BRANCH TO GIVE MESSAGE

	MOVE	PLINK,IGNOP	; YES - LOAD LINK REG.
	SKIPE	EXFLAG		; EXPRESSION?
	JRST	0(PLINK)	; YES - FAIL
	JRST	FRET1##		; NO - FAIL AND CLEAN STACK

	 ; HERE IF ERROR OCCURRED DURING COMPILATION TIME

CERR2:	JSP	SLINK,CERROR##	; PRINT THE MESSAGE

	MOVE	P2,FLAP+1	; POINT TO ...
	ADDI	P2,CRULE	; FIRST TOKEN OF STATEMENT
	MOVE	Z,[CDIF,,ERRST##]	; INSERT ERROR INDICATOR ...
	JSP	QLINK,PUT	; INTO CBLOK

	CLEARM	FGT		; REMOVE GOTO'S
	CLEARM	RGT		; THAT MAY HAVE OCCURRED

	SKIPN	IGNOP+1		; TEST ENTRY POINT
	JRST	TKEOS1		; CERR - JUST RETURN
	JRST	CERR1A		; CERR1 - GO LOOP ON TOKEN


; CERR1 IS LIKE CERR EXCEPT THAT AFTER PRINTING OUT
; AN ERROR MESSAGE, TOKEN IS CALLED REPEATEDLY UNTIL
; AN END-OF-STATEMENT OR ERROR IS GIVEN.

	 ; FIRST GIVE DIAGNOSTIC

CERR1:	SETOM	IGNOP+1		; FLAG TO INDICATE ENTRY
	JRST	CERR+1		; DELIVER MESSAGE

	 ; RETURN IS TO HERE

CERR1A:	JSP	SLINK,TOKEN	; CALL TOKEN UNTIL ...
	JFCL			; (NO-OP) EITHER THIS ...
	JRST	TKEOS1		; OR THIS OCCURS.
	JRST	CERR1A		; OTHERWISE LOOP.


;	SPECIFIC ERRORS

	 ; REPORT CE.GT (ERROR IN GOTO)

RCE.GT:	MOVEI	Z,CE.GT##
	JRST	CERR1

	 ; TOO MANY LEFT PARENS OR LEFT BRACKETS

RCE.BL:	MOVEI	Z,CE.BL
	JRST	CERR1

	 ; INSUFFICIENT NUMBER OF LEFT PARENS OR LEFT BRACKETS

RCE.BR:	MOVEI	Z,CE.BR##
	JRST	CERR1

	 ; PREVIOUSLY DEFINED LABEL

RCE.PL:	MOVEI	Z,CE.PL##
	JRST	CERR1


	SUBTTL	MISC. DATA

	 ; PURE DATA

MTCH$$:	MTCH$##		; ADDRESS OF PATTERN MATCHING ROUTINE
ITEM$$:	ITEM$##		; ADDRESS OF ARRAY REFERENCE ROUTINE

	END
 @Ù