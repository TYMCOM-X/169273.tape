begin "Skeleton"

require "{}[]" delimiters;
Define TemplateName = {"}&(compiler!banner)[44 for 3]&{DEF.DEF"};

require "MYSAIL.DEF" source!file;

require TemplateName source!file;
require !VERSION version;

require "BRKINI.REQ" source!file;
require "CMDSCN.REQ" source!file;

Ifcr ( Declaration( CommandUtilities ) neq 0 )	! utility routines   ;
 thenc						! if they're defined ;
    require CommandUtilities source!file;
 endc



Own Integer Index;
Own String  Command, CmdLine;

Simple Boolean Procedure All.Legal( Reference Integer Foo );
Return( True );


require "CMDTBL.DEF" source!file;
require CommandDefinitions source!file;

Simple procedure NotImp;
Print( Command, " Not Implemented", Crlf );

require CommandRoutines source!file;

IfCR ( #C#Fields land ##F##Quit )
 ThenC

    Simple procedure C.Quit;		! C.Exit ;
    Call( 0, "EXIT" );

 EndC

IfCR ( #C#Fields land ##F##Help )
 ThenC

    Simple Procedure C.Help;
    begin "HELP"

	If ( CmdEOL = #Esc )		! did the user type Escape? ;
	 then Print( Crlf );		! yes, so rectify problem ;

	If ( CmdLine = "?" )		! followed by ? ;
	 then CmdLine_ "HELP";		! make it "..HELP" ;

	If Length( CmdLine )		! "HELP <token>" ;
	 then begin "short help check"

	   Index_ FndCmd( CToken(CmdLine), Cmd#Name, All.Legal );
	    If ( Index geq ArrInfo( Cmd#Name, 1 ) )
	     then Print( Crlf, Cmd#Name[ Index ], Cmd#Text[ Index ],
			 Crlf, Cmd#Info[ Index ], Crlf )
	     else Print( "  Unrecognized or ambigious help command"&Crlf );

	 end "short help check"
	 else begin "long help text"

	    Print(  Crlf & ProgramBanner & Crlf & "Commands:" & Crlf );

	    For Index _ ArrInfo( Cmd#Name, 1 )	! for each keyword;
			step 1 until		!  in the specified;
			ArrInfo( Cmd#Name, 2 )	!  array -- do;
	     do
		    If 0 = ( Cmd#Flag[ Index ] land #C#Inv )
		     then Print("  ", Cmd#Name[Index], Cmd#Text[Index], Crlf);

	 end "long help text";

	Print( Crlf );

    end "HELP";

   EndC


Procedure Cmd.Action(	Reference Integer Command.Index;
			Reference String  Command.Line;
			Reference Integer Break.Char );
begin "command actions"

    Command_ Cmd#Name[ Command.Index ];	!  M-A-I-N  C-M-D  L-O-O-P  ;
    Scan( Command.Line, BrkWht, Brk );	! trim away any whitespace  ;
    Index_   Command.Index;		! copy the passed variable  ;
    CmdLine_ Command.Line;		!   and the text line       ;

    Start!code "perform command"
	Move	 '1,Index;		! get the routine ;
	Skipe	 '1,Cmd#Proc('1);	! get the address ;
	Pushj 	'17,('1);		! execute (if non-zero) ;
    end;

end "command actions";


!	*** Main Program ***
;


CmdScn( "[" & ProgramBanner & "]",	! Cmd.Heading  ;
	ProgramPrompt,			! Cmd.Prompt   ;
	Cmd#Name,			! Cmd.Names    ;
	All.Legal,			! Cmd.Legal    ;
	Cmd.Action,			! Cmd.Actions  ;
	True				! Cmd.Fileline ;
      );


end "Skeleton";
