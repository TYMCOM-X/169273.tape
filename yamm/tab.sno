*
*		PRIMATIVE PATTERNS
*
	ALPHA = ANY("ABCDEFGHIJKLMNOPQRSTUVWXYZ!")
	IDENT = ALPHA ARBNO(ALPHA ! DIGIT)
	DIGIT = ANY("0123456789")
	SEP = NSPAN(" 	")
	CMA = BREAKQ(",")
	PAR = BREAKQ(")")
	DOL = BREAK("$")
	TAB = "	"
*
*		PATTERNS FOR PARSING INPUT FILES
*
	NFTAB = ("nftab" ! "ftab") . TABTYP
	DESC = "." ARB "b!tbl" SEP "(" PAR . TABDSC ")"
	BEGTAB = "." ARB "b!" NFTAB SEP "(" CMA . TABHDR "," CMA ","
+	    CMA . TABTAB "," PAR . TABIND ")"
	ENDTAB = "." ARB ("e!" NFTAB ! "e!tbl")
	SOSNUM = DIGIT DIGIT DIGIT DIGIT DIGIT TAB
	ANGLES = DOL $ A1 "$" DOL $ A2 "$" REM $ A3
	LABEL = "." (ARB ";" ! NULL) SEP IDENT . TABLAB ":"
	NOQUOTE = ('"' BREAK('"') . OUT) ! REM . OUT
	&ANCHOR = 1
	OUTPUT("TTY",0)
	DIRCHAN = OPEN("DSK(0,2)")
	INCHAN = OPEN("DSK(0,2)")
	OUTCHAN = OPEN("DSK(2,0)")
	INPUT("NEXTFILE",LOOKUP("TAB.DIR",DIRCHAN))	:F(FUBAR)
SYNC	NEXTFILE "NAME" REM				:F(SYNC)
NEXT	INFILE = NEXTFILE				:F(END)
	INFILE BREAK(" ") . FN SPAN(" ") BREAK(" ") . EX :F(NEXT)
	INFILE = FN "." EX
	TTY = "Processing " INFILE
	INPUT("IN",LOOKUP(INFILE,INCHAN))		:S(MORE)
	TTY = "?File not found"			:(NEXT)
FUBAR	TTY = "Directory file TAB.DIR not found"	:(END)
MORE	LINE = IN					:F(NEXT)
	LINE SOSNUM REM . LINE
	LINE DESC					:F(MORE)
MORE1	LINE = IN					:F(NEXT)
	LINE SOSNUM REM . LINE
	LINE ENDTAB					:S(MORE)
	LINE BEGTAB					:F(MORE1)
*
*		OPEN OUTPUT FILE FOR THIS TAB
*
	TABNUM = TABNUM + 1
	TABNAM = "TAB" TABNUM
	OUTFILE = TABNAM ".TAB"
	OUTPUT("OUT",ENTER(OUTFILE,OUTCHAN))		:S(OUTOK)
	TTY = "Can't open output file " OUTFILE		:(MORE)
*
*		OUTPUT HEADER MACRO CALL
*
OUTOK	TTY = ".table " TABDSC ",,," TABNAM ":"
	OUT = ".table |" TABDSC "|,TabSiz," TABTAB "," TABNAM ":"
	TABHDR NOQUOTE
	OUT = IDENT(TABTYP,"ftab") NULL
*
*		COPY TAB BODY WITH FILTERING
*
TABTXT	LINE = IN					:F(NEXT)
	LINE SOSNUM REM . LINE
	LINE ENDTAB					:S(TABEND)
ANG	LINE ANGLES = A1 "<" A2 ">" A3			:S(ANG)
	OUT = IDENT(TABTYP,"nftab") NULL
	OUT = LINE					:(TABTXT)
*
*		OUTPUT TRAILER MACRO CALL
*
TABEND	OUT = ".endTable " TABNAM ":"
	CLOSE(OUTCHAN)					:(MORE)
END
