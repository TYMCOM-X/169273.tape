TITLE UNITIO
	T1=1
	T2=2
	T3=3
	T4=4
	J=5
	STR=6
	UX=7
	P=17

PDLLEN==^D60

UNINAM==0
UNIMRC==22
UNIMWC==23
UNIICT==25
UNIOCT==26
UNIMAX==UNIOCT+1
NUMUNI==^D30

DEFINE MSG(TEXT)<PUSHJ P,[MOVEI STR,[ASCIZ/'TEXT/]
	JRST STROUT]>
DEFINE CRLF <THGSIZ==0
	PUSHJ P,DOCRLF
>
DEFINE POS(N) <MOVEI T1,'N
		PUSHJ	P,DOPOS>
DEFINE NXTPOS(SIZE)<
	POS(\<THGSIZ+'SIZE>)
	THGSIZ==THGSIZ+'SIZE
>;END NXTPOS DEFINITION
THGSIZ==0

ST:	JFCL
	RESET
	MOVE	P,[IOWD PDLLEN,PDL]
	MOVE	T1,[CLRBEG,,CLRBEG+1]
	SETZM	CLRBEG
	BLT	T1,CLREND

	SETOM	HEDFLG

LOOP:	SETZB	UX,DVFSTR		;START WITH FIRST UNIT
LOOP1:	MOVE	T1,[LENDVF,,DVFBLK]
	SYSDVF	T1,
	  HALT	.
	SKIPN	DVFSTR
	JRST	ENDLUP
DEFINE THINGS <
X <TOTAL READ>,UNIICT,<^D15>
X <TOTAL WRITTEN>,UNIOCT,<^D15>
X <MONITOR READ>,UNIMRC,<^D15>
X <MONITOR WRITTEN>,UNIMWC,<^D15>
>

DEFINE X(TEXT,WHERE,POS) <
	MSG<'TEXT>
	NXTPOS('POS)
>
	AOSE	HEDFLG
	JRST	NOHED
	NXTPOS(^D15)
	THINGS
	CRLF
	CRLF
NOHED:

DEFINE X(TEXT,WHERE,POS,%A)<
	MOVE	T1,DVFDAT+'WHERE
	PUSH	P,T1
	SUB	T1,%A(UX)
	POP	P,%A(UX)
REPEAT 0,<
	IMULI	T1,^D10*^D60/^D4 ;SCALE IT, TURN TO PAGES/SEC FROM BLOCKS/TIC
	IDIV	T1,DELTAT
	IDIVI	T1,^D10
	PUSH	P,T2
	PUSHJ	P,DECOUT
	OUTCHI	"."
	POP	P,T1
>
	IDIVI	T1,^D4		;TURN TO INCREMENTAL PAGES
	PUSHJ	P,DECOUT
	NXTPOS('POS)
	JRST	%A+NUMUNI
%A:	BLOCK	NUMUNI
>
	MOVE	T3,[15,,12]
	GETTAB	T3,
	  HALT		;;GET UPTIME
	PUSH	P,T3
	SUB	T3,LASTIM(UX)
	MOVEM	T3,DELTAT
	POP	P,LASTIM(UX)
	MOVE	T1,DVFDAT+UNINAM
	PUSHJ	P,SIXOUT
	NXTPOS(^D15)
	THINGS
	CRLF
	AOJA	UX,LOOP1

ENDLUP:	SETZM	DVFSTR
	INCHWL	T1
	CAIE	T1,12
	JRST	.-2
	JRST	LOOP

DECOUT:	IDIVI	T1,^D10
	HRLM	T2,(P)
	SKIPE	T1
	PUSHJ	P,DECOUT
	HLRZ	T1,(P)
	OUTCHI	"0"(T1)
	AOS	CHRPOS
CPOPJ:	POPJ	P,

SIXOUT:	MOVE	T2,[POINT 6,T1]
SIXOU1:	ILDB	T3,T2
	OUTCHI	"0"-'0'(T3)
	AOS	CHRPOS
	TLNE	T2,770000
	JRST	SIXOU1
	POPJ	P,


DOCRLF:	SETZM	CHRPOS
	OUTSTR	[ASCIZ/
/]
	POPJ	P,

DOPOS:	CAMGE	T1,CHRPOS
	POPJ	P,
	OUTCHI	" "
	AOS	CHRPOS
	JRST	DOPOS

STROUT:	OUTSTR	(STR)
	HRLI	STR,440700
STROU1:	ILDB	T2,STR
	JUMPE	T2,CPOPJ
	AOS	CHRPOS
	JRST	STROU1


;CLEARED STORAGE

CLRBEG:
HEDFLG:	0
LASTIM:	BLOCK	NUMUNI
DELTAT:	0
CHRPOS:	0
CLREND==.-1

	LENDVF==UNIMAX+3
PDL:	BLOCK	PDLLEN
DVFBLK:	2		;GET DISK UNIT DATA BLOCKS
DVFSTR:	0		;START WITH ZERO
DVFCNT:	0
DVFDAT:	BLOCK	UNIMAX	;THE DATA.

	END	ST
