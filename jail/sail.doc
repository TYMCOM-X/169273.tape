Sail Addendum - October 1981                                  New Features


                                  SECTION  1

                                 New Features





     This section describes new and changed features in Version A of Sail.



     1.1 - Initialization Phase Numbers


     When explicitly declaring the initialization phase number, the user
     may now specify any value between -1000 and 1000.  Previously the
     allowed values were 0, 1, and 2.  As before, the default is 1 if no
     phase number is specified.



     1.2 - Type Conversion


     The conversion between LONG REAL and INTEGER has been improved so
     that it no longer uses REAL as an intermediate step.  Hence, more
     precision is retained.



     1.3 - Extension to TTYUP Routine


     The TTYUP routine has been extended to allow conversion from
     uppercase to lowercase.  This routine has the form

     OLDVAL := TTYUP (NEWVAL)

     where NEWVAL is an integer whose value has the following effect:  -1
     causes conversion of lowercase characters to their uppercase
     equivalents; 0 causes no conversion to occur; and 1 causes conversion
     from uppercase to lowercase.  Previously NEWVAL was interpreted as a
     boolean which caused conversion to uppercase if TRUE and no
     conversion if FALSE.  Since TRUE is defined to be -1, this extension
     is compatible with existing programs.










                                    1
Sail Addendum - October 1981                                  New Features


     1.4 - Nonstandard Byte Size Strings


     Most of the features described in this section were added primarily
     to allow the handling of strings that have a byte size other than the
     usual 7 bits.  In particular, this enables the processing of Katakana
     strings, which have a byte size of 9 bits.



                               BREAK CHARACTERS


     The modes listed below have been added to the BREAKSET command, which
     has the form

     BREAKSET (TABLE, "BREAK!CHARS", MODE)

     and the SETBREAK command, which has the form

     SETBREAK (TABLE, "BREAK!CHARS", "OMIT!CHARS", "MODES")


     GROUP 0 -- Conversion specifications

     MODE    FUNCTION

     "C"     (Convert to lowercase)  The uppercase letters (A-Z) will be
             converted to lowercase (a-z) before anything else is done.


     GROUP 1 -- Break character specifications

     MODE    FUNCTION

     "U"     (Un-omit)  Only those characters which are NOT contained in the
             String BREAK!CHARS will be omitted from the input string.

     "B"     (Break)  Characters that are both break and omitted characters
             will be treated as break characters.  Mode "B" is the default.

     "T"     (Token)  When characters that are both break and omitted
             characters are encountered at the beginning of the input string,
             they will be treated as omitted characters, but they will revert
             to their break property after the first non-omitted character is
             encountered.  (See examples below.)

     For example, consider the command:

     SETBREAK(WORD := GETBREAK, "ABCDEF", ".-", "XRT")

     The symbols "." and "-" are used here for readability; in a more
     practical example, they would be space and tab.  Suppose the input
     being scanned is:


                                    2
Sail Addendum - October 1981                                  New Features


     ABCD--......E.F..--..G-H+I

     This input would be read as indicated below.  Note that the break
     character appears in brackets (but is not in the input string), and
     the strings are separated by semicolons.

     ABCD[-]; E[.]; F[.]; G[-]; H[+]; repeated reads of NULL[+]

     First ABCD is read and a break occurs on the first "-", which is not
     appended to the input string.  On the next scan, that "-" and all
     succeeding characters up to the "E" are omitted, but as soon as the
     "E" is encountered, "." and "-" revert to their break property.  Thus
     a break occurs on the "." following "E" but it is not appended to the
     input string, so it is ignored at the beginning of the next scan, and
     so on.  Finally, a break occurs on "+", which is not an omitted
     character, so the next scan reads the null string, breaking on the
     same "+".  This reading of the null string happens repeatedly until
     the input process is interrupted in some other way.

     Note that if "A" mode is set and the break occurs on a character that
     is both a break and an omitted character, the character will be
     appended to the input string despite its omitted property.  It will,
     however, never appear as the first character of an input string.

     WARNING:  Input that stops because the count ran out may have stopped
     just before some omitted characters, so be careful when collecting
     strings with:

     S := INPUT(C, TBL)
     WHILE BRCHAR=0 DO S := S & INPUT(C, TBL)

     For example, assuming the same SETBREAK command as in the previous
     example, and a COUNT of 4, the input

     ABCD...EFAB...FEC...

     read by the above code would result in S being assigned the value
     "ABCDEFAB".


     GROUP 4 -- Breaktable size specification

     MODE      FUNCTION

     "1"-"9"   (Breaktable size)  The breaktable contains separate entries for
               each possible N-bit character, where N is the number specified
               here (or the default, "7").  In addition, there is an entry for
               "all others"; this entry is used if a byte encountered by the
               input routine cannot be represented in an N-bit byte (that is,
               its value is not in the range 0 to 2^N-1).  Since this mode
               changes the size of the breaktable, it should be the first mode
               specified in the BREAKSET or SETBREAK command.




                                    3
Sail Addendum - October 1981                                  New Features


     The entries in the breaktable indicate whether or not the character
     is a break character and whether or not it is an omitted character.
     The "all others" entries are changed indirectly when "I", "X", "O",
     or "U" mode is used, and directly whenever a value supplied in one of
     those modes exceeds the size of the breaktable it is to affect.

     GETBREAK returns tables initially set to 7 bits.


     These new breaktable features have necessitated other slight changes.
     SETBREAK now has a different definition, as shown below.  The basic
     difference is that the "omit" specification is done later in order to
     allow setting of the breaktable size first.

     BEGIN "SETBREAK"
     INTEGER MODE;
     WHILE LENGTH(MODES)
       DO IF "U" NEQ (MODE := LOP(MODES)) NEQ "u"
             THEN BREAKSET(TABLE, BREAK!CHARS, MODE)
             ELSE BEGIN
                  BREAKSET(TABLE, OMIT!CHARS, "U");
                  OMIT!CHARS := NULL
                  END;
     IF LENGTH(OMIT!CHARS) THEN BREAKSET(TABLE, OMIT!CHARS, "O")
     END "SETBREAK"

     Furthermore, since the new breaktables are in an incompatible format,
     the "standard breaktable file" has been renamed from "BKTBL.BKT" to
     "BKTBLS.BKT".



                            NOTES ON INPUT AND SCAN


     INPUT returns a string of the same byte size as the byte pointer in
     its buffer pointer.  At Tymshare, this means files opened in Katakana
     mode will return strings of 9-bit bytes.

     SCAN returns a string of the same byte size as its argument string.



                             STRING CONCATENATION


     The byte size of the result of a string concatenation is indicated
     below.








                                    4
Sail Addendum - October 1981                                  New Features


     null & anystring      Result is anystring.
     anystring & null

     null & integer        Result is a 7-bit string.
     integer & null
     integer & integer

     non-null & integer    Result has the same byte size as the string.
     integer & non-null

     non-null & non-null   Yields an error message if both arguments do
                           not have the same byte size, in which case the
                           byte size of the result is the maximum of the
                           two byte sizes.



                         STRING MANIPULATION ROUTINES


     The following string manipulation routines have been added to Sail to
     facilitate the handling of nonstandard byte size strings.


     ------------------------------  WIDTH  ------------------------------

     INTEGER := WIDTH ("STRING")

     If the length of the specified string is 0, WIDTH is 0, otherwise it
     is the byte size of the string.  WIDTH will be evaluated at compile
     time if possible.


     ------------------------------  CMCAT  ------------------------------

     "RESULT" := CMCAT ("STR1", "STR2")

     CMCAT concatenates STR1 and STR2 exactly as the "&" operator would,
     with the following exception:  When both arguments are non-null and
     have different byte sizes, no error occurs.  In this case the byte
     size of the result is the maximum of the two byte sizes.  As is the
     case with "&", concatenation of constant strings will be done at
     compile time where possible.


     ------------------------------  CHAR!W  -----------------------------

     "CHAR" := CHAR!W (VALUE, BYTE!SIZE)

     If BYTE!SIZE is between 1 and 36, CHAR!W returns a string of length 1
     and byte size BYTE!SIZE, consisting of the character represented by
     the low-order BYTE!SIZE bits of VALUE.  If BYTE!SIZE is 0 or omitted,
     7 is used.  If BYTE!SIZE is negative or greater than 36, an error
     occurs.


                                    5
Sail Addendum - October 1981                                  New Features


     ------------------------------  STR!W  ------------------------------

     "RESULT" := STR!W ("STRING", BYTE!SIZE)

     STR!W is analogous to CHAR!W for strings:  It returns a string of
     byte size BYTE!SIZE consisting of the characters in STRING.  It is
     logically equivalent to the following:

     BEGIN "STR!W"
     IF LENGTH(STRING) = 0 THEN RETURN(NULL);
     RESULT := CHAR!W(LOP(STRING), BYTE!SIZE);
     WHILE LENGTH(STRING) DO RESULT := RESULT & LOP(STRING);
     RETURN(RESULT)
     END "STR!W"


     ------------------------------  STMAKE  -----------------------------

     "STRING" := STMAKE(BYTE!POINTER, LEN, BYTE!SIZE)

     STMAKE makes a string whose byte size is BYTE!SIZE by getting LEN
     bytes from BYTE!POINTER.  If BYTE!SIZE is 0 or omitted, the byte size
     from BYTE!POINTER is used.  STMAKE is logically equivalent to the
     following:

     BEGIN "STMAKE"
     IF LEN LEQ 0 THEN RETURN(NULL);
     IF BYTE!SIZE = 0 THEN BYTE!SIZE := LDB(POINT(6, BYTE!POINTER, 12));
     STRING := CHAR!W(ILDB(BYTE!POINTER), BYTE!SIZE);
     FOR LEN := LEN-1 STEP -1 UNTIL 1
       DO STRING := STRING & ILDB(BYTE!POINTER);
     RETURN(STRING)
     END "STMAKE"



     1.5 - Other String Manipulation Routines


     In addition the the string manipulation routines described above, the
     following routines have been added to Sail.  All the routines
     described in this section are evaluated at compile time if possible.


     -------------------------------  KEQU  ------------------------------

     VALUE := KEQU ("STR1", "STR2")

     KEQU is the same as EQU except that it ignores case mismatches; that
     is, lowercase letters (a-z) are considered equal to their
     corresponding uppercase letters (A-Z).  Thus, KEQU("aBc", "AbC") is
     TRUE, whereas EQU("aBc", "AbC)" is FALSE.  KEQU is logically
     equivalent to the following:



                                    6
Sail Addendum - October 1981                                  New Features


     EQU(SCANC(STR1, NULL, NULL, "IK"), SCANC(STR2, NULL, NULL, "IK"))


     ------------------------------  CMPSTR  -----------------------------

     VALUE := CMPSTR ("STR1", "STR2")

     CMPSTR compares two strings (padding the shorter with zeroes if
     necessary) and returns information about which is "greater" and where
     the first different character is.  If the return value is 0, the
     strings are "equal"; note that this does not necessarily mean EQU
     would return TRUE for these strings, because EQU returns FALSE if the
     strings are different lengths.  If the return value of CMPSTR is
     negative, STR1 is "less" than STR2, and if it is positive, STR1 is
     "greater" than STR2.  The absolute value of the result is the first
     character position in which the two strings differ.

     Thus, to test the relation between two strings,

     STR1 <relation> STR2

     where relation is =, <, >, LEQ, GEQ, or NEQ, you would use:

     CMPSTR(STR1, STR2) <relation> 0

     The logical equivalent of CMPSTR is as follows:

     BEGIN "CMPSTR"
     INTEGER C1, C2, POS;
     C1 := LOP(STR1);
     C2 := LOP(STR2);
     FOR POS := 1 STEP 1 WHILE C1 = C2
       DO BEGIN
          IF LENGTH(STR1) = 0 = LENGTH(STR2) THEN RETURN(0);
          C1 := LOP(STR1);
          C2 := LOP(STR2)
          END;
     RETURN(IF C1 < C2 THEN -POS ELSE POS)
     END "CMPSTR"


     ------------------------------  KMPSTR  -----------------------------

     VALUE := KMPSTR ("STR1", "STR2")

     KMPSTR is a case-independent version of CMPSTR.  It behaves as if the
     two strings were forced to uppercase before the comparison.  Note,
     for example, that this means "[" ('133) is treated as greater than
     both "A" ('101) and "a" ('141).  KMPSTR is logically equivalent to:

     CMPSTR(SCANC(STR1, NULL, NULL, "IK"),  SCANC(STR2, NULL, NULL, "IK"))





                                    7
Sail Addendum - October 1981                                  New Features


     1.6 - Byte Manipulation Routines


     ------------------------------  ADJBP  ------------------------------

     VALUE := ADJBP (AMOUNT!TO!MOVE, BYTE!POINTER)

     ADJBP (ADJust Byte Pointer) returns a byte pointer that is equivalent
     to BYTE!POINTER advanced by AMOUNT!TO!MOVE bytes (or backed up, if
     AMOUNT!TO!MOVE is negative), preserving byte alignment (in either
     case).  This is a simulation of the KL instruction "ADJBP" for single
     word byte pointers, or is directly translated to it if the "40" bit
     of compiler switch A is on.  ADJBP is evaluated at compile time if
     possible.

     Briefly, "byte alignment" refers to the number of bits preceding the
     first byte of a word.  Most applications have bytes which begin at
     word boundaries (alignment=0), since ILDB, IDPB, and IBP all force
     the alignment to 0 at each word boundary.  In those cases, ADJBP is
     equivalent to doing the appropriate number of IBP instructions.  For
     a byte pointer constructed by POINT(S, SOME!VAR, N), the byte
     alignment is ((1+N) mod S).  When ADJBP is called with AMOUNT!TO!MOVE
     = 0, byte pointers of the form POINT(...,-1) are converted to point
     to the last byte in the previous word.  For further explanation,
     consult a KS or KL hardware reference manual.

     The simulation is correct if the following conditions hold:

     1: 0 < S < 37

         where S is the byte size of the byte pointer  [bits 6 through 11]

     2: P = 36 or -1 < P < 37-S

         where P is the "position" of the byte pointer [bits 0 through 5]
         (this is 35 minus the third argument to a POINT)

     3: The address part of the byte pointer does not "wrap around" as in
        ADJBP(100, POINT(5,MEMORY['777777], -1)).


     ------------------------------  BBPP  -------------------------------

     VALUE := BBPP (BYTE!SIZE, @EFFECTIVE!ADDRESS, LAST!BIT!NUMBER)

     BBPP is similar to POINT except that it may be compiled into a
     sequence of instructions (or even a constant) rather than a procedure
     call.








                                    8
Sail Addendum - October 1981                                  New Features


     1.7 - INTSET Default


     In the interrupt routine INTSET, which has the form

     INTSET(ITM, OPTIONS)

     the OPTIONS argument may now be omitted, in which case 0 is assumed.



     1.8 - Compiler Switch A


     The switch "A" now takes a new value for its (octal) argument, as
     follows:

             40  use ADJBP opcode, not UUO, for ADJBP routine



     1.9 - Extensions to GOGTAB


     There is a new GOGTAB entry "NOXTMS" which is a boolean flag.
     Setting this entry to TRUE will suppress the "End of SAIL execution"
     message that normally appears on program exit.

     The GOGTAB entry "ENDREN" will always be the last element of the
     GOGTAB array, so the appropriate way to get access to GOGTAB entries
     is now:

     ...
     REQUIRE "SYS:GOGTAB.DEF" SOURCE!FILE;
     EXTERNAL INTEGER ARRAY GOGTAB[0:ENDREN];
     ...




















                                    9
Sail Addendum - October 1981                                    Bugs Fixed


                                  SECTION  2

                                  Bugs Fixed





     These are some of the bugs that have been fixed in version A of Sail.

     1)  The compilation of a program with no BEGIN (as in an empty file)
         now does not yield an ILL MEM REF, but instead substitutes a
         dummy outer block name for the error message.

     2)  Up-level references of value string parameters previously failed
         in several cases.  This has been fixed.  Here is a sample program
         that exercises three distinct bugs in this area:

           BEGIN "STRING UPREF BUGS"
           BOOLEAN PROCEDURE SIMPLE!RRING A);
              BEGIN
              SIMPLE STRING PROCEDURE IP(STRING S); RETURN(A&"");
              RETURN(NOT EQU(IP("IN"), A))
              END;
           BOOLEAN PROCEDURE UP!RET(STRING A);
              BEGIN
              STRING PROCEDURE IP(STRING S); RETURN(A);
              RETURN(NOT EQU(IP("IN"), A))
              END;
           BOOLEAN PROCEDURE SIM!UP!RET(STRING A);
              BEGIN
              SIMPLE STRING PROCEDURE IP(STRING S); RETURN(A);
              RETURN(NOT EQU(IP("IN"), A))
              END;
           IF NOT SIMPLE!REF("OUTER-ARG")
              THEN OUTSTR("UP-REFERENCE OF VALSTR FROM SIMPLE BUG.");
           IF NOT UP!RET("OUTER-ARG")
              THEN OUTSTR("UP-REFERENCE OF VALSTR RESULT BUG.");
           IF NOT SIM!UP!RET("OUTER-ARG")
              THEN OUTSTR("UP-REFERENCE OF VALSTR RESULT:SIMPLE BUG.")
           END "STRING UPREF BUGS"

     3)  ASSIGNC had a few problems with the constant (1 ROT -1).  This
         has been fixed.

     4)  REQUIRE FOO INITIALIZATION, where FOO is declared EXTERNAL or
         FORWARD, now works.  (The FORWARD bit was checked in the wrong
         half.)

     5)  Error recovery:  If the left hand side of an assignment is
         untyped (which happens when a variable is not declared), a very
         good guess as to the proper type is made based on the right hand
         side of the statement.  The guess is basically the same type as



                                    10
Sail Addendum - October 1981                                    Bugs Fixed


         the right hand side, with attention paid to such things as
         procedures and so on.

     6)  LONG REAL "temps" would sometimes lose their longness, thus
         overriding other temps.  Now a temp is either LONG or not, and if
         it is LONG it cannot be used for integers.  The following program
         illustrates this bug:

             BEGIN "LONG TEMP BUG"
             BOOLEAN PROCEDURE TEMP!SIZE!CONFUSION;
                BEGIN "CONFUSION"
                LONG REAL PROCEDURE LONG!NOOP(LONG REAL V); RETURN(V);
                INTEGER PROCEDURE NOOP(INTEGER K); RETURN(K);
                LONG REAL L; INTEGER I;
                COMMENT First make and use two long real temps;
                L := LONG!NOOP(.1@@0)
                      XOR LONG!NOOP(LONG!NOOP(0@@0) XOR LONG!NOOP(0@@0));
                COMMENT Now two integer temps (did make temp size 1);
                I := NOOP(0) + NOOP(NOOP(0) + NOOP(0));
                RETURN(L NEQ .1@@0)
                END "CONFUSION";
             IF TEMP!SIZE!CONFUSION THEN OUTSTR("LONG TEMP SIZE BUG")
             END "LONG TEMP BUG";

         In the debugging version of the compiler, temps can have four
         distinct names, indicating their type:  TEMPxx, DTMPxx, TEMQxx,
         and DTMQxx (the Q means a second fixup was needed).

     7)  Internal consistency check:  "CLEARA" now checks to make sure it
         is only called with an AC value between 0 and 9.

     8)  Arithmetic:  Because of the PDP-10 architecture, Sail defers
         negation of computed values until the last possible moment.  This
         has been a problem because Sail assumes that f(-x)=-f(x) for too
         many functions.  The biggest problem in this class has been
         "convert real number to integer", an implicit conversion.  This
         has been fixed by making the compiler negate values before
         conversion when the conversion function does not satisfy the
         relation f(-x)=-f(x).  The following program illustrates this
         bug:

             BEGIN
             INTEGER INT; REAL X;
             X := 1.5; INT := -X; PRINT(" FIRST ", INT);
             X := -1.5; INT := X; PRINT(" THEN ", INT)
             END

     9)  Error handling:  An IF expression without an ELSE clause will now
         generate an error message to that effect, rather than the
         ubiquitous "SYNTAX ERROR" error message.

     10) Error recovery:  A program that had a statement beginning with
         "UNTIL" would cause the compiler to loop while attempting to



                                    11
Sail Addendum - October 1981                                    Bugs Fixed


         correct the error by inserting a semicolon before the "UNTIL".
         This problem has been fixed, and new error recovery code has been
         included for incorrect "WHILE ... DO ..." and "DO ... UNTIL ..."
         statements.

     11) Parametric procedures:  The former implementation of procedures
         as parameters did not work; procedures could loop looking for
         their parent or could find the wrong parent.  More error checking
         has been added to verify that procedures being passed as
         parameters have only REFERENCE parameters (since any use of
         parametric procedures presumes this).  The following program
         illustrates this bug:

             BEGIN "PROCEDURE PARAMETERS BUG"
             RECURSIVE PROCEDURE RECURSE(INTEGER DEPTH; PROCEDURE FOO);
                BEGIN
                PROCEDURE CHECK!DEPTH;
                IF DEPTH NEQ 2
                   THEN OUTSTR("PARAMETRIC PROCEDURE BUG IS BACK"&CRLF);
                IF DEPTH=2
                   THEN RECURSE(DEPTH-1,CHECK!DEPTH)
                   ELSE IF DEPTH=0
                           THEN FOO
                           ELSE RECURSE(DEPTH-1,FOO)
                END;
             PROCEDURE DUMMY; OUTSTR("???");
             RECURSE(3,DUMMY)
             END "PROCEDURE PARAMETERS BUG";

         In general, the old implementation did not work because there was
         not enough information passed for parametric procedures.
         Unfortunately, this means that the new implementation involves
         passing more information and is (very slightly) slower.  SIMPLE
         now has a meaning for procedure parameters.  Machine language
         debugging IS impacted (certain breakpoints should not be placed).
         Sail run-time procedures, user-defined SIMPLE procedures, and
         SIMPLE parameters may be passed to SIMPLE parameters; any
         procedure (or procedure parameter) may be passed to non-SIMPLE
         parameters.  Any attempt to "coerce" a procedure which is neither
         an run-time nor a SIMPLE procedure causes an error message
         (although an attempt is made to emit the proper code in any
         case).  The values placed on the run-time stack for SIMPLE and
         non-SIMPLE parameters are different (SIMPLE corresponding to the
         old implementation's value).  For more details see the
         implementation notes below.











                                    12
Sail Addendum - October 1981                                    Bugs Fixed


     Implementation notes:

                                       Old                 New
     stacked value for
     SIMPLE procedure            0,,entryAddress     0,,entryAddress

     typical call of
     SIMPLE parameter            PUSHJ P,@-1(RF)     PUSHJ P,@-1(RF)

     stacked value for
     non-SIMPLE procedure        0,,entryAddress     environ,,entryAddress

     typical call of                                 MOVE TEMP,-1(RF)
     non-SIMPLE parameter        PUSHJ P,@-1(RF)     PUSHJ P,.INVOK


         Environ is used to find the static parent of the procedure.  For
         a SIMPLE, outer-block, or run-time procedure, environ is 0.
         INTERNAL and EXTERNAL procedures are presumed to be outer-block.
         All other cases (non-SIMPLE procedures not declared in the outer
         block), environ is RF at the time of the binding of the actual
         procedure to a formal.  Note:  This is NOT recalculated when a
         procedure parameter is passed on as a parameter to yet another
         procedure.

         The rationale for this definition is that the static parent of a
         procedure can be calculated from the procedure that has static
         access to it, and is easily available there.  The environment is
         defined to be zero for those procedures which do not try to find
         their parent.

         .INVOK simply jumps to the named procedure if environ is 0.
         Otherwise, it looks at the first instruction of the procedure
         (following any JRSTs), and makes sure that the instruction is
         "PUSH P,RF" (issue error message and treat as if environ=0 if it
         is not that).  Because of this behavior, DDT (or RAID)
         breakpoints should be placed AFTER the "PUSH P,RF" instruction of
         a non-SIMPLE, non-outer-block procedure that will be called as a
         parameter.  If you go ahead and put them there, .INVOK may
         complain that the procedure being called is SIMPLE when it was
         told to call a non-SIMPLE procedure (normally the value stacked
         for a procedure will indicate that a SIMPLE call sequence is just
         fine).













                                    13
Sail Addendum - October 1981                                    Bugs Fixed


         .INVOK is (in pseudo-SAIL):

             If environ neq 0
                then begin
                     while LH(memory[entryAddress]) = JRST
                        do entryAddress := RH(memory[entryAddress]);
                     If memory[entryAddress] = PUSH P,RF
                        then start!code
                             PUSH P,RF;       ! save true RF;
                             MOVE RF,environ; ! set RF for parent-search;
                             JRST 1+entryAddress; ! avoid the PUSH instr;
                             end;
                     error("not really non-SIMPLE procedure")
                     end;
             start!code
                JRST entryAddress;
             end;

     12) LONG REAL parameters passed by VALUE were causing problems in
         code generation for procedures with OWN arrays.  The problem
         occurs when trying to issue code to jump around the storage for
         the array.  The following code was previously miscompiled:

             BEGIN "DECLARATION AVOIDANCE BUG"
             PROCEDURE BADSTART(LONG REAL V);
                BEGIN "BADSTART"
                OWN INTEGER ARRAY EXECUTED[0:1];
                OUTSTR("IT WORKS")
                END "BADSTART";
             BADSTART(0.0)
             END "DECLARATION AVOIDANCE BUG"

     13) The line and page counters associated with a file by SETPL were
         being updated whenever a line feed or form feed was read by
         INPUT.  This is not the correct behavior if the character is a
         break character and the break table specifies retain mode.  The
         following program illustrates this problem:



















                                    14
Sail Addendum - October 1981                                    Bugs Fixed


             BEGIN "SETPL TROUBLES"
             INTEGER VERTICAL!R, VERTICAL!S;
             INTEGER PAGE, LINE, SOS, CHAN, COUNT, BRK, EOF;
             INTEGER C;
             DEFINE CR="('15&NULL)", FF="('14&NULL)", LF="('12&NULL)";
             DEFINE CRLF="(CR & LF)";

             SETBREAK(VERTICAL!R := GETBREAK, LF & FF, NULL, "IR");
             SETBREAK(VERTICAL!S := GETBREAK, LF & FF, NULL, "IS");

             COMMENT Make a test file and set up for reading;
             OPEN(CHAN := GETCHAN, "DSK",0, 1,1, COUNT := 5, BRK, EOF);
             ENTER(CHAN, "TEST.TMP", EOF);
             OUT(CHAN, CRLF & FF & CRLF);
             CLOSE(CHAN);
             LOOKUP(CHAN, "TEST.TMP", EOF);
             SETPL(CHAN, LINE, PAGE, SOS);

             COMMENT now read it;
             FOR C := 1 STEP 1 UNTIL 50 DO INPUT(CHAN, VERTICAL!R);
             INPUT(CHAN, VERTICAL!S);
             PRINT("PAGE=", PAGE, ", LINE=", LINE, CRLF);
             COMMENT Bug: "PAGE=0, LINE=51",  good: "PAGE=0, LINE=1";

             FOR C := 1 STEP 1 UNTIL 50 DO INPUT(CHAN, VERTICAL!R);
             PRINT("PAGE=", PAGE, ", LINE=", LINE, CRLF);
             COMMENT Bug: "PAGE=50, LINE=0",  good: "PAGE=0, LINE=1";

             DO INPUT(CHAN, VERTICAL!S) UNTIL EOF;
             PRINT("PAGE=", PAGE, ", LINE=", LINE, CRLF);
             COMMENT Bug: "PAGE=51, LINE=1",  good: "PAGE=1, LINE=1";

             RENAME(CHAN, NULL, 0, EOF);
             RELEASE(CHAN)
             END "SETPL TROUBLES"





















                                    15
   U{b—