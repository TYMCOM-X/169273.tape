entry GetDev, FilErr;

BEGIN "File2"
    require "SAI:CSWIT.SAI" source!file;
    define HISEGGING=ifcr COMPILER!SWITCH!H thenc +1 elsec -1 endc;
    define !NOBAYSAILIB! = true;
    REQUIRE "SAI:BAYSAI" SOURCE!FILE;

!-------------------------------------------------
!
! ABSTRACT:
!	Some simple file I/O utilities.
!
!--------------------------------------------------;
! Date:  4-Aug-78
! Who: SAIL Files
! These are routines from FILE.SAI that are of use to anyone doing file I/O
! and hence have been removed from FILE.
! [MS30]
! 
! ;
!-------------------------------------------------
! This is a null design section.
!-------------------------------------------------;
source!l(File2);
internal simple string procedure GetDev(reference String Str;
	string DefaultDev("DSK"));
!-------------------------------------------------
!
! ABSTRACT:
!	Strips the device, if any, off the front of the file
!	specification.  If there is no device, it returns DefaultDev.
!
! PARAMETERS:
!    Str: Reference string which is the complete file specification.
!
!    DefaultDev: String that is the default device (defaults to "DSK").
!
! RESULT:
!	GetDev will return the string that represents the device name
!	(without a trailing colon).
!
! SIDE EFFECTS:
!	The device portion of Str will be removed (if it exists).
!	This includes the ":".
!
! ERRORS:
!
! DESIGN:
!
!-------------------------------------------------;

begin "GetDev"

integer BrkChr;

setbreak(Dum_getbreak,":"," "&TAB,"IKS");
SDum_scan(STR,Dum,BrkChr);
if BrkChr neq ":" then
	begin STR_SDum; SDum_DefaultDev end;
relbreak(Dum);
return(SDum);
end "GetDev";
internal simple string procedure FilErr(integer FLAG!);
!-------------------------------------------------
!
! ABSTRACT:
!	FilErr converts LOOKUP and ENTER error codes into English
!	error messages.
!
! PARAMETERS:
!   FLAG!: Integer that is returned by LOOKUP/ENTER.
!
! RESULTS:
!	The appropriate error message is returned as FilErr's string value.
!
! SIDE EFFECTS:
!
! ERRORS:
!
! DESIGN:
!
!-------------------------------------------------;
      begin "FilErr"
	string REPLY;

	FLAG!_FLAG! land '77777;	! Error codes are 15 bits maximum;
	if FLAG! = 0 then REPLY_"Non-existent file"
	else if FLAG! = 1 then REPLY_"Non-existent UFD"
	else if FLAG! = 2 then REPLY_"Protection failure"
	else if FLAG! = 3 then REPLY_"File in use"
	else if FLAG! = 4 then REPLY_"Filename already in use"
	else if FLAG! = 5 then REPLY_"Illegal UUO sequence"
	else if FLAG! = 6 then REPLY_"UFD or RIB error"
	else if FLAG! = '10 then REPLY_"Illegal filename syntax"
	else if FLAG! = '11 then REPLY_"Device not available"
	else if FLAG! = '12 then REPLY_"No such device"
	else if FLAG! = '14 then REPLY_"Structure full or quota exhausted"
	else if FLAG! = '15 then REPLY_"Structure write-locked"
	else if FLAG! = '16 then REPLY_"Monitor core tables full"
	else if FLAG! = '24 then REPLY_"Search list empty"
	else if FLAG! = '26 then REPLY_"No writable structure in search list"
	else	REPLY_"Error code '"&cvos(FLAG!);

	return(REPLY);
      end "FilErr";

end "File2";
