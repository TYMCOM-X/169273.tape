TITLE	.DATER - Convert LOOKUP block with GMT to DEC format
SUBTTL	Joe Smith, 2-May-85

	T4=1+<T3=1+<T2=1+<T1=1+<T0=0>>>>
	P=17

	OPDEF	DATUUO	[CALLI -55]	;Defined in TYMCOM-X
	.RBCNT==0
	.RBPPN==1
	.RBNAM==2
	.RBEXT==3
	.RBPRV==4

ENTRY	.DATER	;Call with pointer to LOOKUP block in T1

	TWOSEG
	RELOC 400000
.DATER:	HLLZ	T2,.RBCNT(T1)		;Check left half
	SKIPE	T2			;Zero for extended lookup block
	 SUBI	T1,<.RBNAM-.RBCNT>	;Short lookup block, no count
	LDB	T2,[POINT  ^D2,.RBEXT(T1),^D21] ;Get high-order 2 bits
	LSH	T2,^D12
	LDB	T3,[POINT ^D12,.RBPRV(T1),^D35] ;Get low-order 12 bits
	ADD	T2,T3			;Combine
	LDB	T3,[POINT ^D11,.RBPRV(T1),^D23] ;Get creation time
	IMULI	T3,^D<60*60>		;Convert minutes to jiffies
	HRLZI	T4,400000!20		;Date is TYMCOM format, GMT
	MOVEI	T0,T2			;Point to args
	DATUUO	T0,			;Convert
	  JFCL				;Error return never taken
	IDIVI	T3,^D<60*60>		;Convert jiffies to minutes
	DPB	T3,[POINT ^D11,.RBPRV(T1),^D23] ;Updated creation time
	DPB	T2,[POINT ^D12,.RBPRV(T1),^D35] ;Low order 12 bits
	LSH	T2,-^D12
	DPB	T2,[POINT ^D3,.RBEXT(T1),^D20] ;DEC's DATE-75 bits
	POPJ	P,

	END
