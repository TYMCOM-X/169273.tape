	TITLE	MAPPIO Disk I/O using paging.

	;-AC's
	
	T1=1
	T2=2
	T3=3
	T4=4
	P=17				;-Stack pointer

	;-Some definition

	PDLEN=50			;-Pushdown lenght
	INNCHN=1			;-Input channel
	OUTCHN=2			;-Output channel
	.CHMFP==35			;-Map file page
	.CHCFP==40			;-Create file page
	OPDEF	MONRET [EXIT 1,]	;-Return to monitor level

START:	RESET				;-Reset the world
	MOVE	P,[IOWD PDLEN,PDLIST]	;-Initilize the stack
	OPEN	INNCHN,[		;-Initilize channel for input
			17		
			SIXBIT	"DSK"
			0]
	  JRST	ERR1			;-Can't initilize channel
	OPEN	OUTCHN,[		;-Initilize channel for output
			17		
			SIXBIT	"DSK"
			0]
	  JRST	ERR1			;-Can't initilize channel
	LOOKUP	INNCHN,INNFIL		;-Do a long lookup for input file
	  JRST	ERR2			;-Can't do the lookup
	ENTER	OUTCHN,OUTFIL		;-Do a long enter for output file
	  JRST	ERR2			;-Can't do the enter

MAPVP:	MOVE	T1,[.CHMFP,,INNCHN]	;-Map the real file page
	HLLZ	T2,FDECRE		;- declared as in FDECRE
	HRR	T2,VPREAL		;- to virtual page# VPREAL
	MOVE	T3,FPAGNO		;- as file page# FPAGNO
	CHANIO	T1,T2			;-Do it
	  JRST 	ERR3			;-Can't map file page

MAPVP1:	MOVE	T1,[.CHCFP,,OUTCHN]	;-Create an empty disk file page for
	CHANIO	T1,[1]			;- file page# FPAGNO
	  JRST	ERR4			;-Can't create file page

MAPVP2:	MOVE	T1,[.CHMFP,,OUTCHN]	;-Map the empty file page
	HLLZ	T2,FDECDU		;- declared as in FDECDU
	HRR	T2,VPDUMY		;- to virtual page# VPDUMY
	MOVE	T3,FPAGNO		;- as file page# FPAGNO
	CHANIO	T1,T2			;-Do it
	  JRST	ERR3			;-Can't map file page

MAPVP3:	HRLZ	T1,VPREAL
	HRR	T1,VPDUMY
	LSH	T1,^D9
	BLT	T1,11777

MAPVP4:	MOVE	T1,FDECVC
	HRR	T1,VPREAL
	VCLEAR	T1,
	  JRST  ERR5

	CLOSE	INNCHN,
	CLOSE	OUTCHN,
	RELEAS	INNCHN,
	RELEAS	OUTCHN,
	MONRET				;-Done. Go back to monitor level

ERR1:	OUTSTR	[ASCIZ "
Can't initilize I/O channel...
"]
	MONRET
ERR2:	OUTSTR	[ASCIZ /
Can,t do LOOKUP or ENTER...
/]
	MONRET
ERR3:	OUTSTR	[ASCIZ /
Can,t map virtual page, .CHMFP failure...
/]
	MONRET
ERR4:	OUTSTR	[ASCIZ /
Can't create file page, .CHCFP failure...
/]
	MONRET
ERR5:	OUTSTR	[ASCIZ /
VCLEAR error...
/]
	MONRET

INNFIL:	0,,5
	0
	SIXBIT	"FILE"
	SIXBIT	"DAT"
	0	
	0

OUTFIL:	0,,5
	0
	SIXBIT	"FILE"
	SIXBIT	"MAP"
	0
	0

PDLIST:	BLOCK	PDLEN
FDECRE:	0B1+1B7+1B17
FDECDU:	0B1+3B7+1B17
FDECVC:	0B1+1B7+2B17
VPREAL:	10
VPDUMY:	11
FPAGNO:	1
ONEPAG:	777

	END	START
