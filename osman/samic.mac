TITLE SAMIC - SA10 MICROCODE
SUBTTL CONFIGURATION INFO

;CONCATENATE THIS ASSEMBLY WITH EITHER SAMICA.MAC OR SAMICB.MAC
;FOR RELEVANT VERSION OF SA10.

SALL

DEFINE ND(VAR,VAL)<IFNDEF VAR,<VAR==VAL>>

ND NCHN,4	;# OF SUBCHANNELS
ND EADR,0	;1 FOR 22-BIT ADDRESSING
ND KAMEM,0	;1 FOR 18-BIT ADDRESSING
ND CFHQEE,0	;1 IF HALF-QUARTER-EIGHTH-EIGHTH
ND MUXCRD,0	;1 IF SPECIAL MPX TEST CARD IN

ND CV,1		;PANEL TYPE: -=A, 0=B, +=C
IFL CV,<CNOB==1>	;A PANEL MEANS A VERSION
IFDEF CNOB,<
CHASSI==4-EADR-EADR	;4 OR 2
IFG CNOB,<
CHASSI==1
>>

DEFINE CNFBIT(POS,VAL)<CNFBTS==CNFBTS!<<VAL>_<^D35-^D'POS>>>
CNFBTS==0

CNFBIT 0,NCHN/4
CNFBIT 1,CFHQEE
CNFBIT 3,EADR
CNFBIT 4,1-KAMEM
CNFBIT 10,CHASSI
CNFBIT 18,MUXCRD

CONFIG::CNFBTS
SUBTTL MICROINSTRUCTION FIELD DEFS

R0==0
R1==10000
R2==20000
R3==30000
B0==0
B1==1
B2==2
B3==3
B4==4
SW1==5
BUSOUT==5
FOFI==6
FOFIS==7
BUSIN==10
STRD==10
STRDC==11
TAGB==12
STWR==12
INTST==13
STWRD==13
ZERO==14
LOAD==14
MRB==15
WHOLE==15
FIFO==16
B14==16
FIFOS==17

WC==0
CA==2000
PC==4000
A3==6000
IARB==0
INCR==0
WCFLD==1
ADRFLD==2
MAGIC==3
DECR==1
CLRINS==400
CLRTAG==0
STGO==10000
STRQ==10400
STFLG==11000
STPIE==11400
ADROUT==20000
SELOUT==20400
SRVOUT==21000
NOPOUT==21400
SUPOUT==22000
CMDOUT==22400
WRBIT==23000
BUFENB==23400
INS0==30000
INS1==30400
SELERR==31000
CTBYT==31400
CTLERR==32000
SKPFLG==32400
LENERR==33000
PCIFLG==33400
BIT00==0
BIT01==1
BIT10==20
BIT11==21
BIT20==40
BIT21==41
BIT30==60
BIT31==61
BIT40==100
BIT41==101
BIT50==120
BIT51==121
BIT60==140
BIT61==141
BIT70==160
BIT71==161
Z==200
NZ==201
ERR==221
NERR==220
DEVM==240
RBAK==260
NBTM==300
IFN EADR,<SKPC==320>
IFE EADR,<SKPC==Z>
TAGBS==TAGB_10
ADRI0==TAGBS+BIT00
ADRI1==TAGBS+BIT01
SELI0==TAGBS+BIT10
SELI1==TAGBS+BIT11
BFMT0==TAGBS+BIT20
BFMT1==TAGBS+BIT21
OPLI0==TAGBS+BIT30
OPLI1==TAGBS+BIT31
RQI0==TAGBS+BIT40
RQI1==TAGBS+BIT41
STAI0==TAGBS+BIT50
STAI1==TAGBS+BIT51
BCRQ0==TAGBS+BIT60
BCRQ1==TAGBS+BIT61
BFHLT0==TAGBS+BIT71
BFHLT1==TAGBS+BIT70
INSTS==INTST_10
INS00==INSTS+BIT00
INS01==INSTS+BIT01
INS10==INSTS+BIT10
INS11==INSTS+BIT11
PCIF0==INSTS+BIT70
PCIF1==INSTS+BIT71
SW1S==SW1_10
BSY0==SW1S+BIT00
BSY1==SW1S+BIT01
STRQ0==SW1S+BIT10
STRQ1==SW1S+BIT11
STFL0==SW1S+BIT20
STFL1==SW1S+BIT21
WCOK0==SW1S+BIT40
WCOK1==SW1S+BIT41
TIMER0==SW1S+BIT50
TIMER1==SW1S+BIT51
SUBTTL MICROINSTRUCTION MACROS

DEFINE MPAGE
<IFN .&17,<.PRINT <BADMPG>>>

DEFINE ADRSET (NAME)
<IFIDN <NAME>,<>,<ADRF=.+1>
IFDIF <NAME>,<>,<ADRF=NAME>
IF2,<IFN <.&1400>-<ADRF&1400>,<.PRINT <BNDER1>>>
ADR=ADRF&377
>
DEFINE PULSE (BIT,NAME)
<ADRSET NAME
ADRF,,BIT+2000+ADR
>
DEFINE ON (BIT,NAME)
<ADRSET NAME
ADRF,,BIT+4000+ADR
>
DEFINE OFF (BIT,NAME)
<ADRSET NAME
ADRF,,BIT+ADR
>
DEFINE ADRST1 (NAME)
<IFIDN <NAME>,<>,<ADRF=.+1>
IFDIF <NAME>,<>,<ADRF=NAME>
IF2,<IFN <.&1760>-<ADRF&1760>,<.PRINT <BNDERR>>>
ADR=ADRF&17
>
DEFINE XFER (REG,SRC,DEST,NAME)
<ADRST1 NAME
ADRF,,140000+REG+<SRC_10>+<DEST_4>+ADR
>
DEFINE XANC (SRC1,SRC2,REG,DEST,NAME)
<ADRST1 NAME
ADRF,,040000+REG+SRC1+<SRC2_10>+<DEST_4>+ADR
>
DEFINE GOTO (NAME)
<NAME,,NAME
>
DEFINE TEST (REG,SRC,FN,NAME)
<ADRST1 NAME
ADRF,,100000+REG+<SRC_10>+FN^!ADR
>
DEFINE HANG (REG,SRC,FN)
<IFN .&1,<.PRINT <HNGERR>>	;;MUST BE EVEN LOC
ADRF,,100000+REG+<SRC_10>+FN+.&16
>
SUBTTL FORCED TRANSFER VECTOR & RESETS

MCBEG0::PHASE 0

	PULSE CLRINS,RST	;SYS
	ON ADROUT,HIO	;HALT I/O
	PULSE CLRINS,RST	;PROG SYS
	ON INS0,RST	;PROG SEL
	REPEAT 4,<ON INS0,RST>	;PANIC
	REPEAT 10,<0>
	MPAGE

RST:!	OFF STGO
	OFF STFLG
	PULSE CLRTAG
	TEST 0,0,INS01,RS1
RS1:!	ON SUPOUT
	ON NOPOUT
	HANG 0,0,TIMER0
	XFER R0,ZERO,WHOLE
	HANG 0,0,TIMER1
	OFF STRQ
	HANG 0,0,TIMER0
	TEST 0,0,INS00,RS2
RS2:!	OFF NOPOUT,IDLE
HIO1:!	XFER R0,ZERO,WHOLE
	HANG 0,0,OPLI1
	OFF NOPOUT,RS3
	MPAGE

SUBTTL IDLE LOOP & ASYNC STATUS POLLING

HIO:!	OFF SELOUT,HIO1
ASC1:!	TEST R1,B1,BIT10,ASC1A
ASC1A:!	XANC PC,INCR,0,LOAD	;SKIP A CMD IF DEVEND+STAMOD ON DCC
	GOTO ASC1B
IDLE:!	PULSE CLRINS
	XFER R1,ZERO,WHOLE
IDL1:!	TEST 0,0,BSY1,IDL2
	ON SELOUT,ASY1	;DO A POLL CYCLE
IDL2:!	TEST 0,0,STRQ1,IDL3
	OFF STGO,GO0	;START COMMAND
IDL3:!	TEST 0,0,RQI1,IDL1
	GOTO STR	;DUMMY STATUS REQUEST
RS3:!	PULSE CLRTAG
	PULSE CLRINS
	ON CTLERR
	ON INS0,STR	;TERMINATE A SEL RST WITH A CTRL CHECK INT
	MPAGE


ASY1:!	TEST 0,0,OPLI1,ASY2
	OFF SELOUT,ASYU	;GOT SELECT IN ON POLL - REQ IN MUST HAVE DROPPED
ASY2:!	TEST 0,0,SELI1,ASY1
	ON INS1,ASY3	;POLL SUCCESSFUL
HANGPC::!			;PC FOR HANGING SUBCHANNEL IN STATIC TEST
ASY3:!	HANG 0,0,ADRI0
	XFER R1,BUSIN,B2,ASY4	;GET ADDR
ASY5:!	HANG 0,0,ADRI1
	OFF CMDOUT
	HANG 0,0,STAI0
	XFER R1,BUSIN,B1	;GET THE STATUS
	OFF SELOUT,AST	;AND GO TEST FOR DCC
ASY4:!	ON CMDOUT,ASY5
ASYU:!	HANG 0,0,SELI1
	GOTO IDLE
ASP1:!	HANG 0,0,OPLI1
	GOTO STR
	MPAGE

SUBTTL ASYNC STATUS BMUX SCAN & BMUX ACTIVATION

AST:!	TEST R1,B1,NERR,AST1
AST3B:!	TEST R2,B0,BIT01,AST4
AST1:!	ON SRVOUT,ASP	;ERR IN STATUS, GIVE TO PROG
	TEST R1,B1,BIT51,AST2
AST2:!	ON SRVOUT,ASP	;NO DEVEND IN STATUS,GIVE TO PROG
	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,STRD
	GOTO AST3
AST3A:!	TEST R2,B1,BIT01,AST4
	TEST R2,B1,BIT30,AST6	;MATCHING DEV CODE FOUND, CHK WAIT
AST4:!	ON SRVOUT,ASP	;SEL MODE OR END OF LIST,GIVE TO PROG
	XANC A3,INCR,0,STRD
	XFER R2,MRB,WHOLE	;DEV TABLE ENTRY TO R2
AST5:!	TEST R1,0,DEVM,AST3A
AST6:!	ON SRVOUT,ASP	;START OR HALTED, GIVE TO PROG
	ON SUPOUT,ASC	;ALL TESTS PASSED, INDICATE CHAINING
	MPAGE

ASC:!	ON SRVOUT,ASC1
ASC3:!	PULSE CLRINS,FET
ASC1B:!	HANG 0,0,STAI1
	OFF SRVOUT,ASC1C
CMD12:!	GOTO XD4
	0
ASC2:!	XANC A3,DECR,0,STWRD	;BACK UP DEVLST PNTR
	XANC A3,B1,R0,B2	;COPY INTO DEVLST ENTRY
	XANC A3,B2,R0,B3
	XANC A3,B3,R0,B14
	XANC CA,INCR,0,LOAD
	XANC CA,INCR,0,LOAD	;BASE+3
	XANC CA,INCR,R0,STWR	;STORE PTR TO DEVLST
ASC2A:!	XANC PC,ADRFLD,R2,LOAD,ASC3	;SET UP PC
ASP:!	HANG 0,0,STAI1
	OFF SRVOUT,ASP1
	MPAGE

SUBTTL COMMAND FETCH & DECODE, INITIAL SELECTION

FET:!	XANC PC,INCR,0,STRD
FET0:!	ON INS0
	XFER R0,MRB,WHOLE	;GET CMD
	TEST R0,B0,BIT21,FET1
FET1:!	TEST R0,B0,BIT11,FET2	;SP CMD
	XFER R0,B2,BUSOUT	;DEV CMD
	ON ADROUT,INI2
FET4:!	TEST R0,B0,BIT01,FET5
FET2:!	TEST R0,B0,BIT31,FET3
	XANC PC,ADRFLD,R0,LOAD,FET	;TCH
FET3:!	ON INS1,INERR	;HALT
	XANC CA,ADRFLD,R0,LOAD,FET4	;LOAD OR STORE
FET5:!	XANC CA,INCR,0,STRD,FET6	;LOAD
	XANC CA,INCR,R2,STWR,FET	;STORE
FET6:!	XFER R2,MRB,WHOLE,FET
STR1:!	OFF STRQ,STX
MPAGE

INI6:!	HANG 0,0,SELI1
	OFF ADROUT,INI6A
MERR:!	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,STRD
	ON PCIFLG,MERR1
INI5A:!	OFF SELOUT,INI7
INI2:!	ON SELOUT
INI2A:!	XFER R1,MRB,B2,INI5	;SAVE DEVADR IN STAT FOR CU BSY ETC
INI3:!	TEST 0,0,OPLI1,INI4
	OFF SELOUT,INI6	;NOT OPER
INI4:!	TEST 0,0,STAI1,INI5
	OFF ADROUT,CMD	;NORMAL SELECTION
INI5:!	TEST 0,0,SELI1,INI3
	XFER R1,BUSIN,B1,INI5A	;CU BUSY SEQ, GET STATUS
INI7:!	HANG 0,0,STAI1
	OFF ADROUT,INERR
	MPAGE

SUBTTL COMMAND TO DEVICE, INIT STAT PROCESSING, XFER SETUP

CMD:!	OFF SUPOUT,CMD1
CMD2:!	TEST R0,B0,NBTM,CMD3
CMD1:!	HANG 0,0,ADRI0
	XFER R1,BUSIN,B2	;GET ECHOED ADDR
	XFER R0,B1,BUSOUT	;PUT OUT CMD BYTE
	ON CMDOUT,CMD2
CMD3:!	ON CTBYT
	XANC PC,B1,R2,B2
	HANG 0,0,ADRI1	;WAIT FOR CMD ACCEPTED
	OFF CMDOUT,CMD4
CMD10:!	HANG 0,0,STAI0
	TEST 0,BUSIN,Z,CMD11
CMD11:!	GOTO ED
	ON SRVOUT	;ACCEPT INITIAL STATUS OF 0 (NMT)
	HANG 0,0,STAI1
	OFF SRVOUT,CMD12
	MPAGE

CMD4:!	TEST R0,B1,NZ,CMD5
CMD8:!	GOTO DAT
CMD5:!	GOTO ESW	;CMD WAS TEST I/O
	XANC PC,B2,R2,B3
	XANC PC,B3,R2,B14
	TEST R0,B0,BIT00,CMD6
CMD6:!	XANC A3,ADRFLD,R2,LOAD,CMD9	;NMT, SETUP FOR RETRY
	XANC PC,INCR,0,STRD	;START FIRST DCW FET
	XANC A3,ADRFLD,R2,LOAD	;SETUP SAVED PC FOR RETRY
	TEST R0,B1,BIT71,CMD7
CMD7:!	OFF WRBIT,CMD8
	ON WRBIT,CMD8
CMD9:!	ON WRBIT
	ON INS1,CMD10	;MAKE BUFFER NO AVAIL TO DEVICE
INI6A:!	ON SELERR,INERR
INERR:!	OFF SUPOUT,MERR
	MPAGE

SUBTTL STATUS PRESENTATION TO PROG

STR:!	XFER R1,INTST,B0	;STORE STATUS ROUTINE
	XANC WC,B1,R2,B0
	XANC WC,B2,R2,B1
	XANC PC,B1,R2,B2
	XANC PC,B2,R2,B3
	XANC PC,B3,R2,B14
	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,LOAD
	HANG 0,0,STFL1
	XANC CA,INCR,R1,STWR
	XANC CA,INCR,R2,STWR
	XFER R2,MRB,WHOLE	;WAIT FOR LAST STORE DONE
	ON STFLG,STR1
STX2:!	TEST R0,B0,BIT21,STX3
STX3:!	PULSE CLRINS,FET	;NORMAL PCI
	GOTO SIT	;PCI WITH CHNL END AND DCC
	MPAGE

STX8:!	TEST R2,B0,BIT10,STX9
STX:!	TEST 0,0,PCIF1,STX1
STX1:!	TEST 0,0,INS00,STX4
	OFF PCIFLG,STX2
STX4:!	TEST 0,0,INS10,STX5	;ASYNC OR DUMMY
	TEST R0,B0,BIT21,STX6	;INI OR END
STX5:!	XANC CA,MAGIC,0,LOAD,STX7	;DUMMY
	GOTO STX9	;ASYNC
STX6:!	TEST R0,B0,BIT01,STX6A	;HLT
	GOTO STX9	;NO A HLT
STX6A:!	GOTO STX9	;SLOW HALT
	GOTO SIT	;FAST HLT
STX7:!	XANC CA,INCR,0,STRD
	XFER R2,MRB,WHOLE,STX8
STX9:!	HANG 0,0,STFL1
	GOTO IDLE
	MPAGE

SUBTTL BMUX TERM & DCC PROCESSING

MERR1:!	XFER R2,MRB,WHOLE
	TEST R2,B0,BIT01,MERR2
MERR2:!	OFF PCIFLG,STR	;SEL MODE, JUST STORE
MRK:!	XANC CA,INCR,0,LOAD	;BM
	XANC CA,INCR,0,LOAD
	XANC CA,INCR,0,STRD	;GET SAVED PTR TO DEVLST
	XFER R3,MRB,WHOLE
	XANC A3,ADRFLD,R3,LOAD
	XANC A3,INCR,0,STRD	;GET DEVLST ENTRY
	XANC A3,ADRFLD,R3,LOAD	;SET A3 BACK FOR REWRITE
	XFER R2,MRB,WHOLE
	XANC PC,B1,R2,B2
	XANC PC,B2,R2,B3
	XANC CA,WCFLD,0,LOAD
	GOTO MRK1
	0
	MPAGE

MRK1:!	TEST 0,0,PCIF0,MRK2
CMD7A:!	GOTO DAT
MRK2:!	XANC CA,INCR,0,LOAD	;SET CA FOR TERM
	XANC CA,B3,R2,B1,MRK2A
MRK2B:!	XANC A3,INCR,R2,STWR	;STORE UPDATED PTR
	TEST 0,0,PCIF0,MRK3
MRK3:!	OFF PCIFLG,STR	;MARK TERM DONE
	TEST R0,B0,BIT50,MRK4	;MARK HUNG DONE
MRK4:!	ON PCIFLG,STR	;PCI W/DCC CHNL END
SIT:!	TEST R3,B1,BIT01,SIT1	;TEST FOR ASYNC OR GOFLG STRT
SIT1:!	GOTO IDLE	;ASYNC START
	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,LOAD	;SET CA FOR ASC
	GOTO GO2
MRK2A:!	XANC PC,B3,R2,B14,MRK2B
	0
	MPAGE

SUBTTL STARTUP, BMUX START SCAN, CMD COMPLETION

GO0:!	XANC CA,MAGIC,0,LOAD
	XANC CA,INCR,0,STRD
	XFER R2,MRB,WHOLE
	TEST R2,B0,BIT01,GO1
GO1:!	GOTO ASC2A	;SEL MODE
	XANC A3,ADRFLD,R2,LOAD,GO2	;BM
GO3:!	GOTO IDLE	;END OF LIST
	TEST R2,B1,BIT21,GO4
GO4:!	XFER R0,MRB,WHOLE,GO4A	;START FOUND,MAKE R0 NEG
GO2:!	XANC A3,INCR,0,STRD	;KEEP LOOKING
	XFER R2,MRB,WHOLE
	TEST R2,B1,BIT01,GO3
GO5:!	GOTO ASC2	;CA:!BASE+1, A3:!DEVLSTPT+1, R2:!CMD ADR
GO4A:!	XFER R1,MRB,B0	;PUT DEVADR INTO STATUS WORD
	XFER R1,B0,B2	;GET IT IN THE CORRECT BYTE
	XFER R1,ZERO,B1,GO5	;AND CLEAR BYTE 1
	MPAGE

DUN:!	TEST 0,0,PCIF1,DUN1
DUN3:!	ON PCIFLG,STR	;DO PCI
DUN1:!	TEST R0,B0,BIT51,DUN2	;NOT DCC
	OFF PCIFLG,DUN4	;DCC
DUN2:!	PULSE CLRINS,FET	;NOT PCI
	XFER R0,ZERO,WHOLE,DUN3	;PCI, CAUSE RET TO FETCH
DUN4:!	OFF SUPOUT,MRK	;MARKS HUNG
AST3:!	XFER R2,MRB,WHOLE	;BLK MUX FLAG TO R2
	XANC A3,ADRFLD,R2,LOAD
	GOTO AST3B
	0
	0
ASC1C:!	HANG 0,0,OPLI1
	XFER R0,ZERO,WHOLE
	GOTO GO4A
	0
	MPAGE

SUBTTL ENDING STATUS PROCESSING

ED:!	TEST 0,0,OPLI1,ED1
IMM:!	TEST 0,INTST,BIT31,ES1
ES1:!	TEST 0,INTST,BIT61,ES1A	;NO PARERR
	GOTO ES7	;BUS IN PAR ERR
ES1A:!	GOTO ES2	;NO LEN ERR
	TEST R0,B0,BIT61,ES1B	;TEST SLI
ES1B:!	GOTO ES3B	;HALT ON LEN ERR
	GOTO ES2	;IGNORE LEN ERR
ED1:!	ON CTLERR,ED1A	;END STATUS MISSING
	XFER R1,BUSIN,B1,IMM
ESWI:!	HANG 0,0,STAI1
	OFF SRVOUT
ESW:!	TEST 0,0,OPLI1,ESW1
ES0:!	XFER R1,BUSIN,B1,IMM	;MORE STATUS
ESW1:!	GOTO DUN	;NORMAL DISCONNECT
	TEST 0,0,STAI1,ESW
	MPAGE

ES2:!	TEST 0,BUSIN,ERR,ES3
ES8A:!	XANC PC,ADRFLD,R2,LOAD,ES8B	;CHAIN BACK TO PREV CMD
ES3:!	TEST R0,B0,BIT31,ES4	;NO BAD BITS
ES3B:!	TEST 0,BUSIN,BIT61,ES7	;BAD BITS
ES4:!	OFF SELOUT,ES5	;NO CHAINING
ES4A:!	ON SUPOUT,ES6	;CHAINING
ES7:!	OFF SUPOUT,CMDE	;NO UNIT CHECK
	TEST 0,BUSIN,BIT11,ES8	;UN CHECK
ES8:!	OFF SUPOUT,CMDE	;NO STA MOD
	XANC A3,B1,R2,B2	;RETRY STATUS
	XANC A3,B2,R2,B3
	XANC A3,B3,R2,B14,ES8A
ES8B:!	OFF LENERR
	XANC PC,DECR,0,STWRD,ES4A
CMDE:!	OFF SELOUT
	ON SRVOUT,CMDE1
	MPAGE

SUBTTL ENDING STATUS PROCESSING, READ MODE DECODE, XFER COMPLETION

ES6:!	TEST 0,BUSIN,BIT51,ES6A
ES9A:!	TEST R2,B0,BIT01,ES5
ES6A:!	XANC CA,MAGIC,0,LOAD,ES9	;NO DEV END
	TEST 0,BUSIN,BIT11,ES6B	;DEV END
ES6B:!	OFF SELOUT,ESF	;DEVEND W/O STAMOD
	TEST 0,BUSIN,BIT61,ES6C	;STA MOD
ES6C:!	XANC PC,INCR,0,LOAD	;DEVEND, STAMOD, NOT UNCHK
	OFF SELOUT,ESF	;UNCHK OVERRIDES SKIP
ES9:!	XANC CA,INCR,0,STRD
	XFER R2,MRB,WHOLE,ES9A
ES5:!	ON SRVOUT,ESWI
	ON PCIFLG,ES4
XD2D:!	HANG 0,0,BFHLT0
	ON LENERR,XD2C
	0
	0
	MPAGE

DSRO:!	TEST R3,B2,SKPC,DSRO1
XD3:!	TEST R0,B1,BIT71,XD3A
DSRO1:!	TEST R0,B1,RBAK,DSRO3	;NOT SKIP
	TEST R3,B3,Z,DSRO2	;POSS SKIP
DSRO2:!	TEST R0,B1,RBAK,DSRO3	;NOT SKIP
	GOTO SK6	;SKIP
DSRO3:!	GOTO RD6	;RD FWD
	TEST R0,B0,BIT41,DSRO4	;RD BAK
DSRO4:!	GOTO RB6	;RD BAK WD
	GOTO RBB	;RD BAK BYTE
XD3A:!	GOTO XD2	;DON'T DO ANYTHING SPECIAL IF READ
	XANC WC,WCFLD,0,LOAD,XD3B	;FORCE WCOK
XD4:!	XFER R3,ZERO,WHOLE,XD3D	;SET TO CLE@R WC
XD3B:!	XFER R3,ZERO,WHOLE,XD3C	;CLEAR TSD0
XD3D:!	XANC WC,ADRFLD,R3,LOAD,XD3A	;FORCE WC ZERO
XD3C:!	XFER R2,B4,FOFI,XD3D	;WRITE LAST BYTE (HALF FULL)
	MPAGE

SUBTTL XFER MODE DECODE

DAT2:!	HANG 0,0,STAI0
	TEST 0,BUSIN,Z,DAT3	;TEST INITIAL STATUS
DAT3:!	OFF INS1,XD1D	;NOT ZERO, NO XFER
	TEST R0,B1,BIT71,DAT4	;TEST WRITE
DAT4:!	ON SRVOUT,DAT5	;ACCEPT ZERO STATUS ON READ
	XANC CA,INCR,0,STRD	;FETCH FIRST WORD TO WRITE
	TEST R0,B0,TCCOND,DAT6	;TEST TAPE COMPAT MODE
	0
DAT6:!	GOTO DSW0	;NOT TAPE COMPAT
	XFER R2,ZERO,WHOLE	;FOR ZERO BITS IN 9 TRACK MODE
	OFF CTBYT	;MAY HAVE BEEN SET IF 9 TRACK
	GOTO DXW
DAT5:!	HANG 0,0,STAI1
	TEST R0,B0,TCCOND,DAT5A
DAT5A:!	OFF SRVOUT,DSR
	GOTO DXR	;TAPE COMPAT READ
	MPAGE

DSR:!	TEST R3,B2,SKPC,DSR1
DSR6A:!	GOTO SK6
DSR1:!	TEST R0,B1,RBAK,DSR3	;NOT SKIP
	TEST R3,B3,Z,DSR2	;POSS SKIP
DSR2:!	TEST R0,B1,RBAK,DSR3	;NOT SKIP
	TEST R0,B1,RBAK,DSR4	;SKIP
DSR3:!	GOTO RD0	;RD FWD
	TEST R0,B0,BIT71,DSR5	;RD BAK
DSR4:!	GOTO SK0	;SKP FWD
	TEST R0,B0,BIT71,DSR6	;SKP BAK
DSR5:!	TEST R0,B0,BIT41,DSR7	;RD BAK EVEN
	GOTO RB11	;RD BAK ODD
DSR6:!	GOTO SK0	;SKP BAK EVEN
	XFER R2,FIFO,B4,DSR6A	;SKP BAK ODD
DSR7:!	GOTO RB0	;RD BAK WD
	GOTO RBB	;RD BAK BYTE
	MPAGE

SUBTTL READ FWD/BAK

RD1:!	XFER R2,FIFO,B1,RD2
RDX:!	GOTO DEE
RD2:!	XFER R2,FIFO,B2,RD3
RD5:!	XANC CA,INCR,R2,STWR,RD6
	0
RD3:!	XFER R2,FIFO,B3,RD4
RD4:!	XFER R2,FIFO,B4,RD5
RD10:!	XANC CA,INCR,R3,STWR,RD0
RD7:!	XFER R3,FIFO,B1,RD8
RDX2:!	GOTO DEO
RD8:!	XFER R3,FIFO,B2,RD9
RD0:!	XFER R2,FIFO,B0,RD1
	0
	0
RD9:!	XFER R3,FIFO,B3,RD10
RD6:!	XFER R3,FIFO,B0,RD7
	MPAGE

RB1:!	XFER R3,FIFO,B2,RB2
RBX:!	GOTO DEE
RB2:!	XFER R3,FIFO,B1,RB3
RB5:!	XANC CA,DECR,R3,STWR,RB6
	0
RB3:!	XFER R3,FIFO,B0,RB4
RB4:!	XFER R2,FIFO,B4,RB5
RB10:!	XANC CA,DECR,R2,STWR,RB0
RB7:!	XFER R2,FIFO,B2,RB8
RBX2:!	GOTO DEO
RB8:!	XFER R2,FIFO,B1,RB9
RB0:!	XFER R3,FIFO,B3,RB1
ED1A:!	GOTO MERR
RB9:!	XFER R2,FIFO,B0,RB10
RB6:!	XFER R2,FIFO,B3,RB7
RB11:!	XFER R2,FIFO,B4,RB6
MPAGE

SUBTTL SKIP, FAST STATUS EATER, RD BKWD BYTE MODE

SK1:!	XFER R2,FIFO,B1,SK2
SKX:!	GOTO DEE
SK2:!	XFER R2,FIFO,B2,SK3
SK6:!	XFER R3,FIFO,B0,SK7
	0
SK3:!	XFER R2,FIFO,B3,SK4
SK4:!	XFER R2,FIFO,B4,SK6
SK0:!	XFER R2,FIFO,B0,SK1
SK7:!	XFER R3,FIFO,B1,SK8
SKX2:!	GOTO DEO
SK8:!	XFER R3,FIFO,B2,SK9
	0
	0
SK9:!	XFER R3,FIFO,B3,SK0
DSW4:!	HANG 0,0,STAI1
	OFF SRVOUT,WR2
	MPAGE

ESF:!	ON SRVOUT
	TEST R0,B0,BIT51,ESF1
ESF1:!	XANC PC,INCR,0,STRD,ESF2	;NO PCI, START NEXT FETCH
	GOTO ESWI	;PCI, CAN'T BE FAST
ESF2:!	PULSE CLRINS,ESF3
RBB:!	XFER R2,ZERO,WHOLE,RBBA
RBBT:!	TEST R3,B1,BIT71,RBB0	;3 OR 4 BYTES IN 1ST WD
	TEST R3,B1,BIT71,RBB2	;1 OR 2 BYTES IN 1ST WD
RBB1:!	XFER R2,FIFO,B2,RBB2
	GOTO DEE
RBB2:!	XFER R2,FIFO,B1,RBB3	;2 BYTE ENTRY
RBB3:!	XFER R2,FIFO,B0,RBB4	;1 BYTE ENTRY
RBB0:!	XFER R2,FIFO,B3,RBB1	;4 BYTE ENTRY
	XFER R2,FIFO,B2,RBB2	;3 BYTE ENTRY
RBB4:!	XANC CA,DECR,R2,STWR,RBB0
RBBA:!	TEST R3,B1,BIT61,RBBT
	MPAGE

SUBTTL WRITE, READ DATA CHAIN

WR1:!	XFER R2,B1,FOFI,WR2
WRX:!	GOTO DEE
WR2:!	XFER R2,B2,FOFI,WR3
WR3A:!	XFER R3,MRB,WHOLE,WR5
WR4:!	XFER R2,B4,FOFI,WR6
WR3:!	XFER R2,B3,FOFI,WR3A
WR6:!	XFER R3,B0,FOFI,WR7
WR10:!	XFER R2,MRB,WHOLE,WR10A
WR7:!	XFER R3,B1,FOFI,WR8
WRX2:!	GOTO DEO
WR8:!	XFER R3,B2,FOFI,WR9
WR0:!	XFER R2,B0,FOFI,WR1
WR10A:!	XANC CA,INCR,0,STRDC,WR0
WR9:!	XFER R3,B3,FOFI,WR10
WR5:!	XANC CA,INCR,0,STRDC,WR4
WRT:!	XANC CA,INCR,0,STRD,WR10
	MPAGE

DSRE:!	TEST R3,B2,SKPC,DSRE1
	0
DSRE1:!	TEST R0,B1,RBAK,DSRE3	;NOT SKIP
	TEST R3,B3,Z,DSRE2	;POSS SKIP
DSRE2:!	TEST R0,B1,RBAK,DSRE3	;NOT SKIP
	GOTO SK0	;SKIP
DSRE3:!	GOTO RD0	;RD FWD
	TEST R0,B0,BIT41,DSRE4	;RD BAK
DSRE4:!	GOTO RB0	;RD BAK WD
	GOTO RBB	;RD BAK BYTE
DSW2:!	ON SRVOUT,DSW3	;ACCEPT INITIAL STATUS FOR NORMAL WRITE
DSW0:!	TEST R0,B0,BIT71,DSW1
DSW1:!	XFER R2,MRB,WHOLE,DSW2	;WAIT FOR FIRST WORD TO BE WRITTEN
	XFER R2,MRB,WHOLE	;SKIP 2 BYTES
	XANC CA,INCR,0,STRD
	ON SRVOUT,DSW4	;ACCEPT INITIAL STATUS - SKIP 2 BYTES MODE
	MPAGE

SUBTTL EVEN & ODD XFER LOOP EXITS

DEE:!	TEST 0,0,WCOK1,DEE1
DEE2A:!	XANC CA,ADRFLD,R3,LOAD,DEE2B
DEE1:!	TEST 0,0,INS11,DEE2	;WC OVF TEST DC
DEE1A:!	TEST 0,0,INS11,DEE1B	;DEV TRUNC, SEE IF SKIP DCW'S NEEDED
DEE2:!	GOTO XD2	;NO DC (CHNL TRUNC OR OK)
	XANC PC,INCR,0,STRD	;GET NEXT DCW
	XFER R3,MRB,WHOLE,DEE2A	;DONT STEP ON TDS0
DEE2B:!	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD	;SET READ-OK
	TEST R3,B0,BIT00,DEE3
DEE3:!	ON INS1	;FLAG NO MORE DCWS
	TEST R0,B1,BIT71,DEE4
DEE4:!	GOTO DSRE
	GOTO WRT
DEE1B:!	ON LENERR,XD2C	;NO SKIPS NEEDED
	ON INS1,XD1E	;FLAG AS ENDING STATUS, DO SKIPS IF REQUIRED
	MPAGE

DEO:!	TEST 0,0,WCOK1,DEO1
DEO2A:!	XANC CA,ADRFLD,R3,LOAD,DEO2B
DEO1:!	TEST 0,0,INS11,DEO2	;WC OVF TEST DC
	GOTO DEE1A	;DEV TRUNC
DEO2:!	GOTO XD3	;NO DC (CHNL TRUNC OR OK)
	XANC PC,INCR,0,STRD	;GET NEXT DCW
	XFER R3,MRB,WHOLE,DEO2A	;DONT STEP ON TDS0
DEO2B:!	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD	;SET READ-OK
	TEST R3,B0,BIT00,DEO3
DEO3:!	ON INS1	;FLAG NO MORE DCWS
	TEST R0,B1,BIT71,DEO4
DEO4:!	GOTO DSRO	;READ
	XANC CA,INCR,0,STRD	;WRITE
	GOTO WR3A
	0
	MPAGE

SUBTTL XFER CLEANUP, MISC

XD2:!	HANG 0,0,BFHLT0
	TEST 0,0,BCRQ1,XD2A
XD2A:!	TEST 0,0,BFMT1,XD2B
	ON CMDOUT	;CHNL TRUNC
	HANG 0,0,BCRQ1
	OFF CMDOUT,XD2D
XD1C:!	XFER R3,MRB,WHOLE
XD1D:!	TEST R3,B0,BIT00,XD1A
XD2B:!	ON LENERR
XD2C:!	OFF BUFENB,ED
DSW3:!	HANG 0,0,STAI1
	OFF SRVOUT,WR10A
XD1A:!	ON LENE2C	;NO MORE
XD1E:!	TEST R0,B0,BIT61,XD1B	;UNUSED DCW
XD1B:!	ON LENERR,XD2C	;NO SLI OR NO MORE
	XANC PC,INCR,0,STRD,XD1C	;SKIP A DCW
	MPAGE

DAT:!	XFER R3,MRB,WHOLE
	XANC CA,ADRFLD,R3,LOAD
	XANC WC,WCFLD,R3,LOAD
	XANC WC,INCR,0,LOAD	;SET READ-OK
	TEST R3,B0,BIT00,DAT1
	0
ESF3:!	HANG 0,0,STAI1
	OFF SRVOUT
	HANG 0,0,OPLI1
	GOTO FET0
CMDE1:!	HANG 0,0,STAI1
	OFF SRVOUT
	HANG 0,0,OPLI1
	GOTO MERR
DAT1:!	ON INS1	;FLAG NO MORE DCW'S
	ON BUFENB,DAT2
	MPAGE

  \sò