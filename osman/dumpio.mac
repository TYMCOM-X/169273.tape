	TITLE	DUMPIO	Un-Buffered Dump Mode I/O.

		;This program copies a disk file "FILE.DAT" to "FILE.DMP"
		;Files could be any size and type. Also modes could be
		;.IODPR(16) or .IODMP(17). Mode .IOIDP(15) is illegal for
		;disk i/o.

	;-Temporary AC's
	
	T1=1
	T2=2
	T3=3
	P=17

	;-Some definition

	PDLEN=50			;-Pushdown lenght
	WRDCNT=^D512			;-Word count
	INNCHN=1			;-Input channel
	OUTCHN=2			;-Output channel

	OPDEF	MONRET [EXIT 1,]	;-Return to monitor level

START:	RESET				;-Reset the world
	MOVE	P,[IOWD PDLEN,PDLIST]	;-Initilize the stack

	OPEN	INNCHN,[		;-Initilize channel 1 for
			17		;- Un-Buffered Dump Mode I/O
			SIXBIT	"DSK"
			0]
	  JRST	ERR1			;-Can't initilize channel
	OPEN	OUTCHN,[		;-Initilize channel 2 for
			17		;- Un-Buffered Dump Mode I/O
			SIXBIT	"DSK"
			0]
	  JRST	ERR1			;-Can't initilize channel
	ENTER	OUTCHN,OUTFIL	;-Do a long enter
	  JRST	ERR2		;-Can't do the enter
	LOOKUP	INNCHN,INNFIL	;-Do a long lookup
	  JRST	ERR2		;-Can't do the lookup
	MOVE	T2,INNFIL+5	;-Get the file's size in word
	MOVEM	T2,REALWC	;-Save it
RDNWR:	MOVE	T2,REALWC	;-Looks dumb but it is here for the loop
	SUBI	T2,WRDCNT	;-Figure out how much more left in the file
	SKIPG	T2		;-Anything more left to copy
	PUSHJ	P,EOFLG		;-No, set some flags indicating that
	MOVEM	T2,REALWC	;-Yes, then go ahead and read&write the record
	INPUT	INNCHN,IOCLST	;-Read the record
	STATZ	INNCHN,74B23	;-Was there any error reading?
	  JRST	ERR3		;-Yes, go complain about it
	OUTPUT	OUTCHN,IOCLST	;-No, then go write the record
	STATZ	OUTCHN,74B23	;-Was there any errors writing it?
	  JRST	ERR4		;-Yes, go complain
	SKIPN	EOFFLG		;-No, was this the last record to write?
	JRST	RDNWR		;-No, go get some more 
	CLOSE	INNCHN,		;-Yes, it was the last record so
	CLOSE	OUTCHN,		;- close and
	RELEAS	INNCHN,		;- releas
	RELEAS	OUTCHN,		;- input and output channels
	MONRET			;-Return to monitor

EOFLG:	SETOM	EOFFLG		;-Set a flag to indicate last record
	MOVN 	T2,REALWC	;-Get the remaining wordcount
	HRLM	T2,IOCLST	;-Put it into right place for input/output
	POPJ	P,		;-Return to caller

ERR1:	OUTSTR	[ASCIZ "
Can't initilize I/O channel...
"]
	MONRET
ERR2:	OUTSTR	[ASCIZ /
Can,t do LOOKUP or ENTER...
/]
	MONRET
ERR3:	OUTSTR	[ASCIZ /
Read error...
/]
	MONRET
ERR4:	OUTSTR	[ASCIZ /
Write error...
/]
	MONRET

INNFIL:	0,,5			;-Lookup block (0,,#of entries)
	0			;-PPN
	SIXBIT	"FILE"		;-File name
	SIXBIT	"DAT"		;-File extension
	0			;-Privilege word (prot, mode, data/time etc.)
	0			;-File size in words

OUTFIL:	0,,5			;-Enter block (0,,#of entries)
	0			;-PPN
	SIXBIT	"FILE"		;-File name
	SIXBIT	"DMP"		;-File extension
	0			;-Privilege word (prot, mode, data/time etc.)
	0			;-File size in words

IOCLST:	IOWD	WRDCNT,IOBUFF	;-I/O Command list for dump mode 
	0

IOBUFF:	BLOCK	7777		;-Size of I/O buffer
PDLIST:	BLOCK	PDLEN		;-Size of pushdown list
EOFFLG:	0			;-Set to -1 when EOF is reached
REALWC:	0			;-Reminder of...FILE SIZE/WORDCOUNT

	END	START
