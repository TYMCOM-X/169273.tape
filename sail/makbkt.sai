begin	string delims, letts, digs, sailid;
delims_'15&'12&'40&'11&'14;
	comment carriage return, line feed, space, tab, form feed;
letts_ "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz" & '30;
			Comment '30 is control-X (SU-AI underbar);
digs_ "0123456789";
sailid_ letts & digs;

setbreak(1,'12,'15,"INS");
setbreak(2,'12,null,"INA");
setbreak(3,delims,null,"XNR");
setbreak(4,sailid,null,"INS");
setbreak(5,sailid,null,"INR");
setbreak(6,letts,null,"XNR");
setbreak(7,digs,null,"XNR");
setbreak(8,digs,null,"INS");
setbreak(9,digs,null,"INR");
setbreak(10,digs&"+-@.",null,"XNR");
setbreak(11,digs&"+-@.",null,"INS");
setbreak(12,digs&"+-@.",null,"INR");
setbreak(13,null,null,null);
setbreak(14,null,null,null);
setbreak(15,null,null,null);
setbreak(16,null,null,null);
setbreak(17,null,null,null);
setbreak(18,null,null,null);

 begin	integer locn;

    start!code	define temp='14,user='15,p='17,x=5,cdb='11;
		external integer bktchk,gogtab;
	move	user,gogtab;
	movei	x,1;
	movsi	temp,-1;
	pushj	p,bktchk;
	 setz	cdb,;
	movem	cdb,locn;
    end;

  if locn=0
   then usererr(0,0,"Cannot access mystic break table files")
   else begin	boolean eof;	
	open(1,"DSK",'14, 0,2, 0,0,eof);
	if eof then usererr(0,0,"No DSK for output");
	enter(1,"BKTBL.BKT",eof);
	if eof then usererr(0,0,"Cant ENTER file BKTBL.BKT for output");
	arryout(1,memory[locn],0+19+19+128+1+1+1);
	release(1);
	end;
 end;
end $
