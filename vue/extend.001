Module Extend = begin

bind zone1 = #2000;	       ! C-X ( commands in dispatch.

external getsymbol,getroutine,Toprompt,wrtnum;
external escvalue,metaprefix,xprefix;
external MaxPatches,initdispatch,modblk,errstr;
external discat,distbl,patsp,readch,flubfr,writeloss,writestring;
external parmarray,freezone,MaxPPerFrame;
external rstk,cstk,csnum,rptr,parmarray,MaxPPerFrame;
external freestk,repcount,callchar,base,comchar,newcount;

external metam,sttype,ctrlg;
external ctrla,ctrlb,ctrlc,ctrld,ctrle,ctrlf,ctrlg,ctrll,ctrlk,ctrlm,ctrln;
external ctrlo,ctrlp,ctrlq,ctrlr,ctrls,ctrlt,ctrlv,ctrlw,ctrly,ctrlz;
external metaa,metab,metac,metad,metae,metaf,metak,metal,metam,metao;
external metap,metaq,metar,metav,metaw,metaz,metadel,metcw;
external metalt,metagt,metacp,metacn,metalc,metarc,metalb,metarb,rubout;
external Ictrls,Ictrlr,nextpage,lastpage;

forward ctrlxl,uctrll;
forward bindkey;

macro x(arg,name) = (plit asciz arg)<36,7>,name,$;

bind table = plit (

    X ('Bind Function',		bindkey)
    X ('Bind Macro Slot',	ctrlxl)
    X ('Major Mode',		Metam)
    X ('Terminal Type',		sttype)
    X ('Character Manipulation',0)
    X ('Backward Character',	ctrlb)
    X ('Forward Character',	ctrlf)
    X ('Up Text Line',		ctrlp)
    X ('Down Text Line',	ctrln)
    X ('Beginning of Line',	ctrla)
    X ('End of Line',		ctrle)
    X ('Forward Word',		metaf)
    X ('Backward Word',		metab)
    X ('Redisplay Screen',	ctrll)
    X ('Beginning of Screen',	metacp)
    X ('End of Screen',		metacn)
    X ('Next Screen',		ctrlv)
    X ('Previous Screen',	metav)
    X ('Beginning of Text',	metalt)
    X ('End of Text',		metagt)
    X ('Beginning of Page',	metalc)
    X ('End of Page',		metarc)
    X ('Next Page',		nextpage)
    X ('Last Page',		lastpage)
    X ('Up Paragraph',		metalb)
    X ('Down Paragraph',	metarb)
    X ('Scroll Screen',		ctrlz)
    X ('Reverse Scroll Screen',	metaz)
    X ('Abort Operation',	ctrlg)
    X ('Delete Character',	ctrld)
    X ('Rubout Character',	rubout)
    X ('Delete Word',		metad)
    X ('Rubout Word',		metadel)
    X ('Kill Line',		ctrlk)
    X ('Kill Region Mark',	ctrlw)
    X ('Kill Region String',	metaw)
    X ('Copy Region',		metcw)
    X ('Delete Horizontal Space',metak)
    X ('Restore Killed Text',	ctrly)
    X ('Incremental Search',	Ictrls)
    X ('Reverse Search',	Ictrlr)
    X ('String Search',		ctrls)
    X ('Reverse String Search',	ctrlr)
    X ('Replace String',	metar)
    X ('Query Replace String',	metaq)

    

    0, 0);

global routine approc(place,proc) = begin local oldvalue;
    ! proc  is the address of the procedure
    ! place is the index where it should go.

    if (MaxPatches<0,0>)*2+1 leq .patsp then begin
	errstr(plit asciz'Rebind space overflow, reseting with INIT mode.');
	initdispatch();
	(modblk+1)_sixbit'INIT';
	return;
    end;

    oldvalue_.distbl[.place];	       !the value there now.
    if (.oldvalue leq -zone1) or (.oldvalue geq 0) then begin
	!cdr code fault handler.
	discat[.patsp]_.oldvalue;
	discat[.patsp+1]_0;	       !nil at the end of the list.
	distbl[.place]_-.patsp;	       !point to us.
	patsp_.patsp+2;
    end;
    discat[.patsp]_.proc;	       !the procedure.
    discat[.patsp+1]_.distbl[.place];  !the next thing to do.
    distbl[.place]_-.patsp;	       !point to us.
    patsp_.patsp+2;
end;
global routine setproc(place,proc) = distbl[.place]_.proc;

global routine getpair = begin    ! standard command rebinder.
    bind CtrlxValue=#400;
    bind MetaValue=#200;
    local tc,toff;

    toff_0;
    tc_readch(flubfr());	       !get the thing.
    if .tc eql "?G" then return (errstr(plit asciz'Cannot Do Rebind');-1);
    writeloss(.tc);
    if .tc eql .MetaPrefix then toff_MetaValue
    else if .tc eql .XPrefix then toff_CtrlxValue;
    if .toff neq 0 then begin
	tc_readch(flubfr());
	if .tc eql "?G" then return (errstr(plit asciz'Cannot Do Rebind');-1);
	writeloss(.tc);
    end;
    return .toff+.tc;
end;
global routine uctrll = begin
    ! the rebinder.
    local old,new;
    ToPrompt(plit asciz'Copy key:');
    old_getpair();
    if .old lss 0 then return;
    WriteString(plit asciz' to new key:');
    new_getpair();
    if .new lss 0 then return;
    distbl[.new]_.distbl[.old];
end;
global routine bindkey = begin
    external repcount;
    local key,name;
    ToPrompt(plit asciz'Function Name: ');
    name _ GetRoutine(table);
    if .name eql -1 then return ctrlg();
    Writestring(plit asciz'Key: ');
    key _ getpair();
    if .key lss 0 then return ctrlg();
    distbl[.key]_..name;
    repcount_1;				
end;

global routine ctrlxl = begin
    !macro rebinder.
    local mname,new;

    ToPrompt(plit asciz'Macro Slot: ');
    mname_readch(flubfr());
    if .mname geq "A" and .mname leq "Z" then mname_.mname+("a"-"A");
    if .mname lss "a" or .mname gtr "z" then begin
	errstr(plit asciz'Illegal Slot Name.');
	return;
    end;
    writeloss(.mname);
    WriteString(plit asciz' bind to key: ');
    new_getpair();
    if .new lss 0 then return;
    distbl[.new]_-(zone1+.mname-"a");
end;
global routine setparm = begin	       !^Xright square bracket
    local pn,oldcallchar,oldrepcount,oldtp,tbp,tc;

    pn_readch(flubfr());	       !read which parameter we are.
    pn_.pn-"0";			       !adjust it.
    ! First we handle the prompt stuff.
    ToPrompt(plit asciz'');
    while 1 do begin		       !get chars until altmode.
	tc_readch(flubfr());
	if .tc eql .EscValue then exitloop;
	writeloss(.tc);
    end;
    rptr_.rptr-2;
    oldcallchar_.rstk[.rptr];
    oldrepcount_.rstk[.rptr+1];
    oldtp_.cstk[.csnum];
    csnum_.csnum-1;
    tbp_.freezone;		       !the first free byte in parmland.
    while 1 do begin		       !read chars from caller to EscValue
	tc_readch(flubfr());
	writeloss(.tc);		       !and echo it.
	if .tc eql .EscValue then exitloop; !we are done.
	replacei(freezone,.tc);	       !plug it in.
    end;
    replacei(freezone,"?]");	       !part of the return sequence.
    replacei(freezone,"r");	       !the rest thereof.
    csnum_.csnum+1;		       !now restore I/O attachments.
    cstk[.csnum]_.oldtp;	       !in the samemanner as above.
    rstk[.rptr+1]_.oldrepcount;
    rstk[.rptr]_.oldcallchar;
    rptr_.rptr+2;

    parmarray[.csnum*MaxPPerFrame<0,0>+.pn]_.tbp; !remember the parm.
end;
global routine popio = begin
    if .csnum eql 0 then return;   !already popped to top.
    csnum_.csnum-1; !return from qregister.
    rptr_.rptr-2;
    repcount_.rstk[.rptr+1];
    callchar_.rstk[.rptr];
    base_#200;
    comchar_#143; !metac
    newcount_0;
end;


Global routine extend = begin
local i;
external repcount;
    ToPrompt(plit asciz 'Meta-X ');	! Print M-X
    i _ GetRoutine(table);		! Use table from Module
    if .i neq -1 then while .repcount gtr 0 do begin
	if ..i neq 0 then(..i)();	! Attempt to use this.
	repcount _ .repcount -1;
    end;
    repcount_1;				! reset so subtract = 0
    return;
end;


Global routine metas = begin
    local parameters;

    macro iudent=(owns)$;
    macro spacect=(owns+1)$;
    macro tabwidth=(owns+2)$;
    macro indend=(owns+3)$;
    macro commentpos=(owns+4)$;
    macro z(arg,val) = (plit asciz arg)<36,7>, val,$;

    global hatuvl=4,sefmod=1,sosmod=1;
    external owns,baudrate,autoinc,statusline;


    parameters_plit (
    Z ('Baud Rate',			baudrate)
    Z ('Argument Multiplier',		hatuvl)
    Z ('Center Screens',		sefmod)
    Z ('Indentation Width',		tabwidth)
    Z ('Right Justification Margin',	iudent)
    Z ('Page Mode',			sosmod)
    Z ('Autosave Frequency',		autoinc)
    Z ('Escape Value (not meta)',	EscValue)
    Z ('Meta Prefix Value',		metaprefix)
    Z ('Control-X Prefix Value',	xprefix)
    Z ('Status Line',			statusline)
    Z ('Comment Position',		commentpos)
	0,0,0);
    ToPrompt(plit asciz 'Mode parameter: ');
    GetSymbol(.parameters);
end;

end eludom
    