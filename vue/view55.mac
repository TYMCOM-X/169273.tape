Title	View	Macro support subroutines for VUE
Subttl	VUEMAC	By Carl A Baltrunas - Tymshare Inc -

	Search	Jobdat

;Register Definitions -- See BLISS Manual

	s=0		; Sreg - PDL & Stack
	t=1		; SuperTemp - Don't expect anything
	f=2		; Freg - Formal parameter stack
	v=3		; Vreg - Value register
	a=4		; Temp
	b=5		; Temp
	c=6		; Temp
	d=7		; Temp
	e=10		; Temp

	Twoseg		; Generate Shareable code

	Reloc	400000	; Starting at normal address.

Subttl	VUEMAC	Version and relocation information

	loc	.jbver		; VUExxx defined in VUE.INF
	Byte (3) vuewho (9) vuever (6) vuemin (18) vueedt
	reloc

	Ftcommand=1		; Expect RESCAN capability

IFNDEF Ftdebug,<Ftdebug==-1>	; Allow (VUE) & (YEUX) as well as (SYS)

Subttl	Entry Points - Internal / External

	entry fef,fen,wrnum,clulin,clupos,autblk,cluhac,imout,realup,flush
	entry scrsze,screen,curline,dskbuf,filblk,fixfil,ppn,cucl,defdsk,base
	entry comchar,newcount,repcount,readch,lowend,linesz
	entry ttytyp,sst,mark,maxss,muncho,munchi,ocline,scrmod,muline
	entry hend,ttyidle,reset,hbot,p,dskbfh,putch,flubfr,modpos
	entry writestring,writeloss,updatecursor
	entry csnum,qreg,dorescan,setlab,lookop,hitsts,submod,vuetyp
	entry physpos,physlin,tabsize,distbl,bfblk1,bfblk2,scroff,scrlen
	entry relcor,coresize,rsrchr,rsrptr,rsrstk,dmpout,dmpin
	entry ttype,tspeed,gettty,getwid,getlen,getaun,getppn,mail,getjob
	entry moveforward,moveback,quit,modblk
	entry getchx,ftp,therep

external metap,ctrlxr,ctrlxv,uctrlo,read
external silent,escvalue,leftmargin,goxy
external rptr,corecheck,cstk

Subttl	VUEMAC	TYMCOM-X / TOPS-10 Definitions 

Opdef	Auxcal	[042000,,0]	; Terminal / Aux Circuit calls
Opdef	Setmod	[Calli -26]	; Used to set status/mode of line
Opdef	Putlsa	[Calli -63]	; Writes a Stream Accounting Record

.Axi8c==0	; Input character image and wait
.Axi8s==1	; Input character image and skip
.Axsic==10	; skip if character
.Axobr==16	; Read Output baud rate code
.Axwid==27	; Terminal Width
.Axics==45	; input character and skip
.Axost==52	; Outstr to Aux port
.Axrvx==55	; reverse ^S/^Q enable
.Axcfs==64	; Setmod for Aux port
.Axttp==100	; Read terminal type

io.nee==1b27	; no-escape-echo bit
io.nec==1b28	; no-echo bit
io.fcs==1b29	; full-character-set bit

.ioimg==10	; image mode io
.iobin==14	; binary

.Gtfpn==-25	; Frame PPN
.Gtaun==-23	; Acct User Number (ppn)
.Gtlic==-20	; User license
.Gtppn==2	; Gfd'd PPN
.Gtnam==3	; Name!
.Gtprv==6	; Privilage word

Subttl	VUEMAC	Main subroutines - Shareable @ 400000
Subttl	VUEMAC	Reset

;Reset	- Reset i/o and do one-time only checks with PUTLSA & JP.TYM
;
;	Pushj	s,Reset		RESET()
;	 <Only Return>
;
Reset:	Calli	0		; Reset the world!
	Pushj	s,Getprv	; Get user privs.
	Movem	v,Usrprv	; Store them.
	Pushj	s,Getlic	; Get user license.
	Movem	v,Usrlic	; Store them.
	Pushj	s,Getnam	; Get program name.
	Movem	v,Usrnam	; Store it.
	Pushj	s,GetFPN	; Get Frame PPN (where program run from).
	Movem	v,UsrFPN	; Store it.
	Pushj	s,Getppn	; Get the GFD'd PPN
	Movem	v,UsrPPN	; Store it.
	Pushj	s,Getaun	; Get the Logged in PPN too.
	Movem	v,Usraun	; Store it.
	Skipe	UsrFPN		; See if FPN is good.
	  Jrst	BadFPN		; Yes, continue.
	Movem	16,Usrnam	; This guy is either an EXE file or...
	Movem	17,UsrFPN	;  he's been meddling.  Scrunch!!!

BadFPN:	Tdza	b,b		; Start fresh.
	 Tro	b,-44		; Make sure this is done right. (NEVER!)
	Movsi	a,'UTD'		; For file hackers!!! this is VUE - 10101
	Tlo	b,10101		; Here is the 10101 to add
	Addb	a,b		; ...
ifn Ftdebug,<
	Skipe	.Jbddt		; If DDT is loaded
	  Move	a,[sixbit 'dvue'];  then allow a different name!
    > ; End ifn Ftdebug
	Came	a,Usrnam	; Do they still match?
	  Jrst	Tattle		; No-- Tell someone!
	Movsi	b,1		; Build these too!
	Rot	b,24		; Put the 4 into position
	Tlo	b,1		; Set bits for 1,,4
	CAMN	b,UsrFPN	; is it a legal copy?
	  Jrst	Rested		; Seems ok -- DO next step
	Trc	b,4		; Clear the 4
	Tro	b,315372	; Set in UUN for (FTSYS)
	Camn	B,UsrFPN	; Compare
	  Jrst	Rested		; Seems ok -- DO next step
Ifn Ftdebug,<
	Move	a,Usrfpn	; Need to get this for later
	Movsi	b,6777		; get first part 3,3774...shifted some
	Addi	b,022000	; now add next part
	Rot	a,12		; shift test part
	CAME	a,b		; DEBUG COPY?
	CAMN	a,[6777,,024000]; NEW VERSION?
	  Jrst	Rested		; Seems ok -- DO next step
	Rot	a,-12		; No, So move everything back!
	> ; End Ifn Ftdebug

	Tdza	c,c		; Start fresh.
	 Tro	b,-44		; Make sure this is done right. (NEVER!)
	Movsi	b,'UTD'		; For file hackers!!! this is VUE - 10101
	Tlo	c,10101		; Here's the 10101 to add (left half)
	Addb	c,b
Tattle:	PUTLSA	b,		; make a stream record
	 JFCL
	Setz	c,		; Need some scratch
	Move	v,Usrlic	; First check license
	Trne	v,(2b17)	; Does he have WF?
	  Jrst	Delf		; Yes-- No need to check further
	Move	v,UsrFPN	; Get the frame ppn
	Camn	v,UsrPPN	; Does it match? (GFD'd PPN)
	  Tro	c,100		;  Yes!! Only needs Owner access
	Trz	v,-1		; Clear PN
	Hllz	b,Usrppn	; Get Project part
	Camn	v,b		; ... Do they match?
	  Tro	c,10		;  Yes!! Only needs Project access
	Move	v,Usrlic	; See if he has WP set?
	Trne	v,(1b17)	; Either that or WA... both ok.
	  Trz	c,10		;   Seems to - Clear project bit
Delf:	move	v,c		; Now, copy our flag.



	OPEN	[ 17
		  sixbit "DSK"
		  xwd 0,0]
	  Jrst	Rested		; Can't cope -- DO next step
	Skipn	Usrnam		; Get program name.
	  Jrst	cantell		; Can't cope -- Release Disk.
	MOVE	d,[point 18,[sixbit /LOWSHR/
			     sixbit /HGHSAV/
			     sixbit /SWREXE/
			     sixbit /XPN   /]]
Vuecpy:	Move	e+3,UsrFPN	; Reset the FPN (remembered from above)
	Move	e,Usrnam	; Save the name in the AC's LOOKUP block
	Ildb	c,d		; Get one of LOW SHR HGH SAV SWR 0
	 Jumpe	c,cantell	; All done?
	Movsm	c,e+1		; Not yet... store extension to try.
	Setzm	e+2		; Finish setup...
	LOOKUP	e		; Look it up in his directory [FPN]
	  Jrst	Vuecpy		;  Ignore errors - Just go to the next one.
	Jumpe	v,Okdelf	; Ok to delete if WF or WP... set, so go!
	ldb	c,[point 3,e+2,2]; get user protection
	Trne	v,100		; If same user, then...
	 jumpe	c,Okdelf	;  Let him delete it if possible
	Trne	v,100		;  Else ... he must rename it first.
	 jrst	Okrenf		;  ...
	ldb	c,[point 3,e+2,5]; get project protection
	Trne	v,10		; If same project, then...
	 jumpe	c,Okdelf	;  Let him delete it if possible
	Trne	v,10		;  Else ... he must rename it first.
	cail	c,2		;  ... IF that is possible!
	 caia			;  no - no - no!
	  jrst	Okrenf		;  ...  Ok!
	ldb	c,[point 3,e+2,8]; get anyone protection
	 jumpe	c,Okdelf	;  Let him delete it if possible
	cail	c,2		;  Else ... he must rename it first.
	  jrst	Vuecpy		;  ...  Can't!!! so try next file!
Okrenf:	movei	c,00017		;  Protection 0, mode 17
	Dpb	c,[point 13,e+2,12]; store it for the rename.
	Rename	e		; Change the protection
	  jrst	Vuecpy		; If error -- give up & try next one
	setzm	e+2		; Clear this field
	move	e+3,UsrFPN	; Restore the directory name
	hllzs	e+1		; Clear creation date and stuff
	Lookup	e		; Do a lookup again
	  jrst	Vuecpy		; Not there? -- Give up!
Okdelf:	SETZM	e		; Clear name
	RENAME	e		; Delete it
	 Jfcl			;  Ignore errors
	Jrst	Vuecpy		; Loop till all stolen files gone

Cantell: RELEAS			; Release the channel (DSK)
Rested:	movei	a,1b21		; JP.TYM
	tdne	a,Usrprv	; Does the user have license?
	  jrst	Noecho		;   Yes -- Turn off echo & GO!
	lsh	a,-7		; Shift word over and decipher the mode!
	andi	a,3		; Got it, now we have them run something else
	move	a,[sixbit /EDIT10/	; PDP-10 .JPMPD
		   sixbit /EDITOR/	; XEXEC  .JPMXE
		   sixbit /EDITOR/	; XEXECJ .JPMXJ
		   sixbit /EDITOR/](a)	; SUDS   .JPMSU
				; Fall into code to RUN (SYS) ((a))

Subttl	VUEMAC	Runccl	Cucl	RunRPG	Runcca

;Runccl	- Perform a "RUN" at start address + 1 for SYS:(a)
;
;	Move	a,[sixbit /program/]
;	Jrst	Runcca
;	  <never returns>
;
Runccl:	Move	b,[Xwd RPGBLK,e]; Remember this is from SYS:
	Blt	b,e+5		; Copy defaults from RPG
	Movem	a,e+1		; Now store the name
	Jrst	Runcca		; And run it at the CCL point

;Cucl	- Run CCL program at starting addr + 1
;RunRPG	- Run (SYS)RPG at atarting address + 1 to do the last RPG thing
;
;	Jrst	RunRPG		Cucl()  or  Runrpg()
;	  <never returns>
;
cucl:	Pushj	s,Fen		; Turn off the terminal
RunRPG:	Move	b,[Xwd RPGBLK,e]; We are running RPG right?
	Blt	b,e+5		; Copy defaults from RPGBLK
Runcca:	movsi	a,1		; Start offset = 1
	hrri	a,e		; Setup address of block
	run	a,		; Bye!
	  halt	.

RPGBLK:	sixbit/SYS/
	sixbit/RPG/
	0
	0
	1,,4
	0

Subttl	VUEMAC	Fef	Fen

;Fef	- Save Characteristics & Setup Terminal
;
;	Pushj	s,FEF		FEF()
;	  <only return>
;
fef:	seto	a,		; Get the tty characteristics
	getlch	a		; Of your own port
	movem	a,tymtty	; Remember them
	tlo	a,220		; Magic bits NFC!NCM
	setlch	a		; Now setup special ones
	hrroi	a,.Axrvx	; ^S/^Q
	setzm	inirvx		; turn it off and save it
	Auxcal	a,inirvx
	Setom	tspeed		; Get the terminal speed
	hrroi	a,.Axobr	; ... for output anyway.
	Auxcal	a,tspeed	;  .. save it for later
Noecho:	Hrroi	a,.Axcfs	; Now setup port status
	Auxcal	a,io.nee!io.nec!io.fcs!.iobin
Cpopj:	Popj	s,		; Then Return


;Fen	- Restore terminal characteristics
;
;	Pushj	s,Fen		FEN()
;	  <only return>
;
fen:	pushj	s,flubfr	;make sure everything is gone
	move	a,[Xwd 41,5]	; Send a yellow ball throught the net
	Hiber	a,		;   then setup to wait up to 5 seconds
	  jfcl			;   for it to return.  Ignore errors.
	move	a,tymtty	; Remember these characteristics?
	setlch	a		; Now set them back
	hrroi	a,.Axrvx	; ^S/^Q
	push	s,inirvx	; Make sure this doesn't get clobberred
	Auxcal	a,inirvx	; restore it
	pop	s,inirvx	;  ...
	Popj	s,		; Then return

Subttl	VUEMAC	Relcor	Coresize

;Relcor	- ....
;
;	Push	s,[N]			Relcor(N)
;	Pushj	s,Relcor
;	  <only return>				or ERROR!EXIT
;
relcor:	move	a,hend			; Get hend
	idivi	a,5
	addi	a,100
	add	a,p			; a=highest address used by vue.
	movem	a,.Jbff			; Set jobff
	ori	a,777
	movem	a,.Jbrel		; Set this number too.
	calli	a,11			; Get the core
	  jrst	E$$NEC			; Give error message and exit
	popj	s,

;Coresize - Increase core by N words (if necessary?)
;
;	Push	s,[N]			Coresize(N)
;	Pushj	s,Coresize
;	  <only return>				or ERROR!EXIT
;
Coresize:
	move	a,s			; Get the argument
	move	v,.Jbff			; Get Next free location
	move	b,-1(a)			; Get requested length
	addm	b,.Jbff			; Increment to next free
	addb	b,.Jbrel		; Add to last available size
	calli	b,11			; Ask for it
	  jrst	E$$NEC			;  Nope - Give error & exit
	popj	s,

Subttl	VUEMAC	TtyIdle - Return 1 if True, 0 if False

;Ttyidle - Return True(1) or False(0) depending upon pending input
;
;	Pushj	s,TtyIdle	... Ttyidle() ...
;	  <only return>				v == 0 or 1
;	
ttyidle:
	hrroi	v,.Axsic	; Equiv of Skpinc
	Auxcal	v,		;  to see if anything there
Ret1:
ifn v,<	 skipa	v,[1]>		;    1 == True
ife v,<	 tdza	v,v		; Simulate "skipa v,[1]"
	 caia
	 aosa	v>
Ret0:	 setz	v,		;    0 == False
	popj	s,

Subttl	VUEMAC	Readch - reads a char and returns it in v

;Readch	- Read a character from the current input source into V
;
;	Pushj	s,Readch	char _ Readch()
;	  <only return>				v _ char
;
readch:	pushj	s,flubfr	; Everyone wants this, so do it!
	move	a,csnum		; The stack index (-1 for TTY)
	skipn	cstk(a) 	; Do we have a real pointer?
	  jrst	usetty		;  No -- Use TTY
	skipge	cstk(a) 	; Is it a REAL pointer?
	  jrst	usefil		;  No -- From a file
	ildb	v,cstk(a) 	; Yes -- Get a character
	popj	s, 		; Return

usetty:	hrroi	a,.Axi8c	; Inchrw equivalent
	Auxcal	a,v		; (To preserve Image mode)
	  jrst	[movei	a,1
		sleep	a,
		jrst	.-2]	; Loop (waiting for port)

; test & skip if this terminal type wants all 8 bits!!!
	trz	v,200		; Else zap parity
	popj	s, 		; and go home.

Subttl	VUEMAC	usefil - Read a character from a file (Readch)

usefil:	push	s,[4]
	pushj	s,read		; Read a char
	pop	s,a
	jumpl	v,popout	; Branch on EOF
	jumpe	v,usefil	; In files...
	caie	v,15		;   ignore NULL,
	cain	v,12		;       CR and LF
	  jrst	usefil		;
	caie	v,"?"		; Check for <control escape>
	  popj	s,		;   No, -- return character
usefi1:	push	s,[4]		; Yes ---
	pushj	s,read		; Read another character
	pop	s,a
	jumpl	v,popout	; Branch on EOF
	jumpe	v,usefi1	; In files... always ignore NULL
;	caie	v,15		;   But here give a
;	cain	v,12		;       CR and LF
;	  jrst	fromfx		;   Cause an error!!!
	cain	v,"?"		; Our <control escape>?
	  popj	s,		;  Yes -- Return with it
	cail	v,"a"		; Case map all to upper
	caile	v,"z"		; ... only alpha for now
	  caia			; No change, skip
	movei	v,-40(v)	; Map them -- UPPER CASE
	movei	v,-100(v)	; Map them -- CONTROL CHARACTERS
	skipl	v		;  Check if legal?
	  popj	s,		;   Yes -- Return with it

popout:	pushj	s,metap		; EOF or illegal character
	jrst	readch		; Now continue?


fixfil:	move	a,filblk
	movem	a,zapdev+1
	open	3,zapdev
	  jrst	E$$COD		; Can't Open Device
	move	a,filblk+1 	; get the file name.
	movem	a,newblk
	move	a,bakx
	movem	a,newblk+1 	;for the lookup
	move	a,ppn 		; the ppn
	movem	a,newblk+3 	; again for the lookup.
	setzm	newblk+2
	lookup	3,newblk
	  jrst	fixfi0
	setzm	newblk+3	;lookup breaks this field.
	setzm	newblk+2 	; should be zero
	setzm	newblk+1 	; why not.
	setzm	newblk 		; prepare to wipe file.
	rename	3,newblk
	  jfcl
fixfi0:	move	a,filblk+2 	; get the extension
	movem	a,newblk+1 	;and store it.
	move	a,ppn 		;get the ppn again
	movem	a,newblk+3
	move	a,filblk+1 	;the file name
	movem	a,newblk
	setzm	newblk+2 	;just in case.
	lookup	3,newblk 	;for the rename
	  jrst	jrsap 		;just reset and popj
	move	a,bakx 		;new file name
	movem	a,newblk+1 	;extension
	move	a,ppn 		;get the ppn
	movem	a,newblk+3 	;because lookup broke it.
	setzm	newblk+2
	rename	3,newblk
	  jfcl
jrsap:	close	3,
	release	3,
	setzm	filblk+3
	hllz	a,filblk+2 	;preserve only the extension
	movem	a,filblk+2
	popj	s, 		;go home.
munchi:
	move b,s
	move b,-1(b) ;get byte ptr.
	movei d,1
gloop:	sosge dskbfh+2 ;decrement byte count
	jrst getbf ;if buffer empty
	ibp dskbfh+1		;increment it first.
	move c,@dskbfh+1	;get the pointer to the word.
	trnn c,1
	jrst asos
	movni c,6		;number of chars to be zapped.
	addm c,dskbfh+2		;decrement the count.
	aos dskbfh+1	;bump the pointer
	ibp dskbfh+1
asos:	ldb c,dskbfh+1 ;get a byte
	cain c,0
	jrst gloop ; if a null
	cain c,12
	jrst gloop ;if a line feed.
	idpb c,b ;dump it in a the place.
	movei d,1
	addm d,lowend
	move e,lowend
	addi e,20
	camge e,hbot
	jrst gloop
	movem b,save
	pushj s,corecheck
	move b,save
	movei d,1
	jrst gloop

getbf:	in 2,
	 jrst gloop ;no errors or anything.
	popj s, ;else go back home, we are done.

muncho:
	move c,s
	move a,-1(c) ;ptr
	move b,-2(c) ;length
ploop:	sosge b
	jrst	ret1
	caie c,15 ;carriage return was last
	jrst reg
	movei c,12 ;get a line feed
	jrst regpl
reg:	ildb c,a ;get the byte
regpl:	sosg dskbfh+2 ;buffer full?
	jrst putbf ;dmp it first
putc:	idpb c,dskbfh+1 ;dump it in buffer
	cain c,15 ;cr?
	addi b,1 ;if so
	jrst ploop
putbf:	out 2,
		jrst putc ;no errors, go home.
	jrst	ret0	;write failed.
	jrst putc

Subttl	VUEMAC	Putch	Flubfr

;Putch	- Output a character (put it into buffer)
;
;	Push	s,[char]	Putch(.char)
;	Pushj	s,Putch
;	  <only return>
;
putch:	skipe	silent		; If we're being quiet
	  popj	s,		;   then don't say a word
	sosge	ttysfd		; Any room left?
	  pushj	s,flubfr	;   No -- Flush the buffer
	move	a,s		; Get the character
	move	a,-1(a)		;   from the top of the stack
	idpb	a,bufptr	; Put it into the buffer
	popj	s,		; and Return

;Flubfr	- Output the buffer
;
;	Pushj	s,Flubfr	Flubfr()
;	  <only return>
;
flubfr:	setz	a,		; Put a NULL at the end of the buffer
	idpb	a,bufptr
	movei	a,ttybuf-1	; Get the address of the buffer
	hrli	a,10700		; Make a byte pointer
	movem	a,bufptr	; Store it for next time
	movei	a,5*ttybfs-1	; Calculate the buffer size
	movem	a,ttysfd	; Store it for next time
	hrroi	a,.Axost	; Output String to aux-port
	Auxcal	a,ttybuf	; (so Image mode won't be killed)
	Popj	s,		; And return

Subttl	VUEMAC	Writestring

;Writestring -	Routine to write a string to the screen taking care of
;		possibly funny characters.
;
;	Push	s,[Asciz /TEXT/]	Writestring(plit asciz 'TEXT');
;	Pushj	s,Writestring
;	  <only return>
;

WRITESTRIN::				;  global routine writestring(foo)
	Skipe	Silent			;   If .silent
	  Popj	s,			;       then return;
	Move	a,s			;
	Hrrzi	a,@-1(a)		;   WS$Ptr _ Point 7,.foo
	Hrli	a,(Point 7)		;
	Movem	a,WS$Ptr		;
WS$LP:	Ildb	a,WS$Ptr		;   a_scani(WS$Ptr);
	 Jumpe	a,Cpopj			;   if .a eql 0 then return;
	Push	s,a			;
	Pushj	s,Writeloss		;   writeloss(.c);
	Sub	s,[1,,1]		; end while .a neq 0;
	Jrst	WS$LP			;

Subttl	VUEMAC	Writeloss

;Writeloss -	Routine to write a possibly funny character
;
;	Push	s,[char]		Writeloss("char");
;	Pushj	s,Writeloss
;	  <only return>
;
WRITELOSS::				; Global routine writeloss(foo)
	Skipe	Silent			;   If .silent
	  Popj	s,			;       then return;
	Move	a,s			;
	Move	a,-1(a)			; a _ .foo
	Cail	a," "			; If .foo geq " "
	Caile	a,177			;     and lss .Rub
	  Jrst	WLOSS0			; NO
	Push	s,a			;   then begin
	Pushj	s,PUTCH			;     Putch(.foo)
	Pop	s,a
	Aos	Physpos			;     Physpos _ Physpos+1
	  Popj	s,			;   end; Return;

WLOSS0:	Cain	a,15			; else If .foo eql #cr
	Jrst	LEFTMARGIN		;         then return Leftmargin()
	Caie	a,11			; else If .foo eql #tab
	  Jrst	WLOSS2			; NO
	Push	s,[Exp 11]		;	then begin
	Push	s,PHYSPOS		;
	Pushj	s,UPDATECURS		;	  Updatecursor(#tab,.Physpos)
	Sub	s,[2,,2]		;
	Push	s,v			;
	Push	s,PHYSLIN		;
	Pushj	s,GOXY			;	  Goxy( .v, .Physlin)
	Sub	s,[2,,2]		;
	Popj	s,			;	Return;

WLOSS2:	Came	a,ESCVALUE		; else If .foo eql .EscValue
	  Jrst	WLOSS4			; NO
	Push	s,["$"]			;      then
	Pushj	s,PUTCH			;	  Putch("$")
	Sub	s,[1,,1]		;
	Aos	PHYSPOS			;	  Physpos _ .Physpos + 1
	Popj	s,			;	  Return

WLOSS4:	Cain	a,177			; else if .foo eql .Rub
	  Movni	a,35			;	then foo _ "#"-#100;
	Push	s,a			; else+rubout ...
	Push	s,["^"]			;
	Pushj	s,PUTCH			;	  Putch("^")
	Sub	s,[1,,1]		;
	Pop	s,a			;
	Addi	a,100			;
	Push	s,a			
	Pushj	s,PUTCH			;	  Putch(.foo+#100)
	Sub	s,[1,,1]		;
	Aos	PHY		;
	Aos	PHYSPOS			;	  Physpos _ .Physpos + 2
	Popj	s,			;	Return;

Subttl	VUEMAC	UpdateCursor

;UpdateCursor -	Routine to calculate the next character position
;		beginning at .foo if the character is .xc
;
;	Push	s,["char"]		UpdateCursor (xc,foo);
;	Push	s,[position]
;	Pushj	s,UpdateCursor
;	  <only return>
;

UPDATECURS::			
	Move	t,s			;
	Move	v,-1(t)			; Formal (.foo)
	Move	t,-2(t)			; Formal (.xc)
	Caie	t,11			;
	  Jrst	UPDCUR			;
	Soj	v,			;	.foo - 1
	Idiv	v,TABSIZE		;	  % .tabsize
	Imul	v,TABSIZE		;	    * .tabsize
	Add	v,TABSIZE		;	 + .tabsize
	Aoja	v,Cpopj			;	 + 1

UPDCUR:	Caile	t," "			; else if .xc geq " "
	Caile	t,177			;     and .xc lss .Rub
	  Caia				; NO
	Aoja	v,Cpopj			;	then .foo + 1
	Camn	t,ESCVALUE		; else if .xc eql .EscValue
	  Aoja	v,Cpopj			;	then .foo + 1
	Addi	v,2			; else .foo + 2
	Popj	s,			; Global Return!

Subttl	OLD Q-REGister MACRO space 100 words * n return a pointer to it

;Qreg	- Returns a byte pointer to q-space indexed by (a to z)
;
;	Push	s,["a"]		tp _ Qreg("a")
;	Pushj	s,qreg
;	  <only return>				v _ [Point 7,qspace("a")]
;
qreg:	movei	v,qspace-1	; Address of q-space
	hrli	v,10700		; Build a pointer
	move	a,s		; Get the name from the stack
	move	a,-1(a)
	cail	a,"a"		; Allow both UPPER and LOWER case
	 movei	a,-40(a)	;  to map into A-Z
	movei	a,-"A"(a)	; Offset from 0
	imuli	a,qwsize	; 100 words of storage for each
	add	v,a		; add for offset into q-space
	popj	s,		; Return a byte pointer in V

Subttl	VUEMAC	Dorescan - Rescan the command line

dorescan:
	rescan			; Allow the last line to be read
	 skpinl			; Anything there?
	   popj	s,		;  No! Just return

ifn ftcommand,<
reresc:	movei b,6		; Setup a temp buffer
	setzm	rsrwrd		;   for the command name
	move	a,[point 6,rsrwrd]
	movem	a,rsrwpt	; and a pointer to it
>
rescn:	hrroi	a,.Axi8s	; Look for a filename argument
	Auxcal	a,a		;  (Use Aux port so won't kill image mode)
	 skipa
	  trza	a,200
	popj s,			;Flopped:  no hyphen
	caie	a,"("		;[CB @ GC] *** Allow "(" since monitor does
	cain	a,";"		;for visit file
	jrst	vfile
	cain	a,"!"		;for old buffer
	jrst	obuff
ife ftcommand,<
	caie	a,"-"		;[CB] Hack attack
>
ifn ftcommand,<
	cain	a,"-"
	  jrst	rfile		;for read file
	cail	a,"a"
	caile	a,"z"
	  caia
	movei	a,-40(a)
	movei	a,-40(a)	;make sixbit
	 jumple	a,restst	;test word for monitor command
	sojle	b,rescn
	idpb	a,rsrwpt	;save the character
>
	jrst	rescn		;Look again
rfile:	pushj	s,ctrlxr	;Found the filename arg, process it
lrt:
	hrroi	a,.Axi8s	;eat the line feed if we can
	Auxcal	a,a		; (Use Aux port so won't kill image mode)
	 skipa
	  trza	a,200
	popj	s,
	popj	s,
vfile:	pushj	s,ctrlxv
	jrst	lrt		;eat the line feed if we can.
obuff:	pushj	s,uctrlo
	jrst	lrt		;eat the line feed if we can.
ifn ftcommand,<
restst:	caige	b,0
	  setz	b,
	imuli	b,6
	seto	a,
	lsh	a,(b)
	move	b,[sixbit/vue/]
	and	a,b
	camn	a,rsrwrd
	  jrst	rfile
	move	a,[sixbit/ct4/]
	camn	a,rsrwrd
	  jrst	rfile
	move	a,[sixbit/vue/]
	camn	a,rsrwrd
	  jrst	rfile
	jrst	reresc
> ; end ifn ftcommand
Subttl	VUEMAC	Setlab	- Set re-enter address and save bliss stuff

setlab:	movem	s,saveds	; Save S and next 4 BLISS registers
	movem	1,saveds+1	;	????
	movem	2,saveds+2	;	FREG
	movem	3,saveds+3	;	VREG
	move	a,s		; Get address of where to start
	move	a,0(a)		; ...as in RE-Start
	movem	a,place		; Store it some place
	movei	a,readr		; Get REEnter address
	movem	a,.Jbren	; Set REEnter address.
	popj	s,		; Return

quit:	pushj	s,fen		; Reset the terminal
	exit	1,		; Bye.
	pushj	s,fef		; Oh?  Turn it back on.
	popj	s,		; Return to where we came from

exerr:	pushj	s,fen		; Reset the terminal
	exit	1,		; Bye.
readr:	movei	a,1		; Set repeat count to 1
	movem	a,repcount
	setz	a,		; Clear ...
	movem	a,rptr		; Rptr	(pop their stack)
	movem	a,csnum		; Csnum	(tty input)
	move	s,saveds	; Restore saved values...
	move	1,saveds+1
	move	2,saveds+2
	move	3,saveds+3
	pushj	s,fef		; Setup the terminal again
	jumpa	@place

	Reloc	; 0

place:	0
saveds:	block	4	;register save area.

	Reloc	; 400000

realup:	move	a,s
	move	a,-1(a)	;get the first argument
	movei	v,0	;default failure
	lookup	2,0(a)	;lookup on the block.
	  popj	s,	;failed
	movei	v,1	;otherwise return true.
	popj	s,

lookop:	move	a,filblk+4	;ppn
	movem	a,lblk+1
	move	a,filblk+1	;filenam
	movem	a,lblk+2
	move	a,filblk+2	;extension
	movem	a,lblk+3
	movei	a,17
	movem	a,lblk
	lookup	2,lblk
	  jrst	ret0		;lookup failed.

	movsi	a,'dsk'		; Use DSK in all cases.

	movem	a,filblk
	jrst	ret1		; lookup succeeded!

Subttl	VUEMAC	Gettty	Getwid	Getaux	Getlen

gettty:	skipa	a,[-1,,.axttp]	; (100) Terminal type
getwid:	hrroi	a,.axwid	;  (27) Line width
getaux:	seto	v,		; Read my port
	auxcal	a,v		; Value returned in v
	popj s,

getlen:	seto	v,	; Tymcom-X doesn't do this at all
	popj s,

Subttl	VUEMAC	Getaun	Getab	Mail

Mail:	hrroi	v,.gtprv	; Look at user privs
	gettab	v,		;    in the monitor tables
	  setz	v,		; Well?
	trnn	v,1b23		; Check for MAIL-WAITING bit
	  tdza	v,v		;  No! Clear all other bits & skip
	movei	v,1		; Yes! Make it TRUE
	popj	s,		; Return

getnam:	hrroi	v,.gtnam	; Program name
	jrst	getab
getlic:	hrroi	v,.gtlic	; User license
	jrst	getab
getprv:	hrroi	v,.gtprv	; User privilages
	jrst	getab

getfpn:	skipa	v,[-1,,.gtfpn]	; Frame ppn
getaun:	hrroi	v,.gtaun	; Accounting User Number
getab:	gettab	v,		;    for this user
	  setz	v,		; Hmmmm. well indicate FALSE
	popj	s,		; Return

Subttl	VUEMAC	Getppn	Getjob

Getppn:	Calli	v,24		; Getppn v,
	  jfcl			; Jacct skip if 1,2
	popj	s,		; Return

Getjob:	Pjob	v,		; Get my job number
	popj	s,		; Return


imout:	pushj	s,flubfr	;flush the output buffer.
	move	a,s
	move	a,-1(a)		;get the argument
	ioneou	a
	popj	s,

wrnum:	move	a,0		;for indexing.
	move	a,-1(a)		;the argument.
	subi	a,1		;useful. converts to 0 origin.
	move	c,ttytyp	;get the terminal type.
	caie	c,^d10		;is the terminal a vt100?	[cb]
	cain	c,^d14		;is it a hp19
	jrst	heath		;for now, can do better.
	caie	c,^d23		;is it a hp2621?
	cain	c,7		;hp 2648
	jrst	heath		;two of a kind I guess.
	caie	c,^d15		;dm1520
	cain	c,^d16		;act4
	jrst	binout
	cain	c,^d21		;mime4
	jrst	binout
	cain	c,1		;superbee
	jrst	superb
	cain	c,6		;6is concept 100.
	jrst	concep		;the concept 100.
	idivi	a,12		;divide by 10. 2 has x/10, 3 has rem.
	lsh	a,4		;shift l
	add	b,a		;now r2 has bcd version.
	pushj	s,flubfr	;write out lines.
	ioneou	b		;image mode.
	popj	s,		;return.

concep:	addi	a,40		;add thirty-two
	push	s,a
	pushj	s,putch
	pop	s,a
	popj	s,

binout: push	s,a
	pushj	s,imout
	pop	s,a
	popj	s,

superb:	move	c,a		;save a copy thereof.
	push	s,["0"]
	pushj	s,putch
	pop	s,a
	idivi	c,12		;calculate place to go.
	addi	c,"0"
	addi	d,"0"
	push	s,c
	pushj	s,putch
	pop	s,a
	push	s,d
	pushj	s,putch
	pop	s,a
	popj	s,

heath:	move	c,a		;save a copy thereof.
	idivi	c,12		;calculate place to go.
	addi	c,"0"
	addi	d,"0"
	cain	c,"0"
	jrst	zersp
	push	s,c
	pushj	s,putch
	pop	s,a
zersp:	push	s,d
	pushj	s,putch
	pop	s,a
	popj	s,
Subttl	VUEMAC	Moveforward - Move pointer forward n bytes

movefo: ;come here to move forward n bytes. adjust curline too.
	setzm ctrlct
	move a,curlin
	movem a,ocl	;save to see if passed a cr.
	move a,s	;can not indirect off of sp with this bliss.
	move a,-1(a)	;get the number of bytes we are to move.
	movei b,1
; next we note that to get the byte pointer before foo, we
; do use effective address of (foo-1)/5+.p
; and byte offset of 7*(foo-5*((foo-1)/5))
; so we generate byte ptrs for .lowend and .hbot
	move e,lowend
	subi e,1	;lowend-1
	idivi e,5
	hrrz c,e	;move foo-1 fifths to rh of c.
	imuli c,5
	sub c,lowend	;nearly the answer we want.
	imuli c,7
	addi c,44	;add 36 decimal.
	lsh c,36	;move over 12 bits decimal.
	hrli e,700	;byte size.
	add e,p		;add base in.
	add c,e		;now c is the byte ptr we need.
;end calculation of byte ptr.
	move e,hbot
	subi e,1	;hbot-1
	idivi e,5
	hrrz d,e	;move foo-1 fifths to rh of d.
	imuli d,5
	sub d,hbot	;nearly the answer we want.
	imuli d,7
	addi d,44	;add 36 decimal.
	lsh d,36	;move over 12 bits decimal.
	hrli e,700	;byte size.
	add e,p		;add base in.
	add d,e		;now d is the byte ptr we need.
;now d has the from target ptr, c has the to target ptr
;and a has the count of the number of iterations of the next loop:
	add	c,[70000,,0]		;add to backup bptr.
	add	d,[70000,,0]		;add to backup bptr.
	skipge	c
	 sub	c,[430000,,1]
	skipge	d
	 sub	d,[430000,,1]
	addm	a,lowend
	addm	a,hbot
lp1:	sosge a		;do loop ctr.
	jrst car	;go home after checking ocl with curlin.
	ildb e,d	;get byte
	cain e,14
	movem b,ctrlct	;count of these.
	cain e,15	;skip if not a c-ret
	addm b,curlin
	idpb e,c	;dump it
	jrst lp1

Subttl	VUEMAC	Moveback - Move the pointer backward n bytes

moveba: ;come here to move backwards n bytes. adjust curline too.
	setzm ctrlct
	movei a,1
	movem a,cluhac	;set direction bit
	move a,curlin	;save curlin
	movem a,ocl	;in ocl.
	move a,s	;can not indirect off of sp with this bliss.
	move a,-1(a)	;get the number of bytes we are to move.
	movni b,1	;for use to decrement curlin[e].
; next we note that to get the byte pointer before foo, we
; do use effective address of (foo-1)/5+.p
; and byte offset of 7*(foo-5*((foo-1)/5))
; so we generate byte ptrs for .lowend and .hbot
	move e,lowend
	subi e,1	;lowend-1
	idivi e,5
	hrrz c,e	;move foo-1 fifths to rh of c.
	imuli c,5
	sub c,lowend	;nearly the answer we want.
	imuli c,7
	addi c,44	;add 36 decimal.
	lsh c,36	;move over 12 bits decimal.
	hrli e,700	;byte size.
	add e,p		;add base in.
	add c,e		;now c is the byte ptr we need.
;end calculation of byte ptr.
	move e,hbot
	subi e,1	;hbot-1
	idivi e,5
	hrrz d,e	;move foo-1 fifths to rh of d.
	imuli d,5
	sub d,hbot	;nearly the answer we want.
	imuli d,7
	addi d,44	;add 36 decimal.
	lsh d,36	;move over 12 bits decimal.
	hrli e,700	;byte size.
	add e,p		;add base in.
	add d,e		;now d is the byte ptr we need.
;now d has the from target ptr, c has the to target ptr
;and a has the count of the number of iterations of the next loop:
	movn a,a
	addm a,lowend
	addm a,hbot
lp2:	aosle a		;do loop ctr.
	jrst car		;go home and set muline perhaps
	add c,[7b5]	;decrement number
	caig c,0
	sub c,[430000,,1]
	ldb e,c		;get byte
	cain e,14
	movem b,ctrlct
	cain e,15	;skip if not a c-ret
	addm b,curlin
	add d,[7b5]	;decrement the thing.
	caig d,0
	sub d,[430000,,1]
	dpb e,d		;dump it
	jrst lp2

car:	move a,ctrlct
	caie a,0
	jrst mulpag
	move a,ocl
	camn a,curlin
	popj s,
	movei a,1
	movem a,muline
	popj s,

mulpag:	movei a,1
	movem a,muline
	movem a,scrmod
	popj	s,

Subttl	VUEMAC	Dmpin/Dmpout - (Chan, Addr, Size)

; args are channel,address,size
dmpin:	move	a,s
	move	b,-1(a)		;the size
	move	a,-2(a)		;the address
	movn	b,b		;negate it.
	hrlm	b,dmblk		;put the size down
	subi	a,1		;calc address.
	hrrm	a,dmblk		;
	movei	v,1		;won
	in	2,dmblk		;dump mode block
	  popj	s,
	movei	v,0		;error return -> lost.
	popj	s,

dmpout:	move	a,s
	move	b,-1(a)		;the size
	move	a,-2(a)		;the address
	movn	b,b		;negate it.
	hrlm	b,dmblk		;put the size down
	subi	a,1		;calc address.
	hrrm	a,dmblk		;
	output	2,dmblk		;dump mode block
	popj	s,



getchx:	movei	v,^d13			;prepare to return a <cr>.
	move	a,s
	skipg	a,-1(a)			;1 is start, get desired char.
	 popj	s,
	camge	a,lowend		;compare arg with lowend
	 jrst	norel			;in low part of gap.
	add	a,hbot			;add this in.
	sub	a,lowend		;just do the formula
	caml	a,hend
	 popj	s,
norel:	idivi	a,5			;a is count/5, a+1 is remainder
	add	a,p
	ldb	v,table(b)		;magic
	popj	s,

table:	point	7,-1(a),5*7-1
	point	7,(a),1*7-1
	point	7,(a),2*7-1
	point	7,(a),3*7-1
	point	7,(a),4*7-1

ntbl:	100700-1,,-1
	10700-1,,-1
	350700,,0
	260700,,0
	170700,,0

therep:	;Patch for the other version of therep
	; Convert char pos in arg 2 to an ildb ptr.
	move	a,s
	move	b,-1(a)		;the char ptr.

	camge	b,lowend		;compare arg with lowend
	 jrst	norel2			;in low part of gap.
	add	b,hbot			;add this in.
	sub	b,lowend		;just do the formula
norel2:	idivi	b,5			;b is count/5, b+1 is remainder
	add	b,p
	add	b,ntbl(c)		;magic byte ptr we will use.
	jrst	ftpae			;alt entry to ftp

ftp:	;Fast version of therep. Takes as arg 1 (-2(sp)) the line we're
	;looking for, and as arg 2 (-1(sp)) the ildb byte ptr to use.
	;Assumes that line is already uppercased, for speed.
	move	a,s		;get the sp somewhere useful.
	move	b,-1(a)		;the byte ptr to use.
ftpae:	move	a,-2(a)		;the ptr to line
	move	d,0(a)		;the size field
	movei	v,0		;return false by default.

ftplp:	sosge	d		;loop
	jrst	ftpwon		;if we are equal.
	ildb	c,b		;get a char.
	cail	c,"a"		;less than "a" is ok.
	 caile	c,"z"		;less than "z".
	  caia			;no convert to uppercase.
	 subi	c,40		;convert it.
nocv:	came	c,1(a)		;are we equal here?
	popj	s,
	addi	a,1
	jrst	ftplp
ftpwon:	movei	v,1
	popj	s,


E$$COD:	Outstr	[Asciz\
?
?VUECOD	Cannot Open Device for file rename(s) - Please recover with Olde buffer
\]
		Jrst	exerr			; Exit

E$$NEC:	Outstr	[Asciz\
?
?VUENEC Not enough core to edit file
\]
	jrst	exerr


	Reloc	; 0

bufptr: xwd 10700,ttybuf-1

	Radix	^d10

scroff: 0	;offset of buffer in screen.
scrlen: ^d18

ttybfs=^d100		;tty buf size in words.
ttybuf: block ttybfs
ttysfd: 5*ttybfs-1	;init it correctly.
qwsize==^d100
qspace: block 26*qwsize ; 500 chars by 26 macros.

finver: 1
distbl: block 512	; Here is the standard vue environment table !

physpos: 1
physlin: 1
ws$ptr:	0		; Storage - Pointer for Writestring
tabsize: 8
csnum: 0
cluhac:	0
sst: block 132
linesz: 79
clulin:	0
clupos: 0
hitsts:	1
scrmod: 1
muline: 1
ocline: -1
comchar:	0
newcount:	0
repcount:	0
lowend:		0
hend:		0
hbot:		0
p:		0
save: 0
mark: 0
scrsze:	18

vuetyp:	0			; VUE type
submod:	0			; SUB Mode
ttytyp: 0			; Terminal type (numeric)
ttype:	0			; Terminal type in sixbit
tspeed:	0			; Terminal output speed

Usrprv:	z			; User privilage word
Usrlic:	z			; User license word
Usrnam:	z			; User program name
UsrAUN:	z			; User Account Number
UsrFPN:	z			; User FILE PPN
UsrPPN:				; User GFD'd PPN
ppn: 0				;  "    "     "
curline:	1
flush:	0
base:	0

; Here is the end of the standard vue environment.

modpos: 18

maxss: 30
screen:	block	27*132		;[CB] Change 25==>27 hazeltine 2000
dskbfh:	block	3
dskbuf:	block	132
	radix	8

tymtty:	block	1		;[CB @ Tymshare] Remember tty characteristics
inirvx:	block	1		;[CB @ Tymshare] Remember tty characteristics

rsrptr:	point	7,rsrbuf	;pointer into buffer
rsrpnt:	point	7,rsrbuf	;pointer into buffer
rsrcnt:	block	1		;count character space
rsrchr:	block	1		;Rescan break character, -;(
rsrwrd:	block	1
rsrwpt:	point	6,rsrwrd
rsrbuf:	block	<^d135*2>/5	;Room for 2 lines at 132 chars
	rsrend=.
	rsrmax=<rsrend-rsrbuf>*5 ;# characters maximum

;cstk:	block	35		;max depth of  macro calls.
rsrstk:	block	1		;place for rescan pointer

ocl:	0
ctrlct:	0
dmblk:	0
	0
lblk:	block 20


bakx: sixbit/bak/
defdsk: sixbit/dsk/
zapdev:	0
	sixbit/dsk/
	0

bfblk1:	sixbit	/dsk/
	sixbit	/main/
	sixbit	/vue/
	Exp 0,0,0

bfblk2:	sixbit	/dsk/
	sixbit	/main/
	sixbit	/vue/
	Exp 0,0,0

newblk: block	6
filblk: sixbit/dsk/
	sixbit/delete/
	Exp 0,0,0,0

autblk: sixbit/dsk/
	sixbit/vue/
	sixbit/tmp/
	Exp 0,0,0

modblk:	sixbit/dsk/
	sixbit/init/
	sixbit/exe/
	Exp 0,0,0


	Reloc	; 400000		; Literals, etc should be shareable

	End
 7 ~¢