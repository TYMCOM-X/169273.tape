Module Status = Begin

! Copyright 1981 by Carl A Baltrunas, Tymshare, Inc. Cupertino, CA.

External Writeloss,Writestring,ToStatus,Gbmod;
External InverseVideo,NormalVideo,Whiteline;
External Vuetyp,Ttype,Modblk,Submod,Filblk,Bfblk2;
External Hitsts,Ttytype,Autoinc,Lowend,Hend,Hbot;

Global statusline=1;			!true if displaying status line
Global percnt;

Bind JBver = #137, JBddt = #74;

Routine writesix(word) = Begin
    local Chr,I,Tp;
    tp_(.word-1)<0,6>;
    Incr i from 1 to 6 do begin
	chr_scani(tp);
	If .chr neq 0 then writeloss(.chr+#40);
    end;
end;
Global routine dostat = begin
external wrtoct,putch,mail;
Local Ext;
Hitsts_0;			!clear the flag now.
! Putch("?G"); ! Beep !
if not .statusline then return;

InverseVideo();
ToStatus();		!move the cursor to the right place and clear line.
WhiteLine();

writestring(plit asciz ' VUE ');
if .JBver<0,36> neq 0 then begin
    local ver,min,edit,who;
    ver_.JBver<24,9>;
    min_.JBver<18,6>;
    edit_.JBver<0,18>;
    who_.JBver<33,3>;
    writeloss("%");
    wrtoct(.ver);
    if .min neq 0 then writeloss(.min+"@");
    if .edit neq 0 then begin writeloss("(");wrtoct(.edit);writeloss(")"); end;
    if .who neq 0 then begin writeloss("-");writeloss(.who+"0"); end;
    writeloss(" ");
end;
if .vuetyp neq 0 then begin writesix(vuetyp); writeloss(" "); end;
if .JBddt<0,36> neq 0 then writestring(plit asciz '+DDT ');
Writeloss("(");
    Writesix(ttype);		!first terminal type
    Writeloss(" ");
    Writesix(modblk+1);		!mode
    if .submod neq 0 then begin writeloss(" "); writesix(submod); end;
    if .autoinc neq 0 then writestring(plit asciz ' Save');
writestring(plit asciz ') ');

writesix(bfblk2+1);		!buffer name
writestring(plit asciz '  File:');
writesix(filblk+1);		!Filename part
Ext_.(filblk+2) and #777777000000;
if .Ext neq 0 then begin
    writeloss(".");
    writesix(Ext);		!Extension part
end;

writestring (plit asciz '  --');	!a separation.
percnt_.lowend*100/(.lowend+.hend-.hbot);
if .lowend eql 1 then writestring(plit asciz'TOP--')
else if .hend eql .hbot then writestring(plit asciz'BOT--')
else begin
    writeloss(.percnt/10+"0");
    writeloss(.percnt-10*(.percnt/10)+"0");
    writestring(plit asciz'%--');
end;
if gbmod(.(bfblk2+1)) then writestring(plit asciz' *');
if mail() then writestring(plit asciz' {MAIL}');
normalvideo();
return;
end;		!the procedure

end eludom
