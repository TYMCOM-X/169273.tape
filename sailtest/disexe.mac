TITLE	DISEXE - DISASSEMBLER FOR .EXE FILES
SUBTTL	A CHRISTOPHER HALL FECIT

EXTERN	.JBFF,.JBREL,.JBAPR,.JBTPC,.JBSA

;ASSEMBLY INSTRUCTIONS: 	.LOA DISEXE+DISCOM
;				.SAVE DISEXE

FTREL==0		;TELL DISCOM TO DO THE RIGHT THING
DEBUG==0		;NOT ZERO ASSEMBLES CHECKSUMS

;ACCUMULATORS

LN=0	 		;ADDRESS OF WORD BEING OUTPUT
A=1			;\
B=2			; \ TEMPORARY
C=3			; / REGISTERS
D=4			;/
F=5			;FOR FLAGS
SP=6			;SEQUENTIAL SYMBOL TABLE POINTER
WD=7			;ONE WORD FROM THE INPUT FILE
DP=10			;POINTER TO FILE DIRECTORY
ST=11			;POINTER TO ADDRESSES OF ASCII STRINGS
LB=12			;LABEL ADDRESSES
OP=13			;OP-CODES
SY=14			;SYMBOL ADDRESS
CT=15			;COUNTER OF CHARACTERS
CH=16			;CHARACTERS FOR OUTPUT
P=17			;PUSH DOWN STACK

;FLAGS

DGT==1			;A DIGIT HAS BEEN OUTPUT (PUTOCT)
EXT==2			;WORKING WITH AN EXTERNAL
HIS==4	 		;WORKING IN HIGH SEG
FND==10			;FOUND A CHARACTER (GETFIL, STRIN)
CMT==20			;COMMENT HAS BEEN STARTED
OAS==40			;OUTPUTTING AN ASCII STRING
OUF==100		;OUTPUT SPECS HAVE BEEN FOUND (GETFIL)

;LEFT HALF FLAGS

LOC==1			;LOCATION IS A LOCATION
IMD==2			;IMMEDIATE INST - MAY TAKE LOCATION
ACV==10 		;AC TAKES A VALUE
NOP==20			;INSTRUCTION IS A NOOP
JMP==100		;END OF BLOCK OF INSTS
SKP==200		;FALSE END OF A BLOCK

IND==20 		;INDIRECT BIT IS ON

;IO CHANNELS

IN==1
OUT==2

OPDEF	PJRST[JRST]

START:	MOVE	P,[IOWD 10,STACK]
	PUSHJ	P,SETUP 	;DO GENERAL SETTING UP
 	MOVE	A,.JBFF		;FIND FIRST FREE LOCATION
	MOVEM	A,DP		;STORE AS PTR TO DIRECTORY
 	ADDI	A,100		;ADD SOME ROOM FOR OVERFLOW
	MOVEM	A,SYMBAS	;SAVE AS BASE OF SYMBOL TABLE

;READ IN THE DIRECTORY AND STORE

	PUSHJ	P,GETWD 	;GET DIRECTORY HEADER
	HRLZ	C,B		;GET DIRECTORY LENGTH
	MOVNS	C		;NEGATE IT
	AOBJN	C,.+1		;IT WAS ONE TOO SMALL
	HRR	C,DP		;AND PUT IN DIRECTORY ADDRESS
DIRLUP: PUSHJ	P,GETWD 	;GET A FIRST DIRECTORY WORD
	PUSHJ	P,GETWD 	;GET A SECOND DIRECTORY WORD
	HRRM	B,1(C)		;SAVE ADDRESS IN SECOND WORD
	LSH	B,-31		;GET NUMBER OF BLOCKS (HUNKS * 4)
	ADDI	B,3		;PLUS THREE MORE
	MOVEM	B,(C)		;SAVE IT IN FIRST WORD
	AOBJN	C,.+1		;SKIP OVER TWO WORDS
	AOBJN	C,DIRLUP	;READ UNTIL EXHAUSTED

;DIRECTORY IS IN - SKIP TO BEGINNING OF FILE

	PUSHJ	P,STFILE	;GET TO START OF FILE
	MOVE	A,INBUF1	;GET BUFFER ADDRESS
	MOVE	SY,141(A)	;GET VERSION (IF ANY)
	MOVEM	SY,VERSON	;SAVE IT
 	HLRZ	SY,117(A)	;GET ADR OF FIRST FREE HISEG LOCATION
	ADDI	SY,400000	;SET HIGH ORDER ADDRESS BIT
	MOVEM	SY,HGHTOP	;SAVE AS TOP OF HISEG
	MOVE	SY,122(A)	;GET DITTO FOR LOWSEG
	HLRZM	SY,LOWTOP	;SAVE AS TOP OF LOWSEG
 	HRRZS	SY		;GET PROGRAM STARTING ADDRESS
	MOVEM	SY,STRADR	;SAVE IT
	PUSHJ	P,SAVSYM	;ALSO IN SYMBOL TABLE
 	OUTSTR	[ASCIZ /STARTING PASS 1
/]
	MOVEI	A,PAS1ND	;GET ADDRESS FOR END OF FILE
	MOVEM	A,GETLOC	;SAVE IT

;SET UP SYMBOL TABLE - EXIT IS FROM GETWD, TO PAS1ND

PASS1:	PUSHJ	P,GETWD 	;GET A WORD OF THE FILE
       	HRRZ	SY,B		;GET LOCATION FIELD
	CAIG	SY,40		;TOO LOW TO BE A SYMBOL?
	JRST	PASS1		;YES - SKIP IT
	CAMG	SY,LOWTOP	;WITHIN LOWSEG RANGE?
	JRST	PASS1B		;YES - DRIVE ON
	CAIL	SY,400010	;COULD SYMBOL BE IN HISEG?
	CAML	SY,HGHTOP	;..
	JRST	PASS1		;NO - GET ANOTHER PROGRAM WORD

PASS1B:	LDB 	OP,[POINT 9,B,8];GET AN OP CODE
 	CAIL	OP,40		;TOO SMALL,
	CAIL	OP,700		;  OR TOO BIG FOR OP-CODE?
	JRST	PASS1		;YES - IGNORE THIS WORD

	HLRZ	A,B		;GET LEFT HALF OF INSTRUCTION WORD
	CAIN	A,051140	;IS IT A DIRECT OUTSTR?
	PUSHJ	P,TTCSTG	;YES - SAVE ADDRESS OF STRING
 	LSH	OP,1		;MAKE ADDRESS OUT OF OP-CODE
	HRL	F,OPTABL-77(OP) ;SET UP OP-CODE FLAGS
 	TLNN	F,NOP		;IS THIS A NOOP CODE?
 	TLNN	F,LOC!IMD	;NO - DOES THIS OP TAKE A VALUE?
	JRST	PASS1  	 	;NO - SKIP IT
       	CAIGE	SY,140		;SYMBOL VALUE TOO LOW?
 	JRST	[TLNN B,17	;YES - IS INSTRUCTION INDEXED?
 		 TLNE F,IMD	;NO - IMMEDIATE?
 		 JRST PASS1	;YES - SKIP IT, THEN
	 	 JRST .+1]	;NO - IT REALLY IS AN EXTERNAL
 	PUSHJ	P,SAVSYM	;NO - SAVE THE SYMBOL VALUE
	JRST 	PASS1	 	;BUMP POINTER AND GET SOME MORE

;END OF PASS 1 - NOW SET UP OUTPUT FILE

PAS1ND:	PUSHJ	P,SORT		;SORT SYMBOL TABLE
 	JSA	B,PUTASK	;OUTPUT TITLE PSUEDO-OP
	ASCIZ	/TITLE	/
 	MOVE	A,INFIL		;GET FILE NAME
 	PUSHJ	P,PUTSXA	;OUTPUT NAME
	PUSHJ	P,CRLF	 	;GET A NEW LINE
	MOVE	A,SYMCNT	;GET POSITIVE COUNT OF SYMBOLS
 	JFFO	A,.+1		;SEE HOW BIG TABLE IS
	MOVNI	B,-43(B)	;SUBTRACT FROM 36
	MOVEI	A,1		;SET FOR SHIFT
	LSH	A,(B)		;GET POWER OF TWO LESS THAN SIZE
	MOVEM	A,SYSIZ2	;SAVE FOR SEARCHER

	OUTSTR	[ASCIZ /STARTING PASS 2
/]
	MOVE	SP,SYMBAS	;GET START OF SYMBOL TABLE
HDLSM0:	TRZ	F,EXT	 	;DO A NEW HEADER
 	MOVEI	LN,6	 	;DO SIX EXTERNALS PER LINE
HDLSYM: HRRZ	SY,(SP) 	;LOOK AT A SYMBOL
	CAIL	SY,140		;IS IT AN EXTERNAL?
	JRST	HDLVER		;NO - START DECODE
	PUSHJ	P,EXSRC 	;YES - FIND LOCATION ON TABLE
	JUMPL	A,.+2		;FOUND IT?
	PUSHJ	P,TELUSE	;NO - TELL USER
	TRON	F,EXT		;ALREADY PRINTED HEADER?
	PUSHJ	P,HDLEXT	;NO - DO SO NOW (AND SKIP RETURN)
 	PUSHJ	P,COMMA 	;YES - PUT A COMMA BETWEEN THEM
	PUSHJ	P,HDLEXL	;OUTPUT EXTERNAL NAME
	SOJLE	LN,HDLSM0	;NEW LINE IF DONE SIX?
 	AOJA	SP,HDLSYM	;ELSE CHECK NEXT SYMBOL

HDLVER: SKIPN	VERSON		;GOT A VERSION?
	JRST	HDLSET		;NO - DON'T TALK ABOUT IT
 	JSA	B,PUTASK	;YES - OUTPUT VERSION NUMBER
 	ASCIZ	/

	LOC	137
	XWD	/
 	MOVS	SY,VERSON	;OUTPUT LEFT HALF
	PUSHJ	P,PUTOCT	;..
	PUSHJ	P,COMMA 	;SEPARATE WITH A COMMA
	MOVSS	SY		;OUTPUT RIGHT HALF
	PUSHJ	P,PUTOCT	;..
 	JSA	B,PUTASK	;GET BACK TO THE RIGHT LOCATION
 	ASCIZ	/
	RELOC 	0/

HDLSET: TRZ	F,EXT	 	;NO LONGER OUTPUTTING LIST OF EXTERNALS
	MOVEI	LN,140		;GET ADDRESS OF FIRST WORD OF FILE
 	HRRZ	LB,(SP) 	;GET LOCATION OF FIRST LABEL
 	JSA	B,PUTASK	;OUTPUT IT
	ASCIZ /

	TWOSEG			;REMOVE IF ONLY ONE SEGMENT

;ACCUMULATOR ASSIGNMENTS

A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
I=11
J=12
K=13
L=14
M=15
N=16
P=17

/
 	PUSHJ	P,STFILE	;GET BACK TO START OF FILE
	MOVEI	A,HDLEND	;GET ADDRESS FOR JUMP ON END OF FILE
	MOVEM	A,GETLOC	;SAVE IT
 	SETZ	CT,		;NO CHARS OUTPUT YET FOR THIS LINE

HDLWD:	TRZ	F,CMT		;NOT IN A COMMENT NOW
 	PUSHJ	P,GETIWD	;GET AN INSTRUCTION WORD
	JUMPE	LB,.+3		;DONE WITH LABELS IF ZERO
     	CAMG	LB,LN	 	;DOES THIS LINE GET A LABEL?
 	PUSHJ	P,HDLLBL	;YES - LABEL IT
	TRNE	F,OAS		;OUTPUTTING A STRING?
	JRST	HDLAS1		;YES - CONTINUE IT

 	JUMPE	WD,HDLZRO	;BLOCK TOGETHER ZERO WORDS
 	SKIPE	ZERWDS		;FOUND ANY ZERO WORDS?
	PUSHJ	P,HDLBLK   	;YES - OUTPUT A BLOCK STATEMENT
 	PUSHJ	P,TAB		;TAB OVER
HDLWD1:	CAML	LN,STADR(ST)	;AT OR PAST A STRING ADDRESS?
	JRST	[CAMG LN,STADR(ST)	;YES - START STRING HERE?
 		 AOJA ST,HDLASC		;YES - OUTPUT ASCII STRING
 		 AOJA ST,HDLWD1]	;NO - SKIP AND CHECK AGAIN

HDLOP1: LDB 	OP,[POINT 9,WD,8];GET OP-CODE
 	CAIN	OP,51		;IS IT TTCALL?
	JRST	TTCHDL		;YES - GET TTCALL MNEMONIC
 	CAIL	OP,40		;TOO SMALL,
	CAIL	OP,700		;  OR TOO BIG FOR OP-CODE?
	JRST	HDLDTA		;YES - DO AS DATA
	LSH	OP,1		;NO - MAKE ADDRESS OUT OF IT
	HRL	F,OPTABL-77(OP) ;SET UP OP-CODE FLAGS
 	TLNE	F,NOP		;IS THIS A NOOP CODE?
	JRST	HDLDTA		;YES - DO AS DATA

 	HRRZ	SY,WD		;GET LOCATION FIELD
	CAMG	SY,LOWTOP	;IN LOWSEG?
	JRST	HDLINS		;YES - COULD BE AN ADDRESS
	CAIL	SY,400000	;IN HISEG, MAYBE?
	CAML	SY,HGHTOP	;..
	CAIA			;NO - SEE IF INSTRUCTION USES DATA
	JRST	HDLINS		;YES - DRIVE ON
 	TLNE	F,LOC		;DOES OP TAKE A VALUE?
 	JRST	[TLNE WD,17	;NO - IS THERE AN INDEX FIELD?
		 CAMG SY,[-11]	;AND IS A SMALL NEGATIVE NUMBER?
		 JRST HDLDTA	;NO - OUTPUT AS DATA
		 JRST .+1]	;YES - DO IT AS AN INSTRUCTION

;HERE TO OUTPUT AN INSTRUCTION

HDLINS: MOVEI	B,OPTABL-100(OP);SET UP POINTER TO OP-CODE MNEMONIC
;	TRO	F,INS		;REMEMBER THAT INST WAS OUTPUT
	PUSHJ	P,PUTASC	;OUTPUT IT
 	PUSHJ	P,TAB		;AND A TAB

 	LDB 	B,[POINT 4,WD,12];GET AC FIELD
 	JUMPE	B,HDLLOC	;NO AC IF ZERO
	TLNE	F,ACV		;IS IT AN AC OR A VALUE?
	JRST	HDLACV		;VALUE - OUTPUT AS SUCH
	MOVE	CH,ACTABL(B)	;AC - GET ITS NAME
	PUSHJ	P,PUTCHR	;OUTPUT IT
	PUSHJ	P,COMMA 	;AND A COMMA
HDLLOC: TLNE	WD,IND		;IS INDIRECT BIT SET?
	PUSHJ	P,AT		;YES - OUTPUT AN @
	TLNN	F,LOC!IMD	;IS IT A LOCATION OR A VALUE?
	JRST	HDLVAL		;VALUE - DO IT
	TLNE	F,IMD		;IS IT IMMEDIATE,
	CAIL	SY,140		;  AND LOCATION WITHIN RANGE?
	CAIA			;YES - ASSUME IT TAKES A LOCATION
	JRST	HDLVAL		;NO - CALL IT A VALUE, THEN
	JUMPE	SY,HDLINX	;LOCATION - NOTHING IF ZERO
	CAIG	SY,17		;AC IN LOCATION FIELD?
	JRST	HDLLAC		;YES - TAKE CARE OF IT
	CAIGE	SY,140		;IS SYMBOL EXTERNAL?
	JRST	HDLEX0		;YES - DO IT SPECIALLY
	PUSHJ	P,SYSRC 	;LOOK UP SYMBOL IN TABLE
	JUMPL	A,HDLVAL	;IF NOT FOUND GIVE THE VALUE
	PUSHJ	P,DLRSGN	;START ALL SYMBOLS WITH A $
	HLLZ	A,(A)		;GET SYMBOL NAME
 	PUSHJ	P,PUTSXA	;OUTPUT SYMBOL NAME

HDLINX: LDB 	B,[POINT 4,WD,17];GET INDEX
 	JUMPE	B,HDLCMT	;SKIP IF NO INDEX
	PUSHJ	P,LPAREN	;OUTPUT (
	MOVE	CH,ACTABL(B)	;GET AC NAME
	PUSHJ	P,PUTCHR	;OUTPUT IT
 	PUSHJ	P,RPAREN	;AND A )
HDLCMT:	SKIPE	COMENT		;WANT COMMENTS?
	JRST	HDLNXT		;NO - SKIP THIS
 	PUSHJ	P,HDLLNM	;START COMMENT WITH LINE NUMBER
 	HLRZ	SY,WD		;PICK UP LEFT HALF
	JRST	HDLDT2		;OUTPUT WORD AS DATA

;HERE TO OUTPUT THE WHOLE WORD AS DATA

HDLDTA:	HLRZ	SY,WD		;GET LEFT HALF
	JUMPE	SY,HDLDT1	;IF NO LH, JUST OUTPUT RH
 	JSA	B,PUTASK	;OUTPUT IT
	ASCIZ	/XWD/
 	PUSHJ	P,TAB		;TAB OVER
HDLDT2:	PUSHJ	P,PUTOCT	;OUTPUT THE WORD
	PUSHJ	P,COMMA 	;SPLIT THE WORD WITH A COMMA
HDLDT1:	HRRZ	SY,WD		;AND RIGHT HALF OF WORD
	PUSHJ	P,PUTOCT	;OUTPUT IT
	SKIPL	COMENT		;WANT COMMENTS?
	PUSHJ	P,VERASC	;YES - TRY TO MAKE CHARACTERS OUT OF IT

HDLNXT:	PUSHJ	P,CRLF		;END OF THIS LINE
 	AOJA 	LN,HDLWD 	;GET MORE WORDS UNTIL DONE

;HERE ON END OF PASS 2

HDLEND: SUB	LN,ZERWDS	;FIND NUMBER OF LINES LEFT OVER
	TRNE	F,HIS		;IN HIGHSEG?
	SKIPA	WD,HGHTOP	;YES - SET UP TOP OF FILE
	MOVE	WD,LOWTOP	;NO - SET UP REAL TOP OF FILE
	CAML	LN,WD		;ALREADY AT TOP OF FILE?
 	JRST	HDLEN1		;YES - JUST FINISH UP

HDLEN0: JUMPN	LB,.+2		;IS THERE A NEXT LABEL?
	SKIPA	A,WD		;NO - GET END OF FILE
 	MOVE	A,LB		;YES - GET LINE FOR LABEL
	SUB	A,LN		;MINUS PRESENT POSITION
	MOVEM	A,ZERWDS	;SAVE AS SIZE OF BLOCK
	MOVE	LN,LB		;SET UP NEW POSITION
	PUSHJ	P,HDLBLK	;OUTPUT SIZE OF BLOCK
	JUMPE	LB,HDLEN1	;DONE IF NO MORE LABELS
	PUSHJ	P,HDLLBL	;ELSE OUTPUT LABEL
	JRST	HDLEN0		;DO NEXT BLOCK

HDLEN1:	JSA	B,PUTASK	;OUTPUT END STATEMENT
	ASCIZ	/	END	/
 	MOVE	SY,STRADR	;GET PROGRAM STARTING ADDRESS
	PUSHJ	P,SYSRC 	;LOOK IT UP
	PUSHJ	P,DLRSGN	;OUTPUT A $
 	HLLZ	A,(A)		;GET NAME
 	PUSHJ	P,PUTSXA	;OUTPUT NAME
	PUSHJ	P,CRLF		;ONE FINAL CARRIAGE RETURN
 	CLOSE	OUT,		;SHUT IT ALL OFF
	JRST	REENT 		;SEE IF HE WANTS ANOTHER FILE

;HERE TO OUTPUT AC FIELD AS A VALUE

HDLACV: MOVE	C,[POINT 3,B,29];GET POINTER TO LAST 2 OCTAL BYTES OF B
	PUSHJ	P,PUTOCT+1	;OUTPUT AC
	JRST	HDLLOC-1	;BACK TO FLOW - OUTPUT COMMA

;HERE TO OUTPUT LOCATION FIELD AS A VALUE

HDLVAL:	CAIL	SY,770000	;IS VALUE NEGATIVE?
	PUSHJ	P,HDLNEG	;YES - OUTPUT AS SIGN-MAGNITUDE
	PUSHJ	P,PUTOCT	;OUTPUT THE VALUE
	JRST	HDLINX		;SEE IF THERE'S AN INDEX FIELD

HDLNEG: MOVNS	SY		;GET MAGNITUDE
	PJRST	DASH		;OUTPUT MINUS SIGN AND RETURN

;HERE TO HANDLE AN AC IN THE LOCATION FIELD

HDLLAC: MOVE	CH,ACTABL(SY)	;GET AC NAME
	PUSHJ	P,PUTCHR	;OUT WITH IT
	JRST	HDLINX		;BACK TO FLOW

;HERE TO COUNT ZERO WORDS

HDLZRO: AOS	ZERWDS		;BUMP COUNT
 	AOJA 	LN,HDLWD 	;GET MORE WORDS UNTIL DONE

;HERE TO HANDLE TTCALL OP-CODES

TTCHDL: LDB	B,[POINT 4,WD,12];GET AC FIELD
 	MOVE	A,TTTABL(B)	;GET TTCALL MNEMONIC
 	PUSHJ	P,PUTSXA	;OUTPUT OP-CODE
;	TRO	F,INS		;SET INSTRUCTION FLAG
	HRLI	F,11		;FUDGE IN TTCALL FLAGS
	PUSHJ	P,TAB		;TAB OVER
	HRRZ	SY,WD		;GET LOCATION FIELD
 	JRST	HDLLOC		;PICK UP THE FLOW AGAIN

;SUBROUTINE TO STORE ADDRESS OF AN ASCII STRING (IN INTERNAL TABLE)
;CALLED DURING PASS 1

TTCSTG:	SETZ	A,	  	;CLEAR POINTER INTO EXTERNAL TABLE
	MOVE	C,SY		;NEW SYMBOL VALUE
TTCST1: SKIPN	B,STADR(A)  	;IS THERE AN ENTRY HERE?
	SOJA	A,TTCST4	;NO - SAVE HERE
 	CAMLE	C,B	  	;IS IT BIGGER THAN THE NEW ONE?
 	AOJA	A,TTCST1	;NO - LOOP
 	CAMN	C,B	  	;IS IT THE SAME AS THE NEW ONE?
 	POPJ	P,		;YES - JUST RETURN

TTCST2:	EXCH	C,STADR(A)	;SWAP PREVIOUS ONE WITH THIS ONE
 	SKIPE	STADR+1(A)	;ANY MORE ADDRESSES?
	AOJA	A,TTCST2	;YES - LOOP
 			 	;NO - SAVE LAST ONE HERE

TTCST4: MOVEM	C,STADR+1(A)	;SAVE IN OPEN SPOT
	POPJ	P,		;AND RETURN

;SUBROUTINE TO SEARCH EXTERNAL TABLE SEQUENTIALLY (ONLY HAS ABOUT 12 ENTRIES)

EXSRC:
IFN DEBUG,<
      	AOS	$EXREF		;REFS TO EXTERNAL
>
 	HRLZ 	A,EXSIZ		;SET UP NEGATIVE LENGTH OF TABLE IN LH
EXSRC1: HRRZ	C,EXTABL(A)	;GET EXTERNAL VALUE
IFN DEBUG,<
 	AOS	$EXLUK		;EXTERNAL LOOKS
>
 	CAME	C,SY		;IS IT THIS ONE?
 	AOBJN	A,EXSRC1	;NO - KEEP LOOKING
	POPJ	P,

;SUBROUTINE TO SAVE A SYMBOL IN THE TABLE

SAVSYM:	
IFN DEBUG,<
	AOS	$SYSUR		;SYMBOLS FOUND
>
 	MOVE	SP,SY		;SET UP POINTER TO SYMBOL TABLE
 	CAIL	SP,400000	;IS SYMBOL IN HISEG?
	SUBI	SP,350000	;YES - MAKE IT SMALLER
 	LSH	SP,-2		;MAKE THE TABLE POINTER
	ADD	SP,SYMBAS	;..
 	HRRZ	A,(SP)		;GET ENTRY
	MOVE	B,SP
	JUMPE	A,SAVSY2	;IF SLOT IS OPEN, SAVE HERE
	CAMN	A,SY		;ALREADY GOT THIS ENTRY?
	POPJ	P,		;YES - QUIT
	CAMG	A,SY		;IS ENTRY SMALLER THAN NEW ONE?
	JRST	SAVSYB		;YES - GO ELSEWHERE
SAVSYA: PUSHJ	P,SAVACH	;GET NEXT ENTRY
	JUMPE	A,SAVSY2	;IF OPEN, SAVE HERE
	CAMN	A,SY		;ALREADY GOT IT?
	POPJ	P,		;YES - QUIT
	CAML	A,SY		;ENTRY STILL LARGER?
	JRST	SAVSYA		;YES - LOOK AT NEXT ONE DOWN
	MOVE	C,B		;NO - MAKE HOLE HERE FOR ENTRY
SAVSA1: PUSHJ	P,SAVACH	;GET ENTRY
	CAMN	A,SY		;HERE ALREADY?
	POPJ	P,		;YES ALREADY
	JUMPN	A,SAVSA1	;IF OCCUPIED, GET NEXT ONE
	MOVE	D,B		;FOUND OPEN SPACE - PUSH IT UP FOR HOLE
SAVSA2: AOJ	D,		;POINT TO NEXT HIGHER WORD
IFN DEBUG,<
 	AOS	$SYSUM		;SYMBOLS SHUFFLED
>
 	MOVE	CH,(D)		;GET IT
	MOVEM	CH,(B)		;MOVE IT DOWN ONE
	AOJ	B,		;BUMP POINTER
	CAME	B,C		;DONE YET?
	JRST	SAVSA2		;NO - KEEP SHIFTING
	JRST	SAVSY2		;YES - SAVE SYMBOL IN HOLE

SAVACH: SOJ	B,		;POINT DOWN ONE MORE
IFN DEBUG,<
 	AOS	$SYSUL		;SYMBOLS LOOKED AT
>
 	HRRZ	A,(B)	 	;GET CYMBAL VALUE
 	POPJ	P,		;RETURN

SAVSYB: PUSHJ	P,SAVBCH	;GET ENTRY
	JUMPE	A,SAVSY2	;IF OPEN, SAVE HERE
	CAMN	A,SY		;IS THIS IT?
	POPJ	P,		;YES - RETURN
	CAMG	A,SY		;STILL LARGER?
	JRST	SAVSYB		;YES - KEEP LOOKING
	MOVE	C,B		;NO - MAKE HOLE HERE
SAVSB1: PUSHJ	P,SAVBCH	;GET ENTRY
	CAMN	A,SY		;GOT IT ALREADY?
	POPJ	P,		;YES - QUIT
	JUMPN	A,SAVSB1	;IF OCCUPIED, LOOK AT NEXT ONE UP
	CAMLE	B,SYMTOP	;PAST END OF TABLE?
	MOVEM	B,SYMTOP	;YES - SAVE AS NEW END
 	MOVE	D,B		;OPEN - MOVE DOWN FOR HOLE
SAVSB2: SOJ	D,		;POINT TO ONE ENTRY DOWN
IFN DEBUG,<
 	AOS	$SYSUM		;SYMBOLS MOVED
>
 	MOVE	CH,(D)		;GET IT
	MOVEM	CH,(B)		;PUT IT ONE FARTHER UP
	SOJ	B,		;BUMP POINTER DOWN
	CAME	B,C		;MOVED EVERYTHING?
	JRST	SAVSB2		;NO - KEEP IT UP
	JRST	SAVSY2		;YES - SAVE IN HOLE

SAVBCH: AOJ	B,		;BUMP POINTER
IFN DEBUG,<
 	AOS	$SYSUL		;SYMBOLS LOOKED AT
>
 	HRRZ	A,(B)		;GET ENTRY
 	POPJ	P,		;AND RETURN

SAVSY2:	HRRM	SY,(B)		;PUT NEW SYMBOL IN OPEN SPOT
	CAMLE	B,SYMTOP	;IS THIS THE NEW TOP OF TABLE?
	MOVEM	B,SYMTOP	;YES - SAVE IT
 	POPJ	P,		;AND RETURN

;SUBROUTINES TO OUTPUT CERTAIN CHARACTERS (FOR MORE, SEE DISCOM)

DLRSGN: MOVEI	CH,"$"
	PJRST	PUTCHR

ZERO:	MOVEI	CH,"0"
	PJRST	PUTCHR

;SUBROUTINE TO OUTPUT AN EXTERNAL NAME

HDLEX0:	TLNE	WD,17	 	;IS INSTRUCTION INDEXED?
	JRST	HDLVAL	 	;YES - OUTPUT A VALUE, THEN
 	PUSHJ	P,EXSRC 	;LOOK THROUGH TABLE

HDLEXL:	MOVE	D,SY	 	;STORE SY FOR LATER
 	MOVE	SY,A		;PICK UP EXTERNAL TABLE INDEX
 	JSA	B,PUTASK	;OUTPUT START OF NAME
 	ASCIZ	/.JB/
 	JUMPGE	SY,HDLEXX	;IF IT WASN'T FOUND, OUTPUT NUMBERS
 	HLLZ	A,EXTABL(SY)	;ELSE GET REST OF NAME
 	TRNE	F,EXT		;HANDLING EXTERNAL OR LOCATION?
 	PJRST	PUTSXA		;EXTERNAL - OUTPUT NAME AND RETURN
 	PUSHJ	P,PUTSXA	;LOCATION - OUTPUT NAME
 	JRST	HDLINX		;AND CONTINUE THE FLOW

HDLEXX: MOVE	C,[POINT 3,SY,26];GET POINTER TO VALUE
 	MOVE	SY,D	 	;GET SYMBOL ADDRESS BACK
 	TRNE	F,EXT		;HANDLING EXTERNAL OR LOCATION?
 	PJRST	PUTOCT+1	;EXTERNAL - OUTPUT VALUE AND RETURN
 	PUSHJ	P,PUTOCT+1	;LOCATION - OUTPUT VALUE
 	JRST	HDLINX		;AND CONTINUE THE FLOW

;SUBROUTINE TO PUT A LABEL AT THE START OF A LINE

HDLLBL: SKIPE	ZERWDS		;WORKING ON A BLOCK?
	PUSHJ	P,HDLBLK	;YES - OUTPUT IT NOW
 	PUSHJ	P,DLRSGN	;START WITH A DOLLAR SIGN
 	HLLZ	A,(SP)		;GET LABEL NAME
 	PUSHJ	P,PUTSXA	;OUTPUT LABEL
	CAME	LN,LB		;IS THIS REALLY THE LABELLED LINE?
	AOJA	SP,HDLLBA	;NO - DO AN ASSIGNMENT, THEN
 	HRRZ	LB,1(SP) 	;GET NEXT LABEL LOCATION
 	AOJA 	SP,COLON	;COLON THE LABEL AND RETURN

;HERE TO OUTPUT LABEL THAT IS PAST, AS AN ASSIGNMENT

HDLLBA: JSA	B,PUTASK	;OUTPUT EQUALITY
	ASCIZ	/==/
 	MOVE	SY,LB		;OUTPUT LINE LABEL SHOULD BE ON
	PUSHJ	P,PUTOCT
	PUSHJ	P,CRLF		;END OF THIS LINE
	HRRZ	LB,(SP) 	;GET ADDRESS OF NEXT LABEL
	CAMG	LB,LN		;PUT IT HERE, TOO?
	JRST	HDLLBL		;YES - GO THROUGH IT ALL AGAIN
	POPJ	P,		;NO - RETURN

;SUBROUTINE TO OUTPUT EXTERNAL HEADER

HDLEXT:	JSA	B,PUTASK	;OUTPUT IT
	ASCIZ	/
EXTERN	/
	AOS	(P)		;GIVE SKIP
 	POPJ	P,		;  RETURN

;SUBROUTINE TO OUTPUT A BLOCK

HDLBLK: PUSHJ	P,TAB		;TAB OVER
	MOVE	SY,ZERWDS	;GET SIZE OF BLOCK
 	SETZM	ZERWDS		;CLEAR COUNT OF ZEROS
 	CAIN	SY,1		;ONLY ONE ZERO?
	SOJA	LN,HDLBK1	;YES - OUTPUT A 0 THEN
 	JSA	B,PUTASK	;OUTPUT IT
	ASCIZ	/BLOCK	/
 	PUSHJ	P,PUTOCT	;OUTPUT IT
 	PJRST	CRLF		;END WITH <CRLF>

HDLBK1: PUSHJ	P,ZERO		;OUTPUT A ZERO
	PUSHJ	P,HDLLNM+1	;OUTPUT LINE NUMBER
 	AOJA	LN,CRLF		;RESTORE LINE NUMBER AND END WITH <CRLF>

;SUBROUTINE TO TELL USER ABOUT EXTERNAL NOT FOUND

TELUSE:	MOVE	OP,SY
 	MOVE	C,SY		;SET UP FOR CONVERSION
 	LSH	C,4		;CONVERT ITS 3 DIGITS TO ASCII
	ANDI	OP,7		;\
	OR	OP,C		; \    THIS CODE SHIFTS THREE
  	LSH	C,4		;  \   3-BIT OCTAL  DIGITS TO
	ANDI	C,340000	;  /   THREE 7-BIT ASCII BYTES
	OR	OP,C		; /
	ANDI	OP,341607	;/
	ADD	OP,[3014060]	;CONVERT THE OCTALS TO ASCIIS
	LSH	OP,17		;LEFT-JUSTIFY
	ORI	OP,6424		;PUT IN A <CRLF>
 	MOVEM	OP,PRTEXT	;SAVE THE LAST WORD
	OUTSTR	PRTMES
 	POPJ	P,

;COMPRESSES (NOT SORTS) SYMBOL TABLE. ALSO PUTS IN SYMBOL NAMES

SORT:	MOVNI	A,40		;BACK UP TABLE POINTER 40 WORDS,
	ADDB	A,SYMBAS	;  AND GET IT
 	MOVEI	B,1(A)		;MAKE B POINT TO A + 1
	MOVE	D,NAME		;GET STARTING NAME VALUE
SORT1:	
IFN DEBUG,<
 	AOS	$SSORT		;SORT PASSES
>
	SKIPN	C,(B)		;GET AN ENTRY - ZERO?
 	JRST	SORT2 		;YES - IGNORE IT
 	HRL	C,D		;ELSE SET UP THE NAME
 	MOVEM	C,;AND SAVE IN LOWEST HOLE
 	ADDI	D,10000		;SET UP NEXT NAME - INC FIRST LETTER
	CAIGE 	D,730000	;IS IT PAST Z?
	AOJA	A,SORT2	 	;NO - CONTINUE
 	SUBI	D,317700	;YES - MAKE FIRST LETTER A; INC SECOND
	CAIL 	D,417300	;IS SECOND LETTER PAST Z?
	SUBI	D,3177		;YES - MAKE SECOND LETTER A; INC THIRD
	AOJ	A,		;BUMP HOLE POINTER
SORT2:	CAME	B,SYMTOP	;DONE 'EM ALL?
	AOJA	B,SORT1		;NO - DO SOME MORE
 	SETZM	(A)		;FINISH OFF TABLE WITH A ZERO
 	MOVEM	A,SYMTOP	;MAKE TOP OF TABLE RIGHT
	SOS	SYMTOP		;BUT IT WAS ONE TOO MANY
 	SUB	A,SYMBAS	;MAKE NEW SYMCNT
 	MOVEM	A,SYMCNT	;..
 	POPJ	P,

;GET NEW DIRECTORY ITEM AND SET UP ADDRESS

GETIWS: ADDI	DP,2		;POINT TO NEXT ITEM'S ADDRESS
  	MOVE	A,1(DP) 	;GET PAGE ADDRESS
	LSH	A,11		;SHIFT OVER FOR REAL ADDRESS
	MOVE	B,A		;GET IT HERE, TOO
	SKIPE	A		;kluge to avoid bashing LN
 	EXCH 	A,LN	 	;SWAP NEW AND OLD LINE NUMBERS

 	CAIL	B,400000	;IS IT NOW A HISEG ADDRESS?
	TROE	F,HIS		;YES - FIRST ONE IN HISEG?
      	JRST	GETIW2		;NO - GO GET A WORD

 	SKIPN	ZERWDS		;YES - FOUND ANY ZERO WORDS?
	JRST	GETIRL		;NO - JUST DO RELOCATION
	MOVE	B,LOWTOP	;GET FIRST FREE ADR IN LOWSEG
	SUB	B,A		;MINUS OLD LINE NUMBER
				;GIVES NEG NUMBER OF WORDS TOO MANY
	ADDM	B,ZERWDS	;SUBTRACT OVERAGE FROM TOTAL # ZEROS
	SKIPLE	ZERWDS		;OUTPUT BLOCK ONLY?
 	PUSHJ	P,HDLBLK   	;YES - PUT BLOCK BEFORE RELOC

GETIRL:	JSA	B,PUTASK	;RELOCATE TO HIGH SEGMENT
	ASCIZ	/
	RELOC	400000

/
	SETZ	CT,		;CLEAR LINE COUNTER
 	IN	IN,		;GET THE BUFFERFUL
	MOVEI	A,10		;SKIP 8 WORDS OF HISEG HEADER
	ADDI	LN,10	 	;MAKE LINE NUMBER RIGHT, TOO
	PUSHJ	P,GETWD 	;DO THE SKIP
	SOJG	A,.-1		;  (FALL INTO GETIWD WHEN DONE)

;SUBROUTINE TO INPUT AN INSTRUCTION WORD AND SET UP ADDRESS POINTERS

GETIWD:	SOSGE	INBUF1+2	;BUFFER EMPTY?
	JRST	GETIW1		;YES - GET A NEW ONE
	ILDB	WD,INBUF1+1	;GET A WORD
 	POPJ 	P,    		;RETURN WITH IT

GETIW1: SOSGE	(DP)		;DONE WITH THIS DIRECTORY ITEM?
	JRST	GETIWS		;YES - SET UP A NEW ONE
GETIW2:	IN	IN,		;GET A BUFFERFUL
	  JRST GETIWD		;PROCESS IT
	POP	P,A		;END OF FILE - KILL CALL
	JRST	@GETLOC 	;AND GO TO DESIRED LOCATION

;SUBROUTINE TO INPUT A WORD FROM THE FILE

GETWD:	SOSGE	INBUF1+2	;BUFFER EMPTY?
	JRST	GETWD1		;YES - GET A NEW ONE
	ILDB	B,INBUF1+1	;GET A WORD
 	POPJ 	P,    		;RETURN WITH IT

GETWD1:	IN	IN,		;GET A BUFFERFUL
	  JRST GETWD		;PROCESS IT
	POP	P,A		;END OF FILE - KILL CALL
	JRST	@GETLOC 	;AND GO TO DESIRED LOCATION

;SUBROUTINE TO GET TO START OF FILE DATA - WORD 140 OF BLOCK 5

STFILE: USETI	IN,5		;GET TO START OF THE 5TH BLOCK
	IN	IN,		;READ IT IN
	SKIPA	A,[140] 	;PREPARE TO INPUT FIRST WORD OF FILE
	  HALT			;IT BETTER NOT BE END OF FILE
	PUSHJ	P,GETWD 	;SKIP UNTIL START OF FILE
	SOJG	A,.-1
	POPJ	P,		;DONE

;DATA
;EXTERNAL TABLE - SIXBIT NAME IN LH; VALUE IN RH

EXTABL: XWD	464600,121	;.JBFF
	XWD	624554,44	;.JBREL
 	XWD	656557,40	;.JBUUO
	XWD	634100,120	;.JBSA
 	XWD	416062,125	;.JBAPR
 	XWD	646043,127	;.JBTPC
 	XWD	576063,135	;.JBOPS
	XWD	444464,74	;.JBDDT
	XWD	506254,115	;.JBHRL
	XWD	373737,0	;?'S IF NOT FOUND
EXSIZ:	EXTABL+1-.

DEFINE OOPS1,<
;ACCUMULATOR TABLE

ACTABL:	EXP	"0","A","B","C","D","E","F"
	EXP	"G","H","I","J","K","L","M","N","P"

PRTMES:	ASCII /EXTERNAL NOT FOUND: /
PRTEXT:	0			;EXTERNAL LOCATION AND <CRLF> HERE
	0			;ZERO TO END OUTPUT
>
XLIST				;NOW SUPPRESS THE LISTING
OOPS1
LIST

VERSON: 0			;VERSION NUMBER (IF ANY)
ZERWDS: 0			;NUMBER OF ZEROS FOUND IN A ROW
STRADR: 0			;START ADDRESS OF PROGRAM
SYMBAS:	0			;START OF OUTPUT FILE
SYMCNT:	0			;NEGATIVE COUNT OF SYMBOLS
SYMTOP: 0			;TOP OF SYMBOL TABLE
SYSIZ2:	0			;POINTER TO CENTER OF SYMBOL TABLE
GETLOC: 0			;WHERE TO GO ON END OF FILE
HGHTOP: 0			;FIRST FREE ADDRESS OF HIGHSEG
LOWTOP: 0			;DITTO FOR LOWSEG
NAME:	414141			;SIXBIT <AAA> IN RH

STADR:	BLOCK	100		;LIST OF POINTERS TO ASCII STRINGS

;HERE ENDETH DISEXE, AND STARTETH DITHCOM

                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                      r@\ü