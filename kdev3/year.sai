define ! = "comment", !! = "comment";
begin
  require "(klib)sail.hdr" source!file;
  require "(klib)util.req" source!file;

  integer Brk, index, time, years;
  real principle, oldbalance, monthly, apr;
  string str;

  str _ Prompt.User("Enter principle: ");
  principle _ realscan(str, Brk);
  oldbalance _ principle;

  str   _ Prompt.User("Enter apr: ");
  apr _ realscan(str, Brk);
  apr _ (apr/12)/100;

  setformat(8, 2);
  str _ prompt.User("Enter how many years to pay: ");
  years _ 0 + realscan(str, Brk);
  monthly _ principle*(apr/(1-(1+apr)^(-12*years)));
  print ("Monthly payment: ", cvf(monthly), crlf, crlf);
  str _ Prompt.User("Enter monthly payment desired: ");
  monthly _ realscan(str, Brk);
  time _ 1;

  for index _ 1 step 1 until 1 do
  begin
    integer count;
    real minterest, yinterest, mprinciple, yprinciple, newbalance;

    print("Year: ", index, crlf);
    yinterest _ yprinciple _0;
    for count _ 1 step 1 until 12 do
    begin
      real temp;
      
      temp _ (1+apr)^(0-time);
      newbalance _ (1/temp) * (monthly * (temp-1)/apr + principle);
      minterest _ newbalance - oldbalance + monthly;
      yinterest _ yinterest + minterest;
      mprinciple _ monthly - minterest;
      yprinciple _ yprinciple + mprinciple;
      print("Month: ", count,
        ", interest: ", cvf(minterest),
        ", principle: ", cvf(mprinciple), 
        ", balance: ", cvf(newbalance), crlf);
      oldbalance _ newbalance;
      time _ time + 1;
      end;
    print ("principle this year: ", cvf(yprinciple), crlf);
    print ("interest this year: ", cvf(yinterest), crlf, crlf);
  end;

  exit;
end;