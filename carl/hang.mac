	TITLE	HANG	PROGRAM TO PLAY THE HANGMAN GAME
	SUBTTL	BY JIM BECKER & CARL BALTRUNAS

	SEARCH	CARL,MACTEN,JOBDAT,UUOSYM

	TWOSEG		;FOR BOTH HIGH & LOW STUFF

	EXTERN	LIST
	SUBTTL	LOW SEGMENT DATA STORAGE

RUNNAM:	BLOCK	1		; WHERE TO FIND ME
RUNDEV:	BLOCK	1		; SAME AS ABOVE
RUNPPN:	BLOCK	1		; AGAIN THE SAME.

SUCKER:	BLOCK	6		;ROOM FOR SUPERCALIFRAGILISTICEXPIALIDOCIOUS
				; OR FOR..PNEUMONOULTRASILICONVOLCANIKONIOSIS

SEED:	BLOCK	2		; THIS IS FOR RANDOM NUMBERS
MAX:	BLOCK	1		; HOLDS THE MAX NUMBER OF WORDS
MATCH:	BLOCK	1		; THE NUMBER OF CHARS - NUMBER FILLED IN
CHARS:	BLOCK	1		; THE NUMBER OF CHARACTERS IN WORD
WINS:	BLOCK	1		; COUNT HOW MANY TIMES HE GUESSES RIGHT
LOSS:	BLOCK	1		; AND HOW MANY TIMES HE LOSES

RIGHT:	BLOCK	6		; THE ONE'S YOU GOT RIGHT
GOOD:	BLOCK	1		; ALL YOUR GOOD GUESSES
WRONG:	BLOCK	5		; ALL YOUR WRONG GUESSES
WGUESS:	BLOCK	1		; MAX WRONG GUESSES THIS TURN

GUESS:	BLOCK	5		; ALL OF YOUR GUESSES
NUMBER:	BLOCK	1		; THE NUMBER OF WRONG GUESSES.
WORD:	BLOCK	6		; PLACE TO STORE YOUR WORD
	SUBTTL	MAIN PROGRAM.. ETC.


HANG:	RESET			;HAVE TO DO THIS...
	MOVEM	F,RUNNAM	; SAVE NAME WHERE WE CAME FROM
	MOVEM	7,RUNPPN	; AND WHAT PPN
	MOVEM	10,RUNDEV	; AND DEVICE... FOR GETSEG INFO
	MAKPDL 20		;SETUP THE PDL
	OUTSTR	[ASCIZ\

This is a game of HANGMAN!

The computer thinks up a word (He reads alot) for you to guess.
Then you have to figure out the word, letter by letter.
\]
	OUTSTR	[ASCIZ\
After each mistake you make, (and I'm sure you'll make alot),
the computer will draw a picture of your current state.  The
more WRONG guesses you have, the closer to getting HUNG you
will be.  Depending upon the word, you will be given more
chances to guess the wrong letters.
\]
	OUTSTR	[ASCIZ\
If you think you know what the word is... Just type the whole
word, but if you're wrong.. you get HUNG, drawn and quartered
immediately.

Good Luck.

\]
	CALL	RANI		; INITIALIZE THE RANDOM NUMBERS
	SUBTTL	MAIN PROGRAM LOOP

LOOP:	MOVE	T1,MAX			; GET THE MAX NUMBER IN THE RANGE
	CALL	RAN			; THEN GET A RANDOM NUMBER
	CALL	GETIT			; RETURN WITH POINTER IN P1 AND
					;  BLOCK IN LIST::
	MOVE	T1,[POINT 6,SUCKER]	; SETUP POINTER TO FUNNY WORD
	CALL	GETWRD			; THEN TRANSFER THE WORD
	MOVE	T1,MAX			; ONLY SO MANY WORDS NEAR HERE
	CALL	RAN			;   GET ANOTHER RANDOM #
	CALL	GETIT			;      RETURN WITH POINTER P1, ETC.
	MOVE	T1,[POINT 6,WORD]	; AGAIN GET A WORD
	CALL	GETWRD			; TRANSFER IT

	STORE	T1,RIGHT,NUMBER,0	;FILL THESE WITH ZERO'S
	MOVEM	T2,CHARS		; NUMBER OF CHARS IN WORD
	MOVEM	T2,MATCH		; NUMBER TO COUNT DOWN
	MOVE	C,'-'			; GET AN UNDERLINE IN SIXBIT
	MOVE	T1,[POINT 6,RIGHT]	; SETUP TO POINT TO WORD
	IDPB	C,T1			; ONCE FOR EACH CHAR
	SOJG	T2,.-1			; IN THE WORD.. FOR PRINTING
	OUTSTR	[ASCIZ\
Guess this word with			Characters Guessed
 less than \]
	MOVEI	T1,7			; SEVEN EXTRA?
	CALL	RAN			; SEE WHAT HAPPENS
	MOVEI	T1,10(T1)		; NOW GET A NUMBER BETWEEN 8 AND 14
	MOVEM	T1,WGUESS		; MAX WRONG GUESSES
	CALL	OUTDEC			; THIS NUMBER OF GUESSES
	OUTSTR	[ASCIZ\ WRONG answers			Good		Bad

  \]
	SUBTTL	MORE OF MAIN LOOP.

PLAY:	CALL	TYPANS			; THIS IS HOW MUCH HE KNOWS
	CALL	YUGESS			; SO YOU MAKE A GUESS
	  CALL	BADONE			; YOU GUESSED WRONG!
	SKIPE	MATCH			; SEE IF WE'RE FINISHED?
	  JRST	PLAY			; NO-- PLAY SOME MORE
	OUTSTR	[ASCIZ\
You Guessed the word.. Do you want to play again? \]
	CALL	YESNO			; GET HIS ANSWER
	  CAIA				; NO-- TOO BAD
	JRST	LOOP			; GO AHEAD AND PLAY
BYEBYE:	CALL	REPORT			; TELL HOW HE DID
FINISH:	EXIT	1,			; BYEBYE!
	OUTSTR	[ASCIZ\
?Can't Continue
\]					;FAKE IT.
	JRST	FINISH			; NO!
	SUBTTL	SUBROUTINES	GETWRD

;GETWRD - ROUTINE TO TRANSFER A WORD FROM HIGH CORE INTO LOW CORE
;
;	CALLED WITH P1 CONTAINING POINTER TO HIGH CORE
;		AND T1 CONTAINING POINTER TO LOW  CORE
;
;	MOVE	P1,[POINT 6,HIGH-ADDRESS]
;	MOVE	T1,[POINT 6,LOW-ADDRESS]
;
;	THIS ROUTINE ALSO RETURNS IN T2 THE COUNT OF CHARACTERS IN THE WORD
;

GETWRD:	SETZ	T2,			; CLEAR CHARACTER COUNT
GETCHR:	ILDB	C,P1			; GET A CHARACTER
	IDPB	C,T1			; PUT A CHARACTER
	SKIPE	C			; IF BLANK.. THE BYEBYE BYEBYE
	AOJA	T2,GETCHR		; LOOP TILL FIND A BLANK
	POPJ	P,			; RETURN
	SUBTTL	SUBROUTINES	INCHAR	GETIT

;INCHAR - READS A CHARACTER FROM THE TERMINAL AND RETURNS IT IN C
;
;	EOL CHARACTERS RETURN AS -1,  LOWER CASE IS CONVERTED TO UPPER
;

INCHAR:	INCHWL	C			;READ A CHARACTER
	CAIE	C,7			; FUNNY PERSON WITH A BELL
	CAIN	C,33			; OR AN ALTMODE
	  JRST	EOL			; END OF LINE!
	CAIL	C,12			; SEE IF OTHER EOL?
	CAILE	C,15			; ...LF,VT,FF,CR
	  CAIA				; NO--
	JRST	EOL			; THESE ARE FINE.
	CAIL	C,"a"			; OR alpha IN lower case.
	CAILE	C,"z"			; ...
	  POPJ	P,			; RETURN.
	TXZ	C,40			; CLEAR LOWER CASE BIT
	POPJ	P,			; RETURN

EOL:	CAIN	C,15			; SEE IF A CR?
	INCHRW	C			; YEP... SO GET LF
	SETO	C,			; SHOW UP FUNNY.
	POPJ	P,			; THEN RETURN TO USER
	SUBTTL	SUBROUTINES	RANI	RAN

;RANI - INITIALIZE SEED FOR RAN
;

RANI:	MSTIME	T1,			; GET THE TIME SINCE MIDNIGHT
	IMULI	T1,^D18			; MULTIPLY
	ADDI	T1,^D5			; ADD
	MOVEM	T1,SEED			; THEN SAVE IT.

	DATE	T1,			; GET THE DATE
	IDIVI	T1,^D1000		;  .MOD. 1000
	ADDI	T1,^D1000		; THEN ADD 1000 FOR LOOP
	MOVEM	T1,SEED+1		; THEN GO!
	POPJ	P,


;RAN - RANDOM NUMBER ROUTINE, GRATIS GOES TO CROWTHIER & WOODS OF
;	ADVENTURE FAME, AL LA JIM BECKER & KNUTH VOL.2
;
;	USES RANGE FROM 0 TO (T1)-1
;	RETURNS RESULT IN T1.
;

RAN:	MOVE	T2,SEED			; GET THE PLANT.
	MOVEI	T4,1			; GET ONE TO START
	SKIPN	T2			; SKIP IF SEED NON-ZERO
	MOVE	T4,SEED+1		; GET NUMBER
RANLOP:	IMULI	T2,^D1021		; MULT BY LITTLE LESS THAN 1K
	IDIV	T2,[EXP ^D1048576]	; GET A FUNNY NUMBER
	SOJG	T4,RANLOP		; LOOP IF YOU THINK NECESSARY
	MOVEM	T3,SEED			; NEW SEED
	IMUL	T1,T3			; MULT SEED BY RANGE
	IDIV	T1,[EXP ^D1048576]	; AND DIVIDE AGAIN
	POPJ	P,			; RETURN VALUE IN SEED
	SUBTTL	SUBROUTINES	YESNO

; YESNO - GETS A YES OR NO ANSWER FROM THE USER
;
;	RETURNS ARE AS FOLLOWS:
;
;		CALL	YESNO
;		  "NO" RETURN
;		"YES" RETURN

YESNO:	CALL	INCHAR			; GET A CHARACTER
	JUMPL	C,YNDONE		; IF NO ANSWER, ASSUME NO
	CAIE	C,40			; PLEASE IGNORE LEADING SPACE
	CAIN	C,11			; OR TAB... JUST
	  JRST	YESNO			;   IGNORE THEM WHEREVER
	PUSH	P,C			; SAVE THIS FIRST CHARACTER
YNWAIT:	CALL	INCHAR			; AND WAIT TILL HE SHUTS UP
	SKIPL	C			; DID HE SHUT UP?
	  JRST	YNWAIT			; NOPE -- BE PATIENT
	POP	P,C			; FINALLY! GET THAT 1ST CHAR BACK
	CAIN	C,"Y"			; WAS IT "Y"?
	  AOS	(P)			; YES -- GIVE A SKIP RETURN
YNDONE:	POPJ	P,			; RETURN!
	SUBTTL	SUBROUTINES	GETIT

;GETIT - ROUTINE TO GET A WORD.
;
;	CALLED WITH WORD POINTER IN T1.
;

INCORE:	BLOCK	1			; FILE IN CORE

PTROPN:	XWD 0,17			; DUMP MODE
	SIXBIT/DSK/
	Z
PTRFIL:	SIXBIT/HANGP/			; POINTER FILE
	SIXBIT/DAT/
	Z
	Z

WRDOPN:	XWD 0,17			; ALSO DUMP MODE
	SIXBIT/DSK/
	Z
WRDFIL:	SIXBIT/HANGW/			; WORD FILE
	SIXBIT/DAT/
	Z
	Z

ONEBLK:	IOWD 200,LIST			; ONE BLOCK AT LIST
	Z				; END THE COMMAND LIST

GETIT:	SKIPGE	INCORE			; MEANS THAT PTR FILE AINT THERE
	  JRST	HAVPTR			;   OTHERWISE, CARRY ON
	OPEN	1,PTROPN		; PTR FILE ON CHANNEL 1
	  JRST	EROPNP			; ERROR!
	LOOKUP	1,PTRFIL		; GET THAT FILE
	  JRST	ERLKPP			; ERROR AGAIN

HAVPTR:	IDIVI	T1,200			; FIGURE OUT WHICH BLOCK
	USETI	1,1(T1)			; AND POINT IT OUT
	INPUT	1,ONEBLK		; BRING THE BLOCK IN
	  JRST	GETWRD			; OK, GO GET THE WORD NOW
	OUTSTR	[ASCIZ/
DIDN'T GET THE BLOCK FROM THE POINTER FILE.
/]
	EXIT	1,

GETWRD:	MOVE P1,LIST(T2)		; FINALLY, WE HAVE OUR WORD
	RELEAS	1,			; BYE-BYE, PTR FILE
	OPEN	1,WRDOPN		; HELLO, WORD FILE
	  JRST	EROPNW			; OH WELL
	LOOKUP	1,WRDFIL		; FIND THE WORD FILE
	  JRST	ERLKPW			; ...BUT, THEN AGAIN
	USETI	1,1(T1)			; GET THE SAME BLOCK # AS BEFORE
	INPUT	1,ONEBLK		; PLOP IT INTO CORE
	  JRST	MORWRD			; DO SOME MORE TOWARD GETTING WRD
	OUTSTR	[ASCIZ/
COULDN'T GET THE BLOCK FROM THE WORD FILE.
/]
	EXIT	1,


EROPNP:	OUTSTR	[ASCIZ/
ERROR OPENING CHANNEL FOR PTR FILE.
/]
	EXIT	1,

ERLKPP:	OUTSTR	[ASCIZ/
ERROR LOOKING UP PTR FILE.
/]
	EXIT	1,

EROPNW:	OUTSTR	[ASCIZ/
ERROR OPENING CHANNEL FOR WORD FILE.
/]
	EXIT	1,

ERLKPW:	OUTSTR	[ASCIZ/
ERROR LOOKING UP WORD FILE.
/]
	EXIT	1,
	SUBTTL	SUBROUTINES	REPORT

;REPORT - ROUTINE TO TELL HOW MANY WINS AND LOSSES.
;		IT IS SMART ENOUGH TO FIND WINS & LOSS.
;

REPORT:	OUTSTR	[ASCIZ\

Well it looks like time to go...  However, you do make
a fine sport at this game.  You \]
	MOVE	T1,WINS			; SEE IF ANY WINS.
	JUMPG	T1,REP..1		; OK OK SO YOU WONE SOME
	OUTSTR	[ASCIZ\didn't even WIN 1 time.
\]
	JRST	REP..3			; SKIP WIN MESSAGES

REP..1:	SOJG	T1,REP..2		; MORE THAN ONCE..HMMM.
	OUTSTR	[ASCIZ\ONLY beat me ONE time.
\]
	JRST	REP..3			; NOW FOR THE LOSSES

REP..2:	AOJ	T1,			; MAKE SURE ACCURATE REPORTS
	OUTSTR	[ASCIZ\beat me \]	; TYPE FIRST PART
	CALL	OUTDEC			; THEN THE NUMBER
	OUTSTR	[ASCIZ\times.
\]

REP..3:	MOVE	T1,LOSS			; SEE HOW MANY TIMES I BEAT HIM.
	JUMPE	T1,REP..9		; ALL DONE.
	CAME	T1,WINS			; SEE IF TIE MATCH?
	  JRST	REP..4			; NO.
	OUTSTR	[ASCIZ\However, it appears to be a tie!\]
	JRST	REP..9			; FINISH UP

REP..4:	SOJG	T1,REP..5		; SEE IF ONLY ONCE.
	OUTSTR	[ASCIZ\But, you only lost one time.\]
	JRST	REP..9			; FINISH UP

REP..5:	AOJ	T1,			; FIXUP THE RIGHT NUMBER
	OUTSTR	[ASCIZ\And YOU lost to dumb little me, \]
	CALL	OUTDEC			; PRINT NUMBER
	OUTSTR	[ASCIZ\ times.\]	; FINISH UP

REP..9:	OUTSTR	[ASCIZ\

\]
	POPJ	P,			; RETURN
	SUBTTL	SUBROUTINES	OUTDEC

;OUTDEC - ROUTINE TO TYPE OUT A NUMBER IN DECIMAL RADIX
;
;	MOVE	T1,NUMBER	; GET A NUMBER
;	CALL	OUTDEC		; ...
;	  <RETURN>
;

OUTDEC:	IDIVI	T1,^D10		; DIVIDE
	HRLM	T2,(P)		; GET A REMAINDER
	SKIPE	T1		; SKIP WHEN 0
	PUSHJ	P,OUTDEC	; PRINT A NUMBER
	HLRZ	C,(P)		; GET A DIGIT
	MOVEI	C,"0"(C)	; MAKE IT PRINTABLE
	OUTCHR	C		; THEN PRINT IT.
	POPJ	P,		; RETURN.. THAT WAS QUICK.
	SUBTTL	SUBROUTINES	TYPANS

;TYPANS - ROUTINE TO TYPE THE ANSWER KNOWN SO FAR
;

TYPANS:	MOVE	T1,[POINT 6,RIGHT]	; GET A LIST FOR ME
	MOVE	T2,CHARS		; GET CHARACTER COUNT
TYPSTR:	ILDB	C,T1			; GET A CHAR
	MOVEI	C,40(C)			; PRINT A GOOD ONE
	OUTCHR	C			; PRINT 1 CHAR
	OUTCHR	[" "]			; FOLLOWED BY SPACE
	SOJG	T2,TYPSTR		; TYPE THE STRING
	OUTCHR	["	"]		; TYPE A TAB
	MOVE	T1,[POINT 6,GOOD]	; LOOK AT GOOD ONES
TYPGUD:	ILDB	C,T1			; READ A CHAR
	JUMPLE	C,TYPGOD		; DONE GOOD
	MOVEI	C,40(C)			; MAKE ASCII
	OUTCHR	C			; PRINT A CHAR
	JRST	TYPGUD			; LOOP TILL " "

TYPGOD:	OUTCHR	["	"]		; TAB
	MOVE	T1,[POINT 6,WRONG]	; LIST OF WRONG CHARS
TYPBAD:	ILDB	C,T1			; READ A CHAR
	JUMPLE	C,TYPBOD		; FINISHED
	MOVEI	C,40(C)			; GET IT ASCII
	OUTCHR	C			; TYPE IT
	JRST	TYPBAD			; LIST OF BAD ONES

TYPBOD:	OUTSTR	[ASCIZ\
\]					; FINISH
	POPJ	P,			; DONE
	SUBTTL	SUBROUTINES	YUGESS

;YUGESS - ROUTINE TO ACCEPT THE USER'S RIGHT OR WRONG ANSWER.
;	    IF RIGHT -- SKIP RETURN & ADD TO GOOD CHARS, ETC.
;	    IF WRONG -- NON-SKIP RETURN & ADD TO ERRORS.
;

YUGESS:	SKPINL				;LET'S NOT HAVE ANY ^O
	  JFCL				; CONFUSE THE POOR KID!
	OUTSTR	[ASCIZ\Guess: \]	; LET HIM KNOW YOU'RE WAITING

GESSCH:	CALL	INCHAR			; READ A CHARACTER
	CAIL	C,"A"			;  MAKE SURE IT'S ALPHABETIC
	CAILE	C,"Z"			;  A-Z.
	  JRST	BADCHR			; NO---COMPLAIN!
	MOVEI	C,-20(C)		; MAKE SIXBIT FROM ASCII

ALPHCH:	CALL	GUESSD			; SEE IF I ALREADY GUESSED THIS
	  JRST	GESADY			; YES--SO TRY ANOTHER ONE
	MOVE	T1,[POINT 6,WORD]	; POINTER TO THE WORD.
	MOVE	T2,[POINT 6,RIGHT]	; POINTER TO CORRECT GUESSES
GESIT:	ILDB	T3,T1			; GET A CHARACTER..
	IBP	T2			; POINT AT NEXT CHARACTER
	CAMN	C,T3			; IS IT A MATCH?
	  DPB	C,T2			;  YES--SO PUT IT IN THERE.
	SUBTTL	INITIALIZATION CODE -- THIS WILL EVENTUALLY RESET .JBSA

HANGER:
	END	HANGER
   