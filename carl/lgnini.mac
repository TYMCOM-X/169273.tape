	TITLE	LOGIN - Initialization for LOGIN
	SUBTTL	LOGIN - Initialization for LOGIN

	SEARCH	UUOSYM,MACTEN,SCNMAC,LGNUNV

	LGNDCL

;START HERE

LOGIN::	TDZA	17,17		;CLEAR AC17 ON A NORMAL START
	MOVEI	17,1		;SETUP A 1 ON A CCL START
	SETZB	0,ZZBEG		;PREPARE TO CLEAR CORE
	MOVEI	16,1		;CLEAR THE AC'S
	BLT	16,16		; ..
	MOVE	T1,[ZZBEG,,ZZBEG+1] ;CLEAR OUT THE LOWSEG
	BLT	T1,ZZMAX-1	; ..
	MOVEM	17,SAOFST	;SAVE STARTING OFFSET
	MOVE	T1,[4,,RICC##]	;LOAD UP JOBINT BLOCK
	MOVEM	T1,INTBLK	; ..
	MOVX	T1,ER.ICC	;ASK FOR CONTROL-C
	MOVEM	T1,INTBLK+1	; INTERCEPT
	MOVEI	T1,INTBLK	;CONTROL-C INTERCEPT BLOCK
	MOVEM	T1,.JBINT##	;STORE FOR THE MONITOR
	RESET

	MOVE	P,PDPLST	;Set up the stack
	PJOB	T1,		;Get our job number
	MOVEM	T1,THSJOB	;Store for later
	PUSHJ	P,ISBATC	;Is this a Batch job ?
	SKIPA			;No
	TLO	F,FL.BAT!L.SOPR	;This guy is a real son of a batch
	MOVX	T1,.IOASC	;Assume a Batch job
	TLNN	F,FL.BAT	;Is it ?
	PUSHJ	P,TTYINI##	;INITIAL TTY
	PUSHJ	P,TTYNEC##	;SET TTY NO ECHO
	INIT	UFD,.IODMP	;GET A DISK IN DUMP MODE
	SIXBIT	/SYS/		;LOOKUP ACCOUNTING FILES IN SYS
	XWD	0,0		;NO HEADERS
	  LOGOUT		;THIS IS ALSO UNFIXABLE
	SETOM	RECFLG		;SET FLAG FOR RECOMP MESSAGE
	MOVNI	T3,1		;GET THE TTY CHARACTERISTICS
	GETLCH	T3
	CAMN	T3,[-1]		;IF A NOOP, ASSUME HDX
	MOVX	T3,GL.HDP
	MOVEM	T3,TTBITS
	MOVEM	T3,CNTLJT
	TXNN	T3,GL.ITY!GL.HDP!GL.LCP!GL.PTM	;SKIP IF HALF DUPLEX
	TLO	F,FL.FDX	;SET FULL DUPLEX FLAG
	MOVX	T1,.TOFSP	;Full SCNSER PTY function
	SETO	T2,		;My UDX
	MOVE	T3,[2,,T1]	;Point to args
	TRMOP.	T3,		;Read it
	 MOVEI	T3,0		;Cant
	CAIE	T3,0		;Is it set?
	 TLO	F,FL.FDX	;Yes--Set full duplex for full SCNSER PTYs
	MOVX	T1,%CNSTS	;MAGIC CODES FOR LOC STATES IN SYS
	GETTAB	T1,		;GET TABLE ENTRY
	  SETZ	T1,		;ZERO IF ERROR RETURN
	MOVEM	T1,STATES	;SAVE FOR LATER
	MOVX	T1,%CNST2	;[374] GET SECOND STATES WORD
	GETTAB	T1,		;[374] ..
	  SETZ	T1,		;[374] ..
	MOVEM	T1,STATS2	;[374] SAVE FOR A RAINY DAY
	MOVX	T1,%CNOPR	;GET OPR DEVICE NAME
	GETTAB	T1,		; FROM MONITOR
	  MOVSI T1,(SIXBIT /CTY/)	;ASSUME CTY IF IT WONT TELL US
	MOVEM	T1,SAVOPR	;STORE FOR LATER USE
	GETLIN	T1,		;GET MY TTY NAME
	MOVEM	T1,MYTTY	;SAVE IT
	TXNN	T3,GL.CTY	;IS MY TTY THE CTY?
	CAMN	T1,SAVOPR	;OR THE OPR?
	TLO	F,L.OPR		;YES. REMEMBER THAT IN FLAG AC
	MOVE	T3,THSJOB	;[351] GET OUR JOB NUMBER
	TRMNO.	T3,		;[345] GET TTY UDX
	  SETZ	T3,		;[345] SO WHAT?
	MOVEM	T3,TTYUDX	;[345] SALT AWAY FOR TRMOP. STUFF
	JUMPE	T3,NT2741	;[546] NOT A 2741
	MOVE	T1,[2,,T2]	;[546] ARG POINTER FOR TRMOP.
	MOVX	T2,.TO2741	;[546] READ THE 2741 BIT
	TRMOP.	T1,		;[546] ..
	  SETZ	T1,		;[546] ASSUME NOT 2741
	SKIPE	T1		;[546] IS THIS A 2741?
	SETOM	FL2741		;[546] YES - SET THE FLAG
NT2741:	SETOM	OOBEG		;SET SCAN SWITCH BLOCK TO -1
	MOVE	T1,[XWD OOBEG,OOBEG+1]
	BLT	T1,OOMAX-1
	MOVEI	T1,^D5		;LET THE GUY TRY 5 TIMES
	MOVEM	T1,LOGTRY
	MOVX	T1,%LDFFA	;PPN THAT HAS THE PRIVS
	GETTAB	T1,		;GET IT FROM MONITOR
	  MOVE	T1,[1,,2]	;DEFAULT
	MOVEM	T1,ALPPPN	;SAVE IT FOR LATER
	MOVX	T1,%LDMFD	;GET THE MFD PPN
	GETTAB	T1,		; FROM THE MONITOR
	  MOVE	T1,[1,,1]	;DEFAULT
	MOVEM	T1,MFDPPN	;STORE FOR LATER USE
	MOVSI	T1,(SIXBIT .SYS.)
	DEVPPN	T1,		;GET PPN FOR SYS
	  MOVE	T1,[1,,4]	;[361] DEFAULT
	MOVEM	T1,SYSPPN
	MOVX	T1,%LDUFP	;[557] GET STANDARD UFD PROTECTION
	GETTAB	T1,		;[557] ..
	  MOVSI	T1,775000	;[557] EH?
	ROT	T1,^D9		;[557] RIGHT-JUSTIFY IT
	MOVEM	T1,UFDPRT
	DATE	T1,
	MOVEM	T1,TDATE	;[557] SAVE FOR LATER

	PJRST	LGNSCN##	;GO OFF TO COMMAND SCANNER

	END	LOGIN
