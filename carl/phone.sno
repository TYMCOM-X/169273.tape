	UPPER	=	'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	LOWER	=	'abcdefghijklmnopqrstuvwxyz'
	NULL	=
	CRLF	=	ASCII(15) ASCII(12)

	LINE.NUMBER	=	0
	LAST.LINE	=	55
	PAGE.NUMBER	=	0
	CHEADS		=	0

	DEFINE('OPT(PAT)')
	DEFINE('CENTER(LINE,LENGTH)')
	DEFINE('TEST.BREAK(LINE)')
	DEFINE('FOOTER()')
	DEFINE('HEADER()')
	DEFINE('CHEADER(ARG)')
	DEFINE('CFOOTER()')
	DEFINE('GENERATE(LINE)')
+							:(START)
OPT	OPT	=	PAT ! NULL			:(RETURN)
*
*
CENTER	LENGTH = IDENT(LENGTH) 60
	CENTER = LPAD(LINE,((LENGTH / 2) + (SIZE(LINE) / 2)))
+							:(RETURN)
*
*
*
TEST.BREAK	LINE	LEN(1) . FIRST
	LAST	=	BREAKER
*	TTY = "FIRST:" FIRST " - LAST:" LAST
	BREAKER	=	FIRST
	DIFFER(LAST,FIRST)				:S(RETURN)F(FRETURN)
*
*
FOOTER	LINE.NUMBER	=	0
	PAGE.NUMBER	=	PAGE.NUMBER + 1
	FOOTER	=	ASCII(14)			:(RETURN)
*
*
HEADER	HEADER	=	CRLF CRLF
+			CENTER('Personal Telephone Directory')
+			CRLF CRLF CENTER("Category: " SORT.FIELD)
+			CRLF CRLF
	JUST.HEAD	=	1
	LINE.NUMBER	=	LINE.NUMBER + 6		:(RETURN)
*
*
CFOOTER	CFOOTER	=					:(RETURN)
*
*
CHEADER	BREAKER	 ANY(UPPER LOWER)			:S(OK.HEAD)
	LE(LINE.NUMBER + 2 + 1,LAST.LINE)		:S(DX.HEAD)
	REPORT	=	DUPL(CRLF,LAST.LINE - LINE.NUMBER)
	REPORT	=	FOOTER()	HEADER()
DX.HEAD	CHEADER	=	CRLF CRLF
	LINE.NUMBER	=	LINE.NUMBER + 2		:(CH.RETURN)
OK.HEAD	LE(LINE.NUMBER + 4 + 1,LAST.LINE)		:S(DO.HEAD)
	REPORT	=	DUPL(CRLF,LAST.LINE - LINE.NUMBER)
	REPORT	=	FOOTER()	HEADER()
DO.HEAD	CHEADER	=	CRLF CENTER(BREAKER ARG) CRLF CRLF	
	LINE.NUMBER = LINE.NUMBER + 4
CH.RETURN	JUST.HEAD	=	0		:(RETURN)
*
*
GENERATE
	BREAKER = DIFFER(SORT.FIELD,OLD.SORT.FIELD)	:S(XX.FOOT)
	LE(LINE.NUMBER,LAST.LINE)			:S(NO.FOOT)
XX.FOOT	REPORT	=	FOOTER()
NO.FOOT	IDENT(LINE.NUMBER,0)				:F(NO.HEAD)
	REPORT	=	HEADER()
NO.HEAD	TEST.BREAK(LINE)				:F(NO.BREAK)
	IDENT(CHEADS,0)					:S(NO.CFOOT)
	REPORT	=	CFOOTER()
NO.CFOOT	REPORT	=	CHEADER()
NO.BREAK	REPORT	=	IDENT(JUST.HEAD,1) CHEADER(' (cont.)')
	REPORT	=	LINE
	LINE.NUMBER	=	LINE.NUMBER + 1		:(RETURN)
*
*
START	TTY = '[PHNEPF	Enter phone filename]'
	FILESPEC = TTY
	FILE(FILESPEC)					:F(TRYDFT)
	INPUT(.DSKIN,FILESPEC)				:S(OKFILE)
TRYDFT	FILE("PHONE.NUM")				:F(START)
	INPUT(.DSKIN,"PHONE.NUM")			:F(START)
OKFILE	OUTPUT(.REPORT,"PHONE.LST","T")
	ENTRIES = 0
	LIST = TABLE()
LOOP	ELEMENT = DSKIN					:F(NOW.SORT)
	ENTRIES = ENTRIES + 1
	ELEMENT	(BREAK('@') '@' BREAK('@')) . PERSON
+		'@' BREAK('@') . PHONE			:F(PHNBDE)
	PERSON	'@' = ' @'
	LIST<PERSON> = 	PHONE				:(LOOP)

NOW.SORT	PHONE.LIST = SORT(LIST)
	PROTOTYPE(PHONE.LIST) BREAK(',') . SIZE
	COUNT	= 1
PRINT	OLD.SORT.FIELD = SORT.FIELD
	PHONE.LIST<COUNT,1> BREAK('-') . SORT.FIELD '-' BREAK('@') . NAME
PRINTX	NAME ';' = ' '					:S(PRINTX)
	GENERATE(RPAD(TRIM(NAME),40,'.') '  ' PHONE.LIST<COUNT,2> CRLF)
LIST.LOOP	COUNT = LT(COUNT,SIZE) COUNT + 1	:S(PRINT)F(DONE)
PHNBDE	TTY = '%PHNBDE Bad data for Entry: ' ENTRIES	:(LOOP)
DONE	TTY = '[PHNNOE Number of entries: ' ENTRIES ' ]'
END
