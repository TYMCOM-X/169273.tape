	TITLE	LGNIO - Input output module for LOGIN

	SEARCH	UUOSYM,MACTEN,SCNMAC,LGNUNV
	LGNDCL

; Initialize terminal
TTYINI::PUSH	P,T1			;SAVE T1
	SETZM	TTYBUF##		;CLEAR FIRST WORD OF BUFFER
	MOVE	T1,[TTYBUF##,,TTYBUF##+1] ;SET UP BLT
	BLT	T1,TTYBUF##+TTYBSZ-1	;CLEAR OUR BUFFER
	MOVE	T1,[POINT 7,TTYBUF##]	;SET UP
	MOVEM	T1,TTYPTR##		; BYTE POINTER
	MOVEI	T1,<TTYBSZ*5>-1		;SET UP
	MOVEM	T1,TTYCNT##		; BYTE COUNT
	POP	P,T1			;RESTORE T1
	POPJ	P,			;AND RETURN


; Output entire buffer
TTYOUT::TLNE	F,FL.DET		;DETACHED?
	SETZM	TTYBUF			;YES--NEVER DO OUTPUT
	SKIPE	TTYBUF##		;ANYTHING IN BUFFER?
	OUTSTR	TTYBUF			;YES--DUMP IT
	PJRST	TTYINI			;RESET POINTER AND COUNT


; Output a character
TTYTYO::SOSGE	TTYCNT			;COUNT CHARACTERS
	PUSHJ	P,TTYOUT		;BUFFER FULL--OUTPUT
	IDPB	CH,TTYPTR		;STORE CHARACTER
	CAIN	CH,.CHCRT		;CARRAGE RETURN?
	SETZM	HPOS			;YES--BACK TO START OF LINE
	CAIL	CH," "			;CONTROL CHARACTER?
	AOS	HPOS			;NO--UPDATE POSITION
	CAIN	CH,.CHLFD		;LINE FEED?
	PJRST	TTYOUT			;YES--FORCE BUFFER OUT
	POPJ	P,			;RETURN


; Output a prompt and force output
TTYPMT::PUSH	P,T1			;SAVE PROMPT TEXT
	PUSHJ	P,NEWLIN		;TYPE A CRLF IF NEEDED
	POP	P,T1			;RESTORE T1
	PUSHJ	P,.TSTRG##		;TYPE PROMPT STRING
	PJRST	TTYOUT			;FORCE OUTPUT


; Set terminal echo status
TTYECH::SKIPA	T1,[TXZ	T1,GL.NEC]	;ECHO
TTYNEC::MOVE	T1,[TXO	T1,GL.NEC]	;NO ECHO
	PUSH	P,T1			;SAVE INSTRUCTION
	MOVNI	T1,1			;-1 FOR OUR LINE
	GETLCH	T1			;GET LINE CHARACTERISTICS
	XCT	(P)			;SET OR CLEAR ECHO
	SETLCH	T1			;SET LINE CHARACTERISTICS
	POP	P,(P)			;TRIM STACK
	POPJ	P,			;RETURN
;SUBROUTINE TO INPUT CHARS TO 1ST NON-SIXBIT CHAR - SAVE 2 WORDS OF CHARS
;VALUES	T1,T2=SIXBIT CHARS
;	CH=TERMINATING CHAR
;	FL.BRK BIT IN LH F=1 IF TERMINATING CHAR IS A BREAK CHAR

GET2WD:	PUSHJ	P,.SAVE1##	;SAVE P1
	SETZB	T1,T2
	MOVE	P1,[POINT 6,T1]
GETNL:	PUSHJ	P,.TICHE##	;GET A CHAR. WITH THE RIGHT
				; AMOUNT OF CONVERSION.
	JUMPLE	C,.POPJ##	;[456] JUMP IF END OF LINE
	CAIL	C,"A"+40	;CONVER LOWER CASE
	CAILE	C,"Z"+40	; TO UPPER CASE
	SKIPA
	SUBI	C,40	
	MOVEI	CH,-40(C)	;CONVERT TO SIXBIT
	CAME	P1,[600,,T2]	;POINTER EXPIRED
	IDPB	CH,P1		;NO--STASH AWAY
	JRST	GETNL		;GET THE NEXT CHAR
;SUBROUTINE TO DETERMINE IF JOB NUMBER IN T1 IS A BATCH JOB
;ARGS	T1=JOB NUMBER
;RETURN	.POPJ IF JOB IS NOT BATCH
;	.POPJ1 IF JOB IS BATCH

ISBATC:	HRLZ	T4,T1
	HRRI	T4,.GTLIM	;[602] GETTAB INDEX FOR JBTLIM
	GETTAB	T4,		;[602] GET BITS
	  SETZ	T4,
	TXNN	T4,JB.LBT	;[602] BATCH JOB?
	  POPJ	P,		;[602] NO
	JRST	.POPJ1##	;[602] YES - SKIP RETURN

;[333] SUBROUTINE TO DETERMINE WHETHER THIS JOB IS A SUBJOB OF OPSER.
;[333] RETURN	POPJ IF NOT
;[333]		POPJ1 IF IT IS

ISOPSR:	SETO	T1,		;[333,340] THIS JOB
	CTLJOB	T1,		;[333] WHO CONTROLS ME?
	  POPJ	P,		;[333] I DUNNO
	MOVE	T2,T1		;[404] REMEMBER CNTRL JOB NO.
	TRMNO.	T1,		;[333] WHERE'S HIS TTY AT?
	  POPJ	P,		;[333,340] DUNNO
	GETLCH	T1		;[333,341] WHAT KIND OF TERMINAL
	TXNE	T1,GL.ITY	;[333] PTY?
	POPJ	P,		;[333,340] YES--PROBABLY BATCON
	HRLZ	T1,T2		;[337,340,404] GET JOB NO.
	HRRI	T1,.GTPRG	;[333] SET UP FOR GETTAB
	GETTAB	T1,		;[333] GET PROGRAM NAME
	  POPJ	P,		;[333] A REAL LOSER
	CAME	T1,[SIXBIT /OPSER/]
	POPJ	P,
	JRST	.POPJ1##
STYO:	MOVE	CH,T1		;CHARACTER OUTPUT FOR SCAN & WILD
TYO:	JRST	TTYTYO

TYI:	PUSHJ	P,.TIAUC##	;GET CHAR BY SCAN'S SUBROUTINE
	MOVE	CH,C		;[331] COPY CHAR
	POPJ	P,
;SUBROUTINE TO GET ONE CHAR AND BOMB USER FOR BEING
; SLOW.
;
;CALLED ONLY FROM SCAN -- MAY CHANGE NO AC'S

TTYGET:	PUSHJ	P,.SAVE2##	;SAVE P1 AND P2
	SKIPN	FL2741		;IS THIS A 2741?
	TRNE	F,R.PTYJ	;PTY JOB?
	JRST	TTGET2		;YES -- GO INTO TI WAIT
	MOVEI	P2,TTYSLP	;NUMBER OF MINS. TO SLEEP
TTGET1:	SKPINL			;ANYTHING THERE??
	  SKIPA			;NO--LOOP
	JRST	TTGET2		;YES--GO GET IT
	MOVE	P1,[HB.RTL!HB.RWJ!^D60000]
	HIBER	P1,		;HIBER FOR 1 MIN.
	  JRST	TTGET2		;TINY MONITOR
	SKPINL
	  SKIPA
	JRST	TTGET2
	SOJG	P2,TTGET1	;LOOP FOR A WHILE
	FATAL	(WFS,<Waiting for >,EF.SIL,HELPR)

TTGET2:	INCHWL	C		;GET A CHAR. INTO C
	CAIE	C,.CHCNC	;CONTROL-C?
	CAIN	C,.CHCNZ	;[343] OR CONTROL-Z?
	PJRST	RICC##		;YES--GO EXIT
	CAIN	C,.CHCRT	;IS IT A CARRAGE RETURN?
	SETZM	HPOS		;YES--UPDATE HORIZ. POSITION
	CAIL	C," "		;PRINTING CHARACTER?
	AOS	HPOS		;YES--UPDATE HORIZ POSITION
	POPJ	P,		;RETURN


;HERE IF USER DOES NOT TYPE ANYTHING WITHIN 2 MIN.
HELPR:	MOVE	T1,HELP		;POINT TO HELP TEXT
	PUSHJ	P,.TSTRG##	;TYPE IT
	MOVEI	T1,[ASCIZ /; please start over/]
	PJRST	.TSTRG##	;TYPE END OF ERROR AND RETURN


; SUBROUTINE TO PROMPT FOR A PPN
GIVNBR::PUSHJ	P,NEWLIN	;GIVE A CRLF IF NEEDED
	MOVEI	CH,[ASCIZ /project-programmer number/]
	MOVEM	CH,HELP		;STORE REASON FOR WAITING
				;FALL INTO SCNPMT


; SUBROUTINE TO PROMPT.  CALLED FROM SCAN
SCNPMT::PUSHJ	P,TTYECH	;SET TTY ECHO
	TLNE	F,FL.BAT	;ARE WE A BATCH JOB?
	FATAL	(EIB,<Error in BATCON - LOGIN aborted>)
	MOVEI	T1,"#"		;ALWAYS PROMPT WITH "#" WHETHER
	PUSHJ	P,.TCHAR##	;FIRST OR CONTINUATION LINE
	PJRST	TTYOUT		;FORCE OUTPUT AND RETURN


;SUBROUTINE TO TYPE A CRLF IF ONE IS NEEDED
NEWLIN:	SKIPN	HPOS		;IS A CRLF NEEDED?
	POPJ	P,0		;NO--JUST RETURN
	PJRST	.TCRLF##	;YES--GO TYPE ONE
	SUBTTL	LOGIN messages

	MSGOK==1		;FLAG TO TYPE MEESSAGE EVEN IF USER
				; HAS SEEN IT.
	FNAME==2		;FLAG TO REQUEST FILE NAME PRINTING
	STONLY==4		;[357] FLAG TO PRINT STR NAME ONLY

;HERE TO PRINT RANDOM FILES USER GETS ON LOGIN

DAYMES:	PUSHJ	P,.SAVE1##	;SAVE P1
	MOVE	T1,PPN		;GET PPN
	TLNE	F,FL.WLD	;WILD PPN?
	 INFO	LIA,<You are logged in as >,,.TXWDW##
	MSTIME	T1,		;GET TIME OF DAY
	IDIVI	T1,^D60000	;TO MINUTES
	IDIVI	T1,^D60		;NOW BREAK IN MINS AND HRS
	PUSH	P,T2		;SAVE MINUTES
	MOVEI	T2,"0"		;[754] ZERO FILL
	PUSHJ	P,.TDEC2##	;[754]TYPE HOURS
	PUSHJ	P,.TCOLN##	;TYPE A COLON
	POP	P,T1		;[754]MINUTES
	MOVEI	T2,"0"		;[754] ZERO FILL
	PUSHJ	P,.TDEC2##	;[754]TYPE
	PUSHJ	P,.TTABC##	;TAB
	PUSHJ	P,.TDATN##	;[754] And current date
	PUSHJ	P,.TTABC##
	MOVE	T1,SAVEDA	;DAY OF WEEK
	MOVEI	T1,WEEKDA(T1)
	PUSHJ	P,.TSTRG##	;TYPE DAY OF WEEK
	PUSHJ	P,.TCRLF##

	MOVEI	T1,2		;[454] GET DEFAULT FOR /NOTICE
	MOVE	P1,U.NOTC	;[453] GET SWITCH TYPED
	AOSN	P1		;[453] WAS A SWITCH TYPED?
	  MOVEM	T1,U.NOTC	;[453] NO - USE DEFAULT
	MOVEI	T1,NOTSPC	;[362] /NOTE FILESPEC
	MOVEI	P1,FNAME	;[362] PRINT FILE NAME
	PUSHJ	P,TYPE		;[362] TYPE IT
	PUSHJ	P,ISOPSR	;[333] IS THIS AN OPSER SUBJOB?

DIEMSG:	SKIPA	T1,[DAYSPC]	;[333,337] NO--PRINT NOTICE.TXT
	POPJ	P,0		;[662] [333] YES--DON'T PRINT IT
	TRNE	F,R.SESSION	;[754] SESSION Command?
	 POPJ	P,		;[754] Yes--No message for him either
	MOVEI	P1,0		;FLAGS
	PJRST	TYPE		;GO PRINT IT AND RETURN

;[662]	CREATE SEPERATE /STR PRINTER
STRMES::SKIPG	U.STR		;/STR GIVEN
	POPJ	P,0		;NO--RETURN
	MOVEI	T1,STRSPC	;STR.TXT SPEC
	MOVEI	P1,MSGOK!FNAME!STONLY	;[357] OK TO TYPE MESSAGE
	PJRST	TYPE		;PRINT IT

;FILE SPEC FOR SYS:NOTICE.TXT/PHYSICAL/NOSTR
DAYSPC:	SIXBIT	/SYS/
	SIXBIT	/NOTICE/
	EXP	-1
	XWD	'TXT',-1
	EXP	FX.PHY!FX.NOM
	EXP	FX.STR!FX.PHY!FX.NOM
	BLOCK	.FXLEN-<.-DAYSPC>

;FILE SPEC FOR SYS:STR.TXT/PHYSICAL/STRS
STRSPC:	SIXBIT	/ALL/
	SIXBIT	/STR/
	EXP	-1
	XWD	'TXT',-1
	EXP	FX.PHY!FX.STR
	EXP	FX.PHY!FX.STR
	XWD	1,4
	XWD	-1,-1
	BLOCK	.FXLEN-<.-STRSPC>
;SUBROUTINE TO TYPE A FILE
;CALL WITH:
;	MOVEI	T1,ADDRESS-OF-SCAN-STYLE-FILESPEC
;	MOVEI	P1,FLAGS
;	PUSHJ	P,TYPE
;	RETURN HERE
TYPE:	SKIPN	.FXNAM(T1)	;[512] ANY NAME OR MASK SPECIFIED?
	  POPJ	P,		;[512] NO - SKIP IT
	HRLZ	T1,T1		;FLIP AROUND
	HRRI	T1,TYPSPC	;ADDRESS OF OUT SPEC
	BLT	T1,TYPSPC+.FXLEN-1 ;[436] COPY THE FILESPEC
	SETZM	WLDPNT		;CLEAR MEMORY FOR FIRST CALL
	MOVX	T1,FX.NOM!FX.PRT ;/OKNONE AND /OKPROT
	IORM	T1,TYPSPC+.FXMOD;FORCE SWITCH TO BE
	IORM	T1,TYPSPC+.FXMOM; SET ON ALL CALLS TO WILD
TYPE1:	MOVE	T1,[4,,[[TYPSPC],,0
			UFDBUF,,SECBUF
			.FXLEN,,.RBPRV+1
			UFD,,WLDPNT]]
	MOVX	T2,.IOASC	;SET UP ASCII MODE
	MOVEM	T2,UFDBUF	; ..
	PUSHJ	P,.LKWLD##	;LOOK FOR FILE
	  POPJ	P,0		;ALL DONE
	MOVEI	T1,B.DC##	;SET UP ADDRESS OF BUFFER HEADER
	MOVEM	T1,UFDBUF+2	; ..
	OPEN	UFD,UFDBUF	;OPEN THE FILE
	  PJRST	E.DFO##		;CAN'T
	LOOKUP	UFD,SECBUF	;LOOKUP FILE
	JRST	[PUSHJ P,E.DFL## ;REPORT ERROR
		 JRST  TYPE1]	;LOOK FOR NEXT FILE
IFN SUPNOT,<
	TRNE	P1,MSGOK	;WANT MESSAGE ALWAYS?
	JRST	TYPE3		;YES--GO TYPE IT
	SKIPE	UFDNDL		;SKIP IF UFD CAN BE DELETED
	JRST	TYPE2		; SAW THE MESSAGE
	LDB	T1,[POINTR(SECBUF+.RBPRV,RB.CRD)]
	LDB	T2,[POINTR(SECBUF+.RBEXT,RB.CRX)] ;GET TOP 3 BITS OF DATE
	LSH	T2,^D12		;MOVE OVER TO CORRECT PLACE
	IORI	T1,(T2)		;COMBINE
	CAMLE	T1,UFDDAT	;IS FILE NEWER THAN UFD?
	JRST	TYPE3		;YES--PRINT ANYWAY
	CAME	T1,UFDDAT	;SAME DAY?
	JRST	TYPE2		;NO--DISTINCTLY OLDER
	LDB	T2,[POINTR(SECBUF+.RBPRV,RB.CRT)]
	CAML	T2,UFDTIM	;IS MESSAGE NEWER THAN UFD
	JRST	TYPE3		;YES--GO PRINT
TYPE2:	MOVE	T1,U.NOTC	;[450] GET /NOTICE VALUE
	CAIL	T1,2		;[450,453] SOMETIMES OR NEVER?
	JRST	TYPE1		;[453] YES -LOOK FOR NEXT FILE
>
TYPE3:	MOVE	T1,U.NOTC	;[450] GET /NOTICE VALUE
	CAIN	T1,3		;[450] NEVER PRINT?
	  JRST	TYPE1		;[450] YES - SKIP IT
	TXNE	P1,FNAME	;WANT TO SEE FILE NAME?
	PUSHJ	P,TYPFIL	;TYPE THE FILE SPEC
TYPE4:	PUSHJ	P,.NXDTW##	;GET A BYTE
	  JRST	TYPE1		;END OF FILE
	PUSHJ	P,.TCHAR##	;TYPE IT
	JRST	TYPE4
;SUBROUTINE TO TYPE A FILESPEC
;CALL WITH:
;	UFDBUF = OPEN BLOCK
;	SECBUF = LOOKUP BLOCK
;	PUSHJ	P,TYPFIL
;	RETURN HERE
TYPFIL:	PUSHJ	P,NEWLIN	;[377] DON'T DOUBLE-SPACE
	MOVE	T1,UFDBUF+1	;GET DEVICE NAME
	PUSHJ	P,.TSIXN##	;TYPE IT
	PUSHJ	P,.TCOLN##	;TYPE IT
	TXNE	P1,STONLY	;[357] ONLY WANT STR NAME?
	PJRST	.TSPAC##	;[357] YES -- FINISH UP
	MOVE	T1,SECBUF+.RBNAM ;GET FILE NAME
	PUSHJ	P,.TSIXN##	;TYPE IT
	MOVEI	T1,"."		;TYPE A DOT
	PUSHJ	P,.TCHAR##	;TYPE THE DOT
	HLLZ	T1,SECBUF+.RBEXT;TYPE THE EXTENSION
	PUSHJ	P,.TSIXN##	;TYPE EXTENSION
	MOVEI	T1,SECBUF+.RBPPN;POINT TO PPN
	PUSHJ	P,.TDIRB##	;PRINT IT
	PJRST	NEWLIN		;START A NEW LINE AND RETURN


WEEKDA:	ASCIZ	.Wed.
	ASCIZ	.Thur.
	ASCIZ	.Fri.
	ASCIZ	.Sat.
	ASCIZ	.Sun.
	ASCIZ	.Mon.
	ASCIZ	.Tue.

	END
 