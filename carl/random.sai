
entry

	RAN,	Seed,	SetRan

;
begin "Random"

require "(SAILIB)SAIL.DEF"   source!file;
require "(SAILIB)UUOSYM.DEF" source!file;


internal boolean SetRan;
internal integer array Seed[0:1];

preset!with 1048576;
own integer array Funny[0:0];


! RANI - INITIALIZE SEED FOR RAN
;

simple procedure RANI;
start!code
    define T1='1, P='17;		! REGISTERS USED ;

	PUSH	P,T1;			! SAVE REGISTER ;
	uuo!MSTIME T1,;			! GET THE TIME SINCE MIDNIGHT ;
	IMULI	T1,18;			! MULTIPLY ;
	ADDI	T1,5;			! ADD ;
	MOVEM	T1,SEED[0];		! THEN SAVE IT. ;

	uuo!DATE T1,;			! GET THE DATE ;
	IDIVI	T1,1000;		!  .MOD. 1000 ;
	ADDI	T1,1000;		! THEN ADD 1000 FOR LOOP ;
	MOVEM	T1,SEED[1];		! THEN GO! ;
	SETOM	SETRAN;			! KILROY WAS HERE ;
	POP	P,T1;			! RESTORE TEMP AC. ;

end "RANI";



! RAN - RANDOM NUMBER ROUTINE, GRATIS GOES TO CROWTHIER & WOODS OF
!	ADVENTURE FAME AL LA KNUTH VOL.2
!
!	USES RANGE FROM 0 TO TOP-1
!	RETURNS RESULT IN T1.
!
!	AC'S USED:	T1-T4 (T2-T4 preserved)
;

internal simple integer procedure RAN( integer Top );
start!code
    define T1='1, T2='2, T3='3, T4='4, P='17;
    label RANLOP;

	SKIPN	SETRAN;			! SEE IF WE'VE BEEN HERE BEFORE? ;
	  PUSHJ	P,RANI;			! NO! ;
	MOVE	T1,Top;			! GET MAXIMUM VALUE ;
	PUSH	P,T2;			! SAVE AC'S T2 THROUGH T4 ;
	PUSH	P,T3;
	PUSH	P,T4;
	MOVE	T2,SEED[0];		! GET THE PLANT. ;
	MOVEI	T4,1;			! GET ONE TO START ;
	SKIPN	T2;			! SKIP IF SEED NON-ZERO ;
	MOVE	T4,SEED[1];		! GET NUMBER ;
RANLOP:	IMULI	T2,1021;		! MULT BY LITTLE LESS THAN 1K ;
	IDIV	T2,Funny[0];		! GET A FUNNY NUMBER ;
	SOJG	T4,RANLOP;		! LOOP IF YOU THINK NECESSARY ;
	MOVEM	T3,SEED[0];		! NEW SEED ;
	IMUL	T1,T3;			! MULT SEED BY RANGE ;
	IDIV	T1,Funny[0];		! AND DIVIDE AGAIN ;
	POP	P,T4;			! RESTORE AC'S T4 THROUGH T2 ;
	POP	P,T3;
	POP	P,T2;

end;


end "Random";
