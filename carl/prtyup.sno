*	Program to pretty up MACRO output from FASBOL
*	Edward C. Mulrean	Version 1(1)	28-May-76
*
*-CROSREF
*	DECLARE('SNOBOL.MAIN','PRTYUP')
	&ANCHOR = 1
	COMMENT = 0
	OCTAL = '01234567'
	DECIMAL = '0123456789'
	ALPHA = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	TAB = SUBSTR(&ALPHABET,1,9)
	VERSION = '1(1)'
*
	DEFINE('OPT(PATTERN)')				:(PATTERNS)
OPT	OPT = PATTERN ! NULL				:(RETURN)
*
PATTERNS LABEL.PAT = OPT(ANY(ALPHA) OPT(SPAN(ALPHA DECIMAL)) ':')
+		. LABEL OPT(' ') REM . STMT
	D.PAT = BREAK('^') . STMT.1 '^D' ANY(OCTAL) . D.EXP
+		OPT(NOTANY(DECIMAL) REM) . STMT.2 RPOS(0)
	OP.PAT = SPAN(ALPHA DECIMAL) . STMT.1 ' ' REM . STMT.2
*
*	FREEZE()
	EXIT(-1)
*	OUTPUTC = 'FASBOL file: '
	TTY = 'FASBOL file: '
*	FASBOL.FILE = INPUT				:F(END)
	FASBOL.FILE = TTY				:F(END)
*	OPEN('DSK(0,2)',1)
*	LOOKUP(FASBOL.FILE,1)
*	INPUT('IN',1,132)
	INPUT('IN',FASBOL.FILE)
*	OPEN('DSK(2)',2)
*	ENTER(FASBOL.FILE,2)
*	OUTPUT('OUT',2,132)
	OUTPUT('OUT','XXXX.MAC')
LOOP	LINE = IN					:F(DONE)
	NE(COMMENT)					:S(DOING.COM)
	LINE ';'					:F(NO.COMMENT)
	OUT = ';'
	COMMENT = -1
DUMP.LINE OUT = LINE					:(LOOP)
DOING.COM LINE ';'					:S(DUMP.LINE)
	OUT = ';'
	COMMENT = 0
NO.COMMENT LINE LABEL.PAT				:F(DUMP.LINE)
DROP.D	STMT D.PAT = STMT.1 D.EXP STMT.2		:S(DROP.D)
	STMT OP.PAT = STMT.1 TAB STMT.2
	OUT = LABEL TAB STMT				:(LOOP)
*DONE	RELEASE(1) RELEASE(2)
DONE	ENDFILE(FASBOL.FILE) ENDFILE('XXXX.MAC')
*	OUTPUT = 'PRTYUP %' VERSION ' task completed.'
	TTY = 'PRTYUP %' VERSION ' task completed.'
END
