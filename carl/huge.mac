Title	Huge	Program to create HUGE empty files (with holes)
Subttl	By Carl A Baltrunas - Tymshare, Inc.  1982

	Search Carl,Macten,Jobdat,Uuosym

	.Request Tymcab		; Library

	External Swdec

Huge:	Reset
	Move	P,[Iowd 20,PDL]	; Setup PDL
	Outstr	[Asciz\
HUGE file size: \]
	Pushj	P,Swdec		; Get the size
	  Jumpe	N,none		; No size specified
	Open	1,[  17
		sixbit /dsk/
		0]
	  Jrst	E$$DNA		; Disk not available
	Lookup	1,lkpblk	; Look for file
	  Hllzs	lkpblk+.rbext	; Clear error code or data
	Enter	1,lkpblk	; Make file
	  Jrst	E$$FEE		; File enter error
	Move	T1,[.Chcfp,,1]	; Prepare: Create file page
	Move	T2,[.Chdfp,,1]	; Prepare: Delete file page
	Movei	T3,1		; Start at the very beginning
Loop:	Chanio	T1,T3		; Create page
	  Jrst	E$$CFP		;   Huh?
Delpage: Chanio	T2,T3		; Delete it
	  Jrst	E$$DFP		;   Huh?
	Sojle	N,.+2		; Count them
	Aoja	T3,Loop		; Next page
	Move	T1,[.Chftr,,1]	; Now make eof
	Lsh	T3,^D9		; Convert pages into words
	Chanio	T1,T3		;  at present location
	  Jfcl			; Ignore errors.
	Close	1,		; Make them permanent
	Releas	1,		; Now done
None:	Exit	1,
	  Jrst	.-1

E$$DNA:	Outstr	[Asciz\
Disk not available?
\]
	Jrst	None

E$$CFP:	Caie	T1,16		; Page already there?
	  Jrst	M$$CFP		; No. Give error
	Move	T1,[.Chcfp,,1]	; Reset
	Jrst	Delpage		; Delete it.
M$$CFP:	Outstr	[Asciz\
Create page error: \]
	Move	T4,T1		; Look here
M$$:	Exch	T1,T4		; Use T1
	Pushj	P,Outoct##	; Print error code
	Outstr	[Asciz\ at page \]
	Move	T1,T3		; Then page #
	Pushj	P,Outdec##	; ...
	Jrst	None

E$$DFP:	Outstr	[Asciz\
Delete page error: \]
	Move	T4,T2		; Look here
	Jrst	M$$

E$$FEE:	Outstr	[Asciz\
File Enter Error
\]
	Jrst	None

Lkpblk:	.Rbpid
	Z		; PPN
	Sixbit /HUGE/	; Name
	Sixbit /FIL/	; Ext
	Block .Rbpid

PDL:	Block 20

	End Huge
