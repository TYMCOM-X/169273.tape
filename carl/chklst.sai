
begin "CHKLST"

require "(SAILIB)SAIL.DEF" source!file;
require "(CARL)VMFILE.REQ" source!file;

Define Uppercase(x) = {Scan(x, B!Up, Dummy)};
Define White(x) = {Scan(x, B!Wht, Brk)};

Real PerCnt;
Real Total,Tax,Goods;
Real TTot,TTax,TGds;

Integer	Brk,	Dummy,	B!CR	! Break variable, dummy break, Break on CR;
,	B!Lin,	B!Cmd,	B!Brk	! Break on line, command, brk-char;
,	B!Nam,	B!Usr,	B!Ppn	! Break on Name, user, ppn;
,	B!Quote,B!Sub,	B!Par	! Break on quotes, substitution, parameter;
,	B!Num,	B!Wht,	B!Up	! Break on numerics, whitespace, uppercase;
,	Chan
,	DD,MM,LD,LM
;

String DataFile, Line, Cmd, Copy, What;

Define     $Tax=0, $Tot=1,  $Itm=2;
Preset!with "Tax", "Total", "Item";
Own safe string array Commands[ 0:2 ];


procedure BrkIni;
begin
    Define ##Cmd = {";=/ "&#ht}
    ,      ##Wht = {" "&#ht&#cr}
    ,      ##Brk = {" !@#%^&*()a=~`[]|\:;'<>,?/" & '42 & '173 & '175}
    ,      ##Sub = {"""',/= " & #ht & '177}
    ,      #Upper = {"ABCDEFGHIJKLMNOPQRSTUVWXYZ"}
    ,      #Lower = {"abcdefghijklmnopqrstuvwxyz"}
    ,      #Alpha = {#Upper & #Lower}
    ,      #Digit = {"0123456789"}
    ,      #Chars = {#Alpha & #Digit}
    ,      #Par   = {#Alpha & "$-_"}
    ;

    setbreak(B!CR _Getbreak,#cr,         null, "AINF");	! carriage-return;
    setbreak(B!Lin_Getbreak,#lf,         null, "SINF");	! line;
    setbreak(B!Nam_Getbreak,#Chars&crlf, crlf, "RXNF");	! name or token;
    setbreak(B!Usr_Getbreak, ")",        null, "SINK");	! end of username;
    setbreak(B!Ppn_Getbreak,"01234567,", null, "SXNK");	! only allow these;
    setbreak(B!Num_Getbreak,#Digit&"+-.",null, "RXNK");	! skip digits;
    setbreak(B!Wht_Getbreak,##Wht&crlf,  crlf, "RXNK");	! white space;
    setbreak(B!Brk_Getbreak,#lf & ##Brk, #cr,  "RINK");	! all break chars;
    setbreak(B!Par_Getbreak, #Par,       null, "RXNK");	! parameter variable;
    setbreak(B!Sub_Getbreak, ##Sub,      #cr,  "SINF");	! substitutions;
    setbreak(B!Cmd_Getbreak,#lf & ##Cmd, #cr,  "SINK");	! command;
    setbreak(B!Up _Getbreak, null,       null, "SINK"); ! make uppercase;
end;
require BrkIni initialization;

internal simple integer procedure FndKey( String Key; String Array CMD.Names );
begin "Find Key"
    Own Integer Low, High, CMD.Index, Cmd;

    Low_  ArrInfo( CMD.Names, 1 );	! initialize low index   ;
    High_ ArrInfo( CMD.Names, 2 );	! initialize high index  ;

    CMD.Index_ Low - 1;			! setup default: invalid ;

    For Cmd_ Low step 1 until High	! for each keyword;
     do
	If KEqu( Key, CMD.Names[ Cmd ][1 for length(Key)] )
	 then if KEqu( Key, CMD.Names[ Cmd ] )
	     then Return( Cmd )		!  -- exact match --;
	     else if CMD.Index < Low	!  -check ambiguity-;
		then CMD.Index_ Cmd	!  Ok, set instance ;
		else Return( Low - 2 );	!  No, set error    ;

    Return ( CMD.Index );		! return result;
end "Find Key";


simple string procedure Prompt( string P );
begin
    Outstr( P );
    Return( Inchwl );
end;


simple boolean procedure Numeric( String Str );
begin
    Scan( Str, B!Num, Dummy);
    Return( Length( Str ) = 0 );
end;


string procedure TOK ( reference string S );
begin
    string S1;
    White( S );				! clear whitespace;
    S1 _ scan(S, B!Brk, BRK);		! get the next word;
    if length(S1) = 0			! null?;
	then if length(S) = 0		!   null source also?;
	    then return (null)		!    yes - return null;
	    else S1 _ lop(S);		!    no  - get a character;
    White( S );				! clear whitespace;
!    Print( "TOK: ",S1,crlf );
    return(S1);				! return the token;
end;


!	*** M A I N   P R O G R A M ***
;

PerCnt_ RealScan( Line_ Prompt( "Percentage: " ), Brk );
If ( Brk = -1 ) then PerCnt_ 0.065;
If ( PerCnt geq 1.0 ) then PerCnt_ PerCnt / 100.0;

DataFile_ Prompt( "Data file? " );
If ( Length( DataFile ) = 0 )
 then DataFile_ "CHKLST.DAT";

If not( 0 < Chan_ VMFile( DataFile, VM$Update ) )
 then UserErr( 0,0, "Cannot create file: "&DataFile, "X" );

If ( VMGetC( Chan, VM$Size ) )
 then begin VMMove(Chan,-1); VMMove(Chan,1); end;

While ( Length( Line_ Prompt( "Item: " ) ) )
 do begin "process"

    If ( Length( Cmd_ IntScan( Cmd_ Tok( Copy_ Line ), Brk ) ) )
     then If not( MM_ IntScan( Cmd_ Tok( Line ), Brk) ) then MM_ LM;
    If ( Brk neq -1 ) and ( Line = "," or Line = "/" ) then Lop( Line );
    If ( Length( Cmd_ IntScan( Cmd_ Tok( Copy_ Line ), Brk ) ) )
     then If not( DD_ IntScan( Cmd_ Tok( Line ), Brk) ) then DD_ LD;
    If ( Brk neq -1 ) and ( Line = "," or Line = "/" ) then Lop( Line );
    LD_ DD; LM_ MM;

    If ( Numeric(Cmd_ Tok( Copy_ Line )) )
     then begin "numeric?"
	Total_ RealScan( Line, Brk );
	If ( Brk neq -1 ) and ( Line = "," or Line = ";" ) then Lop( Line );
	If ( Numeric(Cmd_ Tok( Copy_ Line )) )
	 then Tax_ RealScan( Line, Brk );
	If ( Brk neq -1 ) and ( Line = "," or Line = ";" ) then Lop( Line );
	If ( Numeric(Cmd_ Tok( Copy_ Line )) )
	 then Goods_ RealScan( Line, Brk );
     end "numeric?"
     else Case FndKey( Cmd, Commands ) of begin
	[$Tax] begin
	    Tax_ Total_ RealScan( Line, Brk );
	    end;
	[$Tot] begin
	    Total_ RealScan( Line, Brk );
	    Goods_ Total / (1.0 + PerCnt);
	    Tax_ Total - Goods;
	    end;
	[$Itm] begin
	    Goods_ RealScan( Line, Brk );
	    Tax_ Goods * PerCnt;
	    Total_ Goods + Tax;
	    end;
	[else] begin
	    Print( "?huh: ",line,crlf );
	    continue "process";
	    end
     end;

    White( Line );
    What_ Line;

    If not( Total )			! tax+goods = total ;
     then if not( Tax )			! calculate tax? ;
	   then Total_ Goods + (Tax_ Goods * PerCnt)
	   else Total_ Tax + Goods
     else begin "a total"
	If not( Tax and Goods )	! not all there - fix it ;
	 then begin "total plus"
	    If ( Tax )			! total+tax ;
	     then Goods_ Total - Tax	! got all 3 now ;
	     else If ( Goods )
		   then Tax_ Total - Goods	! got all 3 now ;
		   else begin "only total"
		      Goods_ Total / (1.0 + PerCnt);
		      Tax_ Total - Goods;
		   end "only total";
	  end "total plus";
     end "a total";

	TTot_ TTot + Total;
	TTax_ TTax + Tax;
	TGds_ TGds + Goods;

	SetFormat( -2,0 );
	VMText( Chan, "85"&Cvs(MM)&Cvs(DD)&" " ); Print( "85", MM, DD, " " );
	SetFormat( 10,2 );
	VMText( Chan, CvF(Goods)&CvF(Tax)&CvF(Total)&"  "&What[1 for 40]&Crlf );
	Print(  CvF(Goods), CvF(Tax), CvF(Total), Crlf& "Totals ",
		CvF(TGds),  CvF(TTax), CvF(TTot),"  ", What[1 for 40], Crlf );
	Total_ Tax_ Goods_ 0.0;	MM_ DD_ 0;

 end "process";

VMFree( Chan );

Call( 0, "EXIT" );

End "CHKLST";