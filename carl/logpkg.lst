
LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 1
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

1	entry
2		Talk,   CTYLog, CvName, RunSlf
3	,	CChild, FrmXct, SetLog, LogInfo
4	;
5	
6	begin "LOGPKG"
7	
8	require "(SAILIB)SAIL.DEF" source!file;
9	require "(MPL)UUOSYM.DEF"  source!file;
10	require "(CARL)VMFILE.REQ" source!file;
11	require "(CARL)DAYTIM.REQ" source!file;
12	
13	Define LogTry  = 25;			! times to try logging to file ;
14	Define LogWait = 200;			! ms to wait while trying ;
15	
16	Define !gtRDV = '135, !gtRDI = '136, !gtRFN = '137;
17	Define Gettab(x,y) = { calli( !xwd( (x), (y) ), calli!GETTAB ) };
18	Define !Job(x) = { ( (x) land '7777 ) };
19	
20	Define DayStamp  = { cvSDat( !lh( GetTDT ) ) };		! day  only ;
21	Define TimeStamp = { cvSTim( !rh( GetTDT ) ) };		! time only ;
22	Define DateStamp = { TymDay( GetTDT ) };		! date-time ;
23	
24	Own integer OldZone, NewZone;		! Time Zone stuff  ;
25	
26	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 2
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

27	!	Initialization support
28	;
29	
30	
31	simple procedure SetDetach;
32	begin
33	
34	    if ( !lh( Gettab(-1,!gtLIN) ) = 0 )
35	     then calli(JP!NAT lor Gettab(-1,!GTPRV), calli!SETPRV);
36	
37	end;
38	require SetDetach initialization;
39	
40	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 3
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

41	
42	Internal Simple procedure Talk( integer job; string text );
43	! ----------------------------------------------------------------------;
44	!									;
45	!	Talk( Job, Text )						;
46	!		Output the specified Text to the port connect to Job	;
47	!		by executing the SETUUO call to talk to a frame.	;
48	!									;
49	! ----------------------------------------------------------------------;
50	begin "Talk"
51	    Define T1=1, T2=2, !stTLK = '10;
52	    Label Talk, TalkOk;
53	
54	    begin!code				! simulate OUTSTR to Job (CTY) ;
55		Move	T2,Text;		! string byte pointer ;
56		Movei	T1,Text;		! addr of byte pointer ;
57		Hrlz	T1,-1(T1);		! setup size variable ;
58		Hrr	T1,Job;			!  to job Job (0 = CTY ) ;
59	Talk:	Hrri	0,T1;			! pointer to block ;
60		Hrli	0,!stTLK;		! function to talk ;
61		uuo!SETUUO;			! doit ;
62		  caia;				! error - skip ;
63		 jrst	TalkOk;			! ok, so leave ;
64		Hrrzi	0,200;			! 200 ms wait ;
65		uuo!HIBER;			! then setup to ;
66		  jfcl;				!  try it all ;
67		jrst	Talk;			!  again ;
68	TalkOk:
69	    end;
70	
71	end "Talk";
72	
73	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 4
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

74	
75	Internal Simple boolean procedure CTYLog( String Text; Boolean Date(false) );
76	! ----------------------------------------------------------------------;
77	!									;
78	!	CTYLog		Routine to print a string on the system CTY	;
79	!			stamped with the current GMT date and time.	;
80	!									;
81	! ----------------------------------------------------------------------;
82	begin
83	
84	    Talk( 0, (if Date			! if date expected ;
85		      then DateStamp		!  today's daytime ;
86		      else TimeStamp		! else -time only- ;
87		      ) &
88		     " GMT " &			! mark it as GMT ;
89		     Text &			! add the message ;
90		     Crlf );			! and crlf ;
91	
92	    if ( !lh(Gettab(-1,!gtLIN)) = !lh(cvsix("TTY")) )
93	     then Talk( calli(0,calli!PJOB), TimeStamp&" GMT "&Text&Crlf );
94	
95	    return( false );
96	
97	end;
98	
99	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 5
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

100	
101	internal simple boolean procedure CChild( integer Cradle );
102	! ----------------------------------------------------------------------;
103	!									;
104	!	CChild		Routine to create a child frame from the	;
105	!			information found at the address Cradle.	;
106	!									;
107	! ----------------------------------------------------------------------;
108	begin "birth procedure"
109	    Redefine CF!LIC = !bit(16);		! until UUOSYM.DEF is right;
110	    Redefine CF!LOG = 0;		! really !bit(17);
111	    Redefine E!Runtime = 0;
112	    Redefine E!License = 10;
113	
114	    Own Integer frame!index;
115	    Own safe integer array Frame[E!Runtime:E!License];
116	
117	    ArrBlt( Frame[E!Runtime+1], memory[Cradle+1], E!License-E!Runtime );
118	    Frame[ E!Runtime ]_ CF!LIC lor CF!LOG lor (E!License - E!Runtime - 1);
119	
120	    start!code				! take a trip to the hospital ;
121		Setom	!skip!;			! skip return;
122		Hrli	1,!foCFM;		! function: create frame;
123		uuo!FRMOP 1,Frame[E!Runtime];	! attempt to bear a child;
124		  Setzm	!skip!;			!   unbearable!;
125		Skipn	!skip!;			! successful birth? ;
126		  Exch	1,!skip!;		! no - swap error & skip ;
127	    end;
128	
129	end "birth procedure";
130	
131	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 6
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

132	
133	internal simple procedure
134	  FrmXct(Integer Frame; Reference Integer Arg; Integer Funct);
135	! ----------------------------------------------------------------------;
136	!									;
137	!	FrmXct		Routine to execute a frame operation.		;
138	!			Results are returned in ac 1 and !skip!		;
139	!									;
140	! ----------------------------------------------------------------------;
141	begin!code
142	
143		Hrrz	1,Frame;		! Get frame number from array ;
144		Hrl	1,Funct;		! Get function ;
145		Movei	2,Arg;			! Get argument address ;
146		Setom	!skip!;			! Set skip return ;
147		uuo!FRMOP  1,(2);		! Do the work ;
148		  Setzm	!skip!;			! OOPS! too bad ;
149	end;
150	
151	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 7
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

152	
153	simple procedure LogAll( String Name, Text; Integer Tries(LogTry) );
154	! ----------------------------------------------------------------------;
155	!									;
156	!	LogAll		Routine to make an entry to the appropriate	;
157	!			job queue log file.  Automatically adds CRLF	;
158	!			to the end of the message.			;
159	!									;
160	! ----------------------------------------------------------------------;
161	begin "Log Job Info"
162	    Integer Chan, Timer;
163	
164	    for Timer_ 1 upto Tries
165	     do begin "get file"			! get file in $Update ;
166		if ( 0 < Chan_ VMFile(Name, VM$Append lor VM$Update) )
167		 then done;
168		calli( !Xwd(0,LogWait), calli!HIBER );	! sleep a bit ;
169	     end "get file";
170	
171	    if ( Chan leq 0 )				! all ok? ;
172	     then CTYLog( "No access: "&Name )		! not exactly... ;
173	     else begin "setup file"			! yes! ;
174	
175		VMText( Chan, TimeStamp&Text&Crlf );	! write time info  ;
176		VMFree( Chan );				! release  channel ;
177	
178	     end "setup file";
179	
180	    if ( !lh(Gettab(-1,!gtLIN)) = !lh(cvsix("TTY")) )
181	     then Talk( calli(0,calli!PJOB), TimeStamp&Text&Crlf );
182	
183	end "Log Job Info";
184	
185	
186	own string LogFile;
187	internal simple procedure SetLog( String File );
188	LogFile_ File;
189	
190	
191	internal simple procedure LogInfo( String Text );
192	LogAll( LogFile, Text, LogTry );
193	
194	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 8
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

195	
196	internal simple boolean procedure RunSlf;
197	! ----------------------------------------------------------------------;
198	!									;
199	!	RunSlf		Routine to figure out where you are and run	;
200	!			the same program.  This assumes that we are	;
201	!			NOT using a funny extension or were run with	;
202	!			a load, start or execute command.		;
203	!									;
204	! ----------------------------------------------------------------------;
205	begin "run self"
206	    Own Safe Integer Array RunBlock[0:5];
207	
208	    CtyLog( "Restarting by Feature-Test" );
209	
210	    RunBlock[0]_ Gettab( -1, !gtRDV );
211	    RunBlock[1]_ Gettab( -1, !gtRFN );
212	    RunBlock[4]_ Gettab( -1, !gtRDI );
213	
214	    Calli( location(RunBlock[0]), calli!RUN );
215	    if not( !Skip! )
216	     then return(false );
217	
218	end "run self";
219	
220	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 9
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

221	
222	internal simple string procedure CvNAME( integer AUN );
223	! ----------------------------------------------------------------------;
224	!									;
225	!	CvName		Routine to get the username for a specific	;
226	!			AUN by looking up the UFD.  Uses the .chNXT	;
227	!			feature to get a channel and releases it to	;
228	!			preserve outside file/channel integrity.	;
229	!									;
230	! ----------------------------------------------------------------------;
231	begin
232	    integer NotEof, Chan;
233	    Preset!with '17, cvSIX( "DSK   " ), 0;
234	    Own safe integer array Dev[ 0 : 2 ];
235	    Own safe integer array Look[ 0 : !RBUNM+1 ];
236	
237	    Look[ !RBCNT ]_ !RBUNM+1;		! PPN -> User;
238	    Look[ !RBPPN ]_ !Xwd( 1,1 );	! (UFD)  ;
239	    Look[ !RBNAM ]_ AUN;		! [user] ;
240	    Look[ !RBEXT ]_ cvSIX("UFD   ");	! .UFD   ;
241	
242	    Chan_ VMCHan;			! channel to use ;
243	    Chnior( Chan, Dev[ 0 ], !chOPN );	! open DSK ;
244	    If ( NotEof_ !skip! )
245	     then begin
246		Chnior( Chan, Look[!RBCNT], !chLK );
247		NotEof_ !skip!;
248		Chnior( Chan, memory[0], !chREL );
249	     end;
250	
251	    If ( NotEof )
252	     then return( Cv6str(Look[!RBUNM])&Cv6str(Look[!RBUNM+1]) )
253	     else return( Cvos(!lh(aun))&","&Cvos(!rh(aun)) );
254	end;
255	
256	

LOGPKG.SAI                      created 14-Nov-86 14:23                                  Page 10
Program Listing                 by CRFSYM %5 at 03-Dec-86 17:01

257	end "LOGPKG";
 