entry
	ESC!,   NTQ!,   TIM!,   SetTim,
	ESCENB, NTQENB, TIMENB, EXCINI,
	ESCDIS, NTQDIS, TIMDIS, EXCDIS
;

begin	"EXCTRP"

require "(SAILIB)SAIL.DEF" source!file;
require "(MPL)UUOSYM.DEF"  source!file;
require "(CARL)INTTRP.DEF" source!file;
!get!definitions(tymint);


!	Variable definitions for outer block;

Internal boolean
	NTQ!	! true if notice-to-quit ;	! generally from KSYS ;
,	ESC!	! true if ^C or ESC hit ;	! keyboard interrupt ;
,	TIM!	! true if timer interrupt ;	! general clock timer ;
;

!	Interrupt routines
;
preset!with cvasc( '15&'0 );
own safe integer array escape[0:0];

simple procedure ESC;
begin
    ESC!_ true;		! set ESC flag and return ;
end;


simple procedure NTQ;	NTQ!_ true;	! set NTQ flag and return ;
simple procedure TIM;	TIM!_ true;	! set TIM flag and return ;

internal simple procedure EXCINI;
begin
    TIM!_ NTQ!_ ESC!_ False;
    intmap (intass(!IATIM,int!TIM),TIM,0);      enable (newenb(int!TIM));
    intmap (intass(!IANTQ,int!NTQ),NTQ,0);      enable (newenb(int!NTQ));
    intmap (tinass(-1,!IAESC,int!ESC),ESC,0);   enable (newenb(int!ESC));
end;
require EXCINI initialization;

internal simple procedure TIMEnb;
begin
    TIM!_ False;
    intmap (intass(!IATIM,int!TIM),TIM,0);
    enable (newenb(int!TIM));
end;

internal simple procedure NTQEnb;
begin
    NTQ!_ False;
    intmap (intass(!IANTQ,int!NTQ),NTQ,0);
    enable (newenb(int!NTQ));
end;


internal simple procedure EscEnb;
begin
    ESC!_ False;
    intmap (tinass(-1,!IAESC,int!ESC),ESC,0);
    enable (newenb(int!ESC));
end;


internal simple procedure EscDis;
begin
    intmap (tinass(-1,!IAESC,0),ESC,0);
    disable (newenb(int!ESC));	! <esc> ;
end;

internal simple procedure TimDis;
begin
    intmap (intass(!IATIM,0),TIM,0);
    disable (newenb(int!TIM));	! timer ;
end;
    
internal simple procedure NTQDis;
begin
    intmap (intass(!IANTQ,0),NTQ,0);
    disable (newenb(int!NTQ));	! NTQ ;
end;

internal simple procedure EXCDIS;
begin
    EscDis;
    NTQDis;
    TimDis;
end;

internal simple procedure SetTim( integer Time, Units( 0 ) );
calli( !Xwd( Units,Time ), calli!SETTIM );

end "EXCTRP" $

