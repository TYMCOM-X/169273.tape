begin	"XEXEC"

require "XEXEC.INF" source!file;

Define XEXEC!VERSION = (XEXEC!!VER lsh 24) lor XEXEC!!EDIT;
require XEXEC!VERSION version;

require "

XEXEC %"&CVOS(XEXEC!VERSION lsh -24)&"("&CVOS(XEXEC!VERSION land '777777)&")"&
     " - TYMCOM-X Periodic Processing Scheduler

Assembly: Load @XEXEC.CMD


Sources:  XEXEC.SAI                 Main program source file
          XEXEC.INF                 Main release information file
          XEXEC.PAR                 Main parameter file
          XEXEC.REQ                 Global declaration file
          SAIL.DEF                  General macro definition file
          UUOSYM.DEF                System call definition file
          JQUEUE.DEF                Job queue definition file

Library:  JQUE.REQ   [SAI] [REL]    Job queue manipulation package
          NEXTIM.REQ [SAI] [REL]    Job reschedule daytime routine
          XEXPKG.REQ [SAI] [REL]    Miscellaneous support package
          VMFILE.REQ [SAI] [REL]    VM input file mapping pacakge
          DAYTIM.REQ [SAI] [REL]    Date time conversion package
          XEXINT.REQ [SAI] [REL]    Interrupt trapping package

License:  ALL license bits!!!
          WC RC SY OP GD TD ST JL AC XC HF RA WA
          WC       - absolute frame access rights
          SY       - reading info about jobs
          HF QM JL - writing system queue and log files

" message;

require "(SAILIB)SAIL.DEF" source!file;
require "(CARL)XEXEC.PAR"  source!file;
require "(MPL)UUOSYM.DEF"  source!file;
require "(CARL)JQUEUE.DEF" source!file;
require "(CARL)JQUE.REQ"   source!file;
require "(CARL)NEXTIM.REQ" source!file;
require "(CARL)VMFILE.REQ" source!file;
require "(CARL)XEXPKG.REQ" source!file;
require "(CARL)DAYTIM.REQ" source!file;
require "(CARL)XEXINT.REQ" source!file;


!	Definitions
;

Define
	Sys$Run   =  0		! normal operation ;
,	Sys$Reset =  1		! system reset ;
,	Sys$Abort =  2		! error indication ;
;

Define Minute = 60;		! how many seconds in a minute ;
Define Spaces = { "                                                  " };
Define LPad(x,y) = { (Spaces[1 for y-1]&(x))[inf-(y-1) to inf] };
Define !gtRDV = '135, !gtRDI = '136, !gtRFN = '137;
Define Gettab(x,y) = { calli( !xwd( (x), (y) ), calli!GETTAB ) };
Define !Job(x) = { ( (x) land '7777 ) };
Define cvj(x) = { cvs( !Job(x) ) };
Define Xvs(x) = { ( if x > E!RunMax then "stream #" & cvs(x-E!RunMax)
				    else "run #" & cvs(x) ) };

Define SetFlg(x,y) = { x_ x lor (1 lsh y) };		! set bit lsh x ;
Define ClrFlg(x,y) = { x_ x land lnot(1 lsh y) };	! clr bit lsh x ;

Define VersionString = { "%"&CVOS(XEXEC!VERSION lsh -24) &
			 "("&CVOS(XEXEC!VERSION land '777777)&")" };
Define DayStamp  = { cvSDat( !lh( GetTDT ) ) };		! day  only ;
Define TimeStamp = { cvSTim( !rh( GetTDT ) ) };		! time only ;
Define DateStamp = { TymDay( GetTDT ) };		! date-time ;

!	Variable definitions for outer block;

Internal integer
	MyJob			! current job slot;
,	XEXJob			! my job number for interrupts ;
;

boolean
	NeedEscape		! flag whether waiting for escape;
,	NeedReset		! flag whether this is a reset ;
,	OverTime		! flag whether time has passed ;
,	TRU!			! flag whether runtime expired ;
,	HaveProcessor		! flag whether we think we have this ;
;

integer 
	JPri			! schedule priority level -1,0,1-9,10-15;
,	JobType			! type of job, 2 = DET + RUN = RJob ;
,	Minutes			! how many minutes we've been running ;
,	Seq			! current sequence number index;
,	SlotAdr			! address of record slot in memory ;
,	Status			! job status code from record ;
;

string
	S			! temporary string variable;
,	RunProg			! place holder for program name ;
;

!	Record and array definitions
;

record!class job( Integer Run, Ran, Seq, Sts, Opt, Tim; r!p (job) Nxt );
r!p (job) jobs, tjob, njob, ljob;

Own safe r!p (job) array JobPri[ JP$Run : JP$Max ]; ! job queue by priority ;

Preset!with [E!Length] 0;
Own safe integer array Del[ 0 : E!Length-1 ];	! For Delete ;
Preset!with [E!Length] 0;
Own safe integer array Rec[ 0 : E!Length-1 ];	! The Record ;

!	Support routines
;

Internal Simple Procedure GetInt( reference integer Flag; integer Why );
return;


procedure SetTimeLimit( Integer TimeLimit );
begin
    own safe integer array Limit[0:2];
    define calli!DAYTIM = -'143;

    if TimeLimit = 0
     then Limit[0]_ 0
     else begin "new limit"
	Limit[0]_ Calli( 0,calli!DAYTIM ) + (60 * TimeLimit);
	Limit[2]_ !xwd( '600020,'500020 );	! old=TYM-SEC,,new=TYM-UDT ;
	calli( location(Limit[0]), calli!DATUUO );
     end "new limit";

    QRT( MyJob )_ Limit[0];

end;


Simple Boolean Procedure ClearFrame( Integer This );
begin "clear frame"

    if ( QRF( This ) )			! anything here? ;
     then begin "frame must die"
	QRF( This )_ 0;			! clear frame position ;
     end "frame must die";

    return( false );			! give 'em what they came for ;

end "clear frame";


Simple procedure ClearSlot( Integer JobSlot, Address(0) );
begin "Clear Slot"
    Own integer Adr;

    if ( Address )			! if one is specified ;
     then Adr_ Address			!  then use it, else calculate ;
     else Adr_ QueRun + (E!Length * (JobSlot - 1 ));

    memory[Adr]_ 0;			! clear active slot area ;
    ArrBlt( memory[Adr+1], memory[Adr], E!Length );

    QRS( JobSlot )_ QRA( JobSlot )_	! cleanup database: status, options ;
    QRR( JobSlot )_ QRT( JobSlot )_	! cleanup database: tru, time limit ;
    QRL( JobSlot )_ 			! cleanup database: log limits ;
    QRF( JobSlot )_ QRU( JobSlot )_ 0;	! cleanup database: frame, uid ;

end "Clear Slot";


Simple procedure KillJob( Integer J );
begin "Kill Job"

    if not( J )				! don't try anything to job 0 ;
     then return;			! it won't buy us much ;

    if not( calli( !Xwd(1,!Job(J) ), calli!EVICT ) )
     then begin "assinate"
	calli( !Xwd(3,!Job(J) ), calli!HANG );
	QQ( Running )_ 0 max ( QQ( Running ) - 1 );
     end "assinate";

end "Kill Job";


Simple procedure EXIT( String Msg(Null) );
! ----------------------------------------------------------------------;
!									;
!	Exit	Routine to print a message to the CTY and then exit.	;
!									;
! ----------------------------------------------------------------------;
begin

    EscDis;				! disable any interrrupts ;
    NtqDis;				! for ESC, NTQ ;
    SiaDis;				! and from frame ;

    LogInfo( Msg );			! report the reason for leaving ;
    CtyLog( "XEXEC: "& Msg, true );	! to the log and the CTY ;

    if ( InQue land que$Map )		! check for logoff flag ;
     then begin "finish up"		! time to check out? ;

	if ( calli(0,calli!PJOB) = QQ(Schedule) )
	 then QQ( Schedule )_ QQ( HaveS )_ 0;
	QQ( Flags )_ QQ( Flags ) land
		    lnot ( QF$Log lor QF$Run lor QF$Act );

     end "finish up";

    calli(queact lsh -9, calli!CLBRLI);	! release ACT page interlock ;
    calli(queact lsh -9, calli!CLBLEV);	! leave ACT page club ;
    calli(querun lsh -9, calli!CLBRLI);	! release RUN page interlock ;
    calli(querun lsh -9, calli!CLBLEV);	! leave RUN page club ;
    ClsQue;

    if not( !lh( Gettab(-1,!gtLIN) ) )	! if no terminal attached ;
     then calli( 0, calli!LOGOFF )	!  then logout normally ;
     else start!code uuo!EXIT end;	!  else just exit ;

end;

!	Initialization routines;

simple procedure WhoShouldIBe;
begin
    LogWho( "Scheduler" );		! label my messages ;
    XEXJob_ calli( 0,calli!PJOB );	! remember my name  ;
    RunProg_ cv6str( Gettab( XEXJob,!gtRFN ) );
end;
require WhoShouldIBe initialization;


simple procedure LicenseCheck;
! ----------------------------------------------------------------------;
!									;
!	LicenseCheck	Routine to check the license set on the frame	;
!			and the program to determine if it is really	;
!			sufficient to get any work accomplished.	;
!									;
! ----------------------------------------------------------------------;
begin
! '777617 ;
    If ( Gettab( -1, !GTLIC )
  Ifcr PRELIMINARY thenc
	land ( LC!SY lor LC!JL lor LC!WC )
	 neq ( LC!SY lor LC!JL lor LC!WC ) )
  elsec
	land ( LC!SY lor LC!JL lor LC!WC lor LC!HF )
	 neq ( LC!SY lor LC!JL lor LC!WC lor LC!HF ) )
  endc
     then EXIT( "Insufficient License - SY JL WC HF -" );
end;
require LicenseCheck initialization;


Simple boolean procedure FrontEnd( integer Adr );
begin "FrontEnd"
    own integer C, Chan;
    own string TempFile;
    own safe integer array RunBlock[0:5];

    If ( ESC! or NTQ! )				! ^C or Notice-to-quit? ;
     then return( false );			!  just return ;

    If ( LAPRunF(Adr) )				! if RUN program  ;
      and ( LAPDetF(Adr) )			!    and DETACH   ;
     then begin "run detached job"		!  then do it.    ;

	if not( QRF(MyJob) and LPStap( QRS(MyJob) ) geq Proc$Run )
	 then begin "new job"
	    if (QRF(MyJob)_ CChild(Adr) )	! create a child frame;
	     then QRU(MyJob)_ Gettab( QRF(MyJob), !gtUID )
	     else begin "birth defect"
		dpb( ( if ( !rh( QRF(MyJob) ) < 2 )
			then Sched$Frame	! capacity error ;
			else Abort$Frame ), PStap( QRS(MyJob) ) );
		return( false );		! abort & give up! ;
	     end "birth defect";
	 end "new job";

	RunBlock[0]_ if ( LAPCclF(Adr) or LAPTmpF(Adr) or 
			  memory[Adr+E!Parameters] )	! CCL parameters? ;
		      then !Xwd( 1,5 ) else 5;	! setup run offset+count ;
	RunBlock[1]_ memory[ Adr+E!CmdFile+F!Device ];
	RunBlock[2]_ Adr+E!CmdFile+F!User;
	RunBlock[3]_ memory[ Adr+E!CmdFile+F!Name ];
	Runblock[4]_ memory[ Adr+E!CmdFile+F!Ext ] land (-1 lsh 18);

	if ( !lh( RunBlock[0] ) )		! CCL entry expected? ;
	 then begin "write TMP file"		! create temp file ;
	    TempFile_ cvs(1000+!job(QRF(MyJob)))[inf-2 to inf] &
		      (if ( LAPTmpF(Adr) )
		        then cv6str( LAPTmpF(Adr) lsh 18 )
			else cv6str( RunBlock[3] )[1 for 3]) & ".TMP";
	    if ( 0 > Chan_ VMFile( TempFile, VM$Write ) )
	     then begin "write parameters"
		for C_ 0 upto (E!ParLength div 5)-1
		 do if ( memory[Adr+E!Parameters+C] )
		     then VMText( Chan, cvAstr( memory[Adr+E!Parameters+C] ) )
		     else done;
		VMText( Chan, crlf );		! finish text with crlf ;
		VMFree( Chan );			! close temp file ;
	     end "write parameters";
	 end "write TMP file";

	FrmXct( QRF(MyJob), RunBlock[0], !foRUN );
	EcfEnb( QRF(MyJob) );			! enable ECF ;
	dpb( Proc$Run, PStap( QRS(MyJob) ) );	! Processing, Run ;
	SetTimeLimit( LAPTimL(Adr) );		! set any time limit ;

     end "run detached job"
     else dpb( Sched$Wait, PStap(QRS(MyJob)) );	! Scheduled, Wait ;

    LogInfo( "Scheduling " & xvs(MyJob) & " " & InfJob( Adr ) );
    if ( LPCtyF( QRA(MyJob) ) )
     then CtyLog( "Scheduling " & xvs(MyJob) & " " & InfJob( Adr ) );

end "FrontEnd";


simple boolean procedure CommandMode;
return( Gettab( QRF(MyJob), !gtSTS ) geq 0 );


simple boolean procedure ErrorMode;
return( Gettab( QRF(MyJob), !gtSTS ) land jb!jer );


procedure ESCAPE;
begin "Escape"

    if ( CommandMode )				! if command mode ;
     then ClrFlg( NeedEscape, MyJob )		!  clear escape   ;
     else begin "halt frame"
	DEFINE !FOSTP = '41;
	FrmXct( QRF(MyJob), 0, !foSTP );	! stop the frame  ;
	SetFlg( NeedEscape, MyJob );		!  set NeedEscape ;
     end "halt frame";

end "Escape";


simple procedure CreateProcessor;
begin "create a processor"
    preset!with 5,cvSIX("SYS"),0,cvSIX("EXECX"),0,0;
    own safe integer array RunBlock[0:5];
    own safe integer array Frame[E!Runtime:E!License];

    ArrClr( Frame );				! setup empty slate ;
    Frame[ E!PPN ]_      Gettab( -1,!gtPPN );	! gfd'd ppn ;
    Frame[ E!Privs ]_    Gettab( -1,!gtPRV );	! privileges ;
    Frame[ E!AUN ]_      Gettab( -1,!gtAUN );	! login ppn ;
    Frame[ E!Username ]_ Gettab( -1,!gtUNM );	! username - part 1 ;
    Frame[E!Username+1]_ Gettab( -1,!gtUN1 );	! username - part 2 ;
    Frame[ E!License ]_  Gettab( -1,!gtLIC );	! license ;

    if ( QQ( Process )_ CChild( location(Frame[E!Runtime]) ) )
     then begin "successful"			! create processor ;

	RunBlock[1]_ Gettab( -1,!gtRDV );
	RunBlock[2]_ Gettab( -1,!gtRDI );

	FrmXct( QQ( Process ), RunBlock[0], !foRUN );
	if not( !Skip! )
	 then LogInfo( "Problem with processor in frame "& cvj(QQ( Process )) );

     end "successful";

end "create a processor";


simple procedure StartProcessor;
begin "startup a processor"
    integer Page, Err;

    if not( ClubInit( Page_ queAct lsh -9 ) )	! can we get interlock? ;
     then begin "not mine"
	QQ( Flags )_ QQ( Flags ) land lnot QF$Act;
	if ( !skip! = CBAIL# )			! already allocated ;
	 then if ( QQ( Process ) ) and
		 ( QQ( HaveP ) = Gettab( QQ( Process ), !gtUID ) )
	       then begin "have processor"
		  LogInfo( "Job processor already in frame "&cvj(QQ( Process )) );
		  HaveProcessor_ QQ( Process );	! remember for myself ;
		  return;			! yes, return. ;
	       end "have processor"
		else begin "wrong processor"
		  LogInfo( "Job processor says frame "&cvj(QQ( Process )) );
		  SetFeature( Shut );
		  QQ( HaveP )_ HaveProcessor_ false;
		  calli( 5,calli!SLEEP );
		end "wrong processor"
	 else begin "bad interlock"		! hmmm, indicate problem ;
	    LogInfo("Processor interlock problem ("&cvos(!skip!)&") check system");
	    LogInfo( "Processor= "&cvj(QQ( Process ))&" uid= "&cvs(QQ( HaveP ))
		&" not "&cvs( Gettab( QQ( Process ), !gtUID ) ) );
	    SetFeature( Shut );
	    QQ( HaveP )_ HaveProcessor_ false;
	    calli( 5,calli!SLEEP );		! sleep for effect ;
	 end "bad interlock";
     end "not mine";

    ClearFeature( Shut );
    CreateProcessor;				! create a processor job ;
    SetTim( 300 );				! maximum of 5 minutes ;
    while not( TIM! )
     do if ( QQ( Process ) and			! if processor setup ;
	     QQ( HaveP ) = Gettab( QQ(Process),!gtUID ) )
	 then begin "give away interlock"
	    calli( Page, calli!CLBRLI );	! release interlock ;
	    LogInfo( "Given interlock" );	! give message ;
	    done;
	 end "give away interlock"
	 else calli( 1, calli!SLEEP );		! else sleep ;

    if not( TIM! )
     then begin "wait for interlock"
	SetTim( 300 );
	while not( TIM! )
	 do if ( QQ( HaveP ) neq QQ( ProcessUDT ) )
	     then calli( 1, calli!SLEEP )
	     else done;
     end "wait for interlock";
    if not( TIM! )
     then begin "processor started"
	QQ( ProcessUDT )_ GetTDT;		! set time process started ;
	LogInfo( "Starting processor in frame "& cvj(QQ( Process )) );
     end "processor started"
     else calli( location(Page),calli!CLBINI );	! else take interlock ;

    SetTim( Minute );				! reset timer ;

 end "startup a processor";


Simple Boolean Procedure NextRecord;
begin "next record"

    arrclr( Rec );			! clear record fields ;

    if ( ESC! or NTQ! )			! if ( ^C or Notice-to-quit ) ;
     then return( false );		!  just return immediately ;

    dpb( Seq, PPSeq( Rec ) );		! set sequence number to select ;
    return( GetRec( Rec[E!Runtime] ) );	! oh, get on with it ;

end "next record";


Procedure SortQueue;
begin "sort queue"
    Integer ASeq;

    Seq_ 0;					! loop through them all ;

    if ( LPPStat(Rec) = JS$Cancel )		!  if cancel, remove it ;
     then begin "cancel it"
	ModRec( Del[E!Runtime], Rec[E!Runtime] );
	return;
     end "cancel it";

    ASeq_ LPPSeq( Rec );			! remember this a while ;

    if not( JobPri[ JP$RUN ] )			! check the run list ;
     then njob_ JobPri[JP$Run]_ new!record(job)	!  null?  make a record ;
     else njob_ JobPri[JP$Run];			! begin at top of list  ;

    while ( njob_ job:Nxt[tjob_njob] )		! until the empty list  ;
     do if ( job:Seq[njob] = ASeq )		! if same as record ;
	 then return;				!  it's running, ignore ;

    JPri_ LPPJPri( Rec );			! get priority level ;
    if ( JPri = JP$Min )			! absolute minimum says ;
     then JPri_ LPJPri( QQ( Action) );		!  use default priority ;

    if not( JobPri[ JPri ] )			! check this priority   ;
     then njob_ JobPri[JPri]_ new!record(job)	!  null?  make a record ;
     else njob_ JobPri[JPri];			! begin at top of list  ;

    while ( njob_ job:Nxt[tjob_njob] )		! until the empty list  ;
     do if ( Rec[E!Runtime] < job:Run[njob] )	!  if runtime < job:Run ;
	 then done;				!   then fall to insert ;

    tjob_ job:Nxt[tjob]_ new!record( job );	! create the new record ;
    job:Nxt[tjob]_ njob;			! keep any forward ptrs ;
    job:Opt[tjob]_ Rec[E!Action];		! copy all action flags ;
    job:Tim[tjob]_ Rec[E!LogLim];		! copy limit info ;
    job:Sts[tjob]_ Rec[E!Status];		! copy status field ;
    job:Ran[tjob]_ Rec[E!Rantime];		! copy any last runtime ;
    job:Run[tjob]_ Rec[E!Runtime];		! copy runtime  & seq # ;
    job:Seq[tjob]_ ASeq;

end "sort queue";


Procedure Prune( Integer ASeq  );
begin "prune"
    r!p (job) nj,tj;
    integer Pri;

    for Pri_ JP$Run upto JP$Max		! for each priority level ;
     do begin "each level of priority"

	if not( nj_ JobPri[ Pri ] )	! if nothing at this level ;
	 then continue;			!  then try the next one ;

	while ( nj_ job:Nxt[tj_nj] )	! for each record ;
	 do if ( job:Seq[nj] = ASeq )	! if seq match ;
	   then begin "process"

	      job:Nxt[tj]_ job:Nxt[nj];	! delete NJOB from list ;

	   end "process";		! search for any multiples ;

     end "each level of priority";

end "prune";


Procedure TellAll;
begin "Tell All"
    r!p (job) nj,tj;
    integer Pri;

    for Pri_ JP$Run upto JP$Max		! for each priority level ;
     do begin "each level of priority"

	if not( nj_ JobPri[ Pri ] )	! if nothing at this level ;
	 then continue;			!  then try the next one ;

	while ( nj_ job:Nxt[tj_nj] )	! for each record ;
	 do LogInfo("Rec:"& LPad( cvs(job:Seq[nj]), 3 ) &
		" at " & Tymday(job:Run[nj]) &
		" pri " & LPad( cvs(Pri), 2 ) &
		" opt " & LPad( cvos(job:Opt[nj]), 12 ) &
		" sts " & cvos(job:Sts[nj])    );

     end "each level of priority";

    for MyJob_ 1 upto E!JobMax
     do if ( QRS(MyJob) )
	 then LogInfo( Xvs(MyJob) & " seq" & LPad(cvs(LPSeq(QRS(MyJob))),3) &
		   " opt " & LPad( cvos(QRA(MyJob)), 12 ) &
		   " sts " & cvos(QRS(MyJob))  );

end "Tell All";


simple procedure ReBuildQueue;
begin "rebuild"

    ljob_ JobPri[ JP$Run ];		! remember this poin
    ArrClr( JobPri );			! forget this table now ;
    JobPri[ JP$Run ]_ ljob;		! remember this pointer ;

    Seq_ -1;				! initialize sequencer  ;

    While ( NextRecord )		! while records exist   ;
     do SortQueue;			!  sort/insert in list  ;

end "rebuild";


Simple Procedure ReSchedule( Integer JobSlot, Type( Sys$Run ) );
begin "reschedule"
    Own integer Adr,Sts;

    arrClr( Rec );
    Adr_ QueRun + E!Length * ( JobSlot - 1 );
    dpb( Seq_ LPSeq(QRS(JobSlot)), PPSeq(Rec) );

    if ( Seq )
     then begin "valid slot info"
	LogInfo( "-R- " & Xvs(JobSlot) & " [" & cvs(Type) & "]" &
		 " Seq #"&cvs(LPSeq(QRS(JobSlot))) &
		 " File " & InfNam( Adr+E!CmdFile ) &
		 " C<" & cvs(LPStat(QRS(JobSlot))) &
		 ":" & cvs(LPProc(QRS(JobSlot))) & ">" );

	if ( GetRec( Rec[E!Runtime] ) )
	 then begin "valid queue entry"

	    Sts_ Queued$Wait;		! initial expected default ;
	    case ( LPStat(QRS(JobSlot)) )
	     of begin "new status"
	      [JS$Done]
		Sts_ if ( LPProc(QRS(JobSlot)) = PSD$Restart )
		      then Hold$Restart
		      else Queued$Wait;
	!     [JS$Sched]			!  Sts_ Queued$Wait ;
	      [JS$Proc]
		Sts_ if ( LPRestart( QRA(JobSlot) ) )
		      then (if (Type) then Hold$Restart else Queued$Wait)
		      else case Type of (
			[Sys$Reset] Queued$Crash,
			[Sys$Abort] Hold$Restart,
			[Sys$Run]   Queued$Wait   );
	      [JS$Abort]
	        Sts_ LPStap(QRS(JobSlot))
	     end "new status";

	    if ( Sts = Queued$Wait )
	     then begin "new entry"		! if OK, delete old entry ;
		ModRec( Del[E!Runtime], Rec[E!Runtime] );
		dpb( 0, APSeq( Adr ) );		! zero seq for new # ;
		memory[Adr+E!Runtime]_ NexTim( memory[Adr],GetTDT );
	     end "new entry";

	    dpb( Sts, APStap( Adr ) );		! set new status ;
	    if not( if ( memory[Adr+E!Runtime] )
		     then ModRec( memory[Adr+E!Runtime], Rec[E!Runtime] )
		     else ModRec( Del[E!Runtime], Rec[E!Runtime] ) )
	     then LogInfo( Xvs(MyJob)&" Modrec failed = "&cvs(QError) );

	 end "valid queue entry";

	Prune( LPSeq(QRS(JobSlot)) );	! prune old from JobPri tree ;
	if ( LPCtyF( QRA(MyJob) ) )
	 then CtyLog( "Finished " & xvs(MyJob) & " " & InfJob( Adr ) );
     end "valid slot info";

    ClearSlot( JobSlot, Adr );		! clear out slot ;

end "reschedule";


Integer Procedure SlotsAvailable;
begin "slots available"
    Own r!p (job) rjob;
    Own integer Adr, Counter, Temp;

    if ( ESC! or NTQ! )				! if ( ^C or Notice-to-quit ) ;
     then return( 0 );				!  just go home now... ;

    QQ( Daytime )_ GetTDT;			! specify schedule time ;

! *** Don't forget %KSYS time ***
;
    if ( QQ( New ) )				! if anything changed ;
     then begin "un-mark queue"
	QQ( New )_ 0;				! clear change flag ;
	ReBuildQueue;				! and sort the queue ;
     end "un-mark queue";

    for JPri_ JP$Max downto JP$Min		! for each priority level ;
     do begin "each level of priority"

	if not( njob_ JobPri[ JPri ] )		! if nothing at this level ;
	 then continue;				!  then try the next one ;

	while ( njob_ job:Nxt[tjob_njob] )	! for each record ;
	 do begin "process queue"

	    if ( ESC! or NTQ! )			! if ( ^C or Notice-to-quit ) ;
	     then return( 0 );			!  just go home now... ;

	    if ( LPStap(job:Sts[njob]) neq Queued$Wait )
	     then continue;			! only schedule queued jobs ;

	    if ( job:Run[njob] > GetTDT )	! scheduled for later? ;
	     then done;				! none left on this level ;

	    if ( job:Run[njob] = -1 )  and	! startup? ;
	       ( job:Ran[njob] > QQ( System ) )	! but run since startup ;
	     then continue;			! go to next in list ;

	    if ( Gettab( 6,!gtNSW ) )		! if %KSYS set ;
	     then begin "time limit check"
		if not( Temp_ LPTimL( job:Tim[njob] ) )
		 then continue;			! don't schedule infinate ;
		if ( Temp > Gettab( 6,!gtNSW ) )
		 then continue;			! don't schedule long jobs ;
	     end "time limit check";

	    if ( LPRunF( job:Opt[njob] )	! if runnable job ;
	      and LPDetF( job:Opt[njob] ) )	!  and run detached ;
	     then begin "running job"

		if ( QQ( Running ) geq QQ( RJobs ) )
		 then continue;			! run slots available? ;
		for MyJob_ 1 upto QQ( RSlot )	! yes, find right slot ;
		 do if not( QRS( MyJob ) )	!  in range 1 to Q!Rslot ;
		     then done;
		if ( QRS( MyJob ) )		! find one? ;
		 then continue;			! no? ;

	     end "running job"
	     else begin "interactive job"

		if ( QQ( Active ) geq QQ( IJobs ) )
		 then continue;			! i slots available? ;
		for MyJob_ E!RunMax+1 upto QQ( ISlot )
		 do if not( QRS( MyJob ) )	!  in range for Q!ISlot ;
		     then done;
		if ( QRS( MyJob ) )		! find one? ;
		 then continue;			! no? ;

	     end "interactive job";

	    dpb( job:Seq[njob], PPSeq( Rec ) );	! store sequence ;
	    if not( GetRec( Rec[E!Runtime] ) )	! get record from queue ;
	     then begin "missing record"	! not there? oops! ;
		job:Nxt[tjob]_ job:Nxt[njob];	!  delete NJOB from list ;
		continue;			!  and go on to the next ;
	     end "missing record";

	    ArrBlt(memory[Adr_querun+(E!Length*(MyJob-1))], Rec[0], E!Length);
            QRS( MyJob )_ memory[Adr+E!Status];	! copy status ;
            QRA( MyJob )_ memory[Adr+E!Action];	! copy options ;
	    QRR( MyJob )_ memory[Adr+E!TruLim];	! copy tru limit ;
	    QRL( MyJob )_ memory[Adr+E!LogLim];	! copy log,time limits ;

	    if not( FrontEnd( Adr ) )		! if unable to schedule it ;
	     then begin "not ready"
		if ( ESC! or NTQ! ) and ( Rec[E!Status] = QRS(MyJob) )
                 then done;			! don't bother if NTQ ;
		LogInfo( "Front end problem" );
		Reschedule( MyJob );		!  reschedule it ;
		continue;			!  go to the next ;
	     end "not ready";

	    job:Nxt[tjob]_ job:Nxt[njob];	! delete NJOB from list ;

	    tjob_ JobPri[ JP$Run ];		! to insert in active list ;
	    while ( tjob_ job:Nxt[rjob_tjob] )	!  loop through the list ;
	     do if ( job:Run[njob] < job:Run[tjob] )
		 then done;			!  runtime < job:Run ;

	    job:Nxt[njob]_ tjob;		! current record pointer ;
	    job:Nxt[rjob]_ njob;		!  insert in active list ;

	    job:Ran[njob]_ Rec[ E!RanTime ]_ GetTDT;	! remember ;
	    ModRec( Rec[E!Runtime], memory[Adr] );	! time ;
	    memory[ Adr+E!Rantime ]_ Rec[ E!Rantime ];	! started ;

	    counter_ 0;				! reset counter ;
	    return( job:Seq[njob] );		! return this record number ;

	 end "process queue";
     end "each level of priority";

    if ( Minutes > counter + 15 )
     then begin "15 minute wait"
	counter_ Minutes;
	Loginfo( "Nothing to do, just hanging around" );
     end "15 minute wait";

    return( 0 );				! nothing there to do ;

end "slots available";


Simple procedure CheckRestart;
begin "check restart"

! some work here -- don't do all this in this order if a restart ;

    if not( TestFeature( Inter ) )		! if no interlock ;
     then StartProcessor			!   get right to work ;
     else if ( ClubInit( Seq_ queAct lsh -9 ) )	! can we get interlock? ;
	 then begin "my interlock"
	    QQ( Flags )_ QQ( Flags ) lor QF$Act;
	    QQ( HaveP )_ HaveProcessor_ false;	!  yes, clear flags ;
	 end "my interlock"
	 else begin "not mine"
	    QQ( Flags )_ QQ( Flags ) land lnot QF$Act;
	    if not( !skip! = CBAIL# )		! already allocated ;
	     then LogInfo( "Processor interlock error ("& cvos(!skip!) &")" )
	     else if ( QQ( HaveP ) = Gettab( QQ( Process ), !gtUID ) )
		   then HaveProcessor_ QQ( Process );
	 end "not mine";

    ClearFeature( RSched );			! always clear restart flag ;

end "check restart";


Simple procedure FirstTime;
begin "first time"

    for MyJob_ 1 upto E!RunMax + E!JobMax	! loop thru active area ;
     do begin "reset job slot"

	ReSchedule( MyJob, Sys$Reset );		!  and better reschedule ;

     end "reset job slot";

    QQ( Active )_ QQ( Running )_ 0;		! nobody is active now ;

end "first time";


Simple procedure SecondTime;
begin "scheduler restart"
    integer Act, Run;				! count of active jobs ;

    Act_ Run_ 0;				! clear active job count ;
    for MyJob_ 1 upto E!RunMax			! for each running job ;
     do if ( QRS(MyJob) )
	 then begin "capture jobs"

	if ( QRF(MyJob)   and			! frame and uid's match ;
	     Gettab(QRF(MyJob),!gtUID) = QRU(MyJob) )
	 then begin "graft job"			! this incarnation ok ;

	    Run_ Run + 1;			! increment running jobs ;
	    FrmXct( QRF(MyJob), QQ(Schedule), !foGFT );
	    LogInfo( "Run #" & cvs(QRF(MyJob)) &
		    (if ( !skip! ) then null else " not") & " grafted." );

	 end "graft job"
	 else begin "clear job"			! wrong incarnation here ;
	    LogInfo( "Aborting " & Xvs(MyJob) );
	    Reschedule( MyJob, Sys$Abort );	! clear it out for later ;
	 end "clear job";

     end "capture jobs";

    QQ( Running )_ Run;				! set running total now ;

    for MyJob_ 1 upto E!JobMax			! for each active job ;
     do begin "count active jobs"

	if ( QQS(MyJob) )			! with a valid entry ;
	 then Act_ Act + 1;			! increment the counter ;

     end "count active jobs";

    if ( QQ( Active )_ Act )			! set active total now ;
	 and ( TestFeature( Inter ) )		!  if interlock then ;
     then StartProcessor;			!  maybe start processor ;

end "scheduler restart";


Simple procedure SetupJobTables;
begin "setup job tables"
    Own integer Err;

    JobPri[ JP$Run ]_ null!record;		! clear running schedule ;
    ReBuildQueue;				! Initialize "my" queue ;

    if not( ClubJoin( Seq_queRun lsh -9 ) )	! join the scheduler club ;
     then Exit( "No Club for Scheduler" );	!  if there is one ;
    if not( ClubInit( Seq ) )			! and init the interlock ;
     then Exit( "Scheduler interlock busy ("&cvos(!Skip!)&")" );

    QQ( Flags )_ QQ( Flags ) lor QF$Run;	! have scheduler interlock ;
    QQ( ScheduleUDT )_ GetTDT;			! setup my start time ;
    QQ( Schedule )_ XEXJob;			! remember job number ;
    QQ( HaveS )_ Gettab( XEXJob,!gtUID );	! and universal ident ;
    LogInfo( "** "& DayStamp &" "& RunProg &" "& VersionString &
	     " in frame "& cvs(QQ( Schedule )) &" **" );
    CtyLog( RunProg &" in frame "& cvs(QQ( Schedule )), true );

    if not( ClubJoin( Seq_ queAct lsh -9 ) )	! join club on ACT page ;
     then Exit( "No Club for Processor" );	!  or complain heavily ;

    while ( 0 > !Skip!_ SysTDT )		! waiting for date-time ;
     do if not( !Skip! )			! SysTDT not implemented ;
	 then begin "assume startup"
	    !Skip!_ QQ( ScheduleUDT );		! make believe same ;
	    done;				! as scheduler startup ;
	 end "assume startup"
	 else calli( 1, calli!SLEEP );		! sleep for a second ;

    if not( QQ( System ) ) or			! If never been set or  ;
      ( QQ( System ) < !Skip! )			!  less than current ;
     then FirstTime				!  the system went away ;
     else SecondTime;				!  else was I went away ;

    QQ( System )_ SysTDT;			! set current system up ;
    if not( QQ( System ) )			! if not implemented ;
     then QQ( System )_ QQ( ScheduleUDT );	!  assume current time ;

    NTQEnb;					! enable for system ksys ;
    ESCEnb;					! enable for escapes ;
    if ( TestFeature( Trigger ) )		! using frame interrupts? ;
     then SIAEnb;				! yes, enable them ;

end "setup job tables";


Simple procedure MonitorRunningJobs;
begin "monitor running jobs"

    if ( ECF! or LCF! or FEX! )
     then LogInfo("ECF: "&cvos(ECF!)&" LCF: "&cvos(LCF!)&" FEX: "&cvos(FEX!));

    if ( ECF! or LCF! )
     then for MyJob_ 1 upto E!RunMax
	   do begin "check exit"

	      if ( ( ECF! lor LCF! ) land (1 lsh MyJob) )
	       then begin "stopped job"

		  if ( CommandMode )
		   then begin "job finished"
		      if ( LPHaltF(QRA(MyJob)) )	! check HALT Flag ;
		       then FrontEnd( queRun+(E!Length*(MyJob-1)) )
		       else begin "finish job"
			  Dpb( Done$End, PStap(QRS(MyJob)) );
			  ReSchedule( MyJob );
			  QQ( Running )_ ( 0 max ( QQ( Running )-1 ) );
		       end "finish job";
		   end "job finished"
		   else LogInfo( Xvs(MyJob) & "spurious ECF-LCF:" &
				 cvos( ECF! ) & " " & cvos( LCF! )  );

		  ClrFlg( ECF!, MyJob );
		  ClrFlg( LCF!, MyJob );

	       end "stopped job";

	   end "check exit";

    for MyJob_ 1 upto E!RunMax
     do begin "check running jobs"

	if ( QRS(MyJob) )
	 then begin "have a job"
	    if ( LPStat(QRS(MyJob)) > JS$Sched )
	     then begin "legal to check"
		if not( QRU(MyJob) = Gettab(QRF(MyJob),!gtUID) )
		 then begin "job went away"
		    ReSchedule( MyJob, Sys$Abort );
		    QQ( Running )_ ( 0 max ( QQ( Running ) - 1 ) );
		 end "job went away";
	     end "legal to check";
	 end "have a job";

     end "check running jobs";

 end "monitor running jobs";


Simple procedure MonitorActiveJobs;
for MyJob_ 1 upto E!JobMax	
 do begin "monitor active jobs"

    if ( TestFeature( Inter ) )		! if using interlock ;
	and not( QQ( HaveP ) )		! if there isn't a processor ;
     then StartProcessor;		!  then start one ;

    if ( QQS( MyJob ) )			! something in slot ;
     then begin "active slot"

	case ( LPStat(QQS(MyJob)) )
	 of begin

	[JS$Hold] ;			! hold user request - restart ;

	[JS$Done]			! complete, good termination ;
	if ( LPProc(QQS(MyJob)) = PSD$Restart )
	 then dpb( Hold$Restart, PStap( QQS(MyJob) ) )
	 else begin "complete and reschedule"
	    ! Send Mail here ;
	    ReSchedule( MyJob+E!RunMax );
	    QQ( Active )_ ( 0 max ( QQ( Active )-1 ) );
	 end "complete and reschedule";

	[JS$Abort]			! abort on error condition ;
	 begin "abort and reschedule"
	    ! Send Mail here ;
	    ReSchedule( MyJob+E!RunMax );
	    QQ( Active )_ ( 0 max ( QQ( Active )-1 ) );
	 end "abort and reschedule";

	[else]
	end;

     end "active slot";

    if ( TestFeature( Inter ) ) and	! if checking interlock ;
        not( QQ( Active ) )  and	!  and no activity left ;
	( QQ( HaveP ) )  and		!  we have a processor ;
	( QQ( Process ) )  and		! double check identity card ;
       not( TestFeature( Shut ) )	! and not shutting down ;
     then begin "Kill Processor"

	LogInfo("Cancelling EXECX in frame "& cvj(QQ( Process )) );
	SetFeature( Shut );
	calli( 10, calli!SLEEP );	! sleep for a bit ;

	HaveProcessor_ queAct lsh -9;	! get page number for interlock ;
	calli( location(HaveProcessor), calli!CLBINI );
	HaveProcessor_ false;		! shut it all down ;

     end "Kill Processor";

end "monitor active jobs";


simple procedure EveryMinute;
begin "once a minute"
    integer Time;

    SetTim( Minute );				! reset the "minute" counter ;
    Minutes_ Minutes + 1;			! increment the time ;

    for MyJob_ 1 upto E!RunMax			! for each job ;
     do begin "time checks"

	if ( Time_ QRT( MyJob ) )		!  if a time limit exists ;
	     and ( Time < QQ( Daytime ) )	!   and it has passed ;
	 then SetFlg( OverTime, MyJob );	!  then mark the job ;
	if ( Time_ QRR( MyJob ) )		!  if a TRU limit exists ;
	     and ( Time < GetTru( QRF( MyJob ) ) )
	 then SetFlg( TRU!, MyJob );		!   and passed, mark job ;

     end "time checks";
end "once a minute";


simple procedure DebugOptions;
begin "Debug Options"

    if ( TestFeature( Tell ) )
     then begin "dump table"
	TellAll;			! dump the table ;
	ClearFeature( Tell );		! clear feature flag ;
     end "dump table";

    if ( TestFeature( RSched ) )
     then begin "finish up"

	ClsQue;				! close the queue ;
	if not( RunSlf )		! attempt to run. ;
	 then Exit( "Cannot run self" );

     end "finish up";

end "Debug Options";


simple procedure CleanupJobs;
begin "time to leave"

    for MyJob_ 1 upto E!RunMax		!  for each run job ;
     do if ( QRS( MyJob ) )		!   if there is one ;
	then KillJob( QRF(MyJob) );	!    knock it off   ;

    if ( HaveProcessor ) and		! if I think that I ;
       ( QQ( HaveP ) )  and		!  have a processor ;
       ( QQ( Process ) )		!  check him out..  ;
     then SetFeature( Shut );		!   shut him down   ;

    SetTim( 150 );			! 2 1/2 minutes ;
    while ( QQ( Running ) )		! anything going on ;
     do if not ( TIM! )			! check time limit ;
	 then MonitorRunningJobs	!  check them out   ;
	 else done;

    ESC!_ true;				!  set escape flag  ;

end "time to leave";


!	M A I N   program
;

if not( OpnQue( que$Real lor que$Run lor que$Act ) )	! we're in deep #### ;
 then Exit( "Serious startup error, check job queue" );

SetupJobTables;				! set everything up ;
CheckRestart;				! check restart flags ;
SetTim( Minute );			! set timer for once a minute ;

while not( ESC! )			! while it's ok to proceed ;
 do begin "the system is running"	! *** working loop *** ;

    if ( TIM! )				! check watchdog timer ;
     then EveryMinute;			!  time to look around ;

    if ( QQ( Running ) )		! anything going ;
     then MonitorRunningJobs;		! yes, so check them out ;
    if ( QQ( Active ) )			! anything active ;
     then MonitorActiveJobs;		! yes, monitor activity ;

    if ( Seq_ SlotsAvailable )		! if any slots are available ;
     then begin "check availability"	!  try to schedule it ;

        if ( MyJob > E!RunMax )		! increment proper counter ;
         then QQ( Active )_  QQ( Active )  + 1
         else QQ( Running )_ QQ( Running ) + 1;

     end "check availability";

    if ( NTQ! )				! if notice received ;
     then CleanupJobs;			! clean up stuff now ;

    if ( QQ( Feature ) < 0 )		! if debug flag on ;
     then DebugOptions;			!  then check options ;

    if not( TIM! )			! if not already waiting ;
     then calli( Ldb( point(6,QQ( Feature ),23) ) max 5, calli!SLEEP );

 end "the system is running";

Exit( "Signing off." );			! time to leave ;

end 	"XEXEC" $

    U@=‡