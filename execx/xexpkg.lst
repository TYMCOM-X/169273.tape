
XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 1
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

1	entry
2		Talk,   CTYLog, CvName
3	,	RunSlf, RunJob, OnlyMe
4	,	FrmXct, GetTRU, CChild
5	,	InfNam, InfJob, SetLog
6	,	Logit,  LogWho, LogInfo
7	,	ClubJoin, ClubInit
8	;
9	
10	begin "XEXPKG"
11	
12	require "(SAILIB)SAIL.DEF" source!file;
13	require "(MPL)UUOSYM.DEF"  source!file;
14	require "(CARL)JQUEUE.DEF" source!file;
15	require "(CARL)JQUE.REQ"   source!file;
16	require "(CARL)XEXINT.REQ" source!file;
17	require "(CARL)VMFILE.REQ" source!file;
18	require "(CARL)DAYTIM.REQ" source!file;
19	
20	Define LogTry  = 25;			! times to try logging to file ;
21	Define LogWait = 200;			! ms to wait while trying ;
22	
23	Define !gtRDV = '135, !gtRDI = '136, !gtRFN = '137;
24	Define Gettab(x,y) = { calli( !xwd( (x), (y) ), calli!GETTAB ) };
25	Define !Job(x) = { ( (x) land '7777 ) };
26	
27	Define DayStamp  = { cvSDat( !lh( GetTDT ) ) };		! day  only ;
28	Define TimeStamp = { cvSTim( !rh( GetTDT ) ) };		! time only ;
29	Define DateStamp = { TymDay( GetTDT ) };		! date-time ;
30	
31	Internal string QueLog;			! (SUBMIT)YYMMDD.JQL ;
32	Internal string DebLog;			! (SUBMIT)JDEBUG.LOG ;
33	
34	Own string  WhoAmI;			! Simplify my name ;
35	Own integer OldZone, NewZone;		! Time Zone stuff  ;
36	
37	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 2
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

38	!	Initialization support
39	;
40	
41	internal simple procedure SetLog;
42	begin "set log name"
43	    own string Today;
44	    own integer Month;
45	
46	    Today_ TymDay( GetTDT );
47	    Today_ Today[8 for 2] &
48		   ("0"&cvs( CMonth( Today[4 for 3] ) ))[inf-1 to inf] &
49		   Today[1 for 2];
50	
51	    QueLog_ "(SUBMIT)"& Today &".JQL";
52	    DebLog_ "(SUBMIT)"& Today &".JQD";
53	
54	end "set log name";
55	require SetLog initialization;
56	
57	
58	simple procedure SetZone;		! set timezone to GMT ;
59	begin
60	
61	    NewZone_ OldZone_ Gettab( -1,!gtPRV );
62	    dpb( '20, point( 6,NewZone,7 ) );
63	    calli( NewZone, calli!SETPRV );
64	
65	end;
66	require SetZone initialization;
67	
68	
69	simple procedure SetDetach;		! set NOATTACH ;
70	begin
71	
72	    calli(JP!NAT lor Gettab(-1,!GTPRV), calli!SETPRV);
73	
74	end;
75	require SetDetach initialization;
76	
77	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 3
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

78	
79	Internal Simple procedure Talk( integer j; string s );
80	! ----------------------------------------------------------------------;
81	!									;
82	!	Talk( Job, Text )						;
83	!		Output the specified Text to the port connect to Job	;
84	!		by executing the SETUUO call to talk to a frame.	;
85	!									;
86	! ----------------------------------------------------------------------;
87	begin "Talk"
88	    Own integer Tries;
89	    Define T1=1, T2=2, !stTLK = '10;
90	    Label Talk, TalkOk;
91	
92	    begin!code				! simulate OUTSTR to CTY ;
93		Move	T2,S;			! string byte pointer ;
94		Movei	T1,S;			! addr of byte pointer ;
95		Hrlz	T1,-1(T1);		! setup size variable ;
96		Hrr	T1,J;			!  to job J (0 = CTY ) ;
97		Setom	!skip!;			! set success flag ;
98		Movei	0,300;			! get 300 * 200ms = 60sec
99		Movem	0,Tries;		! clear loop counter ;
100	Talk:	Hrri	0,T1;			! pointer to block ;
101		Hrli	0,!stTLK;		! function to talk ;
102		uuo!SETUUO;			! doit ;
103		  Setzm	!skip!;			! error clear flag ;
104		Skipe	!skip!;			! check error flag ;
105		  jrst	TalkOk;			! all ok, so leave ;
106		Sosg	Tries;			! is any more left ;
107		  jrst	TalkOk;			! no, so leave ;
108		Hrrzi	0,200;			! 200 ms wait ;
109		uuo!HIBER;			! then setup to ;
110		  jfcl;				!  try it all ;
111		jrst	Talk;			!  again ;
112	TalkOk:
113	    end;
114	
115	end "Talk";
116	
117	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 4
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

118	
119	Internal Simple boolean procedure CTYLog( String Text; Boolean Date(false) );
120	! ----------------------------------------------------------------------;
121	!									;
122	!	CTYLog		Routine to print a string on the system CTY	;
123	!			stamped with the current GMT date and time.	;
124	!									;
125	! ----------------------------------------------------------------------;
126	begin
127	
128	    Talk( 0, (if Date			! if date expected ;
129		      then DateStamp		!  today's daytime ;
130		      else TimeStamp		! else -time only- ;
131		      ) &
132		     " GMT " &			! mark it as GMT ;
133		     Text &			! add the message ;
134		     Crlf );			! and crlf ;
135	
136	    if ( !lh(Gettab(-1,!gtLIN)) = !lh(cvsix("TTY")) )
137	     then Talk( calli(0,calli!PJOB), TimeStamp&" GMT "&Text&Crlf );
138	
139	    return( false );
140	
141	end;
142	
143	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 5
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

144	
145	internal simple boolean procedure CChild( integer Cradle );
146	! ----------------------------------------------------------------------;
147	!									;
148	!	CChild		Routine to create a child frame from the	;
149	!			information found at the address Cradle.	;
150	!									;
151	! ----------------------------------------------------------------------;
152	begin "birth procedure"
153	    Own Integer frame!index;
154	    Own safe integer array Frame[E!Runtime:E!License];
155	
156	    Redefine CF!LIC = !bit(16);		! until UUOSYM.DEF is right;
157	    Redefine CF!LOG = 0;		! really !bit(17);
158	
159	    ArrBlt( Frame[E!Runtime+1], memory[Cradle+1], E!License-E!Runtime );
160	    Frame[ E!Runtime ]_ CF!LIC lor CF!LOG lor (E!License - E!Runtime - 1);
161	
162	    start!code				! take a trip to the hospital ;
163		Setom	!skip!;			! skip return;
164		Hrli	1,!foCFM;		! function: create frame;
165		uuo!FRMOP 1,Frame[E!Runtime];	! attempt to bear a child;
166		  Setzm	!skip!;			!   unbearable!;
167		Skipn	!skip!;			! successful birth? ;
168		  Exch	1,!skip!;		! no - swap error & skip ;
169	    end;
170	
171	end "birth procedure";
172	
173	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 6
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

174	
175	simple procedure LogAll( String Name, Text; Integer Tries(LogTry) );
176	! ----------------------------------------------------------------------;
177	!									;
178	!	LogAll		Routine to make an entry to the appropriate	;
179	!			job queue log file.  Automatically adds CRLF	;
180	!			to the end of the message.			;
181	!									;
182	! ----------------------------------------------------------------------;
183	begin "Log Job Info"
184	    Integer Chan, Timer;
185	
186	    for Timer_ 1 upto Tries
187	     do begin "get file"			! get file in $Update ;
188		if ( 0 < Chan_ VMFile(Name, VM$Append lor VM$Update) )
189		 then done;
190		calli( !Xwd(0,LogWait), calli!HIBER );	! sleep a bit ;
191	     end "get file";
192	
193	    if ( Chan leq 0 )				! all ok? ;
194	     then CTYLog( WhoAmI&"No access: "&Name )	! not exactly... ;
195	     else begin "setup file"			! yes! ;
196	
197		VMText( Chan, TimeStamp & WhoAmI &	! write time & who ;
198			      Text & Crlf );		! and text message ;
199		VMFree( Chan );				! release  channel ;
200	
201	     end "setup file";
202	
203	    if ( !lh(Gettab(-1,!gtLIN)) = !lh(cvsix("TTY")) )
204	     then Talk( calli(0,calli!PJOB), TimeStamp&WhoAmI&Text&Crlf );
205	
206	end "Log Job Info";
207	
208	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 7
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

209	
210	internal simple procedure LogWho( String Who );
211	WhoAmI_ " " & Who[1 for 1] & " ";
212	
213	
214	internal simple procedure LogInfo( String Text );
215	LogAll( QueLog, Text, LogTry );
216	
217	
218	internal simple procedure LogIt( Integer Bits; String Text );
219	begin "Log It"
220	
221	    if not( InQue land que$Map )		! non-zero must be open ;
222	     then return;
223	
224	    if not( QQ( Feature ) land QFet$LogAll )	! not logging everything ;
225	     then if not( QQ(Feature) land Bits )	! correct bits enabled? ;
226		   then return;
227	
228	    LogAll( DebLog, Text, LogTry*5 );		! copy the message ;
229	
230	end "Log It";
231	
232	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 8
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

233	
234	internal simple boolean procedure RunSlf;
235	! ----------------------------------------------------------------------;
236	!									;
237	!	RunSlf		Routine to figure out where you are and run	;
238	!			the same program.  This assumes that we are	;
239	!			NOT using a funny extension or were run with	;
240	!			a load, start or execute command.		;
241	!									;
242	! ----------------------------------------------------------------------;
243	begin "run self"
244	    Own Safe Integer Array RunBlock[0:5];
245	
246	    CtyLog( "Restarting by Feature-Test" );
247	
248	    RunBlock[0]_ Gettab( -1, !gtRDV );
249	    RunBlock[1]_ Gettab( -1, !gtRFN );
250	    RunBlock[4]_ Gettab( -1, !gtRDI );
251	
252	    Calli( location(RunBlock[0]), calli!RUN );
253	    if not( !Skip! )
254	     then return(false );
255	
256	end "run self";
257	
258	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 9
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

259	
260	internal simple boolean procedure OnlyMe;
261	begin "only me"
262	    integer Name, PPN, Index, Top, Me;
263	
264	    Me_ calli( 0, calli!PJOB );		! my job number ;
265	    Name_ Gettab( -1,!gtNAM );		! my name ;
266	    PPN_  Gettab( -1,!gtFPN );		! run directory ;
267	    if not( PPN )
268	     then PPN_ Gettab( -1,!gtPPN );	! if none, use my ppn ;
269	
270	    Top_ Gettab( !cnNJB,!gtCNF );	! highest job ;
271	    for Index_ 1 upto Top		! foreach frame ;
272	     do if not( Index = Me )		!  if not me and same program ;
273		 then if ( Name = Gettab( Index,!gtNAM ) ) and
274			 ( PPN  = Gettab( Index,!gtFPN ) ) and
275			 ( Gettab( Index,!gtSTS ) < 0 )
276		       then return( false );	!    and running then return ;
277	
278	    return( true );
279	
280	end "only me";
281	
282	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 10
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

283	
284	internal simple procedure
285	  FrmXct(Integer Frame; Reference Integer Arg; Integer Funct);
286	! ----------------------------------------------------------------------;
287	!									;
288	!	FrmXct		Routine to execute a frame operation.		;
289	!			Results are returned in ac 1 and !skip!		;
290	!									;
291	! ----------------------------------------------------------------------;
292	begin!code
293	
294		Hrrz	1,Frame;		! Get frame number from array ;
295		Hrl	1,Funct;		! Get function ;
296		Movei	2,Arg;			! Get argument address ;
297		Setom	!skip!;			! Set skip return ;
298		uuo!FRMOP  1,(2);		! Do the work ;
299		  Setzm	!skip!;			! OOPS! too bad ;
300	end;
301	
302	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 11
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

303	
304	internal simple boolean procedure RunJob( integer Frame, ASlot; string Par );
305	begin "run job"
306	    preset!with cvsix("SAV"), cvsix("SHR"), cvsix("HGH"),
307			cvsix("SWR"), cvsix("LOW");
308	    own safe integer array EXTS[ 1:5 ];
309	    own safe integer array RunBlock[0:5];
310	    integer C, Chan;
311	    string  TempFile;
312	
313	    RunBlock[0]_ if ( LAPCclF( ASlot ) or	! CCL or parameters? ;
314			      memory[ ASlot+E!Parameters ] )
315			  then !Xwd( 1,5 ) else 5;	! setup run offset+count ;
316	
317	    RunBlock[1]_ memory[ ASlot+E!CmdFile+F!Device ];
318	    RunBlock[2]_ ASlot+E!CmdFile+F!User;
319	    RunBlock[3]_ memory[ ASlot+E!CmdFile+F!Name ];
320	    Runblock[4]_ memory[ ASlot+E!CmdFile+F!Ext ] land (-1 lsh 18);
321	
322	    for C_ 1 upto 5				! for each extension ;
323	     do if ( RunBlock[4] = EXTS[ C ] )		! does it match list? ;
324		  then begin "fix extension"
325		     RunBlock[4]_ 0;			! clear bad extension ;
326		     done;				! fall out of loop ;
327		  end "fix extension";
328	
329	    if ( !lh( RunBlock[0] ) )			! CCL entry expected? ;
330	     then begin "write TMP file"		! create temp file ;
331	
332		TempFile_ cvs(1000+!job( Frame ))[inf-2 to inf] &
333			  (if ( LAPCCLF( ASlot ) )
334			    then cv6str( LAPCCLF( ASlot ) lsh 18 )
335			    else cv6str( RunBlock[3] )[1 for 3]) & ".TMP";
336	
337		if ( 0 < Chan_ VMFile( TempFile, VM$Write ) )
338		 then begin "write parameters"
339	
340		    VMText( Chan, Par & crlf );		! finish text with crlf ;
341	
342		    if not( VMFree( Chan, 0 ) )		! close temp file ;
343		     then begin "bad temp file"
344			VMFree( Chan, -1 );		! won't: try delete ;
345			return( false );		! then return false ;
346		     end "bad temp file";
347	
348		 end "write parameters"
349		 else return( false );
350	
351	     end "write TMP file";
352	
353	    FrmXct( Frame, RunBlock[0], !foRUN );
354	
355	    EcfEnb( Frame );			! enable ECF ;
356	    return( true );			! and return ;
357	
358	end "run job";
359	
360	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 12
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

361	
362	internal simple string procedure CvNAME( integer AUN );
363	! ----------------------------------------------------------------------;
364	!									;
365	!	CvName		Routine to get the username for a specific	;
366	!			AUN by looking up the UFD.  Uses the .chNXT	;
367	!			feature to get a channel and releases it to	;
368	!			preserve outside file/channel integrity.	;
369	!									;
370	! ----------------------------------------------------------------------;
371	begin
372	    integer NotEof, Chan;
373	    Preset!with '17, cvSIX( "DSK   " ), 0;
374	    Own safe integer array Dev[ 0 : 2 ];
375	    Own safe integer array Look[ 0 : !RBUNM+1 ];
376	
377	    Look[ !RBCNT ]_ !RBUNM+1;		! PPN -> User;
378	    Look[ !RBPPN ]_ !Xwd( 1,1 );	! (UFD)  ;
379	    Look[ !RBNAM ]_ AUN;		! [user] ;
380	    Look[ !RBEXT ]_ cvSIX("UFD   ");	! .UFD   ;
381	
382	    Chan_ VMCHan;			! channel to use ;
383	    Chnior( Chan, Dev[ 0 ], !chOPN );	! open DSK ;
384	    If ( NotEof_ !skip! )
385	     then begin
386		Chnior( Chan, Look[!RBCNT], !chLK );
387		NotEof_ !skip!;
388		Chnior( Chan, memory[0], !chREL );
389	     end;
390	
391	    If ( NotEof )
392	     then return( Cv6str(Look[!RBUNM])&Cv6str(Look[!RBUNM+1]) )
393	     else return( Cvos(!lh(aun))&","&Cvos(!rh(aun)) );
394	end;
395	
396	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 13
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

397	
398	internal simple string procedure InfNam( Integer Name );
399	! ----------------------------------------------------------------------;
400	!									;
401	!	InfNam		Routine to return  DEV:(USER)NAME.EXT  as a	;
402	!			string given the memeory address of a block.	;
403	!									;
404	! ----------------------------------------------------------------------;
405	begin "Info File"
406	
407	    return(   Cv6str( memory[ Name + F!Device ] ) & ":" &
408		      "("& Cv6str( memory[ Name + F!User ] ) &
409		      Cv6str( memory[ Name + F!User+1 ] ) & ")" &
410		      Cv6str( memory[ Name + F!Name ] ) &
411		      (if ( memory[ Name + F!Ext] )
412		       then "."&cv6str( memory[ Name + F!Ext ] )[1 for 3]
413		       else "")
414		   );
415	end "Info File";
416	
417	
418	
419	internal simple string procedure InfJob( Integer Name, Status(0) );
420	! ----------------------------------------------------------------------;
421	!									;
422	!	InfJob		Routine to return the job information as a	;
423	!			string given the memeory address of a block.	;
424	!									;
425	! ----------------------------------------------------------------------;
426	begin "Info Job"
427	
428	    return(
429	
430		"Req #" &
431		cvs( if Status
432		      then LPReq( Status )
433		      else LAPReq( Name )
434		    ) &
435	
436		" " &
437	
438		(if LAPLog( Name )
439		 then InfNam( Name + E!LogFile ) & "="
440		 else "") &
441	
442		(if ( LAPRunF( Name ) )
443		 then " Running "
444		 else "") &
445	
446		InfNam( Name + E!CmdFile )
447	
448	    );
449	
450	end "Info Job";
451	
452	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 14
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

453	
454	internal simple boolean procedure ClubJoin( integer Page );
455	! ----------------------------------------------------------------------;
456	!									;
457	!	ClubJoin	Routine to join a club on an existing page	;
458	!			and act like you just joined if you already	;
459	!			belong to the club.				;
460	!									;
461	! ----------------------------------------------------------------------;
462	begin "join club"
463	    integer Err;
464	
465	    Err_ calli( Page, calli!CLBADD );		! join club on a page ;
466	    if not( !Skip! ) and ( Err = CBINC# )	! if already in club  ;
467	     then return( true )			!  act like it worked ;
468	     else return( !Skip! );			!  else return flag.  ;
469	
470	end "join club";
471	
472	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 15
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

473	
474	internal simple boolean pure ClubInit( reference integer Page );
475	! ----------------------------------------------------------------------;
476	!									;
477	!	ClubInit	Routine to try to take the club interlock and	;
478	!			return to the user without waiting.  If the	;
479	!			interlock is unavailable, return false with	;
480	!			the reason for failure in !SKIP!		;
481	!									;
482	! ----------------------------------------------------------------------;
483	begin "init club"
484	    integer Err;
485	
486	    Err_ calli( location(Page), calli!CLBINI );	! get interlock, nowait ;
487	    if not( !Skip! ) and ( Err = CBHIL# )	! already have it? ;
488	     then !Skip!_ true;				! make believe I got it ;
489	
490	    Err Swap !Skip!;				! switch flags ;
491	    return( Err );				! return uuo success ;
492	
493	end "init club";
494	
495	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 16
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

496	
497	internal simple integer procedure GetTRU( Integer Child );
498	! ----------------------------------------------------------------------;
499	!									;
500	!	GetTRU		Routine to return the current TRUs for a child	;
501	!			frame.  On error, 0 is returned.		;
502	!									;
503	! ----------------------------------------------------------------------;
504	begin!code
505	    HRL   1,access(Child);	! Child frame #;
506	    HRRI  1,!GTTRU;		! Double word TRUs;
507	    SETOM !skip!;		! Assume good;
508	    uuo!GETTAB 1,;		! Get TRU's * 10^4;
509	      SETZM !skip!;		! Error?;
510	    DIVI  1,10000;		! Divide to get TRU's;
511	end;
512	
513	

XEXPKG.SAI                      created 02-Dec-86 22:31                                  Page 17
Program Listing                 by CRFSYM %5 at 03-Dec-86 16:59

514	end "XEXPKG";
  {I.—