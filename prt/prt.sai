define  ! = "COMMENT",  !! = "COMMENT";
!! title Module Prt ;
! *************************************************************************
************************************+**************************************
*                                                                         *
*                                                                         *
*				    Prt					  *
*                                                                         *
*                                                                         *
************************************+**************************************


	PRT is a filter for text files which creates in the GFD'd
	directory a .PRT file capable of being handled intelligently
	by half-bright printer-spooler programs like LPQ.


---------------------------------------------------------------------------
			*****  Edit History  *****
---------------------------------------------------------------------------

			    Author:  Ken Dawson

---------------------------------------------------------------------------


	Version 1.00
	(1-January-83:  kmd)

	  Implement the basic program.


---------------------------------------------------------------------------


	<<  Things like "Begin.Parsing" beginning a line
	    fool block-tracing.

	Append mode.

	Append to x.PRT if more than 1 x.y is referenced.

	Read SWITCH.INI file on setup.
	  Default size & mode processing, default output file.

	Bring date stuff from EXTEND into our date module.

	Support command-file mode (@filename).

	Output directly to the terminal makes the TI.Switch make sense.

	Switches.

	Wildcarding in filespecs.
	  See EXTEND's RdWild (or whatever).
	  See also (Ks2020:118)Lptspl.Sai.
	  See also (SNARL:930)Prt2.Sai.

	Error module containing an interface to other DAs, like Files.


**************************************************************************;


begin "Prt"
  require "Sail.Hdr" source!file;

  !version(!ver(0, 6), !edit('237), !date(19-January-1984));

  require "Brktbl.Da" source!file;
  require "Date.Da"   source!file;
  require "Files.Da"  source!file;
  require "Util.Req"  source!file;


! -------------------------------------------------------------------------


		     Output File List management stuff


! ------------------------------------------------------------------------;


  r!p ($File)
    OFL.Head,				! Output File List header;
    OFL.Ptr;				! Output File List pointer;


! ------------------------------------------------------------------------;


  define  T.MARGIN = 3;			! Default Top.Margin;
  define  B.MARGIN = 3;			! Default Bottom.Margin;

  integer Vertical.Bias;		! To adjust for printer top
					  margin settings;

  integer Copy.Tbl;			! Local, switchable Brktbl;

  string  Line;
  string  Line1;

  string  Title.Str;

  integer Page.Count;
  integer SbPage.Count;
  integer PhPage.Count;
  integer Line.Count;

  string  Cr.Date;
  string  Pr.Date;

  boolean LPQ.Flag;		! Set if destination is the LPQ device;
  boolean NLS.Flag;		! Set if destination is NLS paper;
  boolean FF.Flag;
  boolean TOP.Flg;
  boolean Ignore.TOP;
  boolean Wrap.Flg;

  boolean Interpret.Switch;
  boolean TI.Switch;
  boolean Title.Switch;
  boolean Wrap.Switch;
  boolean Block.Trace.Switch;

  integer Brk;			! For use with "scan";

  integer Page.Length;
  integer Header.Length;
  integer Trailer.Length;
  integer Form.Length;
  integer Form.Width;
  integer Page.Width;
  integer Left.Margin;
  integer Right.Margin;
  integer Top.Margin;
  integer Bottom.Margin;
  integer Indent.Level;

  boolean Scope.Begin;
  boolean Scope.End;


! ************************************************************************;
!! title Rtn Initialize ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			      Rtn Initialize				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  procedure Initialize;
  begin "Initialize"
    ttyup(true);
    do.banner("PRT - File Pretty-Print Program");
  end "Initialize";


! ==================================+======================================
==========================================================================;
!! title Rtn Add.Name ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Add.Name				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Place a filename in the Output Filename List.


==========================================================================;


  procedure Add.Name (r!p ($File) File.Ptr);
  begin "Add.Name"
    r!p ($File)
      OFL.Ptr;


    OFL.Ptr _ OFL.Head;

    while (OFL.Ptr) do
    begin
      if (kequ($$Full.Name(File.Ptr), $$Full.Name(OFL.Ptr))) then
	done;

      OFL.Ptr _ $$Next(OFL.Ptr);
    end;

    if (not OFL.Ptr) then
    begin
      OFL.Ptr _ new!record($File);
      $$Next(OFL.Ptr) _ OFL.Head;
      OFL.Head _ OFL.Ptr;

      Copy.File.Record(File.Ptr, OFL.Ptr);
    end
  end "Add.Name";


! ===================================+=====================================
==========================================================================;
!! title Rtn Select.Name ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Select.Name				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  boolean procedure Select.Name (r!p ($File) File.Ptr);
  begin "Select.Name"
    if (not OFL.Head) then
      return(false);

    OFL.Ptr _ OFL.Head;

    while (OFL.Ptr) do
    begin
      if (kequ($$Full.Name(File.Ptr), $$Full.Name(OFL.Ptr))) then
	return(true);

      OFL.Ptr _ $$Next(OFL.Ptr);
    end;

    return(false);
  end "Select.Name";


! ===================================+=====================================
==========================================================================;
!! title Rtn Copy.Text ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			       Rtn Copy.Text				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  procedure Copy.Text (r!p ($File)  Src, Dst);
  begin "Copy.Text"
    define  LM   = <Blanks(Left.Margin)>;
    define  TW   = <8>;					! Tab width;


! ========================================================================;
!! title SubRtn RM ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				 SubRtn RM				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  simple string procedure RM (integer I);
  begin "RM"
    string Tmp;


    Tmp _ null;

    if (Block.Trace.Switch and (Scope.Begin or Scope.End)) then
    begin
      if (not Wrap.Flg) then
	Tmp _ Tmp & " |";

      if (Scope.End) then
	inc(I);

      Tmp _ Tmp & cvs(I) & "|";
    end;

    return(Tmp);
  end "RM";


! ===================================+=====================================
==========================================================================;
!! title SubRtn Title.Line ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			     SubRtn Title.Line				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple boolean procedure Title.Line (string Line);
  begin "Title.Line"
    integer Brk;


    return(
	Line[1 for 1] = "!" and
	(Starts.With(Line _ scan(Line, Blk.Tbl, Brk), "!title") or
	Starts.With(Line, "!!title"))
    );
  end "Title.Line";


! ==================================+======================================
==========================================================================;
!! title SubRtn Do.Trailer ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			     SubRtn Do.Trailer				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Do.Trailer;
  begin "Do.Trailer"
    return(null);
  end "Do.Trailer";


! ==================================+======================================
==========================================================================;
!! title SubRtn Do.Bottom.Margin ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			  SubRtn Do.Bottom.Margin			  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Do.Bottom.Margin;
  begin "Do.Bottom.Margin"
    return(null);
  end "Do.Bottom.Margin";


! ==================================+======================================
==========================================================================;
!! title SubRtn Do.Top.Margin ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			   SubRtn Do.Top.Margin				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Do.Top.Margin;
  begin "Do.Top.Margin"
    string Line1;
    integer I;


    Line1 _ ff;

    for I _ 1 upto (Top.Margin) do
      Line1 _ Line1 & crlf;

    return(Line1);
  end "Do.Top.Margin";


! ==================================+======================================
==========================================================================;
!! title SubRtn Do.Heading ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			     SubRtn Do.Heading				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Do.Heading;
  begin "Do.Heading"
    own integer I;
    own string  Left.Text;
    own string  Right.Text;
    own string  Line1;


    Line1 _ null;

    Right.Text _ "Page " & cvs(Page.Count) & "-" & cvs(SbPage.Count);

    if (PhPage.Count neq Page.Count) then
      Right.Text _ Right.Text & ":" & cvs(PhPage.Count);

    Left.Text _ Cr.Date & $$Full.name(Src);

    I _ Page.Width - (length(Left.Text) + length(Right.Text));
    Line1 _ Line1 & LM & Left.Text & Blanks(I) & Right.Text & crlf;


    Left.Text _ Pr.Date;
    Line1 _ Line1 & LM & Left.Text;

    if (length(Title.Str)) then
    begin
      Right.Text _ Title.Str[1 for (Page.Width - length(Left.Text))];
      I _ Page.Width - (length(Left.Text) + length(Right.Text));
      Line1 _ Line1 & Blanks(I) & Right.Text;
    end;

    Line1 _ Line1 & crlf2;

    return(Line1);
  end "Do.Heading";


! ==================================+======================================
==========================================================================;
!! title SubRtn Top.Of.Page ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			    SubRtn Top.Of.Page				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Top.Of.Page (boolean Foot.Sw);
  begin "Top.Of.Page"
    return
    (
      (
	if (Foot.Sw) then
	  (Do.Trailer & Do.Bottom.Margin)
	else
	  (null)
      ) &

      Do.Top.Margin &
      Do.Heading
    );
  end "Top.Of.Page";


! ==================================+======================================
==========================================================================;
!! title SubRtn Handle.Title ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			    SubRtn Handle.Title				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Handle.Title (reference string Line);
  begin "Handle.Title"
    string Title.Str;


    Title.Str _ Line;

    if (Starts.With(Line, "!!")) then
      Line _ null;

    Scan.Token(Title.Str);
    Scan.Token(Title.Str);

    while ((Title.Str[inf for 1] = sp) or
	(Title.Str[inf for 1] = ";")) do
      Title.Str _ Title.Str[1 for (inf - 1)];

    return(Title.Str);
  end "Handle.Title";


! ==================================+======================================
==========================================================================;
!! title SubRtn Get.Line ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			      SubRtn Get.Line				  =
=                                                                         =
=                                                                         =
===============================+======================================


	! <<  Will loop when buffer overflow causes break, not
	      terminating until lf is found.  Cr is discarded.

	! <<  Put ^J and ^M handling under the Interpret switch.


==========================================================================;


  simple boolean procedure Get.Line (reference string Line);
  begin "Get.Line"
    string Tmp;
    string Tmp1;
    boolean Cr.Seen;

    define  Diff = <((lnot length(Line) land (TW - 1)) + 1)>;

      ! This hack utilizes the fact that the normal tab width is 8.
	It will work for any tab width which is a power of 2, but will
	not work for others;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Line _ null;
    clear(FF.Flag);
    clear(Cr.Seen);
  
    while (true) do
    begin
      Tmp _ input($$Chan(Src), Copy.Tbl);
  
      if ($$Eof(Src)) then
	return(false);

      if (Cr.Seen) then
      begin
	if ($$Brchr(Src) = lf) then
	begin
	  if (not length(Tmp)) then
	  begin
	    inc(Line.Count);
	    done;
	  end
	  else
	  begin
	    Line _ Line & "^M" & Tmp & "^J";
	  end;
	end
	else
	begin
	  Line _ Line & "^M" & Tmp;
	end;
      end
      else
      begin
	Line _ Line & Tmp;

	if($$Brchr(Src) = lf) then
	begin
	  Line _ Line & "^J";
	  continue;
	end;
      end;

      if ($$Brchr(Src) = cr) then
	set(Cr.Seen)
      else
      begin
	clear(Cr.Seen);

	if ($$Brchr(Src) = ht) then
	  Line _ Line & Blanks(Diff)
	else if ($$Brchr(Src) = ff) then
	  set(FF.Flag)
	else if (length(Tmp1 _ Interpret.Char($$Brchr(Src)))) then
	  Line _ Line & Tmp1;
      end;

    end;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Tmp1 _ Line;
    clear(Scope.Begin);
    clear(Scope.End);

    if (Block.Trace.Switch) then
    begin
      string Tok;


      Tok  _ Scan.Token(Tmp1);

      if (kequ(Tok, "begin") or
	  Starts.With(Tok, "start!code") or
	  Starts.With(Tok, "begin!code") or
	  Starts.With(Tok, "quick!code")) then
      begin
	inc(Indent.Level);
	set(Scope.Begin);
      end
      else if (kequ(Tok, "end;") or kequ(Tok, "end")) then
      begin
	dcr(Indent.Level);
	set(Scope.End);
      end;
    end;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    set(Wrap.Flg, (length(Line) > Page.Width));
    return(true);
  end "Get.Line";


! ==================================+======================================
==========================================================================;
!! title SubRtn Wrap.Around ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			    SubRtn Wrap.Around				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  simple string procedure Wrap.Around (reference string Line);
  begin "Wrap.Around"
    string Indent;
    string  Line1;
    string  RMargin;
    integer I;
    integer Tmp;

    define
      WRAP.OFFSET = 6;


    RMargin _ "|";

    Line1  _ Line[1 for Page.Width - 1] & RMargin & RM(Indent.Level) & crlf;
    Line   _ Line[Page.Width to inf];
    Tmp    _ Page.Width - 1 - (Indent.Width(Line1) + WRAP.OFFSET);
    Indent _ Blanks(Page.Width - 1 - (Tmp + 3)) & "->|";

    while (length(Line)) do
    begin
      inc(Line.Count);

      if (Line.Count > Page.Length) then
      begin
	inc(SbPage.Count);
	Line1 _ Line1 & Top.Of.Page(false);
	Line.Count _ 1;
      end;

      if (length(Line) > Tmp) then
      begin
	Line1 _ Line1 &
	    LM &
	    Indent &
	    Line[1 for Tmp] &
	    RMargin &
	    crlf;
	Line  _ Line[(Tmp + 1) to inf];
      end
      else
      begin
	Line1 _ Line1 &
	    LM &
	    Blanks(Page.Width - 1 - (length(Line) + 3)) &
	    "->|" &
	    Line &
	    RMargin;


	Line  _ null;
      end;
    end;

    return(Line1);
  end "Wrap.Around";


! ==================================+======================================
==========================================================================;
!! title Body Copy.Text;
! ========================================================================;


    set(Interpret.Switch);
    set(TI.Switch);
    set(Wrap.Switch);
    set(Block.Trace.Switch);

    Title.Str       _ null;

    Form.Width      _ 85;
    Form.Length     _ 66;
    Header.Length   _ Count.Lfs(Do.Heading);
    Trailer.Length  _ Count.Lfs(Do.Trailer);

    if (LPQ.Flag or NLS.Flag) then
    begin
      Left.Margin   _ 0;
      Right.Margin  _ 10;
    end
    else
    begin
      Left.Margin   _ 5;
      Right.Margin  _ 5;
    end;

    Top.Margin      _ T.MARGIN - Vertical.Bias;
    Bottom.Margin   _ B.MARGIN + Vertical.Bias;

    if (TI.Switch) then
      inc(Bottom.Margin);

    Page.Width _ Form.Width - (Left.Margin + Right.Margin);

    Cr.Date _ "Cre: " & $$Cr.Date(Src) & "  ";
    Pr.Date _ "Prt: " & DateTimeString & "  ";

    Page.Length _ Form.Length -
	(Header.Length + Trailer.Length + Top.Margin + Bottom.Margin);

    clear(Title.Switch);

    Copy.Tbl _
	if (Interpret.Switch) then
	  Int.Tbl
	else
	  Cpy.Tbl;


! ------------------------------------------------------------------------;


    begin
      integer I;


      for I _ 1 step 1 until (Page.Length div 4) do
      begin
	while (Get.Line(Line) and length(Line) = 0) do
	  ;

	if (Title.Line(Line)) then
	begin
	  set(Ignore.TOP);
	  set(Title.Switch);
	  Title.Str _ Handle.Title(Line);
	  done;
	end;
      end;

      useti($$Chan(Src), 1);
    end;

    Page.Count   _ 1;
    SbPage.Count _ 1;
    PhPage.Count _ 1;
    Line.Count   _ 0;

    clear(FF.Flag);
    cprint($$Chan(Dst), Top.Of.Page(false));
    Indent.Level _ 0;


! ------------------------------------------------------------------------;


    while (Get.Line(Line)) do
    begin "Main Loop"
      if (Title.Switch) then
      begin
	if (Title.Line(Line)) then
	begin
	  Title.Str _ Handle.Title(Line);

	  if (Ignore.TOP) then
	  begin
	    clear(FF.Flag);
	    clear(Ignore.TOP);
	  end
	  else
	  begin
	    inc(Page.Count);
	    SbPage.Count _ 1;
	    set(TOP.Flg);
	  end;
	end
	else if (FF.Flag) then
	begin
	  inc(SbPage.Count);
	  set(TOP.Flg);
	end;
      end
      else if (FF.Flag) then
      begin
	inc(Page.Count);
	SbPage.Count _ 1;
	set(TOP.Flg);
      end;
    
      if (not TOP.Flg and Line.Count > Page.Length) then
      begin
	inc(SbPage.Count);
	set(TOP.Flg);
      end;
    
      if (TOP.Flg) then
      begin
	if (FF.Flag) then
	  inc(PhPage.Count);

	cprint($$Chan(Dst), Top.Of.Page(true));
	clear(TOP.Flg);
	Line.Count _ 1;
      end;

      if (Wrap.Switch and Wrap.Flg) then
	cprint($$Chan(Dst), LM, Wrap.Around(Line), crlf)
      else
	cprint($$Chan(Dst), LM, Line, RM(Indent.Level), crlf);
    end "Main Loop";


! ------------------------------------------------------------------------;


    if (TI.Switch and not LPQ.Flag) then
      cprint($$Chan(Dst), ff);
  end "Copy.Text";


! ==================================+======================================
==========================================================================;
!! title Mainline Code;
! **********************************+**************************************
*                                                                         *
*                                                                         *
*			       Mainline Code				  *
*                                                                         *
*                                                                         *
************************************+*************************************;


  begin "Mainline Code"
    string  InFile;
    string  OutFile;
    string  Tmp;
    integer Brk;
    r!p ($File) Src;
    r!p ($File) Dest;


    Initialize;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    while (true) do
    begin
      Tmp _ Prompt.User(crlf & "Vertical Bias (<cr>): ");
      clear(LPQ.Flag);
      clear(NLS.Flag);

      if (length(Tmp) = 0) then
	Vertical.Bias _ 0
      else if (Starts.With(Tmp, "/")) then
	exit
      else if (Starts.With(Tmp, ".")) then
	Vertical.Bias _ T.MARGIN
      else if (Starts.With("NLS", Tmp)) then
      begin
	Vertical.Bias _ 0;
	set(NLS.Flag);
      end
      else if (Starts.With("LPQ", Tmp)) then
      begin
	Vertical.Bias _ T.MARGIN;
	set(LPQ.Flag);
      end
      else if (Starts.With(Tmp, "?") or
	  (not 0 <= cvd(Tmp) <= T.MARGIN)) then
      begin
	print(crlf,
	    ">> Vertical Bias:  (=[0, ", T.MARGIN, "])  (=[<cr>, "".""]).",
		crlf,
	    "   The number of blank lines to remove from the top margin.",
		crlf2,
	    "   (""LPQ"" causes output to be compatible with LPQ device.)",
		crlf2,
	    "   (""NLS"" causes output to be compatible with NLS paper.)",
		crlf2
	);
	continue;
      end
      else if (Is.Numeric(Tmp)) then
	Vertical.Bias _ cvd(Tmp)
      else
	continue;

      print(crlf);
      done;
    end;


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    while (true) do
    begin "Again"
      while (true) do
      begin
	InFile _ scan(InFile _ Prompt.User("Filename: "), Blk.Tbl,
	    Brk);

	if (Starts.With(InFile, "/Q") or (length(InFile) = 0)) then
	  done "Again"
	else
	begin
	  OutFile _ scan(InFile, Ass.Tbl, Brk);

	  if (Brk neq "=" and Brk neq "_") then
	  begin
	    Infile _ OutFile;
	    OutFile _ null;
	  end;

	  if ($$Eof(Src _ Open.File(InFile, $F.INPUT))) then
	    if (F.Error(Src) = FATAL) then
	      done "Again"
	    else
	      continue;
	end;

	done;
      end;

      if (not length(OutFile)) then
	OutFile _ $$Name(Src) & ".PRT";

      if ($$Eof(Dest _ Open.File(OutFile, $F.OUTPUT))) then
	if (F.Error(Dest) = FATAL) then
	  done "Again"
	else
	  continue "Again";

      Copy.Text(Src, Dest);
      Close.File(ALL.FILES);
      print($$Full.Name(Dest), " created.", crlf, crlf);
    end "Again";


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


    Release.Brktbls;
    exit;
  end "Mainline Code";
end "Prt";


! **********************************+**************************************
**************************************************************************;
    #4FDÄ