!! title DA File.IO ;
! :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::::::::::::::::::::::::::::::::::::+::::::::::::::::::::::::::::::::::::::
:                                                                         :
:                                                                         :
:                                File.IO                                  :
:                                                                         :
:                                                                         :
::::::::::::::::::::::::::::::::::::+::::::::::::::::::::::::::::::::::::::


		       TYMCOM-X File I/O handling.


	! <<  The OPEN stuff changes now that we have forward and
	      backward pointers in $File records.

	! <<  Did we ever test the single-file instance of Del.Open?

	! <<  We need a routine to print the contents of a file record.

	! <<  Protection-code support for files.

	! <<  Mode and Direction in I/O modes should be independent.

		  (INPUT, OUTPUT, UPDATE) = Data movement indicator
		  (Yamm I/O modes)	  = Data format   indicator


::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;


  ifc (not declaration($$Brktbls$$Defined)) thenc
    require "Brktbl.Da" source!file;
  endc

    ! Conditional requirement of break tables;



  ifc (not declaration($$Date$$Defined)) thenc
    require "Date.Da" source!file;
  endc

    ! Conditional requirement of date support;



  define
    $$Files$$Defined = true;

    ! This symbol is used throughout the world to avoid multiple
      requirements of this file;


! ------------------------------------------------------------------------;


  define  FATAL     = 0;	! Fatal     error-class designator;
  define  NON.FATAL = 1;	! Non-Fatal error-class designator;


! -------------------------------------------------------------------------


	File Input/Output Mode designators (not necessarily equivalent
	to the PDP-10's notion of I/O modes).


		    For combination by LORing together


- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


  define  $F.INPUT  = 1;	! Input  mode;
  define  $F.OUTPUT = 2;	! Output mode;
  define  $F.UPDATE = 4;	! Update mode;

  define  $F.SETPL  = 8;	! Keep track of Line #, Page # and
				   Sos Line # via SETPL call;


  define
    $F.ASCII         = 0,
    $F.ASCII.LINE    = 1,
    $F.KATAKANA      = 2,
    $F.IMAGE         = 10,
    $F.IMAGE.BINARY  = 13,
    $F.BINARY        = 14,
    $F.DUMP.RECORDS  = 16,
    $F.DUMP          = 17;

    ! Tymcom-X Data Modes;


! -------------------------------------------------------------------------


			  File information record.


- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


  r!c  $File
  (
    string  Full.Name;
    string  Device;
    string  User;
    string  Name;
    string  Ext;
    string  Cr.Date;
    integer Chan;
    integer Mode;
    integer Ibuf;
    integer Obuf;
    integer Count;
    integer Brchr;
    integer Eof;
    integer Linnum;
    integer Pagnum;
    integer Sosnum;
    r!p ($File) Next;
    r!p ($File) Last
  );


  define
    $$Full.Name(X)  = <$File:Full.Name[X]>,
    $$Device(X)     = <$File:Device[X]>,
    $$User(X)       = <$File:User[X]>,
    $$Ext(X)        = <$File:Ext[X]>,
    $$Cr.Date(X)    = <$File:Cr.Date[X]>,
    $$Chan(X)       = <$File:Chan[X]>,
    $$Mode(X)       = <$File:Mode[X]>,
    $$Ibuf(X)       = <$File:Ibuf[X]>,
    $$Obuf(X)       = <$File:Obuf[X]>,
    $$Count(X)      = <$File:Count[X]>,
    $$Brchr(X)      = <$File:Brchr[X]>,
    $$Eof(X)        = <$File:Eof[X]>,
    $$Name(X)       = <$File:Name[X]>,
    $$Pagnum(X)     = <$File:Pagnum[X]>,
    $$Linnum(X)     = <$File:Linnum[X]>,
    $$Sosnum(X)     = <$File:Sosnum[X]>,
    $$Next(X)       = <$File:Next[X]>,
    $$Last(X)       = <$File:Last[X]>;


! -------------------------------------------------------------------------


	Record used to create a list of open files, which can be closed
	either individually, or all at once.


- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


  define  ALL.FILES = <n!r>;	! Argument to Close.File, representing;
				!  all open files;


  r!c  $Open.File
  (
    r!p ($File) Fp;		! File pointer;
    r!p ($Open.File) Nr	! Next record;
  );

  r!p ($Open.File) Open.File.Hdr;


! ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
!! title Rtn Add.Open ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                              Rtn Add.Open                               =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  procedure Add.Open (r!p ($File) Ptr);
  begin "Add.Open"
    r!p ($Open.File) Cur;


    Cur _ new!record($Open.File);
    $Open.File:Nr[Cur] _ Open.File.Hdr;
    Open.File.Hdr _ Cur;
    $Open.File:Fp[Cur] _ Ptr;
  end "Add.Open";


! ==================================+======================================
==========================================================================;
!! title Rtn Del.Open ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                              Rtn Del.Open                               =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  procedure Del.Open (r!p ($File) Ptr);
  begin "Del.Open"
    r!p ($Open.File) Cur;
    r!p ($Open.File) Prev;


    Cur  _ Open.File.Hdr;
    Prev _ n!r;

    while ((Cur neq n!r) and ($Open.File:Fp[Cur] neq Ptr)) do
    begin
      Prev _ Cur;
      Cur  _ $Open.File:Nr[Cur];
    end;

    if (Cur neq n!r) then
      if (Prev neq n!r) then
	$Open.File:Nr[Prev] _ $Open.File:Nr[Cur]
      else
	Open.File.Hdr _ $Open.File:Nr[Cur];
  end "Del.Open";


! ==================================+======================================
!! title Rtn Copy.File.Record ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			   Rtn Copy.File.Record				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Copy the contents of one File Record to another.


==========================================================================;


  procedure Copy.File.Record (r!p ($File) Ptr1, Ptr2);
  begin "Copy.File.Record"
    if (not(Ptr1 and Ptr2)) then
      return;

    $$Full.Name(Ptr2)  _    $$Full.Name(Ptr1);
    $$Device(Ptr2)     _    $$Device(Ptr1);
    $$User(Ptr2)       _    $$User(Ptr1);
    $$Ext(Ptr2)        _    $$Ext(Ptr1);
    $$Cr.Date(Ptr2)    _    $$Cr.Date(Ptr1);
    $$Chan(Ptr2)       _    $$Chan(Ptr1);
    $$Mode(Ptr2)       _    $$Mode(Ptr1);
    $$Ibuf(Ptr2)       _    $$Ibuf(Ptr1);
    $$Obuf(Ptr2)       _    $$Obuf(Ptr1);
    $$Count(Ptr2)      _    $$Count(Ptr1);
    $$Brchr(Ptr2)      _    $$Brchr(Ptr1);
    $$Eof(Ptr2)        _    $$Eof(Ptr1);
    $$Name(Ptr2)       _    $$Name(Ptr1);
    $$Pagnum(Ptr2)     _    $$Pagnum(Ptr1);
    $$Linnum(Ptr2)     _    $$Linnum(Ptr1);
    $$Sosnum(Ptr2)     _    $$Sosnum(Ptr1);
    $$Next(Ptr2)       _    $$Next(Ptr1);
    $$Last(Ptr2)       _    $$Last(Ptr1);
  end "Copy.File.Record";


! ===================================+=====================================
==========================================================================;
!! title Rtn CVUser ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                               Rtn CVUser                                =
=                                                                         =
=                                                                         =
====================================+======================================


	Convert a PPN to a username.  This routine uses the slower of
	the two methods availible for finding the username.  If many
	lookups are anticipated, use the routine CV2User.


==========================================================================;


  simple string procedure CVUser (integer PPN);
  begin "CVUser"
    own integer array LkB[0:'30];
    own integer Chan;
    own integer Eof;


    LkB[0] _ arrinfo(LkB, 2);
    LkB[1] _ calli('16, '41);
    LkB[2] _ PPN;
    LkB[3] _ cvsix("UFD");

    open(Chan _ getchan, "DSK", '17, 0, 0, 0, 0, Eof);

    if (Eof) then
      return(null);

    chnior(Chan, LkB[0], 4);
    release(Chan);
    return(cv6str(LkB['27]) & cv6str(LkB['30]));
  end "CVUser";


! ==================================+======================================
==========================================================================;
!! title Rtn CV2User ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                              Rtn CV2User                                =
=                                                                         =
=                                                                         =
====================================+======================================


	Convert a PPN to a username.  This routine uses the faster of
	the known methods to find the username, and should be used when
	many conversions are anticipated.

	! <<  Recover the cache-logic from the original code in
	      (DWS)stream.sai.


==========================================================================;


  simple string procedure CV2User (integer PPN);
  begin "CV2User"
    own integer DUL;
    own integer DUL!EOF;
    own integer DUL!Block!Number;
    own integer array DUL!Block[0:127];
    own integer i;
    own integer hash;
    own integer lowitem;
    own integer lowvalue;
    own string  username;

  
    DUL _ -1;

    if (DUL < 0) then
    begin
      open(DUL _ getchan, "DSK", '16,0,0,DUL!EOF,DUL!EOF,DUL!EOF);
      lookup(DUL, "DUL.SYS[1,4]", DUL!EOF);
  
      if (DUL!EOF) then
	usererr(0,0,"Can't access (SYS)DUL.SYS");

      DUL!Block!Number _ -1;
    end;
  
    hash _ PPN mod 101 + 1;
    useti(DUL, hash);
  
    if (DUL!EOF) then
      username _ "[" & cvos(lh(PPN)) & "," & cvos(rh(PPN)) & "]"
    else
    begin
      while (true) do
      begin "search DUL"
  	if (hash neq DUL!Block!Number) then
	  arryin(DUL, DUL!Block[0], 128);

	DUL!Block!Number _ hash;
  
	for i _ 0 step 3 until 127 do
	begin "search block"
	  if (DUL!Block[i] = PPN) then
	    done "search DUL"
	  else if (DUL!Block[i] = 0) then
	    done "search DUL"
	  else if (DUL!Block[i] < 0) then
	    done "search block";
	end "search block";
  
	hash _ rh(DUL!Block[i]) + 1;
	useti(DUL, hash);
      end "search DUL";

      if (DUL!Block[i]) then
	username _ cv6str(DUL!Block[i+1]) & cv6str(DUL!Block[i+2])
      else
	username _ "[" & cvos(lh(PPN)) & "," & cvos(rh(PPN)) & "]";
    end;

    return(username);
  end "CV2User";


! ==================================+======================================
==========================================================================;
!! title Rtn Parse ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                               Rtn Parse                                 =
=                                                                         =
=                                                                         =
====================================+======================================


	Scans the filespec and breaks it into Device, Username,
	Filename, and Extension.  If the filespec uses a PPN instead of
	the username, the latter is looked up.


==========================================================================;


  procedure Parse (string Fnam;
      reference string Device, User, Name, Ext);

  begin "Parse"
    string  Tmp;
    integer Xt;
    integer Brk;
    integer Adr.PPN;


    Fnam _ scan(Fnam, Blk.Tbl, Brk);

      !  << This is a temporary hack to see if removing white space will
	    improve the outcome of our parse;

    Tmp _ Fnam;

    Device _ scan(Fnam, Fn.Tbl, Brk);

    ! <<  might scan off a PPN at this point;

    if (Brk neq ":") then
    begin
      Device _ "DSK";
      Fnam _ Tmp;
    end;

    Name _ cv6str(cvfil(Fnam, Xt, Adr.PPN));
    Ext  _ cv6str(Xt)[1 for 3];

    if (not lh(Adr.PPN) and rh(Adr.PPN)) then
      User _ cv6str(memory[Adr.PPN, integer]) &
	  cv6str(memory[Adr.PPN + 1, integer])
    else if (Adr.PPN = 0) then
      User _ null
    else
      User _ CVUser(Adr.PPN);

!    print("..file:      ", Fnam, crlf);
!    print("..device:    ", Device, crlf);
!    print("..user:      ", User, crlf);
!    print("..Adr.PPN:   ", cvos(Adr.PPN), crlf);
!    print("..name:      ", Name, crlf);
!    print("..extension: ", Ext, crlf);
  end "Parse";


! ==================================+======================================
==========================================================================;
!! title Rtn Open.File ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                             Rtn Open.File                               =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  r!p ($File) procedure Open.File (string Fnam; integer Mode);
  begin "Open.File"
    r!p ($File) Q;
    string  T;
    integer Brk;
    integer M;
    integer Ibuf;
    integer Obuf;
    integer array SLB[0:5];

    define  $F.BUFS  = 4;		! Number of buffers for I/O;
    define  $F.COUNT = 200;		! Maximum line length;


    Q _ new!record($File);
    Ibuf _ Obuf _ 0;

    if (Mode land $F.INPUT) then
    begin
      Ibuf _ $F.BUFS;
      M _ 0;
      $$Count(Q) _ $F.COUNT;
    end
    else if (Mode land $F.OUTPUT) then
    begin
      Obuf _ $F.BUFS;
      M _ 0;
      $$Count(Q) _ 0;
    end
    else if(Mode land $F.UPDATE) then
    begin
      Ibuf _ Obuf _ 1;
      M _ $$Count(Q) _ 0;
    end;

    Parse(Fnam, $$Device(Q), $$User(Q), $$Name(Q), $$Ext(Q));

    if (length($$User(Q))) then
      $$Full.Name(Q) _ "(" & $$User(Q) & ")"
    else
      $$Full.Name(Q) _ null;

    $$Full.Name(Q) _
	$$Full.Name(Q) & $$Name(Q) &
	(if (length($$Ext(Q))) then
	  ("." & $$Ext(Q))
	else
	  (null));

    $$Chan(Q)   _ getchan;
    $$Mode(Q)   _ M;
    $$Ibuf(Q)   _ Ibuf;
    $$Obuf(Q)   _ Obuf;

    open($$Chan(Q),
	$$Device(Q),
	$$Mode(Q),
	$$Ibuf(Q),
	$$Obuf(Q),
	$$Count(Q),
	$$Brchr(Q),
	$$Eof(Q) _ true
    );

      ! <<  Eof indicates bad device;

    if (Mode land $F.SETPL) then
      setpl($$Chan(Q), $$Linnum(Q), $$Pagnum(Q), $$Sosnum(Q));

    if (not $$Eof(Q)) then
      if (Ibuf) then
	lookup($$Chan(Q),
	    $$Full.Name(Q),
	    $$Eof(Q) _ true);

	! <<  Eof indicates lookup failure;

      if (Obuf) then
	enter($$Chan(Q),
	    $$Full.Name(Q),
	    $$Eof(Q) _ true);

	! <<  Eof indicates enter failure;

    if (Mode land $F.SETPL) then
      $$Pagnum(Q) _ 1;

    if ($$Eof(Q)) then
      release($$Chan(Q))
    else
    begin			! <<  What does this do for output files?;
      integer Day;
      integer Time;


      fileinfo(SLB);

      Day  _ (((SLB[1] land '140000) lsh -2) lor (SLB[2] land '007777));
      Time _ ((SLB[2] land !xwd('000037, '770000)) lsh -12);

      $$Cr.Date(Q) _ DayTime(Day, Time);
      Add.Open(Q);
    end;

    return(Q);
  end "Open.File";


! ==================================+======================================
==========================================================================;
!! title Rtn Close.File ;
! ====================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                             Rtn Close.File                              =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  procedure Close.File (r!p ($File) Ptr);
  begin "Close.File"
    if (Ptr = ALL.FILES) then
      while (Open.File.Hdr neq n!r) do
      begin
	release($$Chan($Open.File:Fp[Open.File.Hdr]));
	Open.File.Hdr _ $Open.File:Nr[Open.File.Hdr];
      end
    else
    begin
      Del.Open(Ptr);
      release($$Chan(Ptr));
    end;
  end "Close.File";


! ==================================+======================================
==========================================================================;
!! title Rtn F.Error ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                              Rtn F.Error                                =
=                                                                         =
=                                                                         =
====================================+======================================


	! <<  This could be much smarter.


==========================================================================;


  integer procedure F.Error (r!p ($File) Ptr);
  begin "F.Error"
    integer Tmp;


    Tmp _ $$Eof(Ptr);

    print(">> ");

    if (Tmp = -1) then
      print("Device """, $$Device(Ptr), ":"" not availible.")
    else
    begin
      print (case (0 max rh(Tmp) min 4) of
	  ("No Such File ",
	  "Illegal PPN ",
	  "Protection ",
	  "Busy ",
	  "???? "));
      print("""", $$Full.Name(Ptr), """.");
    end;

    print(crlf2);
    return(NON.FATAL);
  end "F.Error";


! ==================================+======================================
==========================================================================;
!! title Rtn Input.Token ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			      Rtn Input.Token				  =
=                                                                         =
=                                                                         =
====================================+======================================


	! <<  What about returning Brk?


==========================================================================;


  string procedure Input.Token (r!p ($File) Ptr);
  begin "Input.Token"
    return(input($$Chan(Ptr), Tok.Tbl));
  end "Input.Token";


! ==================================+======================================
==========================================================================;

! ::::::::::::::::::::::::::::::::::+::::::::::::::::::::::::::::::::::::::
				DA File.IO
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
M>jç