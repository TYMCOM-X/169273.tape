begin "READ"
Integer sixcnt;
Integer Procedure XCLEAR(Value Integer int!string;
			 Reference Integer ret!string);
begin "XCLEAR"
	Preload!with	'003777777777, '774000000000, -29,
			'774017777777, '003760000000, -22,
			'777760077777, '000017700000, -15,
			'777777700377, '000000077400,  -8,
			'777777777400, '000000000376,  -1;
	Own Integer Array mask!s[1:5,1:3];

	Integer count, mask!it;
	ret!string _ int!string;
	If ret!string land 1 then ret!string _ 0 else
	For count _ 1 step 1 while count < 6
	    Do begin "IF LOOP"
		Integer item!1, item!2, item!3;
		item!1 _ ret!string land mask!s[count,2];
		item!3 _ item!2; comment last character!!!;
		item!2 _ item!1 lsh mask!s[count,3];
comment		If item!2 = '12 and not (item!3 = '15 or item!3 = '12)
		   then item!2 _ '177; comment if lf not after cr ignore it;
		Case item!2 of begin
		     [1] [2] [3] [4] [5] [6] [7] [8] [11] [12]
		     [14] [15] [16] [17] [18] [19] [20] [21] [22]
		     [23] [24] [25] [26] [27] [28] [29] [30] [31]
		     [36] [37] [38] [43] [61] [96] [123] [125] [126] [127]
		   Begin "CLEAR STRING"
			 Out(3,cv6str(ret!string)&" ");
			 sixcnt_sixcnt+1;If sixcnt > 9 then begin
					    sixcnt_0;Out(3,'15&'12) end;
			 ret!string _ 0;
			 count _ 6;
			 end "CLEAR STRING" end;
		end "IF LOOP";
	end "XCLEAR";
	Integer eof, secret!word, fl;
	String  indev, outdev, infile, outfile;
	Outstr("What file for INPUT? ");infile _ inchwl;
	Outstr("What file for OUTPUT? ");outfile _ inchwl;
	DEFINE CRLF = "('15&'12)", TAB = "'11";
	Open(1,"DSK",'10,'200000004,0,0,0,eof);
	Open(2,"DSK",'0,0,2,0,0,0);
	Open(3,"DSK",'0,0,2,0,0,0);Enter(3,"read.six",0);
	Lookup(1,infile,fl);Print(fl,CRLF);
	Enter(2,outfile,0);
	While not eof Do begin "READING FILE"
		Integer word!new, word!count;
		XCLEAR(Wordin(1),word!new);
		If    word!new land '777777777777 = '440000000000
		   or word!new land '777777777777 = '700000000000
		   or word!new land '777777777777 = '414000000000
		   or word!new land '777777777777 = '340000000000
		   or word!new land '777777777777 = '540000000000
		   or word!new land '777777777777 = '550000000000
		   or word!new land '777777777777 = '315004020100
		   or word!new land '777777777777 = '415004020100
		   or word!new land '777777777777 = '341004020100
		   or word!new land '777760000000 = '441500000000
		   or word!new land '777760000000 = '261540000000
		   or word!new land '777777700000 = '276657000000
		   then word!new _ 0;
		If word!new then begin "CHECK OUTPUT"
		   If word!count > 0 then Out(2,CRLF);
		   Out(2,cvstr(word!new)&" ");
		   word!count _ 0;
		   end "CHECK OUTPUT"
		Else word!count _ word!count + 1;
		end "READING FILE";
	Close(1);Release(1);Close(2);Release(2);
	Close(3);release(3);
	end "READ";
