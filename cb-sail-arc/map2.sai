begin
  require "(sailib)sail.def" source!file;

  integer map, text, eof, i,j,k, word, wordcount, bits;
  string UnpackedFileName, PackedFileName, Line;
  integer B!T, B!C;
  string array MapLine[ 1 : 60 ];
  integer CityCount;

  procedure WriteWord( integer word );
    begin
      if ( WordCount mod 128 = 0 ) then
        begin
	  integer i;
	  i := WordCount div 128;
	  if ( i = 0 ) then
	    wordout( map, '1000000200 )
	  else if ( i = 1 ) then
	    wordout( map, '2000000200 )
	  else
	    wordout( map, '2000000057 );
	  WordCount := WordCount + 1;
	end;
      wordout( map, word );
      Wordcount := WordCount + 1;
    end;

  print( "[Text] Map:   " );  UnpackedFileName := inchwl;
  open( text := getchan, "dsk", 0,2,0,200,0, eof );
  lookup( text, UnpackedFileName, eof );
  if ( eof ) then usererr( 0,0,"no file" );
  setbreak( B!T := getbreak, '12, '15, "INS" );

  print( "[Packed] Map: " );  PackedFileName := inchwl;
  open( map := getchan, "dsk", '14,0,2,0,0, eof );
  enter( map, PackedFileName, eof );
  if ( eof ) then usererr( 0,0,"cant enter file" );
  WordCount := 0;

  arrclr( MapLine, NULL );
  for i := 1 upto 100 do
    begin
      Line := input( text, B!T );
      if ( length( Line ) < 60 ) then usererr( 0,0,"bad line #"&cvs(i));
      for j := 1 upto 60 do
	MapLine[j] := MapLine[j] & lop( Line );
    end;
    ! write the map back out ;

    CityCount := 0;

    for i := 1 upto 60 do
      for j := 1 upto 5 do
	begin
	  word := 0;
	  for k := 20 downto 1 do
	    begin
	      word := word * 3;
	      if ( MapLine[i][k for 1] = "." ) then
		begin end
	      else if ( MapLine[i][k for 1] = "+" ) then
		word := word + 1
	      else if ( MapLine[i][k for 1] = "*" ) then
		begin
		  word := word + 2;
		  CityCount := CityCount + 1;
		end
	      else
		begin
		  print( "i,j,k=",i,",",j,",",k,crlf );
		  usererr( 0,0,"bad char in map: '"&cvos(MapLine[i]));
		end;
	    end;
	  for k := 1 upto 20 do lop( MapLine[i] );
	  WriteWord( word );
	end;
    WriteWord( '3000000460 );
    close( map );
    print( "There are ",CityCount," cities on this map", crlf );
    if ( CityCount > 70 ) then
      print( "That's ",(CityCount-70)," more than EMPIRE can handle",crlf);
end;
