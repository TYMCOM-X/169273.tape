entry Prompt.User, Substr, Starts.With, Blanks, Indent.Width, Count.Lfs,
      Scan.Token, Is.Numeric, Deposit.Str, Clean, Ask.User, Form.Str,
      Ctr.Str, Rpt.Str, Ceiling, Contains ;

define ! = "comment", !! = "comment";
!! title Module Util ;
! *************************************************************************
************************************+**************************************
*                                                                         *
*                                                                         *
*                                 Util                                    *
*                                                                         *
*                                                                         *
************************************+**************************************


	A set of generally usable procedures and functions which should
	be maintained independently.


************************************+**************************************


           !version(!ver('1, '0), !edit('52), !date(1-April-86))

  edit history
     1-Apr-86   v1.0(52)  kmd   Move break tables into using routines.
    26-Mar-86   v1.0(51)  kmd   Redesign Indent.Width, Clean.
    26-Mar-86   v1.0(50)  kmd   Split the system stuff out and
                                consolidate the other stuff here.


**************************************************************************;


begin "Util"
  require "(KLib)Sail.Hdr"   source!file;
  require "(KLib)Util.Req"   source!file;
  require "(KLib)Sys.Req"    source!file;


! ************************************************************************;
!! title Rtn Prompt.User ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                            Rtn Prompt.User                              =
=                                                                         =
=                                                                         =
====================================+======================================


	Prompt the terminal for a response, and return the string
	provided.  If Silently is set, then suppress echo.

	Note that, irrespective of Silently, the prompt will end
	with a newline.


==========================================================================;


  internal simple string procedure Prompt.User (string Prompt;
      boolean Silently(false));

  begin "Prompt.User"
    string
      Str;


    print(Prompt);

    if (Silently) then
      Suppress.Echo;

    Str _ inchwl;

    if (Silently) then
    begin
      Restore.Echo;
      print(crlf);
    end;

    return(Str);
  end "Prompt.User";


! ==================================+======================================
==========================================================================;
!! title Rtn Substr ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=				Rtn Substr				  =
=                                                                         =
=                                                                         =
====================================+======================================


	Return true if, regardless of case, one string is a left-
	substring of the other (with a length >= Min.Len).


==========================================================================;


  internal simple boolean procedure Substr (string Str1, Str2;
      integer Min.Len(0));

  begin "Substr"
    return(length(Str1) and length(Str2) and
	kequ(Str1[1 for (length(Str2) max Min.Len)],
	Str2[1 for (length(Str1) max Min.Len)]));
  end "Substr";


! ==================================+======================================
==========================================================================;
!! title Rtn Starts.With ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=				Rtn Starts.With				  =
=                                                                         =
=                                                                         =
====================================+======================================


	Return true if, regardless of case, Str2 string is a left-
	substring of Str1.


==========================================================================;


  internal simple boolean procedure Starts.With (string Str1, Str2);
  begin "Starts.With"
    return(length(Str1) and length(Str2) and
	kequ(Str1[1 for length(Str2)], Str2));
  end "Starts.With";


! ==================================+======================================
==========================================================================;
!! title Rtn Blanks ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                              Rtn Blanks                                 =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple string procedure Blanks (integer N);
  begin "Blanks"
    own string Blankstr;


    while (length(Blankstr) < N) do
      Blankstr _ Blankstr & " " & Blankstr;

    return(Blankstr[1 for N]);
  end "Blanks";


! ==================================+======================================
==========================================================================;
!! title SubRtn Indent.Width ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                          SubRtn Indent.Width                            =
=                                                                         =
=                                                                         =
====================================+======================================



==========================================================================;


  internal simple integer procedure Indent.Width (string X);
  begin "Indent.Width"
    integer
      C,
      Col;

    Col _ 1;

    while ((C _ lop(X)) = sp or C = ht) do
    begin
      if (C = sp) then
        inc(Col)
      else
        inc(Col, TabWidth(Col));
    end;

    return(Col - 1);
  end "Indent.Width";


! ==================================+======================================
==========================================================================;
!! title Rtn Count.Lfs ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                             Rtn Count.Lfs                               =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple integer procedure Count.Lfs (string Str);
  begin "Count.Lfs"
    integer
      Cpy.Tbl,
      Brk,
      I;


    I _ 0;
    setbreak(Cpy.Tbl  _ getbreak, (crlf & ht & ff), null, "INS");

    while (length(Str)) do
    begin
      scan(Str, Cpy.Tbl, Brk);

      if (Brk = lf) then
	inc(I);
    end;

    relbreak(Cpy.Tbl);
    return(I);
  end "Count.Lfs";


! ==================================+======================================
==========================================================================;
!! title Rtn Scan.Token ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			      Rtn Scan.Token				  =
=                                                                         =
=                                                                         =
====================================+======================================


	! <<  What about returning Brk?


==========================================================================;


  internal simple string procedure Scan.Token (reference string Str);
  begin "Scan.Token"
    integer
      Brk,
      Tok.Tbl;
    string
      Tmp;

    setbreak(Tok.Tbl _ getbreak, (crlf & sp & ht), (crlf & sp & ht), "ISNT");
    Tmp _ scan(Str, Tok.Tbl, Brk);
    relbreak(Tok.Tbl);
    return(Tmp);
  end "Scan.Token";


! ==================================+======================================
==========================================================================;
!! title Rtn Is.Numeric ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                             Rtn Is.Numeric                              =
=                                                                         =
=                                                                         =
====================================+======================================


	! <<  Does not support floating-point numbers.


==========================================================================;


  internal simple boolean procedure Is.Numeric (string Str);
  begin "Is.Numeric"
    boolean
      Tmp;
    integer
      Brk,
      Num.Tbl;

    setbreak(Num.Tbl  _ getbreak, "0123456789", null, "XNR");
    Tmp _ length(Str) and
	length(Str) = length(scan(Str, Num.Tbl, Brk));
    relbreak(Num.Tbl);
    return(Tmp);
  end "Is.Numeric";


! ==================================+======================================
==========================================================================;
!! title Rtn Deposit.Str ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                            Rtn Deposit.Str                              =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple procedure Deposit.Str (string Str; integer Bpt, Cnt(0));
  begin "Deposit.Str"
    if (not Cnt) then
      Cnt _ length(Str);

    while (Cnt) do
    begin
      idpb(lop(Str), Bpt);
      dcr(Cnt);
    end;
  end "Deposit.Str";


! ==================================+======================================
==========================================================================;
!! title Rtn Clean ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=                               Rtn Clean                                 =
=                                                                         =
=                                                                         =
====================================+======================================


	Returns a copy of the argument string with leading and trailing
	control characters removed.


==========================================================================;


  internal simple string procedure Clean (string Str);
  begin "Clean"
    integer
      C,
      I;


    while (Str <= sp) do
      lop(Str);

    I _ length(Str);

    while (Str[I for 1] <= sp) do
      dcr(I);

    return(Str[1 to I]);
  end "Clean";


! ==================================+======================================
==========================================================================;
!! title Rtn Ask.User ;
! =========================================================================
====================================+======================================
=                                                                         =
=                                                                         =
=			       Rtn Ask.User				  =
=                                                                         =
=                                                                         =
====================================+======================================


==========================================================================;


  internal simple boolean procedure Ask.User (string Str);
  begin "Ask.User"
    string Tmp;


    while (true) do
    begin
      if (SubStr(Tmp _ Prompt.User(Str), "Yes")) then
	return(true)
      else if (SubStr(Tmp, "No")) then
	return(false);
    end;
  end "Ask.User";


! ==================================+======================================
==========================================================================;
!! title Rtn Form.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Form.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple string procedure Form.Str (integer X, Width, Digits(0));
  begin "Form.Str"
    string Tmp;
    integer W;
    integer D;


    getformat(W, D);
    setformat(Width, Digits);
    Tmp _ cvs(X);
    setformat(W, D);
    return(Tmp);
  end "Form.Str";


! ===================================+=====================================
==========================================================================;
!! title Rtn Ctr.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Ctr.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return a string centered on a line of specified length.


==========================================================================;


  internal simple string procedure Ctr.Str (string Str; integer Line.Len);
  begin "Ctr.Str"
    return(Blanks((Line.Len - length(Str _ Clean(Str))) % 2) & Str);
  end "Ctr.Str";


! ===================================+=====================================
==========================================================================;
!! title Rtn Rpt.Str ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Rpt.Str				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


==========================================================================;


  internal simple string procedure Rpt.Str (string Str; integer Count);
  begin "Rpt.Str"
    string
      S;

    S _ null;

    while (Count) do
    begin
      appnd(S, Str);
      dcr(Count);
    end;

    return(S);
  end "Rpt.Str";


! ===================================+================================
==========================================================================;
!! title Rtn Ceiling ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Ceiling				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the least integer >= (X/Y).


==========================================================================;


  internal simple integer procedure Ceiling (integer X, Y);
  begin "Ceiling"
    return((X + Y - 1) div Y);
  end "Ceiling";


! ===================================+=====================================
==========================================================================;
!! title Rtn Contains ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=                              Rtn Contains                               =
=                                                                         =
=                                                                         =
=====================================+=====================================


        Look for the presence of the string Tst in the string Src,
        using the appropriate comparison.  We loosely base this code
        on the "kequ" routine from the SAIL runtimes written in FAIL.


=====================================+====================================;

ifcr (FALSE) thenc

        The following is the design of this routine.  It is better
        expressed in C than SAIL:  (Note that the "!=" and "==" operations
        are presumed to properly implement the casematching option
        selected.)

            {
              if ((TstLen = length(Tst)) < 0 ||
                  (SrcLen = length(Src)) < TstLen) then
                return(FALSE);

              SrcPtr = pointer(Src)+1;

              while (SrcLen > 0)
              {
                TstPtr = pointer(Tst)+1;
                TstLen = length(Tst);

                while (SrcLen >= TstLen && (*SrcPtr != *TstPtr))
                {
                  SrcPtr++;
                  SrcLen--;
                }

                if (SrcLen < TstLen)
                  return(FALSE);

                SrvPtr = SrcPtr;
                SrvLen = SrcLen;

                while (TstLen > 0 && (*SrcPtr == *TstPtr))
                {
                  SrcPtr++;
                  TstPtr++;
                  SrcLen--;
                  TstLen--;
                }

                if (TstLen == 0)
                  return(TRUE);

                SrcPtr = SrvPtr+1;
                SrcLen = SrvLen-1;
              }

              return(FALSE);
            }
endc

 
! ========================================================================;


  internal simple boolean procedure Contains (string Src, Tst;
      boolean Exactly(FALSE));
  begin "Contains"
    integer
      SrvLen,
      SrvPtr;

    start!code
      define
        CASEBIT   = '40,
        R1        = '01,
        R2        = '02,
        R3        = '03,
        R4        = '04,
        SrcLen    = '01,
        SrcPtr    = '02,
        TstLen    = '03,
        TstPtr    = '04,
        Ch1       = '13,
        Ch2       = '14,
        MatchCase = '15,
        Sp        = '16,
        P         = '17;

      label
        A1,
        B1,
        B2,
        C1,
        D1,
        D2,
        E1,
        TrueRet,
        FalseRet;


        move      MatchCase, Exactly;
        push      P, R2;
        push      P, R3;
        push      P, R4;

        hrrz      TstLen, -1(Sp);
        skipg     TstLen;
        jrst      FalseRet;
        hrrz      SrcLen, -3(Sp);
        camge     SrcLen, TstLen;
        jrst      FalseRet;
        move      SrcPtr, -2(Sp);
        ibp       SrcPtr;

      A1:
        jumple    SrcLen, FalseRet;
        move      TstPtr, 0(Sp);
        ibp       TstPtr;
        hrrz      TstLen, -1(Sp);

      B1:
        camge     SrcLen, TstLen;
        jrst      C1;
        jumple    SrcLen, C1;
        ldb       Ch1, SrcPtr;
        ldb       Ch2, TstPtr;
        xor       Ch1, Ch2;
        jumpe     Ch1, C1;
        jumpn     MatchCase, B2;
        caie      Ch1, CASEBIT;
        jrst      B2;
        trz       Ch2, CASEBIT;
        cail      Ch2, "A";
        caile     Ch2, "Z";
        skipa;
        jrst      C1;

      B2:
        ibp       SrcPtr;
        soja      SrcLen, B1;
        
      C1:
        camge     SrcLen, TstLen;
        jrst      FalseRet;
        jumple    SrcLen, FalseRet;
        movem     SrcPtr, SrvPtr;
        movem     SrcLen, SrvLen;

      D1:
        jumple    TstLen, E1;
        ldb       Ch1, SrcPtr;
        ldb       Ch2, TstPtr;
        xor       Ch1, Ch2;
        jumpe     Ch1, D2;
        jumpn     MatchCase, E1;
        caie      Ch1, CASEBIT; 
        jrst      E1;
        trz       Ch2, CASEBIT;
        cail      Ch2, "A";
        caile     Ch2, "Z";
        jrst      E1;

      D2:
        ibp       SrcPtr;
        ibp       TstPtr;
        soj       SrcLen, ;
        soja      TstLen, D1;

      E1:
        jumpe     TstLen, TrueRet;
        move      SrcPtr, SrvPtr;
        ibp       SrcPtr;
        move      SrcLen, SrvLen;
        soja      SrcLen, A1;

      FalseRet:
        tdza      R1, R1;

      TrueRet:
        seto      R1, ;

        pop       P, R4;
        pop       P, R3;
        pop       P, R2;
    end;
  end "Contains";


! ===================================+=====================================
==========================================================================;

end "Util";

! **********************************+**************************************
**************************************************************************;
  Y ê