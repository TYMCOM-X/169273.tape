entry P.Date, Get.Date, Is.Leap.Year, Abs.LYs, Leap.Years, Cr.Dec.Date,
      Month.Offset, Cr.Tym.Date, Cv.Tym.Date, Cv.Dec.Date,
      Dc.Date, CvS.Date;
define ! = "comment", !! = "comment";
!! title DA Date ;
! :::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
::::::::::::::::::::::::::::::::::::+::::::::::::::::::::::::::::::::::::::
:                                                                         :
:                                                                         :
:                                  Date                                   :
:                                                                         :
:                                                                         :
::::::::::::::::::::::::::::::::::::+::::::::::::::::::::::::::::::::::::::


		Last Modified: 8-February-1983 4:56pm (kmd)


	Perform a myriad of date functions.

	! <<  Convert to Module format, organize


:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::


	Days and months are in the range [0, n-1] as opposed to [1, n].

	Days of the week are counted from Sunday to Saturday in the
	range [0, 6].

	The beginning of time for Tymcom-X was 1-January-1964
	(a Wednesday, as it happens).

	DEC Date format is the calculated number of days since
	th beginning of time, using months which are all 31 days long.
	(See Rtn Cr.Dec.Date() for the actual computation.)

	Tymshare Standard Date format is the actual number of
	days since the beginning of time.  (See Rtn Cr.Tym.Date() for the
	appropriate computation.)

	The string date formats are as follows:

	    Format DF1:		03-Apr-56	Wed
	    Format DF2:		04/03/56	Wed
	    Format DF3:		03-April-1956	Wednesday

	Full Century format refers to a year designation that is
	fully spelled out (eg, "1964", as opposed to "64").  Most
	year designations are handled in Full Century format.


! ========================================================================;


begin "Date"
  require "(KLib)Sail.Hdr" source!file;


! -------------------------------------------------------------------------

			       Normal Years

! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


  define
    N.Y.Days = 365;				! Days per normal year;

  preset!with
      31, 28, 31,
      30, 31, 30,
      31, 31, 30,
      31, 30, 31
  ;
  
  own integer array N.M.Days   [0:11];		! Length of each month;

  preset!with
        0,  31,  59,
       90, 120, 151,
      181, 212, 243,
      273, 304, 334
  ;

  own integer array N.M.Offset [0:11];		! The number of days in the
						  year before each month;


! -------------------------------------------------------------------------

				Leap Years

! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -


	Leap years occur in a 400-year cycle according to the
	following rule:

	    Every 4th year is a leap year,
	      except for every 100th year, which isn't,
		except for every 400th year, which is.


				 *   *   *


	The proportion of leap years that occur within that cycle is

	    1/4 - 1/100 + 1/400 = 97/400,

	which means that the proportion of non-leap years is 

	    1 - 97/400 = 303/400.


				 *   *   *

	
	The true average length of a year is therefore

	    (97/400) * 366 + (303/400) * 365 = 365.2425.


! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - ;


  define
    L.Y.Days = 366;				! Days per leap year;

  preset!with
      31, 29, 31,
      30, 31, 30,
      31, 31, 30,
      31, 30, 31
  ;
  
  own integer array L.M.Days   [0:11];		! Length of each month;

  preset!with
        0,  31,  60,
       91, 121, 152,
      182, 213, 244,
      274, 305, 335
  ;

  own integer array L.M.Offset [0:11];		! The number of days in the
						  year before each month;


! ------------------------------------------------------------------------;


  define
    DC.ALL    = 0,
    DC.DAY    = 1,
    DC.WKDAY  = 2,
    DC.MONTH  = 3,
    DC.YEAR   = 4;

    ! Components of the date;


! ------------------------------------------------------------------------;


  define
    DF1 = 1				! Date Format #1;,
    DF2 = 2				! Date Format #2;,
    DF3 = 3				! Date Format #3;,

    DATE.SEP.1 = <"-">			! Date Separator #1;,
    DATE.SEP.2 = <"/">			! Date Separator #2;
  ;


  preset!with
      "January", "February", "March",
      "April",   "May",      "June",
      "July",    "August",   "September",
      "October", "November", "December"
  ;

  own safe string array
    Months [0:11];

    ! Note that the substring "Months[i][1 for 3]" is the proper
      3-character abbreviation;


! ------------------------------------------------------------------------;


  preset!with
      "Sunday",    "Monday",   "Tuesday",
      "Wednesday", "Thursday", "Friday",
      "Saturday"
  ;

  own string array Weekday [0:6];

    ! Note that the substring "Weekday[i][1 for 3]" is the proper
      3-character abbreviation;


! ------------------------------------------------------------------------;


  forward internal simple integer procedure Cv.Tym.Date (integer D.Date);


! ::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
!! title Rtn P.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn P.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the specified component of a DEC Date.



==========================================================================;


  internal simple integer procedure P.Date (integer Date, Component);
  begin "P.Date"
    case (Component) of 
    begin
      [DC.YEAR]
	return((Date div (31 * 12)) + 1964);

      [DC.MONTH]
	return((Date div 31) mod 12);

      [DC.DAY]
	return(Date mod 31);

      [DC.WKDAY]
	return((3 + Cv.Tym.Date(Date)) mod 7)

	  ! Since the beginning of time was a Wednesday, the value of
	    DC.WKDAY for that day should be 3;
    end;
  end "P.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Get.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn Get.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Returns the current date in DEC Date format.


==========================================================================;


  internal simple integer procedure Get.Date;
  begin "Get.Date"
    integer
      Date;

    do
      Date _ calli('0, '14)
    until (Date = calli('0, '14));

      ! We loop here to insure that we are not caught at the transition
	to midnight;

    return(Date);
  end "Get.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Is.Leap.Year ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Is.Leap.Year				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Decide whether the specified year is a leap year.
	The input must be in full century format.


==========================================================================;


  internal simple boolean procedure Is.Leap.Year (integer Year);
  begin "Is.Leap.Year"
    return((Year mod 4 = 0 and Year mod 100 neq 0) or (Year mod 400 = 0));
  end "Is.Leap.Year";


! ===================================+=====================================
==========================================================================;
!! title Rtn Abs.LYs ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Abs.LYs				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the number of leap years that have occurred between
	the beginning of positive time (ie, 0 AD) and the beginning
	of the specified year.


==========================================================================;


  internal simple integer procedure Abs.LYs (integer Year);
  begin "Abs.LYs"
    dcr(Year);
    return((Year div 4) - (Year div 100) + (Year div 400));
  end "Abs.LYs";


! ===================================+=====================================
==========================================================================;
!! title Rtn Leap.Years ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Leap.Years				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the number of leap years which have occured between
	the beginning of Year.1 and the beginning of Year.2.


==========================================================================;


  internal simple integer procedure Leap.Years
      (integer Year.2, Year.1(1964));

  begin "Leap.Years"
    return(Abs.LYs(Year.2) - Abs.LYs(Year.1));
  end "Leap.Years";


! ===================================+=====================================
==========================================================================;
!! title Rtn Cr.Dec.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Cr.Dec.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Create a date in DEC Date format.


==========================================================================;


  internal simple integer procedure Cr.Dec.Date (integer Day, Month, Year);
  begin "Cr.Dec.Date"
    return(31 * (12 * (Year - 1964) + Month) + Day);
  end "Cr.Dec.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Month.Offset ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			     Rtn Month.Offset				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the number of days before the beginning of the indicated
	month in the indicated year.


==========================================================================;


  internal simple integer procedure Month.Offset (integer Month, Year);
  begin "Month.Offset"
    return(
	(if (Is.Leap.Year(Year)) then
	  L.M.Offset[Month]
	else
	  N.M.Offset[Month])
    );
  end "Month.Offset";


! ===================================+=====================================
==========================================================================;
!! title Rtn Cr.Tym.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Cr.Tym.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Create a date in Tymshare Standard Date format.


==========================================================================;


  internal simple integer procedure Cr.Tym.Date (integer Day, Month, Year);
  begin "Cr.Tym.Date"
    return(
	(Year - 1964) * N.Y.Days
	  + Leap.Years(Year)
	  + Month.Offset(Month, Year)
	+ Day
    );
  end "Cr.Tym.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Cv.Tym.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Cv.Tym.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Convert DEC Date format to Tymshare Standard Date format.


==========================================================================;


  internal simple integer procedure Cv.Tym.Date (integer Date);
  begin "Cv.Tym.Date"
    return(Cr.Tym.Date(
	P.Date(Date, DC.DAY),
	P.Date(Date, DC.MONTH),
	P.Date(Date, DC.YEAR))
    );
  end "Cv.Tym.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Cv.Dec.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			      Rtn Cv.Dec.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Convert from Tymshare Standard Date format to DEC Date format.


				   Note:

	Days.Per.Month is a minimum (28) to insure that Mo is never
	less than it "should be", although it may be more.


==========================================================================;


  internal simple integer procedure Cv.Dec.Date (integer Date);
  begin "Cv.Dec.Date"
    integer
      Month,
      Day,
      Year,
      Tmp,
      Tmp1;

    define
      Days.Per.Year  = <((303/400) * 365 + (97/400) * 366)>,
      Days.Per.Month = 28;


    Year   _ (Date / Days.Per.Year) + 1964;
    Tmp1 _ Date - Cr.Tym.Date(0, 0, Year);
    Month   _ Tmp1 / Days.Per.Month;

    if (Month < 12) then
    begin
      Tmp _ Month.Offset(Month, Year);

      if (Tmp > Tmp1) then
	dcr(Month);
    end
    else
      Month _ 11;

    Day _ Date - Cr.Tym.Date(0, Month, Year);
    return(Cr.Dec.Date(Day, Month, Year));
  end "Cv.Dec.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn Dc.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=				Rtn Dc.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================



	Dc DEC Date to its constituent components, observing the
	specified format.

	Date.In is in DEC Date format.


=====================================================================;


  internal simple procedure Dc.Date
      (reference string Day.Out, Month.Out, Year.Out;
      integer Date.In, Format(DF1));

  begin "Dc.Date"
    integer
      Digits,
      Width;


    getformat(Width, Digits);
    setformat(-2, 0);

    Day.Out _ cvs(P.Date(Date.In, DC.DAY) + 1);

    case (Format) of
    begin
      [DF1]
	begin	  
	  Month.Out _ Months[P.Date(Date.In, DC.MONTH)][1 for 3];
	  Year.Out _ cvs(P.Date(Date.In, DC.YEAR) - 1900);
	end;

      [DF2]
      begin
	Month.Out _ cvs(P.Date(Date.In, DC.MONTH) + 1);
	Year.Out _ cvs(P.Date(Date.In, DC.YEAR) - 1900);
      end;

      [DF3]
	begin
	  Month.Out _ Months[P.Date(Date.In, DC.MONTH)];
	  Year.Out _ cvs(P.Date(Date.In, DC.YEAR));
	end
    end;

    setformat(Width, Digits);
  end "Dc.Date";


! ===================================+=====================================
==========================================================================;
!! title Rtn CvS.Date ;
! =========================================================================
=====================================+=====================================
=                                                                         =
=                                                                         =
=			       Rtn CvS.Date				  =
=                                                                         =
=                                                                         =
=====================================+=====================================


	Return the string representation of the input date in the
	specified format.

	Date.In is in DEC Date format.


==========================================================================;


  internal simple string procedure CvS.Date (
      integer Date.In, Format(DF1), Component(DC.ALL));

  begin "CvS.Date"
    own string
      Day,
      Month,
      Str,
      Year;


    Dc.Date(Day, Month, Year, Date.In, Format);

    case (Component) of
    begin
      [DC.ALL]
	if (Format = DF1 or Format = DF3) then
	  Str _ Day & DATE.SEP.1 & Month & DATE.SEP.1 & Year
	else if (Format = DF2) then
	  Str _ Month & DATE.SEP.2 & Day & DATE.SEP.2 & Year;

      [DC.DAY]
	Str _ Day;

      [DC.WKDAY]
	begin
	  Str _ Weekday[P.Date(Date.In, DC.WKDAY)];

	  if (Format = DF1 or Format = DF2) then
	    Str _ Str[1 for 3];
	end;

      [DC.MONTH]
	Str _ Month;

      [DC.YEAR]
	Str _ Year
    end;

    return(Str);
  end "CvS.Date";


! ===================================+=====================================
==========================================================================;

end "Date";

! ::::::::::::::::::::::::::::::::::+::::::::::::::::::::::::::::::::::::::
				 DA Date
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
  @(ä