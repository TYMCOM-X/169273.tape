BEGIN "CHARS"

REQUIRE "(SAILIB)IOMODS.HDR" SOURCE!FILE;
REQUIRE "[][]" DELIMITERS;
DEFINE DEBUG(X)= [
	IF DEBSW THEN
	    BEGIN 
		X;
	    END;];
STRING FNAME,TSTR,THISLINE,INHIBIT;
SAFE BOOLEAN ARRAY ARR2[0:127];
BOOLEAN DEBSW,PROBLEMS;
INTEGER TEMP,LINENO,PAGENO,OLDPAGE,TCHAR,I;
let !proc!=procedure;
define procedure="SIMPLE !PROC!";
PROCEDURE PRINTLIN;
IF ARR2[TCHAR]
 then BEGIN "PRINTLIN"
	STRING TEMP;
	IF OLDPAGE=PAGENO THEN TEMP_CRLF ELSE
		BEGIN OLDPAGE_PAGENO;
		      SETFORMAT(0,7);
		      TEMP _ CRLF&"PAGE "&CVS(PAGENO)&CRLF;
		      SETFORMAT(-5,7);
		END;
	TEMP_TEMP&CVS(LINENO)&TAB&THISLINE;
	OUTTTY(TEMP);
	DEBUG([
		OUTSTR(CRLF&"OUT:"&TEMP);
		]);
	TSTR _ NULL;
	PROBLEMS _ TRUE;
      END "PRINTLIN";


PROCEDURE DRYROT;
	USERERR(0,1,"THIS CAN'T HAPPEN HERE.");

BOOLEAN PROCEDURE INSTRING(INTEGER I; STRING S);
BEGIN "INSTRING"
	WHILE LENGTH(S) DO
	     IF LOP(S)=I THEN RETURN(TRUE);
	RETURN(FALSE);
END "INSTRING";

OUTSTR(CRLF&"DEBUG?");
DEBSW _ (TEMP_INCHWL)="Y" or TEMP = "y";
OUTSTR(CRLF&"INPUT FILE NAME =");
PROBLEMS _ FALSE; COMMENT SO FAR NO FUNNY CHARACTERS SEEN;
LINENO _ 0;
PAGENO _ 1;
FNAME _ INCHWL;
SETFORMAT(-5,7);
INITIN(FNAME,TRUE);
OUTSTR(CRLF&"OUTPUT FILE NAME =");
FNAME _ INCHWL;
INITOUT(FNAME);



INHIBIT _ TAB&CR&LF&FF ; COMMENT CHARACTERS WE'RE NOT PROCESSING THIS TIME;
FOR I _ 1 STEP 1 UNTIL '37,'176 DO
     IF not INSTRING(I,INHIBIT) THEN
	ARR2[I]_TRUE;
WHILE not !INEOF DO
BEGIN "LINELOOP"
	LINENO _ LINENO +1;
	THISLINE _ TSTR _ !INTTY;
DEBUG ([
	OUTSTR(CRLF&"SRC:"&THISLINE);
]);
	WHILE LENGTH(TSTR) DO
	   BEGIN "CHARLOOP"
		
		CASE TCHAR_LOP(TSTR) OF
		BEGIN "CASE"
		   [0][TAB][LF][CR] ;
		   [FF] BEGIN;
			    PAGENO _ PAGENO+1;
			    LINENO_1;
			    IF not DEBSW THEN
				BEGIN SETFORMAT(0,7);
				      OUTSTR(CVS(PAGENO)&"  ");
				      SETFORMAT(-5,7);
				END;
			END;
		   ELSE PRINTLIN
		END "CASE";
	END "CHARLOOP";
END "LINELOOP";
COMMENT FINAL CLOSING OF FILE;
ENDIN;
ENDOUT;
IF not PROBLEMS THEN OUTSTR(CRLF&"NO FUNNY CHARACTERS ENCOUNTERED");
OUTSTR(CRLF);
END "CHARS";
