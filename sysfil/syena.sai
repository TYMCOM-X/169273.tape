COMMENT

This program will set SYSTAT license on the frame from which it is run.
User must be an account supervisor, be in the specified GAN, be running
it on the specified system, and give the correct password
;

BEGIN "SysEna"

REQUIRE "<><>" DELIMITERS;

DEFINE  ReqGan = <'5166>,
        ReqSysNo = <"65-">,
        AccountSup = <'20000>,
        SystatLicense = <'40000>,
        NecessaryLicense = <'440000>,
        CorrectPassword = <"food%143">;

DEFINE Exit = <call(0,"EXIT")>,
       Crlf = <('15&'12)>,
       !Xwd(A,B) = <((A lsh 18) lor (B land '777777))>;

String Sysno,Password;
Integer PriWord,Gan;

simple procedure ErrorReturn(String Reason);
BEGIN
Print (Crlf,Reason,crlf);
Exit;
END;

simple procedure Restore.Echo;
BEGIN
  start!code
    hrroi       1,'64;
    auxcal      1,0;
    end;
END;

Sysno_cvastr(calli('11,'41))[2 to 4];
If not equ(Sysno,ReqSysNo) then ErrorReturn("Incorrect System");
PriWord _ calli(!xwd(-1,6),'41);
if (PriWord land AccountSup = 0) then 
  ErrorReturn("You must be an Account Supervisor to run this program.");
Gan _ calli('11,'24) lsh -18;
if Gan neq ReqGan then ErrorReturn("Not Correct Gan");
Outstr("Password: ");
start!code
  hrroi 1,'64;
  auxcal        1,'200;
  end;

if not kequ(CorrectPassword,inchwl) then 
  BEGIN
  Restore.Echo;
  ErrorReturn ("?");
  END;

Restore.Echo;
calli(SystatLicense,-'10);
if ( NecessaryLicense land calli(!xwd(-1,-'20),'41)  = 0) then
ErrorReturn("Program has insufficient license");
exit;

end;

