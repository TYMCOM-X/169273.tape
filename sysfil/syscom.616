	TITLE 	SYSCOM:  SYSTEM COMMAND PROCESSOR
	IF1 <PRINTX SYSCOM>
MAINSW==0		;INDICATES NOT THE MAIN PROGRAM


;  EXTERNAL SUBROUTINES

	EXTERN	CHKONC	;HCECK SPECIAL CHARACTER (SSCAN)
	EXTERN	CPOPJ1	;SKIP RETURN (SSIO)
	EXTERN	CRLF	;OUTPUT CRLF SEQUENCE (SSIO)
	EXTERN	DECP2X	;(SSIO)
	EXTERN	DP7	;DISK PERFORMANCE ENTRY POINT FOR PSP COMMAND (SSDISK)
	EXTERN	DULOUT	;DUL READING SUBROUTINE (SSIO)
	EXTERN	ERREPT	;PRINT NON-DISK ERROR REPORT (SSDSPL)
	EXTERN	FINIS	;FINISH UP SEQUENCE (SYSTAT)
	EXTERN	GA	;TAKE SNAPSHOT OF T.S. SYSTEM (SSIO)
	EXTERN	GETCHR	;GET CHAR. FROM TTY (SSIO)
	EXTERN	GETRDC	;GET RADIX INPUT (SSIO)
	EXTERN	GETRDX	;GET RADIX INPUT (SSIO)
	EXTERN	LUDCHK	;LUD READING SUBROUTINE (SSIO)
	EXTERN	HELPM	;TELLS USER ABOUT HELP (SSHELP)
	EXTERN	MSG	;MESSAGE PRINTING (SSIO)
	EXTERN	MSGDEC	;PRINT MESSAGE, THEN DECIMAL (SSIO)
	EXTERN	RDXN	;SPECIFIC RADIX OUTPUT - FREE FORM (SSIO)
	EXTERN	SIXBP	;PRINT SIXBIT MESSAGE (SSIO)
	EXTERN	SPACE	;PRINT SPACE (SSIO)
	EXTERN	SWLPZ	;PRINT SWAPPING STATISTICS (SSDSPL)
	EXTERN	THISIS	;HELLO MESSAGE PRINTING (SSIO)
	EXTERN	TYO	;PRINTS CHARACTER ON TERMINAL (SSIO)
        EXTERN  NODIOWD ;IOWD FOR READING NODES.MAP (SSLOW)

;  EXTERNAL LOCATIONS

	EXTERN	DBLOCK,DET,DIO,FONE	;(SSLOW)
	EXTERN	INMGAN,INMUUN,IO,JOBN	;(SSLOW)
	EXTERN	LUDOPN,MYGAN,MYJOB	;(SSLOW)
	EXTERN	LASPPN,MYUUN		;(SSLOW)
	EXTERN	PJOBN			;POINTER (SSDSPL)
	EXTERN	PSPSW,QTAB,RUN,SAVCHR	;(SSLOW)
	EXTERN	SAVWD,SPC,SPEC,TIO	;(SSLOW)
	EXTERN	TOTUSE,%NODBU,.PPN,.PRV	;(SSLOW)
	EXTERN	.STS,.TTY,.TYM		;(SSLOW)
	EXTERN	.USRN1,.USRN2		;(SSLOW)
	EXTERN	SVNM1,SVNM2		;(SSLOW)

;  GLOBAL SUBROUTINES

	INTERN	PSPCMD,PSYSNO,SYSNO,UPCMD,UPSW,USECMD
	INTERN	USESW,WHOAMI,WHOCMD,WHOME,WHRCMD

	SUBTTL	WHO COMMAND

WHOCMD:	IFN FTNAME,<
	MOVE	M,[SIXBIT/WHO/]
	SETNAM	M,
	JRST	.+1>
	PUSHJ	P,GA		;TAKE DUMP OF T.S. SYSTEM
	MOVE	WD,SAVWD	;GET CHAR THAT TERM'D CMD
	CAIE	WD,15		;IS THIS A CR ?
	CAIN	WD,12		;OF A LF ?
	JRST	WHORET		;YES, RETURN ALL WHO
	MOVEM	WD,SAVCHR	;SET UP GETCHR
	JRST	WHOIT		;AND GO TO WHO LOOP

WHORET:	MOVN	J,JOBN
	HRLZS	J		;J LH=-#JOBS, RH=JOB NO.
	PUSHJ	P,WHOLOP
	AOBJN	J,.-1		;LOOP-DE-LOOP
CLUT:	PUSHJ	P,GETCHR
	JRST	FINIS
	JRST	.-2		;KEEP ON TRYING

WHOLOP:	MOVE	JS,@.STS	;GET JOB STATUS
	TLNN	JS,JNA		;LOGGED ON ?
	POPJ	P,		;NO-BYE
	SKIPN	@.USRN1		;USER NAME NULL?
	POPJ	P,		;YES-BYE
	HRRZ	N,J
	PUSHJ	P,DECP2X	;ONE DIG AND SPACE OF TWO DIG
	HRRZ	N,J
	MOVEI	CH," "
	CAMN	N,MYJOB		;MY JOB ?
	MOVEI	CH,"*"		;YES
	PUSHJ	P,TYO
	PUSHJ	P,SPACE
	MOVE	A,@.USRN1
	PUSHJ	P,SIXBP
	SKIPE	A,@.USRN2
	PUSHJ	P,SIXBP
IFN FTGFD,<
; NOW, CHECK FOR A GFD (IF PPN OF JOB # PPN OF LUSER NAME)
	PUSH	P,D		;SAVE D
	PUSH	P,J		;SAVE J
	MOVE	D,@.USRN1	;1ST PT USER NAME
	MOVE	J,@.USRN2	;2ND PT USER NAME
	SKIPE	LUDOPN		;IF ZERO, LUD NOT OPEN, ERROR.
	PUSHJ	P,LUDCHK	;LOOK IT UP IN THE LUD
	JRST	NOGFD		;FIORGET IT, NOT IN LUD.
	MOVE	J,(P)		;GET BACK J.
	SKIPE	A,@.PPN		;GET THIS USER'S PPN
	CAMN	A,LASPPN	;DO PPNS MATCH?
	JRST	NOGFD		;YES, NOT IN A GFD.
IFG FTGFD,<
;MUST BE IN MY GAN IN ORDER TO SEE THE GFD
	HLRZ	B,ME		;GET THE PPN OF THIS JOB
	HLRZ	A,@.PPN		;GET PPN OF JOB WE ARE LOOKING AT
	CAME	A,B		;DO OUR GAN'S MATCH ?
	JRST	NOGFD		;NO, FORGET THIS PRINTOUT
>
; IN A GFD, SO GO TO DULOUT TO PRINT THE USER.
	MOVEI	M,[ASCIZ / (/]	;USER NAME IN () IF GFD
	PUSHJ	P,MSG		;TYPE MESSAGE
	PUSHJ	P,DULOUT	;TYPE USER NAME(OR GFD PPN)
	MOVEI	CH,")"		;TYPE A CLOSING )
	PUSHJ	P,TYO		;ON THE OUTPUT DEV.

;HERE WHEN NOT IN A GFD BUT REGS. NEED RESTORING
NOGFD:	POP	P,J		;GET BACK J
	POP	P,D		;GET BACK D
>

IFN FTPLUS,<
;+ HACK: TYPE A + AFTER USER NAME IF A TYMSHARE USER
	MOVE	A,@.PRV		;GET JBTPRV CELL
	MOVEI	M,[ASCIZ / +/]	;GET PLUS SIGN READY FOR ACTION
	TRNE	A,%TYMLC	;TYMSHARE PROP ?
	PUSHJ	P,MSG		;YES--TYPE IT OUT
>; ASSEMBLE ABOVE FOR + AFTER USER ON WHO COMMAND WHEN TYMSHR
	PUSHJ	P,CRLF
	POPJ	P,		;RETURN

WHOIT:	TLNE	F,FL.BRK	;LAST CHAR FOUND ???
	JRST	FINIS		;YES--ALL GONE
; LOOP CHECKING FOR BLANKS ALSO
	PUSHJ	P,GETCHR	;GET CHAR
	 JRST	FINIS		;NO MRE TO GO
	CAIN	WD," "		;A SPACE ?
	JRST	WHOIT		;YES, LOOP...
	MOVEI	R,^D10
	PUSHJ	P,GETRDX	;GET FIRST ARGUMENT
	PUSHJ	P,GETCHR	;RE-EAT CHARACTER
	JFCL
	CAIN	WD,"?"		;ASKING FOR HELP ?
	JRST	WHOHLP		;YES-GIVE IT TO HIM
	CAIE	WD,"*"		;* COMMAND?
	CAIN	WD,"."		;DOT COMMAND ?
	JRST	[JUMPN N,WHOSYN	;MUST BE FIRST THING SCANNED
		PJOB N,		;SET UP N WITH OUR JOB #
		PUSHJ P,GETCHR	;EAT ANOTHER CHARACTER
		JFCL
		JRST .+1]	;DONTINUE
	CAIN	WD,"-"		;RANGE INDICATOR?
	JRST	WHORNG		;YES
	JUMPL	N,WHOSYN	;MUST BE .GT. 0
; CHECK FOR LEGAL NUMBER TERMINATOR
	TLNE	F,FL.BRK	;A BREAK ?
	JRST	WHOIT1		;YES, OK
	CAIN	WD,","		;OR A COMMA ?
	JRST	WHOIT1		;YES, OK
	CAIE	WD," "		;A SPACE ?
	CAIN	WD,15		;OR A CR ?
	JRST	.+2
	JRST	CHKONC		;SYNTAX
WHOIT1:	CAILE	N,MX.JOB	;IN RANGE?
	JRST	WHOSYN		;NO
	MOVE	J,N		;SET UP J
	PUSHJ	P,WHOLOP	;ASK FOR IT
	JRST	WHOIT		;AND GO AGAIN
WHORNG:	HRRZ	D,N		;SAVE FIRST ARGUMENT
	MOVEI	R,^D10
	PUSHJ	P,GETRDC	;GET SECOND ARGUMENT
	PUSHJ	P,GETCHR	;RE-EAT CHARACTER
	JFCL
	CAIN	WD,"?"		;HELP ?
	JRST	WHOHLP		;YES-GIVE
	CAIN	WD,"."
	JRST	[JUMPN N,WHOSYN	;MUST BE 1ST SCANNED
		PJOB N,		;SET UP N
		PUSHJ P,GETCHR	;EAT NEXT CHAR
		JFCL
		JRST .+1]	;CONTINUE
	JUMPL	N,WHOSYN	;MUST BE .GT. 0
; CHECK FOR A LEGAL NUMBER TERMINATOR
	TLNE	F,FL.BRK	;HAVE WE BROKEN ?
	JRST	WHOIT2		;YES, DONE
	CAIN	WD,","		;COMMA OK
	JRST	WHOIT2		;YES
	CAIE	WD," "		;A SPACE ?
	CAIN	WD,15		;OR A CR?
	JRST	.+2
	JRST	CHKONC		;BAD CHAR SCANNED
WHOIT2:	CAILE	N,MX.JOB	;IN RANGE?
	JRST	WHOSYN		;NO
	HRRZ	C,N

WHORND:	CAMGE	C,D		;ARE WE DONE ??
	JRST	WHOIT		;YES--ANOTHER RANGE
	HRRZ	J,D		;GET J
	PUSHJ	P,WHOLOP	;DISH OUT
	AOJA	D,WHORND	;KEEP ON LOOPING

WHOHLP:	MOVEI	M,XHLPMS	;GIVE LUSER HELP
	PUSHJ	P,MSG
	JRST	FINIS

;HERE ON WHO SYNTAX ERROR
WHOSYN:	MOVEI	M,[ASCIZ /
Syntax Error
/]
	PUSHJ	P,MSG
	JRST	HELPM		;AND SAY HELP

	OFF.LIST
XHLPMS:	ASCIZ	/
WHO command lists all users on the system you are authorized
to see.  Arguments to WHO are:

n	Any particular job number (if it is greater than the
	maximum job number the system is allocated for,
	WHO will output a SYNTAX ERROR message).
n1-n2	Any particular positive range of job numbers
	(ie, nn1 must be greater than nn2)

Notes:
  A "*" or "." may be used as a job number to indicate
	your job number (ie, WHO .)
  A "," may be used to separate ranges or single jobs
	(ie, WHO 1, 2-5, 17, 18)
/
	ON.LIST
	SUBTTL	ME COMMAND

;DO A WHO OF MY JOB ONLY
;TWO ENTRY POINTS:
;	JRST	WHOAMI		;DOES A PUSHJ P,GA FIRST.
;	JRST	WHOME		;ASSSUMES GA ALREADY CALLED
; WHEN DONE GOES TO FINIS
WHOAMI:	PUSHJ	P,GA		;GET JOB TABLES BUILT
WHOME:	MOVE	J,MYJOB		;GET MY JOB #
	PUSHJ	P,WHOLOP	;TYPE WHO LINE
	JRST	FINIS		;FINIS
	SUBTTL	UP COMMAND

;SA SYSTEM UP TIME
;TO ENTRY POINTS:
;	JRST	UPCMD		DOES PUSHJ P,GA FIRST
;	JRST	UPSW		;ASSUMES GA ALREADY CALLED
; WHEN DONE GOES TO FINIS
UPCMD:	PUSHJ	P,GA		;BUILD SYSTEM TABLES
UPSW:	PUSHJ	P,THISIS	;SAY WHO WE ARE
	JRST	FINIS		;AND EXIT
	SUBTTL	PSP COMMAND

PSPCMD:	IFN FTNAME,<
	MOVE	M,[SIXBIT/PSP/]
	SETNAM	M,
	JRST	.+1>
	SETOM	PSPSW		;SET PSP SWITCH
	TLNN	F,FL.BRK	;NOSKIP IF END OF LINE
	JRST	[MOVE CH,SAVWD
		MOVEM CH,SAVCHR	;GET UP GETCHR CALL
		JRST PSPHLP]	;AND PREPARE FOR HELP
PSPCM1:	PUSHJ	P,DP7		;CALL DISK ERROR PERFORMER
	PUSHJ	P,SWLPZ		;CALL SWAPPER ERROR PERFORMER
	PUSHJ	P,ERREPT	;CALL SYSTEM ERROR PERFORMER
	JRST	CLUT		;AND FINISH UP
PSPHLP:	PUSHJ	P,GETCHR
	JRST	PSPCM1		;AND GO LIST THINGS OUT
	CAIE	WD,"?"
	JRST	PSPHLP
	MOVEI	M,PHLPMS
	PUSHJ	P,MSG
	JRST	FINIS

	OFF.LIST
PHLPMS:	ASCIZ	/
PSP command Prints System Problems.  Information listed will be
a) All structures on system and the number of available blocks
   on each structure.
b) Within each structure, the units that make up the
   structure (listed by unit name and id) and the errors on
   each unit.
c) Those units on-line but not in any structure (listed by
   unit name and id) and the errors on each unit
d) Swapper errors
e) System problem counters that are non-zero
f) Status of unhappy bits set

For more details see -SYSTAT ?DISK
/
	ON.LIST
	SUBTTL	WHERE COMMAND

WHRCMD:	IFN FTNAME,<
	MOVE	M,[SIXBIT/WHERE/]
	SETNAM	M,
	JRST	.+1>
	PUSHJ	P,GA		;TAKE DUMP OF T.S. SYSTEM
	MOVE	BP,[POINT 6,A]
	SETZB	A,B		;BUILD IN A-B
	MOVEI	R,^D13		;MAX. CHARACTERS ALLOWABLE
	MOVE	CH,SAVWD	;GET LAST CHAR SAVED
        CAIE    CH,","          ;DON'T SAVE COMMA
	MOVEM	CH,SAVCHR	;PICK UP ON GETCHR
; IGNORE ALL LEADING BLANKS IN LUSER NAME
	PUSHJ	P,GETCHR	;GET CHAR
	 JRST	WHR2		;END OF LINE
	CAIN	WD," "		;A SPACE ?
	JRST	.-3		;YES, IGNORE IT
        CAIE    WD,","          ;DON'T SAVE COMMA
	MOVEM	WD,SAVCHR	;NO, SET UP TO BE CAUGHT BY GETCHR
WHR1:	PUSHJ	P,GETCHR	;GET CHARACTER
	JRST	WHR2		;BREAK CHARACTER FOUND
	CAIE	WD,15
	CAIN	WD,12
	JRST	WHR1
	CAIN	WD,"?"		;QUESTION-MARK?
	JRST	WHRHLP		;YES-BYE.
	CAIN	WD,","		;A COMMA?
        JRST    [SKIPE  A
                 EXCH   WD,SAVCHR
                 CAIE   WD,","
                 JRST   WHR2
                 JRST   WHR1]
	CAIN	WD,":"		;DOES HE WANT DEVICE?
	JRST	WHR4		;YES
	JUMPE	WD,.+2
	SUBI	WD,40		;TO SIX BIT WE GO
	SOJL	R,WHR1		;IGNORE AFTER 12 CHARS
	IDPB	WD,BP		;DEPOSIT CHARACTER
	JRST	WHR1		;AND GET NEXT CHARACTER

; HERE WHEN WHERE COMMAND PROCESSING COMPLETED
; CHECK TO SEE IF A COMMA TYPED AFTER ARGUMENT, IF SO,
; SCAN NEXT ARGUMENT
WHR1A:	PUSHJ	P,GETCHR	;GET A CHARACTER
	 JRST	FINIS		;BREAK CHR. DONE
	CAIE	WD,15		;A CR?
	CAIN	WD,12		;OR LF?
	JRST	WHR1A		;YES, IGNORE IT
	CAIN	WD," "		;IGNORE SPACES
	JRST	WHR1A		;AS SO.
	CAIE	WD,","		;A COMMA?
	JRST	CHKONC		;NO, ILLEGAL CHARACTER.
WHR1B:	PUSHJ	P,GETCHR	;GET CHARACTER
	 JRST	FINIS		;BREAK. CHAR, END
	CAIN	WD," "		;SPACE?
	 JRST	WHR1B		;YES, IGNORE IT
	MOVEM	WD,SAVCHR	;NO, SET GETCHR TO RE-EAT
	SETZB	A,B		;SET UP TO RESTART SCAN
	MOVE	BP,[POINT 6,A]
	MOVEI	R,^D13		;CHARS. ALLOWABLE
	PUSHJ	P,CRLF		;RETURN LINE 1 FOR NEW SCAN
	JRST	WHR1		;CONTINUE SCANNING

WHR2:	JUMPN	A,WHR2B		;NULL - ASK AGAIN
	TLZ	F,FL.BRK	;RESET EOF FLAG
	OUTSTR	[ASCIZ /enter name:  /]
	JRST	WHR1		;AND ASK FOR IT AGAIN

;NOW, CONVERT THE LUSER NAME TO A PPN.
;IF WE CAN NOT DO IT, WHERE COMMAND WILL FAIL MISERABLY.
WHR2B:	MOVE	D,A		;D HAS 1ST PT USER NAME
	MOVE	J,B		;J HAS 2ND PT USER NAME
	SKIPE	LUDOPN		;IF ZERO, LUD NOT OPEN, ERROR
	PUSHJ	P,LUDCHK	;CHECK TO SEE IF IN LUD
	JRST	NINLUD		;NOT IN THE LUD, SAY SO(ANGER)
	MOVE	D,LASPPN	;GET THE PPN OF THE USER
	MOVN	J,JOBN		;SET UP J TO HAVE AOBJN LOOP CNTR
	HRLZS	J		;SET UP J AS SO.
	SETZM	FONE		;RESET FOUND FLAG
WHR2A:	CAMN	D,@.PPN		;DO THE PPN'S MATCH?
	JRST	WHRFND		;WE HAVE FOUND A MATCH.
	CAMN	B,@.USRN2	;2ND PART MATCH?
	CAME	A,@.USRN1	;1ST PART USER NAME MATCH?
	JRST	WHRCNT		;NO MATCH, CONTINUE SCAN.
WHRFND:	PUSHJ	P,WHR3		;YES-PRINT THIS AND THAT
WHRCNT:	AOBJN	J,WHR2A		;LOOP BACK
	SKIPE	FONE		;SKIP IF NOT FOUND
	JRST	WHR1A		;FOUND IT
	MOVEI	M,[ASCIZ /
NOT ENTERED/]
	PUSHJ	P,MSG
	JRST	WHR1A

;HERE WHEN THE USER NAME IS NOT FOUND IN THE LUD
NINLUD:	MOVEI	M,[ASCIZ /
User Name does not exist/]
	PUSHJ	P,MSG		;SY SO
	JRST	WHR1A		;ALL DONE...

WHRHLP:	MOVEI	M,WHLPMS
	PUSHJ	P,MSG
	JRST	FINIS

	OFF.LIST
WHLPMS:	ASCIZ	/
WHERE command gives information about users logged on and system
devices.  Arguments to the WHERE command are as follows:

<username>	If user is logged on, will be indicated by job
		number and (if available) tymnet information. If
		user is not logged on, "NOT ENTERED" will be given.
TTYnn:		If PDP-10 terminal "nn" is attached to a job, will be
		indicated by job number and (if available) user name
		and tymnet informaton.  If terminal is not attached,
		"TTY not attached to a job" is given. (The terminal OPR:
		is valid and addresses the PDP-10 operators terminal)
<device>:	If PDP-10 device <device>: is in use by a job, will
		be indicated by job number, and (if available) tymnet
		information, user name and device state (assigned and-or
		inited). "Device does not exist" indicates the
		<device> is not valid.  "Device is detached" indicates
		the device is not in use, but it is assigned to job 0
		which requires license to init or assign.  "Device not
		available" indicates device exists but you cant access it.

Notes:
   A "," may be used to separate more than one <username>, TTY or
	<device>.  They may appear in any order.
   A "*" after a job number output by WHERE indicates your
	job number.
/
	ON.LIST
; SUBROUTINE USED IN WHERE COMMAND
; CALL AS: PUSHJ P,WHR3
; A,B - HAVE 6-BIT FOR USER NAME WE ARE LOOKING AT (FOR GFD
; DETERMINATION)
; J - HAS JOB NO. WE ARE LOOKING AT
; USING (J), TYPES TYMNET INFO FOR THAT JOBN.
; THEN IF A,B DO NOT MATCH @.USRN1 AND @.USRN2
; A GFD IS DECLARED (WITH APPROPRIATE TYPEOUT)
WHR3:	PUSH	P,A		;SAVE A
	PUSH	P,B		;SAVE B
	MOVEM	A,SVNM1	;SAVE 1ST PT USER NAME
	MOVEM	B,SVNM2	;SAVE 2ND PT USER NAME
	MOVEI	M,[ASCIZ /
On Job /]
	PUSHJ	P,MSG
	HRRZ	N,J
	PUSHJ	P,DECP2X	;PRINT JOB NO

; IF THIS IS THE CURRENT USER'S JOB, PRINT A *
	MOVEI	CH,"*"
	HRRZ	N,J		;GET JUST JOB NO. OF COUNTER
	CAMN	N,MYJOB		;REALLY ME
	PUSHJ	P,TYO		;YES, TYPE *

	PUSHJ	P,SPACE		;PRINT SPACE
	MOVE	JS,@.STS	;CHECK DETACHED
	MOVE	BP,@.TTY
	JUMPE	BP,WHR3E	;IF ZERO CANT FIND THIS OUT
	TLNN	BP,-1
	JRST	WHR3D		;DETACHED
	MOVE	INFO,@.TYM
	LDB	NODE,[POINT 6,INFO,27]
	LDB	NODE1,[POINT 6,INFO,19]
	DPB	NODE1,[POINT 6,NODE,29]
        JRST    WRNODE          ;FIND NODE NAME

; SEARCH FOR VALID NODE IN %NODBUFF
; THREE WORD ENTRIES ARE:
;    1: NUMBER OF NODE
;  2,3: NODE NAME IN 7-BIT WITH A NULL
WFNODE: USETI   NOD,1           ;START AT BEGINNING OF FILE
WNNODE: IN      NOD,NODIOWD     ;READ 3 BLOCKS OF FILE
        SKIPA                   ;NO ERRORS
        JRST    WHR3Z           ;IF ERROR, GIVE UP
WRNODE: CAMGE   NODE,%NODBUFF   ;HAVE CORRECT BLOCKS?
        JRST    WFNODE          ;IF NOT, RETURN TO BEGINNING
        CAMLE   NODE,%NODBUFF+^D381 ;CHECK END OF BLOCK
        JRST    WNNODE          ;MUST GET NEXT 3 BLOCKS
	SETZ	N1,		;INDEX
        MOVEI   M,3000          ;SEARCH THROUGH NODBUFF
W1NODE:	CAMN	NODE,%NODBUFF(N1) ;COMPARE?
	JRST	W2NODE		;YES, OKAY.
        CAMG    M,%NODBUFF(N1)  ;END OF SEARCH?
	JRST	WHR3Z		;YES, GIVE UP.
	ADDI	N1,3		;TO NEXT 3-WORD ENTRY
	JRST	W1NODE		;LOOP

; HERE WHEN NODE FOUND
W2NODE:	MOVEI	M,%NODBUFF+1(N1) ; ADR OF NODE NAME
WHR3G:	PUSHJ	P,MSG
	MOVEI	M,[ASCIZ / #/]
	PUSHJ	P,MSG		;PRINT ' T' FOLLOWED BY TTY#
	HRLZ	A,@.TTY		;GET TTY NO.
	PUSHJ	P,SIXBP
	JRST	WHR3E
WHR3D:	MOVEI	M,[ASCIZ /DETACHED/]
	PUSHJ	P,MSG
	JRST	WHR3E

;HERE WHEN NODE NOT FOUND IN THE TABLE
WHR3Z:	MOVEI	M,[ASCIZ /*/]	;PRINT **NODE#**
	PUSHJ	P,MSG		;PRINT IT.
	MOVEI	R,^O10		;OCTAL RADIX
	PUSHJ	P,RDXN	;PRINT NODE #
	MOVEI	M,[ASCIZ /*/]	;SECOND SET OF **
	JRST	WHR3G		;AND FINISH PRINT-OUT

;HERE TO SEE IF THE LUSER IS FROM A GFD
WHR3E:	IFN FTGFD,<
; IF THE USER NAES IN SVNM2 AND SVNM1 DO NOT MATCH THE LOGIN
; USER NAME, THEN THE LUSER IS ON A GFD
	MOVE	A,@.USRN2	;GET 2ND PART USER NAME
	CAME	A,SVNM2		;DO THEY MATCH?
	JRST	ONAGFD		;NO, USER IS ON A GFD
	MOVE	A,@.USRN1	;GET 1ST PART OF USER NAME
	CAMN	A,SVNM1		;2ND PARTS MATCH?
	JRST	NONGFD		;NOT ON A GFD
;HERE WHEN ON A GFD
ONAGFD:	MOVEI	M,[ASCIZ/ (GFD from /]
	PUSHJ	P,MSG		;SAY A GFD WHERE
	MOVE	A,@.USRN1	;GET 1ST PART USER NAME
	PUSHJ		P,SIXBP		;PRINT IT
	SKIPE	A,@.USRN2	;GET 2ND PART
	PUSHJ	P,SIXBP		;PRINT IF NOT 0
	MOVEI	CH,")"		;CLOSING )
	PUSHJ	P,TYO		;TYPE IT
NONGFD:	;HERE WHEN NOT ON A GFD
>
	SETOM	FONE		;SET FOUND ONE FLAG
	POP	P,B
	POP	P,A
	POPJ	P,		;AND RETURN
WHR4:	SETZM	FONE		;RESET FOUND IT FLAG
; NOW, CHECK FOR A LEGAL DEV MODE FOR THIS DEVIE
; M = DEVCHR BITS
; B = BIT BEING CHECKED
; C = COUNTER
	MOVE	M,A		;GET DEV NAME
	CALLI	M,200004	;DO DEVCHR (DEVCHR=LOCAL SYMBOL)
	JUMPE	M,WHR5		;IF ZERO BAD DEV...
	MOVEI	B,1		;BIT BEING CHECKED.
	SETZ	C,		;CURRENT BIT POSITION.
; NOW, TEST THE BITS
WHR4C:	TRNE	M,(B)		;CHECK THE BIT
	JRST	WHR4Y		;IT IS SET, GOT MODE
; NO CHECK. IF C = 17 THE WE HAVE BLOWN IT.
	CAILE	C,17		;WELL ??
	JRST	WHR5		;NO LEGAL DEV MODE, LOSE...
; ALL SEEMS OKAY, SCAN FOR NEXT BIT
	LSH	B,1		; SHIFT TO NEXT BIT
	AOJA	C,WHR4C		;LOOP
; HERE WHEN C = LEGAL DEVICE MODE
WHR4Y:	TLO	C,400000	;SIGN BIT:IGNORE LOG NAMES
	MOVEM	C,SPEC		;SAVE DEVICE MODE
	MOVEM	A,SPEC+1	;PUT DEVNAME OUT
	SETZM	SPEC+2		;NO I-O BUFFERS
	OPEN	5,SPEC		;TRY AND OPEN DEVICE
	JRST	WHR5		;FAILURE
	RELEASE	5,		;RELEASE DEVICE
	HLLZ	M,A		;CHECK DEV NAME FOR TTY:
	CAMN	M,[SIXBIT/TTY/]	;TTY ?
	JRST	WHR4X		;YES, DIF PRINTOUT.
; NOT TTY, OUTPUT MESSAGE THAT DEV IS AVAILABLE
	MOVEI	M,[ASCIZ /
Device is Available/]

; IF THE USER REQUESTED OPR: THEN SAY SOMETHING ELSE
	CAMN	A,[SIXBIT/OPR/]	;DID HE REQ. OPR?
	MOVEI	M,[ASCIZ /
This is the OPR tty/]
	PUSHJ	P,MSG
	JRST	WHR1A		;AND AWAY WE GO

; USER ASKED ABOUT HIS OWN TTY - SAY SO
WHR4X:	MOVEI	M,[ASCIZ /
This is /]
	PUSHJ	P,MSG		;TYPE THIS IS
	PUSHJ	P,SIXBP		;TYPE (A) IN SIXBIT
	JRST	WHR1A		;AND GO AWAY

WHR5:	MOVE	B,A
	CALLI	B,200004	;DON'T USE DEVCHR, LOCAL SYMBOL****
	JUMPN	B,WHR6		;IF ZERO BAD DEV

;IF "TTY" THEN SAY DIF. MESSAGE
	HLRZ	A,A
	CAIN	A,(SIXBIT /TTY/);TTY ?
	JRST	[MOVEI M,[ASCIZ /
TTY not attached to a job/]
		JRST WHR5L]	;SAY NO SUCH TTY
	CAIN	A,(SIXBIT /OPR/);ASKING ABOUT OPR
	JRST	[MOVEI M,[ASCIZ /
OPR not set/]
		JRST WHR5L]	;SAY OPR MISSING.
	MOVEI	M,[ASCIZ /
Device does not Exist/]
WHR5L:	PUSHJ	P,MSG
	JRST	WHR1A

;CHECK IF "OPR" OR "TTY" AND IF SO SAY WHERE HE IS
;
WHR6:	CAME	A,[SIXBIT /OPR   /]	;LOOKING FOR OPR ?
	JRST	WHR6B		;NO.
	MOVE	A,XOPDEV	;OPERATOR DEVICE GET
	GETTAB	A,		;GET A
	SETZ	A,		;CAN'T GET IT
	HLRZ	B,A		;SEE IF IT'S A TTY
	CAIN	B,(SIXBIT /TTY/);IS IT ?
	JRST	WHR6C		;YES
	MOVEI	M,[ASCIZ /
OPR not set/]
	PUSHJ	P,MSG
	JRST	WHR1A

;SEE IF TTY
WHR6B:	HLRZ	B,A
	CAIE	B,(SIXBIT /TTY/);SKIP IF TTY
	JRST	WHR6D		;NOT TTY
WHR6C:	MOVE	D,A		;D=TTY NAME
	MOVE	A,[POINT 6,D,17];CONVERT TO NUMBER (BINARY)
	SETZ	B,		;WHERE TO GO
WHR6E:	ILDB	C,A		;GET CHARACTER
	SUBI	C,'0'		;CONVERT TO BINARY
	JUMPL	C,WHR6F		;JUMP WHEN DONE
	LSH	B,3		;PRETO ACCEPT DIGIT
	IOR	B,C		;MERGE IT IN
	TLNE	A,(77B5)	;SEE IF AT END OF WORD
	JRST	WHR6E		;NO--LOOP FOR MORE

;B NOW HAS NUMBER OF TTY - FIND IT
WHR6F:	HRL	J,B		;LH=TTY #
	HRR	J,XJOBNO	;GET JOB GIVEN TTY
	GETTAB	J,		;GET IT
	SETZ	J,		;OH WELL
	JUMPE	J,WHR7		;TAKE EASY WAY OUT
; NOW, J HAS JOB NO.  A,B MUST HAVE THIS USER'S NAME
	MOVE	A,@.USRN1	;GET 1ST PART
	MOVE	B,@.USRN2	;GET 2ND PART
	PUSHJ	P,WHR3		;SAY WHERE AT
	PUSHJ	P,SPACE
	MOVE	A,@.USRN1
	PUSHJ	P,SIXBP
	SKIPE	A,@.USRN2
	PUSHJ	P,SIXBP
IFN FTPLUS,<
;+ HACK: TYPE A + AFTER USER NAME IF A TYMSHARE USER
	MOVE	A,@.PRV		;GET JBTPRV CELL
	MOVEI	M,[ASCIZ / +/]	;GET PLUS SIGN READY FOR ACTION
	TRNE	A,%TYMLC	;TYPSHARE PROP ?
	PUSHJ	P,MSG		;YES--TYPE IT OUT
>; ASSEMBLE ABOVE FOR + AFTER USER NAME ON WHERE DEV: WHEN TYMUS
	JRST	WHR1A		;ALL DONE (THANKS BE TO GOD)

;NOT TTY - MUST BE NORMAL DEVICE
WHR6D:	PUSHJ	P,SDEV		;SEARCH FOR DEVICE
	JRST	WHR7		;ALL GONE
	MOVEI	D,DBLOCK+3	;D IS INDEX INTO DDB
	MOVE	C,DEVCHR(D)	;GET DEVICE CHARACTERISTICS
	LDB	N,PJOBN
	HRRZ	J,N
	JUMPE	J,WHR8		;IF NULL JOB SAY SO
	MOVEI	M,[ASCIZ /
On Job /]
	PUSHJ	P,MSG
	PUSHJ	P,DECP2X
	PUSHJ	P,SPACE
	MOVE	A,@.USRN1
	PUSHJ	P,SIXBP
	SKIPN	A,@.USRN2
	JRST	.+2
	PUSHJ	P,SIXBP
	PUSHJ	P,SPACE
	MOVE	B,DEVMOD(D)
	MOVEI	M,[ASCIZ /AS/]
	TRNE	B,ASSCON
	PUSHJ	P,MSG
	TRNN	B,ASSPRG
	JRST	WHR6A
	MOVEI	CH,"+"
	TRNE	B,ASSCON
	PUSHJ	P,TYO
	MOVEI	M,[ASCIZ /INIT/]
	PUSHJ	P,MSG
WHR6A:	JRST	WHR1A		;ALL DONE

WHR7:	MOVEI	M,[ASCIZ /
Device not Available/]
	PUSHJ	P,MSG
	JRST	WHR1A

WHR8:	MOVEI	M,[ASCIZ /
Device is Detached/]
	PUSHJ	P,MSG
	JRST	WHR1A

;SEARCH FOR A PARTICULAR DEVICE BY USING CALLI AC,-24
;SKIP RETURN IF FOUND WITH THE DEVICE DATA BLOCK STORED INTO
;DBLOCK, ELSE NOSKIP RETURN
;USES- A = DEVICE NAME WANTED
;DESTROYS- B
;
SDEV:	SETZM	DBLOCK+1	;INITIALLY THIS IS 0
SDEV1:	SETZM	DBLOCK		;FUNCTION- BY NAME(0)
	MOVE	B,[LN.DDB,,DBLOCK]	;WHERE TO GO
	CALLI	B,-24		;GET IT?
	POPJ	P,		;NO--FOO
	SKIPN	DBLOCK+1	;0 = END OF DDB LIST
	POPJ	P,		;END OF LIST--FAILURE RETURN
	CAME	A,DBLOCK+1	;IS IT THE WANTED DEVICE?
	JRST	SDEV1		;NO KEEP LOOPING
	JRST	CPOPJ1		;YES--RETURN
	SUBTTL	SYSNO, SYS[IN TYMEX/GE/SUDS] COMMAND

SYSNO:	IFN FTNAME,<
	MOVE	M,[SIXBIT/SYSNO/]
	SETNAM	M,
	JRST	.+1>
	PUSHJ	P,PSYSNO	;TYPE SYSTEM NO.
	JRST	FINIS		;GO FINISH UP

PSYSNO:	MOVEI	M,[ASCIZ /TYMSHARE /]
	PUSHJ	P,MSG
IFN FTCPU,<
;CHECK FOR TYPE OF CPU WE IS RUNNING ON
IFG FTCPU,<MOVEI M,[ASCIZ /KA10 /]>
IFL FTCPU,<MOVEI M,[ASCIZ //]>
	MOVNI	B,1		;STANDARD DECSYSTEM10 CPU HACK
	AOBJN	B,.+1		;KI10 IS STUBBORN ABOUT INCR.
	JUMPN	B,.+2		;WILL JUMP IF A KA10
	MOVEI	M,[ASCIZ /KI10 /]
	PUSHJ	P,MSG		;PUT IT OUT
>
	MOVSI	B,-7		;7 WORDS
PSYS1:	MOVEI	A,11		;DATA BLOCK NUMBER
	HRLI	A,0(B)
	GETTAB	A,
	SETZ	A,
	MOVEM	A,DBLOCK(B)
	AOBJN	B,PSYS1
	MOVEI	M,DBLOCK
	PUSHJ	P,MSG
IFG FTDATE,<
	PUSHJ	P,SPACE
	MOVEI	M,DBLOCK+5	;DATE
	PUSHJ	P,MSG
>
	POPJ	P,		;RETURN
	SUBTTL	USERS COMMAND

USECMD:	IFN FTNAME,<
	MOVE	M,[SIXBIT/USERS/]
	SETNAM	M,
	JRST	.+1>
	PUSHJ	P,GA		;TAKE DUMP OF SYSTEM
	MOVE	CH,SAVWD	;GET LAST CHAR SCANNED
	MOVEM	CH,SAVCHR	;PICK UP ON NEXT GETCHR
	TLNN	F,FL.BRK	;SKIP=SIGN BIT SET
	JRST	USEHLP		;YES--GIVE HELP
USERET:	PUSHJ	P,USESW		;DO USE COMMAND
	JRST	FINIS		;ALL DONE....
USESW:	HRRO	B,XJBPPN	;GET MY PPN
	GETTAB	B,
	SETZ	B,		;FAILED??
	HRRM	B,MYUUN		;SAVE MY UUN
	HLRM	B,MYGAN		;SAVE MY GAN
	SETZM	TOTUSE		;TOTAL # USERS
	SETZM	INMGAN		;TOTAL IN MY GAN
	SETZM	INMUUN		;TOTAL LOGGED IN UNDER ME
	SETZM	DET		;TOTAL DETACHED
	SETZM	RUN		;TOTAL RUNNING
	SETZM	DIO		;TOTAL IN DISK IO QUEUE
	SETZM	IO		;TOTAL IN IO QUEUE
	SETZM	TIO		;TOTAL IN TTY IO QUEUE
	SETZM	SPC		;TOTAL SPECIALS


;NOW COUNT UP THOSE LUSERS !
	MOVN	J,JOBN
	HRLZS	J		;SET UP J

USELOP:	SKIPN	@.USRN1		;IF USER NAME ZERO NEXT PLS...
	JRST	USEEND		;YES-CHECK NEXT
	HLRZ	A,@.PPN		;GET PPN
	CAMN	A,MYGAN		;SAME AS MY GAN?
	AOS	INMGAN		;YES-INC COUNTER
	HRRZ	A,@.PPN
	CAMN	A,MYUUN		;SAME AS MY UUN?
	AOS	INMUUN		;YES-INC COUNTER
	MOVE	A,@.TTY
	JUMPE	A,.+3		;IF = 0 THEN CANT GET HIM
	TLNN	A,-1		;DETACHED ?
	AOS	DET		;LUSER IS DETACHED
	AOS	TOTUSE		;ANOTHER LUSER ON THE SYSTEM
	SKIPGE	JS,@.STS	;IF > = THEN ^C
	JRST	USECM1
	AOS	TIO		;ANOTHER JOB IDLE
	JRST	USEEND
USECM1:	LDB	B,[POINT 5,JS,14]
	IDIVI	B,3
	IMULI	C,^D12
	MOVE	A,QTAB(B)
	LSH	A,(C)
	AND	A,[7777B11]
	TRNE	JS,JDCON
	JRST	USERSP
;NOW, SEARCH THE USE COMMAND TABLE
	MOVEI	D,USET1L	;LENGTH OF TABLE
	CAMN	A,USET1(D)	;DOES IT MATCH?
	JRST	USECM2		;YES, GO TO ID
	SOJGE	D,.-2		;SEARCH IT
; FAILURE, INC SPC
USERSP:	AOS	SPC		;INCREMENT IT
	JRST	USEEND		;CONTINUE SCAN

;FOUND MATCH IN TAB, XCT TABLE
USECM2:	XCT	USET2(D)	;XCT THE INCREEMTNER INST
	JRST	USEEND

;SHOULD DROP IN, BUT...
USEEND:	AOBJN	J,USELOP	;OLD LOOP-DE-LOOP

; MACRO YYY TAKES TWO ARGS:
; 1 = NAME OF QUEUE
; 2 = CELL TO PRINT
DEFINE YYY(A,B)<
	MOVEI	M,[ASCIZ/A/]
	MOVE	N,B
	PUSHJ	P,MSGDEC>

	YYY	<Users=>,TOTUSE
	YYY	< Det=>,DET
	YYY	< [>,INMGAN
	YYY	<,>,INMUUN
	YYY	<], Queues: RUN=>,RUN
	YYY	< DIO=>,DIO
	YYY	< TIO=>,TIO
	YYY	< IO=>,IO
	YYY	< SPC=>,SPC
        PUSHJ   P,CRLF
	POPJ	P,

; HERE ON CHARS AFTER COMMAND, CHECK FOR ? COMMAND
USEHLP:	PUSHJ	P,GETCHR	;GET NEXT INPUT LINE CHARACTER
	 JRST	USERET		;NONE LEFT...
	CAIE	WD,15		;A RETURN?
	CAIN	WD," "		;SPACE?
	JRST	USEHLP		;YES, LOOP
	CAIN	WD,12		;LINE-FEED?
	JRST	USERET		;YES, DO USERS COMMAND
	CAIE	WD,"?"		;QUESTION-MARK ?
	JRST	CHKONC		;NOT SPACE OR ?, BAD CHAR
	MOVEI	M,UHLPMS	;QUESTION-MARK, TYPE HELP MESSAGE
	PUSHJ	P,MSG
	JRST	FINIS		;ALL DONE


; TABLE OF THE USE COMMAND BREAK-DOWNS.
; FORMAT OF THE TABLE IS:
;
; XXX AAA,BBB
;
; WHERE:
;
; XXX = XXX (CALL TO MACRO XXX)
; AAA = THE THREE OR TWO CHARACTER NAME OF THE CONDITION
;     (IE   TIO   DIO   SPC     ETC.)
; BBB = THE TWO-CHARACTER MONITOR QUEUE NAME
;      (IE    RN WS TS ETC.)
DEFINE USETAB <

	XXX	RUN,RN;		RUN QUEUE.
	XXX	RUN,WS;		IO WAIT SATISFIED.
	XXX	RUN,TS;		TTY IO WAIT SATISFIED
        XXX     DIO,SS;         SWAPPER WAIT SATISFIED
        XXX     DIO,SW;         SWAPPER OUTPUT WAIT (PGYPGO)
	XXX	DIO,MB;		MONITOR BUFFER WAIT
	XXX	DIO,DA;		DISK ALLOCATION WAIT
	XXX	DIO,CB;		CORE BLOCK ALLOCATION WAIT
	XXX	IO,MT;		MAG TAPE WAIT
	XXX	SPC,AX;		AUX CIRCUIT WAIT
	XXX	IO,IO;		IO WAIT (EXCEPT DISK AND TTY)
	XXX	TIO,TI;		TTY IO WAIT
	XXX	SPC,SL;		SLEEP................
	XXX	TIO,NU;		NULL (AND HOW)
	XXX	TIO,ST;		STOPPED (IE   ^C STATE)

>;	 ABOVE IS THE USE COMMAND TABLE

; TABLES FOR THE USE COMMAND DRIVES
; IF QUEUE NOT FOUND IN THIS TABLE, COUNT'S AS SPC
DEFINE XXX(A,B)<SIXBIT/B/>

USET1:	USETAB		;6-BIT QUEUE NAMES
USET1L==.-USET1		;LENGTH OF TABLE

DEFINE XXX(A,B)<AOS A>

USET2:	USETAB		;XCT DOES AN AOS FOR US

	OFF.LIST
UHLPMS:	ASCIZ	}
Users command gives information about users on system.  This
output is also included in the SYSTAT "PERFORMANCE" command
output.  The format of the output from USERS is as follows:

  Users=#1 Det=#2 [#3,#4], Queues: RUN=#5 DIO=#6 TIO=#7 IO=#8 SPC=#9

Where:
#1 = Jobs logged into the system
#2 = Jobs logged in that are detached
#3 = Jobs logged in with your Global account
#4 = Jobs logged in with your user name
#5 = Jobs RUNning (Monitor states: RN WS TS)
#6 = Jobs doing Disk IO (MB DA CB SS SW)
#7 = Jobs doing Tty IO or Idle (TI ^C)
#8 = Jobs doing non-Disk/Tty IO (MT IO)
#9 = Jobs in special queues (for example: SL AX)
}
	ON.LIST

;  CONSTANTS

XJBPPN:	XWD	0,2	;JOB'S PPN (PRJPRG)
XJOBNO:	XWD	0,-26	;GET JOB NO. GIVEN TTY NO.
XOPDEV:	XWD	13,11	;NAME OF "OPR" TTY
	END
  '\h'Ý