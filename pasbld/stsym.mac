	universal	stsym
	subttl	universal string macro definitions
	exp1==2
	exp2==3
	exp3==4
	exp4==5
	exp5==6
	exp6==7
	top==17
	$true=1
	$false=0
	$nil=377777

;
; Conditional assembly flag $EXTEND indicates whether extended
; string instructions are available.
;

	$extend==$true
	page
	subttl	$ULCASE
;
; $ULCASE generates code to convert a char to upper or to
; lower case, if necessary.
;
	define	$ULCASE	(reg,case),<
	ifidn	<case><U>,<
	cail	reg,141
	caile	reg,172
	jrst	.+2
	subi	reg,40>
	ifidn	<case><L>,<
	cail	reg,101
	caile	reg,132
	jrst	.+2
	addi	reg,40>>
	page
	subttl	$MOVESTRING
;
; $MOVESTRING macro generates modules called directly by the
; compiler-generated code to move strings of descriptor type
; R, C, F, and X, with optional upper/lower case conversion and padding.
; The destination must not be a C and the source must not be an R.
;
; All code required for a C format source is emitted by this macro.
; X and F format source strings are passed to the more general
; routines generated by the $SMOVE macro if extended string instructions
; are not available; if they are all code is emitted directly.
;
	define	$movestring (dest,source,pad,case),<
	entry	M'pad''case'.'dest''source'

	ifidn	<source><C>,<
	ifidn	<dest><R>,<
M'pad''case'.'dest''source':	aos	r1,0(top)
	move	r1,@-1(r1)	;get char itself
	$ulcase(r1,case)
	sosl	$strlen(pda)	;if string not filled
	idpb	r1,$str(pda)	;in previous string
	ifidn	<pad><P>,<
	jrst	r.pad##>
	ifdif	<pad><P>,<
	popj	top,>>
	ifdif	<dest><R>,<
M'pad''case'.'dest''source':	movei	r1,3
	addb	r1,0(top)	;bump return address past args
	movei	r0,@-2(r1)	;get dest length
	movem	r0,$strlen(pda)	;save length of dest
	ifidn	<dest><X>,<
	move	r0,@-3(r1)>	;get bptr to dest
	ifidn	<dest><F>,<
	movei	r0,@-3(r1)	;get addr of dest
	hrli	r0,440700>	;make bptr to dest
	move	r1,@-1(r1)	;get char
	$ulcase(r1,case)
	sosl	$strlen(pda)	;if string not filled
	idpb	r1,r0		;store char
	movem	r0,$str(pda)
	ifidn	<pad><P>,<
	jrst	r.pad##>
	ifdif	<pad><P>,<
	popj	top,>>>

	ifdif	<source><C>,<
	ife	$extend,<	;if not extended instructions
	ifidn	<dest><R>,<
M'pad''case'.'dest''source':	movei	r1,2
	addb	r1,0(top)>
	ifdif	<dest><R>,<
M'pad''case'.'dest''source':	movei	r1,4
	addb	r1,0(top)	;bump return addr past args
	ifidn	<dest><X>,<
	move	r0,@-4(r1)>	;get dest bptr
	ifidn	<dest><F>,<
	movei	r0,@-4(r1)	;get dest addr
	hrli	r0,440700>	;make bptr to dest
	movem	r0,$str(pda)
	movei	r0,@-3(r1)	;get dest length
	movem	r0,$strlen(pda)>
	ifidn	<source><X>,<
	move	r0,@-2(r1)>	;get source bptr
	jrst	X'source'.'pad''case'##>

	ifn	$extend,<	;if extended instructions ok
	$temp	rsave,5
	$temp	rtemp,2
	ifidn	<dest><R>,<
M'pad''case'.'dest''source':	movei	r1,2>
	ifdif	<dest><R>,<
M'pad''case'.'dest''source':	movei	r1,4>
	addb	r1,0(top)	;bump return addr past args
	$save	rsave,r1+1,r1+5	;need six registers
	ifdif	<dest><R>,<
	movei	r0,@-3(r1)	;get destination length
	ifidn	<dest><X>,<
	move	r1,@-4(r1)>	;get destination byte pointer
	ifidn	<dest><F>,<
	movei	r1,@-4(r1)	;get destination address
	hrli	r1,440700>	;make byte pointer
	dmovem	r0,rtemp	;save destination arguments
	move	r1,0(top)>
	movei	r0,@-1(r1)	;get source length
	ifidn	<source><X>,<
	move	r1+1,@-2(r1)>	;get source byte pointer
	ifidn	<source><F>,<
	movei	r1+1,@-2(r1)	;get source address
	hrli	r1+1,440700>	;make into byte pointer
	ifdif	<dest><R>,<
	dmove	r1+3,rtemp>	;retrieve destination args
	ifidn	<dest><R>,<
	skipg	r1+3,$strlen(pda);if target full then
	jrst	ms.fin##	;  forget it
	move	r1+4,$str(pda)>
	move	r1,r0
	jrst	ms.'pad''case'##>>>	;go do instruction
	page
	subttl	$SMOVE
;
; $SMOVE macro generates modules to move strings in X format.
; (Unused if extended string instructions available).
;
	define	$smove (pad, case),<
	entry	XF.'pad''case'
	entry	XX.'pad''case'
	$temp	rsave,1

XF.'pad''case':	movei	r0,@-2(r1)	;get source addr
	hrli	r0,440700	;make source bptr
XX.'pad''case':	movei	r1,@-1(r1)	;get source length
	ifdif	<case><L>,<
	ifdif	<case><U>,<
	camn	r0,$str(pda)	;if move to same string
	jrst	same>>		;then merely update byte pointer
	movem	exp1,rsave
	ifidn	<pad><P>,<
loop:	sojl	r1,srcdone>
	ifdif	<pad><P>,<
loop:	sojl	r1,done>
loop2:	sosge	$strlen(pda)
	jrst	done
	ildb	exp1,r0
	$ulcase(exp1,case)
	idpb	exp1,$str(pda)
	sojge	r1,loop2
	ifidn	<pad><P>,<
srcdone:move	exp1,rsave
	jrst	r.pad##>
done:	move	exp1,rsave
	popj	top,
	ifdif	<case><L>,<
	ifdif	<case><U>,<
same:	camle	r1,$strlen(pda)
	move	r1,$strlen(pda)
	movn	r0,r1
	adjbp	r1,$str(pda)
	movem	r1,$str(pda)
	addm	r0,$strlen(pda)
	ifidn	<pad><P>,<
	jrst	r.pad##>
	ifdif	<pad><P>,<
	popj	top,>>>>
	page
	subttl	$CONCAT
;
; $CONCAT macro generates modules called directly by compiler-generated
; code to concatenate to the end of a string. The target (and first operand
; of the concatenation) may be F or X format, while the second string
; operand may be F, X, or C format. Upper/lower casing and padding with
; blanks is optional.
;
; All code required for a C format source is emitted directly. X and F
; format source strings are passed to the more general routines generated
; by the $CAT macro if extended string instructions are not
; available; otherwise all registers are set up and passed
; along to the routines generated by $EXMOVESTRING.
;
; The calling sequence and argument list is:
;	PUSHJ	TOP,routine
;	ARG	0,addr(dest)	; to byte pointer if X
;	ARG	0,maxlen	; of target
;	ARG	0,length	; current length of target
;	ARG	0,addr(source)	; to byte pointer if X
;	arg	0,length	; of source if not C format
;

	define	$concat (dest,source,pad,case),<
	entry	C'pad''case'.'dest''source'

	ifidn	<source><C>,<
	len==4>
	ifdif	<source><C>,<
	len==5
	ifn	$extend,<
	$temp	rsave,5
	$temp	srclen,1>>
C'pad''case'.'dest''source':	movei	r1,len
	addb	r1,0(top)	; bump return addr past argument list
	ifidn	<dest><F>,<
	movei	r0,@-len(r1)	; get addr of target string
	hrli	r0,440700>	; make a byte pointer
	ifidn	<dest><X>,<
	move	r0,@-len(r1)>	; get target byte pointer
	movem	r0,$str(pda)
	ifidn	<source><C>,<
	movei	r0,@-len+1(r1)	; get target maxlength
	subi	r0,@-len+2(r1)	; find remainder length
	movem	r0,$strlen(pda)
	move	r0,@-1(r1)	; get char
	$ulcase	(r0,case)
	movei	r1,@-2(r1)	; get current length
	adjbp	r1,$str(pda)
	sosl	$strlen(pda)
	idpb	r0,r1
	movem	r1,$str(pda)
	ifidn	<pad><P>,<
	jrst	r.pad##>	; complete padding
	ifdif	<pad><P>,<
	popj	top,>>
	ifdif	<source><C>,<
	ife	$extend,<	; if not extended instructions
	ifidn	<source><X>,<
	move	r0,@-2(r1)	; get source byte pointer
	jrst	CX.'pad''case'##>
	ifidn	<source><F>,<
	jrst	CF.'pad''case'##>>
	ifn	$extend,<	; use extended instruction set
	$save	rsave,r1+1,r1+5
	movei	r0,@-4(r1)	; get maxlen
	subi	r0,@-3(r1)	; calculate remainder length
	movem	r0,$strlen(pda)
	jumple	r0,ms.fin##	; done if string full
	movei	r0,@-1(r1)	; get source length
	movem	r0,srclen	; and save it
	ifidn	<source><X>,<
	move	r0,@-2(r1)>	; get source byte pointer
	ifidn	<source><F>,<
	movei	r0,@-2(r1)	; get source address
	hrli	r0,440700>	; make into byte pointer
	movei	r1+4,@-3(r1)	; get current destination length
	move	r1+3,$strlen(pda); retrieve remainder length
	adjbp	r1+4,$str(pda)	; update dest byte pointer
	move	r1+1,r0		; move source byte pointer 
	move	r1,srclen	; retrieve source length
	jrst	ms.'pad''case'##>>>	; go do move
	page
	subttl	$EXMOVESTRING
;
; $EXMOVESTRING macro generates modules to perform string moves
; using the extended instruction set.
;

	define	$exmovestring (pad,case),<
	$module	ms'pad''case'
	entry	MS.'pad''case'

	ifidn	<pad><P>,<
MS.'pad''case':	setzm	$strlen(pda)>	; no remainder length
	ifdif	<pad><P>,<
MS.'pad''case':	movnm	r1,$strlen(pda)
	addm	r1+3,$strlen(pda)	; calculate remainder length
	camge	r1,r1+3		; if source shorter then
	move	r1+3,r1>	;   use source length
	extend	r1,mstr.'case'## ; do the extended instruction
	jfcl
	movem	r1+4,$str(pda)	; byte pointer to remainder string
	jrst	ms.fin##>	; and go restore registers
	page
	subttl	$CAT
;
; $CAT macro generates modules to concatenate an F or X format
; string to the X format string indicated by $str(pda),
; with optional padding and upper/lower case conversion.
;
; On entry R1 points to the word following the argument list.
;

	define	$cat (pad,case),<
	entry	CX.'pad''case'
	entry	CF.'pad''case'

	$temp	srcbp,1

CF.'pad''case':	movei	r0,@-2(r1)	; get addr of source string
	hrli	r0,440700	; make a byte pointer
CX.'pad''case':	movem	r0,srcbp
	movei	r0,@-4(r1)	; get target max length
	subi	r0,@-3(r1)	; find remainder length
	movem	r0,$strlen(pda)
	movei	r0,@-1(r1)	; get length of source string
	movei	r1,@-3(r1)	; get remainder length of target
	adjbp	r1,$str(pda)	; update target byte pointer
	movem	r1,$str(pda)
	ifidn	<pad><P>,<
loop:	sojl	r0,r.pad##>
	ifdif	<pad><P>,<
loop:	sojl	r0,done>
	sosge	$strlen(pda)
done:	popj	top,
	ildb	r1,srcbp
	$ulcase	(r1,case)
	idpb	r1,$str(pda)
	jrst	loop>
	page
	subttl	$CMPSTRINGS
;
; $CMPSTRINGS macro generates routines to compare two strings.
; If either is C format code is emitted directly, otherwise
; entry points CSP.X or CSP.F are called depending on the type
; of the source descriptor.
;
	define	$cmpstrings (str1,str2),<
	entry	CSP.'str1''str2'
	cformat==1
	result==r1
	ifdif	<str1><C>,<
	ifdif	<str2><C>,<
	cformat==0
	$temp	save1,2>>

	ifg	cformat,<
	$temp	rsave,1
	$temp	len,1
CSP.'str1''str2':	movem	exp1,rsave	;free reg
	movei	r1,3
	addb	r1,0(top)	;bump return addr past args
	ifidn	<str1><C>,<
	movei	r0,@-1(r1)>	;get str2 length
	ifidn	<str2><C>,<
	movei	r0,@-2(r1)>	;get str1 length
	sojl	r0,empty	;go compare with null string
	movem	r0,len
	ifidn	<str1><C>,<
	ifidn	<str2><X>,<
	move	r0,@-2(r1)>	;get str2 bptr
	ifidn	<str2><F>,<
	movei	r0,@-2(r1)	;get str2 addr
	hrli	r0,440700>	;make bptr
	move	r1,@-3(r1)	;get char
	ildb	exp1,r0>	;get first char of string
	ifidn	<str2><C>,<
	ifidn	<str1><X>,<
	move	r0,@-3(r1)>	;get str1 bptr
	ifidn	<str1><F>,<
	movei	r0,@-3(r1)	;get str1 addr
	hrli	r0,440700>	;make bptr
	move	exp1,@-1(r1)	;get char
	ildb	r1,r0>		;get first char of string
	sub	r1,exp1
	jumpn	r1,return	;done if unequal
loop:	sosge	len		;compare rest with blanks
	jrst	return		;return with zero in r1
	ildb	r1,r0
	subi	r1,40		;done if unequal
	jumpe	r1,loop
	ifidn	<str1><C>,<
	movn	r1,r1>
return:	move	exp1,rsave
	popj	top,
	ifidn	<str1><C>,<
empty:	move	r1,@-3(r1)	;get char
	subi	r1,40>		;compare with blank
	ifidn	<str2><C>,<
empty:	move	exp1,@-1(r1)	;get char
	movei	r1,40
	sub	r1,exp1>
	jrst	return>

	ife	cformat,<
CSP.'str1''str2':	movei	r1,4
	addb	r1,0(top)
	ifidn	<str1><X>,<
	move	r0,@-4(r1)>
	ifidn	<str1><F>,<
	movei	r0,@-4(r1)
	hrli	r0,440700>
	movei	r1,@-3(r1)
	dmovem	r0,save1
	move	r1,0(top)
	ifidn	<str2><X>,<
	move	r0,@-2(r1)>
	jrst	CSP.'str2'##>>
	page
	subttl	$INDEXSTRINGS
;
; $INDEXSTRINGS generates routines to perform the index function
; on C, F, and X format descriptored strings.
;
; If either string is C format the code is emitted directly. Otherwise
; IX.X or IX.F is called depending on the type of the second string.
;
	define	$indexstrings (str1,str2),<
	cformat==0
	result==r1
	ifidn	<str1><C>,<cformat==cformat+2>
	ifidn	<str2><C>,<cformat==cformat+1>
	entry	IX.'str1''str2'

	ife	cformat-3,<
IX.'str1''str1':	movei	r1,2		;both C format
	addb	r1,0(top)	;bump return addr
	move	r0,@-2(r1)	;get first char
	move	r1,@-1(r1)	;and second char
	came	r0,r1
	tdza	result,result	;failure return
	movei	result,1	;or character position
	popj	top,>

	ife	cformat-2,<
IX.'str1''str2':	movei	r1,3		;first C format
	addb	r1,0(top)
	movei	r0,@-1(r1)	;get length
	jumpe	r0,success	;done if null string
	caie	r0,1		;must be single char
	jrst	fail
	ifidn	<str2><X>,<
	move	r0,@-2(r1)>	;get bptr
	ifidn	<str2><F>,<
	movei	r0,@-2(r1)	;get addr
	hrli	r0,440700>	;make bptr
	ildb	r0,r0
	came	r0,@-3(r1)
fail:	tdza	result,result
success:movei	result,1
	popj	top,>

	ife	cformat-1,<
	$temp	rsave,2
	$temp	len,1
IX.'str1''str2':	dmovem	exp1,rsave	;second C format
	movei	r1,3
	addb	r1,0(top)	;bump return addr
	movei	r0,@-2(r1)	;get length
	sojl	r0,fail
	movem	r0,len
	ifidn	<str1><X>,<
	move	r0,@-3(r1)>	;get bptr
	ifidn	<str1><F>,<
	movei	r0,@-3(r1)	;get addr
	hrli	r0,440700>	;make bptr
	move	exp1,@-1(r1)	;get char
	movei	result,1
loop:	ildb	exp2,r0		;extract a char
	camn	exp1,exp2
	jrst	found
	sosl	len
	aoja	result,loop
fail:	movei	result,0
found:	dmove	exp1,rsave
	popj	top,>

	ife	cformat,<
	$temp	save1,2
IX.'str1''str2':	movei	r1,4	;neither C format
	addb	r1,0(top)
	ifidn	<str1><X>,<
	move	r0,@-4(r1)>
	ifidn	<str1><F>,<
	movei	r0,@-4(r1)
	hrli	r0,440700>
	movei	r1,@-3(r1)
	dmovem	r0,save1
	move	r1,0(top)
	ifidn	<str2><X>,<
	move	r0,@-2(r1)>
	jrst	IX.'str2'##>>
	page
	subttl	$PATTERN
;
; $PATTERN macro generates search and verify routines with optional
; uppercase for C, F, and X format strings and L, O, and D format sets.
; C format emitted as special cases.
;
	define	$pattern (string,set,case,verify),<
	ifn	verify,<
	entry	VF'case'.'string''set'>
	ife	verify,<
	entry	SR'case'.'string''set'>

	ifidn	<string><C>,<
	ifidn	<set><O>,<
	$temp	op1,1
	op2==r0

	ifn	verify,<
VF'case'.CO:	movei	r1,3>
	ife	verify,<
SR'case'.CO:	movei	r1,3>
	addb	r1,0(top)	;bump return addr
	move	r0,@-2(r1)	;get op1
	movem	r0,op1
	move	r0,@-1(r1)	;get op2
	move	r1,@-3(r1)	;get char
	$ulcase(r1,case)
	ifn	verify,<
	caml	r1,op1
	camle	r1,op2
	jrst	true>
	ife	verify,<
	camg	r1,op2
	camge	r1,op1>
	tdza	r1,r1
true:	movei	r1,1
	popj	top,>

	ifidn	<set><D>,<
	$temp	rsave,2

	ifn	verify,<
VF'case'.CD:	movei	r1,2>
	ife	verify,<
SR'case'.CD:	movei	r1,2>
	addb	r1,0(top)	;bump return addr
	move	r0,@-2(r1)	;get char
	$ulcase(r0,case)
	dmovem	exp1,rsave	;free regs
	dmove	exp1,@-1(r1)	;get two word set
	move	r1,r0
	lshc	exp1,-40(r1)
	ife	verify,<
	movei	r1,0
	lshc	r1,1>
	ifn	verify,<
	jumpge	exp1,.+2
	tdza	r1,r1
	movei	r1,1>
	dmove	exp1,rsave
	popj	top,>

	ifidn	<set><L>,<
	$temp	rsave,1

	ifn	verify,<
VF'case'.CL:	movei	r1,4>
	ife	verify,<
SR'case'.CL:	movei	r1,4>
	addb	r1,0(top)	;bump return addr
	movem	exp1,rsave
	move	r0,@-4(r1)	;get char
	$ulcase(r0,case)
	subi	r0,@-2(r1)	;correct by lwb
	ifn	verify,<
	jumpl	r0,true
	cail	r0,@-1(r1)	;compare with dim
	jrst	true>
	ife	verify,<
	jumpl	r0,false
	cail	r0,@-1(r1)	;compare with dim
	jrst	false>
	movei	exp1,@-3(r1)	;get addr of set
	idivi	r0,^d36
	add	exp1,r0		;add word offset
	move	exp1,0(exp1)	;word containing bit
	lsh	exp1,0(r1)	;isolate bit
	ifn	verify,<
	jumpge	exp1,true>
	ife	verify,<
	jumpl	exp1,true>
false:	tdza	r1,r1
true:	movei	r1,1
	move	exp1,rsave
	popj	top,>
>

	ifdif	<string><C>,<
	$temp	rsave,7
	$temp	strsave,2
	ifidn	<set><L>,<
	$temp	setaddr,1>
	extern	save.7
	extern	rest.7
	result==r1
	strbp==exp1
	strlen==exp2
	char==exp5
	op1==r0
	op2==exp3
	dset==exp3
	temp==exp6
	lwb==r0
	dim==exp3
	temp1==exp6
	temp2==temp1+1
	ifidn	<set><D>,<offset==1>
	ifidn	<set><O>,<offset==2>
	ifidn	<set><L>,<offset==3>
	ifn	verify,<
VF'case'.'string''set':	jsp	r1,save.7>
	ife	verify,<
SR'case'.'string''set':	jsp	r1,save.7>
	movei	r1,offset+2
	addb	r1,0(top)	;bump return addr
	ifidn	<string><X>,<
	move	r0,@-offset-2(r1)>
	ifidn	<string><F>,<
	movei	r0,@-offset-2(r1)
	hrli	r0,440700>
	movei	r1,@-offset-1(r1)
	dmovem	r0,strsave
	move	r1,0(top)
	ifidn	<set><D>,<
	dmove	dset,@-1(r1)>
	ifidn	<set><O>,<
	move	op1,@-2(r1)
	move	op2,@-1(r1)>
	ifidn	<set><L>,<
	movei	r0,@-3(r1)
	movem	r0,setaddr
	movei	lwb,@-2(r1)
	movei	dim,@-1(r1)>

	movei	result,0
	dmove	strbp,strsave

loop:	sojl	strlen,fail.7##
	addi	result,1
	ildb	char,strbp
	$ulcase	(char,case)
	ifidn	<set><O>,<
	ifn	verify,<
	camg	char,op2
	camge	char,op1
	jrst	rest.7
	jrst	loop>
	ife	verify,<
	caml	char,op1
	camle	char,op2
	jrst	loop>
	jrst	rest.7##>
	ifidn	<set><D>,<
	dmove	temp,dset
	lshc	temp,-40(char)
	ifn	verify,<
	jumpl	temp,loop>
	ife	verify,<
	jumpge	temp,loop>
	jrst	rest.7##>
	ifidn	<set><L>,<
	sub	char,lwb
	ifn	verify,<
	jumpl	char,rest.7	;less than lwb == not in set
	caml	char,dim
	jrst	rest.7>		;greater than lwb+dim == not in set
	ife	verify,<
	jumpl	char,loop	;in search, continue if lt lwb
	caml	char,dim
	jrst	loop>		;or greater than lwb+dim
	idivi	char,^d36
	add	char,setaddr
	move	temp2,0(char)
	lsh	temp2,0(temp1)
	ifn	verify,<
	jumpl	temp2,loop>
	ife	verify,<
	jumpge	temp2,loop>
	jrst	rest.7##>
>>
	page
	subttl	SEARCHSTRING and VERIFYSTRING
;
; $SEARCHSTRING and $VERIFYSTRING are just wrappers for
; $PATTERN.
;
	define	$searchstring	(string,set,case),<
	$pattern (string,set,case,$false)>

	define	$verify (string,set,case),<
	$pattern (string,set,case,$true)>
	page
	subttl	$SETOP

;
; $SETOP emits routines for union, intersection, difference, and
; comparison of long sets.
;
	define	$setop (operation),<
	entry	S'operation'.LL
	$temp	temp,2
	$temp	rsave,2
	equal==0
	le==1
	diff==2
	intersect==3
	union==4
	addr1==exp1
	addr2==exp2
	dim==r1


	ifidn	<operation><EQ>,<oper==equal>
	ifidn	<operation><LE>,<oper==le>
	ifidn	<operation><DF>,<oper==diff>
	ifidn	<operation><IN>,<oper==intersect>
	ifidn	<operation><UN>,<oper==union>
S'operation'.LL:	dmovem	addr1,rsave	;free work regs
	movei	r1,6
	addb	r1,0(top)	;bump return address
	movei	r0,@-6(r1)	;get addr of first set
	movei	r1,@-4(r1)	;and its dim
	dmovem	r0,temp
	move	r1,0(top)
	movei	r0,@-3(r1)	;addr of second set
	movei	dim,@-1(r1)	;and dim
	move	addr2,r0
	movni	r0,^d35(dim)
	idivi	r0,^d36		;generates negative word count
	hrl	addr2,r0
	move	addr1,temp	;retrieve addr of first set
	move	dim,1+temp	;and its dim
	movni	r0,^d35(dim)
	idivi	r0,^d36
	hrl	addr1,r0
	ife	oper-le,<
	jumpge	addr2,1+short2	;set2 = [] then set1 mb null
	jumpl	addr1,loop
	movei	r1,0		;set1 = [] then true
	jrst	return>
	ife	oper-equal,<
	jumpge	addr1,short1
	jumpge	addr2,1+short2>

loop:	move	r1,0(a	;get word from s2
	ife	oper-union,<
	iorm	r1,0(addr1)>
	ife	oper-intersect,<
	andm	r1,0(addr1)>
	ife	oper-diff,<
	andcam	r1,0(addr1)>
	ifle	oper-le,<
	ife	oper-le,<
	andca	r1,0(addr1)>
	ife	oper-equal,<
	xor	r1,0(addr1)>
	jumpn	r1,return
	aobjp	addr2,short2
	aobjn	addr1,loop
	ife	oper-equal,<
short1:	skipn	r1,0(addr2)
	aobjn	addr2,short1>
	jrst	return
short2:	aobjp	addr1,return
	skipn	r1,0(addr1)
	jrst	short2>
	ifg	oper-le,<
	ife	oper-intersect,<
	aobjp	addr2,clear>
	ifn	oper-intersect,<
	aobjp	addr2,return>
	aobjn	addr1,loop>
return:	dmove	addr1,rsave
	popj	top,
	ife	oper-intersect,<
clear:	aobjp	addr1,return
	setzm	0(addr1)
	jrst	clear>>
	page
	subttl	$RSAVE
;
; $RSAVE macro generates short routines to save and restore
; a specified number of registers on the stack.
; SAVE routine called with JSP R1, and RESTore routine by JRST.
;
	define	$rsave (size),<
	entry	save.'size'
	entry	rest.'size'
	$temp	rsave,size

save.'size':	$save	rsave,exp1,exp1+size-1
	jrst	0(r1)

rest.'size':	$restore	rsave,exp1,exp1+size-1
	popj	top,>
	end
    ~&¢