TITLE STATIC VERSION 0,1 - TYMSHARE SA10 DIAG ***STATIC TEST ONLY***
		;AS OF 10-JAN-83

;TYMSHARE PARAMETERS

BASE=300
TUNIT=200	;MAGTAPE ADDRESS
;IDEV=300	;DISK ADDRESS

A=1
B=2
C=3
D=4
E=5
F=6
G=7
H=10
I=11
J=12
K=13
L=14
BF=16		; BUSY FLAGS, FOR LIGHT WATCHERS. B0-7 MEAN DRIVE
		;  0-7 WAITING FOR DEVICE END. RH IS MOST RECENT INT DV
P=17		; PUSHDOWN POINTER

	TT=13
	FORT=14
	FT=15
	ST=16
	ELEV=11
	TEN=10
IFNDEF CV,<
CV==1>		; 0 FOR B, 1 FOR C (HARDWARE POINTER LAYOUTS)
IFLE CV,<
CNTSFT==2>	;SHIFT IN LH FOR COUNT
IFG CV,<
CNTSFT==6>	; ..
IFNDEF EADR,<
EADR==1>	;22 BIT ADDRESS FEATURE
IFNDEF BASE,<
BASE==300>	;CHNL 0 GO ADR
SA0==274	;DEVICE NUMBER OF SA-10
;IDEV==300	;DEVICE TO USE FOR UCODE SIM
;IFNDEF DCHAN,<DCHAN==1>		;DISK SUBCHANNEL
;IFNDEF TCHAN,<TCHAN==0>		;TAPE SUBCHANNEL
IFNDEF PCHAN,<PCHAN==1>		; PRINTER SUBCHANNEL
APRCH==2	;PI CH FOR TIME, PAR ERR, AND NXM
IFNDEF RECL,<
RECL==200>	;LENGTH OF RECORD ON DISK
NRPT==^D18	;NUMBER OF RECORDS PER TRACK
IFNDEF NPACKS,<
NPACKS==^D16>	;NUMBER OF DISK PACKS TO RUN IN DSKT
IFNDEF NCHN,<
NCHN==2>	;# OF CHANNELS SIMULATED BY THIS
		;  PARTICULAR SA-10 (2 OR 4)
SA0CH==1	;PI CHANNEL IN DISK ROUTINE
NBF==20		; # OF BYTES IN RING BUFFER (FIFO) IN EACH CHANNEL
NRETRY==^D8	; TIMES TO REREAD A CORRECTABLE DATA CHECK
NBADSP==100	; NUMBER OF BADSPOTS TO REMEMBER

;MACROS FOR WORD POINTERS DEPENDING ON WHICH KIND OF SA10 (B OR C)

IFE CV,<
	DEFINE IOW(CT,ADR)
	<-CT*4,,ADR>
	DEFINE IOWC(CT,ADR)
	<400000-CT*4,,ADR>>
IFN CV,<
	DEFINE IOW(CT,ADR)
	<-CT*100,,ADR>
	DEFINE IOWC(CT,ADR)
	<400000-CT*100,,ADR>>

;BITS FOR CONO
RSTSA==400000	;RESET WHOLE SCHMEER, START CLOCK
CLMERR==200000	;CLEAR MEMORY ERROR FLAGS
SASTEP==1000	;STOP CLOCK IF RUNNING; STEP IT IF STOPPED
SACGO==1040	;START CLOCK
RSTCH==300	;RESET CHANNEL SPECIFIED BY 10 AND 20 BITS
CLBSY==400	;CLEAR BUSY FLAG OF CHANNEL . . .
STBSY==440	;SET BUSY FLAG OF CHANNEL . . .
CLSRQ==500	;CLEAR STATUS REQUEST FLAG OF CHANNEL . . .
STSRQ==540	;SET STATUS REQUEST FLAG OF CHANNEL . . .
CLSTF==600	;CLEAR STATUS FLAG OF CHANNEL . . .
STSTF==640	;SET STATUS FLAG OF CHANNEL . . .
CLPIE==700	;CLEAR PI ENABLE OF CHANNEL . . .
STPIE==740	;SET PI ENABLE OF CHANNEL . . .

;CERTAIN OF ABOVE BITS ALSO SELECT WHAT DIAGNOSTIC WORD WILL
;BE READ BY THE NEXT DATAI TO BE EXECUTED, AS FOLLOWS:

RDCCI==0	;READ CHANNEL SELECTED (BITS 16, 17); CON-
		;TENTS OF CBUS (BITS 18-25); MICRO-PROGRAM
		;COUNTER (BITS 26-35)
RDMA==10	;READ MEMORY ADDRESS REGISTER (BITS 16-35)
RDMBL==20	;READ CHANNEL REFERENCING MEMORY (BITS 16,
		;17); LEFT HALF OF MEMORY BUFFER (BITS 18-
		;35).  THIS M.B. IS ONLY FOR DATA GOING
		;TOWARDS THE MEMORY.
RDMBR==30	;READ PARITY BIT OF MEMORY BUFFER (BIT 17);
		;RIGHT HALF OF MEMORY BUFFER (BITS 18-35)
RDCONI==40	;READ THE "CLOCK RUNNING" BIT (BIT 16); THE
		;"MEMORY SUBROUTINE BUSY" BIT (BIT 17); AND
		;THE SAME DATA SENSED BY CONI (BITS 18-35)
;CONTINUED ON NEXT PAGE


RDBITS==50	;READ SOME MISCELLANEOUS BITS:
		;BITS 16 AND 17 SPECIFY WHICH CHANNEL IS
		;USING THE MEMORY
	WRRQ==400000	;CURRENT MEM CYCLE IS A WRITE
	MUXACK==200000	;ACKNOWLEDGE FROM MEMORY PORT
	MEMACK==100000	;ACKNOWLEDGE FROM MEMORY
	MBUSDN==40000	;MEMORY BUS FINISHED
	MDNSYN==20000	;MEMORY DONE SYNC
	RDDIAG==10000
	IOFWR==4000
	CHRST==2000
	RESET==1000
	BRANCH==400	;THE MICRO-INSTRUCTION WHICH IS
			;ABOUT TO BE EXECUTED IS GOING TO BRANCH
;LOW 3 BITS ARE PI CHANNEL ASSIGNMENT

;BITS FOR CONI, CONSZ, CONSO
PIRQ==400000	;THIS SA-10 IS REQUESTING AN INTERRUPT
PARERR==200000	;PARITY ERROR IN SOME WORD (COMMAND OR
		;DATA) READ FROM MEMORY
NXM==100000	;SA-10 TRIED TO REFERENCE NON-EX MEMORY
PIE0==40000	;PI ENABLE FLAG FOR CHANNEL 0
PIE1==20000	; . . . CHANNEL 1
PIE2==10000	; . . . CHANNEL 2
PIE3==4000	; . . . CHANNEL 3
BSY0==2000	;BUSY FLAG FOR CHANNEL 0
BSY1==1000	; . . . CHANNEL 1
BSY2==400	; . . . CHANNEL 2
BSY3==200	; . . . CHANNEL 3
STF0==100	;STATUS FLAG FOR CHANNEL 0
STF1==40	; . . . CHANNEL 1
STF2==20	; . . . CHANNEL 2
STF3==10	; . . . CHANNEL 3
;LOW 3 BITS ARE PI CHANNEL ASSIGNMENT

HANGPC==64	;MAGIC PLACE TO STICK MICRO-PROGRAM COUNTER
		; THIS IS A MICRO-CODE PC. NOT A PDP-10 ADDRESS

;DEFS FOR TAPE
TRECL==2000	; RECORD LENGTH ON TAPE
;TUNIT==340

DSW==0
DEFINE LOOP(N,LOC)
<	DATAI DSW,0
;	HLRZS A
	CAIN 0,N
	JRST LOC
>

DEFINE SLOOP
<	DATAI DSW,0
	TLNE 0,2
	JRST GOGO
	TLNE 0,1
HALT
>

	DEFINE ALT	;ALTMODE TRAP
<	CONSO TTY,40
	JRST .+15
	DATAI	TTY,FT	;GET TTY INTO AC FT
	ANDI	FT,177	;STRIP OFF VALID DATA
	CAIN	FT,176	;LOOK FOR ALTMODE
	JRST	.+7		;IF DDT
	CAIN FT,175	;LOOK FOR ANOTHER FLAVOR ALT MODE
	JRST .+5
	CAIN FT,33	;STILL LOOKING FOR ALT MODE
	JRST .+3
	CAIE FT,124	;LOOK FOR LETTER T
	JRST .+3
	DATAO PAG,[400000,,400000]
	JRST,MSG+3
>
	DEFINE NALT	;ALT MODE HANG
<	CONSO TTY,40
	JRST .-1
	DATAI TTY,FT
	ANDI FT,177
	CAIN FT,176
	JRST .+7
	CAIN FT,175
	JRST .+5
	CAIN FT,33
	JRST .+3
	CAIE FT,120	;LOOK FOR LETTER P
	JRST .-13
>
PAGE
LOC 4000

GOGO:	MOVEI P,PDL-1
	DATAO PAG,[400000,,400000]
	CONO 635550
	SETZM TABCNT
	PUSHJ P,SETPTR
MSG:	MOVEI P,PDL-1	;SET UP PUSH STACK
	PUSHJ P,SETPTR
	HALT .+1
	MOVEI C,[ASCIZ/


	SA10 STATIC TEST
/]
	PUSHJ P,STYO
BEG:	CONO 635550	;CLEAR OUT THE WORLD
	DATAO	PAG,[0]	;CLEAR PAGING FOR BEG
	SETZM	ER28
	SETZM	BEGERR#		;REALLY A COUNT O F#PASSES
	CLEARM PIE	;NO INTERRUPTS ARE EXPECTED
	CONO PI,11577
	MOVE A,[JSR UUOH]
	MOVEM A,41	;INITIALIZE MAGIC MEMORY LOCATIONS
	MOVEI B,42	;ALL INTERRUPT ROUTINES ARE SAME LENGTH
	MOVE A,[JSR INT1]
PISETL:	MOVEM A,(B)
	ADDI A,INT2-INT1
	ADDI B,2
	CAIE B,60
	JRST PISETL
	CONO SA0,RSTSA!CLMERR
BTE:	CONO 200000	;I O RESET
	MOVEI A,^D300
	SOJG A,.	;DELAY A BIT
	CONI SA0,A
	CAME A,[1,,0]	;CONI SHOULD GIVE 1 IN L.H.
	HALT		;HALT IF WE GOT NO SA10 OR ANY "HUNG " I/O BUS BITS
	JRST BTE2	;IT'S OK
BTE2:	MOVEI A,10
	CONO PI,2377	;ENABLE ALL PI CHANNELS
	SOJG A,.	;WAIT NERVOUSLY
	CONO SA0,7	;SET PIA BITS
	CONI SA0,A
	CAME A,[1,,7]
	HALT		;IF PIA DOESN'T READ IN
	CONO 200000	;I O RESET SHOULD CLEAR PIA
	CONI SA0,A
	CONO PI,11577	;RESET PI
	CAME A,[1,,0]
	HALT	;PIA DIDN'T CLEAR
BTE4:	CONO SA0,RDCONI+7	;CAN WE DO A DATAI?
	DATAI SA0,A
	CAME A,[2,,7]	;CLEN, PIA
	HALT		;IF WRONG
	CONO SA0,SASTEP	;STOP THE CLOCK
	CONO	SA0,RDCONI	;NOW CHECK FOR CLOCK STOPPED
	DATAI SA0,A
	SETZM B
	CAME A ,B
	HALT
BEG1AA:	MOVEI B,2*NCHN
BEG1A:	DATAO SA0,[0]
	CONO SA0,SASTEP!RDCCI
	DATAI SA0,C
	MOVE A,C
	LSH A,-22	;FLUSH CBUS AND MICRO-P.C.
	TDNE A,[-1_2]
	HALT	;DATAI READ IN EXTRANEOUS BITS
	JUMPN A,BEG1	;WAIT UNTIL WE GET TO CHANNEL 0
BEG1B:	LOOP 1,BEG1A
BEG1C:	LOOP 2,BEG1AA
	MOVSI A,-NCHN	;CLEAR OUT COUNT TABLE
BEG20:	CLEARM CCTBL(A)
	AOBJN A,BEG20
BEG20A:	MOVEI B,100*NCHN
BEG2:	DATAO SA0,[0]	;GET THE THING A NO-OP TO EXECUTE
	CONO SA0,SASTEP!RDCCI
	DATAI SA0,A
	LSH A,-22
	ANDI A,-1_<-44+2>
	AOS CCTBL(A)	;NOTE THAT THIS CHANNEL HAS BEEN COUNTED
	SOJG B,BEG2
	MOVSI A,-NCHN	;MAKE SURE ALL CHANNELS GOT SERVICE
BEG21:	SKIPN CCTBL(A)
	HALT	;THIS GUY DIDN'T GET COUNTED
BEG21A:	LOOP 3,BEG20A
	AOBJN A,BEG21	;CHECK THE OTHERS
;FALL THRU

;MAKE SURE ALL BITS OF MICRO-P.C. CAN BE SET
	PUSHJ P,STEPC0
	MOVEI A,^D10
	JSR PTN	;TRY VALUES FOR 10 BITS
IARL:	MOVE B,A
	DATAO SA0,B
	PUSHJ P,STEP0
	ANDI A,1777
	CAME A,B
	HALT	;MICRO-P.C. NOT BEING SET PROPERLY
IARL1:	LOOP 4,IARL
	DATAO SA0,[040000]	;CBUS_IARB
	DATAI SA0,A
	LDB A,[121000,,A]
	MOVE C,B
	ANDI C,377
	CAME A,C
	HALT	;CBUS_IARB LOSING
IARL2:	LOOP 5,IARL
	JSR PTN0	;ON TO NEXT DATA VALUE
;SEE IF SOURCE OF ZEROES WORKS
SZER:	MOVEI A,176320	;REG. 3_0
	PUSHJ P,C0XCT
	MOVEI A,072260	;START DUMMY WRITE FROM REG. 3
	PUSHJ P,C0XCT
	CONO SA0,RDMBL
	DATAI SA0,A	;SHOULD GIVE US 0 IN THE R.H. OF A
	TRNE A,-1
	HALT	;FAILS HERE IF NO ROMS INSTALLED YET
SZER1:	LOOP ^D40,SZER
MAMB:	MOVEI A,^D36
	JSR PTN	;TRY ALL 36 BITS OF WORD
MAMB1:	MOVE C,[200000,,TEST]
	PUSHJ P,LC0CW
	MOVE C,A
	HLRZM C,MAMBTL
	HRRZM C,MAMBTR
	PUSHJ P,LC0R2
	MOVEI A,062260	;START DUMMY WRITE FROM REG. 2
	PUSHJ P,C0XCT
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
	CONO SA0,RDMA
	DATAI SA0,C
	MOVE A,C
	AND A,[3,,-1]
	CAIE A,TEST
	HALT	;MEM ADDR REG HAS WRONG THING IN IT
MAMB1A:	LOOP ^D11,MAMB1
	CONO SA0,RDMBL
	DATAI SA0,A
	HRRZ B,A
	CAME B,MAMBTL
	HALT	;MB LEFT LOSING
MAMB2:	LOOP 6,MAMB1
	TLNE A,-1
	HALT	;WRONG CHANNEL HAS MEMORY SUBROUTINE
MAMB3:	LOOP 7,MAMB1
	CONO SA0,RDMBR
	DATAI SA0,A
	CAME A,MAMBTR
	HALT	;MB RIGHT LOSING
MAMB4:	LOOP ^D12,MAMB1
	JSR PTN0	;ON TO NEXT DATA VALUE
CWTST0:	MOVEI G,NCHN-1	;TRY OUT WC, CA, PC PATHS
CWTST:	PUSHJ P,STEPCX	;STEP CLOCK TO APPROPRIATE CHANNEL
CWTST1:	MOVEI A,^D36	;CHECK 36 BIT POSITIONS
	JSR PTN
	MOVEM	A,CWTA	;GET AND SAVE DATA
	SETCM	B,A	;COMPLEMENT OF A TO B
	LSH	B,1	;SLIGHTLY DIFFERENT #
	MOVEM	B,CWTB	;CWTA AND B JUST FOR NOTHING
	LDB	C,[POINT 11,A,11]	;GET WC FIELD
	DPB	C,[POINT 11,WCX,11]	;STORE IN MASKED LOC
	LDB	C,[POINT 20,A,35]	;GET ADDRESS FIELD
	DPB	C,[POINT 20,CAX,19]	;PUT IT IN BITS 0-X,IE.,16-32 OF ADDR
	LDB	C,[POINT 3,A,15]	;GET BITS 14 SND 15
	DPB	C,[POINT 3,CAX,23]	;TUCK IT AWAY
	LDB	C,[POINT 20,B,35]	;GET ADDRESS FLD. OF WORD 2
	DPB	C,[POINT 20,PCX,19]	;STORE THIS AWAY
	LDB	C,[POINT 3,B,15]	;DON'T FORGET THESE TWO
	DPB	C,[POINT 3,PCX,23]	;PRBABLY WRON FOR THIS POINT
	JRST CWTST2
	WCXBP:	POINT  8,WCX	;8 BIT BYTE  FROM WCX
	POINT 	8,CAX	;8 BIT BYTE FROM CAX
	POINT	8,PCX	;8 BITS FROM PCX
	
WCX:	400077400000
CAX:	0
PCX:	0
CWTST2:	MOVE C,CWTA
	PUSHJ P,LCXCW	;PUT INTO WC, CA REGISTERS
	MOVE C,CWTB
	PUSHJ P,LCXR2
	MOVEI A,065300	;PC_ADR FILED OF REG. 2
	PUSHJ P,CXXCT
	MOVEI C,040400	;CBUS_BYTE 1 OF WC
	MOVEI	H,0	;INDEX REG FOR 1 FO 3 BYTE POINTERS
	MOVEI F,3	;DO STUFF FOR EACH OF 3 REGISTERS
CWT1:	MOVEI B,3	;DO THIS STUFF FOR EACH OF 3 BYTES
	MOVE	J,WCXBP(H)	;GET THE BYTE POINTER
CWT2:	DATAO SA0,C	;GET DATA ONTO CBUS
	CONO SA0,RDCCI
	DATAI SA0,A
	LDB A,[121000,,A]	;GET CONTENTS OF CBUS
	ILDB D,J	;GET EXPECTED ANSWER
	CAME D,A	;ARE THEY THE SAME?
	HALT	;NOPE
CWT2A:	ADDI C,400	;ADVANCE TO NEXT BYTE OF REGISTER
	SOJG B,CWT2	;THAT'S ALL FOR THAT REGISTER
	ADDI C,400	;ADVANCE TO NEXT REGISTER
	AOS	H	;INC XR FOR BYTE POINTERS
	SOJG F,CWT1	;WE'VE TESTED ALL THREE REGISTERS
	JSR PTN0	;TRY NEXT DATA VALUE
	SOJGE G,CWTST	;TRY ALL CHANNELS
	LOOP ^D28,CWTST0
INCT:	MOVEI J,0	;0 OR -1
INCT0:	HRLOI F,17	;SHIFTS
INCT1:	MOVE C,F	;DATA WORD
	XOR C,J	;COMPLEMENTED SECOND TIME THROUGH
	MOVEM C,INCTW	;REAL DATA WORD
	ADDM J,INCTW
	JUMPL J,INCT1A
	AOS INCTW	;SIMULATE THE INCREMENTER
INCT1A:	PUSHJ P,LC0CW	;LOAD CA, WC
	MOVEI A,42260	;MICRO INST, XANC INCREMENT CA,DUMMY,DIAG
	JUMPGE J,INCT2
	MOVEI A,42660	;MICRO INST,XANC, DECREMENT CA,DUMMY DIAG
INCT2:	PUSHJ P,C0XCT	;DO THE INCREMENT/DECREMENT
	MOVEI A,42260
	PUSHJ P,C0XCT
	CONO SA0,RDMA	;TO READ THE MA BACK
	DATAI SA0,A
	CONO SA0,RDBITS	;TO READ THE EXTRA ADDR BITS
	DATAI SA0,B
	DPB B,[240200,,A]	;PUT EXTRA BITS WHERE THEY BELONG
	XOR A,INCTW	;COMPARE
	TDNE A,[17,,777777]	;WHERE WORDS SHOULD MATCH
	HALT	;IF ERROR
INCT4:	LOOP ^D39,INCT1	;USE SAME DATA IF LOOPING
	JUMPE F,INCT5	;IF ALL BIT POSITIONS TRIED
	ASH F,-1
	JRST INCT1	;OTHERWISE TRY NEXT ONE

INCT5:	SETCA J,0	;COMPLEMENT J
	JUMPL J,INCT0	;AND DO OTHER PASS IF NEEDED
MRD0:	MOVE C,[200000,,TEST]
	PUSHJ P,LC0CW
	MOVEI E,146720	;REG. 0_MEM RD BUFFER
MRD1:	MOVEI A,042200	;START READ AT CA
	PUSHJ P,C0XCT
	MOVEI B,3
MRD:	MOVE A,E
	PUSHJ P,C0XCT
	HALT	;MEM CYCLE TOOK TOO MANY TICKS
	CONO SA0,RDCONI
	DATAI SA0,A
	TLNE A,1
	JRST MRD
MRD1A:	LOOP ^D8,MRD1
	MOVE A,E
	PUSHJ P,C0XCT
	ADDI E,10000	;FILL NEXT REGISTER
	TRNE E,40000	;ALL FILLED?
	JRST MRD1	;NO
	SETOM TEST1	;CLOBBER PLACE WHERE DATA WILL BE READ BACK
	CLEARM TEST1+1
	MOVE A,TEST+2
	SETCAM A,TEST1+2
	MOVEM A,TEST1+3
	MOVEI E,042240	;START WRITE CYCLE
MWR1:	MOVE A,E
	PUSHJ P,C0XCT
	MOVEI B,3
MWR:	HALT	;MEM CYCLE TOOK TOO MANY TICKS
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
	MOVEI A,0
	PUSHJ P,C0XCT
	CONO SA0,RDCONI
	DATAI SA0,A
	TLNE A,1
	JRST MWR
MWR1A:	LOOP ^D9,MWR1
	ADDI E,10000
	TRNE E,40000
	JRST MWR1
	MOVSI A,-4
MCMP:	MOVE B,TEST1(A)
	MOVE C,TEST(A)
	CAME B,C
	HALT	;DATA GOTTEN BACK WRONG
MCMP1:	LOOP ^D10,MRD0
	AOBJN A,MCMP


;FALLEN INTO FROM PREVIOUS PAGE
STCL:	MOVEI G,NCHN-1	;HANG ALL CHANNELS
STCLH:	PUSHJ P,STEPCX	;GET TO RIGHT CHANNEL
	MOVEI A,HANGPC	;FEED IT MAGIC PC WHICH WILL HANG IT
	PUSHJ P,CXXCT
	SOJGE G,STCLH	;DO SAME FOR ALL CHANNELS
	CONO SA0,SACGO	;START THE CLOCK SO CONO'S WILL WORK
	MOVEI E,10*<NCHN-1>
	MOVEI D,PIE0_<1-NCHN>
	MOVEI C,NCHN-1
STCL1:	CONO SA0,STPIE(E)
	CONSO SA0,(D)
	HALT
STCL1A:	CONO SA0,CLPIE(E)
	CONSZ SA0,(D)
	HALT
STCL1B:	LSH D,-4
	CONO SA0,STBSY(E)
	CONSO SA0,(D)
	HALT
STCL1C:	CONO SA0,CLBSY(E)
	CONSZ SA0,(D)
	HALT
STCL1D:	LSH D,-4
	CONO SA0,STSTF(E)
	CONSO SA0,(D)
	HALT
STCL1E:	CONO SA0,CLSTF(E)
	CONSZ SA0,(D)
	HALT
STCL1F:	LSH D,10+1
	LOOP ^D13,STCL1
	SUBI C,1
	SUBI E,10
	JUMPGE E,STCL1
	MOVSI B,-3
	CONO SA0,SASTEP
	PUSHJ P,STEPC0
STCLM2:	MOVE A,STCLM(B)
	PUSHJ P,C0XCT
	CONSO SA0,@STCLM1(B)
	HALT
STCLM3:	MOVE A,STCLM(B)
	SUBI A,4000
	PUSHJ P,C0XCT
	CONSZ SA0,@STCLM1(B)
	HALT
STCLM4:	LOOP ^D19,STCLM2
	AOBJN B,STCLM2
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
QFIFO:	MOVEI G,NCHN-1	;FILL UP THE FIRST-IN, FIRST-OUT
		;BUFFERS OF EACH OF THE CHANNELS
FIFO1:	PUSHJ P,STEPCX	;STEP TO THE APPROPRIATE CHANNEL
	MOVEI A,002000	;CLEAR TAG BYTE
	PUSHJ P,CXXCT
	MOVEI A,027000	;SET WRITE BIT IN TAG BYTE
	PUSHJ P,CXXCT
	MOVEI A,027400	;SET BUFFER ENABLE BIT IN TAG BYTE
	PUSHJ P,CXXCT
	MOVEI A,145000	;TEST BUFFER EMPTY BIT
	PUSHJ P,CXTST
	TRNN A,40_12	;CHECK RESULTS OF TEST
	HALT	;BUFFER WASN'T EMPTY
FIFO1A:	LOOP ^D21,FIFO1
	MOVEI C,0
	PUSHJ P,LCXCW	;CAUSE AN APPARENT W.C. OVERFLOW
	MOVEI D,NBF	;SET UP COUNT OF # BYTES TO SEND
FIFO2:	MOVE A,G
	LSH A,5
	ADD A,D		;SYNTHESIZE A UNIQUE BYTE OF DATA
	PUSHJ P,CXXCT	;PUT IT INTO MICRO-P.C.
	MOVEI A,040140	;PUT "FIFO_MICRO-P.C." IN MICROIN-
	PUSHJ P,CXTST	;STRUCTION REGISTER TO SEE IF IT
			;WILL BRANCH
	PUSHJ P,STEPX	;NOW EXECUTE THE MICROINSTRUCTION
	MOVEI A,145000	;TEST BUFFER EMPTY BIT
	PUSHJ P,CXTST
	TRNE A,40_12
	HALT	;BUFFER WAS EMPTY
FIFO2A:	LOOP ^D22,FIFO2
	SOJG D,FIFO2	;KEEP DOING IT UNTIL FIFO SHOULD BE FULL
FIFO2C:	MOVEI A,040140	;TRY TO PUT ANOTHER WORD INTO FIFO
	PUSHJ P,CXXCT
	TRNN A,1	;OUGHT TO COMPLAIN THIS TIME
	HALT	;DIDN'T
FIFO2B:	LOOP ^D24,FIFO2C
	SOJGE G,FIFO1	;FILL UP FIFOS OF OTHER CHANNELS
;O.K., FOLKS, NOW WE SEE IF WE CAN GET THE DATA BACK
;FALLS THROUGH TO NEXT PAGE


;FALLEN INTO FROM PREVIOUS PAGE
	MOVEI G,NCHN-1	;START UNLOADING HIGHEST CHANNEL
FIFO3:	PUSHJ P,STEPCX	;STEP COUNTER TO APPROPRIATE CHANNEL
	MOVEI A,023400	;CLEAR BUFFER ENABLE
	PUSHJ P,CXXCT
	MOVEI A,027400	;SET BUFFER ENABLE
	PUSHJ P,CXXCT
	MOVEI A,145000	;TEST BUFFER EMPTY (SHOULD BE)
	PUSHJ P,CXTST
	TRNN A,40_12
	HALT	;WASN'T
FIFO3A:	LOOP ^D25,FIFO3
	MOVEI A,023000	;CLEAR WRITE
	PUSHJ P,CXXCT
FIFO3Z:	MOVEI D,NBF
FIFO4:	MOVEI A,145000	;TEST BUFFER EMPTY
	PUSHJ P,CXTST
	TRNE A,40_12
	HALT	;OOPS, IT WAS
FIFO4A:	LOOP ^D26,FIFO4
	MOVEI A,147000	;R0B0_FIFO
	PUSHJ P,CXTST
	PUSHJ P,STEPX
	MOVE H,G
	LSH H,5
	ADD H,D
	MOVEI A,140000	;CBUS_R0B0
	PUSHJ P,CXTST
	LDB A,[121000,,A]
	CAME A,H
	HALT	;WRONG DATA READ BACK FROM FIFO
FIFO4B:	SOJG D,FIFO4	;READ BACK THE REST
	LOOP ^D27,FIFO3Z
	MOVEI A,145000	;BUFFER SHOULD NOW BE EMPTY
	PUSHJ P,CXTST
	TRNN A,40_12
	HALT	;OOPS, IT ISN'T
FIFO4C:	LOOP ^D23,FIFO3
	SOJGE G,FIFO3	;READ BACK FIFOS OF OTHER CHANNELS
;FALL THRU

;TRY OUT P.I. REQUEST LOGIC
PITST0:	CONO SA0,SACGO
	MOVEI G,<NCHN-1>*10
PITST:	CONO SA0,CLSRQ(G)	;CLEAR STATUS REQUEST FLAG
	CONO SA0,CLSTF(G)	;AND STATUS FLAG
	CONO SA0,STPIE(G)	;AND SET P.I. ENABLE FLAG
	SUBI G,10
	JUMPGE G,PITST	;FOR ALL CHANNELS
	CONSZ SA0,PIRQ	;SHOULD NOT BE REQUESTING AN INTERRUPT
	HALT	;OOPS, WAS
PITST1:	LOOP ^D29,PITST0
	CLEARM PIE	;NOT EXPECTING ANY INTERRUPTS
	CONO PI,12377	;ENABLE ALL P.I. CHANNELS
	MOVEI B,7
PIT1:	CONO SA0,(B)
	SOJG B,.-1	;SHOULD NEVER INTERRUPT
	MOVEI G,<NCHN-1>*10
PIT2:	MOVEI B,7
PIT3:	MOVEM B,PIE
	ADD B,G
	CONO SA0,STSTF(B)	;SHOULD CAUSE AN INTERRUPT
	ROT 377	;DELAY FOR KI-10
	JFCL
	SKIPE PIE	;DID IT?
	HALT	;NO, COMPLAIN
PIT3A:	LOOP ^D38,PIT3
	SUB B,G
	SOJG B,PIT3	;TRY ALL P.I. CHANNELS
	SUBI G,10
	JUMPGE G,PIT2	;TRY ALL SA-10 CHANNELS
UCT:	MOVEI B,0	;ADDRESS
	PUSHJ P,STEPC0	;DO GOTO
UCT0:	HRRZ C,MCBEG0(B)	;WHAT UINST SHOULD BE
			;ABOVE INSTRUCTION CHANGED TO CLEAR LEFT HALF OF AC
	TRNE C,140000
	JRST UCT8	;IF NOT GOTO OR OFF/ON
	MOVE A,B	;CHANGED 26-APR-76-***LEM
	PUSHJ P,C0XCT	;IT'S HARMLESS, DO IT
	PUSHJ P,STEP0
	LDB D,[100200,,B]
	TRZE C,36000
	DPB D,[100200,,C]
	ANDI A,1777
	CAME A,C
	JRST	ER41N
UCT7:	LOOP ^D41,UCT0
UCT8:	CAIGE B,1400-1
	AOJA B,UCT0
	AOS	TEN,BEGERR#
	ALT
	DATAO	PI,TEN
	JRST MSG+3	;O.K., IT ALL WORKS.  TRY IT OVER AGAIN
ER41N:	CAIE	3,20
	HALT
	AOS	ER28
	JRST	UCT7
;HERE


BEG1:	SOJG B,BEG1A
	HALT	;DIDN'T GET TO CHANNEL 0
ER28:	0


PAGE

STEPC0:	MOVEI G,0
STEPCX:	CONO SA0,RDCCI
STPC0A:	DATAI SA0,A
	LDB H,[220200,,A]	;CHANNEL SELECTION.
	CAMN H,G		;DESIRED CHANNEL?
	POPJ P,			;YES.
	DATAO SA0,[HANGPC]	;NO. STICK OTHER CHANNELS AT STICKY PC
STEPX:
STEPX1:	CONO SA0,SASTEP!RDCCI
	JRST STPC0A

LC0CW:	MOVEI G,0
LCXCW:	PUSHJ P,LCXR2
	MOVEI A,060700	;WC_WC FIELD OF REG. 2
	PUSHJ P,CXXCT
	MOVEI A,063300	;CA_CA FIELD OF REG. 2
CXXCT:	DATAO SA0,A
	JRST STEPX

C0XCT:	DATAO SA0,A
STEP0:	MOVEI G,0
	JRST STEPX1

CXTST:	DATAO SA0,A
	CONO SA0,RDCCI
	DATAI SA0,A
	POPJ P,

LC0R2:	MOVEI G,0
LCXR2:	MOVEI D,060000-20	;IARB TO BYTE OF REG. 2
LCXR2A:	ADDI D,20	;ADVANCE TO NEXT BYTE OF REGISTER
	MOVEI B,0
	LSHC B,10
	DATAO SA0,B	;PUT A BYTE OF DATA INTO MICRO-P.C.
	PUSHJ P,STEPX
	DATAO SA0,D	;TRANSFER IT TO BYTE OF REG. 2
	PUSHJ P,STEPX
	TRNN D,100	;WAS THAT THE LAST BYTE?
	JRST LCXR2A	;NO
	POPJ P,	;I CAN'T BELIEVE WE FILLED THE WHOLE WORD


PAGE

PDL:	BLOCK 200	;PUSHDOWN LIST

R0==0
R1==10000
R2==20000
R3==30000
B0=1==1
B2==2
B3==3
B4==4
SW1==5
BUSOUT==5
FOFI==6
FOFIS==7
BUSIN==10
STRD==10
STRDC==11
TAGB==12
STWR==12
INTST==13
STWRD==13
ZERO==14
LOAD==14
MRB==15
WHOLE==15
FIFO==16
B14==16
FIFOS==17

WC==0
CA==2000
PC==4000
A3==6000
IARB==0
INCR==0
WCFLD==1
ADRFLD==2
MAGIC==3
DECR==1
CLRINS==400
CLRTAG==0
STGO==10000
STRQ==10400
STFLG==11000
STPIE==11400
ADROUT==20000
SELOUT==20400
SRVOUT==21000
NOPOUT==21400
SUPOUT==22000
CMDOUT==22400
WRBIT==23000
BUFENB==23400
INS0==30000
INS1==30400
SELERR==31000
CTBYT==31400
CTLERR==32000
SKPFLG==32400
LENERR==33000
PCIFLG==33400
BIT00==0
BIT01==1
BIT10==20
BIT11==21
BIT20==40
BIT21==41
BIT30==60
BIT31==61
BIT40==100
BIT41==101
BIT50==120
BIT51==121
BIT60==140
BIT61==141
BIT70==160
BIT71==161
Z==200
NZ==201
ERR==221
NERR==220
DEVM==240
RBAK==260
NBTM==300
IFN EADR,<SKPC==320>
IFE EADR,<SKPC==Z>
TAGBS==TAGB_10
ADRI0==TAGBS+BIT00
ADRI1==TAGBS+BIT01
SELI0==TAGBS+BIT10
SELI1==TAGBS+BIT11
BFMT0==TAGBS+BIT20
BFMT1==TAGBS+BIT21
OPLI0==TAGBS+BIT30
OPLI1==TAGBS+BIT31
RQI0==TAGBS+BIT40
RQI1==TAGBS+BIT41
STAI0==TAGBS+BIT50
STAI1==TAGBS+BIT51
BCRQ0==TAGBS+BIT60
BCRQ1==TAGBS+BIT61
BFHLT0==TAGBS+BIT71
BFHLT1==TAGBS+BIT70
INSTS==INTST_10
INS00==INSTS+BIT00
INS01==INSTS+BIT01
INS10==INSTS+BIT10
INS11==INSTS+BIT11
PCIF0==INSTS+BIT70
PCIF1==INSTS+BIT71
SW1S==SW1_10
BSY0==SW1S+BIT00
BSY1==SW1S+BIT01
STRQ0==SW1S+BIT10
STRQ1==SW1S+BIT11
STFL0==SW1S+BIT20
STFL1==SW1S+BIT21
WCOK0==SW1S+BIT40
WCOK1==SW1S+BIT41
TIMER0==SW1S+BIT50
TIMER1==SW1S+BIT51

DEFINE MPAGE
<IFN .&17,<.PRINT <BADMPG>>>

DEFINE ADRSET (NAME)
<IFIDN <NAME>,<>,<ADR=.+1>
IFDIF <NAME>,<>,<ADR=NAME>
IF2,<IFN <.&1400>-<ADR&1400>,<.PRINT <BNDER1>>>
ADR=ADR&377
>
DEFINE PULSE (BIT,NAME)
<ADRSET NAME
.,,BIT+2000+ADR
>

DEFINE ON (BIT,NAME)
<ADRSET NAME
.,,BIT+4000+ADR
>
DEFINE OFF (BIT,NAME)
<ADRSET NAME
.,,BIT+ADR
>
DEFINE ADRST1 (NAME)
<IFIDN <NAME>,<>,<ADR=.+1>
IFDIF <NAME>,<>,<ADR=NAME>
IF2,<IFN <.&1760>-<ADR&1760>,<.PRINT <BNDERR>>>
ADR=ADR&17
>
DEFINE XFER (REG,SRC,DEST,NAME)
<ADRST1 NAME
.,,140000+REG+<SRC_10>+<DEST_4>+ADR
>
DEFINE XANC (SRC1,SRC2,REG,DEST,NAME)
<ADRST1 NAME
.,,040000+REG+SRC1+<SRC2_10>+<DEST_4>+ADR
>
DEFINE GOTO (NAME)
<.,,NAME
>
DEFINE TEST (REG,SRC,FN,NAME)
<ADRST1 NAME
.,,100000+REG+<SRC_10>+<<FN!<ADR+1>>-<FN&<ADR+1>>>
>
DEFINE HANG (REG,SRC,FN)
<.,,100000+REG+<SRC_10>+FN+.&17
>
;THIS IS WHERE THE MICROCODE IS LOADED

	XALL
MCBEG0:

DEPHASE

MCBEG:!
TABCNT:	0
ADRLO:	40000
PASS:	0
PATCH:
PAT:!	BLOCK 1000
	LIT
	VAR
LPTPTR:	0
PTRBUF:	0
PNOP:	BYTE (8) 240,3,0,0
PSENSE:	120020000000+<0_14>	;SENSE
	IOW 30,SENSED		;24 BYTES OF SENSE DATA
SENSED:	BLOCK 6
ADRHI:	777000
RNDM:	123456,,654321
PIE:	0
UUOH:	HALT
CCTBL:	BLOCK NCHN
MAMBTL:	0
MAMBTR:	0
INCTW:	0
CWTA:	0
CWTB:	0
SETPTR:	PUSH P,L
	MOVE L,[POINT 7,PTRBUF,]
	MOVEM L,LPTPTR
	SETZM PTRBUF
	POP P,L
	POPJ P,


STYO:	HRLI C,440700
STYO1:	ILDB B,C
	CAIN B,"%"
	JRST STYO2
	CAIN B,"$"
	JRST STYO3		;DECIMAL TYPEOUT
	CAIN B,"\"
	JRST STYO4		;HEX TYPEOUT
	CAIN B,15	;IS THIS A CR
	JRST TERM	;YES FINISH PRINTING
TTYO:	JUMPE B,CPOPJ	;ALL FINISHED
	PUSHJ P,TYO	;NO STUFF ANOTHER CHARACTER
	JRST STYO1

STYO2:	ILDB B,C
	MOVE A,-100(B)
	PUSHJ P,OPT
	JRST STYO1
STYO3:	ILDB B,C
	MOVE A,-100(B)
	PUSHJ P,DPT
	JRST STYO1
STYO4:	ILDB B,C
	MOVE A,-100(B)
	PUSHJ P,HPT
	JRST STYO1

CR4:	PUSHJ P,CR2
CR2:	PUSHJ P,CR
	JRST CR

OPT:	TLNN A,-1
	JRST OPT1
	PUSH P,A
	HLRZS A
	PUSHJ P,OPT2
	POP P,A
	MOVEI B,54
	PUSHJ P,TYO
	PUSHJ P,TYO
OPT1:	HRRZS A
OPT2:	IDIVI A,10
	JUMPE A,OPT3
	HRLM B,(P)
	PUSHJ P,OPT2
	HLRZ B,(P)
OPT3:	ADDI B,60
	CAILE B,"9"
	ADDI B,"A"-"9"-1
TYO:	CONSZ TTY,20
	JRST .-1
	ALT
	CAIN B,11
	JRST TABRTN
	DATAI DSW,0
	TLNE 0,040000
	POPJ P,
FTYO:	DATAO TTY,B
	AOS ,TABCNT
	POPJ P,

TABRTN:	PUSH P,BF
	MOVE BF,TABCNT
	TRNN BF,7
	JRST TABE
TABL:	MOVEI B,40
	PUSHJ P,TYO
	JRST TABRTN+1
TABE:	CAIN B,11
	JRST TABL
	POP P,BF
	POPJ P,


JPOPJ:	PUSHJ P,SETPTR
	SETZM TABCNT
	DATAO LPT,PTRBUF	;JAM NULLS TO SEE IF LPT BUSY
	CONSO LPT,100
	JRST .-1
	CONO LPT,2000
	CONSO LPT,100
	JRST .-1
CPOPJ:	POPJ P,
CSET:	MOVE A,CHAN
	LSH A,2
	ADDI A,BASE
	MOVEM A,CBASE
	ADDI A,1
	MOVEM A,CBASE+1
	ADDI A,1
	MOVEM A,CBASE+2
	MOVE A,CHAN
	DPB A,[030200,,STBSYC]
	DPB A,[030200,,CLSTFC]
	DPB A,[030200,,STPIEC]
	DPB A,[030200,,STSRQC]
	POPJ P,0

CHAN:	0
CBASE:	BLOCK 3
STBSYC:	STBSY+SA0CH
CLSTFC:	CLSTF+SA0CH
STPIEC:	STPIE+SA0CH
STSRQC:	STSRQ+SA0CH

INT1:	0
	MOVEI I,1
	CAME I,PIE	;ARE WE EXPECTING AN INTERRUPT ON THIS CHANNEL?
	HALT	;NO
	CONO SA0,CLSTF(B)	;REMOVE THE CAUSE FOR THIS INTERRUPT
			;(MAIN PROGRAM HAS SET B UP)
	CLEARM PIE	;NOTE THAT NO FURTHER INTERRUPTS ARE EX-
			;PECTED, ALSO TELL MAIN PROGRAM THAT ONE
			;INTERRUPT DID OCCUR
	JRST 12,@INT1

INT2:	0
	MOVEI I,2
	CAME I,PIE
	HALT
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT2

INT3:	0
	MOVEI I,3
	CAME I,PIE
	HALT
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT3

INT4:	0
	MOVEI I,4
	CAME I,PIE
	HALT
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT4

INT5:	0
	MOVEI I,5
	CAME I,PIE
	HALT
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT5


INT6:	0
	MOVEI I,6
	CAME I,PIE
	HALT
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT6

INT7:	0
	MOVEI I,7
	CAME I,PIE
	HALT
	CONO SA0,CLSTF(B)
	CLEARM PIE
	JRST 12,@INT7
PTN0:	0	;CALLED BY JSR
	JRST PTN1	;ON TO NEXT VALUE

PTN:	0
	ADDI A,PTNTX	;BEG OF SINGLE BITS IN TABLE
	MOVEM A,PTNA	;WHERE IN TABLE TO STOP
	MOVEI A,PTNTX-1
	MOVEM A,PTNP	;INITIALIZE POINTER
PTN1:	AOS A,PTNP
	CAML A,PTNA	;AT END OF DATA?
	JRST PTN5	;YES, MAKE 1 RANDOM #
	MOVE A,(A)	;NO, TAKE ENTRY FROM TABLE
	JRST @PTN	;EXIT TO TOP OF LOOP

PTN5:	CAMN A,PTNA	;GIVEN RANDOM VALUE YET?
	JRST @PTN0	;YES, EXIT TO BOTTOM OF LOOP
	MOVEI A,105	;NO, MAKE IT NOW
	FMPB A,RNDM
	JRST @PTN	;AND EXIT TO TOP OF LOOP
TEST:	0
	-1
	252525,,252525
	525252,,525252	;PSEUDO-RANDOM TEST DATA
TEST1:	BLOCK 4	;PLACE TO READ STUFF BACK INTO

CWTD:
IFE CV,<325125,,340125>
IFN CV,<325137,,340125>
	125120,,252252
	240000,,

STCLM:	014000	;SET BUSY FLAG
	015000	;SET STATUS FLAG
	015400	;SET P.I. ENABLE FLAG

STCLM1:	BSY0
	STF0
	PIE0

BITS:	REPEAT ^D36,<	EXP 1B<.-BITS>>
DPT:	MOVEI B,"-"
	SKIPGE A
	PUSHJ P,TYO
	MOVMS A
DPT1:	IDIVI A,12
	JUMPE A,OPT3
	HRLM B,(P)
	PUSHJ P,DPT1
	HLRZ B,(P)
	JRST OPT3

HPT:	MOVEI B,0
	ROTC A,-4
	ROT B,4
	JUMPE A,OPT3
	HRLM B,(P)
	PUSHJ P,HPT
	HLRZ B,(P)
	JRST OPT3
TERM:	SETZM TABCNT
	JRST TTYO
	PUSH P,L
	MOVE L,LPTPTR
	TLNE L,760000
	PUSHJ P,FOUT
	CONSZ LPT,200
	JRST .-1
	ILDB B,C
	CAIN B,12
	ILDB B,C
	POP P,L
	JUMPE B,JPOPJ
	JRST STYO1+1


FOUT:	CONSZ LPT,200
	JRST .-1
	DATAO LPT,PTRBUF
	CONSO LPT,200
	JRST .-1
	PUSHJ P,SETPTR
	POPJ P,

PTNTX:	REPEAT ^D36,<	EXP 1B<PTNTX-.+^D35>>

PTNT:	0
	-1
	252525,,252525
	525252,,525252

PTNA:	0
PTNP:	0

END

    nIS@à